//BP13D400  JOB (BP13P),'BP13D400',CLASS=I,MSGCLASS=N,                  00010048
//        MSGLEVEL=(1,1),NOTIFY=CSD0071,SCHENV=PKZIP                    00020048
//JOBLIB  DD DSN=BP13.BATCH.LOADCOB,DISP=SHR                            00030048
//* ============================================================== *    00040048
//*                                                                *    00050048
//*     T A K E     P O S S E S S I O N     J O B                  *    00060048
//*                                                                *    00070048
//* ============================================================== *    00080048
//* CHGE REQ  DATE        BY   DESCRIPTION                              00090048
//* --------  ----        ---  -----------                              00100048
//* BP132341  05/05/2003  CLT1 TO BACKUP BO CASE                        00110048
//* BP132341  13/05/2003  CLT1 STEP35 TO EXCLUDE BO CASE                00120048
//* BP132546  11/06/2004  YCH2 INCLUDE STORCLAS(PRODRLS) & LOG(NONE)    00130048
//* BP132604  11/10/2004  YCH2 EXPAND BP17K000 FROM 80 TO 200           00140048
//* BP132702  12/08/2005  CLT1 ADD P13L441D FOR BP13C441                00150048
//* BP132801  12/08/2005  CT2  INCL STEPS TO CREATE F225 FILE TO EPRS   00160048
//* BP132842  16/08/2006  CLT1 UPDATE AHG USED IN BP13C425              00170048
//* BP132891  25/01/2007  SD10 INCLUDE LOAN TAG = N/P FOR JV (STEP66)   00180048
//* BP133505  03/01/2009  LSB1 ADD DBSS FILE TO BP13C411                00190048
//* BP133527  07/04/2009  YCH2 PRINT LN OR LNS REPORT                   00200048
//* BP133527  14/05/2009  YCH2 LIFT OUT LN OR LNS REPORT TRANSFER TO    00210048
//*                            BP13W470 RUN ON EVERY SAT                00220048
//* BP133783  25/11/2009  PLC3 ADD SY02F002 TO C464, C604               00230048
//* BP133725  20/01/2010  DW5  ADD INTERFACE FILES TO MAMS FOR NPL      00240048
//* BP133926  06/07/2010  ESA1 ADD READING OF BP13K421 TO BP13C402      00250048
//* BP133997  06/10/2010  ESA1 ADD READING OF BM06K110 TO BP13C238      00260048
//* BP134148  14/02/2011  LJL1 TO PASS THE INFORMATION TO RHMS          00270048
//*                            TRANSACTION FILE BP13.F410.STSALE SOLD   00280048
//*                            UNDER PUBLIC SCHEME (SALE TYPE 1 AND IDP 00290048
//*                            SCHEME='LRT') AND (SALE TYPE 1 AND ALLOC 00300048
//*                            MODE = 'SHS')                            00310048
//* BP134166  05/03/2011  YCH2 ENHANCE BP13C233 AND DROP SORT SRS CASES 00320048
//* BP134125  23/05/2011  DW5  REMOVE CLEARING AB03.F806.SOC            00330048
//* BP134021  29/03/2011  DW5  TO CATER FOR FPS(ERP) IMPLEMENTATION     00340048
//* BP134216  18/10/2011  LJL  TO VERIFY BP13K310                       00350048
//* BP134760  15/08/2012  LSB  MOVE BP13C410 TO BP13D407, NO READ AG07  00360048
//*                            IN THIS JOB                              00370048
//* BP135027  10/08/2013  ESA  ADD BP13KD15 ALT-KEY1 IN BP13C411        00380048
//* BP135197  08/01/2014  LJL  TO CATER FOR SA CASE THAT BOOK AND TP    00390048
//*                            SAME DATE 23 DEC 2013.                   00400048
//* BP135197  10/01/2014  LJL  TO REVERT BACK TO NORMAL AFTER UPDATE    00410048
//*                            OF SA CASE.                              00420048
//* BP135171  11/02/2014  RJE1 TO CATER FOR DB2 UPGRADE                 00430048
//* BP135325  16/06/2014  PCL4 ADD BP17K120 IN BP13C656                 00440048
//* BP135041  08/07/2014  ESD1 CATER FOR MGL CASES FOR PGM C411 AND C60100450048
//* BP135398  15/07/2014  ESD1 INCLUDE MGL CASES AS INPUT FOR C601      00460048
//* BP135571  04/12/2014  MRN1 CREATE BP13F415 FOR USE IN DEBAR UPDATE  00470048
//* BP135610  26/01/2015  LSB1 TP APPOINTMENT FILE TO BQG DU04 SYSTEM   00480048
//* BP135670  23/04/2015  ESA1 TO INCLUDE BP13C37A TO CREATE AB03F021   00490048
//* BP135641  24/04/2015  RJB1 TO INCLUDE BP13C476 FOR TEMPLOAN         00500048
//* BP135696  15/06/2015  LSB1 REMOVE AC08.F202.APPL.SOC                00510048
//* BP135366  02/09/2015  RJB1 INCLUDE AB03F123 INTO PGM BP13C656       00520048
//* BP135366  02/09/2015  LJL1 INCLUDE BP13.AB03.F123.SERSCTRA          00530048
//* BP135571  06/11/2015  MRN1 TO INCLUDE BP13K820 IN STEP FOR BP13S800 00540048
//* BP136098  02/02/2016  KSJ3 TO ADD SALES TYPE 18 IN THE OMIT COND    00550048
//*                            FOR STEP67                               00560048
//* BP136196  21/03/2016  KVL1 ADDED STEPS 86 TO 89 FOR PPO PROCESSING  00570048
//* BP135887  14/7/20166  YCH2 REMOVE UPD K120 FROM C233 AND INCLUDE    00580048
//*                            S800 LOGIC                               00590048
//* BP136651  07/02/2017  KVL1 ADDED STEP 90 TO RUN MAINTENANCE PGM FOR 00600048
//*                            FILES BP13K62A AND BP13K62E              00610048
//* BP136806  18/05/2017  PCL4 TO CATER FOR TEMP LOAN - BP13C37H        00620048
//* BP136869  26/06/2017  KSJ3 TO CATER FOR TEMP LOAN - BP13C601        00630048
//* BP136873  06/07/2017  KVL1 ADDED STEPS 90A AND 90B TO HOUSEKEEP     00640048
//*                            FROM BP13K62A/E ALL REINSTATED CASES     00650048
//*                            ADDED STEP93 TO SEND EMAIL ALERT FOR     00660048
//*                            REINSTATED CASES HOUSEKEPT               00670048
//* BP136883  08/08/2017  KVL1 ADDED REINSTATED FILE TO STEP91          00680048
//* BP136925  30/08/2017  KVL1 CHANGED HOUSEKEEPING CONDITION FR 6 MOS  00690048
//*                            TO 15 MONTHS (STEP90)                    00700048
//* BP136987  29/12/2017  LJL1 TO BACKUP YEARLY FILE FOR BP13F600       00710048
//*                            CPF WITHDRAWAL FILE                      00720048
//* BP137196  26/02/2018  LSB1 IF CASE DEBARMENT VIA ONLINE EAPG PORTAL 00730048
//*                            DO NOT PASS RECORD TO DEBARMENT SYSTEM   00740048
//* BP137287  15/05/2018  LJL1 TO CATER FOR DDS TP CASES                00750048
//* BP137182  06/06/2018  PCL4 TO ADD BP13C30A                          00760048
//* BP137182  30/08/2018  LJL1 TO CATER FOR BACKUP FILE FOR             00770048
//*                            BP13.F40C.SALEORD.RSV                    00780048
//*                            BP13.F40C.SALEORD.RSV.MTH                00790048
//* BP137429  30/08/2018  LJL1 TO READ BP13K410 INSTEAD OF BP13K400     00800048
//* BP137438  16/10/2018  LJL1 TO CATER FOR BK FEE TR FOR DDS CASES     00810048
//* BP137225  25/10/2018  LJL1 TO SEPARATE SALES TYPE 10 FROM SALE TYPE 00820048
//*                            13 AND 18 FOR DDS CASES.                 00830048
//* BP137836  16/05/2019  KV5  TO INCLUDED SORT CONDITION 'DT' OR 'TU'  00840048
//* BP137782  21/05/2019  LJL1 TO CATER FOR BK FEE TR FOR DDS CASES CUM 00850048
//*                            TEMP LOAN FOR TP CASES.                  00860048
//* BP137725  27/05/2019  LSB1 ADD BP13K399, BM06K110 TO BP13C448       00870048
//* BP137074  21/11/2018  FNP1 ADD SENDING OF FILE ATTACHMENT TO USER   00880048
//*                            FOR BP13C26C EXCEPTION REPORT            00890048
//* BP137887  04/09/2019  LSB1 ADD BP13K800 TO BP13D400                 00900048
//* BP138002  27/09/2019  KR13 ADD BM06K110 & BP13K936 TO C26C.         00910048
//* BP138080  15/11/2019  LSB1 REMOVE TAG FS DEBARMENT BP13C308         00920048
//* BP138123  11/12/2019  LSB1 MOVE C26C TO BP13D420                    00930048
//* BP138071  15/01/2020  LJL1 TO SEND KA FILE TO FWD FOR FIMS          00931048
//* BP138170  27/02/2020  LJL1 TO CATER FOR DA = Y                      00931149
//*                            BACKUP BP13.F385.LEAS.DA                 00931249
//* BP138014  03/03/2020  LJL1 TO ENSURE NO DUPLICATS IN                00931350
//*                            BP13.F385.LEAS.DA                        00931450
//* BP138258  26/03/2020  LSB1 ACCUM BP13.F430                          00931551
//* BP138258  26/03/2020  LSB1 RUN BP13C631(CA),THEN BP13C617 (Z1/Z2)   00931551
//* BP138261  25/09/2020  LJL1 TEMP LOAN CASE WITH K482/K486 REC FOUND  00931551
//* BP138646  22/03/2021  LJL1 TO CATER DDS BANK LOAN                   00931551
//* BP138645  15/04/2021  KR13 TO ADD CZ3M - GET CUST_ID FROM BE01C808          
//* BP138865  28/10/2021  KR13 REMOVED REPORTS BP13L601 & L601A.                
//* BP138086  30/11/2021  FP6  ADD BP13C40F FOR SER CONTRA REPORT               
//* BP139526  07/03/2023  LJL1 TO CATER FOR COTMA CASES                         
//* BP139605  09/05/2023  LJL1 TO CATER FOR HFE IMPLEMENTATION ON               
//*                            9 MAY 2023.                              00931450
//*-------------------------------------------------------------------* 00932048
//* TO SEND DYNAMIC JDE AHEAD OF THE PRINT DATASETENT                 * 00933048
//* PRT01 : CLASS = 2                                                 * 00934048
//*-------------------------------------------------------------------* 00935048
//PRT01    EXEC PGM=IEBGENER                                            00936048
//SYSUT1   DD *                                                         00937048
1$DJDE$ BTEXT='DPT=GRPID-EAALAG1A CP-1',END;                            00938048
1$DJDE$ JDE=BP13D,JDL=BP13,END;                                         00939048
//FORMID   OUTPUT FORMS=1WD1,GROUPID=EAALAG1A                           00940048
//SYSUT2   DD SYSOUT=2,OUTPUT=(*.FORMID),DEST=IOM2                      00950048
//SYSPRINT DD SYSOUT=*                                                  00960048
//SYSIN    DD DUMMY                                                     00970048
//*-------------------------------------------------------------------* 00980048
//* TO SEND DYNAMIC JDE AHEAD OF THE PRINT DATASETENT                 * 00990048
//* PRT02 : CLASS = 2                                                 * 01000048
//*-------------------------------------------------------------------* 01010048
//PRT02    EXEC PGM=IEBGENER                                            01020048
//SYSUT1   DD *                                                         01030048
1$DJDE$ BTEXT='DPT=GRPID-EAALSS1A CP-1',END;                            01040048
1$DJDE$ JDE=BP13D,JDL=BP13,END;                                         01050048
//FORMID   OUTPUT FORMS=1WD1,GROUPID=EAALSS1A                           01060048
//SYSUT2   DD SYSOUT=2,OUTPUT=(*.FORMID),DEST=IOM2                      01070048
//SYSPRINT DD SYSOUT=*                                                  01080048
//SYSIN    DD DUMMY                                                     01090048
//*                                                                     01100048
//*-------------------------------------------------------------------* 01110048
//* TO SEND DYNAMIC JDE AHEAD OF THE PRINT DATASETENT                 * 01120048
//* PRT03 : CLASS = 2, 1 COPY        (SALES - AGREEMENT TEAM)         * 01130048
//*-------------------------------------------------------------------* 01140048
//PRT03    EXEC PGM=IEBGENER                                            01150048
//SYSUT1   DD *                                                         01160048
1$DJDE$ BTEXT='DPT=GRPID-EAALAG1B CP-1',END;                            01170048
1$DJDE$ JDE=13C645,JDL=BP13,COPIES=1,END;                               01180048
//FORMID   OUTPUT FORMS=1OS2,GROUPID=EAALAG1B                           01190048
//SYSUT2   DD SYSOUT=2,OUTPUT=(*.FORMID),DEST=IOM2                      01200048
//SYSPRINT DD SYSOUT=*                                                  01210048
//SYSIN    DD DUMMY                                                     01220048
//*                                                                     01230048
//*-------------------------------------------------------------------* 01240048
//* TO SEND DYNAMIC JDE AHEAD OF THE PRINT DATASETSNT                 * 01250048
//* PRT04 : 1 COPY                 RENTAL HOUSING ALLOCATION UNIT     * 01260048
//*-------------------------------------------------------------------* 01270048
//PRT04    EXEC PGM=IEBGENER                                            01280048
//SYSUT1   DD *                                                         01290048
1$DJDE$ BTEXT='DPT=GRPID-HARHRA1A CP-1',END;                            01300048
1$DJDE$ JDE=BP13D,JDL=BP13,END;                                         01310048
//FORMID   OUTPUT FORMS=1WD1,GROUPID=HARHRA1A                           01320048
//SYSUT2   DD SYSOUT=2,OUTPUT=(*.FORMID),DEST=IOM2                      01330048
//SYSPRINT DD SYSOUT=*                                                  01340048
//SYSIN    DD DUMMY                                                     01350048
//*                                                                     01360048
//*-------------------------------------------------------------------* 01370048
//* TO SEND DYNAMIC JDE AHEAD OF THE PRINT DATASETSNT                 * 01380048
//* PRT05 : CLASS = 2, COPIES = 3                                     * 01390048
//*-------------------------------------------------------------------* 01400048
//PRT05    EXEC PGM=IEBGENER                                            01410048
//SYSUT1   DD *                                                         01420048
1$DJDE$ BTEXT='DPT=GRPID-EAALAG3A CP-3',END;                            01430048
1$DJDE$ JDE=BP13D,COPIES=3,JDL=BP13,END;                                01440048
//FORMID   OUTPUT FORMS=1WD1,GROUPID=EAALAG3A                           01450048
//SYSUT2   DD SYSOUT=2,OUTPUT=(*.FORMID),DEST=IOM2                      01460048
//SYSPRINT DD SYSOUT=*                                                  01470048
//SYSIN    DD DUMMY                                                     01480048
//*-------------------------------------------------------------------* 01490048
//* TO SEND DYNAMIC JDE AHEAD OF THE PRINT DATASETSNT                 * 01500048
//* PRT06 : CLASS = 2, COPIES = 2                                     * 01510048
//*-------------------------------------------------------------------* 01520048
//PRT06    EXEC PGM=IEBGENER                                            01530048
//SYSUT1   DD *                                                         01540048
1$DJDE$ BTEXT='DPT=GRPID-EAALAG2A CP-2',END;                            01550048
1$DJDE$ JDE=BP13D,COPIES=2,JDL=BP13,END;                                01560048
//FORMID   OUTPUT FORMS=1WD1,GROUPID=EAALAG2A                           01570048
//SYSUT2   DD SYSOUT=2,OUTPUT=(*.FORMID),DEST=IOM2                      01580048
//SYSPRINT DD SYSOUT=*                                                  01590048
//SYSIN    DD DUMMY                                                     01600048
//*-------------------------------------------------------------------* 01610048
//*  VERIFY FILES BEFORE DAILY PRODUCTION RUN                         * 01620048
//*-------------------------------------------------------------------* 01630048
//STEP00   EXEC PGM=IDCAMS,COND=(0,NE)                                  01640048
//INPUT1   DD DSN=BP13.K010.DEBAR,DISP=OLD                              01650048
//INPUT2   DD DSN=BP13.K410.ACCUMSO,DISP=OLD                            01660048
//INPUT3   DD DSN=BP13.K800.APPLN,DISP=OLD                              01670048
//INPUT4   DD DSN=BP17.K120.SERSUNIT,DISP=OLD                           01680048
//INPUT5   DD DSN=BP13.K310.TRANS,DISP=OLD                              01690048
//INPUT6   DD DSN=BP13.K40C.SALEORD.RSV,DISP=OLD                        01700048
//SYSPRINT DD SYSOUT=*                                                  01710048
//SYSIN   DD *                                                          01720048
          VFY FILE(INPUT1)                                              01730048
          VFY FILE(INPUT2)                                              01740048
          VFY FILE(INPUT3)                                              01750048
          VFY FILE(INPUT4)                                              01760048
          VFY FILE(INPUT5)                                              01770048
          VFY FILE(INPUT6)                                              01780048
//*-----------------------------------------------------------------    01790048
//*  VERIFY FILES BEFORE DAILY PRODUCTION RUN                           01800048
//*-----------------------------------------------------------------    01810048
//STEP0A   EXEC PGM=IDCAMS,COND=(0,NE)                                  01820048
//INPUT1   DD DSN=BM06.K100.BLOCK,DISP=OLD                              01830048
//INPUT2   DD DSN=BM06.K110.FLATINV,DISP=OLD                            01840048
//INPUT3   DD DSN=BM06.K190.LOG,DISP=OLD                                01850048
//INPUT4   DD DSN=BP13.K130.SOCHIST,DISP=OLD                            01860048
//SYSPRINT DD SYSOUT=*                                                  01870048
//SYSIN   DD *                                                          01880048
          VFY FILE(INPUT1)                                              01890048
          VFY FILE(INPUT2)                                              01900048
          VFY FILE(INPUT3)                                              01910048
          VFY FILE(INPUT4)                                              01920048
//*-------------------------------------------------------------------* 01930048
//* TO SORT TRANS FILE BY REGN , TRANS                                * 01940048
//*-------------------------------------------------------------------* 01950048
//STEP01   EXEC SORT,COND=(0,NE)                                        01960048
//SORTIN   DD DSN=BP13.F310.DUMMY,DISP=OLD                              01970048
//SORTOUT  DD DSN=&&STEP0B,DISP=(NEW,PASS),                             01980048
//            SPACE=(TRK,(50,10),RLSE),UNIT=SYSDA,                      01990048
//            DCB=(RECFM=FB,LRECL=150,BLKSIZE=23400)                    02000048
//SYSIN    DD *                                                         02010048
     SORT FIELDS=(1,10,A),FORMAT=CH                                     02020048
//*-------------------------------------------------------------------* 02030048
//* DELETE/DEFINE TRANSACTION FILE                                    * 02040048
//*-------------------------------------------------------------------* 02050048
//STEP02   EXEC PGM=IDCAMS,COND=(0,NE)                                  02060048
//BP13F310 DD DSN=&&STEP0B,DISP=(OLD,PASS)                              02070048
//SYSPRINT DD SYSOUT=*                                                  02080048
//SYSIN    DD *                                                         02090048
         DEL    (BP13.K310.TRANS) CL PURGE                              02100048
         DEF CL (NAME(BP13.K310.TRANS)    -                             02110048
                 CYL(5 2)                 -                             02120048
                 UNIQUE                   -                             02130048
                 STORCLAS(PRODRLS)        -                             02140048
                 LOG(NONE)                -                             02150048
                 SHR(2 3))                -                             02160048
                 DATA                     -                             02170048
                 (NAME(BP13.K310.TRANS.DATA) -                          02180048
                 KEYS(10 0)               -                             02190048
                 RECSZ(150 150)           -                             02200048
                 CISZ(4096)) -                                          02210048
                 INDEX                    -                             02220048
                 (NAME(BP13.K310.TRANS.INDEX))                          02230048
        IF MAXCC = 0 THEN                                               02240048
           REPRO INFILE(BP13F310) ODS(BP13.K310.TRANS)                  02250048
//*-------------------------------------------------------------------* 02260048
//* SORT THE DAILY SALES ORDER FILE (INCLUSIVE OF KIV CASES)          * 02270048
//* IN SALES TYPE AND CURRENT REG NO SEQUENCE.                        * 02280048
//*-------------------------------------------------------------------* 02290048
//STEP03   EXEC SORT,COND=(0,NE)                                        02300048
//SORTIN   DD DSN=BP13.K400.SALEORD,DISP=SHR                            02310048
//SORTOUT  DD DSN=&&P13F400,DISP=(NEW,PASS),                            02320048
//            SPACE=(TRK,(20,5),RLSE),UNIT=SYSDA,                       02330048
//            DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                       02340048
//SYSIN    DD  *                                                        02350048
      SORT FIELDS=(37,2,A,1,8,A),FORMAT=CH                              02360048
      OMIT COND=(1,8,CH,EQ,C'        ')                                 02370048
//*-------------------------------------------------------------------* 02380048
//* VALIDATE DAILY SALES ORDER RAISED AND PRINT ERROR                 * 02390048
//*-------------------------------------------------------------------* 02400048
//STEP04   EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE),REGION=4096K       02410048
//BP13F400 DD DSN=&&P13F400,DISP=(OLD,PASS)                             02420048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             02430048
//            AMP=('BUFND=4','BUFNI=8')                                 02440048
//BP13F405 DD DSN=BP13.F405.KIVSO,DISP=OLD                              02450048
//BP13F410 DD DSN=BP13.F410.DAILYSO,DISP=OLD                            02460048
//AB03K030 DD DSN=AB03.K030.SALEMAST,DISP=SHR,                          02470048
//            AMP=('BUFND=4','BUFNI=8')                                 02480048
//BM06K110 DD DSN=BM06.K110.FLATINV,DISP=SHR,                           02490048
//            AMP=('BUFND=4','BUFNI=8')                                 02500048
//BP13K420 DD DSN=BP13.K421.CC.INFO,DISP=SHR,                           02510048
//            AMP=('BUFND=4','BUFNI=8')                                 02520048
//BP13K421 DD DSN=BP13.K421.CC.INFO.PATH1,DISP=SHR,                     02530048
//            AMP=('BUFND=4','BUFNI=8')                                 02540048
//BP13L402 DD SYSOUT=2,OUTPUT=(*.PRT01.FORMID),DEST=IOM2                02550048
//SYSOUT   DD SYSOUT=*                                                  02560048
//SYSTSPRT DD SYSOUT=*                                                  02570048
//SYSPRINT DD SYSOUT=*                                                  02580048
//SYSDBG   DD SYSOUT=*                                                  02590048
//SYSDBOUT DD SYSOUT=*                                                  02600048
//SYSTSIN  DD *                                                         02610048
  DSN SYSTEM(DSN)                                                       02620048
  RUN PROGRAM(BP13C402) PLAN(BP13PCC) -                                 02630048
      LIB('BP13.BATCH.LOADCOB')                                         02640048
  END                                                                   02650048
//*-------------------------------------------------------------------* 02660048
//* REBUILD VSAM FILE BP13.K010.DEBAR                                 * 02670048
//*-------------------------------------------------------------------* 02680048
//STEP05   EXEC PGM=IDCAMS,COND=(0,NE)                                  02690048
//SYSPRINT DD SYSOUT=*                                                  02700048
//INPUT1   DD DSN=BP13.BLANK.CHAR300,DISP=OLD                           02710048
//SYSIN    DD  *                                                        02720048
          DEL (BP13.K010.DEBAR) CL PURGE                                02730048
          DEF CLUSTER                                -                  02740048
              (NAME(BP13.K010.DEBAR)                -                   02750048
              CYL(50 10)                             -                  02760048
              STORCLAS(PRODRLS)                      -                  02770048
              LOG(NONE)                              -                  02780048
              UNIQUE                                 -                  02790048
              SHR(2 3))                              -                  02800048
            DATA                                     -                  02810048
              (NAME(BP13.K010.DEBAR.DATA)           -                   02820048
              KEYS(44 0)                             -                  02830048
              RECSZ(300 300)                         -                  02840048
              CISZ(8192)                             -                  02850048
              FSPC(10 10))                           -                  02860048
            INDEX                                    -                  02870048
              (NAME(BP13.K010.DEBAR.INDEX)           -                  02880048
              CISZ(8192))                                               02890048
      IF MAXCC = 0 THEN                              -                  02900048
         REPRO IFILE(INPUT1)                         -                  02910048
               ODS(BP13.K010.DEBAR)                                     02920048
//*-------------------------------------------------------------------* 03256048
//* SORT THE DAILY SO FILE IN REGN-NO ORDER                           * 03257048
//* INCLUDE ONLY CA=Y CASES                                           * 03258048
//*-------------------------------------------------------------------* 03259048
//STEP06   EXEC SORT,COND=(0,NE)                                        03260048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=OLD                            03270048
//SORTOUT  DD DSN=&&STEP2D,DISP=(,PASS),                                03280048
//         UNIT=(SYSDA,1),SPACE=(CYL,(60,10),RLSE),                     03290048
//         DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                          03300048
//SYSIN     DD  *                                                       03310048
   SORT FIELDS=(1,8,A),FORMAT=CH                                        03320048
   INCLUDE COND=(1173,1,CH,EQ,C'Y')                                     03330048
//*-------------------------------------------------------------------* 03340048
//* TAG DEBARMENT FOR NUM-CA-TAG = CA, CD                             * 03350048
//* CLOSE FILE : BP04K010, BP13K665                                   * 03360048
//*-------------------------------------------------------------------* 03370048
//STEP07   EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE),REGION=4096K       03380048
//BP13F410 DD DSN=&&STEP2D,DISP=(OLD,PASS)                              03390048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             03400048
//   AMP=('BUFND=23')                                                   03410048
//BP13K820 DD DSN=BP13.K820.OCCUP,DISP=SHR,                             03420048
//   AMP=('BUFND=23')                                                   03430048
//BP04K010 DD DSN=BP13.K010.DEBAR,DISP=SHR,                             03440048
//   AMP=('BUFND=23')                                                   03450048
//BP13K665 DD DSN=BP13.K665.DEBARREF.SOC,DISP=SHR,                      03460048
//   AMP=('BUFND=23')                                                   03470048
//BP13L631 DD SYSOUT=*                                                  03480048
//SYSPRINT DD SYSOUT=*                                                  03490048
//SYSABOUT DD SYSOUT=*                                                  03500048
//SYSOUT   DD SYSOUT=*                                                  03510048
//SYSTSPRT DD SYSOUT=*                                                  03520048
//SYSTSIN DD *                                                          03530048
 DSN SYSTEM(DSN)                                                        03540048
  RUN PROGRAM(BP13C631) PLAN(BP13PCC) -                                 03550048
     LIB('BP13.BATCH.LOADCOB')                                          03560048
 END                                                                    03570048
//*                                                                     03580048
//*-------------------------------------------------------------------* 02930048
//* SORT THE DAILY SO FILE IN REGN-NO ORDER                           * 02940048
//*-------------------------------------------------------------------* 02950048
//STEP08   EXEC SORT,COND=(0,NE)                                        02960048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=OLD                            02970048
//SORTOUT  DD DSN=&&BP13F410,DISP=(,PASS),                              02980048
//            UNIT=(SYSDA,1),SPACE=(CYL,(60,10),RLSE),                  02990048
//            DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                       03000048
//SYSIN    DD  *                                                        03010048
   SORT FIELDS=(1,8,A),FORMAT=CH                                        03020048
   INCLUDE COND=(1,3,CH,NE,C'   ')                                      03030048
//*-------------------------------------------------------------------* 03040048
//* TAGGING OF SUBSIDISED HOUSING LOAN                                * 03050048
//* CLOSE FILE : BP04K010, BP13K665                                   * 03060048
//*-------------------------------------------------------------------* 03070048
//STEP09   EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE),REGION=4096K       03080048
//BP13F410 DD DSN=&&BP13F410,DISP=(OLD,PASS)                            03090048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             03100048
//            AMP=('BUFND=23')                                          03110048
//BP13K820 DD DSN=BP13.K820.OCCUP,DISP=SHR,                             03120048
//            AMP=('BUFND=23')                                          03130048
//BP13K893 DD DSN=BP13.K893.APPLN.HISTORY,DISP=SHR,                     03140048
//            AMP=('BUFND=23')                                          03150048
//BP13K895 DD DSN=BP13.K895.OCCUP.HISTORY,DISP=SHR,                     03160048
//            AMP=('BUFND=23')                                          03170048
//BP13K891 DD DSN=BP13.K895.OCCUP.HISTORY.PATH1,DISP=SHR,               03180048
//            AMP=('BUFND=23')                                          03190048
//BP04K010 DD DSN=BP13.K010.DEBAR,DISP=SHR,                             03200048
//            AMP=('BUFND=23')                                          03210048
//BP13K640 DD DSN=BP13.K640.CA.ACTION,DISP=SHR,                         03211048
//            AMP=('BUFND=23')                                          03212048
//BP13K665 DD DSN=BP13.K665.DEBARREF.SOC,DISP=SHR,                      03213048
//            AMP=('BUFND=23')                                          03214048
//P04K010  DD DSN=BP04.K010.DEBAR,DISP=SHR,                             03215048
//            AMP=('BUFND=23')                                          03216048
//BP13L617 DD DSN=BP13.L617.MAILRPT,DISP=OLD                            03217048
//P13L617B DD DSN=BP13.L617B.MAILRPT,DISP=OLD                           03218048
//SYSPRINT DD SYSOUT=*                                                  03219048
//SYSABOUT DD SYSOUT=*                                                  03220048
//SYSOUT   DD SYSOUT=*                                                  03230048
//SYSTSPRT DD SYSOUT=*                                                  03240048
//SYSTSIN DD *                                                          03250048
 DSN SYSTEM(DSN)                                                        03251048
  RUN PROGRAM(BP13C617) PLAN(BP13PCC) -                                 03252048
     LIB('BP13.BATCH.LOADCOB')                                          03253048
 END                                                                    03254048
//*                                                                     03255048
//*-------------------------------------------------------------------* 03590048
//* SORT THE DAILY SO FILE IN REGN-NO ORDER                           * 03600048
//* INCLUDE ONLY CA=E/Y CASES                                         * 03610048
//*-------------------------------------------------------------------* 03620048
//STEP10   EXEC SORT,COND=(0,NE)                                        03630048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=OLD                            03640048
//SORTOUT  DD DSN=&&STEP2F,DISP=(,PASS),                                03650048
//         UNIT=(SYSDA,1),SPACE=(CYL,(60,10),RLSE),                     03660048
//         DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                          03670048
//SYSIN     DD  *                                                       03680048
   SORT FIELDS=(1,8,A),FORMAT=CH                                        03690048
   INCLUDE COND=(1173,1,CH,EQ,C'E',OR,                                  03700048
                 1173,1,CH,EQ,C'Y')                                     03710048
//*-------------------------------------------------------------------* 03720048
//* TO CAPTURE ALL ESSENTIAL OCCUPIERS                                * 03730048
//*-------------------------------------------------------------------* 03740048
//STEP11   EXEC PGM=BP13C647,REGION=4096K                               03750048
//BP13F410 DD DSN=&&STEP2F,DISP=(OLD,PASS)                              03760048
//BP13K820 DD DSN=BP13.K820.OCCUP,DISP=SHR,                             03770048
//   AMP=('BUFND=23')                                                   03780048
//P13F820A DD DSN=BP13.F820.OCCUP.FRZ,DISP=SHR                          03790048
//SYSPRINT DD SYSOUT=*                                                  03800048
//SYSABOUT DD SYSOUT=*                                                  03810048
//SYSOUT   DD SYSOUT=*                                                  03820048
//*                                                                     03830048
//*-------------------------------------------------------------------* 03840048
//* TAG DEBARMENT FOR FREEZE OCCUPIER TAG                             * 03850048
//* CLOSE FILE : BP04K010, BP13K665                                   * 03860048
//*-------------------------------------------------------------------* 03870048
//STEP12   EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE),REGION=4096K       03880048
//BP13K410 DD DSN=BP13.K400.SALEORD,DISP=SHR,                           03890048
//            AMP=('BUFND=23')                                          03900048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             03910048
//            AMP=('BUFND=23')                                          03920048
//BP13F820 DD DSN=BP13.F820.OCCUP.FRZ,DISP=SHR                          03930048
//BP04K010 DD DSN=BP13.K010.DEBAR,DISP=SHR,                             03940048
//            AMP=('BUFND=23')                                          03950048
//BP13K665 DD DSN=BP13.K665.DEBARREF.SOC,DISP=SHR,                      03960048
//            AMP=('BUFND=23')                                          03970048
//BP13L644 DD SYSOUT=*                                                  03980048
//SYSPRINT DD SYSOUT=*                                                  03990048
//SYSABOUT DD SYSOUT=*                                                  04000048
//SYSOUT   DD SYSOUT=*                                                  04010048
//SYSTSPRT DD SYSOUT=*                                                  04020048
//SYSTSIN DD *                                                          04030048
 DSN SYSTEM(DSN)                                                        04040048
  RUN PROGRAM(BP13C644) PLAN(BP13PCC) -                                 04050048
     LIB('BP13.BATCH.LOADCOB')                                          04060048
 END                                                                    04070048
//*                                                                     04080048
//*-------------------------------------------------------------------* 04090048
//* SORT OUT TRANS-CD=SP FOR DEBAR RED CAES                           * 04100048
//*-------------------------------------------------------------------* 04110048
//STEP16  EXEC SORT,COND=(0,NE)                                         04120048
//SORTIN   DD  DSN=BP13.F310.TRANS,                                     04130048
//             DISP=SHR                                                 04140048
//SORTOUT  DD  DSN=&&PF310SP,                                           04150048
//             DISP=(NEW,PASS),                                         04160048
//             UNIT=SYSDA,                                              04170048
//             SPACE=(TRK,(150,20),RLSE),                               04180048
//             DCB=(RECFM=FB,LRECL=150,BLKSIZE=23400)                   04190048
//SYSIN    DD  *                                                        04200048
    SORT FIELDS=(17,11,A),FORMAT=CH                                     04210048
    INCLUDE COND=(9,2,CH,EQ,C'SP')                                      04220048
//*-------------------------------------------------------------------* 04230048
//* TO CREATE DEBARMENT REC FOR RED ENJOY TENANCY DISCOUNT            * 04240048
//* CLOSE FILE : BP04K010                                             * 04250048
//*-------------------------------------------------------------------* 04260048
//STEP17   EXEC PGM=BP13C283,COND=(0,NE)                                04270048
//BP13F310 DD DSN=&&PF310SP,DISP=(OLD,PASS)                             04280048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             04290048
//            AMP=('BUFND=4','BUFNI=8')                                 04300048
//BP13K820 DD DSN=BP13.K820.OCCUP,DISP=SHR,                             04310048
//            AMP=('BUFND=4','BUFNI=8')                                 04320048
//BP13K830 DD DSN=BP13.K830.ADDR,DISP=SHR,                              04330048
//            AMP=('BUFND=4','BUFNI=8')                                 04340048
//BP13K831 DD DSN=BP13.K830.ADDR.PATH1,DISP=SHR,                        04350048
//            AMP=('BUFND=4','BUFNI=8')                                 04360048
//BP13K060 DD DSN=BP13.K060.CODETAB,DISP=SHR                            04370048
//BP04K010 DD DSN=BP13.K010.DEBAR,DISP=SHR                              04380048
//BP13L283 DD SYSOUT=2,OUTPUT=(*.PRT04.FORMID),DEST=IOM2                04390048
//SYSPRINT DD SYSOUT=*                                                  04400048
//SYSDBG   DD SYSOUT=*                                                  04410048
//SYSDBOUT DD SYSOUT=*                                                  04420048
//SYSOUT   DD SYSOUT=*                                                  04430048
//*                                                                     04440048
//*-------------------------------------------------------------------* 04450048
//* SORT K010 TO F010 FOR DEBARMENT SYSTEM                            * 04460048
//*-------------------------------------------------------------------* 04470048
//STEP18B  EXEC SORT,COND=(0,NE)                                        04480048
//SORTIN   DD DSN=BP13.K010.DEBAR,DISP=SHR                              04490048
//SORTOUT  DD DSN=&&F010DEBR,DISP=(,PASS),                              04500048
//         UNIT=SYSDA,                                                  04510048
//         SPACE=(CYL,(50,20),RLSE),                                    04520048
//         DCB=(RECFM=FB,LRECL=300,BLKSIZE=27900)                       04530048
//SYSIN   DD *                                                          04540048
    SORT FIELDS=(1,44,A),FORMAT=CH                                      04550048
    INCLUDE COND=(34,2,CH,NE,C'XX')                                     04560048
//*                                                                     04570048
//*-------------------------------------------------------------------* 04580048
//* REPORT FOR CASES DEBARRED UNDER THE EAPG PORTAL DEBARMENT         * 04590048
//*-------------------------------------------------------------------* 04600048
//STEP18C  EXEC PGM=BP13C26D,COND=(0,NE)                                04610048
//P04F010  DD DSN=&&F010DEBR,DISP=(OLD,PASS)                            04620048
//P04K010  DD DSN=BP04.K010.DEBAR,DISP=SHR,                             04630048
//         AMP=('BUFND=4','BUFNI=8')                                    04640048
//BP13K820 DD DSN=BP13.K820.OCCUP,DISP=SHR,                                     
//         AMP=('BUFND=4','BUFNI=8')                                            
//BP13K821 DD DSN=BP13.K820.OCCUP.PATH1,DISP=SHR,                               
//         AMP=('BUFND=4','BUFNI=8')                                            
//P04F010A DD DSN=&&F010DAY,DISP=(,PASS),                               04650048
//         UNIT=SYSDA,                                                  04660048
//         SPACE=(CYL,(50,20),RLSE),                                    04670048
//         DCB=(RECFM=FB,LRECL=300,BLKSIZE=27900)                       04680048
//SYSDBG   DD SYSOUT=*                                                  04690048
//SYSDBOUT DD SYSOUT=*                                                  04700048
//SYSOUT   DD SYSOUT=*                                                  04710048
/*                                                                      04720048
//*-----------------------------------------------------------------            
//* GET CUSTOMER ID FROM BE01C808                                               
//*-----------------------------------------------------------------            
//STEP18E  EXEC PGM=IKJEFT01,COND=(0,NE),DYNAMNBR=20                            
//P04F010  DD DSN=&&F010DAY,DISP=(OLD,PASS)                                     
//BE01KMSG DD DSN=BE01.KMSG.ONLINE,DISP=SHR,                                    
//         AMP=('BUFND=4','BUFNI=8')                                            
//BE01F003 DD DSN=BP13.BE01F003.CUSTUPD.LOGB,DISP=OLD                           
//BE01F903 DD DSN=BP13.BE01F903.INVUPD,DISP=OLD                                 
//BE01F400 DD DSN=BP13.BE01F400.SENDID.NEWID,DISP=OLD                           
//P04F010A DD DSN=&&F010DAY1,DISP=(,PASS),                                      
//            UNIT=SYSDA,                                                       
//            DCB=(RECFM=FB,LRECL=300),                                         
//            SPACE=(CYL,(50,20),RLSE)                                          
//SYSTSPRT DD SYSOUT=*                                                          
//SYSPRINT DD SYSOUT=*                                                          
//SYSUDUMP DD SYSOUT=*                                                          
//SYSOUT   DD SYSOUT=*                                                          
//SYSABOUT DD SYSOUT=*                                                          
//SYSTSIN  DD  *                                                                
  DSN SYSTEM(DSN)                                                               
  RUN PROGRAM(BP13CZ3M) PLAN(BP13PCC) -                                         
      LIB('BP13.BATCH.LOADCOB')                                                 
  END                                                                           
/*                                                                              
//*-------------------------------------------------------------------*         
//* REPRO THE BP13 DATASET AND MOD TO BE01 DATASET                    *         
//*-------------------------------------------------------------------*         
//STEP18F   EXEC PGM=IDCAMS                                                     
//SYSPRINT DD SYSOUT=*                                                          
//INPUT1  DD DSN=BP13.BE01F003.CUSTUPD.LOGB,DISP=SHR                            
//OUTPUT1 DD DSN=BE01.F003.CUSTUPD.LOGB.BP13,DISP=MOD                           
//INPUT2  DD DSN=BP13.BE01F903.INVUPD,DISP=SHR                                  
//OUTPUT2 DD DSN=BE01.F903.INVUPD.BP13,DISP=MOD                                 
//INPUT3  DD DSN=BP13.BE01F400.SENDID.NEWID,DISP=SHR                            
//OUTPUT3 DD DSN=BE01.F400.SENDID.NEWID.BP13,DISP=MOD                           
//SYSIN    DD *                                                                 
      REPRO INFILE(INPUT1)  OUTFILE(OUTPUT1)                                    
      REPRO INFILE(INPUT2)  OUTFILE(OUTPUT2)                                    
      REPRO INFILE(INPUT3)  OUTFILE(OUTPUT3)                                    
//*-------------------------------------------------------------------* 04730048
//* BACKUP DEBARMENT RECORD                                             04740048
//*-------------------------------------------------------------------* 04750048
//STEP18D  EXEC PGM=IDCAMS,COND=(0,NE)                                  04760048
//SYSPRINT DD SYSOUT=*                                                  04770048
//INPUT1   DD DSN=&&F010DAY1,DISP=(OLD,PASS)                            04780048
//OUTPUT1  DD DSN=BP13.F010.DEBAR.SOC,DISP=MOD                          04790048
//OUTPUT2  DD DSN=BP13.F010.DEBAR.SOC.DAILY(+1),DISP=(NEW,CATLG),       04800048
//            SPACE=(CYL,(20,10)),                                      04810048
//            DCB=(BP13.GDG,BLKSIZE=27900,LRECL=300,RECFM=FB)           04820048
//*                                                                     04830048
//INPUT3   DD DSN=&&F010DEBR,DISP=(OLD,PASS)                            04840048
//OUTPUT3  DD DSN=BP13.F010.DEBAR.SOC.ACCUM,DISP=MOD                    04850048
//OUTPUT4  DD DSN=BP13.F010.DEBAR.SOC.D400(+1),DISP=(NEW,CATLG),        04860048
//            SPACE=(CYL,(20,10)),                                      04870048
//            DCB=(BP13.GDG,BLKSIZE=27900,LRECL=300,RECFM=FB)           04880048
//*                                                                     04890048
//SYSIN    DD  *                                                        04900048
   REPRO INFILE(INPUT1)  OUTFILE(OUTPUT1)                               04910048
   REPRO INFILE(INPUT1)  OUTFILE(OUTPUT2)                               04920048
   REPRO INFILE(INPUT3)  OUTFILE(OUTPUT3)                               04930048
   REPRO INFILE(INPUT3)  OUTFILE(OUTPUT4)                               04940048
//*                                                                     04950048
//*-------------------------------------------------------------------* 04960048
//* OUTPUT SALES ORDER TO PUB AND RESETTLEMENT FILES                  * 04970048
//*-------------------------------------------------------------------* 04980048
//STEP19   EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE),REGION=4096K       04990048
//BP13F410 DD DSN=BP13.F410.DAILYSO,DISP=SHR                            05000048
//BP13F418 DD DSN=BP13.F418.PUB,DISP=MOD                                05010048
//BP13F220 DD DSN=BP13.F220.RESETTLE,DISP=MOD                           05020048
//BP13L409 DD SYSOUT=2,OUTPUT=(*.PRT01.FORMID),DEST=IOM2                05030048
//SYSPRINT DD SYSOUT=*                                                  05040048
//SYSTSPRT DD SYSOUT=*                                                  05050048
//SYSOUT   DD SYSOUT=*                                                  05060048
//SYSDBG   DD SYSOUT=*                                                  05070048
//SYSDBOUT DD SYSOUT=*                                                  05080048
//SYSTSIN  DD *                                                         05090048
  DSN SYSTEM(DSN)                                                       05100048
  RUN PROGRAM(BP13C409) PLAN(BP13PCC) -                                 05110048
      LIB('BP13.BATCH.LOADCOB')                                         05120048
  END                                                                   05130048
/*                                                                      05140048
//*-------------------------------------------------------------------* 05150048
//* OUTPUT FILE TO SOC APPLN/OCCUP INFOR TO RDBII                     * 05160048
//*-------------------------------------------------------------------* 05170048
//STEP20   EXEC PGM=BP13C448,COND=(0,NE)                                05180048
//BP13F410 DD DSN=BP13.F410.DAILYSO,DISP=SHR                            05190048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             05200048
//            AMP=('BUFNI=4','BUFND=8')                                 05210048
//BP13K820 DD DSN=BP13.K820.OCCUP,DISP=SHR,                             05220048
//            AMP=('BUFNI=4','BUFND=8')                                 05230048
//BP13K399 DD DSN=BP13.K399.APPLOCCP,DISP=SHR,                          05240048
//            AMP=('BUFNI=4','BUFND=8')                                 05250048
//BM06K110 DD DSN=BM06.K110.FLATINV,DISP=SHR,                           05260048
//            AMP=('BUFNI=4','BUFND=8')                                 05270048
//BE03F222 DD DSN=BP13.BE03.F222.NEWSALE,DISP=MOD                       05280048
//SYSPRINT DD SYSOUT=*                                                  05290048
//SYSOUT   DD SYSOUT=*                                                  05300048
//SYSDBG   DD SYSOUT=*                                                  05310048
//SYSDBOUT DD SYSOUT=*                                                  05320048
//*-------------------------------------------------------------------* 05330048
//* OUTPUT FILE TO SMS SYSTEM                                         * 05340048
//*-------------------------------------------------------------------* 05350048
//STEP21   EXEC PGM=BP13C411,COND=(0,NE)                                05360048
//BP13F410 DD DSN=BP13.F410.DAILYSO,DISP=SHR                            05370048
//BM06K110 DD DSN=BM06.K110.FLATINV,DISP=SHR,                           05380048
//            AMP=('BUFND=4','BUFNI=8')                                 05390048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             05400048
//            AMP=('BUFND=4','BUFNI=8')                                 05410048
//BP17K000 DD DSN=BP17.K000.SCHREF,DISP=SHR,                            05420048
//            AMP=('BUFND=4','BUFNI=8')                                 05430048
//BP17K001 DD DSN=BP17.K000.SCHREF.PATH1,DISP=SHR,                      05440048
//            AMP=('BUFND=4','BUFNI=8')                                 05450048
//BP17K100 DD DSN=BP17.K100.SERSPROJ,DISP=SHR,                          05460048
//            AMP=('BUFND=4','BUFNI=8')                                 05470048
//BP17K110 DD DSN=BP17.K110.SERSBLK,DISP=SHR,                           05480048
//            AMP=('BUFND=4','BUFNI=8')                                 05490048
//BP17K111 DD DSN=BP17.K110.SERSBLK.PATH1,DISP=SHR,                     05500048
//            AMP=('BUFND=4','BUFNI=8')                                 05510048
//BP17K120 DD DSN=BP17.K120.SERSUNIT,DISP=SHR,                          05520048
//            AMP=('BUFND=4','BUFNI=8')                                 05530048
//BP13KD15 DD DSN=BP13.KD15.DBSSTP,DISP=SHR,                            05540048
//            AMP=('BUFND=4','BUFNI=8')                                 05550048
//BP13KD11 DD DSN=BP13.KD15.DBSSTP.PATH1,DISP=SHR,                      05560048
//            AMP=('BUFND=4','BUFNI=8')                                 05570048
//BP13KD12 DD DSN=BP13.KD15.DBSSTP.PATH2,DISP=SHR,                      05580048
//            AMP=('BUFND=4','BUFNI=8')                                 05590048
//BP13KA20 DD DSN=BP13.KA20.BLOCK,DISP=SHR,                             05600048
//            AMP=('BUFND=4','BUFNI=8')                                 05610048
//BP13KD00 DD DSN=BP13.KD00.DBSMAST,DISP=SHR,                           05620048
//            AMP=('BUFND=4','BUFNI=8')                                 05630048
//BP13K480 DD DSN=BP13.K480.CONTRA,DISP=SHR,                            05640048
//            AMP=('BUFND=4','BUFNI=8')                                 05650048
//BP13F430 DD DSN=BP13.F430.SOTRANS,DISP=MOD                            05660048
//BP13K670 DD DSN=BP13.K670.BO,DISP=SHR                                 05670048
//SYSPRINT DD SYSOUT=*                                                  05680048
//SYSOUT   DD SYSOUT=*                                                  05690048
//SYSDBG   DD SYSOUT=*                                                  05700048
//SYSDBOUT DD SYSOUT=*                                                  05710048
//*-------------------------------------------------------------------* 05720048
//* SORT SOC INTEFACE WITH SMS FOR POLLING SYSTEM                     * 05730048
//*-------------------------------------------------------------------* 05740048
//STEP22   EXEC SORT,COND=(0,NE)                                        05750048
//SORTIN   DD DSN=BP13.F430.SOTRANS,DISP=SHR                            05760048
//SORTOUT  DD DSN=BP13.F430.SOTRANS.POLL,DISP=MOD                       05770048
//SYSIN    DD *                                                         05780048
     SORT FIELDS=(1,11,A),FORMAT=CH                                     05790048
/*                                                                      05800048
//*-------------------------------------------------------------------* 05810048
//* UPDATE K800-NUM-ALLOC-TAG TO 'KI' FOR KEY ISSUED CASES            * 05820048
//* CLOSE FILE : BP13K800                                             * 05830048
//*-------------------------------------------------------------------* 05840048
//STEP23   EXEC PGM=BP13C425,COND=(0,NE)                                05850048
//BP13F410 DD DSN=BP13.F410.DAILYSO,DISP=SHR                            05860048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             05870048
//            AMP=('BUFND=4','BUFNI=8')                                 05880048
//BP13KD05 DD DSN=BP13.KD05.CPFGRANT,DISP=SHR,                          05890048
//            AMP=('BUFND=4','BUFNI=8')                                 05900048
//SYSPRINT DD SYSOUT=*                                                  05910048
//SYSOUT   DD SYSOUT=*                                                  05920048
//SYSDBG   DD SYSOUT=*                                                  05930048
//SYSDBOUT DD SYSOUT=*                                                  05940048
//*-------------------------------------------------------------------* 05950048
//* ACCUMULATE THE DAILY VALID SALES ORDER TO CUMULATIVE SALES ORDER  * 05960048
//* PRINT DETAIL SALE ORDER IF REC FOUND IN BP13K410                  * 05970048
//* CLOSE FILE : BP13K410                                             * 05980048
//*-------------------------------------------------------------------* 05990048
//STEP24   EXEC PGM=BP13C418,COND=(0,NE)                                06000048
//BP13F410 DD DSN=BP13.F410.DAILYSO,DISP=SHR                            06010048
//BP13K410 DD DSN=BP13.K410.ACCUMSO,DISP=OLD,                           06020048
//            AMP=('BUFND=4','BUFNI=8')                                 06030048
//BP13L418 DD SYSOUT=*                                                  06040048
//SYSPRINT DD SYSOUT=*                                                  06050048
//SYSOUT   DD SYSOUT=*                                                  06060048
//SYSDBG   DD SYSOUT=*                                                  06070048
//SYSDBOUT DD SYSOUT=*                                                  06080048
//*-------------------------------------------------------------------* 03590048
//* SORT THE DAILY SO FILE IN REGN-NO ORDER                           * 03600048
//* COTMA CASES  COTMA = Y OR P                                       * 03610048
//*-------------------------------------------------------------------* 03620048
//STEP24A  EXEC SORT,COND=(0,NE)                                        03630048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=OLD                            03640048
//SORTOUT  DD DSN=&&STEP10A,DISP=(,PASS),                               03650048
//         UNIT=(SYSDA,1),SPACE=(CYL,(60,10),RLSE),                     03660048
//         DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                          03670048
//SYSIN     DD  *                                                       03680048
   SORT FIELDS=(1,8,A),FORMAT=CH                                        03690048
   INCLUDE COND=(((1495,1,CH,EQ,C'P'),OR,                               03700048
                  (1495,1,CH,EQ,C'Y')),AND,                             03700048
                 1494,1,CH,EQ,C' ')                                     03710048
//*-------------------------------------------------------------------* 03720048
//* TO CAPTURE ALL ESSENTIAL OCCUPIERS                                * 03730048
//*-------------------------------------------------------------------* 03740048
//STEP24B  EXEC PGM=BP13C41D,REGION=4096K                               03750048
//BP13F410 DD DSN=&&STEP10A,DISP=(OLD,PASS)                             03760048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             03770048
//   AMP=('BUFND=23')                                                   03780048
//BP13K410 DD DSN=BP13.K410.ACCUMSO,DISP=SHR                            03790048
//SYSPRINT DD SYSOUT=*                                                  03800048
//SYSABOUT DD SYSOUT=*                                                  03810048
//SYSOUT   DD SYSOUT=*                                                  03820048
//*                                                                     03830048
//*-------------------------------------------------------------------* 06090048
//* SORT KIV SO IN CUR REGN SEQUENCE                                  * 06100048
//*-------------------------------------------------------------------* 06110048
//STEP25   EXEC SORT,COND=(0,NE)                                        06120048
//SORTIN   DD DSN=BP13.F405.KIVSO,DISP=SHR                              06130048
//         DD DSN=BP13.BLANK.CHAR2000,DISP=SHR                          06140048
//SORTOUT  DD DSN=&&BP13F405,DISP=(NEW,PASS),                           06150048
//            SPACE=(TRK,(20,5),RLSE),UNIT=SYSDA,                       06160048
//            DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                       06170048
//SYSIN    DD  *                                                        06180048
      SORT FIELDS=(1,8,A),FORMAT=CH                                     06190048
      SUM  FIELDS=NONE                                                  06200048
//*-------------------------------------------------------------------* 06210048
//* DEFINE SALES ORDER FILE BP13K400 FOR THE NEXT DAY                 * 06220048
//* CLOSE FILE : BP13K400                                             * 06230048
//*-------------------------------------------------------------------* 06240048
//STEP26   EXEC PGM=IDCAMS,COND=(0,NE)                                  06250048
//SYSPRINT DD SYSOUT=*                                                  06260048
//INPUT    DD DSN=&&BP13F405,DISP=(OLD,PASS)                            06270048
//SYSIN    DD *                                                         06280048
         DEL (BP13.K400.SALEORD) CL PURGE                               06290048
         DEF CL (NAME(BP13.K400.SALEORD)         -                      06300048
                 TRK(30 50)                      -                      06310048
                 DATACLAS(MULTIVOL)              -                      06320048
                 STORCLAS(PRODRLS)               -                      06330048
                 LOG(NONE)                       -                      06340048
                 SPEED                           -                      06350048
                 UNIQUE                          -                      06360048
                 SHR(2 3))                       -                      06370048
                 DATA                            -                      06380048
                 (NAME(BP13.K400.SALEORD.DATA)   -                      06390048
                 KEYS(8 0)                       -                      06400048
                 RECSZ(2000 2000)                -                      06410048
                 FSPC(10 10)                     -                      06420048
                 CISZ(4096))                     -                      06430048
                 INDEX                           -                      06440048
                 (NAME(BP13.K400.SALEORD.INDEX))                        06450048
         IF LASTCC = 0 THEN                      -                      06460048
           REPRO IFILE(INPUT)                    -                      06470048
                 ODS(BP13.K400.SALEORD)                                 06480048
//*-------------------------------------------------------------------* 06490048
//* SORT DAILY SO RAISED TO WEEKLY ACCUMULATED FILE                   * 06500048
//* ACCUMULATED SO FILE CONTAINS REC WILL BE CLEAR ON SATURDAY        * 06510048
//*-------------------------------------------------------------------* 06520048
//STEP27   EXEC SORT,COND=(0,NE)                                        06530048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            06540048
//SORTOUT  DD DSN=BP13.F410.ACCUMSO,DISP=MOD                            06550048
//SYSIN    DD *                                                         06560048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      06570048
/*                                                                      06580048
//*-------------------------------------------------------------------* 06590048
//* SORT DAILY SO RAISED TO HFE  SO FILE                             *  06600048
//*-------------------------------------------------------------------* 06620048
//STEP27A  EXEC SORT,COND=(0,NE)                                        06530048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            06540048
//SORTOUT  DD DSN=BP13.F410.DAILYSO.HFE,DISP=MOD                        06550048
//SYSIN    DD *                                                         06560048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      06570048
/*                                                                      06580048
//*-------------------------------------------------------------------* 06590048
//* SORT DAILY SO RAISED TO MTHLY SO FILE                             * 06600048
//* MTHLY SO FILE WILL BE CLEARED ON MTH END                          * 06610048
//*-------------------------------------------------------------------* 06620048
//STEP28   EXEC SORT,COND=(0,NE)                                        06630048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            06640048
//SORTOUT  DD DSN=BP13.F410.MTHSO,DISP=MOD                              06650048
//SYSIN    DD *                                                         06660048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      06670048
//*-------------------------------------------------------------------* 06680048
//* SORT DAILY CPF-SETASIDE FILE AS BACKUP                            * 06690048
//*-------------------------------------------------------------------* 06700048
//STEP28A  EXEC SORT,COND=(0,NE)                                        06710048
//SORTIN   DD DSN=BP13.K40C.SALEORD.RSV,DISP=SHR                        06720048
//SORTOUT  DD DSN=BP13.F40C.SALEORD.RSV,DISP=SHR                        06730048
//SYSIN    DD *                                                         06740048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      06750048
//*-------------------------------------------------------------------* 06760048
//* SORT DAILY CPF-SETASIDE FILE TO MONTHLY CPF-SETASIDE FILE         * 06770048
//* MTHLY SO FILE WILL BE CLEARED ON MTH END                          * 06780048
//*-------------------------------------------------------------------* 06790048
//STEP28B  EXEC SORT,COND=(0,NE)                                        06800048
//SORTIN   DD DSN=BP13.K40C.SALEORD.RSV,DISP=SHR                        06810048
//SORTOUT  DD DSN=BP13.F40C.SALEORD.RSV.MTH,DISP=MOD                    06820048
//SYSIN    DD *                                                         06830048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      06840048
//*-------------------------------------------------------------------* 06850048
//* SORT OUT SITTING TENANT CASES TO PASS TO RENTAL SYSTEM            * 06860048
//*-------------------------------------------------------------------* 06870048
//STEP29   EXEC SORT,COND=(0,NE)                                        06880048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            06890048
//SORTOUT  DD DSN=BP13.F410.STSALE,DISP=MOD                             06900048
//SYSIN    DD *                                                         06910048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      06920048
     INCLUDE COND=(37,2,CH,EQ,C'02',OR,                                 06930048
                   37,2,CH,EQ,C'2B')                                    06940048
//*-------------------------------------------------------------------* 06950048
//* SORT OUT SITTING TENANT CASES TO PASS TO RENTAL SYSTEM SALES TYPE1* 06960048
//*-------------------------------------------------------------------* 06970048
//STEP29A  EXEC SORT,COND=(0,NE)                                        06980048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            06990048
//SORTOUT  DD DSN=BP13.F410.STSALE,DISP=MOD                             07000048
//SYSIN    DD *                                                         07010048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      07020048
     INCLUDE COND=(37,2,CH,EQ,C'01',AND,                                07030048
                   40,3,CH,EQ,C'LRT')                                   07040048
//*-------------------------------------------------------------------* 07050048
//* SORT OUT SPECIAL HOUSING SCHEME TO PASS TO RENTAL SYSTEM SALES TY1* 07060048
//*-------------------------------------------------------------------* 07070048
//STEP29B  EXEC SORT,COND=(0,NE)                                        07080048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            07090048
//SORTOUT  DD DSN=BP13.F410.STSALE,DISP=MOD                             07100048
//SYSIN    DD *                                                         07110048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      07120048
     INCLUDE COND=(37,2,CH,EQ,C'01',AND,                                07130048
                   914,3,CH,EQ,C'SHS')                                  07140048
//*-------------------------------------------------------------------* 07150048
//* SORT OUT SITTING TENANT CASES TO PASS TO STFS                     * 07160048
//*-------------------------------------------------------------------* 07170048
//STEP31   EXEC SORT,COND=(0,NE)                                        07180048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            07190048
//SORTOUT  DD DSN=BP13.F410.DAILYSO.STFS,DISP=MOD                       07200048
//SYSIN    DD *                                                         07210048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      07220048
     INCLUDE COND=(37,2,CH,EQ,C'2B')                                    07230048
//*-------------------------------------------------------------------* 07240048
//* SORT OUT TYPE '10' SA-TP CASES ONLY                               * 07250048
//* OMIT BANK LOAN CASES                                              * 07260048
//*-------------------------------------------------------------------* 07270048
//STEP32   EXEC SORT,COND=(0,NE)                                        07280048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            07290048
//SORTOUT  DD DSN=&&SATP,DISP=(NEW,PASS),                               07300048
//         SPACE=(TRK,(10,5),RLSE),UNIT=SYSDA,                          07310048
//         DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                          07320048
//SYSIN    DD *                                                         07330048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      07340048
     INCLUDE COND=((37,2,CH,EQ,C'10',AND,1135,1,CH,EQ,C'Y'),&,          07350048
                   (68,1,CH,NE,C'B'),OR,(43,3,CH,EQ,C'MGL'))            07360048
//*-------------------------------------------------------------------* 07370048
//* SORT OUT TYPE '13''18' WITH TEMP LOAN SA-TP CASES ONLY              07380048
//* OMIT BANK LOAN CASES                                              * 07390048
//*-------------------------------------------------------------------* 07400048
//STEP32A  EXEC SORT,COND=(0,NE)                                        07410048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            07420048
//SORTOUT  DD DSN=&&SATP,DISP=(MOD,PASS),                               07430048
//         SPACE=(TRK,(10,5),RLSE),UNIT=SYSDA,                          07440048
//         DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                          07450048
//SYSIN    DD *                                                         07460048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      07470048
     INCLUDE COND=((37,2,CH,EQ,C'13',OR,37,2,CH,EQ,C'18'),&,            07480048
                  (1135,1,CH,EQ,C'Y'),&,                                07490048
                  (654,7,CH,GT,C'0000000'),&,                           07500048
                  (68,1,CH,NE,C'B'))                                    07510048
//*-------------------------------------------------------------------* 07520048
//* UPDATE BK FEE AND LEASE STAMP FEE TO RFF MASTER  FOR TYPE 10, 13, * 07530048
//* 18 SA-TP CASES                                                    * 07540048
//* CLOSE FILE : BP13K800                                             * 07550048
//*-------------------------------------------------------------------* 07560048
//STEP33   EXEC PGM=BP13C601,COND=(0,NE)                                07570048
//BP13F410 DD DSN=&&SATP,DISP=(OLD,PASS)                                07580048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             07590048
//            AMP=('BUFND=4','BUFNI=8')                                 07600048
//BP13K820 DD DSN=BP13.K820.OCCUP,DISP=SHR,                             07610048
//            AMP=('BUFND=4','BUFNI=8')                                 07620048
//BP13K480 DD DSN=BP13.K480.CONTRA,DISP=SHR,                            07630048
//            AMP=('BUFND=4','BUFNI=8')                                 07640048
//SYSPRINT DD SYSOUT=*                                                  07670048
//SYSOUT   DD SYSOUT=*                                                  07680048
//SYSDBG   DD SYSOUT=*                                                  07690048
//SYSDBOUT DD SYSOUT=*                                                  07700048
//*-------------------------------------------------------------------* 07710048
//* SORT OUT TYPE '13''18' WITH TEMP LOAN TP DDS CASES ONLY             07720048
//* WITH BOOKING FEES TRANSFER                                        * 07730048
//* OMIT BANK LOAN CASES                                              * 07740048
//*-------------------------------------------------------------------* 07750048
//STEP32B  EXEC SORT,COND=(0,NE)                                        07760048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            07770048
//SORTOUT  DD DSN=&&DDSTPT,DISP=(MOD,PASS),                             07780048
//         SPACE=(TRK,(10,5),RLSE),UNIT=SYSDA,                          07790048
//         DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                          07800048
//SYSIN    DD *                                                         07810048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      07820048
     INCLUDE COND=((37,2,CH,EQ,C'13',OR,37,2,CH,EQ,C'18'),&,            07830048
                  (1135,1,CH,NE,C'Y'),&,                                07840048
                  (654,7,CH,GT,C'0000000'),&,                           07850048
                  (1028,6,CH,GT,C'000000'),&,                           07860048
                  (68,1,CH,NE,C'B'))                                    07870048
//*-------------------------------------------------------------------* 07880048
//* UPDATE BK FEE AND LEASE STAMP FEE TO RFF MASTER  FOR TYPE 10, 13, * 07890048
//* 18 SA-TP CASES                                                    * 07900048
//* CLOSE FILE : BP13K800                                             * 07910048
//*-------------------------------------------------------------------* 07920048
//STEP33A  EXEC PGM=BP13C601,COND=(0,NE)                                07930048
//BP13F410 DD DSN=&&DDSTPT,DISP=(OLD,PASS)                              07940048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             07950048
//            AMP=('BUFND=4','BUFNI=8')                                 07960048
//BP13K820 DD DSN=BP13.K820.OCCUP,DISP=SHR,                             07970048
//            AMP=('BUFND=4','BUFNI=8')                                 07980048
//BP13K480 DD DSN=BP13.K480.CONTRA,DISP=SHR,                            07990048
//            AMP=('BUFND=4','BUFNI=8')                                 08000048
//SYSPRINT DD SYSOUT=*                                                  08030048
//SYSOUT   DD SYSOUT=*                                                  08040048
//SYSDBG   DD SYSOUT=*                                                  08050048
//SYSDBOUT DD SYSOUT=*                                                  08060048
//*-------------------------------------------------------------------* 08070048
//* SORT OUT TYPE 'DDS' CASES WITH BOOKING FEE TRANSFER ONLY FOR        08080048
//* SALES TYPE 10 TP CASES                                            * 08090048
//* OMIT BANK LOAN CASES                                              * 08100048
//*-------------------------------------------------------------------* 08110048
//STEP32C  EXEC SORT,COND=(0,NE)                                        08120048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            08130048
//SORTOUT  DD DSN=&&DDSTPA,DISP=(NEW,PASS),                             08140048
//         SPACE=(TRK,(10,5),RLSE),UNIT=SYSDA,                          08150048
//         DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                          08160048
//SYSIN    DD *                                                         08170048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      08180048
     INCLUDE COND=((37,2,CH,EQ,C'10'),&,                                08190048
                  (1135,1,CH,EQ,C'N'),&,                                08200048
                  (1028,6,CH,GT,C'000000'))                             08210048
//*-------------------------------------------------------------------* 08220048
//* UPDATE BK FEE AND LEASE STAMP FEE TO RFF MASTER  FOR TYPE 10 TP   * 08230048
//* DDS CASES                                                         * 08240048
//* CLOSE FILE : BP13K800                                             * 08250048
//*-------------------------------------------------------------------* 08260048
//STEP33B  EXEC PGM=BP13C601,COND=(0,NE)                                08270048
//BP13F410 DD DSN=&&DDSTPA,DISP=(OLD,PASS)                              08280048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             08290048
//            AMP=('BUFND=4','BUFNI=8')                                 08300048
//BP13K820 DD DSN=BP13.K820.OCCUP,DISP=SHR,                             08310048
//            AMP=('BUFND=4','BUFNI=8')                                 08320048
//BP13K480 DD DSN=BP13.K480.CONTRA,DISP=SHR,                            08330048
//            AMP=('BUFND=4','BUFNI=8')                                 08340048
//SYSPRINT DD SYSOUT=*                                                  08370048
//SYSOUT   DD SYSOUT=*                                                  08380048
//SYSDBG   DD SYSOUT=*                                                  08390048
//SYSDBOUT DD SYSOUT=*                                                  08400048
//*-------------------------------------------------------------------* 08410048
//* GENERATE GL TRANSACTION & HISTORICAL FIN TRANS FILE FOR BK FEE TR * 08420048
//* FOR TYPE 10 , 13 AND 18 SA-TP CASES.                              * 08430048
//*-------------------------------------------------------------------* 08440048
//STEP34   EXEC PGM=BP13C604,COND=(0,NE)                                08450048
//BP13F400 DD DSN=&&SATP,DISP=(OLD,PASS)                                08460048
//SY02F001 DD DSN=SY02.F001.DATE,DISP=SHR                               08470048
//BP13F120 DD DSN=BP13.F120.SATRAN,DISP=MOD                             08480048
//BP13F130 DD DSN=BP13.F130.SATRAN,DISP=MOD                             08490048
//SYSOUT   DD SYSOUT=*                                                  08500048
//*-------------------------------------------------------------------* 08510048
//* SORT OUT TYPE '10' WITH DDS CASES FOR TP ONLY                       08520048
//* INCL BANK LOAN CASES AND NO LOAN CASES                            * 08530048
//*               (68,1,CH,NE,C'B'))                                    08650048
//*-------------------------------------------------------------------* 08540048
//STEP32B  EXEC SORT,COND=(0,NE)                                        08550048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            08560048
//SORTOUT  DD DSN=&&DDSTP,DISP=(MOD,PASS),                              08570048
//         SPACE=(TRK,(10,5),RLSE),UNIT=SYSDA,                          08580048
//         DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                          08590048
//SYSIN    DD *                                                         08600048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      08610048
     INCLUDE COND=((37,2,CH,EQ,C'10'),&,                                08620048
                  (1135,1,CH,NE,C'Y'),&,                                08630048
                  (1028,6,CH,GT,C'0000000'))                            08640048
//*-------------------------------------------------------------------* 08660048
//* SORT OUT SALES TYPE 13 AND 18 WITH TEMP LOAN, BK FEE TR  AND TP   * 08670048
//* CASES                                                             * 08680048
//*-------------------------------------------------------------------* 08690048
//STEP32C  EXEC SORT,COND=(0,NE)                                        08700048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            08710048
//SORTOUT  DD DSN=&&DDSTP,DISP=(MOD,PASS),                              08720048
//         SPACE=(TRK,(10,5),RLSE),UNIT=SYSDA,                          08730048
//         DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                          08740048
//SYSIN    DD *                                                         08750048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      08760048
     INCLUDE COND=((37,2,CH,EQ,C'13',OR,                                08770048
                    37,2,CH,EQ,C'18'),&,                                08780048
                  (1135,1,CH,NE,C'Y'),&,                                08790048
                  (654,9,CH,GT,C'000000000'),&,                         08800048
                  (1028,6,CH,GT,C'0000000'),&,                          08810048
                  (68,1,CH,NE,C'B'))                                    08820048
//*-------------------------------------------------------------------* 08830048
//* GENERATE GL TRANSACTION & HISTORICAL FIN TRANS FILE FOR BK FEE TR * 08840048
//* FOR TYPE 10 ,13 AND 18 DDS TP CASES                               * 08850048
//*-------------------------------------------------------------------* 08860048
//STEP34A  EXEC PGM=BP13C604,COND=(0,NE)                                08870048
//BP13F400 DD DSN=&&DDSTP,DISP=(OLD,PASS)                               08880048
//SY02F001 DD DSN=SY02.F001.DATE,DISP=SHR                               08890048
//BP13F120 DD DSN=BP13.F120.SATRAN,DISP=MOD                             08900048
//BP13F130 DD DSN=BP13.F130.SATRAN,DISP=MOD                             08910048
//SYSOUT   DD SYSOUT=*                                                  08920048
//*-------------------------------------------------------------------* 08930048
//* SORT OUT F410-NUM-BUYBACK = N/Y CASES                             * 08940048
//* OMIT BANK LOAN CASES                                              * 08950048
//*-------------------------------------------------------------------* 08960048
//STEP35   EXEC SORT,COND=(0,NE)                                        08970048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            08980048
//SORTOUT  DD DSN=&&AA,DISP=(NEW,PASS),                                 08990048
//            SPACE=(TRK,(10,5),RLSE),UNIT=SYSDA,                       09000048
//            DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                       09010048
//SYSIN    DD *                                                         09020048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      09030048
     INCLUDE COND=((1000,1,CH,EQ,C'Y',OR,1000,1,CH,EQ,C'N'),&,          09040048
                   (68,1,CH,NE,C'B'))                                   09050048
//*-------------------------------------------------------------------* 09060048
//* UPDATE BK FEE AND LEASE STAMP FEE TO SOC MASTER FOR AA CASES      * 09070048
//* CLOSE FILE : BP13K800                                             * 09080048
//*-------------------------------------------------------------------* 09090048
//STEP36   EXEC PGM=BP13C461,COND=(0,NE)                                09100048
//BP13F410 DD DSN=&&AA,DISP=(OLD,PASS)                                  09110048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             09120048
//            AMP=('BUFND=4','BUFNI=8')                                 09130048
//BP13K820 DD DSN=BP13.K820.OCCUP,DISP=SHR,                             09140048
//            AMP=('BUFND=4','BUFNI=8')                                 09150048
//BP13L461 DD SYSOUT=2,OUTPUT=(*.PRT01.FORMID),DEST=IOM2                09160048
//P13L461A DD SYSOUT=2,OUTPUT=(*.PRT01.FORMID),DEST=IOM2                09170048
//SYSPRINT DD SYSOUT=*                                                  09180048
//SYSOUT   DD SYSOUT=*                                                  09190048
//SYSDBG   DD SYSOUT=*                                                  09200048
//SYSDBOUT DD SYSOUT=*                                                  09210048
//*-------------------------------------------------------------------* 09220048
//* GENERATE GL TRANSACTION & HISTORICAL FIN TRANS FILE FOR BK FEE TR * 09230048
//* FOR AA CASES                                                      * 09240048
//*-------------------------------------------------------------------* 09250048
//STEP37   EXEC PGM=BP13C464,COND=(0,NE)                                09260048
//BP13F400 DD DSN=&&AA,DISP=(OLD,PASS)                                  09270048
//SY02F001 DD DSN=SY02.F001.DATE,DISP=SHR                               09280048
//BP13F120 DD DSN=BP13.F120.SATRAN,DISP=MOD                             09290048
//BP13F130 DD DSN=BP13.F130.SATRAN,DISP=MOD                             09300048
//SYSOUT   DD SYSOUT=*                                                  09310048
//*-------------------------------------------------------------------* 09320048
//* SORT BP17K000 IN REGN NO SEQUENCE                                 * 09330048
//*-------------------------------------------------------------------* 09340048
//STEP40   EXEC SORT,COND=(0,NE)                                        09350048
//SORTIN   DD DSN=BP17.K000.SCHREF,DISP=SHR                             09360048
//SORTOUT  DD DSN=&&BP17F000,DISP=(NEW,PASS),                           09370048
//         UNIT=3380,SPACE=(CYL,(10,20),RLSE),                          09380048
//         DCB=(RECFM=FB,LRECL=200,BLKSIZE=27800)                       09390048
//SYSIN    DD *                                                         09400048
      SORT FIELDS=(18,8,A),FORMAT=CH                                    09410048
//*-------------------------------------------------------------------* 09420048
//* SORT DAILY SO FILE IN REGN NO SEQUENCE                            * 09430048
//* INCLUDE IDP SCHEME = 'HSP' CASES                                  * 09440048
//*-------------------------------------------------------------------* 09450048
//STEP41   EXEC SORT,COND=(0,NE)                                        09460048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=OLD                            09470048
//SORTOUT  DD DSN=&&BP13F411,DISP=(NEW,PASS),                           09480048
//            UNIT=3380,SPACE=(TRK,(10,20),RLSE),                       09490048
//            DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                       09500048
//SYSIN    DD *                                                         09510048
      SORT FIELDS=(1,8,A),FORMAT=CH                                     09520048
      INCLUDE COND=(40,3,CH,EQ,C'HSP')                                  09530048
//*-------------------------------------------------------------------* 09540048
//* TO PRINT RECORDS FROM BP13F410 FOR SERS CASES                     * 09550048
//*-------------------------------------------------------------------* 09560048
//STEP42   EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE),REGION=4096K       09570048
//BP13F410 DD DSN=&&BP13F411,DISP=(OLD,PASS)                            09580048
//BP17F000 DD DSN=&&BP17F000,DISP=(OLD,PASS)                            09590048
//BP17K120 DD DSN=BP17.K120.SERSUNIT,DISP=SHR                           09600048
//AB03F123 DD DSN=BP13.AB03.F123.SERSCTRA,DISP=MOD                      09610048
//BP13L656 DD SYSOUT=2,OUTPUT=(*.PRT01.FORMID),DEST=IOM2                09620048
//SYSTSPRT DD SYSOUT=*                                                  09630048
//SYSPRINT DD SYSOUT=*                                                  09640048
//SYSUDUMP DD SYSOUT=*                                                  09650048
//SYSOUT   DD SYSOUT=*                                                  09660048
//SYSDBOUT DD SYSOUT=*                                                  09670048
//SYSABOUT DD SYSOUT=*                                                  09680048
//SYSTSIN  DD *                                                         09690048
  DSN SYSTEM(DSN)                                                       09700048
  RUN PROGRAM(BP13C656) PLAN(BP13PCC) -                                 09710048
      LIB('BP13.BATCH.LOADCOB')                                         09720048
  END                                                                   09730048
//*-------------------------------------------------------------------* 09740048
//* SORT DAILY SO FILE IN REGN NO SEQUENCE                            * 09750048
//*-------------------------------------------------------------------* 09760048
//STEP43   EXEC  SORT,COND=(0,NE)                                       09770048
//SORTIN   DD  DSN=BP13.F410.DAILYSO,DISP=OLD                           09780048
//SORTOUT  DD  DSN=&&F410KEY,DISP=(NEW,PASS),                           09790048
//             UNIT=SYSDA,                                              09800048
//             SPACE=(CYL,(150,20),RLSE),                               09810048
//             DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                      09820048
//SYSIN    DD  *                                                        09830048
    SORT FIELDS=(1,8,A),FORMAT=CH                                       09840048
//*-------------------------------------------------------------------* 09850048
//* SORT SERS CONTROL FILE IN REGISTRATION NUMBER SEQUENCE            * 09860048
//*-------------------------------------------------------------------* 09870048
//STEP44   EXEC  SORT,COND=(0,NE)                                       09880048
//SORTIN   DD  DSN=BP17.K000.SCHREF,DISP=SHR                            09890048
//SORTOUT  DD  DSN=&&BP17SERS,DISP=(NEW,PASS),                          09900048
//             UNIT=SYSDA,                                              09910048
//             SPACE=(TRK,(150,20),RLSE),                               09920048
//             DCB=(RECFM=FB,LRECL=200,BLKSIZE=27800)                   09930048
//SYSIN    DD  *                                                        09940048
    SORT FIELDS=(18,8,A),FORMAT=CH                                      09950048
//*-------------------------------------------------------------------* 09960048
//* UPDATE SERS UNIT WITH DATE KEY ISSUE AND DATE SO EFFECTIVE        * 09970048
//* CLOSE FILE : BP17K120                                             * 09980048
//*-------------------------------------------------------------------* 09990048
//STEP45   EXEC  PGM=BP13C233,COND=(0,NE)                               10000048
//BP13F410 DD  DSN=&&F410KEY,DISP=(OLD,PASS)                            10010048
//BP17F000 DD  DSN=&&BP17SERS,DISP=(OLD,PASS)                           10020048
//BP13K820 DD  DSN=BP13.K820.OCCUP,DISP=SHR                             10030048
//P13F410O DD  DSN=BP13.F410.DAILYSO.SRS,DISP=MOD                       10040048
//BP13F415 DD  DSN=BP13.F415.SERS,DISP=MOD,                             10050048
//          UNIT=SYSDA,SPACE=(CYL,(30,40),RLSE),                        10060048
//          DCB=(RECFM=FB,LRECL=100,BLKSIZE=27900)                      10070048
//SYSPRINT DD  SYSOUT=*                                                 10080048
//SYSDBG   DD  SYSOUT=*                                                 10090048
//SYSDBOUT DD  SYSOUT=*                                                 10100048
//SYSOUT   DD  SYSOUT=*                                                 10110048
//*-------------------------------------------------------------------* 10120048
//* BACKUP BP13.F310.CANCEL                                           * 10130048
//* EMPTY BP13.F310.CANCEL                                            * 10140048
//*-------------------------------------------------------------------* 10150048
//STEP49   EXEC PGM=IDCAMS,COND=(0,NE)                                  10160048
//SYSPRINT DD SYSOUT=*                                                  10170048
//INPUT1   DD DSN=BP13.F310.CANCEL,DISP=SHR                             10180048
//OUTPUT1  DD DSN=BP13.F310.CANCEL.BKUP,DISP=SHR                        10190048
//INPUT2   DD DSN=NULLFILE,DISP=SHR                                     10200048
//OUTPUT2  DD DSN=BP13.F310.CANCEL,DISP=OLD                             10210048
//SYSIN    DD *                                                         10220048
     REPRO INFILE(INPUT1)  OUTFILE(OUTPUT1)                             10230048
     REPRO INFILE(INPUT2)  OUTFILE(OUTPUT2)                             10240048
//*                                                                     10250048
//*-------------------------------------------------------------------* 10260048
//* SORT BP13F410 BY REGN AND INCLUDE SALES TYPE OF '1', '2',         * 10270048
//* '2B', '03', AND '3A'.                                             * 10280048
//*-------------------------------------------------------------------* 10290048
//STEP52 EXEC SORT,CYL=50,COND=(0,NE)                                   10300048
//SORTIN  DD DSN=BP13.F410.DAILYSO,DISP=SHR                             10310048
//SORTOUT DD DSN=&&BP13F41A,DISP=(NEW,PASS),                            10320048
//            UNIT=SYSDA,SPACE=(CYL,(184,200),RLSE),                    10330048
//            DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                       10340048
//SYSIN   DD *                                                          10350048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      10360048
     INCLUDE COND=(37,2,CH,EQ,C'01',OR,37,2,CH,EQ,C'02',OR,             10370048
                   37,2,CH,EQ,C'2B',OR,37,2,CH,EQ,C'03',OR,             10380048
                   37,2,CH,EQ,C'3A')                                    10390048
//*                                                                     10400048
//*-------------------------------------------------------------------* 10410048
//* TO PASS NEW SALES CASES WITH SALES TYPE '1', '2', '2B', '3' , AND * 10420048
//* '3A' AND SALES AGREEMENT EXECUTION DATE IS ON OR BEFORE CURR DTE  * 10430048
//*-------------------------------------------------------------------* 10440048
//STEP53   EXEC PGM=BP13C414,COND=(0,NE)                                10450048
//BP13F410 DD   DSN=&&BP13F41A,DISP=(SHR,DELETE)                        10460048
//BR11F203 DD   DSN=BP13.BR11.F203.SOC,DISP=MOD                         10470048
//BP13K800 DD   DSN=BP13.K800.APPLN,DISP=SHR,                           10480048
//         AMP=('BUFNI=4','BUFND=8')                                    10490048
//BP13K820 DD   DSN=BP13.K820.OCCUP,DISP=SHR,                           10500048
//         AMP=('BUFNI=4','BUFND=8')                                    10510048
//SYSOUT   DD   SYSOUT=*                                                10520048
//SYSPRINT DD   SYSOUT=*                                                10530048
//SYSDBG   DD   SYSOUT=*                                                10540048
//SYSDBOUT DD   SYSOUT=*                                                10550048
/*                                                                      10560048
//*-------------------------------------------------------------------* 10570048
//* TO EXTRACT OUT BACK-TO-BACK CASES TO PIDB AND ASM                 * 10580048
//*-------------------------------------------------------------------* 10590048
//STEP54 EXEC    PGM=BP13C481,COND=(0,NE)                               10600048
//BP13F410 DD  DSN=BP13.F410.DAILYSO,DISP=SHR                           10610048
//BM06K110 DD  DSN=BM06.K110.FLATINV,                                   10620048
//             DISP=SHR                                                 10630048
//P13F410A DD  DSN=&&P13F410B,DISP=(,PASS),                             10640048
//         UNIT=SYSDA,SPACE=(TRK,(20,10),RLSE),                         10650048
//         DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                          10660048
//BP13F417 DD  DSN=BP13.F417.AM.ASSET,DISP=MOD                          10670048
//SYSOUT   DD  SYSOUT=*                                                 10680048
//SYSUDUMP DD  SYSOUT=*                                                 10690048
//SYSABEND DD  SYSOUT=*                                                 10700048
//SYSDBUG  DD  SYSOUT=*                                                 10710048
//SYSABOUT DD  SYSOUT=*                                                 10720048
//*-------------------------------------------------------------------* 10730048
//* MOD CASES INTO BP13.F410.DAILYSO.PIDB                             * 10740048
//*-------------------------------------------------------------------* 10750048
//STEP56   EXEC PGM=IDCAMS,COND=(0,NE)                                  10760048
//SYSPRINT DD SYSOUT=*                                                  10770048
//INPUT1   DD DSN=&&P13F410B,DISP=(SHR,PASS)                            10780048
//OUTPUT1  DD DSN=BP13.F410.DAILYSO.PIDB,DISP=MOD                       10790048
//SYSIN    DD *                                                         10800048
     REPRO INFILE(INPUT1)  OUTFILE(OUTPUT1)                             10810048
//*-------------------------------------------------------------------* 10820048
//* TO PASS RESALE PREMIUM TAGGING CASE TO RESALE SYSTEM              * 10830048
//*-------------------------------------------------------------------* 10840048
//STEP57   EXEC PGM=BP13C441,COND=(0,NE)                                10850048
//BP13F410 DD DSN=BP13.F410.DAILYSO,DISP=SHR                            10860048
//BP13K470 DD DSN=BP13.K470.PREMIUM,DISP=SHR                            10870048
//BB24K500 DD DSN=BB24.K500.RESLHIST,DISP=SHR,                          10880048
//         AMP=('BUFNI=4','BUFND=8')                                    10890048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             10900048
//         AMP=('BUFNI=4','BUFND=8')                                    10910048
//BP13F470 DD DSN=BP13.F470.PREMIUM,DISP=MOD                            10920048
//BP13L441 DD SYSOUT=8,DEST=IOM8                                                
//P13L441A DD SYSOUT=8,DEST=IOM8                                                
//P13L441B DD SYSOUT=8,DEST=IOM8                                                
//P13L441C DD SYSOUT=8,DEST=IOM8                                                
//P13L441D DD SYSOUT=8,DEST=IOM8                                                
//SYSPRINT DD SYSOUT=*                                                  10980048
//SYSDBG   DD SYSOUT=*                                                  10990048
//SYSDBOUT DD SYSOUT=*                                                  11000048
//SYSOUT   DD SYSOUT=*                                                  11010048
//*                                                                     11020048
//*-------------------------------------------------------------------* 11030048
//* TO PASS RESALE PREMIUM TAGGING CASE TO RESALE SYSTEM              * 11040048
//*-------------------------------------------------------------------* 11050048
//STEP58   EXEC SORT,COND=(0,NE)                                        11060048
//SORTIN   DD DSN=BB24.F004.CONTRA.BP13,DISP=SHR                        11070048
//SORTOUT  DD DSN=BP13.BB24.F004.CONTRA,DISP=SHR                        11080048
//SYSIN    DD  *                                                        11090048
    SORT FIELDS=(1,11,A),FORMAT=CH                                      11100048
/*                                                                      11110048
//*-------------------------------------------------------------------* 11120048
//* TO PASS RESALE TEMP LOAN CASES FROM    RESALE SYSTEM              * 11130048
//*-------------------------------------------------------------------* 11140048
//STEP58A  EXEC SORT,COND=(0,NE)                                        11150048
//SORTIN   DD DSN=BB24.F004.TEMPLOAN.BP13,DISP=SHR                      11160048
//SORTOUT  DD DSN=BP13.BB24.F004.TEMPLOAN,DISP=SHR                      11170048
//SYSIN    DD  *                                                        11180048
    SORT FIELDS=(1,11,A),FORMAT=CH                                      11190048
/*                                                                      11200048
//*-------------------------------------------------------------------* 11210048
//* MERGE BB24F004 AND KIVFILE                                        * 11220048
//*-------------------------------------------------------------------* 11230048
//STEP59   EXEC SORT,COND=(0,NE)                                        11240048
//SORTIN   DD DSN=BP13.BB24.F004.CONTRA,DISP=SHR                        11250048
//         DD DSN=BP13.BB24.F004.CONTRA.KIV,DISP=SHR                    11260048
//SORTOUT  DD DSN=&&BB24F004,DISP=(NEW,PASS),                           11270048
//            SPACE=(TRK,(10,2),RLSE),UNIT=SYSDA,                       11280048
//            DCB=(RECFM=FB,LRECL=50)                                   11290048
//SYSIN    DD  *                                                        11300048
    SORT FIELDS=(1,11,A),FORMAT=CH                                      11310048
/*                                                                      11320048
//*-------------------------------------------------------------------* 11330048
//* TO PASS A DAILY INTERFACING FILE FOR CONTRA CASES TO SMS          * 11340048
//*-------------------------------------------------------------------* 11350048
//STEP60   EXEC PGM=BP13C473,COND=(0,NE)                                11360048
//BP13K480 DD DSN=BP13.K480.CONTRA,DISP=SHR                             11370048
//BP13K410 DD DSN=BP13.K410.ACCUMSO,DISP=SHR                            11380048
//BM06K110 DD DSN=BM06.K110.FLATINV,DISP=SHR                            11390048
//BB24F004 DD DSN=&&BB24F004,DISP=(SHR,DELETE)                          11400048
//BP13CNTR DD DSN=BP13.BB24.F004.CONTRA.KIV,DISP=SHR                    11410048
//AB03F123 DD DSN=BP13.AB03.F123.CONTRA,DISP=MOD                        11420048
//BP13L473 DD SYSOUT=8,DEST=IOM8                                                
//SYSDBG   DD SYSOUT=*                                                  11440048
//SYSDBOUT DD SYSOUT=*                                                  11450048
//SYSOUT   DD SYSOUT=*                                                  11460048
/*                                                                      11470048
//*-------------------------------------------------------------------* 11480048
//* MERGE BB24F004 AND KIVFILE                                        * 11490048
//*-------------------------------------------------------------------* 11500048
//STEP59A  EXEC SORT,COND=(0,NE)                                        11510048
//SORTIN   DD DSN=BP13.BB24.F004.TEMPLOAN,DISP=SHR                      11520048
//         DD DSN=BP13.BB24.F004.TEMPLOAN.KIV,DISP=SHR                  11530048
//SORTOUT  DD DSN=&&BB24F04A,DISP=(NEW,PASS),                           11540048
//            SPACE=(TRK,(10,2),RLSE),UNIT=SYSDA,                       11550048
//            DCB=(RECFM=FB,LRECL=50)                                   11560048
//SYSIN    DD  *                                                        11570048
    SORT FIELDS=(1,11,A),FORMAT=CH                                      11580048
/*                                                                      11590048
//*-------------------------------------------------------------------* 11600048
//* TO PASS A DAILY INTERFACING FILE FOR TEMPLOAN CASES TO SMS        * 11610048
//*-------------------------------------------------------------------* 11620048
//STEP60A  EXEC PGM=BP13C476,COND=(0,NE)                                11630048
//BP13K480 DD DSN=BP13.K480.CONTRA,DISP=SHR                             11640048
//BP13K410 DD DSN=BP13.K410.ACCUMSO,DISP=SHR                            11650048
//BM06K110 DD DSN=BM06.K110.FLATINV,DISP=SHR                            11660048
//BB24F004 DD DSN=&&BB24F04A,DISP=(SHR,DELETE)                          11670048
//BP13CNTR DD DSN=BP13.BB24.F004.TEMPLOAN.KIV,DISP=SHR                  11680048
//AB03F123 DD DSN=BP13.AB03.F123.TEMPLOAN,DISP=MOD                      11690048
//BP13L476 DD SYSOUT=8,DEST=IOM8                                                
//SYSDBG   DD SYSOUT=*                                                  11710048
//SYSDBOUT DD SYSOUT=*                                                  11720048
//SYSOUT   DD SYSOUT=*                                                  11730048
/*                                                                      11740048
//*-------------------------------------------------------------------* 11750048
//* EMPTY BP13.F310.CANCEL                                            * 11760048
//*-------------------------------------------------------------------* 11770048
//STEP61   EXEC PGM=IDCAMS,COND=(0,NE)                                  11780048
//SYSPRINT DD SYSOUT=*                                                  11790048
//INPUT1   DD DSN=BB24.F004.CONTRA.BP13,DISP=SHR                        11800048
//OUTPUT1  DD DSN=BP13.BB24.F004.CONTRA.BKUP(+1),DISP=(NEW,CATLG),      11810048
//         SPACE=(CYL,(2,1)),                                           11820048
//         DCB=(BP13.GDG,BLKSIZE=27950,LRECL=50,RECFM=FB)               11830048
//INPUT1A  DD DSN=BB24.F004.TEMPLOAN.BP13,DISP=SHR                      11840048
//OUTPUT1A DD DSN=BP13.BB24.F004.TEMPLOAN.BKUP(+1),DISP=(NEW,CATLG),    11850048
//         SPACE=(CYL,(2,1)),                                           11860048
//         DCB=(BP13.GDG,BLKSIZE=27950,LRECL=50,RECFM=FB)               11870048
//INPUT1C  DD DSN=BB24.F004.CONTRA.BP13,DISP=SHR                        11800048
//OUTPUT1C DD DSN=BP13.BB24.F004.CONTRA.ACCUM,DISP=MOD                  11810048
//INPUT1D  DD DSN=BB24.F004.TEMPLOAN.BP13,DISP=SHR                      11840048
//OUTPUT1D DD DSN=BP13.BB24.F004.TEMPLOAN.ACCUM,DISP=MOD                11850048
//INPUT2   DD DSN=NULLFILE,DISP=SHR                                     11880048
//OUTPUT2  DD DSN=BB24.F004.CONTRA.BP13,DISP=OLD                        11890048
//INPUT3   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            11900048
//OUTPUT3  DD DSN=BP13.F410.DAILSO.WKBKUP,DISP=MOD                      11910048
//INPUT4   DD DSN=NULLFILE,DISP=SHR                                     11920048
//OUTPUT4  DD DSN=BB24.F004.TEMPLOAN.BP13,DISP=OLD                      11930048
//SYSIN    DD *                                                         11940048
     REPRO INFILE(INPUT1)  OUTFILE(OUTPUT1)                             11950048
     REPRO INFILE(INPUT1A) OUTFILE(OUTPUT1A)                            11960048
     REPRO INFILE(INPUT1C) OUTFILE(OUTPUT1C)                            11960048
     REPRO INFILE(INPUT1D) OUTFILE(OUTPUT1D)                            11960048
     REPRO INFILE(INPUT2)  OUTFILE(OUTPUT2)                             11970048
     REPRO INFILE(INPUT3)  OUTFILE(OUTPUT3)                             11980048
     REPRO INFILE(INPUT4)  OUTFILE(OUTPUT4)                             11990048
//*                                                                     12000048
//*---------------------------------------------------------------      12010048
//*   SORT SOC TRANS FILE FOR TRANS-TY =  'TL' AND 'TB'                 12020048
//*---------------------------------------------------------------      12030048
//STEP62   EXEC SORT,COND=(0,NE)                                        12040048
//SORTIN   DD DSN=BP13.F310.TRANS,DISP=SHR                              12050048
//SORTOUT  DD DSN=&&P13F310,DISP=(NEW,PASS),                            12060048
//            UNIT=3380,SPACE=(TRK,(10,20),RLSE),                       12070048
//            DCB=(RECFM=FB,LRECL=150,BLKSIZE=23400)                    12080048
//SYSIN    DD  *                                                        12090048
    SORT FIELDS=(1,8,A),FORMAT=CH                                       12100048
    INCLUDE COND=(9,2,CH,EQ,C'TL',OR,9,2,CH,EQ,C'TB',                   12110048
                  OR,9,2,CH,EQ,C'C3')                                   12120048
//*---------------------------------------------------------------      12130048
//*   UPDATE EXPIRED DATE FOR TP (EXISTING CASES)                       12140048
//*   CLOSE FILE : BP13K800                                             12150048
//*---------------------------------------------------------------      12160048
//STEP63  EXEC PGM=BP13C433,COND=(0,NE)                                 12170048
//BP13F310 DD DSN=&&P13F310,DISP=(OLD,PASS)                             12180048
//BM06K100 DD DSN=BM06.K100.BLOCK,DISP=SHR,                             12190048
//            AMP=('BUFNI=8')                                           12200048
//BM06K110 DD DSN=BM06.K110.FLATINV,DISP=SHR,                           12210048
//            AMP=('BUFNI=8')                                           12220048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             12230048
//            AMP=('BUFNI=8')                                           12240048
//BB14K500 DD DSN=BB14.K500.BLKLEASE,DISP=SHR,                          12250048
//            AMP=('BUFNI=8')                                           12260048
//BB14K501 DD DSN=BB14.K500.BLKLEASE.PATH1,DISP=SHR,                    12270048
//            AMP=('BUFNI=8')                                           12280048
//BP13OUT  DD DSN=BP13.F310.PRINTAP,DISP=OLD                            12290048
//BP13L433 DD SYSOUT=2,OUTPUT=(*.PRT01.FORMID),DEST=IOM2                12300048
//SYSOUT   DD SYSOUT=*                                                  12310048
//*-----------------------------------------------------------------    12320048
//*   MOVE THIS STEP FROM BP13D603                                      12330048
//*   MERGE TYPE 2A SALES ORDER WITH THE SOC ACCUMULATED SALES ORDER    12340048
//*   AND COPY TO DSN FOR SMS INTERFACE                                 12350048
//*-----------------------------------------------------------------    12360048
//STEP64   EXEC PGM=IDCAMS,COND=(0,NE)                                  12370048
//SYSPRINT DD SYSOUT=*                                                  12380048
//INPUT1   DD DSN=BP13.F430.BOSALE,DISP=SHR                             12390048
//         DD DSN=BP13.F430.SOTRANS,DISP=OLD                            12400048
//OUTPUT1  DD DSN=BP13.F430.SOC,DISP=MOD                                12410048
//SYSIN    DD *                                                         12420048
         REPRO INFILE(INPUT1) OUTFILE(OUTPUT1)                          12430048
//*-----------------------------------------------------------------    12440048
//*   MERGE SALES ORDER TYEP 2A WITH THE DAILY ACCUMULATED SALES        12450048
//*   ORDER AND COPY TO DSN                                             12460048
//*-----------------------------------------------------------------    12470048
//STEP65   EXEC PGM=IDCAMS,COND=(0,NE)                                  12480048
//SYSPRINT DD SYSOUT=*                                                  12490048
//INPUT1   DD DSN=BP13.F430.SOC,DISP=SHR                                12500048
//OUTPUT1  DD DSN=&&R002F480,DISP=(,PASS),                              12510048
//            UNIT=SYSDA,DCB=(BLKSIZE=27000,LRECL=1000,RECFM=FB),       12520048
//            SPACE=(TRK,(20,5),RLSE)                                   12530048
//SYSIN    DD *                                                         12540048
        REPRO INFILE(INPUT1) OUTFILE(OUTPUT1)                           12550048
//*-----------------------------------------------------------------    12560048
//*  TO REFORMAT RECORDS FOR R002                                       12570048
//*-----------------------------------------------------------------    12580048
//STEP66   EXEC  PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE),REGION=4096K      12590048
//BP13F430 DD DSN=&&R002F480,DISP=(SHR,PASS)                            12600048
//BP13K820 DD DSN=BP13.K820.OCCUP,DISP=SHR,                             12610048
//         AMP=('BUFNI=8','BUFND=4')                                    12620048
//BP13K060 DD DSN=BP13.K060.CODETAB,DISP=SHR                            12630048
//BB14K090 DD DSN=BB14.K090.CODE,DISP=SHR                               12640048
//BP13K830 DD DSN=BP13.K830.ADDR,DISP=SHR,                              12650048
//         AMP=('BUFNI=8','BUFND=4')                                    12660048
//BP13K831 DD DSN=BP13.K830.ADDR.PATH1,DISP=SHR,                        12670048
//         AMP=('BUFNI=8','BUFND=4')                                    12680048
//BP13F480 DD DSN=BP13.F480.NTL,DISP=MOD                                12690048
//SYSTSPRT DD SYSOUT=*                                                  12700048
//SYSPRINT DD SYSOUT=*                                                  12710048
//SYSUDUMP DD SYSOUT=*                                                  12720048
//SYSOUT   DD SYSOUT=*                                                  12730048
//SYSDBOUT DD SYSOUT=*                                                  12740048
//SYSTSIN  DD *                                                         12750048
  DSN SYSTEM(DSN)                                                       12760048
  RUN PROGRAM(BP13C440) PLAN(BP13PCC) -                                 12770048
      LIB('BP13.BATCH.LOADCOB')                                         12780048
  END                                                                   12790048
//*-----------------------------------------------------------------    12800048
//* EXCLUDE SA FLAT (SALES TYPE 13 AND 18) FROM PASSING TO SMS          12810048
//*-----------------------------------------------------------------    12820048
//STEP67   EXEC SORT,COND=(0,NE)                                        12830048
//SORTIN   DD DSN=BP13.F430.SOTRANS,DISP=SHR                            12840048
//SORTOUT  DD DSN=&&BP13F430,DISP=(NEW,PASS),                           12850048
//            UNIT=SYSDA,DCB=(BLKSIZE=27000,LRECL=1000,RECFM=FB),       12860048
//            SPACE=(TRK,(20,5),RLSE)                                   12870048
//SYSIN    DD *                                                         12880048
     SORT FIELDS=(1,11,A),FORMAT=CH                                     12890048
     OMIT COND=((536,2,CH,EQ,C'13',AND,447,7,CH,EQ,C'0000000'),OR,      12900048
               (536,2,CH,EQ,C'18',AND,447,7,CH,EQ,C'0000000'))          12910048
//*-------------------------------------------------------------------- 12920048
//*  CREATE TRANSFER CD TO MTL REC                                      12930048
//*-------------------------------------------------------------------- 12940048
//STEP68   EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE),REGION=4096K       12950048
//BP13F430 DD DSN=&&BP13F430,DISP=(OLD,PASS)                            12960048
//SY02F001 DD DSN=SY02.F001.DATE,DISP=SHR                               12970048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR,                             12980048
//            AMP=('BUFNI=8','BUFND=4')                                 12990048
//BP13K130 DD DSN=BP13.K130.SOCHIST,DISP=SHR                            13000048
//BP13F130 DD DSN=BP13.F130.MLTRF,DISP=SHR                              13010048
//BP13F440 DD DSN=BP13.F440.CD,DISP=MOD                                 13020048
//BP13F450 DD DSN=BP13.F450.MTLPRT,DISP=MOD                             13030048
//SYSOUT   DD SYSOUT=*                                                  13040048
//SYSTSPRT DD SYSOUT=*                                                  13050048
//SYSPRINT DD SYSOUT=*                                                  13060048
//SYSDBG   DD SYSOUT=*                                                  13070048
//SYSDBOUT DD SYSOUT=*                                                  13080048
//SYSTSIN  DD *                                                         13090048
  DSN SYSTEM(DSN)                                                       13100048
  RUN PROGRAM(BP13C420) PLAN(BP13PCC) -                                 13110048
      LIB('BP13.BATCH.LOADCOB')                                         13120048
//*-----------------------------------------------------------------    13130048
//* BACKUP FILE FOR SMS INTERFACE                                       13140048
//*-----------------------------------------------------------------    13150048
//STEP69   EXEC PGM=IDCAMS,COND=(0,NE)                                  13160048
//SYSPRINT DD SYSOUT=*                                                  13170048
//INPUT2   DD DSN=BP13.F430.SOC,DISP=SHR                                13180048
//OUTPUT2  DD DSN=BP13.F430.SMS(+1),DISP=(,CATLG,DELETE),               13190048
//             DCB=(BP13.GDG,BLKSIZE=27000,LRECL=1000,RECFM=FB),        13200048
//             SPACE=(TRK,(12,2),RLSE)                                  13210048
//OUTPUT3  DD DSN=BP13.F430.SOC.ACCUM,DISP=MOD                          13211051
//SYSIN    DD *                                                         13220048
        REPRO INFILE(INPUT2) OUTFILE(OUTPUT2)                           13230048
        REPRO INFILE(INPUT2) OUTFILE(OUTPUT3)                           13231051
//*-------------------------------------------------------------------- 13240048
//* EMPTY DIALY SMS INTERFACE TRANS FILE                                13250048
//*-------------------------------------------------------------------- 13260048
//STEP70   EXEC PGM=IDCAMS,COND=(0,NE)                                  13270048
//SYSPRINT DD SYSOUT=*                                                  13280052
//INPUT4   DD DSN=BP13.EMPTY.CHAR700,DISP=OLD                           13290048
//OUTPUT4  DD DSN=BP13.F430.SOTRANS,DISP=OLD                            13300048
//INPUT5   DD DSN=BP13.EMPTY.CHAR700,DISP=OLD                           13310048
//OUTPUT5  DD DSN=BP13.F430.BOSALE,DISP=OLD                             13320048
//SYSIN    DD *                                                         13330053
     REPRO INFILE(INPUT4)  OUTFILE(OUTPUT4)                             13340048
     REPRO INFILE(INPUT5)  OUTFILE(OUTPUT5)                             13350048
//*                                                                     13360048
//*-------------------------------------------------------------------- 13370048
//* TO PASS INTERFACE FILE TO DJ02                                      13380048
//*-------------------------------------------------------------------- 13390048
//STEP71   EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE)                    13400048
//BP13F410 DD DSN=BP13.F410.DAILYSO,DISP=SHR                            13410048
//BM06K110 DD DSN=BM06.K110.FLATINV,DISP=SHR                            13420048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR                              13430048
//DJ02F850 DD DSN=BP13.DJ02.F850.INTRFC,DISP=MOD                        13440048
//SYSOUT   DD SYSOUT=*                                                  13450048
//SYSTSPRT DD SYSOUT=*                                                  13460048
//SYSPRINT DD SYSOUT=*                                                  13470048
//SYSTSIN  DD *                                                         13480048
  DSN SYSTEM(DSN)                                                       13490048
  RUN PROGRAM(BP13C41A) PLAN(BP13PCC) -                                 13500048
      LIB('BP13.BATCH.LOADCOB')                                         13510048
  END                                                                   13520048
//*-----------------------------------------------------------------    13530048
//* SORT OUT ACTIVE NPL CASES                                           13540048
//*-----------------------------------------------------------------    13550048
//STEP72   EXEC SORT,COND=(0,NE)                                        13560048
//SORTIN   DD DSN=BP13.K800.APPLN,DISP=SHR                              13570048
//SORTOUT  DD DSN=&&P13F800,DISP=(NEW,PASS),                            13580048
//         UNIT=SYSDA,                                                  13590048
//         DCB=(RECFM=FB,LRECL=2000),                                   13600048
//         SPACE=(CYL,(5,2),RLSE)                                       13610048
//SYSIN    DD *                                                         13620048
    SORT FIELDS=(4,8,A),FORMAT=CH                                       13630048
    INCLUDE COND=(924,3,CH,EQ,C'NPL',AND,                               13640048
                  1,1,CH,NE,C'C')                                       13650048
//*-----------------------------------------------------------------    13660048
//* CREATE INTERFACE FILE - ALL ACTIVE NPL CASES                        13670048
//*-----------------------------------------------------------------    13680048
//STEP73   EXEC PGM=BP13C238,COND=(0,NE)                                13690048
//BP13F800 DD DSN=&&P13F800,DISP=(OLD,PASS)                             13700048
//BM06K110 DD DSN=BM06.K110.FLATINV,DISP=SHR                            13710048
//BC03F818 DD DSN=BP13.BC03.F818.ACTIVE,DISP=MOD                        13720048
//SYSPRINT DD SYSOUT=*                                                  13730048
//SYSOUT   DD SYSOUT=*                                                  13740048
//*------------------------------------------------------------------*  13750048
//* SORT OUT NPL CASES WITH STATUS CHANGE                               13760048
//*-----------------------------------------------------------------    13770048
//STEP74   EXEC SORT,COND=(0,NE)                                        13780048
//SORTIN   DD DSN=BP13.K800.APPLN,DISP=SHR                              13790048
//SORTOUT  DD DSN=&&P13F800A,DISP=(NEW,PASS),                           13800048
//         UNIT=SYSDA,                                                  13810048
//         DCB=(RECFM=FB,LRECL=2000),                                   13820048
//         SPACE=(CYL,(5,2),RLSE)                                       13830048
//SYSIN    DD *                                                         13840048
    SORT FIELDS=(4,8,A),FORMAT=CH                                       13850048
    INCLUDE COND=(924,3,CH,EQ,C'NPL')                                   13860048
//*--------------------------------------------------------------------*13870048
//* CREATE INTERFACE FILE - CASES WITH CHANGE OF STATUS                 13880048
//*--------------------------------------------------------------------*13890048
//STEP75   EXEC PGM=BP13C239,COND=(0,NE)                                13900048
//BP13F800 DD DSN=&&P13F800A,DISP=(OLD,PASS)                            13910048
//BM06K110 DD DSN=BM06.K110.FLATINV,DISP=SHR                            13920048
//BC03F818 DD DSN=BP13.BC03.F818.STATUS,DISP=MOD                        13930048
//SYSPRINT DD SYSOUT=*                                                  13940048
//SYSOUT   DD SYSOUT=*                                                  13950048
//*--------------------------------------------------------------------*13960048
//*                SORT BP13K421 BY K421-KEY-FLD                        13970048
//*--------------------------------------------------------------------*13980048
//STEP76 EXEC    SORT,COND=(0,NE)                                       13990048
//SORTIN   DD  DSN=BP13.K421.CC.INFO,                                   14000048
//             DISP=SHR                                                 14010048
//SORTOUT  DD  DSN=&&P13F421A,                                          14020048
//             DISP=(NEW,PASS),                                         14030048
//             UNIT=SYSDA,                                              14040048
//             SPACE=(CYL,(10,5),RLSE),                                 14050048
//             DCB=(BLKSIZE=27900,LRECL=450,RECFM=FB)                   14060048
//SYSIN    DD  *                                                        14070048
    SORT FIELDS=(1,24,A),FORMAT=CH                                      14080048
/*                                                                      14090048
//*--------------------------------------------------------------------*14100048
//*            TO DELETE BP13K421 CLOSING CEREMONY FILE                 14110048
//*                                                                     14120048
//*--------------------------------------------------------------------*14130048
//STEP77 EXEC    PGM=BP13C370,COND=(0,NE)                               14140048
//BP13F421 DD  DSN=&&P13F421A,                                          14150048
//             DISP=(SHR,PASS)                                          14160048
//BP13K421 DD  DSN=BP13.K421.CC.INFO,                                   14170048
//             DISP=SHR                                                 14180048
//SYSDBG   DD  SYSOUT=*                                                 14190048
//SYSDBOUT DD  SYSOUT=*                                                 14200048
//SYSOUT   DD  SYSOUT=*                                                 14210048
//*---------------------------------------------------------------------14220048
//* MOD BP13.F417.AM.ASSET TO ACCUM FILE                                14230048
//*---------------------------------------------------------------------14240048
//STEP78   EXEC PGM=IDCAMS,COND=(0,NE)                                  14250048
//SYSPRINT DD  SYSOUT=*                                                 14260048
//INPUT1   DD  DSN=BP13.F417.AM.ASSET,DISP=SHR                          14270048
//OUTPUT1  DD  DSN=BP13.F417.AM.ASSET.ACCUM,                            14280048
//             DISP=MOD                                                 14290048
//SYSIN    DD  *                                                        14300048
         REPRO INFILE(INPUT1) OUTFILE(OUTPUT1)                          14310048
/*                                                                      14320048
//*---------------------------------------------------------------------14330048
//* SEND FILE TO FIMS AXS MACHINE                                       14340048
//*---------------------------------------------------------------------14350048
//* BP13C468 - TO WRITE TO AB20F302 FOR FWD                             14360048
//*-------------------------------------------------------------------- 14370048
//STEP79A  EXEC SORT,COND=(0,NE)                                        14380048
//SORTIN   DD DSN=BP13.K800.APPLN,DISP=SHR                              14390048
//SORTOUT  DD DSN=&&STEP79,DISP=(NEW,PASS),                             14400048
//            SPACE=(CYL,(50,10),RLSE),UNIT=SYSDA,                      14401048
//            DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                       14402048
//SYSIN    DD  *                                                        14403048
  SORT FIELDS=(4,8,A),FORMAT=CH                                         14404048
  INCLUDE COND=(155,11,CH,NE,C'           ')                            14405048
//*-------------------------------------------------------------------- 14406048
//* BP13C468 - TO WRITE TO AB20F302 FOR FWD                             14407048
//* SEND FILE TO FIMS AXS MACHINE                                       14408048
//*-------------------------------------------------------------------- 14409048
//STEP79   EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE),REGION=4096K       14410048
//BP13F800 DD DSN=&&STEP79,DISP=(OLD,PASS)                              14410148
//BM06K110 DD DSN=BM06.K110.FLATINV,DISP=SHR                            14410248
//AB20F302 DD DSN=BP13.AB20.F302.FIREINSU.AXS,DISP=SHR                  14410348
//SYSPRINT DD SYSOUT=*                                                  14410448
//SYSTSPRT DD SYSOUT=*                                                  14410548
//SYSOUT   DD SYSOUT=*                                                  14410648
//SYSABOUT DD SYSOUT=*                                                  14410748
//SYSTSIN  DD *                                                         14410848
  DSN SYSTEM(DSN)                                                       14410948
  RUN PROGRAM(BP13C468) PLAN(BP13PCC) -                                 14411048
      LIB('BP13.BATCH.LOADCOB')                                         14411148
  END                                                                   14411248
//*                                                                     14411348
//*-----------------------------------------------------------------    14411448
//* BACKUP DAILY FILE                                                   14411548
//*-----------------------------------------------------------------    14411648
//STEP80   EXEC PGM=IDCAMS,COND=(0,NE)                                  14411748
//SYSPRINT DD SYSOUT=*                                                  14411848
//INPUT2   DD DSN=BP13.F415.SERS,DISP=SHR                               14411948
//OUTPUT2  DD DSN=BP13.F415.SERS.DAILY(+1),DISP=(NEW,CATLG),            14412048
//             DCB=(BP13.GDG,BLKSIZE=27900,LRECL=100,RECFM=FB),         14413048
//             SPACE=(TRK,(12,2),RLSE)                                  14414048
//SYSIN    DD *                                                         14415048
        REPRO INFILE(INPUT2) OUTFILE(OUTPUT2)                           14416048
//*-----------------------------------------------------------------    14417048
//* BACKUP MONTHLY FILE                                                 14418048
//*-----------------------------------------------------------------    14419048
//STEP81   EXEC PGM=IDCAMS,COND=(0,NE)                                  14420048
//SYSPRINT DD SYSOUT=*                                                  14430048
//INPUT2   DD DSN=BP13.F415.SERS,DISP=SHR                               14440048
//OUTPUT2  DD DSN=BP13.F415.SERS.MONTH,DISP=MOD                         14450048
//SYSIN    DD *                                                         14460048
        REPRO INFILE(INPUT2) OUTFILE(OUTPUT2)                           14470048
//*-----------------------------------------------------------------    14471049
//* BACKUP BP13.F385.LEAS.DA                                            14472049
//*-----------------------------------------------------------------    14473049
//STEP81A  EXEC PGM=IDCAMS,COND=(0,NE)                                  14474049
//SYSPRINT DD SYSOUT=*                                                  14475049
//INPUT2   DD DSN=BP13.F385.LEAS.DA,DISP=SHR                            14476049
//OUTPUT2  DD DSN=BP13.F385.LEAS.DA.ACCUM,DISP=MOD                      14477049
//SYSIN    DD *                                                         14478049
        REPRO INFILE(INPUT2) OUTFILE(OUTPUT2)                           14479049
//*-------------------------------------------------------------------- 14479150
//* TO ENSURE NO DUPLICATES IN BP13.F385.LEAS.DA                        14479250
//*-------------------------------------------------------------------- 14479350
//STEP81B EXEC SORT,COND=(0,NE)                                         14479450
//SORTIN   DD DSN=BP13.F385.LEAS.DA,DISP=SHR                            14479550
//SORTOUT  DD DSN=BP13.F385.LEAS.DA,DISP=SHR                            14479650
//SYSIN    DD *                                                         14480050
    SORT FIELDS=(1,8,A),FORMAT=CH,FILSZ=E20000                          14480150
    SUM FIELDS=NONE                                                     14480250
//*                                                                     14480450
//*-------------------------------------------------------------------- 14481048
//* PASS INTERFACE FILE TO BQG ON CASES WITH FUTURE TP-APPOINTMENT DATE 14490048
//* COL 475 DTE-SO-APPMT > 20150101                                     14500048
//*-------------------------------------------------------------------- 14510048
//STEP82  EXEC SORT,COND=(0,NE)                                         14520048
//SORTIN   DD DSN=BP13.K800.APPLN,DISP=SHR,                             14530048
//            AMP=('BUFND=4','BUFNI=8')                                 14540048
//SORTOUT  DD DSN=&&F800TPMT,DISP=(NEW,PASS),                           14550048
//         UNIT=SYSDA,SPACE=(CYL,(20,10),RLSE),                         14560048
//         DCB=(RECFM=FB,LRECL=2000,BLKSIZE=18000)                      14570048
//SYSIN    DD *                                                         14580048
    SORT FIELDS=(4,8,A),FORMAT=CH,FILSZ=E20000                          14590048
    INCLUDE COND=(1,1,CH,NE,C'C',AND,                                   14600048
                475,8,CH,GT,C'20150115')                                14610048
//*                                                                     14620048
//*---------------------------------------------------------------------14630048
//* PASS INTERFACE FILE TO BQG DU04 SYSTEM                              14640048
//*---------------------------------------------------------------------14650048
//STEP83   EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE),REGION=4096K       14660048
//BP13F800 DD DSN=&&F800TPMT,DISP=(OLD,DELETE)                          14670048
//BP13K435 DD DSN=BP13.K435.KEY.BORROW,DISP=SHR,                        14680048
//            AMP=('BUFND=4','BUFNI=8')                                 14690048
//BP13K436 DD DSN=BP13.K435.KEY.BORROW.PATH1,DISP=SHR,                  14700048
//            AMP=('BUFND=4','BUFNI=8')                                 14710048
//BP13K431 DD DSN=BP13.K431.BORROWER.CODE,DISP=SHR                      14720048
//BM06K510 DD DSN=BM06.K510.ESTCDE,DISP=SHR                             14730048
//SY02F001 DD DSN=SY02.F001.DATE,DISP=SHR                               14740048
//DU04FQ10 DD DSN=BP13.DU04.FQ10.UNITS,DISP=OLD                         14750048
//SYSTSPRT DD SYSOUT=*                                                  14760048
//SYSPRINT DD SYSOUT=*                                                  14770048
//SYSUDUMP DD SYSOUT=*                                                  14780048
//SYSOUT   DD SYSOUT=*                                                  14790048
//SYSDBOUT DD SYSOUT=*                                                  14800048
//SYSABOUT DD SYSOUT=*                                                  14810048
//SYSTSIN  DD *                                                         14820048
  DSN SYSTEM(DSN)                                                       14830048
  RUN PROGRAM(BP13CD5D) PLAN(BP13PCC) -                                 14840048
      LIB('BP13.BATCH.LOADCOB')                                         14850048
  END                                                                   14860048
//*-------------------------------------------------------------------* 14870048
//* SORT BM06.F192.LOG.PIDB WITH  TRANS-TYPE = '020102'                 14880048
//*-------------------------------------------------------------------* 14890048
//STEP84   EXEC SORT,COND=(0,NE)                                        14900048
//SORTIN   DD DSN=BM06.F192.LOG.PIDB,DISP=SHR                           14910048
//SORTOUT  DD DSN=&&BM06F190,DISP=(NEW,PASS),                           14920048
//            UNIT=SYSDA,                                               14930048
//            DCB=(BLKSIZE=0,LRECL=650,RECFM=FB),                       14940048
//            SPACE=(CYL,(20,10),RLSE)                                  14950048
//SYSOUT   DD SYSOUT=*                                                  14960048
//SYSIN    DD  *                                                        14970048
   SORT FIELDS=(1,11,A),FORMAT=CH                                       14980048
   INCLUDE COND=(637,6,CH,EQ,C'020102')                                 14990048
//*-------------------------------------------------------------------* 15000048
//* CREATE BP13.AB03.F021.REJTP TO BE PASSED TO SMS FOR PROCESSING      15010048
//*                                                                     15020048
//*-------------------------------------------------------------------* 15030048
//STEP85   EXEC PGM=BP13C37A,COND=(0,NE)                                15040048
//BM06F190 DD DSN=&&BM06F190,DISP=(OLD,DELETE)                          15050048
//AB03F021 DD DSN=BP13.AB03.F021.REJTP,DISP=MOD                         15060048
//SYSDBG   DD SYSOUT=*                                                  15070048
//SYSDBOUT DD SYSOUT=*                                                  15080048
//SYSOUT   DD SYSOUT=*                                                  15090048
/*                                                                      15100048
//*-------------------------------------------------------------------* 15110048
//* SORT DAILYSO FILE IN ASCENDING REGN NUMBER                        * 15120048
//*-------------------------------------------------------------------* 15130048
//STEP86   EXEC SORT,PARM='DYNALLOC=(,255)',COND=(0,NE)                 15140048
//SORTIN   DD DSN=BP13.F410.DAILYSO,DISP=SHR                            15150048
//SORTOUT  DD DSN=&&DAILYSO,DISP=(NEW,PASS),                            15160048
//            SPACE=(TRK,(50,10),RLSE),UNIT=SYSDA,                      15170048
//            DCB=(RECFM=FB,LRECL=2000,BLKSIZE=18000)                   15180048
//SYSIN    DD *                                                         15190048
  SORT FIELDS=(1,08,A),FORMAT=CH                                        15200048
/*                                                                      15210048
//*-------------------------------------------------------------------* 15220048
//* EXTRACT WITH KEY ISSUE DATE = '      '                            * 15230048
//*-------------------------------------------------------------------* 15240048
//STEP87   EXEC SORT,PARM='DYNALLOC=(,255)',COND=(0,NE)                 15250048
//SORTIN   DD DSN=BP13.K73E.PPO.MONITOR,DISP=SHR                        15260048
//SORTOUT  DD DSN=&&PPO,DISP=(NEW,PASS),                                15270048
//            SPACE=(TRK,(50,10),RLSE),UNIT=SYSDA,                      15280048
//            DCB=(RECFM=FB,LRECL=500,BLKSIZE=27500)                    15290048
//SYSIN    DD *                                                         15300048
  SORT FIELDS=(1,08,A),FORMAT=CH                                        15310048
  INCLUDE COND=(272,8,CH,EQ,C'        ',OR,272,8,CH,EQ,X'00000000',OR,  15320048
                272,8,CH,EQ,C'00000000')                                15330048
/*                                                                      15340048
//*-------------------------------------------------------------------* 15350048
//* UPDATE BP13K73E KEY ISSUE DATE AND DATE BR10 PASSED               * 15360048
//*-------------------------------------------------------------------* 15370048
//STEP88 EXEC  PGM=BP13C37B,COND=(0,NE)                                 15380048
//BP13F73E DD  DSN=&&PPO,DISP=(OLD,DELETE)                              15390048
//BP13F410 DD  DSN=&&DAILYSO,DISP=(OLD,DELETE)                          15400048
//BP13K73E DD  DSN=BP13.K73E.PPO.MONITOR,DISP=SHR                       15410048
//BP13L73E DD  DSN=BP13.F73E.KEYISS.OUT,DISP=MOD                        15420048
//SYSTSPRT DD SYSOUT=*                                                  15430048
//SYSPRINT DD SYSOUT=*                                                  15440048
//SYSUDUMP DD SYSOUT=*                                                  15450048
//SYSOUT   DD SYSOUT=*                                                  15460048
//SYSDBOUT DD SYSOUT=*                                                  15470048
/*                                                                      15480048
//*-------------------------------------------------------------------* 15490048
//* EXTRACT MONITOR = 'Y' AND KEY-ISS-DTE > 0 AND                     * 15500048
//* DATE BR10 PASSED= CURRENT DATE                                    * 15510048
//*-------------------------------------------------------------------* 15520048
//STEP89   EXEC SORT,PARM='DYNALLOC=(,255)',COND=(0,NE)                 15530048
//SORTIN   DD DSN=BP13.K73E.PPO.MONITOR,DISP=SHR                        15540048
//SORTOUT  DD DSN=BP13.F73G.PPO.BR10,DISP=MOD                           15550048
//SYSIN    DD *                                                         15560048
  INCLUDE COND=(348,01,CH,EQ,C'Y',AND,272,8,CH,GT,C'00000000',AND,      15570048
                367,08,ZD,EQ,DATE1P)                                    15580048
  OUTREC FIELDS=(01,210,C'N',212,33,257,244)                            15590048
  SORT   FIELDS=COPY                                                    15600048
/*                                                                      15610048
//*-------------------------------------------------------------------* 15620048
//* EXTRACT FROM BP13.K62A.STAMPREF:                                  * 15630048
//* K62A-DTE-IRAS-PROCESS-REFUND= 6 MONTHS OR MORE HAVE PASSED        * 15640048
//* SORT IN REGN NUMBER                                               * 15650048
//*-------------------------------------------------------------------* 15660048
//STEP90   EXEC SORT,PARM='DYNALLOC=(,255)',COND=(0,NE)                 15670048
//SYSOUT   DD SYSOUT=*                                                  15680048
//SYSPRINT DD SYSOUT=*                                                  15690048
//SORTIN   DD DSN=BP13.K62A.STAMPREF,DISP=SHR                           15700048
//SORTOUT  DD DSN=&&BP13F62A,DISP=(,PASS),                              15710048
//            UNIT=SYSDA,SPACE=(CYL,(10,20),RLSE),                      15720048
//            DCB=(RECFM=FB,LRECL=2000,BLKSIZE=18000)                   15730048
//SYSIN    DD *                                                         15740048
  SORT FIELDS=(30,08,A),FORMAT=CH                                       15750048
  INCLUDE COND=(690,08,CH,NE,C'        ',AND,                           15760048
                690,06,ZD,LE,DATE2P-15,AND,                             15770048
                12,05,CH,EQ,C'ALEO1')                                   15780048
  OUTREC FIELDS=(30,8)                                                  15790048
/*                                                                      15800048
//*-------------------------------------------------------------------* 15810048
//* EXTRACT REINSTATED CASES FROM BP13.K840.APPLHIST:                 * 15820048
//* K840-DTE-REINSTATE(1:4) GE '2015'                                 * 15830048
//* SORT IN REGN NUMBER                                               * 15840048
//*-------------------------------------------------------------------* 15850048
//STEP90A  EXEC SORT,PARM='DYNALLOC=(,255)',COND=(0,NE)                 15860048
//SYSOUT   DD SYSOUT=*                                                  15870048
//SYSPRINT DD SYSOUT=*                                                  15880048
//SORTIN   DD DSN=BP13.K840.APPLHIST,DISP=SHR                           15890048
//SORTOUT  DD DSN=&&BP13F840,DISP=(,PASS),                              15900048
//            UNIT=SYSDA,SPACE=(CYL,(50,10),RLSE),                      15910048
//            DCB=(RECFM=FB,LRECL=500,BLKSIZE=27500)                    15920048
//SYSIN    DD *                                                         15930048
  SORT FIELDS=(01,08,A),FORMAT=CH                                       15940048
  INCLUDE COND=(213,04,CH,GE,C'2015')                                   15950048
/*                                                                      15960048
//*-------------------------------------------------------------------* 15970048
//* CREATE REINSTATED CASES FOR HOUSEKEEPING AND EMAIL                * 15980048
//*-------------------------------------------------------------------* 15990048
//STEP90B  EXEC PGM=BP13C984,COND=(0,NE)                                16000048
//BP13F840 DD DSN=&&BP13F840,DISP=(OLD,DELETE)                          16010048
//BP13K62A DD DSN=BP13.K62A.STAMPREF,DISP=SHR                           16020048
//BP13K800 DD DSN=BP13.K800.APPLN,DISP=SHR                              16030048
//BP13L984 DD DSN=BP13.L984.REINSTED,DISP=OLD                           16040048
//BP13F984 DD DSN=BP13.F984.REINSTED,DISP=OLD                           16050048
//SYSTSPRT DD SYSOUT=*                                                  16060048
//SYSPRINT DD SYSOUT=*                                                  16070048
//SYSUDUMP DD SYSOUT=*                                                  16080048
//SYSOUT   DD SYSOUT=*                                                  16090048
//SYSDBOUT DD SYSOUT=*                                                  16100048
/*                                                                      16110048
//*-------------------------------------------------------------------* 16120048
//* MERGE RECORDS AS INPUT FOR STEP 92:                               * 16130048
//* 6 MONTHS HAVE PASSED SINCE IRAS ACKNOWLEDGED STAMP REFUND AND     * 16140048
//* CASES ALREADY KEY HAS BEEN ISSUED BEFORE CANCELLATION             * 16150048
//*-------------------------------------------------------------------* 16160048
//STEP91   EXEC SORT,PARM='DYNALLOC=(,255)',COND=(0,NE)                 16170048
//SYSOUT   DD SYSOUT=*                                                  16180048
//SYSPRINT DD SYSOUT=*                                                  16190048
//SORTIN   DD DSN=&&BP13F62A,DISP=(OLD,DELETE)                          16200048
//         DD DSN=BP13.F410.DAILYSO,DISP=SHR                            16210048
//         DD DSN=BP13.F984.REINSTED,DISP=SHR                           16220048
//SORTOUT  DD DSN=&&MERGE,DISP=(,PASS),                                 16230048
//            UNIT=SYSDA,SPACE=(CYL,(10,20),RLSE),                      16240048
//            DCB=(RECFM=FB,LRECL=2000,BLKSIZE=18000)                   16250048
//SYSIN    DD *                                                         16260048
  SORT FIELDS=(1,08,A),FORMAT=CH                                        16270048
/*                                                                      16280048
//*-------------------------------------------------------------------* 16290048
//* MAINTENANCE PROGRAM FOR BP13K62A AND BP13K62E                     * 16300048
//* MOVE TO HISTORY FILES ALL RECORDS THAT HAS BEEN ISSUED W/ KEY AND * 16310048
//* ALREADY PROCESSED BY IRAS 15 MONTHS AGO OR MORE AND REINSTATED CASE 16320048
//*-------------------------------------------------------------------* 16330048
//STEP92   EXEC PGM=BP13C997,COND=(0,NE)                                16340048
//BP13FINP DD DSN=&&MERGE,DISP=(OLD,DELETE)                             16350048
//BP13K62A DD DSN=BP13.K62A.STAMPREF,DISP=SHR                           16360048
//BP13K621 DD DSN=BP13.K62A.STAMPREF.PATH2,DISP=SHR                     16370048
//BP13K62E DD DSN=BP13.K62E.STAMPREF.PARTY,DISP=SHR                     16380048
//BP13K62D DD DSN=BP13.K62D.STAMPREF.HIS,DISP=SHR                       16390048
//BP13K62F DD DSN=BP13.K62F.STAMP.PARTY.HIS,DISP=SHR                    16400048
//SYSTSPRT DD SYSOUT=*                                                  16410048
//SYSPRINT DD SYSOUT=*                                                  16420048
//SYSUDUMP DD SYSOUT=*                                                  16430048
//SYSOUT   DD SYSOUT=*                                                  16440048
//SYSDBOUT DD SYSOUT=*                                                  16450048
/*                                                                      16460048
//*-------------------------------------------------------------------* 16470048
//*  SEND REINSTATED CASES                                            * 16480048
//*-------------------------------------------------------------------* 16490048
//STEP93   EXEC SENDMAIL,COND=(4,LT)                                    16500048
//INPUT    DD DSN=BP13.L984.REINSTED,DISP=SHR                           16510048
/*                                                                      16520048
//*---------------------------------------------------------------------16530048
//* SORT BP13F410                                                       16540048
//*---------------------------------------------------------------------16550048
//STEP93A  EXEC SORT,COND=(4,LT)                                        16560048
//SORTIN   DD DSN=BP13.F410.MTHSO,DISP=SHR                              16570048
//SORTOUT  DD DSN=&&BP13F412,DISP=(NEW,PASS),                           16580048
//         UNIT=SYSDA,SPACE=(CYL,(100,50),RLSE),                        16590048
//         DCB=(RECFM=FB,LRECL=2000)                                    16600048
//SYSIN    DD *                                                         16610048
   SORT FIELDS=(1,8,A),FORMAT=CH                                        16620048
       INCLUDE COND=(26,2,CH,EQ,C'TM',OR,                               16630048
                     26,2,CH,EQ,C'DT',OR,                               16640048
                     26,2,CH,EQ,C'TU')                                  16650048
//*-------------------------------------------------------------------- 16660048
//* BP13C37H - PRINT TEMP LOAN CASES                                    16670048
//*-------------------------------------------------------------------- 16680048
//STEP93B EXEC  PGM=BP13C37H,COND=(4,LT)                                16690048
//BP13F410 DD  DSN=&&BP13F412,DISP=(OLD,PASS)                           16700048
//BP13K480 DD  DSN=BP13.K480.CONTRA,DISP=SHR                            16710048
//BP13L37H DD  DSN=BP13.L37H.BP13L37H,DISP=SHR                          16720048
//MAILC37H DD  DSN=BP13.MAILRPT.BP13L37H,DISP=SHR                       16730048
//SYSDBG   DD SYSOUT=*                                                  16740048
//SYSDBOUT DD SYSOUT=*                                                  16750048
//SYSOUT   DD SYSOUT=*                                                  16760048
//*-------------------------------------------------------------------- 16770048
//* BP13C37H - SEND MAIL - TEMP LOAN CASES                              16780048
//*-------------------------------------------------------------------- 16790048
//STEP93C  EXEC SENDMAIL,COND=(4,LT)                                    16800048
//INPUT    DD DSN=BP13.MAILRPT.BP13L37H,DISP=SHR                        16810048
//*---------------------------------------------------------------------16820048
//* SORT OUT CASES FOR CPF USAGE                                        16830048
//*---------------------------------------------------------------------16840048
//STEP94D  EXEC  SORT                                                   16850048
//SORTIN   DD  DSN=BP13.K40C.SALEORD.RSV,DISP=SHR                       16860048
//SORTOUT  DD DSN=&&BP13F40C,DISP=(NEW,PASS),                           16870048
//             SPACE=(CYL,(50,100),RLSE),                               16880048
//             DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                      16890048
//SYSIN    DD  *                                                        16900048
     SORT FIELDS=(1,8,A),FORMAT=CH                                      16910048
  INCLUDE COND=(1689,8,CH,EQ,DATE1)                                     16920048
//*---------------------------------------------------------------------16930048
//* DELETE CPF USAGE WHEN RAISING OF SALES ORDER IS NOT COMPLETED       16940048
//*-------------------------------------------------------------------- 16950048
//STEP94E EXEC   PGM=BP13C30A,COND=(0,NE)                               16960048
//BP13F40C DD  DSN=&&BP13F40C,DISP=(OLD,DELETE)                         16970048
//BP13K40C DD  DSN=BP13.K40C.SALEORD.RSV,DISP=SHR                       16980048
//BP13K400 DD  DSN=BP13.K410.ACCUMSO,DISP=SHR                           16990048
//SYSOUT   DD  SYSOUT=*                                                 17000048
//SYSPRINT DD  SYSOUT=*                                                 17010048
//SYSDBG   DD  SYSOUT=*                                                 17020048
//SYSDBOUT DD  SYSOUT=*                                                 17030048
//*-----------------------------------------------------------------    17040048
//* STEP 95: SEND THE SMTP MAIL                                         17050048
//*------------------------------------------------------------------*  17060048
//STEP95  EXEC SENDMAIL                                                 17070048
//INPUT   DD DSN=BP13.L617.MAILRPT,DISP=SHR                             17080048
/*                                                                      17090048
//*-----------------------------------------------------------------    00130010
//* PREMIUM + PREMIUM RATE = 0 FOR LEVY-----------------------------    00130010
//* HDB LOAN AND REPAYMENT > 0             -------------------------    00130010
//*-----------------------------------------------------------------    00130010
//STEP96A  EXEC SORT,COND=(0,NE)                                                
//SORTIN  DD DSN=BP13.K410.ACCUMSO,DISP=SHR                                     
//SORTOUT  DD DSN=&&BP13F40D,DISP=(NEW,PASS),                           16870048
//             SPACE=(CYL,(50,100),RLSE),                               16880048
//             DCB=(RECFM=FB,LRECL=2000,BLKSIZE=0)                      16890048
//SYSIN   DD  *                                                                 
  SORT FIELDS=(1,8,A),FORMAT=CH                                                 
   INCLUDE  COND=(40,3,CH,EQ,C'HSP',AND,                                        
                  839,8,CH,GE,C'20211015',AND,                                  
                 1718,8,CH,GT,C'00000000',AND,                                  
                  1993,2,CH,EQ,C'  ')                                           
//*-------------------------------------------------------------------- 16950048
//* WRITE A REPORT ON EJV FOR SERS CONTRA STILL PENDING                 16940048
//*-------------------------------------------------------------------- 16950048
//STEP96  EXEC PGM=BP13C40F,COND=(0,NE)                                 16960048
//BP13F410 DD  DSN=&&BP13F40D,DISP=(OLD,PASS)                           16970048
//BP13LEJV DD  DSN=BP13.LEJV.REPORT,DISP=OLD                            16980048
//SYSOUT   DD  SYSOUT=*                                                 17000048
//SYSPRINT DD  SYSOUT=*                                                 17010048
//SYSDBG   DD  SYSOUT=*                                                 17020048
//SYSDBOUT DD  SYSOUT=*                                                 17030048
//*-----------------------------------------------------------------    17040048
//* STEP 97: SEND THE SMTP MAIL                                         17050048
//*------------------------------------------------------------------*  17060048
//STEP97  EXEC SENDMAIL                                                 17070048
//INPUT   DD DSN=BP13.LEJV.REPORT,DISP=SHR                              17080048
/*                                                                      17090048
//*-------------------------------------------------------------------* 17100048
//*                       END OF JOB                                  * 17110048
//*-------------------------------------------------------------------* 17120048
