       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C94V.                                                 
       AUTHOR.        GURU..                                                    
      *DATE-WRITTEN.  22/03/2022.                                               
                                                                                
      *===============================================================*         
      *===         HDB SYSTEM OF COMMITMENT (BP13)                 ===*         
      *===============================================================*         
      *                   MOE DATA                                    *         
      *  OBJECTIVES : TO WRITE F21A FROM F94V WHEN                    *         
      *          F94V-DTE-ACCEPTANCE > 0 & F94V-DTE-KEY-ISSUED = 0    *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *===============================================================*         
      * CHGE-REQ   DATE        AUTHOR  REMARKS                        *         
      * --------------------------------------------------------------*         
      * BP138857   22/03/2022  KR13    NEW PROGRAM.                   *         
      *****************************************************************         
                                                                                
      *--------------------*                                                    
       ENVIRONMENT DIVISION.                                                    
      *--------------------*                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F94V  ASSIGN       TO BP13F94V.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS BP13K800-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                    ALTERNATE RECORD KEY IS K820-NUM-NRIC                       
                                            WITH DUPLICATES                     
                            FILE STATUS  IS BP13K820-STATUS.                    
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS BM06K110-STATUS.                   
                                                                                
           SELECT BB14K580  ASSIGN        TO BB14K580                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K580-KEY-FLD                       
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS BB14K580-STATUS.                   
                                                                                
           SELECT BP13F21A  ASSIGN        TO BP13F21A.                          
                                                                                
      *--------------*                                                          
       DATA DIVISION.                                                           
      *--------------*                                                          
       FILE SECTION.                                                            
                                                                                
       FD   BP13F94V                                                            
            RECORD CONTAINS 9 CHARACTERS                                        
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   BP13F94V-REC.                                                       
            05 F94V-NUM-NRIC   PIC X(9).                                        
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BB14K580                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BB14K580.                                                           
                                                                                
       FD   BP13F21A                                                            
            RECORD CONTAINS 750 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F21A.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13C913.               *                 
      *-------------------------------------------------------*                 
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
       01  WS-BP13C913                  PIC X(8)  VALUE 'BP13C913'.             
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-DATE              PIC X(08) VALUE SPACES.                 
           05  FILLER                   PIC X(13) VALUE SPACES.                 
                                                                                
       01  WS-CONTROl-DATE.                                                     
           05  WS-SYS-CCYY              PIC X(04) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE '-'.                    
           05  WS-SYS-MM                PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE '-'.                    
           05  WS-SYS-DD                PIC X(02) VALUE SPACES.                 
                                                                                
       01 WS-VARIABLES.                                                         
          05 BP13K800-STATUS            PIC 9(02) VALUE ZEROES.                 
          05 BP13K820-STATUS            PIC 9(02) VALUE ZEROES.                 
          05 BM06K110-STATUS            PIC 9(02) VALUE ZEROES.                 
          05 BB14K580-STATUS            PIC 9(02) VALUE ZEROES.                 
          05 WS-F94V-EOF                PIC X(01) VALUE 'N'.                    
          05 WS-K820-EOF                PIC X(01) VALUE 'N'.                    
          05 WS-K110-KI-SPACES          PIC X(01) VALUE 'N'.                    
          05 WS-K800-BOOK-FD            PIC X(01) VALUE 'N'.                    
          05 WS-SAVE-K800               PIC X(2000) VALUE SPACES.               
                                                                                
       01 WS-REC-CNTR.                                                          
          05 WS-F94V-READ               PIC 9(08) VALUE ZEROES.                 
          05 WS-K820-READ1              PIC 9(08) VALUE ZEROES.                 
          05 WS-K820-READ1-REGN         PIC 9(08) VALUE ZEROES.                 
          05 WS-K820-NFND1              PIC 9(08) VALUE ZEROES.                 
          05 WS-K820-READ               PIC 9(08) VALUE ZEROES.                 
          05 WS-K820-NFND               PIC 9(08) VALUE ZEROES.                 
          05 WS-K800-READ               PIC 9(08) VALUE ZEROES.                 
          05 WS-K800-VALID              PIC 9(08) VALUE ZEROES.                 
          05 WS-K800-INVALID            PIC 9(08) VALUE ZEROES.                 
          05 WS-K800-NFND               PIC 9(08) VALUE ZEROES.                 
          05 WS-K110-READ               PIC 9(08) VALUE ZEROES.                 
          05 WS-K110-NFND               PIC 9(08) VALUE ZEROES.                 
          05 WS-K110-VALID              PIC 9(08) VALUE ZEROES.                 
          05 WS-K110-KI                 PIC 9(08) VALUE ZEROES.                 
          05 WS-K580-READ               PIC 9(08) VALUE ZEROES.                 
          05 WS-K580-NFND               PIC 9(08) VALUE ZEROES.                 
          05 WS-F21A-WRITE              PIC 9(08) VALUE ZEROES.                 
          05 WS-F21A-WRITE-NOTFD        PIC 9(08) VALUE ZEROES.                 
          05 WS-CNT                     PIC 9(05) VALUE ZEROES.                 
          05 WS-CNT1                    PIC 9(05) VALUE ZEROES.                 
          05 HA-DETAILS OCCURS 4 TIMES.                                         
             10  WS-NRIC                   PIC X(09).                           
             10  WS-NAME                   PIC X(66).                           
                                                                                
      *==================*                                                      
       PROCEDURE DIVISION.                                                      
      *==================*                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-INITIALIZE      THRU 1000-EXIT.                         
                                                                                
           PERFORM 2000-READ-F94V       THRU 2000-EXIT.                         
                                                                                
           PERFORM 3000-PROCESS         THRU 3000-EXIT                          
             UNTIL WS-F94V-EOF = 'Y'.                                           
                                                                                
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-INITIALIZE.                                                         
      *----------------*                                                        
                                                                                
           OPEN  INPUT BP13F94V                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BM06K110                                                 
                       BB14K580                                                 
                OUTPUT BP13F21A.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'OPEN ERROR OCCURED FOR BP13K800'                         
              DISPLAY 'FILE STATUS :' BP13K800-STATUS                           
              MOVE BP13K800-STATUS      TO RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                          
           END-IF                                                               
                                                                                
           IF BP13K820-STATUS NOT = 00                                          
              DISPLAY 'OPEN ERROR OCCURED FOR BP13K820'                         
              DISPLAY 'FILE STATUS :' BP13K820-STATUS                           
              MOVE BP13K820-STATUS      TO RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                          
           END-IF                                                               
                                                                                
           IF BM06K110-STATUS NOT = 00                                          
              DISPLAY 'OPEN ERROR OCCURED FOR BM06K110'                         
              DISPLAY 'FILE STATUS :' BM06K110-STATUS                           
              MOVE BM06K110-STATUS      TO RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                          
           END-IF                                                               
                                                                                
           IF BB14K580-STATUS NOT = 00                                          
              DISPLAY 'OPEN ERROR OCCURED FOR BB14K580'                         
              DISPLAY 'FILE STATUS :' BB14K580-STATUS                           
              MOVE BB14K580-STATUS      TO RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                          
           END-IF                                                               
                                                                                
           MOVE SPACES TO BP13F21A-DETAIL-REC                                   
           INITIALIZE BP13F21A-HEADER-REC BP13F21A-CONTROL-REC                  
           MOVE 'H' TO F21A-HEADER.                                             
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
           MOVE WS-SYS-DATE(1:4)        TO WS-SYS-CCYY.                         
           MOVE WS-SYS-DATE(5:2)        TO WS-SYS-MM.                           
           MOVE WS-SYS-DATE(7:2)        TO WS-SYS-DD.                           
           MOVE WS-CONTROL-DATE         TO F21A-DTE-SEND.                       
           WRITE BP13F21A-HEADER-REC.                                           
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       2000-READ-F94V.                                                          
      *------------------------*                                                
                                                                                
           READ BP13F94V AT END                                                 
                MOVE 'Y'        TO WS-F94V-EOF                                  
                GO TO 2000-EXIT.                                                
           ADD  1 TO WS-F94V-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3000-PROCESS.                                                            
      *------------------------*                                                
                                                                                
           MOVE 'N' TO WS-K800-BOOK-FD.                                         
           PERFORM 4000-START-K820        THRU 4000-EXIT                        
                                                                                
           IF WS-SAVE-K800 NOT = SPACES                                         
              MOVE WS-SAVE-K800           TO BP13K800-MASTER                    
              PERFORM 4500-READ-BM06K110  THRU 4500-EXIT                        
              IF WS-K110-KI-SPACES = 'Y'                                        
                 PERFORM 5000-PROCESS-K820  THRU 5000-EXIT                      
                 PERFORM 6000-READ-BB14K580 THRU 6000-EXIT                      
                 PERFORM 7000-WRITE-F21A       THRU 7000-EXIT                   
              ELSE                                                              
                 MOVE SPACES TO BP13F21A-DETAIL-REC                             
                 MOVE F94V-NUM-NRIC     TO F21A-NUM-NRIC-HA1                    
                 ADD 1 TO WS-F21A-WRITE-NOTFD                                   
                 ADD 1 TO WS-F21A-WRITE                                         
                 WRITE BP13F21A-DETAIL-REC                                      
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES TO BP13F21A-DETAIL-REC                                
              MOVE F94V-NUM-NRIC     TO F21A-NUM-NRIC-HA1                       
              ADD 1 TO WS-F21A-WRITE-NOTFD                                      
              ADD 1 TO WS-F21A-WRITE                                            
              WRITE BP13F21A-DETAIL-REC                                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F94V         THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4000-START-K820.                                                         
      *---------------*                                                         
           MOVE SPACES TO BP13K820-REC BP13F21A-DETAIL-REC WS-SAVE-K800         
           INITIALIZE  BP13K820-REC BP13F21A-DETAIL-REC                         
                                                                                
           MOVE 'N'                        TO WS-K820-EOF.                      
           MOVE SPACES                     TO K820-KEY-FLD                      
           MOVE F94V-NUM-NRIC              TO K820-NUM-NRIC                     
                                                                                
           START BP13K820 KEY >= K820-NUM-NRIC.                                 
                                                                                
           EVALUATE BP13K820-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4100-READ-K820 THRU 4100-EXIT                       
                      UNTIL WS-K820-EOF = 'Y'                                   
                      OR WS-K800-BOOK-FD = 'Y'                                  
               WHEN 23                                                          
                    DISPLAY 'KEY NOT FOUND IN BP13K820!' K820-KEY-FLD           
                    ADD 1   TO WS-K820-NFND1                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K820!'                          
                             ' / KEY : ' K820-KEY-FLD                           
                             ' / FILE STATUS :' BP13K820-STATUS                 
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4100-READ-K820.                                                          
      *---------------*                                                         
                                                                                
           READ BP13K820 NEXT                                                   
                                                                                
           EVALUATE BP13K820-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K820-NUM-NRIC = F94V-NUM-NRIC                            
                       MOVE K820-NUM-REGN         TO K800-NUM-REGN              
                       PERFORM 4150-READ-BP13K800 THRU 4150-EXIT                
                       ADD 1                      TO WS-K820-READ1              
                    ELSE                                                        
                       MOVE 'Y'                   TO WS-K820-EOF                
                       ADD 1   TO WS-K820-READ1-REGN                            
                    DISPLAY '1)K820-NOTFD    :' K820-KEY-FLD                    
                            'FILE STATUS : ' BP13K820-STATUS                    
                    END-IF                                                      
               WHEN 23                                                          
                    DISPLAY '2)K820-NOTFD    :' K820-KEY-FLD                    
                            'FILE STATUS : ' BP13K820-STATUS                    
                    ADD 1   TO WS-K820-NFND1                                    
                    MOVE 'Y'                      TO WS-K820-EOF                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K820!'                           
                    DISPLAY 'K820-NUM-REGN :' K820-KEY-FLD                      
                            'FILE STATUS : ' BP13K820-STATUS                    
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4150-READ-BP13K800.                                                      
      ****************************                                              
                                                                                
           READ BP13K800                                                        
           EVALUATE BP13K800-STATUS                                             
               WHEN 00                                                          
                    ADD 1                       TO WS-K800-READ                 
                    IF (K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES)           
                       AND (K800-NUM-STATUS NOT = 'C')                          
                       MOVE 'Y' TO WS-K800-BOOK-FD                              
                       ADD 1                    TO WS-K800-VALID                
                       MOVE BP13K800-MASTER     TO WS-SAVE-K800                 
                    ELSE                                                        
                       ADD 1                    TO WS-K800-INVALID              
                    END-IF                                                      
               WHEN 23                                                          
                    ADD 1 TO WS-K800-NFND                                       
                    DISPLAY 'K800 RECORD NOT FOUND: ' K820-NUM-REGN             
               WHEN OTHER                                                       
                    DISPLAY 'ERROR  READING BP13K800. STATUS IS '               
                         BP13K800-STATUS                                        
                    PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4500-READ-BM06K110.                                                      
      *------------------*                                                      
           MOVE SPACES TO K110-REC.                                             
           INITIALIZE K110-REC.                                                 
           MOVE 'N'                        TO WS-K110-KI-SPACES                 
           MOVE K800-NUM-SCH-ACC           TO K110-SCH-ACC-NO                   
                                                                                
           READ BM06K110                                                        
           EVALUATE BM06K110-STATUS                                             
               WHEN 00                                                          
                    ADD 1                  TO WS-K110-READ                      
                    IF K110-DTE-KEY-ISSUED = SPACES OR ZEROES                   
                       MOVE 'Y'            TO WS-K110-KI-SPACES                 
                       ADD 1                  TO WS-K110-VALID                  
                    ELSE                                                        
                       ADD 1                  TO WS-K110-KI                     
                    END-IF                                                      
               WHEN 23                                                          
                    ADD 1 TO WS-K110-NFND                                       
                    DISPLAY 'K110 RECORD NOT FOUND: ' K110-SCH-ACC-NO           
               WHEN OTHER                                                       
                    DISPLAY 'ERROR  READING BM06K110. STATUS IS '               
                         BM06K110-STATUS                                        
                    PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       5000-PROCESS-K820.                                                       
      *------------------*                                                      
           MOVE SPACES TO BP13K820-REC                                          
           INITIALIZE  BP13K820-REC                                             
                                                                                
           MOVE 'N'                        TO WS-K820-EOF.                      
           MOVE ZEROES                     TO WS-CNT WS-CNT1.                   
           MOVE SPACES                     TO K820-KEY-FLD                      
           MOVE K800-NUM-REGN              TO K820-NUM-REGN                     
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K820-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 5100-READ-K820 THRU 5100-EXIT                       
                      UNTIL WS-K820-EOF = 'Y'                                   
               WHEN 23                                                          
                    DISPLAY 'KEY NOT FOUND IN BP13K820!' K820-KEY-FLD           
                    ADD 1   TO WS-K820-NFND                                     
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K820!'                          
                             ' / KEY : ' K820-KEY-FLD                           
                             ' / FILE STATUS :' BP13K820-STATUS                 
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       5100-READ-K820.                                                          
      *---------------*                                                         
                                                                                
           READ BP13K820 NEXT                                                   
                                                                                
           EVALUATE BP13K820-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K800-NUM-REGN = K820-NUM-REGN                            
                       PERFORM 5150-STORE-DETAILS THRU 5150-EXIT                
                       ADD 1                      TO wS-K820-READ               
                    ELSE                                                        
                       MOVE 'Y'                   TO WS-K820-EOF                
                    END-IF                                                      
               WHEN 23                                                          
                    MOVE 'Y'                      TO WS-K820-EOF                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K820!'                           
                    DISPLAY 'K820-NUM-REGN :' K820-NUM-REGN                     
                            'FILE STATUS : ' BP13K820-STATUS                    
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5150-STORE-DETAILS.                                                      
      *-------------------*                                                     
           IF K820-NUM-NRIC = K800-NUM-NRIC1 OR K800-NUM-NRIC2 OR               
                              K800-NUM-NRIC3 OR K800-NUM-NRIC4                  
              ADD 1 TO WS-CNT                                                   
              EVALUATE WS-CNT                                                   
                  WHEN 1                                                        
                       MOVE K820-NUM-NRIC    TO F21A-NUM-NRIC-HA1               
                       MOVE K820-NME-OCCP    TO F21A-NME-HA1                    
                  WHEN 2                                                        
                       MOVE K820-NUM-NRIC    TO F21A-NUM-NRIC-HA2               
                       MOVE K820-NME-OCCP    TO F21A-NME-HA2                    
                  WHEN 3                                                        
                       MOVE K820-NUM-NRIC    TO F21A-NUM-NRIC-HA3               
                       MOVE K820-NME-OCCP    TO F21A-NME-HA3                    
                  WHEN 4                                                        
                       MOVE K820-NUM-NRIC    TO F21A-NUM-NRIC-HA4               
                       MOVE K820-NME-OCCP    TO F21A-NME-HA4                    
              END-EVALUATE                                                      
           ELSE                                                                 
              ADD 1 TO WS-CNT1                                                  
              EVALUATE WS-CNT1                                                  
                  WHEN 1                                                        
                       MOVE K820-NUM-NRIC    TO F21A-NUM-NRIC-OC1               
                       MOVE K820-NME-OCCP    TO F21A-NME-OC1                    
                  WHEN 2                                                        
                       MOVE K820-NUM-NRIC    TO F21A-NUM-NRIC-OC2               
                       MOVE K820-NME-OCCP    TO F21A-NME-OC2                    
                  WHEN 3                                                        
                       MOVE K820-NUM-NRIC    TO F21A-NUM-NRIC-OC3               
                       MOVE K820-NME-OCCP    TO F21A-NME-OC3                    
                  WHEN 4                                                        
                       MOVE K820-NUM-NRIC    TO F21A-NUM-NRIC-OC4               
                       MOVE K820-NME-OCCP    TO F21A-NME-OC4                    
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
       5150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       6000-READ-BB14K580.                                                      
      *-------------------*                                                     
           MOVE SPACES                      TO BB14K580-REC.                    
           INITIALIZE                          BB14K580-REC.                    
                                                                                
           MOVE K800-NUM-SCH-ACC            TO K580-KEY-FLD.                    
                                                                                
           READ BB14K580.                                                       
                                                                                
           EVALUATE BB14K580-STATUS                                             
               WHEN 00                                                          
                    ADD 1                   TO WS-K580-READ                     
                    MOVE K580-DTE-UNIT-DELIVERY TO F21A-DTE-DPD                 
               WHEN 23                                                          
                    ADD 1                   TO WS-K580-NFND                     
                    MOVE SPACES             TO F21A-DTE-DPD                     
               WHEN OTHER                                                       
                    ADD 1                   TO WS-K580-NFND                     
                    DISPLAY 'ERROR READING BB14K580!'                           
                    DISPLAY 'K580-KEY-FLD :' K580-KEY-FLD                       
                    DISPLAY 'FILE STATUS : ' BB14K580-STATUS                    
                    MOVE BB14K580-STATUS    TO RETURN-CODE                      
                    PERFORM 9900-ABNORMAL-END  THRU 9900-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       7000-WRITE-F21A.                                                         
      *-----------------*                                                       
           IF WS-K110-KI-SPACES = 'N'                                           
              MOVE F94V-NUM-NRIC         TO F21A-NUM-NRIC-HA1                   
           ELSE                                                                 
              MOVE SPACES                   TO WS-LINK-REC                      
              MOVE K800-NUM-SCH-ACC(1:4)    TO WS-LINK-NUM-SCH                  
              MOVE K800-NUM-SCH-ACC(5:4)    TO WS-LINK-NUM-ACC                  
              MOVE K800-NUM-SCH-ACC(9:1)    TO WS-LINK-NUM-CHK                  
              PERFORM 7100-CALL-BP13C913    THRU 7100-EXIT                      
              MOVE K800-NUM-REGN            TO F21A-NUM-REGN                    
              MOVE COMM8-NUM-BLK            TO F21A-NUM-BLK                     
              MOVE COMM8-NME-STREET         TO F21A-NUM-STREET                  
              MOVE COMM8-NUM-LEVEL          TO F21A-NUM-LEVEL                   
              STRING COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB                     
                   DELIMITED BY SIZE INTO F21A-NUM-UNIT                         
              MOVE COMM8-NUM-POSTAL-CODE    TO F21A-NUM-POSTAL                  
              MOVE K800-DTE-SALE-AGMT       TO F21A-DTE-SALE-AGR                
           END-IF                                                               
                                                                                
      *    MOVE ';' TO FILLER1 FILLER2 FILLER3 FILLER4 FILLER5 FILLER6          
      *                FILLER7 FILLER8 FILLER9 FILLER10 FILLER11                
      *                FILLER12 FILLER13 FILLER14 FILLER15 FILLER16             
      *                FILLER17 FILLER18  FILLER19 FILLER20 FILLER21            
      *                FILLER22 FILLER23 FILLER24.                              
                                                                                
           WRITE BP13F21A-DETAIL-REC                                            
           ADD 1 TO WS-F21A-WRITE.                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       7100-CALL-BP13C913.                                                      
      *---------------------*                                                   
                                                                                
           CALL WS-BP13C913 USING WS-LINK-REC, BP13COMM8-REC                    
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
                                                                                
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
           MOVE SPACES TO BP13F21A-CONTROL-REC BP13F21A-DETAIL-REC              
           MOVE 'T' TO F21A-CONTROL.                                            
           MOVE WS-F21A-WRITE TO F21A-NUM-TOTAL                                 
           WRITE BP13F21A-CONTROL-REC.                                          
                                                                                
           CLOSE BP13F94V                                                       
                 BP13F21A.                                                      
                                                                                
           CLOSE BP13K800                                                       
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'ERROR CLOSING FILE : BP13K800!'                          
              DISPLAY 'FILE-STATUS :' BP13K800-STATUS                           
           END-IF                                                               
                                                                                
           CLOSE BP13K820                                                       
           IF BP13K820-STATUS NOT = 00                                          
              DISPLAY 'ERROR CLOSING FILE : BP13K820!'                          
              DISPLAY 'FILE-STATUS :' BP13K820-STATUS                           
           END-IF                                                               
                                                                                
           CLOSE BM06K110                                                       
           IF BM06K110-STATUS NOT = 00                                          
              DISPLAY 'ERROR CLOSING FILE : BM06K110!'                          
              DISPLAY 'FILE-STATUS :' BM06K110-STATUS                           
           END-IF                                                               
                                                                                
           CLOSE BB14K580                                                       
           IF BB14K580-STATUS NOT = 00                                          
              DISPLAY 'ERROR CLOSING FILE : BB14K580!'                          
              DISPLAY 'FILE-STATUS :' BB14K580-STATUS                           
           END-IF                                                               
                                                                                
           PERFORM 9100-DISPLAY-STATISTICS  THRU 9100-EXIT                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9900-ABNORMAL-END.                                                       
      *-----------------*                                                       
           DISPLAY ' THE PROGRAM TERMINATES NOW!!!'                             
           PERFORM 9100-DISPLAY-STATISTICS  THRU 9100-EXIT                      
           STOP RUN                                                             
           .                                                                    
      *----------*                                                              
       9900-EXIT.                                                               
      *----------*                                                              
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       9100-DISPLAY-STATISTICS.                                                 
      *-----------------------*                                                 
           DISPLAY '*================================================*'         
           DISPLAY '*== PROGRAM STATISTICS - BP13C94V    ============*'         
           DISPLAY '*================================================*'         
           DISPLAY '*NUMBER OF F94V RECS READ    :' WS-F94V-READ                
           DISPLAY '*                            :'.                            
           DISPLAY '*NUMBER OF K820 RECS READ1   :' WS-K820-READ1.              
           DISPLAY '*NUMBER OF K820 NOTFD REGN   :' WS-K820-READ1-REGN.         
           DISPLAY '*NUMBER OF K820 NOTFD READ1  :' WS-K820-NFND1.              
           DISPLAY '*                            :'.                            
           DISPLAY '*NUMBER OF K800 RECS READ    :' WS-K800-READ.               
           DISPLAY '*NUMBER OF K800 VALID(BOOK)  :' WS-K800-VALID.              
           DISPLAY '*NUMBER OF K800 NO BOOK, CAN :' WS-K800-INVALID.            
           DISPLAY '*NUMBER OF K800 REGN NOTFD   :' WS-K800-NFND.               
           DISPLAY '*                            :'.                            
           DISPLAY '*NUMBER OF K110 RECS READ    :' WS-K110-READ.               
           DISPLAY '*NUMBER OF K110 RECS VALID   :' WS-K110-VALID.              
           DISPLAY '*NUMBER OF K110 REGN NOTFD   :' WS-K110-NFND.               
           DISPLAY '*NUMBER OF K110 KEY-ISSUED   :' WS-K110-KI.                 
           DISPLAY '*                            :'.                            
           DISPLAY '*NUMBER OF K580 RECS READ    :' WS-K580-READ.               
           DISPLAY '*NUMBER OF K580 RECS NOTFD   :' WS-K580-NFND.               
           DISPLAY '*                            :'.                            
           DISPLAY '*NUMBER OF K820 RECS READ    :' WS-K820-READ.               
           DISPLAY '*NUMBER OF K820 RECS NOTFD   :' WS-K820-NFND.               
           DISPLAY '*NUMBER OF F21A RECS WRITE   :' WS-F21A-WRITE.              
           DISPLAY '*NUMBER OF F21A RECS NOTFD   :' WS-F21A-WRITE-NOTFD.        
                                                                                
           DISPLAY '*================================================*'         
           .                                                                    
       9100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********  E N D   O F   P R O G R A M   B P 1 3 C 9 4 v *********        
