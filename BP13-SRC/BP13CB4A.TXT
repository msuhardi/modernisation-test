      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CB4A.                                                 
       AUTHOR.        MRN1                                                      
       DATE-WRITTEN.  08/03/2017.                                               
                                                                                
      ***************************************************************           
      *            SYSTEM OF COMMITMENT (BP13)                      *           
      *=============================================================*           
      *   OBJECTIVE  : SUBR TO RETRIEVE ALL GRANT ASSESSMENT INFO   *           
      *                BASED ON REGNO AS INPUT                      *           
      *               -IF MORE THAN 5 NUM GRANT PER HOUSEHOLD, WILL *           
      *                RETURN COM73-NUM-NEXT-NUM-GRANT TO INDICATE  *           
      *                THE NEED TO CALL SUBR AGAIN FOR MORE RECORDS *           
      *   INPUT      : COM73-NUM-RGSTRN-INPUT                       *           
      *   OUTPUT     : HOUSEHOLD + PERSON + RECOVERY                *           
      *   COMMAREA   : 12000 BYTES                                  *           
      *   ACTION CODE: 'R' -> RETRIEVE                              *           
      ***************************************************************           
      * CHGE REQ #  DATE        AUTHOR  REMARKS                     *           
      * ----------  ----------  ------  -------------------------   *           
      * BP13XXXX    08/03/2017  MRN1    NEW SUBROUTINE              *           
      * BP136254    11/12/2017  MRN1    ADD NEW ACTION CODE FOR CE11*           
      * BP137471    25/09/2018  MRN1    INITIALIZE VAR BEFORE SELECT*           
      ***************************************************************           
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       DATA DIVISION.                                                           
      *-----------------------*                                                 
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND                 PIC S9(4) COMP VALUE +000.          
           05  WS-SQL-NOT-FOUND             PIC S9(4) COMP VALUE +100.          
           05  WS-SQL-MORE-ROWS             PIC S9(4) COMP VALUE -811.          
                                                                                
       01  WS-SQLCODE                       PIC S9(4) COMP.                     
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-RGSTRN                PIC X(8).                           
           05  WS-NUM-GRANT-TYPE            PIC X(6).                           
           05  WS-NUM-IDNTY-NO              PIC X(9).                           
           05  WS-TME-CREATE                PIC X(26).                          
           05  WS-CTR                       PIC 9(2) VALUE ZEROES.              
           05  WS-CTR1                      PIC 9(2) VALUE ZEROES.              
           05  WS-CTR2                      PIC 9(2) VALUE ZEROES.              
           05  WS-CTR-G                     PIC 9(2) VALUE ZEROES.              
           05  WS-CTR-P                     PIC 9(2) VALUE ZEROES.              
           05  WS-CURSOR1-EOF               PIC X VALUE 'N'.                    
           05  WS-CURSOR2-EOF               PIC X VALUE 'N'.                    
           05  WS-CURSOR3-EOF               PIC X VALUE 'N'.                    
           05  WS-CURSOR4-EOF               PIC X VALUE 'N'.                    
           05  WS-LIVE-EXIST                PIC X VALUE 'N'.                    
           05  WS-SQL-CODE                  PIC 9(4).                           
           05  WS-SQL-CODE-X REDEFINES WS-SQL-CODE PIC X(4).                    
           05  WS-AMT-GRANT                 PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-GRANT-AMT-SUM             PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-AMT-RECOVERY              PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-AMT-RECOVERY-INT          PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-EX-AMT-RECOVERY           PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-EX-GRANT-AMT-SUM          PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-TOT-GRANT-AMT-SUM         PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-TOT-AMT-RECOVERY          PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-TOT-EX-AMT-RECOVERY       PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-TOT-EX-GRANT-AMT-SUM      PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-AMT-RECOVERY-INDNULL      PIC S9(4) COMP.                     
           05  WS-AMT-RECOVERY-INT-INDNULL  PIC S9(4) COMP.                     
           05  WS-EX-AMT-RECOVERY-INDNULL   PIC S9(4) COMP.                     
           05  WS-GRANT-AMT-SUM-INDNULL     PIC S9(4) COMP.                     
           05  WS-EX-GRANT-AMT-SUM-INDNULL  PIC S9(4) COMP.                     
           05  WS-EX-GRANT-TOT-INDNULL      PIC S9(4) COMP.                     
           05  WS-EX-RCVRY-TOT-INDNULL      PIC S9(4) COMP.                     
                                                                                
           EXEC SQL INCLUDE P13VGXAS END-EXEC.                                  
           EXEC SQL INCLUDE P13VGXAP END-EXEC.                                  
           EXEC SQL INCLUDE P13VGRCV END-EXEC.                                  
           EXEC SQL INCLUDE P13VGASM END-EXEC.                                  
           EXEC SQL INCLUDE P13VGAPN END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
      *-----------------------*                                                 
       LINKAGE SECTION.                                                         
      *-----------------------*                                                 
                                                                                
       COPY P13COM73.                                                           
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION USING COM73-COMMAREA.                                 
      *------------------*                                                      
                                                                                
           EXEC SQL WHENEVER NOT FOUND                                          
                             CONTINUE END-EXEC.                                 
           EXEC SQL WHENEVER SQLERROR                                           
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
           EXEC SQL WHENEVER SQLWARNING                                         
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
                                                                                
      *-----------------------*                                                 
       0000-START-ROUTINE.                                                      
      *-----------------------*                                                 
           MOVE ZEROES      TO  COM73-CDE-ERROR.                                
           MOVE SPACES      TO  COM73-ERR-MSG.                                  
                                                                                
           EVALUATE COM73-NUM-CALL-ACTION                                       
              WHEN 'R'                                                          
                    PERFORM 1000-READ-GRANT-ASMNT THRU 1000-EXIT                
              WHEN 'A'                                                          
                    PERFORM 1000-READ-GRANT-ASMNT THRU 1000-EXIT                
                    IF WS-LIVE-EXIST NOT = 'Y'                                  
                       PERFORM 5000-READ-EX-GRANT-ASMNT THRU 5000-EXIT          
                    END-IF                                                      
              WHEN OTHER                                                        
                    MOVE 'E517'       TO COM73-CDE-ERROR                        
                    MOVE '(CB4A)INVALID ACTION CODE' TO COM73-ERR-MSG           
           END-EVALUATE.                                                        
                                                                                
           PERFORM 9100-GOBACK          THRU 9100-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       1000-READ-GRANT-ASMNT.                                                   
      *-----------------------*                                                 
           MOVE ZEROES TO WS-AMT-GRANT                                          
                          WS-GRANT-AMT-SUM                                      
                          WS-AMT-RECOVERY                                       
                          WS-AMT-RECOVERY-INT                                   
                          WS-EX-AMT-RECOVERY                                    
                          WS-EX-GRANT-AMT-SUM                                   
                          WS-TOT-GRANT-AMT-SUM                                  
                          WS-TOT-AMT-RECOVERY                                   
                          WS-TOT-EX-AMT-RECOVERY                                
                          WS-TOT-EX-GRANT-AMT-SUM.                              
           MOVE 'N' TO WS-CURSOR1-EOF                                           
                       WS-LIVE-EXIST.                                           
           PERFORM 1100-OPEN-CURSOR     THRU 1100-EXIT.                         
           PERFORM 1200-FETCH-CURSOR    THRU 1200-EXIT                          
                   VARYING WS-CTR1 FROM 1 BY 1                                  
                   UNTIL (SQLCODE = WS-SQL-NOT-FOUND)                           
                      OR (WS-CURSOR1-EOF) = 'Y'.                                
           PERFORM 1300-CLOSE-CURSOR    THRU 1300-EXIT.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       1100-OPEN-CURSOR.                                                        
      *================================================================*        
           INITIALIZE SQLCODE.                                                  
           MOVE ZEROES                TO COM73-CDE-SYSERR.                      
           MOVE ZEROES                TO WS-CTR-G.                              
           IF COM73-NUM-RGSTRN-INPUT = SPACES OR LOW-VALUES                     
              MOVE 'E500'           TO COM73-CDE-ERROR                          
              MOVE 'REGNO IS BLANK' TO COM73-ERR-MSG                            
              GO TO 9100-GOBACK                                                 
           ELSE                                                                 
              MOVE COM73-NUM-RGSTRN-INPUT                                       
                           TO NUM-RGSTRN OF DCLGRANT-ASMNT                      
           END-IF.                                                              
                                                                                
      *-------------------------------------*                                   
      *  DECLARE CURSOR - GRANT ASSESSMENT  *                                   
      *-------------------------------------*                                   
           EXEC SQL                                                             
             DECLARE CURSOR1 CURSOR FOR                                         
               SELECT NUM_RGSTRN,                                               
                      NUM_GRANT_TYPE,                                           
                      NUM_ELGBL_GRANT,                                          
                      NUM_GRANT_AMOUNT_ELGBL,                                   
                      NUM_GRANT_STATUS,                                         
                      DTE_APRV,                                                 
                      DTE_CANCEL,                                               
                      DTE_REJECT,                                               
                      DTE_SUBMIT,                                               
                      DTE_WTHDRW,                                               
                      NUM_RJCTN_REASON,                                         
                      NUM_WTDRWL_REASON,                                        
                      TXT_CNCLTN_REASON,                                        
                      DTE_GRANT_RQST,                                           
                      NUM_GRANT_DOC_RCV,                                        
                      DTE_ACK,                                                  
                      DTE_GRANT_FORM_ISSUE,                                     
                      DTE_GRANT_OPT_OUT_FORM_ISSUE,                             
                      NUM_GRANT_OPT_OUT_REASON,                                 
                      DTE_GRANT_OPT_OUT_REASON,                                 
                      NUM_ALCTN_SCHEME,                                         
                      NUM_HSHLD_CTZNSP,                                         
                      NUM_WRKNG_CHILD_ASMNT,                                    
                      QTY_MONTH_WORK,                                           
                      NUM_APPEAL_ATCHMT,                                        
                      NUM_PARENT_ADRS_BLDNG,                                    
                      NUM_PARENT_ADRS_LEVEL,                                    
                      NUM_PARENT_ADRS_UNIT,                                     
                      TXT_PARENT_ADRS1,                                         
                      TXT_PARENT_ADRS2,                                         
                      NUM_PARENT_ADRS_POSTAL,                                   
                      DTE_PYMNT,                                                
                      DTE_CPF_ACK_GRANT_RCV,                                    
                      NUM_GRANT_CREDIT,                                         
                      TME_APNTMT,                                               
                      DTE_APNTMT,                                               
                      NUM_ACNT,                                                 
                      NUM_SCHEME,                                               
                      NUM_TNCY,                                                 
                      NUM_HALF_GRANT                                            
               FROM                                                             
                 GRANT_ASMNT                                                    
               WHERE                                                            
                 NUM_RGSTRN = :DCLGRANT-ASMNT.NUM-RGSTRN                        
               ORDER BY NUM_GRANT_TYPE                                          
           END-EXEC.                                                            
                                                                                
      *----------------------------------*                                      
      *  OPEN CURSOR - GRANT ASSESSMENT  *                                      
      *----------------------------------*                                      
           EXEC SQL                                                             
             OPEN CURSOR1                                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 0                                                               
             CONTINUE                                                           
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM73-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE '(CB4A) OPEN CURSOR1 ERROR' TO COM73-ERR-MSG                  
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       1200-FETCH-CURSOR.                                                       
      *================================================================*        
                                                                                
           EXEC SQL                                                             
             FETCH CURSOR1                                                      
               INTO                                                             
                 :DCLGRANT-ASMNT.NUM-RGSTRN,                                    
                 :DCLGRANT-ASMNT.NUM-GRANT-TYPE,                                
                 :DCLGRANT-ASMNT.NUM-ELGBL-GRANT,                               
                 :DCLGRANT-ASMNT.NUM-GRANT-AMOUNT-ELGBL,                        
                 :DCLGRANT-ASMNT.NUM-GRANT-STATUS,                              
                 :DCLGRANT-ASMNT.DTE-APRV,                                      
                 :DCLGRANT-ASMNT.DTE-CANCEL,                                    
                 :DCLGRANT-ASMNT.DTE-REJECT,                                    
                 :DCLGRANT-ASMNT.DTE-SUBMIT,                                    
                 :DCLGRANT-ASMNT.DTE-WTHDRW,                                    
                 :DCLGRANT-ASMNT.NUM-RJCTN-REASON,                              
                 :DCLGRANT-ASMNT.NUM-WTDRWL-REASON,                             
                 :DCLGRANT-ASMNT.TXT-CNCLTN-REASON,                             
                 :DCLGRANT-ASMNT.DTE-GRANT-RQST,                                
                 :DCLGRANT-ASMNT.NUM-GRANT-DOC-RCV,                             
                 :DCLGRANT-ASMNT.DTE-ACK,                                       
                 :DCLGRANT-ASMNT.DTE-GRANT-FORM-ISSUE,                          
                 :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-FORM-ISSUE,                  
                 :DCLGRANT-ASMNT.NUM-GRANT-OPT-OUT-REASON,                      
                 :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-REASON,                      
                 :DCLGRANT-ASMNT.NUM-ALCTN-SCHEME,                              
                 :DCLGRANT-ASMNT.NUM-HSHLD-CTZNSP,                              
                 :DCLGRANT-ASMNT.NUM-WRKNG-CHILD-ASMNT,                         
                 :DCLGRANT-ASMNT.QTY-MONTH-WORK,                                
                 :DCLGRANT-ASMNT.NUM-APPEAL-ATCHMT,                             
                 :DCLGRANT-ASMNT.NUM-PARENT-ADRS-BLDNG,                         
                 :DCLGRANT-ASMNT.NUM-PARENT-ADRS-LEVEL,                         
                 :DCLGRANT-ASMNT.NUM-PARENT-ADRS-UNIT,                          
                 :DCLGRANT-ASMNT.TXT-PARENT-ADRS1,                              
                 :DCLGRANT-ASMNT.TXT-PARENT-ADRS2,                              
                 :DCLGRANT-ASMNT.NUM-PARENT-ADRS-POSTAL,                        
                 :DCLGRANT-ASMNT.DTE-PYMNT,                                     
                 :DCLGRANT-ASMNT.DTE-CPF-ACK-GRANT-RCV,                         
                 :DCLGRANT-ASMNT.NUM-GRANT-CREDIT,                              
                 :DCLGRANT-ASMNT.TME-APNTMT,                                    
                 :DCLGRANT-ASMNT.DTE-APNTMT,                                    
                 :DCLGRANT-ASMNT.NUM-ACNT,                                      
                 :DCLGRANT-ASMNT.NUM-SCHEME,                                    
                 :DCLGRANT-ASMNT.NUM-TNCY,                                      
                 :DCLGRANT-ASMNT.NUM-HALF-GRANT                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-FOUND                                                    
             IF NUM-GRANT-TYPE OF DCLGRANT-ASMNT                                
                                      >= COM73-NUM-NEXT-NUM-GRANT               
                ADD  1 TO WS-CTR-G                                              
                MOVE 0 TO WS-CTR-P                                              
                IF WS-CTR-G = 6                                                 
                   MOVE NUM-GRANT-TYPE OF DCLGRANT-ASMNT                        
                                       TO COM73-NUM-NEXT-NUM-GRANT              
                   MOVE 'Y'                  TO WS-CURSOR1-EOF                  
                   MOVE 0 TO WS-CTR-G                                           
                ELSE                                                            
                   MOVE 'Y' TO WS-LIVE-EXIST                                    
                   MOVE SPACES TO COM73-NUM-NEXT-NUM-GRANT                      
                   PERFORM 1250-MOVE-GRANT-ASMNT THRU 1250-EXIT                 
                   PERFORM 2000-RETRIEVE-PERSON  THRU 2000-EXIT                 
                   PERFORM 4000-GET-EX-RCVRY-AMT-TOTAL THRU 4000-EXIT           
                   MOVE WS-TOT-GRANT-AMT-SUM                                    
                                       TO COM73-SUM-AMT-GRANT(WS-CTR-G)         
                   MOVE WS-TOT-AMT-RECOVERY                                     
                                       TO COM73-SUM-AMT-RCVRY(WS-CTR-G)         
                   MOVE WS-TOT-EX-GRANT-AMT-SUM                                 
                                    TO COM73-SUM-EX-AMT-GRANT(WS-CTR-G)         
                   MOVE WS-TOT-EX-AMT-RECOVERY                                  
                                    TO COM73-SUM-EX-AMT-RCVRY(WS-CTR-G)         
                END-IF                                                          
             END-IF                                                             
           WHEN WS-SQL-NOT-FOUND                                                
             MOVE 'Y'                  TO WS-CURSOR1-EOF                        
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM73-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE '(CB4A) FETCH CURSOR ERROR' TO COM73-ERR-MSG                  
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       1250-MOVE-GRANT-ASMNT.                                                   
      *---------------------------------------------------------------*         
           MOVE NUM-RGSTRN OF DCLGRANT-ASMNT                                    
                             TO COM73-NUM-RGSTRN(WS-CTR-G).                     
           MOVE NUM-GRANT-TYPE OF DCLGRANT-ASMNT                                
                             TO COM73-NUM-GRANT-TYPE(WS-CTR-G)                  
                                WS-NUM-GRANT-TYPE.                              
           MOVE NUM-ELGBL-GRANT OF DCLGRANT-ASMNT                               
                             TO COM73-ELGBL-GRANT(WS-CTR-G).                    
           MOVE NUM-GRANT-AMOUNT-ELGBL OF DCLGRANT-ASMNT                        
                             TO COM73-GRANT-AMOUNT-ELGBL(WS-CTR-G).             
           MOVE NUM-GRANT-STATUS OF DCLGRANT-ASMNT                              
                             TO COM73-GRANT-STATUS(WS-CTR-G).                   
           MOVE DTE-APRV OF DCLGRANT-ASMNT                                      
                             TO COM73-DTE-APRV(WS-CTR-G).                       
           MOVE DTE-CANCEL OF DCLGRANT-ASMNT                                    
                             TO COM73-DTE-CANCEL(WS-CTR-G).                     
           MOVE DTE-REJECT OF DCLGRANT-ASMNT                                    
                             TO COM73-DTE-REJECT(WS-CTR-G).                     
           MOVE DTE-SUBMIT OF DCLGRANT-ASMNT                                    
                             TO COM73-DTE-SUBMIT(WS-CTR-G).                     
           MOVE DTE-WTHDRW OF DCLGRANT-ASMNT                                    
                             TO COM73-DTE-WTHDRW(WS-CTR-G).                     
           MOVE NUM-RJCTN-REASON OF DCLGRANT-ASMNT                              
                             TO COM73-RJCTN-REASON(WS-CTR-G).                   
           MOVE NUM-WTDRWL-REASON OF DCLGRANT-ASMNT                             
                             TO COM73-WTDRWL-REASON(WS-CTR-G).                  
           MOVE TXT-CNCLTN-REASON OF DCLGRANT-ASMNT                             
                             TO COM73-CNCLTN-REASON(WS-CTR-G).                  
           MOVE DTE-GRANT-RQST OF DCLGRANT-ASMNT                                
                             TO COM73-DTE-GRANT-RQST(WS-CTR-G).                 
           MOVE NUM-GRANT-DOC-RCV OF DCLGRANT-ASMNT                             
                             TO COM73-GRANT-DOC-RCV(WS-CTR-G).                  
           MOVE DTE-ACK OF DCLGRANT-ASMNT                                       
                             TO COM73-DTE-ACK(WS-CTR-G).                        
           MOVE DTE-GRANT-FORM-ISSUE OF DCLGRANT-ASMNT                          
                             TO COM73-DTE-GRANT-ISSUE(WS-CTR-G).                
           MOVE DTE-GRANT-OPT-OUT-FORM-ISSUE OF DCLGRANT-ASMNT                  
                             TO COM73-DTE-OPT-OUT-ISSUE(WS-CTR-G).              
           MOVE NUM-GRANT-OPT-OUT-REASON OF DCLGRANT-ASMNT                      
                             TO COM73-OPT-OUT-REASON(WS-CTR-G).                 
           MOVE DTE-GRANT-OPT-OUT-REASON OF DCLGRANT-ASMNT                      
                             TO COM73-DTE-OPT-OUT-REASON(WS-CTR-G).             
           MOVE NUM-ALCTN-SCHEME OF DCLGRANT-ASMNT                              
                             TO COM73-ALCTN-SCHEME(WS-CTR-G).                   
           MOVE NUM-HSHLD-CTZNSP OF DCLGRANT-ASMNT                              
                             TO COM73-HSHLD-CTZNSP(WS-CTR-G).                   
           MOVE NUM-WRKNG-CHILD-ASMNT OF DCLGRANT-ASMNT                         
                             TO COM73-WRKNG-CHILD-ASMNT(WS-CTR-G).              
           MOVE QTY-MONTH-WORK OF DCLGRANT-ASMNT                                
                             TO COM73-QTY-MONTH-WORK(WS-CTR-G).                 
           MOVE NUM-APPEAL-ATCHMT OF DCLGRANT-ASMNT                             
                             TO COM73-APPEAL-ATCHMT(WS-CTR-G).                  
           MOVE NUM-PARENT-ADRS-BLDNG OF DCLGRANT-ASMNT                         
                             TO COM73-PARENT-ADRS-BLDNG(WS-CTR-G).              
           MOVE NUM-PARENT-ADRS-LEVEL OF DCLGRANT-ASMNT                         
                             TO COM73-PARENT-ADRS-LEVEL(WS-CTR-G).              
           MOVE NUM-PARENT-ADRS-UNIT OF DCLGRANT-ASMNT                          
                             TO COM73-PARENT-ADRS-UNIT(WS-CTR-G).               
           MOVE TXT-PARENT-ADRS1 OF DCLGRANT-ASMNT                              
                             TO COM73-PARENT-ADRS1(WS-CTR-G).                   
           MOVE TXT-PARENT-ADRS2 OF DCLGRANT-ASMNT                              
                             TO COM73-PARENT-ADRS2(WS-CTR-G).                   
           MOVE NUM-PARENT-ADRS-POSTAL OF DCLGRANT-ASMNT                        
                             TO COM73-PARENT-ADRS-POSTAL(WS-CTR-G).             
           MOVE DTE-PYMNT OF DCLGRANT-ASMNT                                     
                             TO COM73-DTE-PYMNT(WS-CTR-G).                      
           MOVE DTE-CPF-ACK-GRANT-RCV OF DCLGRANT-ASMNT                         
                           TO COM73-DTE-CPF-ACK-GRANT-RCV(WS-CTR-G).            
           MOVE NUM-GRANT-CREDIT OF DCLGRANT-ASMNT                              
                             TO COM73-GRANT-CREDIT(WS-CTR-G).                   
           MOVE TME-APNTMT OF DCLGRANT-ASMNT                                    
                             TO COM73-TME-APNTMT(WS-CTR-G).                     
           MOVE DTE-APNTMT OF DCLGRANT-ASMNT                                    
                             TO COM73-DTE-APNTMT(WS-CTR-G).                     
           MOVE NUM-ACNT OF DCLGRANT-ASMNT                                      
                             TO COM73-NUM-ACNT(WS-CTR-G).                       
           MOVE NUM-SCHEME OF DCLGRANT-ASMNT                                    
                             TO COM73-NUM-SCHEME(WS-CTR-G).                     
           MOVE NUM-TNCY OF DCLGRANT-ASMNT                                      
                             TO COM73-NUM-TNCY(WS-CTR-G).                       
           MOVE NUM-HALF-GRANT OF DCLGRANT-ASMNT                                
                             TO COM73-NUM-HALF-GRANT(WS-CTR-G).                 
                                                                                
       1250-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       1300-CLOSE-CURSOR.                                                       
      *---------------------------------------------------------------*         
           INITIALIZE SQLCODE.                                                  
           MOVE ZEROES TO COM73-CDE-SYSERR.                                     
                                                                                
      *-----------------------------------*                                     
      * CLOSE CURSOR - GRANT ASSESSMENT   *                                     
      *-----------------------------------*                                     
           EXEC SQL CLOSE CURSOR1                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN 0                                                            
                   CONTINUE                                                     
              WHEN OTHER                                                        
                MOVE SQLCODE TO WS-SQL-CODE COM73-CDE-ERROR                     
                MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                      
                IF SQLCODE < ZEROES                                             
                   MOVE '-'             TO COM73-CDE-ERROR(1:1)                 
                END-IF                                                          
                MOVE '(CB4A) CLOSE CURSOR ERROR' TO COM73-ERR-MSG               
                GO TO 9000-ABORT-PROCESS                                        
           END-EVALUATE.                                                        
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2000-RETRIEVE-PERSON.                                                    
      *================================================================*        
           PERFORM 2100-OPEN-CURSOR2    THRU 2100-EXIT.                         
           PERFORM 2200-FETCH-CURSOR2   THRU 2200-EXIT                          
                   VARYING WS-CTR2 FROM 1 BY 1                                  
                   UNTIL (WS-CTR2 > 10) OR (SQLCODE = WS-SQL-NOT-FOUND)         
                      OR (WS-CURSOR2-EOF) = 'Y'.                                
           PERFORM 2300-CLOSE-CURSOR2   THRU 2300-EXIT.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2100-OPEN-CURSOR2.                                                       
      *================================================================*        
           INITIALIZE SQLCODE.                                                  
           MOVE ZEROES                TO COM73-CDE-SYSERR.                      
           MOVE 'N'                   TO WS-CURSOR2-EOF.                        
           MOVE ZEROES                TO WS-CTR-P                               
                                         WS-TOT-GRANT-AMT-SUM                   
                                         WS-TOT-AMT-RECOVERY                    
                                         WS-TOT-EX-GRANT-AMT-SUM                
                                         WS-TOT-EX-AMT-RECOVERY.                
                                                                                
           MOVE COM73-NUM-RGSTRN-INPUT                                          
                           TO NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON.              
                                                                                
           MOVE WS-NUM-GRANT-TYPE                                               
                           TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON.          
                                                                                
      *------------------------------------------*                              
      *  DECLARE CURSOR - GRANT ASSESSMENT PERSON                               
      *------------------------------------------*                              
           EXEC SQL                                                             
             DECLARE CURSOR2 CURSOR FOR                                         
               SELECT NUM_RGSTRN,                                               
                      NUM_GRANT_TYPE,                                           
                      NUM_IDNTY_NO,                                             
                      NUM_IDNTY_NO_TYPE,                                        
                      NUM_CSTMR_SOURCE,                                         
                      NUM_CPF_ACNT,                                             
                      AMT_GRANT,                                                
                      AMT_GRANT_USE,                                            
                      NUM_ROLE_TAG,                                             
                      NUM_RLTNSP,                                               
                      NME_PERSON,                                               
                      NUM_GENDER,                                               
                      NUM_CTZNSP,                                               
                      NUM_MRTL_STATUS,                                          
                      AMT_INCOME,                                               
                      NUM_1YR_CNTNS_EMPLY,                                      
                      NUM_DEBAR,                                                
                      NUM_DEBAR_REASON,                                         
                      NUM_RCVRY,                                                
                      AMT_GRANT_THRSHD,                                         
                      AMT_GRANT_EXCESS                                          
               FROM                                                             
                 GRANT_ASMNT_PERSON                                             
               WHERE                                                            
                 NUM_RGSTRN = :DCLGRANT-ASMNT-PERSON.NUM-RGSTRN AND             
                 NUM_GRANT_TYPE = :DCLGRANT-ASMNT-PERSON.NUM-GRANT-TYPE         
               ORDER BY NUM_IDNTY_NO                                            
           END-EXEC.                                                            
                                                                                
      *----------------------------------------*                                
      *  OPEN CURSOR - GRANT ASSESSMENT PERSON *                                
      *----------------------------------------*                                
           EXEC SQL                                                             
             OPEN CURSOR2                                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 0                                                               
             CONTINUE                                                           
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM73-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE '(CB4A) OPEN CURSOR2 ERROR' TO COM73-ERR-MSG                  
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       2200-FETCH-CURSOR2.                                                      
      *================================================================*        
                                                                                
           EXEC SQL                                                             
             FETCH CURSOR2                                                      
               INTO                                                             
                 :DCLGRANT-ASMNT-PERSON.NUM-RGSTRN,                             
                 :DCLGRANT-ASMNT-PERSON.NUM-GRANT-TYPE,                         
                 :DCLGRANT-ASMNT-PERSON.NUM-IDNTY-NO,                           
                 :DCLGRANT-ASMNT-PERSON.NUM-IDNTY-NO-TYPE,                      
                 :DCLGRANT-ASMNT-PERSON.NUM-CSTMR-SOURCE,                       
                 :DCLGRANT-ASMNT-PERSON.NUM-CPF-ACNT,                           
                 :DCLGRANT-ASMNT-PERSON.AMT-GRANT,                              
                 :DCLGRANT-ASMNT-PERSON.AMT-GRANT-USE,                          
                 :DCLGRANT-ASMNT-PERSON.NUM-ROLE-TAG,                           
                 :DCLGRANT-ASMNT-PERSON.NUM-RLTNSP,                             
                 :DCLGRANT-ASMNT-PERSON.NME-PERSON,                             
                 :DCLGRANT-ASMNT-PERSON.NUM-GENDER,                             
                 :DCLGRANT-ASMNT-PERSON.NUM-CTZNSP,                             
                 :DCLGRANT-ASMNT-PERSON.NUM-MRTL-STATUS,                        
                 :DCLGRANT-ASMNT-PERSON.AMT-INCOME,                             
                 :DCLGRANT-ASMNT-PERSON.NUM-1YR-CNTNS-EMPLY,                    
                 :DCLGRANT-ASMNT-PERSON.NUM-DEBAR,                              
                 :DCLGRANT-ASMNT-PERSON.NUM-DEBAR-REASON,                       
                 :DCLGRANT-ASMNT-PERSON.NUM-RCVRY,                              
                 :DCLGRANT-ASMNT-PERSON.AMT-GRANT-THRSHD,                       
                 :DCLGRANT-ASMNT-PERSON.AMT-GRANT-EXCESS                        
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-FOUND                                                    
             ADD 1 TO WS-CTR-P                                                  
             PERFORM 2250-MOVE-GRANT-ASMNT-PERSON THRU 2250-EXIT                
           WHEN WS-SQL-NOT-FOUND                                                
             MOVE 'Y'                  TO WS-CURSOR2-EOF                        
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM73-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE '(CB4A) FETCH CURSOR ERROR' TO COM73-ERR-MSG                  
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       2250-MOVE-GRANT-ASMNT-PERSON.                                            
      *---------------------------------------------------------------*         
           MOVE NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON TO WS-NUM-RGSTRN.           
           MOVE NUM-IDNTY-NO OF DCLGRANT-ASMNT-PERSON                           
                       TO COM73-IDNTY-NO(WS-CTR-G WS-CTR-P)                     
                          WS-NUM-IDNTY-NO.                                      
           MOVE NUM-IDNTY-NO-TYPE OF DCLGRANT-ASMNT-PERSON              .       
                       TO COM73-IDNTY-NO-TYPE(WS-CTR-G WS-CTR-P).       .       
           MOVE NUM-CSTMR-SOURCE OF DCLGRANT-ASMNT-PERSON                       
                       TO COM73-CSTMR-SOURCE(WS-CTR-G WS-CTR-P).                
           MOVE NUM-CPF-ACNT OF DCLGRANT-ASMNT-PERSON                           
                       TO COM73-CPF-ACNT(WS-CTR-G WS-CTR-P).                    
           MOVE AMT-GRANT OF DCLGRANT-ASMNT-PERSON                              
                       TO COM73-AMT-GRANT(WS-CTR-G WS-CTR-P)                    
                          WS-AMT-GRANT                                          
           MOVE AMT-GRANT-USE OF DCLGRANT-ASMNT-PERSON                          
                       TO COM73-AMT-GRANT-USE(WS-CTR-G WS-CTR-P).               
           MOVE NUM-ROLE-TAG OF DCLGRANT-ASMNT-PERSON                           
                       TO COM73-ROLE-TAG(WS-CTR-G WS-CTR-P).                    
           MOVE NUM-RLTNSP OF DCLGRANT-ASMNT-PERSON                             
                       TO COM73-RLTNSP(WS-CTR-G WS-CTR-P).                      
           MOVE NME-PERSON OF DCLGRANT-ASMNT-PERSON                             
                       TO COM73-NME-PERSON(WS-CTR-G WS-CTR-P).                  
           MOVE NUM-GENDER OF DCLGRANT-ASMNT-PERSON                             
                       TO COM73-GENDER(WS-CTR-G WS-CTR-P).                      
           MOVE NUM-CTZNSP OF DCLGRANT-ASMNT-PERSON                             
                       TO COM73-CTZNSP(WS-CTR-G WS-CTR-P).                      
           MOVE NUM-MRTL-STATUS OF DCLGRANT-ASMNT-PERSON                        
                       TO COM73-MRTL-STATUS(WS-CTR-G WS-CTR-P).                 
           MOVE AMT-INCOME OF DCLGRANT-ASMNT-PERSON                             
                       TO COM73-AMT-INCOME(WS-CTR-G WS-CTR-P).                  
           MOVE NUM-1YR-CNTNS-EMPLY OF DCLGRANT-ASMNT-PERSON                    
                       TO COM73-1YR-CNTNS-EMPLY(WS-CTR-G WS-CTR-P).             
           MOVE NUM-DEBAR OF DCLGRANT-ASMNT-PERSON                              
                       TO COM73-DEBAR(WS-CTR-G WS-CTR-P).                       
           MOVE NUM-DEBAR-REASON OF DCLGRANT-ASMNT-PERSON                       
                       TO COM73-DEBAR-REASON(WS-CTR-G WS-CTR-P).                
           MOVE NUM-RCVRY OF DCLGRANT-ASMNT-PERSON                              
                       TO COM73-RCVRY(WS-CTR-G WS-CTR-P).                       
           MOVE AMT-GRANT-THRSHD OF DCLGRANT-ASMNT-PERSON                       
                       TO COM73-AMT-GRANT-THRSHD(WS-CTR-G WS-CTR-P).            
                                                                                
           MOVE AMT-GRANT-EXCESS OF DCLGRANT-ASMNT-PERSON                       
                       TO COM73-AMT-GRANT-EXCESS(WS-CTR-G WS-CTR-P).            
                                                                                
           PERFORM 3000-GET-RECOVERY-AMT THRU 3000-EXIT.                        
           MOVE WS-AMT-RECOVERY TO COM73-AMT-RCVRY(WS-CTR-G WS-CTR-P).          
                                                                                
           MOVE WS-AMT-RECOVERY-INT                                             
                           TO COM73-AMT-RCVRY-INTRST(WS-CTR-G WS-CTR-P).        
           PERFORM 3500-GET-EX-RECOVERY-AMT THRU 3500-EXIT.                     
           MOVE WS-EX-GRANT-AMT-SUM                                             
                               TO COM73-AMT-EX-GRANT(WS-CTR-G WS-CTR-P).        
           ADD WS-AMT-GRANT        TO WS-TOT-GRANT-AMT-SUM.                     
           ADD WS-AMT-RECOVERY     TO WS-TOT-AMT-RECOVERY.                      
                                                                                
       2250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       2300-CLOSE-CURSOR2.                                                      
      *---------------------------------------------------------------*         
           INITIALIZE SQLCODE.                                                  
           MOVE ZEROES TO COM73-CDE-SYSERR.                                     
                                                                                
      *-----------------------------------*                                     
      * CLOSE CURSOR - GRANT ASSESSMENT   *                                     
      *-----------------------------------*                                     
           EXEC SQL CLOSE CURSOR2                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN 0                                                            
                   CONTINUE                                                     
              WHEN OTHER                                                        
                MOVE SQLCODE TO WS-SQL-CODE COM73-CDE-ERROR                     
                MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                      
                IF SQLCODE < ZEROES                                             
                   MOVE '-'             TO COM73-CDE-ERROR(1:1)                 
                END-IF                                                          
                MOVE '(CB4A) CLOSE CURSOR ERROR' TO COM73-ERR-MSG               
                GO TO 9000-ABORT-PROCESS                                        
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-GET-RECOVERY-AMT.                                                   
      *---------------------------------------------------------------*         
            MOVE ZEROES TO WS-AMT-RECOVERY                                      
                           WS-AMT-RECOVERY-INT.                                 
                                                                                
            EXEC SQL                                                            
               SELECT SUM(T2.AMT_GRANT_RCVR),                                   
                      SUM(T2.AMT_GRANT_RCVR_INTRST)                             
               INTO   :WS-AMT-RECOVERY:WS-AMT-RECOVERY-INDNULL,                 
                      :WS-AMT-RECOVERY-INT:WS-AMT-RECOVERY-INT-INDNULL          
               FROM                                                             
                 GRANT_ASMNT_PERSON T1,                                         
                 GRANT_RCVRY T2                                                 
               WHERE                                                            
                 T1.NUM_RGSTRN     = :WS-NUM-RGSTRN          AND                
                 T1.NUM_GRANT_TYPE = :WS-NUM-GRANT-TYPE      AND                
                 T1.NUM_IDNTY_NO   = :WS-NUM-IDNTY-NO        AND                
                 T1.NUM_RGSTRN     = T2.NUM_RGSTRN           AND                
                 T1.NUM_GRANT_TYPE = T2.NUM_GRANT_TYPE       AND                
                 T1.NUM_IDNTY_NO   = T2.NUM_IDNTY_NO         AND                
                 T1.NUM_IDNTY_NO_TYPE = T2.NUM_IDNTY_NO_TYPE                    
            END-EXEC.                                                           
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-FOUND                                                    
             CONTINUE                                                           
           WHEN WS-SQL-NOT-FOUND                                                
             MOVE ZEROES             TO WS-GRANT-AMT-SUM                        
                                        WS-AMT-RECOVERY                         
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM73-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE 'READ GRANT-RCVRY ERROR' TO COM73-ERR-MSG                     
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3500-GET-EX-RECOVERY-AMT.                                                
      *---------------------------------------------------------------*         
            MOVE ZEROES TO WS-EX-GRANT-AMT-SUM                                  
                           WS-EX-AMT-RECOVERY.                                  
                                                                                
            EXEC SQL                                                            
               SELECT SUM(T1.AMT_GRANT),                                        
                      SUM(T2.AMT_GRANT_RCVR)                                    
               INTO   :WS-EX-GRANT-AMT-SUM:WS-EX-GRANT-AMT-SUM-INDNULL,         
                      :WS-EX-AMT-RECOVERY:WS-EX-AMT-RECOVERY-INDNULL            
               FROM                                                             
                 EX_GRANT_ASMNT_PERSON T1,                                      
                 GRANT_RCVRY T2,                                                
                 EX_GRANT_ASMNT T3                                              
               WHERE                                                            
                 T1.NUM_RGSTRN     = :WS-NUM-RGSTRN          AND                
                 T1.NUM_GRANT_TYPE = :WS-NUM-GRANT-TYPE      AND                
                 T1.NUM_IDNTY_NO   = :WS-NUM-IDNTY-NO        AND                
                 T1.NUM_RGSTRN     = T2.NUM_RGSTRN           AND                
                 T1.NUM_RGSTRN     = T3.NUM_RGSTRN           AND                
                 T1.NUM_GRANT_TYPE = T2.NUM_GRANT_TYPE       AND                
                 T1.NUM_GRANT_TYPE = T3.NUM_GRANT_TYPE       AND                
                 T1.NUM_IDNTY_NO   = T2.NUM_IDNTY_NO         AND                
                 T1.NUM_IDNTY_NO_TYPE = T2.NUM_IDNTY_NO_TYPE AND                
                 T1.TME_CREATE = T2.TME_CREATE AND                              
                 T1.TME_CREATE = T3.TME_CREATE AND                              
                 T3.DTE_PYMNT NOT IN ('0001-01-01')                             
            END-EXEC.                                                           
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-FOUND                                                    
             CONTINUE                                                           
           WHEN WS-SQL-NOT-FOUND                                                
             MOVE ZEROES             TO WS-EX-GRANT-AMT-SUM                     
                                        WS-AMT-RECOVERY                         
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM73-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE 'READ EX-GRANT-RCVRY ERROR' TO COM73-ERR-MSG                  
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-GET-EX-RCVRY-AMT-TOTAL.                                             
      *---------------------------------------------------------------*         
            MOVE ZEROES TO WS-TOT-EX-GRANT-AMT-SUM                              
                           WS-TOT-EX-AMT-RECOVERY.                              
                                                                                
            EXEC SQL                                                            
               SELECT SUM(T2.AMT_GRANT),                                        
                      SUM(T3.AMT_GRANT_RCVR)                                    
               INTO   :WS-TOT-EX-GRANT-AMT-SUM:WS-EX-GRANT-TOT-INDNULL,         
                      :WS-TOT-EX-AMT-RECOVERY:WS-EX-RCVRY-TOT-INDNULL           
               FROM                                                             
                 EX_GRANT_ASMNT T1,                                             
                 EX_GRANT_ASMNT_PERSON T2,                                      
                 GRANT_RCVRY T3                                                 
               WHERE                                                            
                 T2.NUM_RGSTRN     = :WS-NUM-RGSTRN          AND                
                 T2.NUM_GRANT_TYPE = :WS-NUM-GRANT-TYPE      AND                
                 T1.NUM_RGSTRN     = T2.NUM_RGSTRN           AND                
                 T2.NUM_RGSTRN     = T3.NUM_RGSTRN           AND                
                 T1.NUM_GRANT_TYPE = T2.NUM_GRANT_TYPE       AND                
                 T2.NUM_GRANT_TYPE = T3.NUM_GRANT_TYPE       AND                
                 T2.NUM_IDNTY_NO   = T3.NUM_IDNTY_NO         AND                
                 T2.NUM_IDNTY_NO_TYPE = T3.NUM_IDNTY_NO_TYPE AND                
                 T1.TME_CREATE = T1.TME_CREATE               AND                
                 T2.TME_CREATE = T3.TME_CREATE               AND                
                 T1.DTE_PYMNT NOT IN ('0001-01-01')                             
            END-EXEC.                                                           
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-FOUND                                                    
             CONTINUE                                                           
           WHEN WS-SQL-NOT-FOUND                                                
             MOVE ZEROES             TO WS-TOT-EX-GRANT-AMT-SUM                 
                                        WS-TOT-EX-AMT-RECOVERY                  
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM73-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE 'READ EX-GRANT-RCVRY ERROR' TO COM73-ERR-MSG                  
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       5000-READ-EX-GRANT-ASMNT.                                                
      *-----------------------*                                                 
           MOVE ZEROES TO WS-AMT-GRANT                                          
                          WS-GRANT-AMT-SUM                                      
                          WS-AMT-RECOVERY                                       
                          WS-AMT-RECOVERY-INT                                   
                          WS-EX-AMT-RECOVERY                                    
                          WS-EX-GRANT-AMT-SUM                                   
                          WS-TOT-GRANT-AMT-SUM                                  
                          WS-TOT-AMT-RECOVERY                                   
                          WS-TOT-EX-AMT-RECOVERY                                
                          WS-TOT-EX-GRANT-AMT-SUM.                              
           MOVE 'N' TO WS-CURSOR3-EOF.                                          
           PERFORM 5100-OPEN-CURSOR     THRU 5100-EXIT.                         
           PERFORM 5200-FETCH-CURSOR    THRU 5200-EXIT                          
                   VARYING WS-CTR1 FROM 1 BY 1                                  
                   UNTIL (SQLCODE = WS-SQL-NOT-FOUND)                           
                      OR (WS-CURSOR3-EOF) = 'Y'.                                
           PERFORM 5300-CLOSE-CURSOR    THRU 5300-EXIT.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       5100-OPEN-CURSOR.                                                        
      *================================================================*        
           INITIALIZE SQLCODE.                                                  
           MOVE ZEROES                TO COM73-CDE-SYSERR.                      
           MOVE ZEROES                TO WS-CTR-G.                              
           IF COM73-NUM-RGSTRN-INPUT = SPACES OR LOW-VALUES                     
              MOVE 'E500'           TO COM73-CDE-ERROR                          
              MOVE 'REGNO IS BLANK' TO COM73-ERR-MSG                            
              GO TO 9100-GOBACK                                                 
           ELSE                                                                 
              MOVE COM73-NUM-RGSTRN-INPUT                                       
                           TO NUM-RGSTRN OF DCLGRANT-ASMNT                      
           END-IF.                                                              
                                                                                
      *-------------------------------------*                                   
      *  DECLARE CURSOR - EX GRANT ASSESSMENT  *                                
      *-------------------------------------*                                   
           EXEC SQL                                                             
             DECLARE CURSOR3 CURSOR FOR                                         
               SELECT NUM_RGSTRN,                                               
                      NUM_GRANT_TYPE,                                           
                      TME_CREATE,                                               
                      NUM_ELGBL_GRANT,                                          
                      NUM_GRANT_AMOUNT_ELGBL,                                   
                      NUM_GRANT_STATUS,                                         
                      DTE_APRV,                                                 
                      DTE_CANCEL,                                               
                      DTE_REJECT,                                               
                      DTE_SUBMIT,                                               
                      DTE_WTHDRW,                                               
                      NUM_RJCTN_REASON,                                         
                      NUM_WTDRWL_REASON,                                        
                      TXT_CNCLTN_REASON,                                        
                      DTE_GRANT_RQST,                                           
                      NUM_GRANT_DOC_RCV,                                        
                      DTE_ACK,                                                  
                      DTE_GRANT_FORM_ISSUE,                                     
                      DTE_GRANT_OPT_OUT_FORM_ISSUE,                             
                      NUM_GRANT_OPT_OUT_REASON,                                 
                      DTE_GRANT_OPT_OUT_REASON,                                 
                      NUM_ALCTN_SCHEME,                                         
                      NUM_HSHLD_CTZNSP,                                         
                      NUM_WRKNG_CHILD_ASMNT,                                    
                      QTY_MONTH_WORK,                                           
                      NUM_APPEAL_ATCHMT,                                        
                      NUM_PARENT_ADRS_BLDNG,                                    
                      NUM_PARENT_ADRS_LEVEL,                                    
                      NUM_PARENT_ADRS_UNIT,                                     
                      TXT_PARENT_ADRS1,                                         
                      TXT_PARENT_ADRS2,                                         
                      NUM_PARENT_ADRS_POSTAL,                                   
                      DTE_PYMNT,                                                
                      DTE_CPF_ACK_GRANT_RCV,                                    
                      NUM_GRANT_CREDIT,                                         
                      TME_APNTMT,                                               
                      DTE_APNTMT,                                               
                      NUM_ACNT,                                                 
                      NUM_SCHEME,                                               
                      NUM_TNCY,                                                 
                      NUM_HALF_GRANT                                            
               FROM                                                             
                 EX_GRANT_ASMNT                                                 
               WHERE                                                            
                 NUM_RGSTRN = :DCLGRANT-ASMNT.NUM-RGSTRN AND                    
                 DTE_PYMNT NOT IN ('0001-01-01')                                
               ORDER BY NUM_GRANT_TYPE                                          
           END-EXEC.                                                            
                                                                                
      *----------------------------------*                                      
      *  OPEN CURSOR - EX GRANT ASSESSMENT                                      
      *----------------------------------*                                      
           EXEC SQL                                                             
             OPEN CURSOR3                                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 0                                                               
             CONTINUE                                                           
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM73-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE '(CB4A) OPEN CURSOR3 ERROR' TO COM73-ERR-MSG                  
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5200-FETCH-CURSOR.                                                       
      *================================================================*        
                                                                                
           EXEC SQL                                                             
             FETCH CURSOR3                                                      
               INTO                                                             
                 :DCLEX-GRANT-ASMNT.NUM-RGSTRN,                                 
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-TYPE,                             
                 :DCLEX-GRANT-ASMNT.TME-CREATE,                                 
                 :DCLEX-GRANT-ASMNT.NUM-ELGBL-GRANT,                            
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-AMOUNT-ELGBL,                     
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-STATUS,                           
                 :DCLEX-GRANT-ASMNT.DTE-APRV,                                   
                 :DCLEX-GRANT-ASMNT.DTE-CANCEL,                                 
                 :DCLEX-GRANT-ASMNT.DTE-REJECT,                                 
                 :DCLEX-GRANT-ASMNT.DTE-SUBMIT,                                 
                 :DCLEX-GRANT-ASMNT.DTE-WTHDRW,                                 
                 :DCLEX-GRANT-ASMNT.NUM-RJCTN-REASON,                           
                 :DCLEX-GRANT-ASMNT.NUM-WTDRWL-REASON,                          
                 :DCLEX-GRANT-ASMNT.TXT-CNCLTN-REASON,                          
                 :DCLEX-GRANT-ASMNT.DTE-GRANT-RQST,                             
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-DOC-RCV,                          
                 :DCLEX-GRANT-ASMNT.DTE-ACK,                                    
                 :DCLEX-GRANT-ASMNT.DTE-GRANT-FORM-ISSUE,                       
                 :DCLEX-GRANT-ASMNT.DTE-GRANT-OPT-OUT-FORM-ISSUE,               
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-OPT-OUT-REASON,                   
                 :DCLEX-GRANT-ASMNT.DTE-GRANT-OPT-OUT-REASON,                   
                 :DCLEX-GRANT-ASMNT.NUM-ALCTN-SCHEME,                           
                 :DCLEX-GRANT-ASMNT.NUM-HSHLD-CTZNSP,                           
                 :DCLEX-GRANT-ASMNT.NUM-WRKNG-CHILD-ASMNT,                      
                 :DCLEX-GRANT-ASMNT.QTY-MONTH-WORK,                             
                 :DCLEX-GRANT-ASMNT.NUM-APPEAL-ATCHMT,                          
                 :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-BLDNG,                      
                 :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-LEVEL,                      
                 :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-UNIT,                       
                 :DCLEX-GRANT-ASMNT.TXT-PARENT-ADRS1,                           
                 :DCLEX-GRANT-ASMNT.TXT-PARENT-ADRS2,                           
                 :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-POSTAL,                     
                 :DCLEX-GRANT-ASMNT.DTE-PYMNT,                                  
                 :DCLEX-GRANT-ASMNT.DTE-CPF-ACK-GRANT-RCV,                      
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-CREDIT,                           
                 :DCLEX-GRANT-ASMNT.TME-APNTMT,                                 
                 :DCLEX-GRANT-ASMNT.DTE-APNTMT,                                 
                 :DCLEX-GRANT-ASMNT.NUM-ACNT,                                   
                 :DCLEX-GRANT-ASMNT.NUM-SCHEME,                                 
                 :DCLEX-GRANT-ASMNT.NUM-TNCY,                                   
                 :DCLEX-GRANT-ASMNT.NUM-HALF-GRANT                              
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-FOUND                                                    
             IF NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT                             
                                      >= COM73-NUM-NEXT-NUM-GRANT               
                ADD  1 TO WS-CTR-G                                              
                MOVE 0 TO WS-CTR-P                                              
                IF WS-CTR-G = 6                                                 
                   MOVE NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT                     
                                       TO COM73-NUM-NEXT-NUM-GRANT              
                   MOVE 'Y'                  TO WS-CURSOR3-EOF                  
                   MOVE 0 TO WS-CTR-G                                           
                ELSE                                                            
                   MOVE SPACES TO COM73-NUM-NEXT-NUM-GRANT                      
                   PERFORM 5250-MOVE-EX-GRANT-ASMNT THRU 5250-EXIT              
                   PERFORM 6000-RETRIEVE-EX-PERSON  THRU 6000-EXIT              
                   PERFORM 4000-GET-EX-RCVRY-AMT-TOTAL THRU 4000-EXIT           
                   MOVE WS-TOT-GRANT-AMT-SUM                                    
                                       TO COM73-SUM-AMT-GRANT(WS-CTR-G)         
                   MOVE WS-TOT-AMT-RECOVERY                                     
                                       TO COM73-SUM-AMT-RCVRY(WS-CTR-G)         
                   MOVE WS-TOT-EX-GRANT-AMT-SUM                                 
                                    TO COM73-SUM-EX-AMT-GRANT(WS-CTR-G)         
                   MOVE WS-TOT-EX-AMT-RECOVERY                                  
                                    TO COM73-SUM-EX-AMT-RCVRY(WS-CTR-G)         
                END-IF                                                          
             END-IF                                                             
           WHEN WS-SQL-NOT-FOUND                                                
             MOVE 'Y'                  TO WS-CURSOR3-EOF                        
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM73-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE '(CB4A) FETCH CURSOR ERROR' TO COM73-ERR-MSG                  
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       5250-MOVE-EX-GRANT-ASMNT.                                                
      *---------------------------------------------------------------*         
           MOVE NUM-RGSTRN OF DCLEX-GRANT-ASMNT                                 
                             TO COM73-NUM-RGSTRN(WS-CTR-G).                     
           MOVE NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT                             
                             TO COM73-NUM-GRANT-TYPE(WS-CTR-G)                  
                                WS-NUM-GRANT-TYPE.                              
           MOVE TME-CREATE OF DCLEX-GRANT-ASMNT TO WS-TME-CREATE.               
           MOVE NUM-ELGBL-GRANT OF DCLEX-GRANT-ASMNT                            
                             TO COM73-ELGBL-GRANT(WS-CTR-G).                    
           MOVE NUM-GRANT-AMOUNT-ELGBL OF DCLEX-GRANT-ASMNT                     
                             TO COM73-GRANT-AMOUNT-ELGBL(WS-CTR-G).             
           MOVE NUM-GRANT-STATUS OF DCLEX-GRANT-ASMNT                           
                             TO COM73-GRANT-STATUS(WS-CTR-G).                   
           MOVE DTE-APRV OF DCLEX-GRANT-ASMNT                                   
                             TO COM73-DTE-APRV(WS-CTR-G).                       
           MOVE DTE-CANCEL OF DCLEX-GRANT-ASMNT                                 
                             TO COM73-DTE-CANCEL(WS-CTR-G).                     
           MOVE DTE-REJECT OF DCLEX-GRANT-ASMNT                                 
                             TO COM73-DTE-REJECT(WS-CTR-G).                     
           MOVE DTE-SUBMIT OF DCLEX-GRANT-ASMNT                                 
                             TO COM73-DTE-SUBMIT(WS-CTR-G).                     
           MOVE DTE-WTHDRW OF DCLEX-GRANT-ASMNT                                 
                             TO COM73-DTE-WTHDRW(WS-CTR-G).                     
           MOVE NUM-RJCTN-REASON OF DCLEX-GRANT-ASMNT                           
                             TO COM73-RJCTN-REASON(WS-CTR-G).                   
           MOVE NUM-WTDRWL-REASON OF DCLEX-GRANT-ASMNT                          
                             TO COM73-WTDRWL-REASON(WS-CTR-G).                  
           MOVE TXT-CNCLTN-REASON OF DCLEX-GRANT-ASMNT                          
                             TO COM73-CNCLTN-REASON(WS-CTR-G).                  
           MOVE DTE-GRANT-RQST OF DCLEX-GRANT-ASMNT                             
                             TO COM73-DTE-GRANT-RQST(WS-CTR-G).                 
           MOVE NUM-GRANT-DOC-RCV OF DCLEX-GRANT-ASMNT                          
                             TO COM73-GRANT-DOC-RCV(WS-CTR-G).                  
           MOVE DTE-ACK OF DCLEX-GRANT-ASMNT                                    
                             TO COM73-DTE-ACK(WS-CTR-G).                        
           MOVE DTE-GRANT-FORM-ISSUE OF DCLEX-GRANT-ASMNT                       
                             TO COM73-DTE-GRANT-ISSUE(WS-CTR-G).                
           MOVE DTE-GRANT-OPT-OUT-FORM-ISSUE OF DCLEX-GRANT-ASMNT               
                             TO COM73-DTE-OPT-OUT-ISSUE(WS-CTR-G).              
           MOVE NUM-GRANT-OPT-OUT-REASON OF DCLEX-GRANT-ASMNT                   
                             TO COM73-OPT-OUT-REASON(WS-CTR-G).                 
           MOVE DTE-GRANT-OPT-OUT-REASON OF DCLEX-GRANT-ASMNT                   
                             TO COM73-DTE-OPT-OUT-REASON(WS-CTR-G).             
           MOVE NUM-ALCTN-SCHEME OF DCLEX-GRANT-ASMNT                           
                             TO COM73-ALCTN-SCHEME(WS-CTR-G).                   
           MOVE NUM-HSHLD-CTZNSP OF DCLEX-GRANT-ASMNT                           
                             TO COM73-HSHLD-CTZNSP(WS-CTR-G).                   
           MOVE NUM-WRKNG-CHILD-ASMNT OF DCLEX-GRANT-ASMNT                      
                             TO COM73-WRKNG-CHILD-ASMNT(WS-CTR-G).              
           MOVE QTY-MONTH-WORK OF DCLEX-GRANT-ASMNT                             
                             TO COM73-QTY-MONTH-WORK(WS-CTR-G).                 
           MOVE NUM-APPEAL-ATCHMT OF DCLEX-GRANT-ASMNT                          
                             TO COM73-APPEAL-ATCHMT(WS-CTR-G).                  
           MOVE NUM-PARENT-ADRS-BLDNG OF DCLEX-GRANT-ASMNT                      
                             TO COM73-PARENT-ADRS-BLDNG(WS-CTR-G).              
           MOVE NUM-PARENT-ADRS-LEVEL OF DCLEX-GRANT-ASMNT                      
                             TO COM73-PARENT-ADRS-LEVEL(WS-CTR-G).              
           MOVE NUM-PARENT-ADRS-UNIT OF DCLEX-GRANT-ASMNT                       
                             TO COM73-PARENT-ADRS-UNIT(WS-CTR-G).               
           MOVE TXT-PARENT-ADRS1 OF DCLEX-GRANT-ASMNT                           
                             TO COM73-PARENT-ADRS1(WS-CTR-G).                   
           MOVE TXT-PARENT-ADRS2 OF DCLEX-GRANT-ASMNT                           
                             TO COM73-PARENT-ADRS2(WS-CTR-G).                   
           MOVE NUM-PARENT-ADRS-POSTAL OF DCLEX-GRANT-ASMNT                     
                             TO COM73-PARENT-ADRS-POSTAL(WS-CTR-G).             
           MOVE DTE-PYMNT OF DCLEX-GRANT-ASMNT                                  
                             TO COM73-DTE-PYMNT(WS-CTR-G).                      
           MOVE DTE-CPF-ACK-GRANT-RCV OF DCLEX-GRANT-ASMNT                      
                           TO COM73-DTE-CPF-ACK-GRANT-RCV(WS-CTR-G).            
           MOVE NUM-GRANT-CREDIT OF DCLEX-GRANT-ASMNT                           
                             TO COM73-GRANT-CREDIT(WS-CTR-G).                   
           MOVE TME-APNTMT OF DCLEX-GRANT-ASMNT                                 
                             TO COM73-TME-APNTMT(WS-CTR-G).                     
           MOVE DTE-APNTMT OF DCLEX-GRANT-ASMNT                                 
                             TO COM73-DTE-APNTMT(WS-CTR-G).                     
           MOVE NUM-ACNT OF DCLEX-GRANT-ASMNT                                   
                             TO COM73-NUM-ACNT(WS-CTR-G).                       
           MOVE NUM-SCHEME OF DCLEX-GRANT-ASMNT                                 
                             TO COM73-NUM-SCHEME(WS-CTR-G).                     
           MOVE NUM-TNCY OF DCLEX-GRANT-ASMNT                                   
                             TO COM73-NUM-TNCY(WS-CTR-G).                       
           MOVE NUM-HALF-GRANT OF DCLEX-GRANT-ASMNT                             
                             TO COM73-NUM-HALF-GRANT(WS-CTR-G).                 
                                                                                
       5250-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       5300-CLOSE-CURSOR.                                                       
      *---------------------------------------------------------------*         
           INITIALIZE SQLCODE.                                                  
           MOVE ZEROES TO COM73-CDE-SYSERR.                                     
                                                                                
      *-----------------------------------*                                     
      * CLOSE CURSOR - GRANT ASSESSMENT   *                                     
      *-----------------------------------*                                     
           EXEC SQL CLOSE CURSOR3                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN 0                                                            
                   CONTINUE                                                     
              WHEN OTHER                                                        
                MOVE SQLCODE TO WS-SQL-CODE COM73-CDE-ERROR                     
                MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                      
                IF SQLCODE < ZEROES                                             
                   MOVE '-'             TO COM73-CDE-ERROR(1:1)                 
                END-IF                                                          
                MOVE '(CB4A) CLOSE CURSOR ERROR' TO COM73-ERR-MSG               
                GO TO 9000-ABORT-PROCESS                                        
           END-EVALUATE.                                                        
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       6000-RETRIEVE-EX-PERSON.                                                 
      *================================================================*        
           PERFORM 6100-OPEN-CURSOR4    THRU 6100-EXIT.                         
           PERFORM 6200-FETCH-CURSOR4   THRU 6200-EXIT                          
                   VARYING WS-CTR2 FROM 1 BY 1                                  
                   UNTIL (WS-CTR2 > 10) OR (SQLCODE = WS-SQL-NOT-FOUND)         
                      OR (WS-CURSOR4-EOF) = 'Y'.                                
           PERFORM 6300-CLOSE-CURSOR4   THRU 6300-EXIT.                         
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       6100-OPEN-CURSOR4.                                                       
      *================================================================*        
           INITIALIZE SQLCODE.                                                  
           MOVE ZEROES                TO COM73-CDE-SYSERR.                      
           MOVE 'N'                   TO WS-CURSOR4-EOF.                        
           MOVE ZEROES                TO WS-CTR-P                               
                                         WS-TOT-GRANT-AMT-SUM                   
                                         WS-TOT-AMT-RECOVERY                    
                                         WS-TOT-EX-GRANT-AMT-SUM                
                                         WS-TOT-EX-AMT-RECOVERY.                
                                                                                
           MOVE COM73-NUM-RGSTRN-INPUT                                          
                           TO NUM-RGSTRN OF DCLEX-GRANT-ASMNT-PERSON.           
                                                                                
           MOVE WS-NUM-GRANT-TYPE                                               
                   TO NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT-PERSON.               
                                                                                
           MOVE WS-TME-CREATE                                                   
                   TO TME-CREATE OF DCLEX-GRANT-ASMNT-PERSON.                   
                                                                                
      *------------------------------------------*                              
      *  DECLARE CURSOR - EX GRANT ASSESSMENT PERSON                            
      *------------------------------------------*                              
           EXEC SQL                                                             
             DECLARE CURSOR4 CURSOR FOR                                         
               SELECT NUM_RGSTRN,                                               
                      NUM_GRANT_TYPE,                                           
                      TME_CREATE,                                               
                      NUM_IDNTY_NO,                                             
                      NUM_IDNTY_NO_TYPE,                                        
                      NUM_CSTMR_SOURCE,                                         
                      NUM_CPF_ACNT,                                             
                      AMT_GRANT,                                                
                      AMT_GRANT_USE,                                            
                      NUM_ROLE_TAG,                                             
                      NUM_RLTNSP,                                               
                      NME_PERSON,                                               
                      NUM_GENDER,                                               
                      NUM_CTZNSP,                                               
                      NUM_MRTL_STATUS,                                          
                      AMT_INCOME,                                               
                      NUM_1YR_CNTNS_EMPLY,                                      
                      NUM_DEBAR,                                                
                      NUM_DEBAR_REASON,                                         
                      NUM_RCVRY,                                                
                      AMT_GRANT_THRSHD,                                         
                      AMT_GRANT_EXCESS                                          
               FROM                                                             
                 EX_GRANT_ASMNT_PERSON                                          
               WHERE                                                            
                 NUM_RGSTRN = :DCLEX-GRANT-ASMNT-PERSON.NUM-RGSTRN AND          
                 NUM_GRANT_TYPE =                                               
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-GRANT-TYPE AND          
                 TME_CREATE = :DCLEX-GRANT-ASMNT-PERSON.TME-CREATE              
               ORDER BY NUM_IDNTY_NO                                            
           END-EXEC.                                                            
                                                                                
      *----------------------------------------*                                
      *  OPEN CURSOR - EX GRANT ASSESSMENT PERSON *                             
      *----------------------------------------*                                
           EXEC SQL                                                             
             OPEN CURSOR4                                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 0                                                               
             CONTINUE                                                           
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM73-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE '(CB4A) OPEN CURSOR4 ERROR' TO COM73-ERR-MSG                  
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6200-FETCH-CURSOR4.                                                      
      *================================================================*        
                                                                                
           EXEC SQL                                                             
             FETCH CURSOR4                                                      
               INTO                                                             
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-RGSTRN,                          
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-GRANT-TYPE,                      
                 :DCLEX-GRANT-ASMNT-PERSON.TME-CREATE,                          
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-IDNTY-NO,                        
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-IDNTY-NO-TYPE,                   
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-CSTMR-SOURCE,                    
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-CPF-ACNT,                        
                 :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT,                           
                 :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT-USE,                       
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-ROLE-TAG,                        
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-RLTNSP,                          
                 :DCLEX-GRANT-ASMNT-PERSON.NME-PERSON,                          
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-GENDER,                          
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-CTZNSP,                          
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-MRTL-STATUS,                     
                 :DCLEX-GRANT-ASMNT-PERSON.AMT-INCOME,                          
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-1YR-CNTNS-EMPLY,                 
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-DEBAR,                           
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-DEBAR-REASON,                    
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-RCVRY,                           
                 :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT-THRSHD,                    
                 :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT-EXCESS                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-FOUND                                                    
             ADD 1 TO WS-CTR-P                                                  
             PERFORM 6250-MOVE-GRANT-EX-PERSON THRU 6250-EXIT                   
           WHEN WS-SQL-NOT-FOUND                                                
             MOVE 'Y'                  TO WS-CURSOR4-EOF                        
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM73-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE '(CB4A) FETCH CURSOR ERROR' TO COM73-ERR-MSG                  
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       6200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       6250-MOVE-GRANT-EX-PERSON.                                               
      *---------------------------------------------------------------*         
           MOVE NUM-RGSTRN OF DCLEX-GRANT-ASMNT-PERSON TO WS-NUM-RGSTRN.        
           MOVE NUM-IDNTY-NO OF DCLEX-GRANT-ASMNT-PERSON                        
                       TO COM73-IDNTY-NO(WS-CTR-G WS-CTR-P)                     
                          WS-NUM-IDNTY-NO.                                      
           MOVE NUM-IDNTY-NO-TYPE OF DCLEX-GRANT-ASMNT-PERSON                   
                       TO COM73-IDNTY-NO-TYPE(WS-CTR-G WS-CTR-P).               
           MOVE NUM-CSTMR-SOURCE OF DCLEX-GRANT-ASMNT-PERSON                    
                       TO COM73-CSTMR-SOURCE(WS-CTR-G WS-CTR-P).                
           MOVE NUM-CPF-ACNT OF DCLEX-GRANT-ASMNT-PERSON                        
                       TO COM73-CPF-ACNT(WS-CTR-G WS-CTR-P).                    
      *    MOVE AMT-GRANT OF DCLEX-GRANT-ASMNT-PERSON                           
      *                TO COM73-AMT-GRANT(WS-CTR-G WS-CTR-P)                    
      *                   WS-AMT-GRANT.                                         
           MOVE AMT-GRANT-USE OF DCLEX-GRANT-ASMNT-PERSON                       
                       TO COM73-AMT-GRANT-USE(WS-CTR-G WS-CTR-P).               
           MOVE NUM-ROLE-TAG OF DCLEX-GRANT-ASMNT-PERSON                        
                       TO COM73-ROLE-TAG(WS-CTR-G WS-CTR-P).                    
           MOVE NUM-RLTNSP OF DCLEX-GRANT-ASMNT-PERSON                          
                       TO COM73-RLTNSP(WS-CTR-G WS-CTR-P).                      
           MOVE NME-PERSON OF DCLEX-GRANT-ASMNT-PERSON                          
                       TO COM73-NME-PERSON(WS-CTR-G WS-CTR-P).                  
           MOVE NUM-GENDER OF DCLEX-GRANT-ASMNT-PERSON                          
                       TO COM73-GENDER(WS-CTR-G WS-CTR-P).                      
           MOVE NUM-CTZNSP OF DCLEX-GRANT-ASMNT-PERSON                          
                       TO COM73-CTZNSP(WS-CTR-G WS-CTR-P).                      
           MOVE NUM-MRTL-STATUS OF DCLEX-GRANT-ASMNT-PERSON                     
                       TO COM73-MRTL-STATUS(WS-CTR-G WS-CTR-P).                 
           MOVE AMT-INCOME OF DCLEX-GRANT-ASMNT-PERSON                          
                       TO COM73-AMT-INCOME(WS-CTR-G WS-CTR-P).                  
           MOVE NUM-1YR-CNTNS-EMPLY OF DCLEX-GRANT-ASMNT-PERSON                 
                       TO COM73-1YR-CNTNS-EMPLY(WS-CTR-G WS-CTR-P).             
           MOVE NUM-DEBAR OF DCLEX-GRANT-ASMNT-PERSON                           
                       TO COM73-DEBAR(WS-CTR-G WS-CTR-P).                       
           MOVE NUM-DEBAR-REASON OF DCLEX-GRANT-ASMNT-PERSON                    
                       TO COM73-DEBAR-REASON(WS-CTR-G WS-CTR-P).                
           MOVE NUM-RCVRY OF DCLEX-GRANT-ASMNT-PERSON                           
                       TO COM73-RCVRY(WS-CTR-G WS-CTR-P).                       
           MOVE AMT-GRANT-THRSHD OF DCLEX-GRANT-ASMNT-PERSON                    
                       TO COM73-AMT-GRANT-THRSHD(WS-CTR-G WS-CTR-P).            
                                                                                
           MOVE AMT-GRANT-EXCESS OF DCLEX-GRANT-ASMNT-PERSON                    
                       TO COM73-AMT-GRANT-EXCESS(WS-CTR-G WS-CTR-P).            
                                                                                
      *    PERFORM 3000-GET-RECOVERY-AMT THRU 3000-EXIT.                        
      *    MOVE WS-AMT-RECOVERY TO COM73-AMT-RCVRY(WS-CTR-G WS-CTR-P).          
      *                                                                         
      *    MOVE WS-AMT-RECOVERY-INT                                             
      *                    TO COM73-AMT-RCVRY-INTRST(WS-CTR-G WS-CTR-P).        
      *    ADD WS-AMT-GRANT        TO WS-TOT-GRANT-AMT-SUM.                     
                                                                                
           PERFORM 3500-GET-EX-RECOVERY-AMT THRU 3500-EXIT.                     
           MOVE WS-EX-AMT-RECOVERY                                              
                                 TO COM73-AMT-RCVRY(WS-CTR-G WS-CTR-P).         
           MOVE WS-EX-GRANT-AMT-SUM                                             
                               TO COM73-AMT-EX-GRANT(WS-CTR-G WS-CTR-P).        
           ADD WS-EX-AMT-RECOVERY  TO WS-TOT-AMT-RECOVERY.                      
                                                                                
       6250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       6300-CLOSE-CURSOR4.                                                      
      *---------------------------------------------------------------*         
           INITIALIZE SQLCODE.                                                  
           MOVE ZEROES TO COM73-CDE-SYSERR.                                     
                                                                                
      *-----------------------------------*                                     
      * CLOSE CURSOR - GRANT ASSESSMENT   *                                     
      *-----------------------------------*                                     
           EXEC SQL CLOSE CURSOR4                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN 0                                                            
                   CONTINUE                                                     
              WHEN OTHER                                                        
                MOVE SQLCODE TO WS-SQL-CODE COM73-CDE-ERROR                     
                MOVE WS-SQL-CODE-X      TO COM73-CDE-ERROR                      
                IF SQLCODE < ZEROES                                             
                   MOVE '-'             TO COM73-CDE-ERROR(1:1)                 
                END-IF                                                          
                MOVE '(CB4A) CLOSE CURSOR ERROR' TO COM73-ERR-MSG               
                GO TO 9000-ABORT-PROCESS                                        
           END-EVALUATE.                                                        
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       9000-ABORT-PROCESS.                                                      
      *-----------------------*                                                 
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE       TO COM73-CDE-SYSERR.                              
           MOVE SQLCODE       TO RETURN-CODE.                                   
           DISPLAY 'CB4A - SYSERR = ' COM73-CDE-SYSERR.                         
           GOBACK.                                                              
                                                                                
      *-----------------------*                                                 
       9100-GOBACK.                                                             
      *-----------------------*                                                 
                                                                                
           GOBACK.                                                              
                                                                                
       9100-EXIT.                                                               
            EXIT.                                                               
