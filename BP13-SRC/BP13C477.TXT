       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C477.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  03/04/2017.                                               
      * ========================================================== *            
      * OBJECTIVE    : DAILY PROGRAM TO WRITE SENT CPF CASES TO    *            
      *                CPF PENDING FILE                            *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F600                               *            
      * I-O    FILES :  1.) BP13K60F                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136755 KSJ3 03/04/2017 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F600  ASSIGN        TO BP13F600.                          
                                                                                
           SELECT BP13K60F  ASSIGN        TO BP13K60F                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K60F-KEY-FLD                       
                            ALTERNATE KEY IS K60F-ALT-KEY2                      
                            FILE STATUS   IS WS-K60F-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F600                                                             
           RECORD CONTAINS 1800 CHARACTERS.                                     
           COPY BP13F600.                                                       
                                                                                
       FD  BP13K60F.                                                            
           COPY BP13K60F.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K60F-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F600-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F600-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K60F-WRITE         PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F600-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K60F-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13F600-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F600-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F600                                                 
                I-O    BP13K60F.                                                
                                                                                
           IF WS-K60F-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K60F ERROR ' WS-K60F-STATUS                  
              MOVE     WS-K60F-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F600-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F600 NEXT                                                   
              AT END MOVE 'Y'              TO   WS-F600-EOF.                    
                                                                                
           IF WS-F600-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-F600-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K60F-REC.                   
           MOVE F600-NUM-ORIG-REGN         TO   K60F-NUM-REGN.                  
           MOVE F600-NUM-CPF               TO   K60F-NUM-CPF.                   
           MOVE F600-CDE-CPF               TO   K60F-CDE-CPF.                   
           MOVE F600-NUM-SNO               TO   K60F-NUM-SNO.                   
                                                                                
           PERFORM 3010-BP13K60F-READ      THRU 3010-EXIT.                      
                                                                                
           IF WS-K60F-FND NOT = 'Y'                                             
              IF F600-DTE-PRINT NOT NUMERIC                                     
                 MOVE ZEROES               TO   F600-DTE-PRINT                  
              END-IF                                                            
                                                                                
              IF F600-DTE-SEND NOT NUMERIC                                      
                 MOVE ZEROES               TO   F600-DTE-SEND                   
              END-IF                                                            
                                                                                
              IF F600-DTE-SEND = ZEROES                                         
                 ADD 1                     TO   WS-CNT-F600-SKIP                
                 DISPLAY 'SEND DATE IS ZEROES: '                                
                          F600-NUM-ORIG-REGN F600-NUM-CPF                       
                          F600-CDE-CPF       F600-NUM-SNO                       
              ELSE                                                              
                 PERFORM 3100-MOVE-VALUES  THRU 3100-EXIT                       
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'DUPLICATE RECORD IN PENDING FILE: '                      
                       F600-NUM-ORIG-REGN F600-NUM-CPF                          
                       F600-CDE-CPF       F600-NUM-SNO                          
              ADD 1                        TO   WS-CNT-F600-SKIP                
           END-IF.                                                              
                                                                                
       3000-CONTINUE-NEXT-REC.                                                  
                                                                                
           PERFORM 2000-BP13F600-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3010-BP13K60F-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K60F KEY IS K60F-ALT-KEY2.                                  
                                                                                
           EVALUATE WS-K60F-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K60F-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K60F-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K60F ERROR '    WS-K60F-STATUS                  
                      ' / '                     K60F-ALT-KEY2                   
              MOVE     WS-K60F-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-MOVE-VALUES.                                                        
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K60F-REC.                   
                                                                                
           IF F600-TAG-EHPS-APPLN = 'Y'                                         
              MOVE 'Y'                     TO   K60F-TAG-EHPS-APPLN             
           ELSE                                                                 
              MOVE 'N'                     TO   K60F-TAG-EHPS-APPLN             
           END-IF.                                                              
                                                                                
           MOVE F600-DTE-SEND              TO   K60F-DTE-CREATE.                
           MOVE F600-NUM-ORIG-REGN         TO   K60F-NUM-REGN.                  
           MOVE F600-NUM-CPF               TO   K60F-NUM-CPF.                   
           MOVE F600-CDE-CPF               TO   K60F-CDE-CPF.                   
           MOVE F600-NUM-SNO               TO   K60F-NUM-SNO.                   
                                                                                
           MOVE F600-NUM-USERID            TO   K60F-NUM-USERID.                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   K60F-DTE-UPDATE.                
           MOVE FUNCTION CURRENT-DATE(9:8) TO   K60F-TME-UPDATE.                
                                                                                
           PERFORM 3110-BP13K60F-WRITE     THRU 3110-EXIT.                      
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3110-BP13K60F-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13K60F-REC.                                                  
                                                                                
           EVALUATE WS-K60F-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K60F-WRITE               
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITE BP13K60F ERROR '   WS-K60F-STATUS                  
                      ' / '                     K60F-KEY-FLD                    
              MOVE     WS-K60F-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F600                                                       
                 BP13K60F.                                                      
                                                                                
           IF WS-K60F-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K60F ERROR ' WS-K60F-STATUS                  
              MOVE     WS-K60F-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C477 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F600 READ      : ' WS-CNT-F600-READ.          
           DISPLAY ' 2. NO OF BP13F600 SKIPPED   : ' WS-CNT-F600-READ.          
           DISPLAY ' 3. NO OF BP13K60F WRITTEN   : ' WS-CNT-K60F-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
