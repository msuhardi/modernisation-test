       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C316.                                                 
       AUTHOR.        CHUNG SD                                                  
       DATE-WRITTEN.  05/09/89.                                                 
      * ======================================================== *              
      *                SYSTEM OF COMMITMENT  (BP13)              *              
      * ======================================================== *              
      *                                                          *              
      *    OBJECTIVE :                                           *              
      *      THIS IS A PROGRAM TO PRINT REPORT ON AGREEMENT      *              
      *      ORDER NOT RAISED WITHIN THE SAME DAY.               *              
      *                                                          *              
      *    INPUT FILE  :  BP13F300                               *              
      *                                                          *              
      *    OUTPUT FILE :  BP13L316                               *              
      *                                                          *              
      * CHG-NO   BY  DATE   DESCRIPTION                          *              
      * -------- --- ------ -----------                          *              
      * SOCPH8   LJL 930724 TO EXPAND THE REGN NO FROM 7 TO 8    *              
      *                     BYTES.                               *              
      * BP130020 SSS 951007 REPOSITION L136-PR-HEAD-01 TO ROW 1  *              
      * BP130418 SGK 980604 TO CATER FOR Y2K.                    *              
      * BP136402 KSJ 160818 To print separate reports for Normal *              
      *                     and SERS cases.                      *              
      *                     To migrate from hardcopy to email    *              
      *                     reports with new report format.      *              
      * BP139262 KAC1220725 To update mailing list for L316-PR-HEAD-03A         
      *                     Send To: Wai Mun HOO & Stanley YY NG *              
      *                     Send CC: Katherine GL NG & Theresa MT NGOOI         
      *                     removed email from L316-PR-HEAD-03A: *              
      *                        SOFFIA_RAPHAY@HDB.GOV.SG          *              
      *                        FANG_YAN@HDB.GOV.SG               *              
      *                        CHUNG_MENG_HWEE@HDB.GOV.SG        *              
      * ======================================================== *              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F300 ASSIGN        TO BP13F300.                           
                                                                                
           SELECT BP13K022 ASSIGN        TO BP13K022                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K022-KEY-FLD                        
                           ALTERNATE KEY IS K022-NUM-REGN                       
                           FILE STATUS   IS WS-K022-STATUS.                     
                                                                                
           SELECT SY08K010 ASSIGN        TO SY08K010                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K010-NUM-USERID                     
                           ALTERNATE KEY IS K010-NUM-LOTUS-NCKNM                
                           FILE STATUS   IS WS-K010-STATUS.                     
                                                                                
           SELECT BP17K000 ASSIGN        TO BP17K000                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K000-KEY-FLD                        
                           ALTERNATE KEY IS K000-NUM-REGNO                      
                           FILE STATUS   IS WS-K000-STATUS.                     
                                                                                
           SELECT P13L316A ASSIGN        TO P13L316A.                           
           SELECT P13L316B ASSIGN        TO P13L316B.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F300                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORD IS STANDARD.                                            
           COPY BP13F300.                                                       
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13K022.                                                       
                                                                                
       FD  SY08K010                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY SY08K010.                                                       
                                                                                
       FD  BP17K000                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP17K000.                                                       
                                                                                
       FD  P13L316A                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  P13L316A-REC               PIC X(132).                               
                                                                                
       FD  P13L316B                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  P13L316B-REC               PIC X(132).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K000-STATUS         PIC 9(2)    VALUE ZEROES.                 
           05  WS-K010-STATUS         PIC 9(2)    VALUE ZEROES.                 
           05  WS-K022-STATUS         PIC 9(2)    VALUE ZEROES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F300-READ       PIC 9(4)    VALUE ZEROS.                  
           05  WS-CNT-L316A-PRINT     PIC 9(4)    VALUE ZEROS.                  
           05  WS-CNT-L316B-PRINT     PIC 9(4)    VALUE ZEROS.                  
                                                                                
       01  WS-FLAG-VARIABLES.                                                   
           05  WS-K010-FND            PIC X(1)    VALUE 'N'.                    
           05  WS-K022-FND            PIC X(1)    VALUE 'N'.                    
           05  WS-FILE-IND            PIC X(1)    VALUE 'N'.                    
               88 WS-F300-EOF                     VALUE 'Y'.                    
           05  WS-HDR-PRINTED-A       PIC X(1)    VALUE 'N'.                    
           05  WS-HDR-PRINTED-B       PIC X(1)    VALUE 'N'.                    
                                                                                
       01  WS-DATES-VARIABLES.                                                  
           05  WS-CURRENT-DATE.                                                 
               10 WS-CUR-CCYY         PIC 9(4).                                 
               10 WS-CUR-MM           PIC 99.                                   
               10 WS-CUR-DD           PIC 99.                                   
           05  WS-EDITED-DATE.                                                  
               10 WS-CUR-DD           PIC 99.                                   
               10 FILLER              PIC X       VALUE '/'.                    
               10 WS-CUR-MM           PIC 99.                                   
               10 FILLER              PIC X       VALUE '/'.                    
               10 WS-CUR-CCYY         PIC 9(4).                                 
                                                                                
       01  WS-SERS-VARIABLES.                                                   
           05  WS-SCH-FND             PIC X(1)    VALUE SPACES.                 
           05  WS-ALLOC-SCH           PIC X(3)    VALUE SPACES.                 
           COPY SERSCH.                                                         
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-CRM             PIC X(5)    VALUE SPACES.                 
           05  WS-NME-CRM             PIC X(20)   VALUE SPACES.                 
                                                                                
      *-------------------------------------------------------------            
      *    EMAIL REPORT COMMANDS                                                
      *-------------------------------------------------------------            
       01  MAIL-COMMANDS.                                                       
           05  WS-MAIL-HELO           PIC X(132)  VALUE                         
               'HELO SGPHDB1'.                                                  
           05  WS-MAIL-MAIL-FROM      PIC X(132)  VALUE                         
               'MAIL FROM:<OPCP@SGPHDB1>'.                                      
           05  WS-MAIL-RCPT-TO-1      PIC X(132)  VALUE                         
               'RCPT TO:<SOFFIA_RAPHAY@HDB.GOV.SG>'.                            
           05  WS-MAIL-RCPT-TO-2      PIC X(132)  VALUE                         
               'RCPT TO:<FANG_YAN@HDB.GOV.SG>'.                                 
           05  WS-MAIL-RCPT-TO-3      PIC X(132)  VALUE                         
               'RCPT TO:<CHUNG_MENG_HWEE@HDB.GOV.SG>'.                          
           05  WS-MAIL-RCPT-TO-4      PIC X(132)  VALUE                         
               'RCPT TO:<TAN_SIEW_LAY@HDB.GOV.SG>'.                             
           05  WS-MAIL-RCPT-TO-5      PIC X(132)  VALUE                         
               'RCPT TO:<HOO_Wai_Mun@hdb.gov.sg>'.                              
           05  WS-MAIL-RCPT-TO-6      PIC X(132)  VALUE                         
               'RCPT TO:<Stanley_YY_NG@hdb.gov.sg>'.                            
           05  WS-MAIL-RCPT-TO-7      PIC X(132)  VALUE                         
               'RCPT TO:<Katherine_GL_NG@hdb.gov.sg>'.                          
           05  WS-MAIL-RCPT-TO-8      PIC X(132)  VALUE                         
               'RCPT TO:<Theresa_MT_NGOOI@hdb.gov.sg>'.                         
           05  WS-MAIL-DATA           PIC X(132)  VALUE 'DATA'.                 
           05  WS-MAIL-FROM           PIC X(132)  VALUE                         
               'FROM:Soc System - Email Alert'.                                 
           05  WS-MAIL-TO-1           PIC X(132)  VALUE                         
               'TO:<SOFFIA_RAPHAY@HDB.GOV.SG>'.                                 
           05  WS-MAIL-TO-2           PIC X(132)  VALUE                         
               'TO:<FANG_YAN@HDB.GOV.SG>'.                                      
           05  WS-MAIL-TO-3           PIC X(132)  VALUE                         
               'TO:<CHUNG_MENG_HWEE@HDB.GOV.SG>'.                               
           05  WS-MAIL-TO-4           PIC X(132)  VALUE                         
               'TO:<TAN_SIEW_LAY@HDB.GOV.SG>'.                                  
           05  WS-MAIL-TO-5           PIC X(132)  VALUE                         
               'TO:<HOO_Wai_Mun@hdb.gov.sg>'.                                   
           05  WS-MAIL-TO-6           PIC X(132)  VALUE                         
               'TO:<Stanley_YY_NG@hdb.gov.sg>'.                                 
           05  WS-MAIL-TO-7           PIC X(132)  VALUE                         
               'CC:<Katherine_GL_NG@hdb.gov.sg>'.                               
           05  WS-MAIL-TO-8           PIC X(132)  VALUE                         
               'CC:<Theresa_MT_NGOOI@hdb.gov.sg>'.                              
           05  WS-MAIL-SUBJECT-1.                                               
               10  FILLER             PIC X(132)  VALUE                         
                   'SUBJECT : Detail List of Agreement Order Updated but        
      -            ' not raised for Bank Loan cases'.                           
           05  WS-MAIL-SUBJECT-2.                                               
               10  FILLER             PIC X(132)  VALUE                         
                   'SUBJECT : Detail List of Agreement Order Updated but        
      -            ' not raised for SERS Bank Loan cases'.                      
           05  WS-MAIL-DATE.                                                    
               10  FILLER             PIC X(6)    VALUE                         
                   'DATE: '.                                                    
               10  WS-MAIL-DTE-CURR   PIC X(10)   VALUE SPACES.                 
           05  WS-MAIL-BODY-1.                                                  
               10  FILLER             PIC X(132)  VALUE                         
                   'Below are the Bank Loan cases pending your follow-up        
      -            ' action. Thank You.'.                                       
           05  WS-MAIL-BODY-2.                                                  
               10  FILLER             PIC X(132)  VALUE                         
                   'Below are the SERS Bank Loan cases pending your foll        
      -            'ow-up action. Thank You.'.                                  
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT                                                        
      *-------------------------------------------------------------            
       01  L316-PR-HEAD-01.                                                     
           05  FILLER                 PIC X(14)   VALUE 'BP13L316A'.            
           05  FILLER                 PIC X(8)    VALUE 'HDB3    '.             
           05  FILLER                 PIC X(25)   VALUE SPACES.                 
           05  FILLER                 PIC X(39)   VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                 PIC X(13)   VALUE SPACES.                 
           05  FILLER                 PIC X(7)    VALUE 'DATE: '.               
           05  PRINT-CURRENT-DATE     PIC X(10)   VALUE SPACES.                 
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  FILLER                 PIC X(7)    VALUE 'PAGE: '.               
           05  PRINT-PAGE             PIC ZZZ9.                                 
                                                                                
       01  L316-PR-HEAD-02.                                                     
           05  FILLER                 PIC X(58)   VALUE SPACES.                 
           05  FILLER                 PIC X(14)   VALUE                         
              'AGREEMENT TEAM'.                                                 
           05  FILLER                 PIC X(60)   VALUE SPACES.                 
                                                                                
       01  L316-PR-HEAD-03A.                                                    
           05  FILLER                 PIC X(29)   VALUE SPACES.                 
           05  FILLER                 PIC X(73)   VALUE                         
              'DETAIL LIST OF AGREEMENT ORDER UPDATED BUT NOT RAISED FOR        
      -       ' BANK LOAN CASES'.                                               
                                                                                
       01  L316-PR-HEAD-03B.                                                    
           05  FILLER                 PIC X(27)   VALUE SPACES.                 
           05  FILLER                 PIC X(78)   VALUE                         
              'DETAIL LIST OF AGREEMENT ORDER UPDATED BUT NOT RAISED FOR        
      -       ' SERS BANK LOAN CASES'.                                          
                                                                                
       01  L316-PR-TOTAL-LINE.                                                  
           05  FILLER                 PIC X(45)   VALUE SPACES.                 
           05  FILLER                 PIC X(31)                                 
           VALUE 'TOTAL NO. OF RECORDS PRINTED = '.                             
           05  L316-TOTAL-REC         PIC ZZZ9.                                 
           05  FILLER                 PIC X(56)   VALUE SPACES.                 
                                                                                
       01  L316-PR-ASTERICK-LINE.                                               
           05  FILLER                 PIC X(56)   VALUE SPACES.                 
           05  FILLER                 PIC X(15)   VALUE ALL '*'.                
           05  FILLER                 PIC X(61)   VALUE SPACES.                 
                                                                                
      *-------------------------------------------------------------            
      *    REPORT 1 DETAILS (NORMAL CASES)                                      
      *-------------------------------------------------------------            
       01  L316A-PR-TITLE-1.                                                    
           05  FILLER                 PIC X(132)  VALUE                         
              'REGN       USERID    TOP-UP AMT   CONV CASH    SOC DEBTOR        
      -       '   STATUS                                     CRM'.              
                                                                                
       01  L316A-PR-TITLE-2.                                                    
           05  FILLER                 PIC X(132)  VALUE                         
              '--------   -------   ----------   ----------   ----------        
      -       '   ----------------------------------------   -----------        
      -       '---------------'.                                                
                                                                                
       01  L316A-PR-DTL.                                                        
           05  L316A-NUM-REGN         PIC X(8).                                 
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L316A-NUM-USERID       PIC X(7).                                 
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L316A-AMT-TOPUP        PIC ZZZ,ZZ9.99.                           
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L316A-AMT-CONV-CASH    PIC ZZZ,ZZ9.99.                           
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L316A-AMT-SOC-DEBTOR   PIC ZZZ,ZZ9.99.                           
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L316A-NUM-STATUS       PIC X(40).                                
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L316A-NUM-CRM          PIC X(5).                                 
           05  FILLER                 PIC X(1)    VALUE '-'.                    
           05  L316A-NME-CRM          PIC X(20).                                
                                                                                
      *-------------------------------------------------------------            
      *    REPORT 2 DETAILS (SERS CASES)                                        
      *-------------------------------------------------------------            
                                                                                
       01  L316B-PR-TITLE-1.                                                    
           05  FILLER                 PIC X(132)  VALUE                         
              'REGN       SERS SCH ACCT       ICP         STAMP FEE             
      -       ' CONVEY FEE    SOC SERS CONTRA   STATUS / CRM'.                  
                                                                                
       01  L316B-PR-TITLE-2.                                                    
           05  FILLER                 PIC X(132)  VALUE                         
              '--------   -------------   ------------   ------------           
      -       '------------   ---------------   ------------------------        
      -       '----------------'.                                               
                                                                                
       01  L316B-PR-DTL-1.                                                      
           05  L316B-NUM-REGN         PIC X(8).                                 
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L316B-NUM-SERS-SCH     PIC X(9).                                 
           05  FILLER                 PIC X(9)    VALUE SPACES.                 
           05  L316B-AMT-SERS-ICP     PIC ZZZ,ZZ9.99.                           
           05  FILLER                 PIC X(5)    VALUE SPACES.                 
           05  L316B-AMT-SERS-STAMP   PIC ZZZ,ZZ9.99.                           
           05  FILLER                 PIC X(5)    VALUE SPACES.                 
           05  L316B-AMT-SERS-CONVEY  PIC ZZZ,ZZ9.99.                           
           05  FILLER                 PIC X(8)    VALUE SPACES.                 
           05  L316B-AMT-SOC-SERCNTR  PIC ZZZ,ZZ9.99.                           
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L316B-NUM-STATUS       PIC X(40).                                
                                                                                
       01  L316B-PR-DTL-2.                                                      
           05  FILLER                 PIC X(90)   VALUE SPACES.                 
           05  L316B-NUM-CRM          PIC X(5).                                 
           05  FILLER                 PIC X(1)    VALUE '-'.                    
           05  L316B-NME-CRM          PIC X(20).                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ****************************************************************          
       MAIN-ROUTINE SECTION.                                                    
      ****************************************************************          
                                                                                
           PERFORM 100-OPEN-ROUTINE        THRU 100-EXIT.                       
           PERFORM 200-MAIN-ROUTINE        THRU 200-EXIT                        
                   UNTIL WS-F300-EOF.                                           
           PERFORM 900-CLOSE-ROUTINE       THRU 900-EXIT.                       
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       100-OPEN-ROUTINE.                                                        
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F300                                                 
                       BP13K022                                                 
                       SY08K010                                                 
                       BP17K000                                                 
                OUTPUT P13L316A                                                 
                       P13L316B.                                                
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO   RETURN-CODE                     
              PERFORM  900-CLOSE-ROUTINE   THRU 900-EXIT                        
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING SY08K010 ERROR ' WS-K010-STATUS                  
              MOVE     WS-K010-STATUS      TO   RETURN-CODE                     
              PERFORM  900-CLOSE-ROUTINE   THRU 900-EXIT                        
           END-IF.                                                              
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP17K000 ERROR ' WS-K000-STATUS                  
              MOVE     WS-K000-STATUS      TO   RETURN-CODE                     
              PERFORM  900-CLOSE-ROUTINE   THRU 900-EXIT                        
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       200-MAIN-ROUTINE.                                                        
      ****************************************************************          
      *                                                              *          
      *   1. READ FILE BP13F300.                                     *          
      *   2. CHECK IF NORMAL OR SERS ACCOUNT                         *          
      *   3. IF SERS ACCOUNT, GET SERS SCH ACCT VIA BP17K000         *          
      *   4. GET CRM ID AND NAME VIA BP13K022 AND SY08K010           *          
      *   5. POPULATE REPORTS                                        *          
      *                                                              *          
      ****************************************************************          
                                                                                
           READ BP13F300                                                        
                AT END MOVE 'Y'            TO   WS-FILE-IND                     
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1                           TO   WS-CNT-F300-READ.               
                                                                                
      *---------------------------------------------------------------          
      *    CHECK SERS TABLE. IF FOUND, GET SERS SCHEME ACCOUNT                  
      *---------------------------------------------------------------          
           MOVE F300-ALLOC-SCH             TO   WS-ALLOC-SCH.                   
           PERFORM 210-CHECK-SERS-TABLE    THRU 210-EXIT.                       
                                                                                
           IF WS-SCH-FND = 'Y'                                                  
              MOVE SPACES                  TO   BP17K000-REC                    
              MOVE F300-CUR-REG            TO   K000-NUM-REGNO                  
              PERFORM 220-READ-BP17K000    THRU 220-EXIT                        
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *    GET CRM ID AND NAME                                                  
      *---------------------------------------------------------------          
           MOVE SPACES                     TO   BP13K022-REC                    
                                                WS-NUM-CRM                      
                                                WS-NME-CRM.                     
           MOVE F300-CUR-REG               TO   K022-NUM-REGN.                  
           PERFORM 230-READ-BP13K022       THRU 230-EXIT.                       
                                                                                
           IF (WS-K022-FND = 'Y') AND                                           
              (K022-NUM-OIC-APPL NOT = SPACES AND LOW-VALUES)                   
              MOVE K022-NUM-OIC-APPL       TO   WS-NUM-CRM                      
                                                                                
              MOVE K022-NUM-OIC-APPL       TO   K010-NUM-LOTUS-NCKNM            
              PERFORM 240-READ-SY08K010    THRU 240-EXIT                        
                                                                                
              IF WS-K010-FND = 'Y'                                              
                 MOVE K010-NME-USER        TO   WS-NME-CRM                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *    PRINT REPORT                                                         
      *---------------------------------------------------------------          
           IF WS-SCH-FND NOT = 'Y'                                              
              PERFORM 300-PRINT-NORMAL     THRU 300-EXIT                        
           ELSE                                                                 
              PERFORM 400-PRINT-SERS       THRU 400-EXIT                        
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       210-CHECK-SERS-TABLE.                                                    
      ****************************************************************          
                                                                                
           IF WS-ALLOC-SCH NOT = LOW-VALUES AND SPACES                          
              SET WS-SERS-PTR              TO   1                               
              SEARCH WS-SERS-SCH                                                
                 AT END                                                         
                    MOVE 'N'               TO   WS-SCH-FND                      
                 WHEN WS-SERS-SCHEME(WS-SERS-PTR) = WS-ALLOC-SCH                
                    MOVE 'Y'               TO   WS-SCH-FND                      
              END-SEARCH                                                        
           ELSE                                                                 
              MOVE 'N'                     TO   WS-SCH-FND                      
           END-IF.                                                              
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       220-READ-BP17K000.                                                       
      ****************************************************************          
                                                                                
           READ BP17K000 KEY IS K000-NUM-REGNO.                                 
                                                                                
           EVALUATE WS-K000-STATUS                                              
           WHEN ZEROES                                                          
              CONTINUE                                                          
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE SPACES                  TO   K000-NUM-SCH-ACC                
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP17K000, STATUS = ' WS-K000-STATUS        
              MOVE    WS-K000-STATUS       TO   RETURN-CODE                     
              PERFORM 900-CLOSE-ROUTINE    THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       230-READ-BP13K022.                                                       
      ****************************************************************          
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K022-FND                     
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K022-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE WS-K022-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BP13K022, STATUS = ' WS-K022-STATUS        
              PERFORM 900-CLOSE-ROUTINE    THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       230-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       240-READ-SY08K010.                                                       
      ****************************************************************          
                                                                                
           READ SY08K010 KEY IS K010-NUM-LOTUS-NCKNM.                           
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K010-FND                     
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K010-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE WS-K010-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING SY08K010, STATUS = ' WS-K010-STATUS        
              PERFORM 900-CLOSE-ROUTINE    THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       240-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       300-PRINT-NORMAL.                                                        
      ****************************************************************          
                                                                                
           IF WS-HDR-PRINTED-A NOT = 'Y'                                        
              PERFORM 310-PRINT-NORMAL-HDR THRU 310-EXIT                        
           END-IF.                                                              
                                                                                
           IF F300-AMT-TOPUP-CASH NOT NUMERIC                                   
              MOVE ZEROES                  TO   F300-AMT-TOPUP-CASH.            
                                                                                
           IF F300-AMT-CONV-CASH NOT NUMERIC                                    
              MOVE ZEROES                  TO   F300-AMT-CONV-CASH.             
                                                                                
           IF F300-AMT-SOC-DEBTOR NOT NUMERIC                                   
              MOVE ZEROES                  TO   F300-AMT-SOC-DEBTOR.            
                                                                                
           MOVE F300-CUR-REG               TO   L316A-NUM-REGN.                 
           MOVE F300-USERID                TO   L316A-NUM-USERID.               
           MOVE F300-AMT-TOPUP-CASH        TO   L316A-AMT-TOPUP.                
           MOVE F300-AMT-CONV-CASH         TO   L316A-AMT-CONV-CASH.            
           MOVE F300-AMT-SOC-DEBTOR        TO   L316A-AMT-SOC-DEBTOR.           
                                                                                
           IF F300-DTE-AGREEMENT-FOR-LEASE = SPACES OR LOW-VALUES OR            
                                             ZEROES                             
              MOVE 'AO raised and Pending Signing of AFL'                       
                                           TO   L316A-NUM-STATUS                
           END-IF.                                                              
                                                                                
           MOVE WS-NUM-CRM                 TO   L316A-NUM-CRM.                  
           MOVE WS-NME-CRM                 TO   L316A-NME-CRM.                  
                                                                                
           WRITE P13L316A-REC              FROM L316A-PR-DTL.                   
           ADD 1                           TO   WS-CNT-L316A-PRINT.             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       310-PRINT-NORMAL-HDR.                                                    
      ****************************************************************          
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-CURRENT-DATE.                
           MOVE CORR WS-CURRENT-DATE       TO   WS-EDITED-DATE.                 
           MOVE WS-EDITED-DATE             TO   WS-MAIL-DTE-CURR.               
                                                                                
           WRITE P13L316A-REC              FROM WS-MAIL-HELO.                   
           WRITE P13L316A-REC              FROM WS-MAIL-MAIL-FROM.              
      **CR9262                                                                  
      **   WRITE P13L316A-REC              FROM WS-MAIL-RCPT-TO-1.              
      **   WRITE P13L316A-REC              FROM WS-MAIL-RCPT-TO-2.              
      **   WRITE P13L316A-REC              FROM WS-MAIL-RCPT-TO-3.              
                                                                                
           WRITE P13L316A-REC              FROM WS-MAIL-RCPT-TO-5.              
           WRITE P13L316A-REC              FROM WS-MAIL-RCPT-TO-6.              
           WRITE P13L316A-REC              FROM WS-MAIL-RCPT-TO-7.              
           WRITE P13L316A-REC              FROM WS-MAIL-RCPT-TO-8.              
           WRITE P13L316A-REC              FROM WS-MAIL-DATA.                   
           WRITE P13L316A-REC              FROM WS-MAIL-FROM.                   
      **CR9262                                                                  
      **   WRITE P13L316A-REC              FROM WS-MAIL-TO-1.                   
      **   WRITE P13L316A-REC              FROM WS-MAIL-TO-2.                   
      **   WRITE P13L316A-REC              FROM WS-MAIL-TO-3.                   
                                                                                
           WRITE P13L316A-REC              FROM WS-MAIL-TO-5.                   
           WRITE P13L316A-REC              FROM WS-MAIL-TO-6.                   
           WRITE P13L316A-REC              FROM WS-MAIL-TO-7.                   
           WRITE P13L316A-REC              FROM WS-MAIL-TO-8.                   
           WRITE P13L316A-REC              FROM WS-MAIL-SUBJECT-1.              
           WRITE P13L316A-REC              FROM WS-MAIL-DATE.                   
           WRITE P13L316A-REC              FROM WS-MAIL-BODY-1.                 
                                                                                
           MOVE SPACES                     TO   P13L316A-REC.                   
           WRITE P13L316A-REC.                                                  
                                                                                
           MOVE WS-EDITED-DATE             TO   PRINT-CURRENT-DATE.             
           MOVE 1                          TO   PRINT-PAGE.                     
                                                                                
           WRITE P13L316A-REC              FROM L316-PR-HEAD-01.                
           WRITE P13L316A-REC              FROM L316-PR-HEAD-02.                
           WRITE P13L316A-REC              FROM L316-PR-HEAD-03A.               
                                                                                
           MOVE SPACES                     TO   P13L316A-REC.                   
           WRITE P13L316A-REC.                                                  
                                                                                
           WRITE P13L316A-REC              FROM L316A-PR-TITLE-1.               
           WRITE P13L316A-REC              FROM L316A-PR-TITLE-2.               
                                                                                
           MOVE SPACES                     TO   P13L316A-REC.                   
           WRITE P13L316A-REC.                                                  
                                                                                
           MOVE 'Y'                        TO   WS-HDR-PRINTED-A.               
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       400-PRINT-SERS.                                                          
      ****************************************************************          
                                                                                
           IF WS-HDR-PRINTED-B NOT = 'Y'                                        
              PERFORM 410-PRINT-SERS-HDR   THRU 410-EXIT                        
           END-IF.                                                              
                                                                                
           IF F300-AMT-SER-CONTRA-ICP NOT NUMERIC                               
              MOVE ZEROES TO F300-AMT-SER-CONTRA-ICP.                           
                                                                                
           IF F300-AMT-SER-CONTRA-ICP-NEW NOT NUMERIC                           
              MOVE ZEROES TO F300-AMT-SER-CONTRA-ICP-NEW.                       
                                                                                
           IF F300-AMT-SER-CONTRA-STP-FEES NOT NUMERIC                          
              MOVE ZEROES TO F300-AMT-SER-CONTRA-STP-FEES.                      
                                                                                
           IF F300-AMT-SER-CONTRA-CONVYFEES NOT NUMERIC                         
              MOVE ZEROES TO F300-AMT-SER-CONTRA-CONVYFEES.                     
                                                                                
           IF F300-AMT-SOC-SERCNTR NOT NUMERIC                                  
              MOVE ZEROES TO F300-AMT-SOC-SERCNTR.                              
                                                                                
           MOVE F300-CUR-REG               TO   L316B-NUM-REGN.                 
           MOVE K000-NUM-SCH-ACC           TO   L316B-NUM-SERS-SCH              
                                                                                
           IF F300-AMT-SER-CONTRA-ICP-NEW > 0                                   
              MOVE F300-AMT-SER-CONTRA-ICP-NEW                                  
                                           TO   L316B-AMT-SERS-ICP              
           ELSE                                                                 
              MOVE F300-AMT-SER-CONTRA-ICP TO   L316B-AMT-SERS-ICP              
           END-IF.                                                              
                                                                                
           MOVE F300-AMT-SER-CONTRA-STP-FEES                                    
                                           TO   L316B-AMT-SERS-STAMP            
           MOVE F300-AMT-SER-CONTRA-CONVYFEES                                   
                                           TO   L316B-AMT-SERS-CONVEY           
           MOVE F300-AMT-SOC-SERCNTR       TO   L316B-AMT-SOC-SERCNTR           
                                                                                
           IF F300-DTE-AGREEMENT-FOR-LEASE = SPACES OR LOW-VALUES OR            
                                             ZEROES                             
              IF F300-AMT-SOC-CASH > 0                                          
                 MOVE 'Journal Not Raised' TO   L316B-NUM-STATUS                
              ELSE                                                              
                 MOVE 'AO raised and Pending Signing of AFL'                    
                                           TO   L316B-NUM-STATUS                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE P13L316B-REC              FROM L316B-PR-DTL-1.                 
                                                                                
           MOVE WS-NUM-CRM                 TO   L316B-NUM-CRM.                  
           MOVE WS-NME-CRM                 TO   L316B-NME-CRM.                  
                                                                                
           WRITE P13L316B-REC              FROM L316B-PR-DTL-2.                 
           ADD 1                           TO   WS-CNT-L316B-PRINT.             
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       410-PRINT-SERS-HDR.                                                      
      ****************************************************************          
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-CURRENT-DATE.                
           MOVE CORR WS-CURRENT-DATE       TO   WS-EDITED-DATE.                 
           MOVE WS-EDITED-DATE             TO   WS-MAIL-DTE-CURR.               
                                                                                
           WRITE P13L316B-REC              FROM WS-MAIL-HELO.                   
           WRITE P13L316B-REC              FROM WS-MAIL-MAIL-FROM.              
           WRITE P13L316B-REC              FROM WS-MAIL-RCPT-TO-1.              
           WRITE P13L316B-REC              FROM WS-MAIL-RCPT-TO-4.              
           WRITE P13L316B-REC              FROM WS-MAIL-DATA.                   
           WRITE P13L316B-REC              FROM WS-MAIL-FROM.                   
           WRITE P13L316B-REC              FROM WS-MAIL-TO-1.                   
           WRITE P13L316B-REC              FROM WS-MAIL-TO-4.                   
           WRITE P13L316B-REC              FROM WS-MAIL-SUBJECT-2.              
           WRITE P13L316B-REC              FROM WS-MAIL-DATE.                   
           WRITE P13L316B-REC              FROM WS-MAIL-BODY-2.                 
                                                                                
           MOVE SPACES                     TO   P13L316B-REC.                   
           WRITE P13L316B-REC.                                                  
                                                                                
           MOVE WS-EDITED-DATE             TO   PRINT-CURRENT-DATE.             
           MOVE 1                          TO   PRINT-PAGE.                     
                                                                                
           WRITE P13L316B-REC              FROM L316-PR-HEAD-01.                
           WRITE P13L316B-REC              FROM L316-PR-HEAD-02.                
           WRITE P13L316B-REC              FROM L316-PR-HEAD-03B.               
                                                                                
           MOVE SPACES                     TO   P13L316B-REC.                   
           WRITE P13L316B-REC.                                                  
                                                                                
           WRITE P13L316B-REC              FROM L316B-PR-TITLE-1.               
           WRITE P13L316B-REC              FROM L316B-PR-TITLE-2.               
                                                                                
           MOVE SPACES                     TO   P13L316B-REC.                   
           WRITE P13L316B-REC.                                                  
                                                                                
           MOVE 'Y'                        TO   WS-HDR-PRINTED-B.               
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       900-CLOSE-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           IF WS-HDR-PRINTED-A = 'Y'                                            
              MOVE SPACES                  TO   P13L316A-REC                    
              WRITE P13L316A-REC                                                
                                                                                
              WRITE P13L316A-REC           FROM L316-PR-ASTERICK-LINE           
                                                                                
              MOVE SPACES                  TO   P13L316A-REC                    
              WRITE P13L316A-REC                                                
                                                                                
              MOVE WS-CNT-L316A-PRINT      TO   L316-TOTAL-REC                  
              WRITE P13L316A-REC           FROM L316-PR-TOTAL-LINE              
           END-IF.                                                              
                                                                                
           IF WS-HDR-PRINTED-B = 'Y'                                            
              MOVE SPACES                  TO   P13L316B-REC                    
              WRITE P13L316B-REC                                                
                                                                                
              WRITE P13L316B-REC           FROM L316-PR-ASTERICK-LINE           
                                                                                
              MOVE SPACES                  TO   P13L316B-REC                    
              WRITE P13L316B-REC                                                
                                                                                
              MOVE WS-CNT-L316B-PRINT      TO   L316-TOTAL-REC                  
              WRITE P13L316B-REC           FROM L316-PR-TOTAL-LINE              
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13C316 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1.  NO OF BP13F300 READ      : ' WS-CNT-F300-READ.         
           DISPLAY ' 2.  NO OF P13L316A PRINTED   : ' WS-CNT-L316A-PRINT        
           DISPLAY ' 3.  NO OF P13L316B PRINTED   : ' WS-CNT-L316B-PRINT        
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F300                                                       
                 BP13K022                                                       
                 SY08K010                                                       
                 BP17K000                                                       
                 P13L316A                                                       
                 P13L316B.                                                      
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING SY08K010 ERROR ' WS-K010-STATUS                  
              MOVE     WS-K010-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP17K000 ERROR ' WS-K000-STATUS                  
              MOVE     WS-K000-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
