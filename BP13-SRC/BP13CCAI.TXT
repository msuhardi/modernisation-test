       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CCAI.                                                 
       AUTHOR.        RJEM.                                                     
      *DATE-WRITTEN.  28/10/2013.                                               
                                                                                
      *****************************************************************         
      *                SYSTEM OF COMMITMENT (BP13)                    *         
      *===============================================================*         
      *  OBJECTIVES : 1.                                              *         
      *               2.                                              *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      * CHGE REQ #  DATE      AUTHOR    REMARKS                       *         
      * ----------  ----      ------    -------                       *         
      * BP135120    28102013  RJE1      ADHOC PROGRAM                 *         
      *                                                               *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13FD00 ASSIGN        TO BP13FD00.                           
                                                                                
           SELECT B05K006  ASSIGN        TO B05K006                             
                  ACCESS MODE            IS DYNAMIC                             
                  ORGANIZATION           IS INDEXED                             
                  RECORD KEY             IS K006-HDB-REF-SF                     
                  FILE STATUS            IS WS-K006-STATUS.                     
                                                                                
           SELECT BP13K640 ASSIGN        TO BP13K640                            
                  ACCESS MODE            IS DYNAMIC                             
                  ORGANIZATION           IS INDEXED                             
                  RECORD KEY             IS K640-KEY-FLD                        
                  FILE STATUS            IS WS-K640-STATUS.                     
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                  ACCESS MODE            IS DYNAMIC                             
                  ORGANIZATION           IS INDEXED                             
                  RECORD KEY             IS K800-NUM-REGN                       
                  FILE STATUS            IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13LCAH ASSIGN        TO BP13LCAH.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13FD00                                                             
           RECORD    CONTAINS 1600 CHARACTERS                                   
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13FD00.                                                           
                                                                                
       FD  B05K006                                                              
           RECORD    CONTAINS 800 CHARACTERS.                                   
       COPY B05K006.                                                            
                                                                                
       FD  BP13K640                                                             
           RECORD CONTAINS 500  CHARACTERS.                                     
       COPY BP13K640.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13LCAH                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS BP13LCAH-REC.                                         
       01  BP13LCAH-REC            PIC X(132).                                  
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  WS-K006-STATUS      PIC 9(02)     VALUE ZEROES.                  
           05  WS-K640-STATUS      PIC 9(02)     VALUE ZEROES.                  
           05  WS-K800-STATUS      PIC 9(02)     VALUE ZEROES.                  
       01  WS-COUNTERS.                                                         
           05  WS-FD00-READ        PIC 9(8)      VALUE ZEROS.                   
           05  WS-K006-READ        PIC 9(8)      VALUE ZEROS.                   
           05  WS-K640-READ        PIC 9(8)      VALUE ZEROS.                   
           05  WS-K800-READ        PIC 9(8)      VALUE ZEROS.                   
           05  WS-LCAH-WRITE       PIC 9(8)      VALUE ZEROS.                   
           05  WS-PAGE-CNT         PIC 9(2)      VALUE ZEROS.                   
           05  WS-LINE-CNT         PIC 9(2)      VALUE ZEROS.                   
       01  WS-FD00-EOF             PIC X         VALUE SPACES.                  
       01  WS-HEAD3-SW             PIC X         VALUE SPACES.                  
       01  WS-BLANK-LINE           PIC X(132)    VALUE SPACES.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 CURRENT-CC           PIC 99.                                      
           05 CURRENT-YY           PIC 99.                                      
           05 CURRENT-MM           PIC 99.                                      
           05 CURRENT-DD           PIC 99.                                      
                                                                                
      ***REPORT                                                                 
       01  LCAH-HEADING1.                                                       
           05  LCAH-REPORT-ID       PIC X(8)  VALUE 'BP13LCAH'.                 
           05  FILLER               PIC X(10) VALUE '      HDB3'.               
           05  FILLER               PIC X(27) VALUE SPACES.                     
           05  FILLER               PIC X(39) VALUE                             
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER               PIC X(14) VALUE SPACES.                     
           05  FILLER               PIC X(7)  VALUE 'DATE : '.                  
           05  LCAH-CURRENT-DATE.                                               
               10  CURRENT-DD       PIC 99.                                     
               10  FILLER           PIC X        VALUE '/'.                     
               10  CURRENT-MM       PIC 99.                                     
               10  FILLER           PIC X        VALUE '/'.                     
               10  CURRENT-CC       PIC 99.                                     
               10  CURRENT-YY       PIC 99.                                     
           05  FILLER               PIC X(4)  VALUE SPACES.                     
           05  FILLER               PIC X(7)  VALUE 'PAGE : '.                  
           05  LCAH-PAGE            PIC ZZZZ9.                                  
                                                                                
       01  LCAH-HEADING2.                                                       
           05  FILLER               PIC X(37) VALUE SPACES.                     
           05  FILLER               PIC X(50) VALUE                             
               'DBSS CASES WITH BALANCE STAMP FEE AVAILABLE IN SRF'.            
                                                                                
       01  LCAH-HEADING3.                                                       
           05  FILLER               PIC X(12) VALUE 'PROJECT CODE'.             
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(11) VALUE 'SCH ACCT NO'.              
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(11) VALUE 'DBSS BLK NO'.              
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(12) VALUE 'DBSS UNIT NO'.             
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(08) VALUE 'LOAN TAG'.                 
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE 'CF TYPE'.                  
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(26) VALUE                             
                                          'STAMP FEE AVAILABLE IN SRF'.         
                                                                                
       01  LCAH-DETAILS.                                                        
           05  FILLER               PIC X(03) VALUE SPACES.                     
           05  LCAH-PROJECT         PIC X(03) VALUE SPACES.                     
           05  FILLER               PIC X(08) VALUE SPACES.                     
           05  LCAH-SCHACC          PIC X(11) VALUE SPACES.                     
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  LCAH-BLK-NO          PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(08) VALUE SPACES.                     
           05  LCAH-UNIT-NO         PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(06) VALUE SPACES.                     
           05  LCAH-LOAN-TAG        PIC X(01) VALUE SPACES.                     
           05  FILLER               PIC X(08) VALUE SPACES.                     
           05  LCAH-CF-TYPE         PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(06) VALUE SPACES.                     
           05  LCAH-STAMP           PIC $$,$$$,$$$,$$9.99.                      
                                                                                
      *-----------------------------------------------------------*             
      *        LINKAGE FOR SUB-PROGRAM BP13C913                   *             
      *-----------------------------------------------------------*             
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH      PIC X(04).                              
               10  WS-LINK-NUM-ACC      PIC X(05).                              
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-FD00-READ        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-ROUTINE  THRU 4000-EXIT                         
             UNTIL WS-FD00-EOF         = 'Y'.                                   
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13FD00                                                 
                       BP13K640                                                 
                       BP13K800                                                 
                       B05K006                                                  
               OUTPUT  BP13LCAH.                                                
                                                                                
           IF WS-K006-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K006 STATUS IS ' WS-K006-STATUS            
              MOVE WS-K006-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K640-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K640-STATUS IS ' WS-K640-STATUS            
              MOVE WS-K640-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K800-STATUS IS ' WS-K800-STATUS            
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           INITIALIZE WS-FD00-READ                                              
                      WS-K640-READ                                              
                      WS-K800-READ                                              
                      WS-K006-READ                                              
                      WS-LCAH-WRITE.                                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
           MOVE CORRESPONDING WS-CURRENT-DATE TO LCAH-CURRENT-DATE.             
           PERFORM 5000-WRITE-HEADER     THRU 5000-EXIT.                        
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       2000-FD00-READ.                                                          
      *****************************************************************         
           READ BP13FD00 AT END                                                 
                MOVE 'Y'                 TO WS-FD00-EOF                         
                GO                       TO 2000-EXIT.                          
                                                                                
           ADD 1                         TO WS-FD00-READ.                       
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       3000-K006-READ.                                                          
      *****************************************************************         
                                                                                
           MOVE FD00-NUM-SCH-ACCNT TO K006-HDB-REF-SF.                          
           READ B05K006.                                                        
                                                                                
           IF WS-K006-STATUS = 00 OR 02                                         
              IF K006-AMT-AVAIL-BAL NOT NUMERIC                                 
                 MOVE ZEROES               TO LCAH-STAMP                        
              ELSE                                                              
                 MOVE K006-AMT-AVAIL-BAL   TO LCAH-STAMP                        
              END-IF                                                            
           ELSE                                                                 
              IF WS-K006-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K006, '                       
                    K006-HDB-REF-SF                                             
                 MOVE ZEROES               TO LCAH-STAMP                        
              ELSE                                                              
                 MOVE WS-K006-STATUS       TO RETURN-CODE                       
                 DISPLAY 'ERROR READING BP13K006, ' K006-HDB-REF-SF             
                 MOVE ZEROES               TO LCAH-STAMP                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       3500-K800-READ.                                                          
      *****************************************************************         
                                                                                
           MOVE FD00-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00 OR 02                                         
              MOVE K800-NUM-CF-TYPE TO LCAH-CF-TYPE                             
              MOVE K800-NUM-LOAN-TAG  TO LCAH-LOAN-TAG                          
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K800, ' K800-NUM-REGN         
              ELSE                                                              
                 MOVE WS-K800-STATUS       TO RETURN-CODE                       
                 DISPLAY 'ERROR READING BP13K800, ' K800-NUM-REGN               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       3700-K640-READ.                                                          
      *****************************************************************         
                                                                                
           MOVE FD00-NUM-REGN TO K640-REGN-NO.                                  
           READ BP13K640.                                                       
                                                                                
           IF WS-K640-STATUS = 00 OR 02                                         
              MOVE K640-NUM-LOAN-TAG  TO LCAH-LOAN-TAG                          
           ELSE                                                                 
              IF WS-K640-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K640, ' K640-REGN-NO          
              ELSE                                                              
                 MOVE WS-K640-STATUS       TO RETURN-CODE                       
                 DISPLAY 'ERROR READING BP13K640, ' K640-REGN-NO                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       4000-PROCESS-ROUTINE.                                                    
      *****************************************************************         
                                                                                
           PERFORM 3000-K006-READ  THRU 3000-EXIT.                              
           PERFORM 3500-K800-READ  THRU 3500-EXIT.                              
      *    PERFORM 3700-K640-READ  THRU 3700-EXIT.                              
           PERFORM 6000-CHECK-PIDB THRU 6000-EXIT.                              
           MOVE FD00-NUM-PROJECT   TO LCAH-PROJECT.                             
           MOVE FD00-NUM-SCH-ACCNT TO LCAH-SCHACC.                              
                                                                                
           IF WS-LINE-CNT > 50                                                  
              PERFORM 5000-WRITE-HEADER  THRU 5000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-HEAD3-SW = 'N'                                                 
              PERFORM 5200-WRITE-HEADER3 THRU 5200-EXIT                         
              MOVE 'Y'                   TO WS-HEAD3-SW                         
           END-IF.                                                              
                                                                                
           WRITE BP13LCAH-REC FROM LCAH-DETAILS.                                
           ADD 1                         TO WS-LINE-CNT.                        
                                                                                
           PERFORM 2000-FD00-READ THRU 2000-EXIT.                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-WRITE-HEADER.                                                       
      *****************************************************************         
            ADD 1               TO WS-PAGE-CNT.                                 
            MOVE WS-PAGE-CNT    TO LCAH-PAGE.                                   
            MOVE SPACES         TO BP13LCAH-REC.                                
            WRITE BP13LCAH-REC  FROM LCAH-HEADING1 AFTER PAGE.                  
            WRITE BP13LCAH-REC  FROM LCAH-HEADING2.                             
            WRITE BP13LCAH-REC  FROM WS-BLANK-LINE.                             
                                                                                
            MOVE 'N'            TO WS-HEAD3-SW.                                 
            MOVE 3              TO WS-LINE-CNT.                                 
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       5200-WRITE-HEADER3.                                                      
      *****************************************************************         
           WRITE BP13LCAH-REC FROM WS-BLANK-LINE.                               
           WRITE BP13LCAH-REC FROM LCAH-HEADING3.                               
                                                                                
           ADD  3              TO WS-LINE-CNT.                                  
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       6000-CHECK-PIDB.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                     TO BP13COMM8-REC.                    
           INITIALIZE                         BP13COMM8-REC.                    
                                                                                
           MOVE FD00-NUM-SCH-ACCNT         TO WS-LINK-REC.                      
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0 AND 100                                  
              DISPLAY 'ERROR IN READING PIDB TABLE  '                           
                      COMM8-CDE-SYSERR                                          
           ELSE                                                                 
              IF COMM8-CDE-SYSERR = 0                                           
                 MOVE COMM8-NUM-BLK           TO LCAH-BLK-NO                    
                 STRING '#' COMM8-NUM-LEVEL                                     
                        '-' COMM8-NUM-UNIT-MAIN                                 
                    DELIMITED BY SIZE       INTO LCAH-UNIT-NO                   
                 END-STRING                                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
           DISPLAY '  CONTROL LIST - BP13CCAH   '                               
           DISPLAY '  '.                                                        
           DISPLAY '  ** INPUT COUNT **         '.                              
           DISPLAY 'A) TOTAL FD00 REC READ      = ' WS-FD00-READ.               
           DISPLAY 'B) TOTAL AB05 REC READ      = ' WS-K006-READ.               
           DISPLAY '  '.                                                        
           DISPLAY ' **  OUTPUT COUNT **        '.                              
           DISPLAY '  '.                                                        
           DISPLAY 'C) TOTAL RECORDS WRITTEN   = ' WS-LCAH-WRITE.               
                                                                                
           CLOSE      BP13FD00                                                  
                      BP13K800                                                  
                      BP13K640                                                  
                      B05K006                                                   
                      BP13LCAH.                                                 
                                                                                
           IF WS-K006-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR K006 STATUS IS ' WS-K006-STATUS            
              MOVE WS-K006-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K640-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR K640 STATUS IS ' WS-K640-STATUS            
              MOVE WS-K640-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR K800 STATUS IS ' WS-K800-STATUS            
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
         9000-EXIT.                                                             
           EXIT.                                                                
      *====================  END OF PROGRAM  ====================*              
