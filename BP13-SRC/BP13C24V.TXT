      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C24V.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  19 SEPTEMBER 2014.                                        
      *--------------------------------------------------------------*          
      *                                                              *          
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)                 *          
      *                                                              *          
      *    SYSTEM ID   : BP13                                        *          
      *                                                              *          
      *    OBJECTIVES :  TO SORT CANCEL CASES WITH APPOINTMENT       *          
      *                  ACCORDING TO BP13F205 INPUT                 *          
      *                                                              *          
      *    INPUT FILES  : BP13F800                                   *          
      *                 : BP13F205                                   *          
      *                 : BP13K820                                   *          
      *    OUTPUT       : BP13L24X  - REMOVED                        *          
      *    OUTPUT       : BP13K288                                   *          
      *--------------------------------------------------------------*          
      * CHG-NO  BY   DATE     DETAILS                                *          
      *-------- ---- -------- ---------------------------------------*          
      *BP135468 ESA1 19092014 NEW PROGRAM                            *          
      *BP135468 KAM4 18112014 REVISED PROGRAM AND WRITE TO BP13K288  *          
      *BP135563 KAM4 03022015 TO CORRECT MOVING OF NUM-QUEUE TO K288 *          
      *BP135561 KAM4 25022015 TO CATER FOR K200-NUM-HHTY-QUEUE-SERIAL*          
      *-------- ---- -------- ---------------------------------------*          
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN        TO BP13F205.                           
                                                                                
           SELECT BP13F800 ASSIGN        TO BP13F800.                           
                                                                                
           SELECT BP13K200                                                      
                           ASSIGN        TO BP13K200                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K200-KEY-FLD                        
                           ALTERNATE KEY IS K200-NUM-REGN                       
                           FILE STATUS   IS WS-K200-STATUS.                     
                                                                                
           SELECT BP13K820 ASSIGN        TO BP13K820                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K820-KEY-FLD                        
                           FILE STATUS   IS WS-K820-STATUS.                     
                                                                                
           SELECT BP13K813  ASSIGN        TO BP13K813                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K813-KEY-FLD                       
                            FILE STATUS   IS WS-K813-STATUS.                    
                                                                                
           SELECT BP13K288 ASSIGN        TO BP13K288                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K288-KEY-FLD                        
                           FILE STATUS   IS WS-K288-STATUS.                     
                                                                                
           SELECT SY02F001 ASSIGN        TO SY02F001.                           
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            RECORDING MODE  IS F.                                               
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE  IS F.                                               
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K200                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13K200.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD  BP13K288                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K288.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F800-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-F205-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-F001-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-K288-FOUND              PIC X(1)  VALUE 'N'.                  
           05  WS-K200-FND                PIC X(1)  VALUE 'N'.                  
           05  WS-STOP                    PIC X(1)  VALUE 'N'.                  
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-EDITED-DATE             PIC X(10) VALUE SPACES.               
           05  WS-NUM-NEW-TOWN            PIC X(20) VALUE SPACES.               
           05  WS-HA1-NAME                PIC X(66) VALUE SPACES.               
           05  WS-K288-KEY                PIC X(29) VALUE SPACES.               
           05  WS-K200-STATUS             PIC 9(2)  VALUE 0.                    
           05  WS-K820-STATUS             PIC 9(2)  VALUE 0.                    
           05  WS-K813-STATUS             PIC 9(2)  VALUE 0.                    
           05  WS-K288-STATUS             PIC 9(2)  VALUE 0.                    
           05  WS-CNT-F800-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F205-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-K288-WRITE          PIC 9(8)  VALUE 0.                    
           05  WS-CNT-K288-DELETED        PIC 9(8)  VALUE 0.                    
           05  WS-K200-NOTFND-CNT         PIC 9(8)  VALUE 0.                    
           05  WS-CAN                     PIC 9(8)  VALUE 0.                    
           05  WS-CUR                     PIC 9(8)  VALUE 0.                    
           05  WS-SNO                     PIC 9(5)  VALUE 0.                    
           05  WS-DAYS-CANCEL             PIC 9(3)  VALUE 0.                    
           05  WS-F800-COUNTER            PIC 9(2)  VALUE 0.                    
           05  WS-SAVE-DTE-BK             PIC X(8)  VALUE SPACES.               
           05  WS-NUM-QUEUE               PIC X(5)  VALUE SPACES.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-CURR-DTE                PIC 9(8)  VALUE ZEROES.               
                                                                                
       01  WS-F800-DTE-CANCEL.                                                  
           05  WS-DTE-CAN-CHAR            PIC X(8) VALUE SPACES.                
           05  WS-DTE-CAN-NUM  REDEFINES  WS-DTE-CAN-CHAR.                      
               10  WS-DTE-CANCEL-NUM   PIC 9(8).                                
                                                                                
       01  WS-TIME-HHMMSSCC.                                                    
           05  WS-TIME.                                                         
               10  WS-HH               PIC X(2).                                
               10  WS-MM               PIC X(2).                                
               10  WS-SS               PIC X(2).                                
           05  WS-TIME-CC.                                                      
               10  WS-CC               PIC X(2).                                
                                                                                
       01  WS-EDITED-TIME.                                                      
           05  WS-SYS-TIME             PIC X(8).                                
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 1050-READ-SY02F001     THRU 1050-EXIT.                       
           PERFORM 1100-PROCESS           THRU 1100-EXIT                        
               UNTIL WS-F800-EOF = 'Y'.                                         
                                                                                
           IF WS-CNT-F205-READ = ZERO                                           
              DISPLAY 'NO RECORD FOUND IN BP13F205.'                            
           END-IF.                                                              
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13F800                                                 
                       SY02F001                                                 
                       BP13K200                                                 
                       BP13K820                                                 
                       BP13K813                                                 
                I-O    BP13K288.                                                
                                                                                
           IF WS-K200-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K200 STATUS ' WS-K200-STATUS               
              MOVE  WS-K200-STATUS              TO RETURN-CODE                  
              PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT                    
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K820 STATUS ' WS-K820-STATUS               
              MOVE  WS-K820-STATUS              TO RETURN-CODE                  
              PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT                    
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K813 STATUS ' WS-K813-STATUS               
              MOVE  WS-K813-STATUS              TO RETURN-CODE                  
              PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT                    
           END-IF.                                                              
                                                                                
           IF WS-K288-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K288 STATUS ' WS-K288-STATUS               
              MOVE  WS-K288-STATUS              TO RETURN-CODE                  
              PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT                    
           END-IF.                                                              
                                                                                
      *    MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
      *                                                                         
      *    STRING WS-CURR-DTE(7:2) '/' WS-CURR-DTE(5:2) '/'                     
      *           WS-CURR-DTE(1:4) DELIMITED BY SIZE                            
      *           INTO WS-EDITED-DATE                                           
      *    END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1050-READ-SY02F001.                                                      
      *--------------------------------------------------------------*          
                                                                                
           READ SY02F001                                                        
                AT END  MOVE 'Y' TO WS-F001-EOF                                 
                GO TO 1050-EXIT                                                 
           END-READ.                                                            
                                                                                
           MOVE F001-DTE-CURRENT TO WS-SYSTEM-DATE.                             
                                                                                
           STRING WS-CURR-DTE(7:2) '/' WS-CURR-DTE(5:2) '/'                     
                  WS-CURR-DTE(1:4) DELIMITED BY SIZE                            
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1100-PROCESS.                                                            
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1200-READ-F205         THRU 1200-EXIT.                       
           PERFORM 1500-READ-F800         THRU 1500-EXIT.                       
                                                                                
      *    DISPLAY 'F205-DTE-BALLOT   : ' F205-DTE-BALLOT                       
      *    DISPLAY 'F205-NUM-SELECTION: ' F205-NUM-SELECTION                    
      **DELETE ALL CASES THAT MATCH WITH DTE-BALLOT AND ALLO-CAT**              
           MOVE SPACES                    TO K288-KEY-FLD.                      
           MOVE F205-DTE-BALLOT           TO K288-DTE-BALLOT.                   
           PERFORM 1700-START-DEL-K288    THRU 1700-EXIT.                       
                                                                                
           PERFORM 2000-PROCESS           THRU 2000-EXIT                        
                UNTIL WS-F800-EOF = 'Y'.                                        
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1200-READ-F205.                                                          
      *--------------------------------------------------------------*          
                                                                                
           READ BP13F205                                                        
                AT END  MOVE 'Y' TO WS-F205-EOF                                 
                GO TO 1200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F205-READ.                                           
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1500-READ-F800.                                                          
      *--------------------------------------------------------------*          
                                                                                
           READ BP13F800                                                        
                AT END  MOVE 'Y' TO WS-F800-EOF                                 
                GO TO 1500-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F800-READ.                                           
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1700-START-DEL-K288.                                                     
      *--------------------------------------------------------------*          
           MOVE 'N'           TO WS-STOP.                                       
           START BP13K288 KEY >  K288-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K288-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                                                                                
                 PERFORM 1800-READNEXT-K288    THRU 1800-EXIT                   
                    UNTIL K288-DTE-BALLOT > F205-DTE-BALLOT OR                  
                          WS-STOP = 'Y'                                         
              WHEN 10                                                           
              WHEN 23                                                           
                   DISPLAY 'NO RECORD TO DELETE IN BP13K288.'                   
                                                                                
              WHEN OTHER                                                        
                DISPLAY 'ERROR WITH START READ OF BP13K288. STATUS IS '         
                       WS-K288-STATUS                                           
                MOVE WS-K288-STATUS              TO RETURN-CODE                 
                PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                   
           END-EVALUATE.                                                        
       1700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1800-READNEXT-K288.                                                      
      *--------------------------------------------------------------*          
           READ BP13K288 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K288-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF (K288-DTE-BALLOT   = F205-DTE-BALLOT) AND                
                       (K288-NUM-ALLO-CAT = F205-NUM-SELECTION)                 
                        PERFORM 1900-DELETE-BP13K288 THRU 1900-EXIT             
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'   TO WS-STOP                                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K288 : READNEXT ERROR. STATUS IS '             
                             WS-K288-STATUS                                     
                    MOVE WS-K288-STATUS                TO RETURN-CODE           
                    PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       1800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       1900-DELETE-BP13K288.                                                    
      *-------------------------------------------------------------*           
           DELETE BP13K288.                                                     
                                                                                
           EVALUATE WS-K288-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO WS-CNT-K288-DELETED                                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WITH WRITING OF BP13K288. STATUS IS '        
                             WS-K288-STATUS                                     
                    MOVE WS-K288-STATUS              TO RETURN-CODE             
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       1900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-PROCESS.                                                            
      *--------------------------------------------------------------*          
           MOVE ZEROES                    TO WS-DAYS-CANCEL.                    
           MOVE SPACES                    TO WS-SAVE-DTE-BK                     
                                             WS-NUM-QUEUE.                      
           MOVE 'N'                       TO WS-K200-FND.                       
                                                                                
           IF F800-DTE-CANCEL NOT NUMERIC                                       
              MOVE ZEROES                 TO F800-DTE-CANCEL                    
           END-IF.                                                              
                                                                                
           MOVE F800-DTE-CANCEL        TO WS-DTE-CAN-CHAR.                      
                                                                                
           MOVE ZEROES   TO WS-CUR  WS-CAN                                      
                                                                                
           IF WS-DTE-CANCEL-NUM = ZEROES                                        
              DISPLAY 'CANCEL DATE IS ZEROES. REGN : ' F800-NUM-REGN            
              DISPLAY '  '                                                      
              PERFORM 1500-READ-F800               THRU 1500-EXIT               
              GO TO 2000-EXIT                                                   
           END-IF.                                                              
                                                                                
           COMPUTE WS-CUR = FUNCTION INTEGER-OF-DATE(WS-CURR-DTE)               
           COMPUTE WS-CAN = FUNCTION INTEGER-OF-DATE(WS-DTE-CANCEL-NUM)         
           COMPUTE WS-DAYS-CANCEL = WS-CUR - WS-CAN                             
                                                                                
           IF F800-NUM-MGPS = SPACES OR LOW-VALUES                              
              IF F800-DTE-BK-APPT NOT = SPACES AND LOW-VALUES AND ZEROES        
                 IF ((F205-DTE-BALLOT    =  F800-DTE-BALLOT)   AND              
                     (F205-NUM-SELECTION =  F800-NUM-ALLO-CAT) AND              
                     (F800-DTE-BK-APPT  >=  F001-DTE-CURRENT)  AND              
                     (F800-DTE-BK-APPT  >=  F800-DTE-CANCEL)   AND              
                     (WS-DAYS-CANCEL    <=  5))                                 
                      MOVE F800-DTE-BK-APPT            TO WS-SAVE-DTE-BK        
                      MOVE F800-NUM-NT-FT-QUEUE        TO WS-NUM-QUEUE          
                      PERFORM 3000-MOVE-WRITE-DTLS   THRU 3000-EXIT             
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 2100-READ-BP13K200          THRU 2100-EXIT             
                 IF WS-K200-FND = 'Y'                                           
                    IF ((F205-DTE-BALLOT    = F800-DTE-BALLOT)      AND         
                        (F205-NUM-SELECTION = F800-NUM-ALLO-CAT)    AND         
                        (K200-DTE-BKAPPMT-DATE >= F001-DTE-CURRENT) AND         
                        (K200-DTE-BKAPPMT-DATE >= F800-DTE-CANCEL)  AND         
                        (WS-DAYS-CANCEL        <= 5))                           
                         MOVE K200-DTE-BKAPPMT-DATE    TO WS-SAVE-DTE-BK        
                         MOVE K200-NUM-HHTY-QUEUE-SERIAL TO WS-NUM-QUEUE        
                         PERFORM 3000-MOVE-WRITE-DTLS  THRU 3000-EXIT           
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF F800-DTE-BK-APPT-MGPS NOT = SPACES AND LOW-VALUES              
                                         AND ZEROES                             
                IF ((F205-DTE-BALLOT    = F800-DTE-BALLOT)      AND             
                    (F205-NUM-SELECTION = F800-NUM-ALLO-CAT)    AND             
                    (F800-DTE-BK-APPT-MGPS >= F001-DTE-CURRENT) AND             
                    (F800-DTE-BK-APPT-MGPS >= F800-DTE-CANCEL)  AND             
                    (WS-DAYS-CANCEL        <= 5))                               
                     MOVE F800-DTE-BK-APPT-MGPS      TO WS-SAVE-DTE-BK          
                     MOVE F800-NUM-QUEUE-MGPS        TO WS-NUM-QUEUE            
                     PERFORM 3000-MOVE-WRITE-DTLS  THRU 3000-EXIT               
                END-IF                                                          
             ELSE                                                               
               PERFORM 2100-READ-BP13K200          THRU 2100-EXIT               
               IF WS-K200-FND = 'Y'                                             
                 IF ((F205-DTE-BALLOT   = F800-DTE-BALLOT)   AND                
                    (F205-NUM-SELECTION = F800-NUM-ALLO-CAT) AND                
                    (K200-DTE-BKAPPMT-DATE-MGPS >= F001-DTE-CURRENT) AND        
                    (K200-DTE-BKAPPMT-DATE-MGPS >= F800-DTE-CANCEL)  AND        
                    (WS-DAYS-CANCEL             <= 5))                          
                     MOVE K200-DTE-BKAPPMT-DATE-MGPS TO WS-SAVE-DTE-BK          
                     MOVE K200-NUM-MGPS-QUEUE-SERIAL TO WS-NUM-QUEUE            
                     PERFORM 3000-MOVE-WRITE-DTLS  THRU 3000-EXIT               
                 END-IF                                                         
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
           PERFORM 1500-READ-F800               THRU 1500-EXIT.                 
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2100-READ-BP13K200.                                                      
      *--------------------------------------------------------------*          
           MOVE SPACES                    TO BP13K200-REC.                      
           INITIALIZE                        BP13K200-REC.                      
           MOVE F800-NUM-REGN             TO K200-NUM-REGN.                     
                                                                                
           READ BP13K200 KEY IS K200-NUM-REGN.                                  
           EVALUATE WS-K200-STATUS                                              
           WHEN 00                                                              
                MOVE 'Y'                  TO WS-K200-FND                        
           WHEN 23                                                              
                ADD   1                   TO WS-K200-NOTFND-CNT                 
                DISPLAY 'BP13K200 NOT FOUND REGN: ' K200-NUM-REGN               
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K200 - ' WS-K200-STATUS              
                MOVE WS-K200-STATUS            TO RETURN-CODE                   
                PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       3000-MOVE-WRITE-DTLS.                                                    
      *--------------------------------------------------------------*          
           MOVE SPACES                    TO K813-KEY-FLD.                      
           IF F800-NUM-ALLO-CAT = 'BTO'                                         
              MOVE F800-NUM-BTO-ZONE      TO K813-NUM-ZONE                      
           ELSE                                                                 
             IF F800-NUM-ALLO-CAT = 'SBF'                                       
                MOVE F800-NUM-NEW-TOWN    TO K813-NUM-ZONE                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-BTO-FLAT-TYPE    TO K813-NUM-FLAT-TYPE.                
           MOVE F800-DTE-BALLOT           TO K813-DTE-BALLOT.                   
                                                                                
           PERFORM 3100-READ-BP13K813   THRU 3100-EXIT.                         
                                                                                
           MOVE SPACES                    TO BP13K288-REC.                      
           MOVE F800-DTE-BALLOT           TO K288-DTE-BALLOT.                   
           MOVE F800-NUM-REGN             TO K288-NUM-REGN.                     
           MOVE F800-DTE-CANCEL           TO K288-DTE-CANCEL.                   
           MOVE F800-NUM-ALLO-CAT         TO K288-NUM-ALLO-CAT.                 
           MOVE F800-NUM-NEW-TOWN         TO K288-NUM-NT-ZONE.                  
           MOVE K813-NUM-SITE             TO K288-NUM-SITE.                     
                                                                                
           MOVE F800-NUM-FLAT-TYPE        TO K288-NUM-FLAT-TYPE.                
           MOVE WS-NUM-QUEUE              TO K288-NUM-NT-FT-QUEUE.              
           MOVE WS-SAVE-DTE-BK            TO K288-DTE-BK-APPT.                  
                                                                                
           PERFORM 3300-GET-HA1-NAME    THRU 3300-EXIT.                         
           MOVE WS-HA1-NAME               TO K288-NME-HA1.                      
                                                                                
           MOVE F205-NUM-USERID           TO K288-NUM-USERID.                   
                                                                                
           ACCEPT  WS-TIME-HHMMSSCC     FROM TIME.                              
           MOVE WS-TIME-HHMMSSCC          TO WS-SYS-TIME.                       
                                                                                
           MOVE F205-DTE-UPDATE           TO K288-DTE-UPDATE.                   
           MOVE WS-EDITED-TIME            TO K288-TME-UPDATE.                   
           DISPLAY '---> WS-EDITED-TIME : ' WS-EDITED-TIME.                     
           DISPLAY '---> K288-TME-UPDATE: ' K288-TME-UPDATE.                    
                                                                                
           PERFORM 3400-WRITE-BP13K288    THRU 3400-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       3100-READ-BP13K813.                                                      
      *-------------------------------------------------------------*           
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    MOVE SPACES            TO K813-NUM-SITE                     
                    DISPLAY 'RECORD NOT FOUND IN BP13K813 FOR'                  
                    DISPLAY 'KEY :' K813-KEY-FLD                                
                    DISPLAY 'F205-NUM-SELECTION : ' F205-NUM-SELECTION          
                    DISPLAY 'F800-NUM-ALLO-CAT : ' F800-NUM-ALLO-CAT            
                    DISPLAY 'F800-NUM-REGN : ' F800-NUM-REGN                    
                    DISPLAY ' '                                                 
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K813!'                           
                    DISPLAY 'KEY :' K813-KEY-FLD                                
                    DISPLAY 'FILE STATUS : ' WS-K813-STATUS                     
                    DISPLAY '+++++++++++++++++++++++++++++++++++'               
                    DISPLAY ' '                                                 
           END-EVALUATE.                                                        
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       3300-GET-HA1-NAME.                                                       
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES                TO WS-HA1-NAME.                           
           MOVE SPACES                TO K820-KEY-FLD.                          
           MOVE F800-NUM-REGN         TO K820-NUM-REGN.                         
           MOVE F800-NUM-NRIC1        TO K820-NUM-NRIC.                         
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              MOVE K820-NME-OCCP      TO WS-HA1-NAME                            
           ELSE                                                                 
              IF WS-K820-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY: '                  
                         K820-KEY-FLD                                           
              ELSE                                                              
                 DISPLAY 'ERROR WITH READING BP13K820, STATUS '                 
                          WS-K820-STATUS                                        
                         ', KEY: ' K820-KEY-FLD                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       3400-WRITE-BP13K288.                                                     
      *-------------------------------------------------------------*           
           WRITE BP13K288-REC.                                                  
                                                                                
           IF WS-K288-STATUS = 00                                               
              DISPLAY '+++BP13K288 WRITE OK.'                                   
              ADD 1        TO WS-CNT-K288-WRITE                                 
           ELSE                                                                 
              DISPLAY 'BP13K288 - WRITE ERROR, STAT : ' WS-K288-STATUS          
              DISPLAY ' KEY: ' K288-KEY-FLD                                     
              DISPLAY ' '                                                       
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
                                                                                
           CLOSE BP13F205                                                       
                 BP13F800                                                       
                 SY02F001                                                       
                 BP13K200                                                       
                 BP13K820                                                       
                 BP13K813                                                       
                 BP13K288.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K820-STAT ' WS-K820-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR WS-K813-STATUS:' WS-K813-STATUS            
           END-IF.                                                              
                                                                                
           IF WS-K288-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K288-STAT ' WS-K288-STATUS             
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '********* BP13C24V CONTROL *********'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F205) = ' WS-CNT-F205-READ.                
           DISPLAY 'NO. OF READ (BP13F800) = ' WS-CNT-F800-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECS WRITE  (K288) = ' WS-CNT-K288-WRITE             
           DISPLAY 'NO. OF RECS DELETED(K288) = ' WS-CNT-K288-DELETED           
           DISPLAY SPACES.                                                      
           DISPLAY '************************************'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
