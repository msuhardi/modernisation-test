       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF07.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  29/04/10.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO CHECK FOR CASES THAT MC SUMB DATE IS     *            
      *              BLANK AND DEFER DUE DATE IS = CURRENT DATE    *            
      *  INPUT FILE :  1. BP13F399                                 *            
      *  OUTPUT FILE:  1. BP13MAIL                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133681 ZDD 20100429   NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F399 ASSIGN TO BP13F399.                                  
                                                                                
            SELECT BP13K022 ASSIGN TO BP13K022                                  
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
                                                                                
            SELECT BP13K820 ASSIGN TO BP13K820                                  
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS WS-K820-STATUS.                            
                                                                                
            SELECT BP13K800 ASSIGN TO BP13K800                                  
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
            SELECT BP13K410 ASSIGN TO BP13K410                                  
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K410-KEY-FLD                               
                  FILE STATUS     IS WS-K410-STATUS.                            
                                                                                
            SELECT BM06K110 ASSIGN TO BM06K110                                  
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
            SELECT BP13K857 ASSIGN TO BP13K857                                  
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K857-KEY-FLD                               
                  FILE STATUS     IS WS-K857-STATUS.                            
                                                                                
           SELECT BP13MAIL ASSIGN TO BP13MAIL.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F399                                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F399.                                                           
                                                                                
       FD  BP13MAIL                                                             
           RECORD CONTAINS 267 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13MAIL-REC.                                                        
           05  MAIL-CSM            PIC X(05).                                   
           05  MAIL-REGN-NUM       PIC X(08).                                   
           05  MAIL-HA1-UIN        PIC X(09).                                   
           05  MAIL-HA1-NAME       PIC X(30).                                   
           05  MAIL-PHONE-NUM-1    PIC X(08).                                   
           05  MAIL-HA2-UIN        PIC X(09).                                   
           05  MAIL-HA2-NAME       PIC X(30).                                   
           05  MAIL-PHONE-NUM-2    PIC X(08).                                   
           05  MAIL-HA3-UIN        PIC X(09).                                   
           05  MAIL-HA3-NAME       PIC X(30).                                   
           05  MAIL-PHONE-NUM-3    PIC X(08).                                   
           05  MAIL-HA4-UIN        PIC X(09).                                   
           05  MAIL-HA4-NAME       PIC X(30).                                   
           05  MAIL-PHONE-NUM-4    PIC X(08).                                   
           05  MAIL-DEFER-DTE      PIC X(08).                                   
           05  MAIL-DTE-KEY-AVAIL  PIC X(08).                                   
           05  MAIL-EMAIL          PIC X(50).                                   
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F399-READ        PIC 9(5)    VALUE ZEROS.                     
           05  NO-F399-WRITTEN     PIC 9(5)    VALUE ZEROS.                     
           05  WS-BYPASS           PIC 9(5)    VALUE ZEROS.                     
           05  WS-DATE             PIC X(8)    VALUE SPACES.                    
           05  WS-EOF              PIC X(1)    VALUE 'N'.                       
           05  WS-K022-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-K820-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-K800-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-K410-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-K110-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-K857-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-FOUND            PIC X(1)    VALUE 'N'.                       
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F399        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F399                                                 
                       BP13K022                                                 
                       BP13K820                                                 
                       BP13K800                                                 
                       BP13K410                                                 
                       BM06K110                                                 
                       BP13K857                                                 
                OUTPUT BP13MAIL.                                                
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 OPEN ERROR, STATUS IS ' WS-K022-STATUS          
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 OPEN ERROR, STATUS IS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 OPEN ERROR, STATUS IS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 OPEN ERROR, STATUS IS ' WS-K410-STATUS          
              MOVE WS-K410-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 OPEN ERROR, STATUS IS ' WS-K110-STATUS          
              MOVE WS-K110-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K957 OPEN ERROR, STATUS IS ' WS-K857-STATUS          
              MOVE WS-K857-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F399.                                                          
      *-------------------------------------------------------------            
            READ BP13F399 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 GO TO 2000-EXIT.                                               
            ADD 1 TO NO-F399-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           PERFORM 4200-PROCESS-RECORD  THRU 4200-EXIT                          
           PERFORM 2000-READ-F399 THRU 2000-EXIT.                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES       TO BP13K022-REC                                   
                                 BP13MAIL-REC                                   
                                 BP13K820-REC                                   
                                 BP13K857-REC.                                  
                                                                                
            IF F399-DTE-MC-DEFER = WS-DATE                                      
               MOVE F399-NUM-REGN            TO MAIL-REGN-NUM                   
                                                K022-NUM-REGN                   
                                                K800-NUM-REGN                   
                                                K857-NUM-REGN                   
               PERFORM 4500-READ-BP13K800    THRU 4500-EXIT                     
                 IF WS-FOUND = 'Y'                                              
                    MOVE K800-NUM-SCH-ACC   TO  K110-SCH-ACC-NO                 
                    IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES               
                       MOVE K800-NUM-REGN   TO K820-NUM-REGN                    
                       MOVE K800-NUM-NRIC1  TO K820-NUM-NRIC                    
                                               MAIL-HA1-UIN                     
                       PERFORM 4400-READ-BP13K820    THRU 4400-EXIT             
                       IF WS-FOUND = 'Y'                                        
                          MOVE K820-NME-OCCP       TO MAIL-HA1-NAME             
                          IF K820-NUM-PHONE = SPACES AND LOW-VALUES             
                             MOVE K820-NUM-HP-PGR  TO MAIL-PHONE-NUM-1          
                          ELSE                                                  
                             MOVE K820-NUM-PHONE   TO MAIL-PHONE-NUM-1          
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                                                                                
                    IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES               
                       MOVE K800-NUM-REGN   TO K820-NUM-REGN                    
                       MOVE K800-NUM-NRIC2  TO K820-NUM-NRIC                    
                                               MAIL-HA2-UIN                     
                       PERFORM 4400-READ-BP13K820    THRU 4400-EXIT             
                       IF WS-FOUND = 'Y'                                        
                          IF K820-NUM-PHONE = SPACES AND LOW-VALUES             
                             MOVE K820-NUM-HP-PGR  TO MAIL-PHONE-NUM-2          
                          ELSE                                                  
                             MOVE K820-NUM-PHONE   TO MAIL-PHONE-NUM-2          
                          END-IF                                                
                          MOVE K820-NME-OCCP       TO MAIL-HA2-NAME             
                       END-IF                                                   
                    END-IF                                                      
                                                                                
                    IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES               
                       MOVE K800-NUM-REGN   TO K820-NUM-REGN                    
                       MOVE K800-NUM-NRIC3  TO K820-NUM-NRIC                    
                                               MAIL-HA3-UIN                     
                       PERFORM 4400-READ-BP13K820    THRU 4400-EXIT             
                       IF WS-FOUND = 'Y'                                        
                          IF K820-NUM-PHONE = SPACES AND LOW-VALUES             
                             MOVE K820-NUM-HP-PGR  TO MAIL-PHONE-NUM-3          
                          ELSE                                                  
                             MOVE K820-NUM-PHONE   TO MAIL-PHONE-NUM-3          
                          END-IF                                                
                          MOVE K820-NME-OCCP       TO MAIL-HA3-NAME             
                       END-IF                                                   
                    END-IF                                                      
                                                                                
                    IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES               
                       MOVE K800-NUM-REGN   TO K820-NUM-REGN                    
                       MOVE K800-NUM-NRIC4  TO K820-NUM-NRIC                    
                                               MAIL-HA4-UIN                     
                       PERFORM 4400-READ-BP13K820    THRU 4400-EXIT             
                       IF WS-FOUND = 'Y'                                        
                          IF K820-NUM-PHONE = SPACES AND LOW-VALUES             
                             MOVE K820-NUM-HP-PGR  TO MAIL-PHONE-NUM-4          
                          ELSE                                                  
                             MOVE K820-NUM-PHONE   TO MAIL-PHONE-NUM-4          
                          END-IF                                                
                          MOVE K820-NME-OCCP       TO MAIL-HA4-NAME             
                       END-IF                                                   
                    END-IF                                                      
                                                                                
                 ELSE                                                           
                    PERFORM 4600-READ-BP13K410    THRU 4600-EXIT                
                    IF WS-FOUND = 'Y'                                           
                       MOVE K410-NUM-SCH-ACC    TO K110-SCH-ACC-NO              
                       IF K410-NRIC1 NOT = SPACES AND LOW-VALUES                
                          MOVE K410-NRIC1 TO MAIL-HA1-UIN                       
                          MOVE K410-NAME1 TO MAIL-HA1-NAME                      
                       END-IF                                                   
                                                                                
                       IF K410-NRIC2 NOT = SPACES AND LOW-VALUES                
                          MOVE K410-NRIC2 TO MAIL-HA2-UIN                       
                          MOVE K410-NAME2 TO MAIL-HA2-NAME                      
                       END-IF                                                   
                                                                                
                       IF K410-NRIC3 NOT = SPACES AND LOW-VALUES                
                          MOVE K410-NRIC3 TO MAIL-HA3-UIN                       
                          MOVE K410-NAME3 TO MAIL-HA3-NAME                      
                       END-IF                                                   
                                                                                
                       IF K410-NRIC4 NOT = SPACES AND LOW-VALUES                
                          MOVE K410-NRIC4 TO MAIL-HA4-UIN                       
                          MOVE K410-NAME4 TO MAIL-HA4-NAME                      
                       END-IF                                                   
                 END-IF                                                         
                                                                                
            END-IF.                                                             
                                                                                
            IF F399-DTE-MC-DEFER NOT = WS-DATE                                  
               ADD 1 TO WS-BYPASS                                               
               GO TO 4200-EXIT                                                  
            END-IF.                                                             
                                                                                
            PERFORM 4700-READ-BM06K110    THRU 4700-EXIT                        
               IF WS-FOUND = 'Y'                                                
                  MOVE K110-DTE-KEY-AVAIL  TO  MAIL-DTE-KEY-AVAIL               
               END-IF                                                           
                                                                                
            PERFORM 4300-READ-BP13K022    THRU 4300-EXIT                        
               IF WS-FOUND = 'Y'                                                
                  MOVE K022-NUM-OIC        TO MAIL-CSM                          
               END-IF                                                           
                                                                                
            PERFORM 4800-READ-BP13K857    THRU 4800-EXIT                        
               IF WS-FOUND = 'Y'                                                
                  MOVE K857-NUM-EMAIL      TO MAIL-EMAIL                        
               END-IF                                                           
                                                                                
            MOVE F399-DTE-MC-DEFER        TO MAIL-DEFER-DTE.                    
            WRITE BP13MAIL-REC.                                                 
            ADD 1 TO NO-F399-WRITTEN.                                           
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4300-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN                                   
                                                                                
           EVALUATE WS-K022-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K022. STATUS IS '                   
                       WS-K022-STATUS                                           
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4400-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K820. STATUS IS '                   
                       WS-K820-STATUS                                           
              MOVE WS-K820-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4500-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K800. STATUS IS '                   
                       WS-K800-STATUS                                           
              MOVE WS-K800-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4600-READ-BP13K410.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K410. STATUS IS '                   
                       WS-K410-STATUS                                           
              MOVE WS-K410-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4700-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BM06K110. STATUS IS '                   
                       WS-K110-STATUS                                           
              MOVE WS-K110-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4800-READ-BP13K857.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K857. STATUS IS '                   
                       WS-K857-STATUS                                           
              MOVE WS-K857-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13MAIL                                                       
                 BP13F399                                                       
                 BP13K022                                                       
                 BP13K820                                                       
                 BP13K800                                                       
                 BP13K410                                                       
                 BM06K110                                                       
                 BP13K857.                                                      
                                                                                
           DISPLAY 'NO. OF F399 REC READ   > ' NO-F399-READ.                    
           DISPLAY 'NO. OF F399 REC WRITTEN> ' NO-F399-WRITTEN.                 
           DISPLAY 'NO. OF F399 REC BYPASS > ' WS-BYPASS.                       
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 CLOSE ERROR, STATUS IS ' WS-K022-STATUS         
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 CLOSE ERROR, STATUS IS ' WS-K820-STATUS         
              MOVE WS-K820-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 CLOSE ERROR, STATUS IS ' WS-K800-STATUS         
              MOVE WS-K800-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 CLOSE ERROR, STATUS IS ' WS-K410-STATUS         
              MOVE WS-K410-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 CLOSE ERROR, STATUS IS ' WS-K110-STATUS         
              MOVE WS-K110-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 CLOSE ERROR, STATUS IS ' WS-K857-STATUS         
              MOVE WS-K857-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
