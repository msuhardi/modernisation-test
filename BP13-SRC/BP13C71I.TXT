       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C71I.                                                 
      *AUTHOR.        LSB1.                                                     
      *DATE-WRITTEN.  07/07/2007.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  AFTER UPDATE COHORT FOR UNSUCC CASES        *            
      *                UPDATE SUCC-NT-FT = SPACES.                 *            
      *                                                            *            
      *  NOTE : BP13C717 - PROGRAM TO UPDATE SUCC WITHIN 100%      *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F740                                 *            
      *  I-O FILE   :  1. BP13K740                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133292 LSB 20070707 UPDATE SUCC -CDOE                    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS RANDOM                                           
                RECORD KEY IS K740-KEY-FLD                                      
                FILE STATUS IS K740-STATUS.                                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13K740                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K740.                                                           
                                                                                
       FD  BP13F740                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F740.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F740-READ        PIC 9(5)    VALUE ZEROS.                     
           05  NO-K740-REWRITE     PIC 9(5)    VALUE ZEROS.                     
           05  NO-SUCC             PIC 9(5)    VALUE ZEROS.                     
           05  NO-UNSUCC           PIC 9(5)    VALUE ZEROS.                     
                                                                                
       01  WS-DATE                  PIC X(8)  VALUE SPACES.                     
       01  WS-EOF                   PIC X     VALUE 'N'.                        
       01  K740-STATUS              PIC 99    VALUE 0.                          
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F740        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT BP13F740                                                  
                I-O   BP13K740.                                                 
                                                                                
           IF K740-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K740 - OPEN ERROR, STATUS: ' K740-STATUS             
              MOVE K740-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                    
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F740.                                                          
      *-------------------------------------------------------------            
            READ BP13F740 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 GO TO 2000-EXIT.                                               
            ADD 1 TO NO-F740-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           PERFORM 4200-UPDATE-K740-REC THRU 4200-EXIT                          
           PERFORM 2000-READ-F740 THRU 2000-EXIT.                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-UPDATE-K740-REC.                                                    
      *-------------------------------------------------------------            
            MOVE F740-KEY-FLD TO K740-KEY-FLD.                                  
            READ BP13K740                                                       
            EVALUATE K740-STATUS                                                
            WHEN 00                                                             
                 PERFORM 4300-UPDATE-K740-REC THRU 4300-EXIT                    
                                                                                
            WHEN 23                                                             
                 DISPLAY 'BP13K740 - RECORD NOT FOUND, KEY: '                   
                          F740-KEY-FLD                                          
            WHEN OTHER                                                          
                 DISPLAY 'BP13K740 - ERROR READ, STATUS: '                      
                          K740-STATUS                                           
                 PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4300-UPDATE-K740-REC.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF K740-CDE-SUCC-NT-ZONE NOT = SPACES                                
               MOVE SPACES TO K740-CDE-SUCC-NT-ZONE                             
               ADD 1 TO NO-SUCC                                                 
           END-IF.                                                              
                                                                                
           REWRITE BP13K740-REC                                                 
           EVALUATE K740-STATUS                                                 
           WHEN 00                                                              
                ADD 1 TO NO-K740-REWRITE                                        
                DISPLAY ' REGN: ' K740-NUM-REGN                                 
                        ' CDE-STATUS: ' K740-CDE-REQUEST-STATUS                 
                        ' COHORT: '  K740-CDE-COHORT                            
                        ' SUCC-NT: ' K740-CDE-SUCC-NT-ZONE                      
           WHEN OTHER                                                           
                DISPLAY 'BP13K740 - REWRITE ERROR, STATUS: '                    
                         K740-STATUS                                            
                PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13K740                                                       
                 BP13F740.                                                      
                                                                                
           IF K740-STATUS NOT = '00'                                            
             DISPLAY 'CLOSING ERROR K740, STATUS ' K740-STATUS                  
             MOVE K740-STATUS TO RETURN-CODE.                                   
                                                                                
           DISPLAY ' BP13C71A CONTROL TOTAL '.                                  
           DISPLAY '---------------------------------'.                         
           DISPLAY ' NO. OF F740 REC READ   > ' NO-F740-READ.                   
           DISPLAY ' NO. OF K740 REC UPDATE > ' NO-K740-REWRITE.                
           DISPLAY ' NO. OF SUCC TAG AS W   > ' NO-SUCC.                        
           DISPLAY ' NO. OF UNSUCC TAG AS W > ' NO-UNSUCC.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
