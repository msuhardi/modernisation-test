      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.   BP13C457.                                                  
       AUTHOR.       MCC.                                                       
                                                                                
      ************************************************************              
      *                                                          *              
      *  OBJECTIVE: TO CHECK INCONSISTENCY OF APPT DATE & TIME   *              
      *             BETWEEN APPT FILE(BP13F454) AND MASTER FILE  *              
      *             BP13K800 AND REMOVE UNSUCCESSFUL K800 TAG    *              
      *             FROM APPT FILE(BP13K454).                    *              
      *                                                          *              
      *  INPUT FILE :- BP13F454                                  *              
      *  I-O   FILE :- BP13K454                                  *              
      *              - BP13K800                                  *              
      ************************************************************              
      * CHGE REQ DATE          BY   DESCRIPTION                  *              
      * -------- ----------   ----  ---------------------------- *              
      * BP132206 20/09/2002   MCC   NEW PROGRAM                  *              
      *                                                          *              
      * BP133600 28/03/2011   ESD1  CATER FOR TR APPOINTMENT NOT *              
      *                             TO UPDATE DTE-SO.            *              
      *                                                          *              
      * BP134646 10/01/2013   ZDD1  CATER FOR P  APPOINTMENT NOT *              
      *                             TO UPDATE DTE-SO.            *              
      * BP135901 08/09/2015   KAM4  CATER TO CHECK FOR SL/TP/SP/SR              
      * BP136580 08/03/2017   KSJ3  CATER FOR SELF BOOKING       *              
      * BP136848 14/06/2017   KVL1  CATER FOR APPT TYPES TL/WT/SL*              
      * BP136980 12/10/2017   RJB1  CATER FOR SELF BOOKING  WP   *              
      ************************************************************              
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F454 ASSIGN TO BP13F454.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K454 ASSIGN TO BP13K454                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K454-KEY-FLD                               
                  FILE STATUS     IS WS-K454-STATUS.                            
                                                                                
           SELECT BP13L457 ASSIGN TO BP13L457.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F454                                                             
           RECORDING MODE IS F.                                                 
       COPY BP13F454.                                                           
                                                                                
       FD  BP13K800.                                                            
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K454.                                                            
       COPY BP13K454.                                                           
                                                                                
       FD   BP13L457     BLOCK CONTAINS 0 RECORDS                               
                         RECORD CONTAINS 132 CHARACTERS                         
                         LABEL RECORDS ARE STANDARD                             
                         RECORDING MODE IS F.                                   
       01   BP13L457-REC                 PIC X(132).                            
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-VARIABLES.                                                        
           05  WS-CURRENT-DTE            PIC X(8) VALUE SPACES.                 
           05  WS-OLD-APPT-DTE           PIC X(8) VALUE SPACES.                 
           05  WS-OLD-APPT-TME           PIC X(4) VALUE SPACES.                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K454-STATUS            PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F454-EOF               PIC X    VALUE 'N'.                    
               88 F454-EOF                        VALUE 'Y'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-REC-CNT                PIC 9(5) VALUE ZEROS.                  
           05  WS-LINE-CNT               PIC 9(5) VALUE ZEROS.                  
           05  WS-PAGE-CNT               PIC 9(5) VALUE ZEROS.                  
           05  WS-F454-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K800-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K800-REWRITE           PIC 9(6) VALUE ZEROS.                  
           05  WS-K800-NOTFD             PIC 9(6) VALUE ZEROS.                  
           05  WS-K454-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K454-REWRITE           PIC 9(6) VALUE ZEROS.                  
           05  WS-K454-NOTFD             PIC 9(6) VALUE ZEROS.                  
           05  WS-RECS-BYPASS            PIC 9(6) VALUE ZEROS.                  
                                                                                
      *---------------------------------------------------------------*         
      *    PRINT CONTROL LISTING                                                
      *---------------------------------------------------------------*         
       01  PRT-HDG-01.                                                          
           05  FILLER              PIC X(14)   VALUE 'BP13L457      '.          
           05  FILLER              PIC X(8)    VALUE 'HDB3    '.                
           05  FILLER              PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(08)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE '      '.                  
           05  PRINT-DATE          PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE '      '.                  
           05  PRINT-PAGE          PIC ZZZ9.                                    
                                                                                
       01  PRT-HDG-02.                                                          
           05  FILLER              PIC X(33)   VALUE SPACES.                    
           05  FILLER              PIC X(52)   VALUE                            
           'REPORT TO VERIFY CHANGE OF APPOINTMENT DATE AND TIME'.              
           05  FILLER              PIC X(47)   VALUE SPACES.                    
                                                                                
       01  PRT-HDG-03.                                                          
           05  FILLER              PIC X(34)     VALUE SPACES.                  
           05  FILLER              PIC X(3)      VALUE 'OLD'.                   
           05  FILLER              PIC X(9).                                    
           05  FILLER              PIC X(3)      VALUE 'OLD'.                   
           05  FILLER              PIC X(9).                                    
           05  FILLER              PIC X(3)      VALUE 'NEW'.                   
           05  FILLER              PIC X(9).                                    
           05  FILLER              PIC X(3)      VALUE 'NEW'.                   
           05  FILLER              PIC X(7).                                    
           05  FILLER              PIC X(3)      VALUE 'UPD'.                   
           05  FILLER              PIC X(2).                                    
                                                                                
       01  PRT-HDG-04.                                                          
           05  FILLER              PIC X(4).                                    
           05  FILLER              PIC X(5)      VALUE 'S/NO '.                 
           05  FILLER              PIC X(4).                                    
           05  FILLE               PIC X(8)      VALUE 'REGN NO '.              
           05  FILLER              PIC X(3).                                    
           05  FILLER              PIC X(4)      VALUE 'TYPE'.                  
           05  FILLER              PIC X(3).                                    
           05  FILLER              PIC X(10)     VALUE 'APPT DATE '.            
           05  FILLER              PIC X(2).                                    
           05  FILLER              PIC X(10)     VALUE 'APPT TIME '.            
           05  FILLER              PIC X(2).                                    
           05  FILLER              PIC X(10)     VALUE 'APPT DATE '.            
           05  FILLER              PIC X(2).                                    
           05  FILLER              PIC X(10)     VALUE 'APPT TIME '.            
           05  FILLER              PIC X(3).                                    
           05  FILLER              PIC X(3)      VALUE 'TAG'.                   
           05  FILLER              PIC X(2).                                    
                                                                                
       01  PRT-DETAILS.                                                         
           05  FILLER              PIC X(4).                                    
           05  PRT-SNO             PIC ZZZZ9.                                   
           05  FILLER              PIC X(4).                                    
           05  PRT-REGN            PIC X(8).                                    
           05  FILLER              PIC X(4).                                    
           05  PRT-APPT-TYPE       PIC X(2).                                    
           05  FILLER              PIC X(4).                                    
           05  PRT-OLD-APPT-DTE    PIC X(10).                                   
           05  FILLER              PIC X(5).                                    
           05  PRT-OLD-APPT-TME    PIC X(4).                                    
           05  FILLER              PIC X(5).                                    
           05  PRT-NEW-APPT-DTE    PIC X(10).                                   
           05  FILLER              PIC X(5).                                    
           05  PRT-NEW-APPT-TME    PIC X(4).                                    
           05  FILLER              PIC X(7).                                    
           05  PRT-K454-TAG        PIC X(1).                                    
           05  FILLER              PIC X(5).                                    
                                                                                
       01  PRT-EMPTY.                                                           
           05  FILLER              PIC X(55)   VALUE SPACES.                    
           05  FILLER              PIC X(22)   VALUE                            
                                               '*--- EMPTY REPORT ---*'.        
           05  FILLER              PIC X(55)   VALUE SPACES.                    
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *---------------*                                                         
       000-MAIN-LOGIC.                                                          
      *---------------*                                                         
            PERFORM 100-OPEN-FILES    THRU 100-EXIT.                            
            PERFORM 200-READ-BP13F454 THRU 200-EXIT.                            
            PERFORM 300-PROCESS-CASE  THRU 300-EXIT                             
                    UNTIL F454-EOF.                                             
            PERFORM 999-CLOSE-FILES   THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       100-OPEN-FILES.                                                          
      *---------------*                                                         
           OPEN INPUT  BP13F454                                                 
                I-O    BP13K454                                                 
                       BP13K800                                                 
                OUTPUT BP13L457.                                                
                                                                                
           IF WS-K454-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K454-STATUS ' WS-K454-STATUS              
              MOVE WS-K454-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DTE.                   
           STRING WS-CURRENT-DTE(7:2) '/'                                       
                  WS-CURRENT-DTE(5:2) '/'                                       
                  WS-CURRENT-DTE(1:4) DELIMITED BY SIZE                         
                  INTO PRINT-DATE.                                              
                                                                                
           PERFORM 700-PRINT-HDG    THRU 700-EXIT.                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       200-READ-BP13F454.                                                       
      *-----------------*                                                       
           READ BP13F454 AT END                                                 
           MOVE 'Y'                      TO WS-F454-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F454-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       300-PROCESS-CASE.                                                        
      *------------------*                                                      
           MOVE F454-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              ADD 1                       TO   WS-K800-READ                     
              PERFORM 400-CHECK-DATE-TIME THRU 400-EXIT                         
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 ADD 1                    TO WS-K800-NOTFD                      
                 DISPLAY 'REC NOTFD BP13K800 ' WS-K800-STATUS ' '               
                                               F454-NUM-REGN                    
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K800,STATUS ' WS-K800-STATUS           
                 MOVE WS-K800-STATUS      TO RETURN-CODE                        
                 GO TO 999-CLOSE-FILES                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F454 THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       400-CHECK-DATE-TIME.                                                     
      *--------------------*                                                    
           DISPLAY '*--------------------------------------*'.                  
           DISPLAY '400-CHECK-DATE-TIME'.                                       
           DISPLAY 'F454-NUM-REGN       = ' F454-NUM-REGN.                      
           DISPLAY 'NUM-UPDATE-BP13K800 = ' F454-NUM-UPDATE-BP13K800.           
           DISPLAY 'F454-CDE-APPT-TYPE  = ' F454-CDE-APPT-TYPE.                 
           DISPLAY 'F454-DTE-APPT       = ' F454-DTE-APPT.                      
           DISPLAY 'F454-TME-APPT       = ' F454-TME-APPT.                      
           DISPLAY 'K800-DTE-AO-APPT    = ' K800-DTE-AO-APPT.                   
           DISPLAY 'K800-TME-AO-APPT    = ' K800-TME-AO-APPT.                   
           DISPLAY 'K800-DTE-SO-APPT    = ' K800-DTE-SO-APPT.                   
           DISPLAY 'K800-TME-SO-APPT    = ' K800-TME-SO-APPT.                   
                                                                                
           IF F454-NUM-UPDATE-BP13K800 = 'Y'                                    
              PERFORM 500-UPDATE-BP13K800 THRU 500-EXIT                         
              PERFORM 600-UPDATE-BP13K454 THRU 600-EXIT                         
           ELSE                                                                 
              IF F454-CDE-APPT-TYPE = 'TR' OR 'LA' OR 'P '                      
                 ADD 01 TO WS-RECS-BYPASS                                       
                 GO TO 400-EXIT                                                 
              END-IF                                                            
              IF F454-CDE-APPT-TYPE = 'SA'                                      
                 IF (F454-DTE-APPT NOT = K800-DTE-AO-APPT) OR                   
                    (F454-TME-APPT NOT = K800-TME-AO-APPT)                      
                    PERFORM 500-UPDATE-BP13K800 THRU 500-EXIT                   
                 ELSE                                                           
                    ADD 01 TO WS-RECS-BYPASS                                    
                 END-IF                                                         
              ELSE                                                              
                IF F454-CDE-APPT-TYPE = 'TP' OR 'SP' OR 'SR' OR                 
                                        'WT' OR 'TL' OR 'PL' OR 'WP'            
                   IF (F454-DTE-APPT NOT = K800-DTE-SO-APPT) OR                 
                      (F454-TME-APPT NOT = K800-TME-SO-APPT)                    
                       PERFORM 500-UPDATE-BP13K800 THRU 500-EXIT                
                   ELSE                                                         
                       ADD 01 TO WS-RECS-BYPASS                                 
                   END-IF                                                       
                ELSE                                                            
                  IF F454-CDE-APPT-TYPE = 'SL'                                  
                     IF (F454-DTE-APPT NOT = K800-DTE-SL-APPT) OR               
                        (F454-TME-APPT NOT = K800-TME-SL-APPT)                  
                         PERFORM 500-UPDATE-BP13K800 THRU 500-EXIT              
                     ELSE                                                       
                         ADD 01 TO WS-RECS-BYPASS                               
                     END-IF                                                     
                  END-IF                                                        
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       500-UPDATE-BP13K800.                                                     
      *------------------*                                                      
           DISPLAY '500-UPDATE-BP13K800'.                                       
           DISPLAY 'CASE UPDATED !!'.                                           
           DISPLAY '*--------------------------------------*'.                  
                                                                                
           MOVE SPACES TO WS-OLD-APPT-DTE.                                      
           MOVE SPACES TO WS-OLD-APPT-TME.                                      
                                                                                
           IF F454-CDE-APPT-TYPE = 'TR' OR 'LA' OR 'P '                         
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF F454-CDE-APPT-TYPE = 'SA'                                      
                 MOVE K800-DTE-AO-APPT TO WS-OLD-APPT-DTE                       
                 MOVE K800-TME-AO-APPT TO WS-OLD-APPT-TME                       
                 MOVE F454-DTE-APPT    TO K800-DTE-AO-APPT                      
                 MOVE F454-TME-APPT    TO K800-TME-AO-APPT                      
              ELSE                                                              
                IF F454-CDE-APPT-TYPE = 'TP' OR 'SP' OR 'SR' OR                 
                                        'WT' OR 'TL' OR 'PL' OR 'WP'            
                   MOVE K800-DTE-SO-APPT TO WS-OLD-APPT-DTE                     
                   MOVE K800-TME-SO-APPT TO WS-OLD-APPT-TME                     
                   MOVE F454-DTE-APPT    TO K800-DTE-SO-APPT                    
                   MOVE F454-TME-APPT    TO K800-TME-SO-APPT                    
                                                                                
                   IF F454-NUM-SELF-BOOK = 'Y'                                  
                      MOVE 'W'           TO K800-CDE-APPT                       
                                                                                
                      EVALUATE F454-CDE-APPT-TYPE                               
                      WHEN 'TP'                                                 
                         MOVE 'WT'       TO K800-CDE-APPT-TYPE                  
                      WHEN 'SP'                                                 
                         MOVE 'WP'       TO K800-CDE-APPT-TYPE                  
                      END-EVALUATE                                              
                   END-IF                                                       
                ELSE                                                            
                  IF F454-CDE-APPT-TYPE = 'SL'                                  
                     MOVE K800-DTE-SL-APPT TO WS-OLD-APPT-DTE                   
                     MOVE K800-TME-SL-APPT TO WS-OLD-APPT-TME                   
                     MOVE F454-DTE-APPT    TO K800-DTE-SL-APPT                  
                     MOVE F454-TME-APPT    TO K800-TME-SL-APPT                  
                  END-IF                                                        
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 550-REWRITE-BP13K800 THRU 550-EXIT.                          
           PERFORM 800-PRINT-REPORT     THRU 800-EXIT.                          
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------*                                                   
       550-REWRITE-BP13K800.                                                    
      *---------------------*                                                   
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE WS-K800-STATUS                                              
             WHEN 0                                                             
               ADD 1 TO WS-K800-REWRITE                                         
             WHEN OTHER                                                         
               DISPLAY 'ERROR IN REWRITING BP13K800'                            
               DISPLAY 'FILE STATUS ' WS-K800-STATUS                            
               MOVE WS-K800-STATUS TO RETURN-CODE                               
               GO TO   999-CLOSE-FILES                                          
           END-EVALUATE.                                                        
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       600-UPDATE-BP13K454.                                                     
      *--------------------*                                                    
           MOVE F454-KEY-FLD  TO K454-KEY-FLD.                                  
                                                                                
           READ BP13K454.                                                       
                                                                                
           IF WS-K454-STATUS = 00                                               
              ADD 1                    TO  WS-K454-READ                         
              PERFORM 650-REWRITE-BP13K454 THRU 650-EXIT                        
           ELSE                                                                 
              IF WS-K454-STATUS = 23                                            
                 ADD 1                 TO  WS-K454-NOTFD                        
                 DISPLAY 'REC NOTFD BP13K454 ' WS-K454-STATUS ' '               
                                           F454-NUM-REGN                        
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K454,STATUS ' WS-K454-STATUS           
                 MOVE WS-K454-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------*                                                   
       650-REWRITE-BP13K454.                                                    
      *---------------------*                                                   
           MOVE SPACE TO K454-NUM-UPDATE-BP13K800.                              
                                                                                
           REWRITE BP13K454-REC.                                                
                                                                                
           EVALUATE WS-K454-STATUS                                              
             WHEN 0                                                             
               ADD 1 TO WS-K454-REWRITE                                         
             WHEN OTHER                                                         
               DISPLAY 'ERROR IN REWRITING BP13K454'                            
               DISPLAY 'FILE STATUS ' WS-K454-STATUS                            
               MOVE WS-K454-STATUS TO RETURN-CODE                               
               GO TO   999-CLOSE-FILES                                          
           END-EVALUATE.                                                        
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------*                                                          
       700-PRINT-HDG.                                                           
      *--------------*                                                          
           MOVE  0             TO WS-LINE-CNT.                                  
           ADD   1             TO WS-PAGE-CNT.                                  
           MOVE  WS-PAGE-CNT   TO PRINT-PAGE.                                   
           WRITE BP13L457-REC FROM PRT-HDG-01   AFTER PAGE.                     
           WRITE BP13L457-REC FROM PRT-HDG-02   AFTER 2.                        
           WRITE BP13L457-REC FROM PRT-HDG-03   AFTER 2.                        
           WRITE BP13L457-REC FROM PRT-HDG-04   AFTER 1.                        
                                                                                
           MOVE  SPACES TO BP13L457-REC.                                        
           WRITE BP13L457-REC.                                                  
                                                                                
           ADD 07 TO WS-LINE-CNT.                                               
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       800-PRINT-REPORT.                                                        
      *-----------------*                                                       
           IF WS-LINE-CNT > 50                                                  
              PERFORM 700-PRINT-HDG THRU 700-EXIT                               
           END-IF.                                                              
                                                                                
           MOVE SPACES                   TO PRT-DETAILS.                        
           ADD 01                        TO WS-REC-CNT.                         
           MOVE WS-REC-CNT               TO PRT-SNO.                            
           MOVE F454-NUM-REGN            TO PRT-REGN.                           
                                                                                
           IF WS-OLD-APPT-DTE NOT = SPACES AND LOW-VALUES AND ZEROES            
              STRING WS-OLD-APPT-DTE(7:2) '/'                                   
                     WS-OLD-APPT-DTE(5:2) '/'                                   
                     WS-OLD-APPT-DTE(1:4) DELIMITED BY SIZE                     
                     INTO PRT-OLD-APPT-DTE                                      
           ELSE                                                                 
              MOVE '    --    '     TO PRT-OLD-APPT-DTE                         
           END-IF.                                                              
                                                                                
           IF WS-OLD-APPT-TME NOT = SPACES AND LOW-VALUES AND ZEROES            
              MOVE WS-OLD-APPT-TME  TO PRT-OLD-APPT-TME                         
           ELSE                                                                 
              MOVE ' -- '           TO PRT-OLD-APPT-TME                         
           END-IF.                                                              
                                                                                
           STRING F454-DTE-APPT(7:2) '/' F454-DTE-APPT(5:2) '/'                 
                  F454-DTE-APPT(1:4) DELIMITED BY SIZE                          
                  INTO PRT-NEW-APPT-DTE.                                        
                                                                                
           MOVE F454-TME-APPT            TO PRT-NEW-APPT-TME.                   
           MOVE F454-NUM-UPDATE-BP13K800 TO PRT-K454-TAG.                       
           MOVE F454-CDE-APPT-TYPE       TO PRT-APPT-TYPE.                      
                                                                                
           MOVE SPACES                   TO BP13L457-REC.                       
           WRITE BP13L457-REC FROM PRT-DETAILS  AFTER 1.                        
           ADD 01                        TO WS-LINE-CNT.                        
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
       999-CLOSE-FILES.                                                         
      *----------------*                                                        
           IF WS-REC-CNT <= ZEROES                                              
              MOVE SPACES TO BP13L457-REC                                       
              WRITE BP13L457-REC FROM PRT-EMPTY AFTER 1                         
           END-IF.                                                              
                                                                                
           CLOSE BP13F454                                                       
                 BP13K454                                                       
                 BP13K800                                                       
                 BP13L457.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800, STATUS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K454-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K454, STATUS ' WS-K454-STATUS          
              MOVE WS-K454-STATUS        TO RETURN-CODE.                        
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------------------ BP13C457 ------------------'.           
           DISPLAY 'NO OF REC READ      BP13F454 = ' WS-F454-READ.              
           DISPLAY 'NO OF REC READ      BP13K800 = ' WS-K800-READ               
           DISPLAY 'NO OF REC NOTFND    BP13K800 = ' WS-K800-NOTFD.             
           DISPLAY 'NO OF REC REWRITTEN BP13K800 = ' WS-K800-REWRITE.           
           DISPLAY 'NO OF REC READ      BP13K454 = ' WS-K454-READ               
           DISPLAY 'NO OF REC NOTFND    BP13K454 = ' WS-K454-NOTFD.             
           DISPLAY 'NO OF REC REWRITTEN BP13K454 = ' WS-K454-REWRITE.           
           DISPLAY 'NO OF REC BYPASSED           = ' WS-RECS-BYPASS.            
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
