      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13CE20.                                                 
      *AUTHOR.        GURU..                                                    
      *DATE-WRITTEN.  28/03/2020.                                               
                                                                                
      ******************************************************************        
      *    SYSTEM NAME :  SYSTEM OF COMMITMENT                         *        
      *    SYSTEM ID   :  BP13                                         *        
      *    OBJECTIVE   :  COMPUTE APPLICATION RATE, DEMAD/SUPPLY ON SALE        
      *                   LAUNCH.                                      *        
      *                                                                *        
      *    INPUT FILES :                                               *        
      *         1.  BP13F762  -  SUMMARY FILE FOR DEMAND/SUPPLY        *        
      *         2.  BP13K813  -  SHOWING BTO/SBF SITE INFORMATION      *        
      *    OUTPUT     :                                                *        
      *         1.  BP13FF02  -  COUNT OF FIRST TIMER AND ITS PERCENTAGE        
      *                                                                *        
      ******************************************************************        
      * CHG-NO     BY      DATE     DESCRIPTION                        *        
      * -------   ----  ----------  --------------------------------   *        
      * BP138180  KR13  28/03/2020  NEW PROGRAM                        *        
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F762  ASSIGN     TO  BP13F762.                            
                                                                                
           SELECT BP13K813  ASSIGN     TO BP13K813                              
                           ACCESS MODE IS RANDOM                                
                          ORGANIZATION IS INDEXED                               
                            RECORD KEY IS K813-KEY-FLD                          
                           FILE STATUS IS WS-K813-STATUS.                       
                                                                                
           SELECT BP13FF02  ASSIGN    TO  BP13FF02.                             
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F762                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13F762.                                                           
                                                                                
       FD   BP13K813                RECORD CONTAINS 1000 CHARACTERS.            
       COPY BP13K813.                                                           
                                                                                
       FD   BP13FF02                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13FF02.                                                           
                                                                                
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K813-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  EOF-F762-IND            PIC X      VALUE 'N'.                    
               88  EOF-F762                       VALUE 'Y'.                    
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05  WS-F762-READ            PIC 9(4)   VALUE ZERO.                   
           05  WS-K813-READ            PIC 9(4)   VALUE ZERO.                   
           05  WS-FF02-WRITE           PIC 9(4)   VALUE ZERO.                   
           05  WS-CTR-2F               PIC 9(4)   VALUE ZERO.                   
           05  WS-CTR-NON-2F           PIC 9(4)   VALUE ZERO.                   
                                                                                
       01  WS-SYSTEM-DATETIME.                                                  
           05  WS-SYS-DATE             PIC 9(08) VALUE ZEROES.                  
           05  WS-SYS-TIME             PIC 9(08) VALUE ZEROES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
                                                                                
       01  WS-TEMP-FF02.                                                        
           05  WS-NUM-HH-FT            PIC ZZ9.9 VALUE ZEROES.                  
           05  WS-NUM-HH-2T            PIC ZZ9.9 VALUE ZEROES.                  
           05  WS-NUM-HH-FTS           PIC ZZ9.9 VALUE ZEROES.                  
           05  WS-NUM-HH-ELDERLY       PIC ZZ9.9 VALUE ZEROES.                  
           05  WS-NUM-OVERALL          PIC ZZ9.9 VALUE ZEROES.                  
           05  WS-NUM-SUPPLY           PIC ZZZZ9 VALUE ZEROES.                  
           05  WS-NUM-DEMAND           PIC ZZZZ9 VALUE ZEROES.                  
                                                                                
       01  WS-DISP-DATE.                                                        
           05  WS-DISP-DD              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-MM              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-CCYY            PIC X(04) VALUE SPACES.                  
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *----------------------*                                                  
       000-MAIN-ROUTINE.                                                        
      *----------------------*                                                  
                                                                                
           PERFORM  1000-OPEN-ROUTINE      THRU   1000-EXIT.                    
                                                                                
           PERFORM  2000-READ-BP13F762     THRU   2000-EXIT.                    
                                                                                
           PERFORM  3000-PROCESS-ROUTINE   THRU   3000-EXIT                     
                    UNTIL EOF-F762.                                             
           PERFORM  9000-CLOSE-ROUTINE     THRU   9000-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    OPENING FILES SECTION                                       *        
      ******************************************************************        
      *----------------------*                                                  
       1000-OPEN-ROUTINE.                                                       
      *----------------------*                                                  
           OPEN INPUT  BP13F762                                                 
                       BP13K813                                                 
                OUTPUT BP13FF02.                                                
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K813 : ' WS-K813-STATUS              
              PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT                    
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATETIME.                  
           DISPLAY 'WS-SYS-DATE = ' WS-SYS-DATE.                                
                                                                                
           MOVE WS-SYS-DATE(1:4)        TO WS-DISP-CCYY.                        
           MOVE WS-SYS-DATE(5:2)        TO WS-DISP-MM.                          
           MOVE WS-SYS-DATE(7:2)        TO WS-DISP-DD.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       2000-READ-BP13F762.                                                      
      *----------------------*                                                  
           READ BP13F762                                                        
             AT END                                                             
                MOVE 'Y'         TO EOF-F762-IND                                
                GO               TO 2000-EXIT                                   
           END-READ.                                                            
                                                                                
           ADD 1                 TO WS-F762-READ.                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3000-PROCESS-ROUTINE.                                                    
      *----------------------*                                                  
           PERFORM 3500-READ-BP13K813       THRU 3500-EXIT                      
                                                                                
           MOVE SPACES                      TO BP13FF02-REC.                    
           INITIALIZE BP13FF02-REC WS-TEMP-FF02.                                
                                                                                
           MOVE F762-NUM-SALE-MODE          TO FF02-NUM-SALE-MODE               
           MOVE F762-DTE-BALLOT             TO FF02-DTE-BALLOT                  
           MOVE F762-NUM-CHOICE             TO FF02-CDE-NT                      
           MOVE F762-NUM-FT                 TO FF02-NUM-FT                      
           MOVE F762-NUM-SUPPLY             TO WS-NUM-SUPPLY                    
           MOVE WS-NUM-SUPPLY               TO FF02-NUM-TOT-UNIT                
           MOVE F762-NUM-DEMAND             TO WS-NUM-DEMAND                    
           MOVE WS-NUM-DEMAND               TO FF02-NUM-TOT-APPLN               
           MOVE K813-NME-EST                TO FF02-NME-ESTATE                  
           MOVE K813-NME-PROJECT-FLAT-AVAI1 TO FF02-NME-PROJECT                 
           MOVE K813-NUM-MATURE-EST-TAG     TO FF02-NUM-MATURE-EST              
           MOVE WS-SYS-DATE                 TO FF02-DTE-RUN                     
                                                                                
           IF F762-NUM-1ST-TIMER NOT NUMERIC                                    
              MOVE ZEROES                TO F762-NUM-1ST-TIMER.                 
           IF F762-NUM-1ST-TIMER-CNT NOT NUMERIC                                
              MOVE ZEROES                TO F762-NUM-1ST-TIMER-CNT.             
           IF F762-NUM-DEMAND NOT NUMERIC                                       
              MOVE ZEROES                TO F762-NUM-DEMAND.                    
           IF F762-NUM-SUPPLY NOT NUMERIC                                       
              MOVE ZEROES                TO F762-NUM-SUPPLY.                    
           IF F762-NUM-1ST-TIMER-CNT-ORIG NOT NUMERIC                           
              MOVE ZEROES                TO F762-NUM-1ST-TIMER-CNT-ORIG.        
           IF F762-NUM-SINGLE-SCH NOT NUMERIC                                   
              MOVE ZEROES                TO F762-NUM-SINGLE-SCH.                
           IF F762-NUM-SINGLE-SCH-CNT NOT NUMERIC                               
              MOVE ZEROES                TO F762-NUM-SINGLE-SCH-CNT.            
           IF F762-NUM-ELDERLY-SCH NOT NUMERIC                                  
              MOVE ZEROES                TO F762-NUM-ELDERLY-SCH.               
           IF F762-NUM-ELDERLY-SCH-CNT NOT NUMERIC                              
              MOVE ZEROES                TO F762-NUM-ELDERLY-SCH-CNT.           
                                                                                
           IF F762-NUM-SUPPLY > 0                                               
              COMPUTE WS-NUM-OVERALL ROUNDED =  F762-NUM-DEMAND /               
                                          F762-NUM-SUPPLY.                      
                                                                                
           IF FF02-NUM-FT = '2F'                                                
              IF F762-NUM-1ST-TIMER-CNT > 0                                     
                 COMPUTE WS-NUM-HH-FT ROUNDED = F762-NUM-1ST-TIMER /            
                                          F762-NUM-1ST-TIMER-CNT                
              END-IF                                                            
                                                                                
              IF F762-NUM-SINGLE-SCH-CNT > 0                                    
                 COMPUTE WS-NUM-HH-FTS ROUNDED = F762-NUM-SINGLE-SCH /          
                                           F762-NUM-SINGLE-SCH-CNT              
              END-IF                                                            
                                                                                
              IF F762-NUM-ELDERLY-SCH-CNT > 0                                   
                 COMPUTE WS-NUM-HH-ELDERLY ROUNDED  =                           
                         F762-NUM-ELDERLY-SCH / F762-NUM-ELDERLY-SCH-CNT        
              END-IF                                                            
                                                                                
              IF (F762-NUM-SUPPLY - F762-NUM-1ST-TIMER-CNT -                    
                 F762-NUM-SINGLE-SCH-CNT - F762-NUM-ELDERLY-SCH-CNT) > 0        
                 COMPUTE WS-NUM-HH-2T ROUNDED =                                 
                    (F762-NUM-DEMAND - F762-NUM-1ST-TIMER -                     
                     F762-NUM-SINGLE-SCH - F762-NUM-ELDERLY-SCH)  /             
                    (F762-NUM-SUPPLY - F762-NUM-1ST-TIMER-CNT -                 
                     F762-NUM-SINGLE-SCH-CNT - F762-NUM-ELDERLY-SCH-CNT)        
                                                                                
              END-IF                                                            
              ADD 1 TO WS-CTR-2F                                                
           ELSE                                                                 
              IF (F762-NUM-SUPPLY - F762-NUM-1ST-TIMER-CNT-ORIG) > 0            
                 COMPUTE WS-NUM-HH-2T ROUNDED =                                 
                                 (F762-NUM-DEMAND - F762-NUM-1ST-TIMER)/        
                        (F762-NUM-SUPPLY - F762-NUM-1ST-TIMER-CNT-ORIG)         
              END-IF                                                            
              IF F762-NUM-1ST-TIMER-CNT-ORIG > 0                                
                 COMPUTE WS-NUM-HH-FT ROUNDED = F762-NUM-1ST-TIMER /            
                                          F762-NUM-1ST-TIMER-CNT-ORIG           
              END-IF                                                            
                                                                                
              MOVE ZEROES                 TO WS-NUM-HH-FTS                      
                                             WS-NUM-HH-ELDERLY                  
              ADD 1 TO WS-CTR-NON-2F                                            
           END-IF                                                               
                                                                                
           MOVE WS-NUM-HH-FT              TO FF02-NUM-HH-FT                     
           MOVE WS-NUM-HH-2T              TO FF02-NUM-HH-2T                     
           MOVE WS-NUM-HH-FTS             TO FF02-NUM-HH-FTS                    
           MOVE WS-NUM-HH-ELDERLY         TO FF02-NUM-HH-ELDERLY                
           MOVE WS-NUM-OVERALL            TO FF02-NUM-OVERALL                   
                                                                                
           PERFORM 4000-WRITE-BP13FF02    THRU 4000-EXIT.                       
                                                                                
           PERFORM 2000-READ-BP13F762     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3500-READ-BP13K813.                                                      
      *----------------------*                                                  
           MOVE SPACES                      TO BP13K813-REC.                    
           INITIALIZE                          BP13K813-REC.                    
                                                                                
           MOVE F762-NUM-CHOICE             TO K813-NUM-ZONE.                   
           MOVE F762-NUM-FT                 TO K813-NUM-FLAT-TYPE.              
           MOVE F762-DTE-BALLOT             TO K813-DTE-BALLOT.                 
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO WS-K813-READ                                       
               WHEN 23                                                          
                    DISPLAY 'RECORD NOT FOUND IN K813 : ' K813-KEY-FLD          
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K813: ' WS-K813-STATUS           
                            ' KEY : ' K813-KEY-FLD                              
                    PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       4000-WRITE-BP13FF02.                                                     
      *----------------------*                                                  
           MOVE ','                   TO FF02-FILLER01 FF02-FILLER02            
                FF02-FILLER03 FF02-FILLER04 FF02-FILLER05 FF02-FILLER06         
                FF02-FILLER07 FF02-FILLER08 FF02-FILLER09 FF02-FILLER10         
                FF02-FILLER11 FF02-FILLER12 FF02-FILLER13 FF02-FILLER14         
                FF02-FILLER15.                                                  
                                                                                
           WRITE BP13FF02-REC.                                                  
                                                                                
           ADD 1 TO WS-FF02-WRITE.                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       9000-CLOSE-ROUTINE.                                                      
      *----------------------*                                                  
           DISPLAY 'PROGRAM ID   : BP13CE20'.                                   
           DISPLAY 'CURRENT-DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
                                                                                
           DISPLAY ' NO OF RECS READ FROM BP13F762 : ' WS-F762-READ.            
           DISPLAY ' NO OF RECS READ FROM BP13K813 : ' WS-K813-READ.            
           DISPLAY '                 NO OF 2F RECS : ' WS-CTR-2F.               
           DISPLAY '             NO OF NON-2F RECS : ' WS-CTR-NON-2F.           
           DISPLAY 'NO OF RECS WRITTEN TO BP13FF02 : ' WS-FF02-WRITE.           
                                                                                
           CLOSE BP13F762                                                       
                 BP13K813                                                       
                 BP13FF02.                                                      
                                                                                
           IF WS-K813-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING - BP13K813 : ' WS-K813-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
