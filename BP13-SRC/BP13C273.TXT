      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C273.                                                 
      *AUTHOR.        RAYMOND H. BUNADO                                         
      *DATE WRITTEN.  28/04/97.                                                 
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  TO PRINT SHORTLISTED-QTR NOT =            *        
      *                      BALLOT-QTR.                               *        
      *                                                                *        
      *   INPUT  FILE :  1.  BP13F200  -  BOOKING APPOINTMENT FILE     *        
      *                  2.  BP13F205  -  CONTROL RECORD      FILE     *        
      *                  3.  BP13K800  -  SOC MASTER          FILE     *        
      *                                                                *        
      *   OUTPUT FILE :  1.  BP13L273  -  LIST OF SHORTLISTED CASES    *        
      *                  2.  P13F200O  -  BOOKING APPOINTMENT FILE     *        
      *                                                                *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ---------------------------------   *        
      *  BP130297   RHB   280497   NEW PGM                             *        
      *  BP130418   MPA   110498   TO CATER FOR Y2000.                 *        
      *  BP132095   MTD   230402   EXPAND LENGTH OF BP13F200/P13F200O  *        
      *                            (FROM 100 TO 300)                   *        
      *  BP136836   ESA1  150817   TO CATER FOR ROF                             
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200  ASSIGN       TO BP13F200.                           
           SELECT BP13F205  ASSIGN       TO BP13F205.                           
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
           SELECT BP13L273  ASSIGN       TO BP13L273.                           
           SELECT P13F200O  ASSIGN       TO P13F200O.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F200                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   P13F200O                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       01   P13F200O-REC    PIC X(300).                                         
                                                                                
       FD   BP13F205                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000  CHARACTERS.                                   
       COPY BP13K800.                                                           
                                                                                
       FD   BP13L273                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L273-REC    PIC X(132).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05 WS-K800-STATUS            PIC 9(02) VALUE 00.                     
                                                                                
       01  WS-MATCH-FIELDS.                                                     
           05 WS-F200-MATCH-KEY.                                                
              10 WS-F200-NUM-NT-ZONE    PIC X(03).                              
              10 WS-F200-NUM-FLAT-TYPE  PIC X(02).                              
              10 WS-F200-DTE-BALLOT     PIC X(06).                              
           05 WS-F205-MATCH-KEY.                                                
              10 WS-F205-NUM-NT-ZONE    PIC X(03).                              
              10 WS-F205-NUM-FLAT-TYPE  PIC X(02).                              
              10 WS-F205-DTE-BALLOT     PIC X(06).                              
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-F200-READ              PIC 9(08) VALUE ZEROES.                 
           05 WS-F205-READ              PIC 9(08) VALUE ZEROES.                 
           05 WS-F200-DELETE            PIC 9(08) VALUE ZEROES.                 
           05 WS-K800-READ              PIC 9(08) VALUE ZEROES.                 
           05 WS-F200-WRITE             PIC 9(08) VALUE ZEROES.                 
           05 WS-MAX-LINES              PIC 9(02) VALUE 60.                     
           05 WS-LINE-COUNT             PIC 9(02) VALUE 65.                     
           05 WS-L273-PRINTED           PIC 9(08) VALUE ZEROES.                 
           05 WS-PAGE-NO                PIC 9(05) VALUE ZEROES.                 
                                                                                
       01  WS-PREV-F205-ZONE            PIC X(03) VALUE SPACES.                 
       01  WS-PREV-F205-FLAT-TYPE       PIC X(02) VALUE SPACES.                 
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F200-EOF-SW            PIC X(01) VALUE SPACES.                 
              88 C-EOF-F200                 VALUE 'Y'.                          
           05 WS-F205-EOF-SW            PIC X(01) VALUE SPACES.                 
              88 C-EOF-F205                 VALUE 'Y'.                          
                                                                                
       01  WS-DATE.                                                             
           05 WS-YY                     PIC 9(04).                              
           05 WS-MM                     PIC 9(02).                              
           05 WS-DD                     PIC 9(02).                              
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DD                PIC 9(02).                              
           05 FILLER                    PIC X(01) VALUE '/'.                    
           05 WS-EDIT-MM                PIC 9(02).                              
           05 FILLER                    PIC X(01) VALUE '/'.                    
           05 WS-EDIT-YY                PIC 9(04).                              
                                                                                
       01  L273-HDG1.                                                           
           05 FILLER                    PIC X(14) VALUE 'BP13C273'.             
           05 FILLER                    PIC X(08) VALUE 'HDB3'.                 
           05 FILLER                    PIC X(18) VALUE SPACES.                 
           05 FILLER                    PIC X(39) VALUE                         
              'S Y S T E M   O F   C O M M I T M E N T'.                        
           05 FILLER                    PIC X(18) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE 'DATE: '.               
           05 L273-DATE                 PIC X(10) VALUE SPACES.                 
           05 FILLER                    PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE 'PAGE: '.               
           05 L273-PAGE                 PIC ZZZZ9.                              
                                                                                
       01  L273-HDG2.                                                           
           05 FILLER                    PIC X(32) VALUE SPACES.                 
           05 FILLER                    PIC X(51) VALUE                         
           'CASES WITH SHORTLIST-QTR NOT THE SAME AS BALLOT-QTR'.               
           05 FILLER                    PIC X(14) VALUE SPACES.                 
           05 FILLER                    PIC X(34) VALUE                         
             'USER: SALES UNIT (ALLOCATION TEAM)'.                              
                                                                                
       01  L273-HDG3.                                                           
           05 FILLER                    PIC X(43) VALUE                         
              'S/N  REGN-NO    ZONE  FLAT-TYPE  BALLOT-QTR'.                    
           05 FILLER                    PIC X(28) VALUE                         
              ' SHORTLIST-QTR  APPLN-STATUS'.                                   
                                                                                
       01  L273-HDG4.                                                           
           05 FILLER                    PIC X(71) VALUE ALL '-'.                
                                                                                
       01  L273-DETAIL-REC.                                                     
           05 L273-SER-NO               PIC ZZ9   VALUE ZEROES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 L273-REGNO                PIC X(8)  VALUE SPACES.                 
           05 FILLER                    PIC X(03) VALUE SPACES.                 
           05 L273-ZONE                 PIC X(3)  VALUE SPACES.                 
           05 FILLER                    PIC X(03) VALUE SPACES.                 
           05 L273-FT                   PIC X(2)  VALUE SPACES.                 
           05 FILLER                    PIC X(09) VALUE SPACES.                 
           05 L273-BAL1                 PIC X(6)  VALUE SPACES.                 
           05 FILLER                    PIC X(05) VALUE SPACES.                 
           05 L273-BAL2                 PIC X(6)  VALUE SPACES.                 
           05 FILLER                    PIC X(09) VALUE SPACES.                 
           05 L273-APPL-STAT            PIC X     VALUE SPACES.                 
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      ***********                                                               
       0000-MAIN.                                                               
      ***********                                                               
                                                                                
            PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                        
            PERFORM 2000-PROCESS-ROUTINE THRU 2000-EXIT.                        
            PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *******************                                                       
       1000-OPEN-ROUTINE.                                                       
      *******************                                                       
                                                                                
           OPEN INPUT BP13F200                                                  
                      BP13F205                                                  
                      BP13K800                                                  
               OUTPUT P13F200O                                                  
                      BP13L273.                                                 
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING BP13K800 FILE, STATUS '                    
                       WS-K800-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                          
           MOVE   WS-DD   TO   WS-EDIT-DD.                                      
           MOVE   WS-MM   TO   WS-EDIT-MM.                                      
           MOVE   WS-YY   TO   WS-EDIT-YY.                                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       2000-PROCESS-ROUTINE.                                                    
      **********************                                                    
                                                                                
           PERFORM 2100-READ-F205      THRU  2100-EXIT.                         
           PERFORM 2200-READ-F200      THRU  2200-EXIT.                         
           PERFORM 2300-PROCESS-RECORD THRU  2300-EXIT                          
              UNTIL C-EOF-F200 OR                                               
                    C-EOF-F205.                                                 
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ****************                                                          
       2100-READ-F205.                                                          
      ****************                                                          
                                                                                
           READ BP13F205                                                        
              AT END MOVE 'Y'       TO  WS-F205-EOF-SW                          
              GO TO 2100-EXIT                                                   
              END-READ.                                                         
                                                                                
           MOVE F205-NUM-NT-ZONE    TO WS-F205-NUM-NT-ZONE.                     
           MOVE F205-NUM-FLAT-TYPE  TO WS-F205-NUM-FLAT-TYPE.                   
           MOVE F205-DTE-BALLOT     TO WS-F205-DTE-BALLOT.                      
           ADD +1                   TO WS-F205-READ.                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************                                                          
       2200-READ-F200.                                                          
      ****************                                                          
                                                                                
           READ BP13F200                                                        
              AT END MOVE 'Y'       TO  WS-F200-EOF-SW                          
              GO TO 2200-EXIT                                                   
              END-READ.                                                         
                                                                                
           MOVE F200-NUM-NT-ZONE    TO WS-F200-NUM-NT-ZONE.                     
           MOVE F200-NUM-FLAT-TYPE  TO WS-F200-NUM-FLAT-TYPE.                   
           MOVE F200-DTE-BALLOT     TO WS-F200-DTE-BALLOT.                      
           ADD +1                   TO WS-F200-READ.                            
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2300-PROCESS-RECORD.                                                     
      *********************                                                     
                                                                                
           IF (WS-F200-MATCH-KEY = WS-F205-MATCH-KEY)                           
              PERFORM 2310-PROCESS-F200   THRU 2310-EXIT                        
              PERFORM 2200-READ-F200      THRU 2200-EXIT                        
           ELSE                                                                 
              IF (WS-F205-MATCH-KEY > WS-F200-MATCH-KEY)                        
                 PERFORM 2200-READ-F200   THRU 2200-EXIT                        
              ELSE                                                              
                 PERFORM 2100-READ-F205   THRU 2100-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       2310-PROCESS-F200.                                                       
      *******************                                                       
                                                                                
           MOVE F200-NUM-REGN      TO K800-NUM-REGN.                            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              ADD +1                   TO WS-K800-READ                          
                 IF F200-DTE-BALLOT = K800-DTE-BALLOT                           
                    WRITE P13F200O-REC FROM BP13F200-REC                        
                    ADD 1 TO WS-F200-WRITE                                      
                 ELSE                                                           
                    PERFORM 3000-WRITE-REPORT THRU 3000-EXIT                    
                 END-IF                                                         
                                                                                
           WHEN  23                                                             
                 ADD +1                   TO WS-F200-DELETE                     
                 DISPLAY 'REGN NOT FOUND IN K800, REGN # : '                    
                          F200-NUM-REGN                                         
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR READING BP13K800 FILE, STATUS : '               
                          WS-K800-STATUS                                        
           END-EVALUATE.                                                        
                                                                                
       2310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       3000-WRITE-REPORT.                                                       
      ******************                                                        
                                                                                
           IF ( WS-LINE-COUNT          < WS-MAX-LINES         )                 
                CONTINUE                                                        
           ELSE                                                                 
              PERFORM 3500-WRITE-HEADING  THRU 3500-EXIT                        
           END-IF.                                                              
                                                                                
           ADD  1                            TO WS-L273-PRINTED.                
           MOVE WS-L273-PRINTED              TO L273-SER-NO.                    
                                                                                
           MOVE    K800-NUM-REGN             TO L273-REGNO.                     
           IF K800-NUM-BTO-ZONE NOT = SPACES AND LOW-VALUES                     
              MOVE K800-NUM-BTO-ZONE         TO L273-ZONE                       
           ELSE                                                                 
              IF K800-NUM-ALLO-CAT = 'SBF' OR 'ROF'                             
                 MOVE K800-NUM-NEW-TOWN      TO L273-ZONE                       
              ELSE                                                              
                 MOVE K800-NUM-ZONE          TO L273-ZONE                       
              END-IF                                                            
           END-IF.                                                              
           MOVE    K800-NUM-FLAT-TYPE        TO L273-FT.                        
           MOVE    K800-DTE-BALLOT           TO L273-BAL1.                      
           MOVE    F200-DTE-BALLOT           TO L273-BAL2.                      
           MOVE    K800-NUM-STATUS           TO L273-APPL-STAT.                 
                                                                                
           WRITE BP13L273-REC  FROM L273-DETAIL-REC AFTER 1.                    
           ADD   1                           TO WS-LINE-COUNT.                  
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************                                                        
       3500-WRITE-HEADING.                                                      
      ******************                                                        
                                                                                
           MOVE  WS-EDIT-DATE          TO L273-DATE.                            
           ADD   1                     TO WS-PAGE-NO.                           
           MOVE  WS-PAGE-NO            TO L273-PAGE.                            
                                                                                
           WRITE BP13L273-REC  FROM L273-HDG1 AFTER ADVANCING PAGE.             
           WRITE BP13L273-REC  FROM L273-HDG2 AFTER ADVANCING 1.                
           WRITE BP13L273-REC  FROM L273-HDG3 AFTER ADVANCING 2.                
           WRITE BP13L273-REC  FROM L273-HDG4 AFTER ADVANCING 1.                
                                                                                
           MOVE SPACES  TO BP13L273-REC.                                        
           WRITE BP13L273-REC.                                                  
           MOVE  6      TO WS-LINE-COUNT.                                       
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ********************                                                      
       9000-CLOSE-ROUTINE.                                                      
      ********************                                                      
                                                                                
           CLOSE BP13F200                                                       
                 BP13F205                                                       
                 BP13K800                                                       
                 P13F200O                                                       
                 BP13L273.                                                      
                                                                                
           IF WS-K800-STATUS NOT = ZEROS                                        
              DISPLAY 'ERROR CLOSING BP13K800 FILE, STATUS '                    
                       WS-K800-STATUS                                           
           END-IF.                                                              
                                                                                
           DISPLAY '                                       '.                   
           DISPLAY ' *****  BP13C273 CONTROL TOTALS ****** '.                   
           DISPLAY ' RUN DATE : ' WS-DATE  (7:2) '/'                            
                                  WS-DATE  (5:2) '/'                            
                                  WS-DATE  (1:4).                               
           DISPLAY '                                       '.                   
           DISPLAY ' RECORDS READ    FROM BP13F200  = ' WS-F200-READ.           
           DISPLAY ' RECORDS WRITE   FROM BP13F200  = ' WS-F200-WRITE.          
           DISPLAY ' RECORDS DELETED FROM BP13F200  = ' WS-F200-DELETE.         
           DISPLAY ' RECORDS READ    FROM BP13F205  = ' WS-F205-READ.           
           DISPLAY ' RECORDS READ    FROM BP13K800  = ' WS-K800-READ.           
           DISPLAY ' RECORDS WRITTEN TO   BP13L273  = ' WS-L273-PRINTED.        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
**************************** END OF PROGRAM ****************************        
