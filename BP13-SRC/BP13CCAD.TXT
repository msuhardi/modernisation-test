       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CCAD.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  31/07/2009.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVE    :  PRINT LETTER ON INTEREST RATE REVISION FOR  *           
      *                 DBSS FLAT.                                  *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13FD00                                *           
      *                 2.  BP13K800                                *           
      *                 3.  BP13K820                                *           
      *                 4.  BP13K830                                *           
      *                 5.  AB03K050                                *           
      *                                                             *           
      * OUTPUT       :  1.  BP13PCAD                                *           
      *                 2.  BP13LCAD                                *           
      * =========================================================== *           
      * CHG REF  OIC  DATE     DESCRIPTION                          *           
      * -------- ---  -------- ------------------------------------ *           
      * BP133662 PCL3 20090731 NEW PROGRAM                          *           
      * BP134398 JB8  20101101 TO USE LN-SCH INSTEAD OF PYMT-SCH    *           
      * BP134040 ZDD1 20111227 TO INCLUDE CONTROL LISTING           *           
      * BP134398 MRN1 20120301 TAKE CURRENT DATE FROM SY02          *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13FD00  ASSIGN       TO BP13FD00.                           
                                                                                
           SELECT AB03K050  ASSIGN       TO AB03K050                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K050-KEY                            
                            FILE STATUS  IS WS-K050-STATUS.                     
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT BP13K830  ASSIGN       TO BP13K830                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
                            ALTERNATE RECORD KEY IS  K830-NUM-REGN              
                            FILE STATUS  IS WS-K830-STATUS.                     
                                                                                
           SELECT SY02F001  ASSIGN       TO SY02F001.                           
           SELECT BP13PCAD  ASSIGN       TO BP13PCAD.                           
           SELECT BP13LCAD  ASSIGN       TO BP13LCAD.                           
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FD00                                                            
            RECORDING MODE  IS   F                                              
            LABEL RECORDS   ARE  STANDARD                                       
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13FD00.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   AB03K050                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY AB03K050.                                                           
                                                                                
       FD   BP13PCAD                                                            
            RECORD CONTAINS 90 CHARACTERS                                       
            RECORDING MODE  IS  F.                                              
       01   BP13PCAD-REC                 PIC X(90).                             
                                                                                
       FD   BP13LCAD                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   BP13LCAD-REC                 PIC X(132).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS           PIC 99     VALUE 00.                    
           05  WS-K820-STATUS           PIC 99     VALUE 00.                    
           05  WS-K830-STATUS           PIC 99     VALUE 00.                    
           05  WS-K050-STATUS           PIC 99     VALUE 00.                    
           05  WS-EOF-FD00              PIC X      VALUE 'N'.                   
           05  WS-EOF-K050              PIC X      VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FD00-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-K800-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-K050-NFND             PIC 9(6)   VALUE ZEROS.                 
           05  WS-PIDB-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-LETTER-PRINTED        PIC 9(6)   VALUE ZEROS.                 
           05  WS-CONTROL-LIST          PIC 9(6)   VALUE ZEROS.                 
           05  WS-SNO                   PIC 9(5)   VALUE ZEROES.                
           05  WS-PAGE-CTR              PIC 9(4)   VALUE ZEROES.                
           05  WS-LINE-CTR              PIC 9(2)   VALUE 60.                    
                                                                                
       01  WS-INTREST-RATE1             PIC 9(2)V99 VALUE ZEROES.               
       01  WS-INTREST-RATE2             PIC 9(2)V99 VALUE ZEROES.               
       01  WS-EFF-DATE1                 PIC X(08)   VALUE SPACES.               
       01  WS-EFF-DATE2                 PIC X(08)   VALUE SPACES.               
       01  WS-DATE                      PIC X(8).                               
       01  WS-TEMP-DATE                 PIC X(8).                               
       01  WS-TEMP-DATE-R REDEFINES WS-TEMP-DATE.                               
           05 WS-CCYY                   PIC 9(4).                               
           05 WS-MM                     PIC 9(2).                               
           05 WS-DD                     PIC 9(2).                               
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
       01  WS-NAME                      PIC X(66)  VALUE SPACES.                
       01  WS-MAXLINE                   PIC 99     VALUE 57.                    
       01  WS-EOF-F001                  PIC X(1)   VALUE 'N'.                   
                                                                                
      *-------------------------------------------------------------            
      * FORMAT LETTER                                                           
      *-------------------------------------------------------------            
       01  PCAD-LINE01A.                                                        
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PCAD-HDB-REF         PIC X(14) VALUE SPACES.                     
                                                                                
       01  PCAD-LINE01.                                                         
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PCAD-OUR-REF         PIC X(8)  VALUE SPACES.                     
                                                                                
       01  PCAD-LINE02.                                                         
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PCAD-DATE            PIC X(10) VALUE SPACES.                     
                                                                                
       01  PCAD-LINE03.                                                         
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PCAD-NAME1           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PCAD-LINE03B.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PCAD-NAME2           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PCAD-LINE03C.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PCAD-NAME3           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PCAD-LINE03D.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PCAD-NAME4           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PCAD-LINE04.                                                         
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PCAD-BLK             PIC X(16) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE SPACES.                     
           05  PCAD-UNIT            PIC X(11) VALUE SPACES.                     
                                                                                
       01  PCAD-LINE04B.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PCAD-ADDR-L2         PIC X(65) VALUE SPACES.                     
                                                                                
       01  PCAD-LINE04C.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PCAD-ADDR-L3         PIC X(20) VALUE SPACES.                     
                                                                                
       01  PCAD-LINE05.                                                         
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  PCAD-BAR-CODE        PIC X(09) VALUE SPACES.                     
                                                                                
       01  PCAD-LINE06.                                                         
           05  FILLER               PIC X(11) VALUE SPACES.                     
           05  PCAD-FLAT-ADDRESS    PIC X(60) VALUE SPACES.                     
                                                                                
       01  PCAD-LINE07.                                                         
           05  FILLER               PIC X(22)   VALUE SPACES.                   
           05  PCAD-INTEREST-FROM   PIC ZZ.99   VALUE ZEROES.                   
           05  FILLER               PIC X(01)   VALUE '%'.                      
           05  FILLER               PIC X(03)   VALUE SPACES.                   
           05  PCAD-INTEREST-TO     PIC ZZ.99   VALUE ZEROES.                   
           05  FILLER               PIC X(01)   VALUE '%'.                      
           05  FILLER               PIC X(14)   VALUE SPACES.                   
           05  PCAD-INTEREST-DATE   PIC X(11)   VALUE SPACES.                   
                                                                                
       01  WS-BLANK-LINE          PIC X(132)     VALUE SPACES.                  
       01  WS-DASH-LINE           PIC X(132)     VALUE ALL '-'.                 
                                                                                
      *-------------------------------------------------------------            
      *  FOR CONTROL LISTING - BP13LCAD                                         
      *-------------------------------------------------------------            
       01  LCAD-HDG1.                                                           
           05  FILLER              PIC X(08)     VALUE 'BP13LCAD'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LCAD-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LCAD-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  LCAD-HDG2.                                                           
           05  FILLER              PIC X(42)     VALUE SPACES.                  
           05  FILLER              PIC X(24)     VALUE                          
           '      INTEREST FROM  -  '.                                          
           05  LCAD-INT-FROM       PIC ZZ.99 .                                  
           05  FILLER              PIC X(10)     VALUE                          
           '   TO  -  '.                                                        
           05  LCAD-INT-TO         PIC ZZ.99 .                                  
                                                                                
       01  LCAD-HDG3.                                                           
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE ' S/N '.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE '  REGN  '.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(13)     VALUE '  SCH ACCT   '.         
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PROJECT'.               
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'LN SCH'.                
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'CDE INT'.               
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'AMT INSTALL'.           
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(14)     VALUE 'DTE LOAN COMM'.         
                                                                                
       01  LCAD-DETAILS.                                                        
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LCAD-SNO            PIC ZZZZ9.                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LCAD-REGN           PIC X(08).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LCAD-SCH-ACCT       PIC X(13).                                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  LCAD-PROJECT        PIC X(07).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LCAD-LOAN-SCH       PIC X(06).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LCAD-CDE-INT        PIC X(07).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LCAD-AMT-INSTALL    PIC ZZZZZZ.99.                               
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  LCAD-DTE-LOAN-COMM  PIC X(14).                                   
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(04).                             
           05 WS-LINK-NUM-ACC            PIC X(04).                             
           05 WS-LINK-NUM-CHK            PIC X(01).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
            PERFORM 1000-OPEN-FILES-ROUTINE   THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13FD00        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-RECORD       THRU  3000-EXIT                   
              UNTIL WS-EOF-FD00 = 'Y'.                                          
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES-ROUTINE.                                                 
      *-------------------------------------------------------------            
                                                                                
            OPEN INPUT  BP13FD00                                                
                        BP13K800                                                
                        BP13K820                                                
                        BP13K830                                                
                        AB03K050                                                
                        SY02F001                                                
                 OUTPUT BP13PCAD                                                
                        BP13LCAD.                                               
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE     WS-K800-STATUS               TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE         THRU 9000-EXIT.               
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR OPENING : ' WS-K820-STATUS              
              MOVE     WS-K820-STATUS               TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE         THRU 9000-EXIT.               
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K830 - ERROR OPENING : ' WS-K830-STATUS              
              MOVE     WS-K830-STATUS               TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE         THRU 9000-EXIT.               
                                                                                
           IF WS-K050-STATUS NOT = 00 AND 97                                    
              DISPLAY 'AB03K050 - ERROR OPENING : ' WS-K050-STATUS              
              MOVE     WS-K050-STATUS               TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE         THRU 9000-EXIT.               
                                                                                
           PERFORM 1500-READ-SY02F001             THRU 1500-EXIT.               
                                                                                
      *    MOVE FUNCTION CURRENT-DATE(1:8)          TO WS-DATE.                 
           MOVE F001-DTE-CURRENT                    TO WS-DATE.                 
                                                                                
           STRING  WS-DATE(7:2) '/'                                             
                   WS-DATE(5:2) '/'                                             
                   WS-DATE(1:4)                                                 
                   DELIMITED BY SIZE INTO PCAD-DATE                             
           END-STRING.                                                          
                                                                                
           STRING  WS-DATE(7:2) '/'                                             
                   WS-DATE(5:2) '/'                                             
                   WS-DATE(1:4)                                                 
                   DELIMITED BY SIZE INTO LCAD-RUN-DATE                         
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1500-READ-SY02F001.                                                      
      *-------------------------------------------------------------            
           READ SY02F001 AT END MOVE 'Y' TO WS-EOF-F001                         
                GO TO 1500-EXIT.                                                
                                                                                
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FD00.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FD00 AT END                                                 
                MOVE 'Y'                              TO WS-EOF-FD00            
                GO TO 2000-EXIT.                                                
                                                                                
           ADD  1                                     TO WS-FD00-READ.          
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3100-INITIALIZE-FIELDS           THRU 3100-EXIT.             
                                                                                
           IF FD00-AMT-LOAN-BAL > 0                                             
              PERFORM 4000-READ-BP13K800            THRU 4000-EXIT              
                                                                                
              IF WS-K800-STATUS  = 00                                           
                 PERFORM 5000-GET-NAMES             THRU 5000-EXIT              
                 PERFORM 6000-GET-ADDRESS           THRU 6000-EXIT              
                 PERFORM 7000-GET-FLAT-ADDR         THRU 7000-EXIT              
                                                                                
                 IF FD00-CDE-LOAN-SCH NOT = SPACES AND LOW-VALUES               
                    PERFORM 7500-START-AB03K050     THRU 7500-EXIT              
                 END-IF                                                         
                                                                                
                 PERFORM 8000-PRINT-LETTER          THRU 8000-EXIT              
                 PERFORM 8100-PRINT-CONTROL-LIST    THRU 8100-EXIT              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD00               THRU 2000-EXIT.             
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3100-INITIALIZE-FIELDS.                                                  
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                    TO PCAD-HDB-REF                       
                                             PCAD-OUR-REF                       
                                             PCAD-NAME1                         
                                             PCAD-NAME2                         
                                             PCAD-NAME3                         
                                             PCAD-NAME4                         
                                             PCAD-BLK                           
                                             PCAD-UNIT                          
                                             PCAD-ADDR-L2                       
                                             PCAD-ADDR-L3                       
                                             PCAD-BAR-CODE                      
                                             PCAD-FLAT-ADDRESS                  
                                             PCAD-INTEREST-DATE.                
                                                                                
           MOVE ZEROES                    TO PCAD-INTEREST-TO                   
                                             PCAD-INTEREST-FROM.                
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE FD00-NUM-REGN                      TO K800-NUM-REGN.            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD  1                             TO WS-K800-READ              
           WHEN 23                                                              
                DISPLAY 'BP13K800 REC NOTFND, KEY ' K800-NUM-REGN               
           WHEN OTHER                                                           
                DISPLAY 'BP13K800 READ ERR, STATUS ' WS-K800-STATUS             
                MOVE WS-K800-STATUS                TO RETURN-CODE               
                PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5000-GET-NAMES.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE K800-NUM-NRIC1                     TO K820-NUM-NRIC.            
           PERFORM 5500-READ-BP13K820            THRU 5500-EXIT.                
           MOVE WS-NAME                            TO PCAD-NAME1.               
                                                                                
           IF K800-NUM-NRIC2 NOT =  SPACES AND  LOW-VALUES                      
              MOVE     K800-NUM-NRIC2              TO K820-NUM-NRIC             
              PERFORM  5500-READ-BP13K820        THRU 5500-EXIT                 
              MOVE     WS-NAME                     TO PCAD-NAME2                
           ELSE                                                                 
              MOVE     SPACES                      TO PCAD-NAME2                
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT =  SPACES AND LOW-VALUES                       
              MOVE     K800-NUM-NRIC3              TO K820-NUM-NRIC             
              PERFORM  5500-READ-BP13K820        THRU 5500-EXIT                 
              MOVE     WS-NAME                     TO PCAD-NAME3                
           ELSE                                                                 
              MOVE     SPACES                      TO PCAD-NAME3                
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT =  SPACES AND  LOW-VALUES                      
              MOVE     K800-NUM-NRIC4              TO K820-NUM-NRIC             
              PERFORM  5500-READ-BP13K820        THRU 5500-EXIT                 
              MOVE     WS-NAME                     TO PCAD-NAME4                
           ELSE                                                                 
              MOVE     SPACES                      TO PCAD-NAME4                
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5500-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                             TO WS-NAME.                  
                                                                                
           MOVE K800-NUM-REGN                      TO K820-NUM-REGN.            
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              MOVE  K820-NME-OCCP                  TO WS-NAME                   
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              DISPLAY 'RECORD NOTFND IN BP13K820 = ' WS-K820-STATUS             
                      ', KEY-FLD = ' K820-KEY-FLD                               
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820, STAT ' WS-K820-STATUS            
                      ', KEY-FLD = ' K820-KEY-FLD                               
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6000-GET-ADDRESS.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE FD00-NUM-REGN                      TO K830-NUM-REGN.            
                                                                                
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K830-STATUS                                              
           WHEN 00                                                              
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO PCAD-BLK                          
                 MOVE K830-NUM-FLAT        TO PCAD-UNIT                         
                 MOVE K830-NME-STREET-BLDG TO PCAD-ADDR-L2                      
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PCAD-ADDR-L3                      
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE    INTO PCAD-ADDR-L3                   
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-POSTAL TO AV02C015-IN-POSTCODE                
                    PERFORM 6500-CALL-AV02C015     THRU 6500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PCAD-BAR-CODE          
                 END-IF                                                         
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO PCAD-BLK                      
                 MOVE K830-NUM-MAIL-FLAT       TO PCAD-UNIT                     
                 MOVE K830-NME-MAIL-STR-BLDG   TO PCAD-ADDR-L2                  
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PCAD-ADDR-L3                  
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PCAD-ADDR-L3                  
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-MAIL-POSTAL TO AV02C015-IN-POSTCODE           
                    PERFORM 6500-CALL-AV02C015     THRU 6500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PCAD-BAR-CODE          
                 END-IF                                                         
              END-IF                                                            
           WHEN 23                                                              
              MOVE SPACES                             TO PCAD-BLK               
                                                         PCAD-UNIT              
                                                         PCAD-ADDR-L2           
                                                         PCAD-ADDR-L3           
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K830, STATUS ' WS-K830-STATUS          
                      ', KEY ' K830-KEY-FLD                                     
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6500-CALL-AV02C015.                                                      
      *-------------------------------------------------------------            
                                                                                
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       7000-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES                              TO BP13COMM8-REC.           
           MOVE K800-NUM-SCH-ACC(1:9)               TO WS-LINK-REC.             
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                ADD 1 TO WS-PIDB-READ                                           
                STRING 'BLK '  COMM8-NUM-BLK  '  '                              
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    '  ' DELIMITED BY SIZE            
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO PCAD-FLAT-ADDRESS                                   
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         K800-NUM-SCH-ACC ', REGN : ' FD00-NUM-REGN             
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' K800-NUM-SCH-ACC                            
                        ', REGN : ' FD00-NUM-REGN                               
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       7500-START-AB03K050.                                                     
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES                  TO AB03K050-REC.                        
           INITIALIZE                      AB03K050-REC.                        
                                                                                
           MOVE SPACES                  TO K050-KEY                             
                                           WS-EFF-DATE1                         
                                           WS-EFF-DATE2.                        
                                                                                
           MOVE ZEROES                  TO WS-INTREST-RATE1                     
                                           WS-INTREST-RATE2.                    
                                                                                
           MOVE 'N'                     TO WS-EOF-K050.                         
           MOVE FD00-CDE-LOAN-SCH       TO K050-CDE-LOAN-SCH.                   
                                                                                
           START AB03K050 KEY > K050-KEY.                                       
                                                                                
           EVALUATE WS-K050-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 7600-READNEXT-AB03K050 THRU 7600-EXIT               
                    PERFORM 7700-GET-INTEREST-RATE THRU 7700-EXIT               
                      UNTIL WS-EOF-K050 = 'Y'                                   
               WHEN 20                                                          
               WHEN 23                                                          
                    ADD 1                            TO WS-K050-NFND            
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR AB03K050 : ' WS-K050-STATUS            
                             ' KEY : ' K050-KEY                                 
                    PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT               
           END-EVALUATE.                                                        
                                                                                
       7500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       7600-READNEXT-AB03K050.                                                  
      *-------------------------------------------------------------*           
                                                                                
           READ AB03K050 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K050.                        
                                                                                
       7600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       7700-GET-INTEREST-RATE.                                                  
      *-------------------------------------------------------------*           
                                                                                
            IF K050-PCT-PA-INT NOT NUMERIC                                      
               MOVE ZEROES                         TO K050-PCT-PA-INT           
            END-IF.                                                             
                                                                                
            IF (K050-CDE-LOAN-SCH = FD00-CDE-LOAN-SCH) AND                      
               (K050-DTE-INT-EFF NOT = SPACES AND LOW-VALUES AND ZEROES)        
               IF K050-DTE-INT-EFF <= WS-DATE                                   
                  IF WS-EFF-DATE1 = SPACES OR                                   
                     WS-EFF-DATE2 = SPACES                                      
                     MOVE K050-DTE-INT-EFF         TO WS-EFF-DATE1              
                                                      WS-EFF-DATE2              
                     MOVE K050-PCT-PA-INT          TO WS-INTREST-RATE1          
                                                      WS-INTREST-RATE2          
                  END-IF                                                        
                                                                                
                  IF K050-DTE-INT-EFF > WS-EFF-DATE1                            
                     IF WS-EFF-DATE2 > WS-EFF-DATE1                             
                        MOVE WS-EFF-DATE2          TO WS-EFF-DATE1              
                        MOVE WS-INTREST-RATE2      TO WS-INTREST-RATE1          
                     END-IF                                                     
                                                                                
                     MOVE K050-DTE-INT-EFF         TO WS-EFF-DATE2              
                     MOVE K050-PCT-PA-INT          TO WS-INTREST-RATE2          
                  END-IF                                                        
                                                                                
                  PERFORM 7600-READNEXT-AB03K050 THRU 7600-EXIT                 
               ELSE                                                             
                  MOVE WS-INTREST-RATE2             TO WS-INTREST-RATE1         
                  MOVE K050-PCT-PA-INT              TO WS-INTREST-RATE2         
                  MOVE K050-DTE-INT-EFF             TO WS-EFF-DATE2             
                  MOVE 'Y'                          TO WS-EOF-K050              
               END-IF                                                           
            ELSE                                                                
               MOVE 'Y'                             TO WS-EOF-K050              
            END-IF.                                                             
                                                                                
       7700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       8000-PRINT-LETTER.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE  FD00-NUM-REGN           TO PCAD-OUR-REF.                       
                                                                                
           STRING FD00-NUM-SCH-ACCNT(1:4) '-'                                   
                  FD00-NUM-SCH-ACCNT(5:4) '-'                                   
                  FD00-NUM-SCH-ACCNT(9:1) '-'                                   
                  FD00-NUM-SCH-ACCNT(10:2)                                      
                  DELIMITED BY SIZE INTO PCAD-HDB-REF.                          
                                                                                
           MOVE WS-INTREST-RATE1         TO PCAD-INTEREST-FROM.                 
           MOVE WS-INTREST-RATE2         TO PCAD-INTEREST-TO.                   
                                                                                
           IF WS-EFF-DATE2 NOT = SPACES                                         
              MOVE WS-EFF-DATE2          TO WS-TEMP-DATE                        
                                                                                
              STRING WS-DD ' ' WS-MONTH(WS-MM) ' ' WS-CCYY                      
                       DELIMITED BY SIZE INTO PCAD-INTEREST-DATE                
           END-IF.                                                              
                                                                                
           MOVE SPACES                TO BP13PCAD-REC.                          
           WRITE BP13PCAD-REC       FROM PCAD-LINE01A AFTER PAGE.               
           WRITE BP13PCAD-REC       FROM PCAD-LINE01.                           
           WRITE BP13PCAD-REC       FROM PCAD-LINE02.                           
           WRITE BP13PCAD-REC       FROM PCAD-LINE03 AFTER 4.                   
           WRITE BP13PCAD-REC       FROM PCAD-LINE03B.                          
           WRITE BP13PCAD-REC       FROM PCAD-LINE03C.                          
           WRITE BP13PCAD-REC       FROM PCAD-LINE03D.                          
           WRITE BP13PCAD-REC       FROM PCAD-LINE04.                           
           WRITE BP13PCAD-REC       FROM PCAD-LINE04B.                          
           WRITE BP13PCAD-REC       FROM PCAD-LINE04C.                          
           WRITE BP13PCAD-REC       FROM PCAD-LINE05 AFTER 2.                   
           WRITE BP13PCAD-REC       FROM PCAD-LINE06 AFTER 6.                   
           WRITE BP13PCAD-REC       FROM PCAD-LINE07 AFTER 4.                   
                                                                                
           ADD 1 TO WS-LETTER-PRINTED.                                          
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       8100-PRINT-CONTROL-LIST.                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                   TO BP13LCAD-REC.                       
           INITIALIZE                       BP13LCAD-REC.                       
                                                                                
           IF WS-LINE-CTR >= 60                                                 
              ADD  1                   TO WS-PAGE-CTR                           
              MOVE WS-PAGE-CTR         TO LCAD-PAGE-NO                          
              MOVE WS-INTREST-RATE1    TO LCAD-INT-FROM                         
              MOVE WS-INTREST-RATE2    TO LCAD-INT-TO                           
              WRITE BP13LCAD-REC     FROM LCAD-HDG1                             
              WRITE BP13LCAD-REC     FROM LCAD-HDG2                             
              WRITE BP13LCAD-REC     FROM LCAD-HDG3                             
              MOVE 1                   TO WS-LINE-CTR                           
           END-IF.                                                              
                                                                                
           ADD  1                   TO WS-SNO                                   
           MOVE WS-SNO              TO LCAD-SNO                                 
           MOVE  FD00-NUM-REGN      TO LCAD-REGN.                               
                                                                                
           STRING FD00-NUM-SCH-ACCNT(1:4) '-'                                   
                  FD00-NUM-SCH-ACCNT(5:4) '-'                                   
                  FD00-NUM-SCH-ACCNT(9:1) '-'                                   
                  FD00-NUM-SCH-ACCNT(10:2)                                      
                  DELIMITED BY SIZE INTO LCAD-SCH-ACCT.                         
                                                                                
           MOVE  FD00-NUM-PROJECT  TO    LCAD-PROJECT.                          
           MOVE  FD00-CDE-LOAN-SCH TO    LCAD-LOAN-SCH.                         
           MOVE  FD00-CDE-INT      TO    LCAD-CDE-INT.                          
           MOVE  FD00-AMT-INSTAL   TO    LCAD-AMT-INSTALL.                      
                                                                                
           STRING  FD00-DTE-LOAN-COMM(7:2) '/'                                  
                   FD00-DTE-LOAN-COMM(5:2) '/'                                  
                   FD00-DTE-LOAN-COMM(1:4)                                      
                   DELIMITED BY SIZE INTO LCAD-DTE-LOAN-COMM                    
           END-STRING.                                                          
                                                                                
                                                                                
           WRITE BP13LCAD-REC       FROM LCAD-DETAILS.                          
                                                                                
           ADD 1 TO WS-CONTROL-LIST                                             
                    WS-LINE-CTR.                                                
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' -- BP13CCAD CONTROL TOTAL -------'.                        
           DISPLAY ' '.                                                         
           DISPLAY ' TOTAL READ FROM FD00 : ' WS-FD00-READ.                     
           DISPLAY ' TOTAL READ FROM K800 : ' WS-K800-READ.                     
           DISPLAY ' TOTAL READ FROM PIDB : ' WS-PIDB-READ.                     
           DISPLAY ' TOTAL LETTER PRINTED : ' WS-LETTER-PRINTED.                
           DISPLAY ' TOTAL REC ON CTRL LST: ' WS-CONTROL-LIST.                  
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13FD00                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 AB03K050                                                       
                 SY02F001                                                       
                 BP13PCAD                                                       
                 BP13LCAD.                                                      
                                                                                
           IF WS-K800-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K800, STAT ' WS-K800-STATUS.           
                                                                                
           IF WS-K820-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K820, STAT ' WS-K820-STATUS.           
                                                                                
           IF WS-K830-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K830, STAT ' WS-K830-STATUS.           
                                                                                
           IF WS-K050-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR AB03K050, STAT ' WS-K050-STATUS.           
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
