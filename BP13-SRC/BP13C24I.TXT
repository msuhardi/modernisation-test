      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C24I.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  11 APR 2012.                                              
      *--------------------------------------------------------------*          
      *                                                              *          
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)                 *          
      *                                                              *          
      *    SYSTEM ID   : BP13                                        *          
      *                                                              *          
      *    OBJECTIVES :  CREATE BP13K110 FOR FLATLIST                *          
      *                                                              *          
      *                                                              *          
      *    INPUT FILES  : BP13FQ50                                   *          
      *    OUTPUT       : BP13K110                                   *          
      *--------------------------------------------------------------*          
      * CHG-NO  BY   DATE     DETAILS                                *          
      *-------- ---- -------- ---------------------------------------*          
      *BP134513 ESA1 11042012 NEW PGM                                *          
      *BP135119 ESA1 29102013 TO CATER FOR PARENT FLAT TYPE VALIDATION          
      *-------- ---- -------- ---------------------------------------*          
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BP13FQ50 ASSIGN TO BP13FQ50.                                  
                                                                                
           SELECT BP13K110 ASSIGN TO BP13K110                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            RECORDING MODE  IS F.                                               
       COPY BP13F205.                                                           
                                                                                
       FD   BP13FQ50                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       COPY BP13FQ50.                                                           
                                                                                
       FD   BP13K110                                                            
            RECORD CONTAINS 80 CHARACTERS.                                      
       COPY BP13K110.                                                           
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-FQ50-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-F205-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-K110-STATUS             PIC 9(2)  VALUE 0.                    
           05  WS-CNT-UNIT                PIC 9(2)  VALUE 0.                    
           05  WS-CNT-FQ50-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-K110-WRITE          PIC 9(8)  VALUE 0.                    
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-EDITED-DATE             PIC X(10)  VALUE SPACES.              
           05  WS-FT                      PIC X(02)  VALUE SPACES.              
           05  WS-PARENT-FT               PIC X(02)  VALUE SPACES.              
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
                                                                                
       01  WS-FQ50-PREV-KEY-FLD.                                                
           05 WS-FQ50-PREV-ESTATE         PIC X(03) VALUE SPACES.               
           05 WS-FQ50-PREV-NGHBRD         PIC X(04) VALUE SPACES.               
           05 WS-FQ50-PREV-CNTRCT         PIC X(04) VALUE SPACES.               
           05 WS-FQ50-PREV-BLOCK          PIC X(05) VALUE SPACES.               
           05 WS-FQ50-PREV-FT             PIC X(02) VALUE SPACES.               
           05 WS-FQ50-PREV-WARD           PIC X(03) VALUE SPACES.               
           05 WS-FQ50-PREV-NT             PIC X(03) VALUE SPACES.               
                                                                                
       01  WS-FQ50-KEY-FLD.                                                     
           05 WS-FQ50-ESTATE              PIC X(03) VALUE SPACES.               
           05 WS-FQ50-NGHBRD              PIC X(04) VALUE SPACES.               
           05 WS-FQ50-CNTRCT              PIC X(04) VALUE SPACES.               
           05 WS-FQ50-BLOCK               PIC X(05) VALUE SPACES.               
           05 WS-FQ50-FT                  PIC X(02) VALUE SPACES.               
           05 WS-FQ50-WARD                PIC X(03) VALUE SPACES.               
           05 WS-FQ50-NT                  PIC X(03) VALUE SPACES.               
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 1100-READ-F205         THRU 1100-EXIT.                       
                                                                                
           PERFORM 1200-READ-FQ50         THRU 1200-EXIT.                       
                                                                                
           PERFORM 2000-PROCESS           THRU 2000-EXIT                        
                UNTIL WS-FQ50-EOF = 'Y'.                                        
                                                                                
           IF WS-CNT-UNIT > ZEROES                                              
              PERFORM 3000-WRITE-K110    THRU 3000-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13FQ50                                                 
                I-O    BP13K110.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       1100-READ-F205.                                                          
      *--------------------------------------------------------------*          
                                                                                
           READ BP13F205                                                        
                AT END  MOVE 'Y' TO WS-F205-EOF                                 
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
                                                                                
           IF F205-NUM-GRO = 'C'                                                
              IF F205-NUM-PARENT-FT = 'SA'                                      
                 MOVE '1 '                    TO WS-PARENT-FT                   
              ELSE                                                              
                 MOVE F205-NUM-PARENT-FT(1:1) TO WS-PARENT-FT                   
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES                     TO WS-PARENT-FT                   
           END-IF.                                                              
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       1200-READ-FQ50.                                                          
      *--------------------------------------------------------------*          
                                                                                
           READ BP13FQ50                                                        
                AT END  MOVE 'Y' TO WS-FQ50-EOF                                 
                GO TO 1200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD   1     TO   WS-CNT-FQ50-READ.                                   
                                                                                
           MOVE FQ50-NUM-ESTATE        TO WS-FQ50-ESTATE.                       
           MOVE FQ50-NUM-NGHBRD        TO WS-FQ50-NGHBRD.                       
           MOVE FQ50-NUM-CNTRCT        TO WS-FQ50-CNTRCT.                       
           MOVE FQ50-NUM-BLOCK         TO WS-FQ50-BLOCK.                        
           IF FQ50-NUM-TYP-ACTUSE = '1A' OR '1B'                                
              MOVE '1A'                TO FQ50-NUM-TYP-ACTUSE                   
           ELSE                                                                 
              IF FQ50-NUM-TYP-ACTUSE = '2A' OR '2B' OR '2 '                     
                 MOVE '2A'                TO FQ50-NUM-TYP-ACTUSE                
              ELSE                                                              
                 IF FQ50-NUM-TYP-ACTUSE = '3A' OR '3 '                          
                    MOVE '3A'                TO FQ50-NUM-TYP-ACTUSE             
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
           MOVE FQ50-NUM-TYP-ACTUSE    TO WS-FQ50-FT.                           
           MOVE FQ50-NUM-NT            TO WS-FQ50-NT.                           
           MOVE FQ50-NUM-WARD          TO WS-FQ50-WARD.                         
                                                                                
           IF WS-CNT-FQ50-READ = 1                                              
              MOVE FQ50-NUM-ESTATE     TO WS-FQ50-PREV-ESTATE                   
              MOVE FQ50-NUM-NGHBRD     TO WS-FQ50-PREV-NGHBRD                   
              MOVE FQ50-NUM-CNTRCT     TO WS-FQ50-PREV-CNTRCT                   
              MOVE FQ50-NUM-BLOCK      TO WS-FQ50-PREV-BLOCK                    
              MOVE FQ50-NUM-TYP-ACTUSE TO WS-FQ50-PREV-FT                       
              MOVE FQ50-NUM-NT         TO WS-FQ50-PREV-NT                       
              MOVE FQ50-NUM-WARD       TO WS-FQ50-PREV-WARD                     
           END-IF.                                                              
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-PROCESS.                                                            
      *--------------------------------------------------------------*          
                                                                                
           IF ((F205-NUM-GRO = FQ50-NUM-MGPS) OR                                
               (F205-NUM-GRO = 'Y' AND (FQ50-NUM-MGPS = SPACES                  
                OR LOW-VALUES)))                                                
               IF WS-PARENT-FT NOT = SPACES AND LOW-VALUES                      
                  IF WS-PARENT-FT(1:1) = FQ50-NUM-PARENT-FT(1:1)                
                     IF WS-FQ50-KEY-FLD NOT = WS-FQ50-PREV-KEY-FLD              
                        PERFORM 3000-WRITE-K110    THRU 3000-EXIT               
                        MOVE 1                TO WS-CNT-UNIT                    
                     ELSE                                                       
                        ADD  1                TO WS-CNT-UNIT                    
                     END-IF                                                     
                     MOVE FQ50-NUM-ESTATE     TO WS-FQ50-PREV-ESTATE            
                     MOVE FQ50-NUM-NGHBRD     TO WS-FQ50-PREV-NGHBRD            
                     MOVE FQ50-NUM-CNTRCT     TO WS-FQ50-PREV-CNTRCT            
                     MOVE FQ50-NUM-BLOCK      TO WS-FQ50-PREV-BLOCK             
                     MOVE FQ50-NUM-TYP-ACTUSE TO WS-FQ50-PREV-FT                
                     MOVE FQ50-NUM-NT         TO WS-FQ50-PREV-NT                
                     MOVE FQ50-NUM-WARD       TO WS-FQ50-PREV-WARD              
                  END-IF                                                        
               ELSE                                                             
                  IF WS-FQ50-KEY-FLD NOT = WS-FQ50-PREV-KEY-FLD                 
                     PERFORM 3000-WRITE-K110    THRU 3000-EXIT                  
                     MOVE 1                TO WS-CNT-UNIT                       
                  ELSE                                                          
                     ADD  1                TO WS-CNT-UNIT                       
                  END-IF                                                        
                  MOVE FQ50-NUM-ESTATE     TO WS-FQ50-PREV-ESTATE               
                  MOVE FQ50-NUM-NGHBRD     TO WS-FQ50-PREV-NGHBRD               
                  MOVE FQ50-NUM-CNTRCT     TO WS-FQ50-PREV-CNTRCT               
                  MOVE FQ50-NUM-BLOCK      TO WS-FQ50-PREV-BLOCK                
                  MOVE FQ50-NUM-TYP-ACTUSE TO WS-FQ50-PREV-FT                   
                  MOVE FQ50-NUM-NT         TO WS-FQ50-PREV-NT                   
                  MOVE FQ50-NUM-WARD       TO WS-FQ50-PREV-WARD                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 1200-READ-FQ50        THRU 1200-EXIT.                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3000-WRITE-K110.                                                         
      *--------------------------------------------------------------*          
           MOVE SPACES               TO  BP13K110-REC.                          
           INITIALIZE                    BP13K110-REC.                          
           IF F205-NUM-GRO = 'Y'                                                
              MOVE WS-FQ50-PREV-WARD      TO  K110-NUM-WARD                     
           ELSE                                                                 
              MOVE 'MGP'             TO  K110-NUM-WARD                          
           END-IF.                                                              
           MOVE WS-FQ50-PREV-NT      TO  K110-NUM-ESTATE.                       
           MOVE WS-FQ50-PREV-FT      TO  K110-NUM-TYP-ACTUSE.                   
           MOVE WS-FQ50-PREV-NGHBRD  TO  K110-NUM-NEIGH.                        
           MOVE WS-FQ50-PREV-CNTRCT  TO  K110-NUM-CNTRCT.                       
           MOVE WS-FQ50-PREV-BLOCK   TO  K110-NUM-BLK.                          
           MOVE WS-CNT-UNIT          TO  K110-NUM-UNIT.                         
                                                                                
           WRITE BP13K110-REC.                                                  
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
               WHEN 22                                                          
                 ADD 1    TO WS-CNT-K110-WRITE                                  
                 IF WS-FQ50-PREV-FT = '1A'                                      
                    MOVE '1B'              TO WS-FT                             
                    PERFORM 3100-WRITE-K110 THRU 3100-EXIT                      
                 ELSE                                                           
                    IF WS-FQ50-PREV-FT = '2A'                                   
                       MOVE '2B'              TO WS-FT                          
                       PERFORM 3100-WRITE-K110 THRU 3100-EXIT                   
                       MOVE '2 '              TO WS-FT                          
                       PERFORM 3100-WRITE-K110 THRU 3100-EXIT                   
                     ELSE                                                       
                       IF WS-FQ50-PREV-FT = '3A'                                
                          MOVE '3B'              TO WS-FT                       
                          PERFORM 3100-WRITE-K110 THRU 3100-EXIT                
                          MOVE '3 '              TO WS-FT                       
                          PERFORM 3100-WRITE-K110 THRU 3100-EXIT                
                       END-IF                                                   
                     END-IF                                                     
                 END-IF                                                         
               WHEN OTHER                                                       
                 DISPLAY 'ERROR WRITING BP13K110. STATUS IS '                   
                          WS-K110-STATUS                                        
                 PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       3100-WRITE-K110.                                                         
      *--------------------------------------------------------------*          
           MOVE SPACES               TO  BP13K110-REC.                          
           INITIALIZE                    BP13K110-REC.                          
           IF F205-NUM-GRO = 'Y'                                                
              MOVE WS-FQ50-PREV-WARD      TO  K110-NUM-WARD                     
           ELSE                                                                 
              MOVE 'MGP'             TO  K110-NUM-WARD                          
           END-IF.                                                              
           MOVE WS-FQ50-PREV-NT      TO  K110-NUM-ESTATE.                       
           MOVE WS-FT                TO  K110-NUM-TYP-ACTUSE.                   
           MOVE WS-FQ50-PREV-NGHBRD  TO  K110-NUM-NEIGH.                        
           MOVE WS-FQ50-PREV-CNTRCT  TO  K110-NUM-CNTRCT.                       
           MOVE WS-FQ50-PREV-BLOCK   TO  K110-NUM-BLK.                          
           MOVE WS-CNT-UNIT          TO  K110-NUM-UNIT.                         
                                                                                
           WRITE BP13K110-REC.                                                  
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
               WHEN 22                                                          
                 ADD 1    TO WS-CNT-K110-WRITE                                  
                 CONTINUE                                                       
               WHEN OTHER                                                       
                 DISPLAY 'ERROR WRITING BP13K110. STATUS IS '                   
                          WS-K110-STATUS                                        
                 PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
                                                                                
           CLOSE BP13FQ50                                                       
                 BP13K110.                                                      
                                                                                
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K110-STATUS        TO RETURN-CODE                      
                 DISPLAY 'CLOSE BP13K110 FAIL. STATUS=' WS-K110-STATUS          
           END-EVALUATE.                                                        
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '********* BP13C24I CONTROL *********'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13FQ50) = ' WS-CNT-FQ50-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF WRITE(BP13K110) = ' WS-CNT-K110-WRITE.               
           DISPLAY SPACES.                                                      
           DISPLAY '************************************'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
