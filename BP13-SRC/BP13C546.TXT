       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   PREC546A.                                                  
      *AUTHOR.       RAYATHATHA ASHOK.                                          
      *DATE-WRITTEN. 24/04/92.                                                  
      ***********************************************************               
      *                                                         *               
      *  OBJECTIVE: TO DELETE THE REQUEST REC FROM REQUEST FILE *               
      *             K500 FOR THOSE WHO HAS BALLOTED IN THAT     *               
      *             QUARTER AND LOAD THE RECORD TO HISTORY FILE *               
      *             BP13K595. THE MATCHING IS PERFORM BY REF-NO *               
      *             ON BALLOT FILE AND REQUEST FILE.            *               
      *                                                         *               
      *                                                         *               
      *  INPUT FILE :- BP13F520 (REQUEST  FILE)                 *               
      *                BP13F500 (REQUESTS FILE)                 *               
      *  OUTPUT FILE:- BP13F595 (BALLOTING LOG FILE)            *               
      *                BP13F510 (REQUEST  FILE)                 *               
      *                                                         *               
      * C9204F6 LSB 220592 - CHECK ON DEBAR FILE FOR STATUS-    *               
      *                      REQUEST 'E', CHG TO SEQ MATCHING   *               
      * N930012 YPH 140793 - SOC PH8 - COBOL2                   *               
      * BP130418CLT 200998 - Y2K COMPLIANCE CHANGES             *               
      *BP130652 NOR 190299 - EXPAND F500, F510, F520 AND F595   *               
      *                      RECL FROM 300 TO 500 CHARACTERS    *               
      *BP132122 CKK 270302 - DON'T READ BP13K542                *               
      *BP132360 CKK 170703 - MOVE CDE-REQUEST-STATUS DIRECTLY   *               
      *BP132810 CKK 300306 - CHANGE REF NO FROM 6 TO 8 BYTES    *               
      ***********************************************************               
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13F595 ASSIGN TO BP13F595.                                  
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13F510 ASSIGN TO BP13F510.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F520   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 500 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F520.                                                           
                                                                                
       FD  BP13F595   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 500 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F595.                                                           
                                                                                
       FD  BP13F500   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 500 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F500.                                                           
                                                                                
       FD  BP13F510   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 500 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F510.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CC                    PIC 9(2).                               
           05  WS-YY                    PIC 9(2).                               
           05  WS-MM                    PIC 9(2).                               
           05  WS-DD                    PIC 9(2).                               
                                                                                
       01  WS-KEY-F520.                                                         
           05  WS-F520-REF              PIC X(8).                               
           05  WS-RUBBISH               PIC X(12).                              
                                                                                
       01  WS-SYSTEM-TIME.                                                      
           05  WS-SYS-HH               PIC 9(2).                                
           05  WS-SYS-MM               PIC 9(2).                                
           05  WS-SYS-SS               PIC 9(2).                                
           05  WS-SYS-CC               PIC 9(2).                                
                                                                                
       01  WS-SWITCHES.                                                         
                                                                                
           05  WS-DEBAR-FOUND            PIC X    VALUE 'N'.                    
           05  WS-READ-CNT-F520          PIC 9(6) VALUE ZEROS.                  
           05  WS-READ-CNT-F500          PIC 9(6) VALUE ZEROS.                  
           05  WS-NO-MATCH               PIC 9(6) VALUE ZEROS.                  
           05  WS-NO-NOMATCH             PIC 9(6) VALUE ZEROS.                  
           05  WS-BALLOT-NOMATCH         PIC 9(6) VALUE ZEROS.                  
           05  WS-S-STATUS-REQ           PIC 9(6) VALUE ZEROS.                  
           05  WS-U-STATUS-REQ           PIC 9(6) VALUE ZEROS.                  
           05  WS-R-STATUS-REQ           PIC 9(6) VALUE ZEROS.                  
           05  WS-C-STATUS-REQ           PIC 9(6) VALUE ZEROS.                  
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F520    THRU 200-EXIT.                             
            PERFORM 300-READ-F500    THRU 300-EXIT.                             
            PERFORM 500-PROCESS-F520 THRU 500-EXIT                              
                    UNTIL F520-NUM-REF = HIGH-VALUES AND                        
                          F500-NUM-REF = HIGH-VALUES.                           
            PERFORM 999-CLOSE-FILES  THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
           OPEN INPUT  BP13F520                                                 
                       BP13F500                                                 
                OUTPUT BP13F595                                                 
                       BP13F510.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F520.                                                           
           READ BP13F520 AT END                                                 
           MOVE HIGH-VALUES              TO F520-NUM-REF                        
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-READ-CNT-F520.                   
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F500.                                                           
           READ BP13F500 AT END                                                 
           MOVE HIGH-VALUES              TO F500-NUM-REF                        
           GO TO 300-EXIT.                                                      
                                                                                
           ADD 1                       TO WS-READ-CNT-F500.                     
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-PROCESS-F520.                                                        
                                                                                
           IF F520-NUM-REF = F500-NUM-REF                                       
              ADD 1                      TO WS-NO-MATCH                         
              PERFORM 600-MATCH-FOUND    THRU 600-EXIT                          
              PERFORM 200-READ-F520      THRU 200-EXIT                          
              PERFORM 300-READ-F500      THRU 300-EXIT                          
           ELSE                                                                 
              IF F520-NUM-REF > F500-NUM-REF                                    
                 ADD 1                   TO WS-NO-NOMATCH                       
                 PERFORM 700-WRITE-F510  THRU 700-EXIT                          
                 PERFORM 300-READ-F500   THRU 300-EXIT                          
              ELSE                                                              
                 ADD 1                   TO WS-BALLOT-NOMATCH                   
                 PERFORM 600-MATCH-FOUND THRU 600-EXIT                          
                 PERFORM 200-READ-F520   THRU 200-EXIT.                         
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-MATCH-FOUND.                                                         
           MOVE SPACES            TO BP13F595-REC.                              
           MOVE BP13F520-REC      TO BP13F595-REC.                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           MOVE WS-CUR-DATE           TO F595-DTE-UPDATE.                       
                                                                                
      *    ACCEPT WS-SYSTEM-TIME FROM TIME.                                     
           MOVE FUNCTION CURRENT-DATE(9:8)  TO  WS-SYSTEM-TIME.                 
           MOVE WS-SYSTEM-TIME   TO F595-TME-SYSTEM.                            
                                                                                
      **  'C'- NOT ELIGIBLE CASES                                               
      **  'U'- UNSUCESSFUL                                                      
      **  'S'- SUCESSFUL  FOR 1% T 100%                                         
      **  'R'- SUCESSFUL  FOR 101% TO 200%                                      
                                                                                
      *       IF F520-CDE-SUCC-NT-ZONE = SPACES OR LOW-VALUES                   
      *          ADD 1 TO WS-U-STATUS-REQ                                       
      *          MOVE 'U'            TO F595-CDE-REQUEST-STATUS                 
      *       ELSE                                                              
      *          ADD 1 TO WS-S-STATUS-REQ                                       
      *          MOVE 'S'           TO F595-CDE-REQUEST-STATUS                  
                                                                                
           IF F520-CDE-SUCC-NT-ZONE = SPACES OR LOW-VALUES                      
              ADD 1 TO WS-U-STATUS-REQ                                          
           ELSE                                                                 
              IF F520-CDE-REQUEST-STATUS = 'S'                                  
                 ADD 1 TO WS-S-STATUS-REQ                                       
              ELSE                                                              
                 IF F520-CDE-REQUEST-STATUS = 'R'                               
                    ADD 1 TO WS-R-STATUS-REQ                                    
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F520-CDE-REQUEST-STATUS  TO F595-CDE-REQUEST-STATUS.            
           MOVE SPACES              TO F595-NUM-REGN-REF.                       
                                                                                
           IF F520-NUM-RANDOM(28:1) = 'G'                                       
              MOVE F520-NUM-REGN    TO F595-NUM-REGN-REF                        
           ELSE                                                                 
              MOVE F520-NUM-REF     TO F595-NUM-REGN-REF                        
           END-IF.                                                              
                                                                                
           MOVE 'P13C546'           TO F595-NUM-USER-ID.                        
           WRITE BP13F595-REC.                                                  
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-WRITE-F510.                                                          
           MOVE SPACES            TO BP13F510-REC.                              
           MOVE BP13F500-REC      TO BP13F510-REC.                              
           WRITE BP13F510-REC.                                                  
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
                                                                                
           CLOSE BP13F520                                                       
                 BP13F595                                                       
                 BP13F500                                                       
                 BP13F510.                                                      
                                                                                
           DISPLAY '*------------------ BP13C546 -------------------'.          
                                                                                
           DISPLAY 'NO OF REC READ      BP13F520     = '                        
                    WS-READ-CNT-F520.                                           
           DISPLAY 'NO OF REC READ      BP13F500     = '                        
                    WS-READ-CNT-F500.                                           
           DISPLAY 'NO OF REC MATCHED IN F500 & F520 = '                        
                    WS-NO-MATCH.                                                
           DISPLAY 'NO OF REC NOT FOUND IN BP13F520  = '                        
                    WS-NO-NOMATCH.                                              
           DISPLAY 'NO OF REC NOT BALLOTED,NOTFD F500= '                        
                    WS-BALLOT-NOMATCH.                                          
           DISPLAY 'NO OF REQUEST-STATUS = R         = '                        
                    WS-R-STATUS-REQ.                                            
           DISPLAY 'NO OF REQUEST-STATUS = C         = '                        
                    WS-C-STATUS-REQ.                                            
           DISPLAY 'NO OF REQUEST-STATUS = S         = '                        
                    WS-S-STATUS-REQ.                                            
           DISPLAY 'NO OF REQUEST-STATUS = U         = '                        
                    WS-U-STATUS-REQ.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
