       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C518.                                                 
      *AUTHOR.        ARIEL HERNANDEZ.                                          
      *DATE-WRITTEN.  05/10/2007.                                               
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVE:  TO CREATE F716 FILE. INCLUDE ONLY       *                
      *                   RECORDS REGISTERED IN CURRENT MONTH  *                
      *                                                        *                
      *    INPUT FILE  : BP13F820                              *                
      *                : BP13F800                              *                
      *    OUTPUT FILE : BP13F716                             *                 
      *========================================================*                
      * CHG-NO   BY   ON     DESCRIPTION                       *                
      * --------------------------------                       *                
      * BP133145 BA3  051007 NEW PROGRAM                       *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13F820   ASSIGN       TO BP13F820.                          
                                                                                
           SELECT BP13F800   ASSIGN       TO BP13F800.                          
                                                                                
           SELECT BP13F716   ASSIGN       TO BP13F716.                          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13F820                                                             
            RECORD CONTAINS 400 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F820.                                                           
                                                                                
       FD  BP13F800                                                             
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F716.                                                           
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 CNT-F800-READ        PIC 9(8)    VALUE ZEROES.                    
           05 CNT-F820-READ        PIC 9(8)    VALUE ZEROES.                    
           05 CNT-F716-WRITE       PIC 9(8)    VALUE ZEROES.                    
           05 CNT-F800-NOTFND      PIC 9(8)    VALUE ZEROES.                    
           05 CNT-F820-NOTFND      PIC 9(8)    VALUE ZEROES.                    
           05 CNT-REC-EQUAL        PIC 9(8)    VALUE ZEROES.                    
           05 WS-F820-NUM-REGN-PRV PIC X(8)    VALUE SPACES.                    
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-CURR-CC           PIC 9(02).                                   
           05 WS-YY                PIC 9(02).                                   
           05 WS-MM                PIC 9(02).                                   
           05 WS-DD                PIC 9(02).                                   
       01  WS-SYS-DATE-TEMP REDEFINES WS-SYS-DATE.                              
           05 WS-CURR-CCYYMM       PIC X(06).                                   
           05 WS-CURR-DD-TEMP      PIC X(02).                                   
                                                                                
       01  WS-CURR-DATE            PIC X(10).                                   
                                                                                
      *================================================================*        
       PROCEDURE DIVISION.                                                      
      *================================================================*        
                                                                                
      *-----------*                                                             
       0000-CONTROL.                                                            
      *-----------*                                                             
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F800    THRU 2000-EXIT.                            
           PERFORM 2500-READ-F820    THRU 2500-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL F800-NUM-REGN = HIGH-VALUES AND                        
                         F820-NUM-REGN = HIGH-VALUES.                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       1000-OPEN-FILES.                                                         
      *--------------*                                                          
           OPEN INPUT  SY02F001                                                 
                       BP13F820                                                 
                       BP13F800                                                 
                OUTPUT BP13F716.                                                
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT      TO WS-SYS-DATE.                           
                                                                                
           STRING WS-DD '/'                                                     
                  WS-MM '/'                                                     
                  WS-CURR-CC                                                    
                  WS-YY DELIMITED BY SIZE INTO WS-CURR-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2000-READ-F800.                                                          
      *-----------------*                                                       
           READ BP13F800 AT END                                                 
                MOVE HIGH-VALUES TO F800-NUM-REGN                               
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO CNT-F800-READ.                                              
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2500-READ-F820.                                                          
      *-----------------*                                                       
           READ BP13F820 AT END                                                 
                MOVE HIGH-VALUES TO F820-NUM-REGN                               
                GO TO 2500-EXIT.                                                
                                                                                
           ADD 1 TO CNT-F820-READ.                                              
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE F820-NUM-REGN           TO WS-F820-NUM-REGN-PRV.                
           IF F800-NUM-REGN = F820-NUM-REGN                                     
              PERFORM 4000-MOVE-DATA     THRU 4000-EXIT UNTIL                   
                         F820-NUM-REGN NOT  = WS-F820-NUM-REGN-PRV OR           
                         F820-NUM-REGN = HIGH-VALUES                            
              PERFORM 2000-READ-F800        THRU 2000-EXIT                      
              ADD 1                           TO CNT-REC-EQUAL                  
           ELSE                                                                 
              IF F800-NUM-REGN < F820-NUM-REGN                                  
                 PERFORM 2000-READ-F800     THRU 2000-EXIT                      
                 ADD 1                        TO CNT-F800-NOTFND                
              ELSE                                                              
                 PERFORM 2500-READ-F820     THRU 2500-EXIT                      
                 ADD 1                        TO CNT-F820-NOTFND                
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4000-MOVE-DATA.                                                          
      *-----------------*                                                       
           IF F800-DTE-REGN (1:6) = WS-CURR-CCYYMM                              
               MOVE SPACES                       TO  BP13F716-REC               
                                                                                
               MOVE F820-NUM-REGN                TO F716-NUM-REGN               
               MOVE F820-NUM-NRIC                TO F716-NUM-NRIC               
                                                                                
               PERFORM 5000-WRITE-F716         THRU 5000-EXIT                   
           END-IF.                                                              
                                                                                
           PERFORM 2500-READ-F820          THRU 2500-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5000-WRITE-F716.                                                         
      *-----------------*                                                       
           WRITE BP13F716-REC.                                                  
           ADD 1                           TO CNT-F716-WRITE.                   
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-ROUTINE.                                                      
      *-----------------*                                                       
                                                                                
           DISPLAY '         PROGRAM-ID :  BP13C518'.                           
           DISPLAY '         ~~~~~~~~~~             '.                          
           DISPLAY '       CURRENT-DATE : ' WS-CURR-DATE.                       
           DISPLAY ' '.                                                         
           DISPLAY '1. NO OF RECS READ FROM F800  : ' CNT-F800-READ.            
           DISPLAY '2. NO OF RECS READ FROM F820  : ' CNT-F820-READ.            
           DISPLAY '3. NO OF RECS WRITTEN (F716)  : ' CNT-F716-WRITE.           
           DISPLAY '4. NO OF RECS NOTFND  (F800)  : ' CNT-F800-NOTFND.          
           DISPLAY '5. NO OF RECS NOTFND  (F820)  : ' CNT-F820-NOTFND.          
           DISPLAY '6. NO OF RECS EQUAL           : ' CNT-REC-EQUAL.            
           DISPLAY '  '                                                         
                                                                                
           CLOSE SY02F001                                                       
                 BP13F800                                                       
                 BP13F820                                                       
                 BP13F716.                                                      
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------- END OF PROGRAM  ---------------------*              
                                                                                
                                                                                
