 /******************************************************************/           
 /*               SYSTEM  OF  COMMITMENT  (BP13)                   */           
 /*================================================================*/           
 /* PROGRAM ID   : BP13S018                                        */           
 /* AUTHOR       : SUDESH KUMAR                                    */           
 /* DATE WRITTEN : 17/05/2003                                      */           
 /* OBJECTIVE    : PRINT REPORT ON RECEIPTS TO MACCOUNT            */           
 /*                0000000404151 CPF MONIES                        */           
 /* INPUT FILES  : BP13F121                                        */           
 /* OUTPUT FILES : BP13L018                                        */           
 /*----------------------------------------------------------------*/           
 /* CHG REF  BY   DATE     DESC                                    */           
 /* -------- ---  -------- ----                                    */           
 /* BP132347 SK   20030517 NEW PROGRAM                             */           
 /*----------------------------------------------------------------*/           
   %GLOBAL WS_FND_K820;                                                         
   %GLOBAL WS_HEAD;                                                             
   OPTIONS NODATE NONUMBER;                                                     
                                                                                
   DATA BP13F121;                                                               
        INFILE BP13F121 END=EOF;                                                
        INPUT  @001  TRN_TYPE $CHAR2.                                           
               @005  NUM_REGN $CHAR8.                                           
               @013  DTE_POST $CHAR8.                                           
               @025  NUM_NRIC $CHAR9.                                           
               @081  AMT_RECP ZD9.2                                             
               @099  PAY_TYPE $CHAR3.                                           
               @102  NUM_SCH  $CHAR11.;                                         
                                                                                
    DATA _NULLS_;                                                               
    SET BP13F121 END=EOF;                                                       
        BY TRN_TYPE NUM_REGN;                                                   
                                                                                
    TOT = 0;                                                                    
    IF TRN_TYPE = '40' THEN                                                     
       DO;                                                                      
         WS_HEAD1 = 'P13L018A    HDBCAT 3';                                     
         WS_HEAD  = 'RECEIPTS FOR ACCOUNT 0000000404151 - CPF MONIES';          
       END;                                                                     
    ELSE                                                                        
       DO;                                                                      
         WS_HEAD1 = 'P13L018B    HDBCAT 3';                                     
         WS_HEAD  = 'RECEIPTS FOR ACCOUNT 0000000544151 - CPF MONIES';          
       END;                                                                     
                                                                                
    LENGTH NAME   $25;                                                          
    LENGTH K820_KEY $17;                                                        
    K820_KEY = NUM_REGN || NUM_NRIC;                                            
                                                                                
    WS_FND_K820 = 'N';                                                          
    INFILE BP13K820 VSAM KEY = K820_KEY;                                        
    INPUT @;                                                                    
    RC = _IORC_;                                                                
                                                                                
    IF RC = 0 THEN                                                              
        DO;                                                                     
          WS_FND_K820 = 'Y';                                                    
          INPUT @028 NME_OCCP $CHAR25. ;                                        
        END;                                                                    
    ELSE                                                                        
       _ERROR_ = 0;                                                             
                                                                                
   IF WS_FND_K820 ^= 'Y' THEN                                                   
      NAME = SPACES;                                                            
   ELSE                                                                         
      NAME = NME_OCCP;                                                          
                                                                                
   OPTIONS PAGESIZE=55 LS=132 NODATE;                                           
   FILE BP13L018 HEADER=RPTHDR LINESLEFT=LL NOTITLES;                           
                                                                                
   RETAIN DTE_TOT 0;                                                            
   RETAIN TRN_TOT 0;                                                            
   RETAIN GTOT_AMT 0;                                                           
                                                                                
   IF FIRST.TRN_TYPE THEN                                                       
      DO;                                                                       
         SLNO=0;                                                                
         PAGE=0;                                                                
         TRN_TOT=0;                                                             
         PUT _PAGE_;                                                            
   END;                                                                         
                                                                                
   IF FIRST.NUM_REGN THEN                                                       
      DO;                                                                       
         DTE_TOT=0;                                                             
   END;                                                                         
                                                                                
   LENGTH SCHAC  $14;                                                           
   LENGTH PTYPE  $10;                                                           
                                                                                
   IF PAY_TYPE = 'SAP' THEN                                                     
      PTYPE = 'SOC';                                                            
   ELSE                                                                         
   IF PAY_TYPE = 'LSM' THEN                                                     
      PTYPE = 'LUMP SUM';                                                       
   ELSE                                                                         
   IF PAY_TYPE = 'LSP' THEN                                                     
      PTYPE = 'LSE STAMP';                                                      
   ELSE                                                                         
   IF PAY_TYPE = 'SCN' THEN                                                     
      PTYPE = 'SALE CONVY';                                                     
   ELSE                                                                         
   IF PAY_TYPE = 'MCN' THEN                                                     
      PTYPE = 'MORT CONVY';                                                     
   ELSE                                                                         
   IF PAY_TYPE = 'SRF' THEN                                                     
      PTYPE = 'SURVEY FEE';                                                     
   ELSE                                                                         
   IF PAY_TYPE = 'LES' THEN                                                     
      PTYPE = 'LSE ESCR';                                                       
   ELSE                                                                         
   IF PAY_TYPE = 'LRG' THEN                                                     
      PTYPE = 'LSE REGN';                                                       
   ELSE                                                                         
   IF PAY_TYPE = 'CRG' THEN                                                     
      PTYPE = 'CAVT REGN';                                                      
   ELSE                                                                         
   IF PAY_TYPE = 'SLP' THEN                                                     
      PTYPE = 'SOLICITOR';                                                      
   IF PAY_TYPE = 'SMS' THEN                                                     
      PTYPE = 'SMS';                                                            
                                                                                
   SCHAC = SUBSTR(NUM_SCH,1,4) ||'-'||                                          
           SUBSTR(NUM_SCH,5,4) ||'-'||                                          
           SUBSTR(NUM_SCH,9,1)||'-'||                                           
           SUBSTR(NUM_SCH,10,2);                                                
                                                                                
   DTEPOST = SUBSTR(DTE_POST,7,2)||'/'||                                        
             SUBSTR(DTE_POST,5,2)||'/'||                                        
             SUBSTR(DTE_POST,1,4);                                              
                                                                                
     SLNO + 1;                                                                  
     PUT @004 SLNO                                                              
         @010 DTEPOST                                                           
         @025 NUM_REGN                                                          
         @035 SCHAC                                                             
         @054 NAME                                                              
         @084 PTYPE                                                             
         @104 AMT_RECP DOLLAR12.2;                                              
                                                                                
     DTE_TOT  = DTE_TOT  + AMT_RECP;                                            
     TRN_TOT  = TRN_TOT  + AMT_RECP;                                            
     GTOT_AMT = GTOT_AMT + AMT_RECP;                                            
                                                                                
     IF LAST.NUM_REGN THEN                                                      
        DO;                                                                     
          PUT @092 'TOTAL:'                                                     
              @101 DTE_TOT DOLLAR15.2/;                                         
        END;                                                                    
                                                                                
     IF LAST.TRN_TYPE THEN                                                      
        DO;                                                                     
          PUT @086 'TYPE TOTAL:'                                                
              @101 TRN_TOT DOLLAR15.2/;                                         
        END;                                                                    
                                                                                
     IF EOF THEN                                                                
        DO;                                                                     
          PUT @086 'GRAND TOTAL:'                                               
              @101 GTOT_AMT DOLLAR15.2/;                                        
        END;                                                                    
 RETURN;                                                                        
                                                                                
 RPTHDR:                                                                        
   PAGE+1;                                                                      
   CUR_DATE=TODAY();                                                            
   PUT @001 WS_HEAD1                                                            
       @039 'S Y S T E M   O F   C O M M I T M E N T'                           
       @100 'DATE : ' CUR_DATE DDMMYY10.                                        
       @118 'PAGE : ' PAGE;                                                     
   PUT @039 WS_HEAD/;                                                           
   PUT @003  'S/NO'                                                             
       @010  'DATE'                                                             
       @025  'REGN NO'                                                          
       @035  'SCHEME A/C'                                                       
       @054  'NAME'                                                             
       @084  'PYMT TYPE'                                                        
       @107  'AMOUNT'                                                           
       ;                                                                        
   PUT @1 132*'-';                                                              
RETURN;                                                                         
