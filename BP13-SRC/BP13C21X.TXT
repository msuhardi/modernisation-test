       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C21X.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  20/08/2010.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO UPDATE BP13KH40                             *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * ========  ====  ========= ================================ *            
      * BP134006  ESA1  20082010  NEW PROGRAM                      *            
      * BP134125  ESA1  20110520  TO CATER FOR NEW BP13KH40 FIELDS *            
      * BP134331  ESA1  20110818  REPLACE BP13K816 WITH BP13K813   *            
      * BP134135  ESA1  26082011  CORRECT READING OF BP13K813      *            
      * BP134375  ESA1  03102011  TO ADD CHECKING FOR FIRST DAY    *            
      *                           ACCEPTANCE                       *            
      * BP134411  ESA1  18012012  CATER FOR 'T3' ZONE              *            
      * BP134601  ESA1  13072012  CATER FOR 3-ROOM PREMIUM         *            
      * BP134681  ESA1  02102012  CATER FOR 3-BYTES ZONE           *            
      * BP135190  SMR2  04122013  EXPAND BP13K813 TO 1000          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS BP13K813-STATUS.                           
                                                                                
           SELECT BP13KH40 ASSIGN TO BP13KH40                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KH40-KEY-FLD                               
                  FILE STATUS     IS BP13KH40-STATUS.                           
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13KH40                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13KH40.                                                           
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-DATE              PIC X(08) VALUE SPACES.                       
          05 WS-CURRENT-TIME      PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F800-READ         PIC 9(05) VALUE ZEROES.                       
          05 WS-F800-SAME         PIC 9(05) VALUE ZEROES.                       
          05 WS-F800-TOT          PIC 9(05) VALUE ZEROES.                       
          05 WS-KH40-ERR-CNT      PIC 9(05) VALUE ZEROES.                       
          05 WS-KH40-FND-SW       PIC X     VALUE SPACES.                       
          05 WS-KH40-READ-ERR     PIC 9(05) VALUE ZEROES.                       
          05 WS-KH40-READ-CNT     PIC 9(05) VALUE ZEROES.                       
          05 WS-KH40-REWRITE-CNT  PIC 9(05) VALUE ZEROES.                       
          05 WS-KH40-REWRITE-ERR  PIC 9(05) VALUE ZEROES.                       
          05 WS-K813-READ-ERR     PIC 9(05) VALUE ZEROES.                       
          05 WS-K813-READ-CNT     PIC 9(05) VALUE ZEROES.                       
          05 WS-FLAG              PIC X     VALUE SPACES.                       
          05 WS-PAIR-FLAG         PIC X     VALUE SPACES.                       
          05 WS-CTR               PIC 9(05) VALUE ZEROES.                       
          05 WS-CTR2              PIC 9(05) VALUE ZEROES.                       
          05 WS-CTR3              PIC 9(05) VALUE ZEROES.                       
          05 WS-NUM-ACCEPT        PIC 9(05) VALUE ZEROES.                       
          05 WS-NUM-ACCEPT-FT-FD  PIC 9(05) VALUE ZEROES.                       
          05 WS-NUM-ACCEPT-FT     PIC 9(05) VALUE ZEROES.                       
          05 WS-NUM-INVITED       PIC 9(05) VALUE ZEROES.                       
          05 WS-NUM-INV-FT-FD     PIC 9(05) VALUE ZEROES.                       
          05 WS-NUM-INV-FT        PIC 9(05) VALUE ZEROES.                       
          05 WS-SUPPLY            PIC 9(05)V99 VALUE ZEROES.                    
          05 WS-SUPPLY-NUM REDEFINES WS-SUPPLY.                                 
             10  WS-SUPPLY-INT    PIC 9(05).                                    
             10  WS-SUPPLY-DEC    PIC 9(02).                                    
          05 WS-PREV-SUPPLY       PIC 9(05) VALUE ZEROES.                       
          05 WS-SUPPLY2           PIC 9(05)V99 VALUE ZEROES.                    
          05 WS-SUPPLY2-NUM REDEFINES WS-SUPPLY2.                               
             10  WS-SUPPLY2-INT   PIC 9(05).                                    
             10  WS-SUPPLY2-DEC   PIC 9(02).                                    
          05 WS-TOTAL-INVITED     PIC 9(05) VALUE ZEROES.                       
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 BP13KH40-STATUS      PIC 9(02) VALUE ZEROES.                       
          05 BP13K813-STATUS      PIC 9(02) VALUE ZEROES.                       
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-F800-EOF                 PIC X(1) VALUE 'N'.                    
          05 WS-EOF-K813                 PIC X(1) VALUE 'N'.                    
          05 WS-ZONE-FND-FLAG            PIC X(1) VALUE 'N'.                    
                                                                                
       01 WS-F800-KEY.                                                          
          10 WS-F800-MAIN-KEY.                                                  
             15 WS-F800-NUM-ALLO-CAT                PIC X(03).                  
             15 WS-F800-DTE-BALLOT                  PIC X(06).                  
             15 WS-F800-NUM-NEW-TOWN                PIC X(03).                  
                                                                                
       01 WS-F800-KEY2.                                                         
          10 WS-F800-MAIN-KEY2.                                                 
             15 WS-F800-NUM-ALLO-CAT2               PIC X(03).                  
             15 WS-F800-DTE-BALLOT2                 PIC X(06).                  
             15 WS-F800-NUM-NEW-TOWN2               PIC X(20).                  
             15 WS-F800-NUM-FLAT-TYPE               PIC X(09).                  
                                                                                
       01 WS-PREV-KEY.                                                          
          10 WS-PREV-MAIN-KEY.                                                  
             15 WS-PREV-NUM-ALLO-CAT                PIC X(03).                  
             15 WS-PREV-DTE-BALLOT                  PIC X(06).                  
             15 WS-PREV-NUM-BTO-ZONE                PIC X(03).                  
                                                                                
       01  WS-C21X-TABLE-DETAILS.                                               
           05  WS-C21X-DETAILS OCCURS 80 TIMES.                                 
               10  WS-NUM-INVITED-TAB               PIC 9(05).                  
               10  WS-NUM-ACCEPT-TAB                PIC 9(04).                  
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL WS-F800-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K813                                                 
                I-O    BP13KH40.                                                
                                                                                
           IF BP13K813-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K813 : ' BP13K813-STATUS             
              MOVE BP13K813-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF BP13KH40-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KH40 : ' BP13KH40-STATUS             
              MOVE BP13KH40-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           ACCEPT WS-CURRENT-TIME   FROM TIME.                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800 AT END                                                 
             MOVE 'Y'         TO WS-F800-EOF                                    
             MOVE HIGH-VALUES TO WS-F800-KEY                                    
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1                         TO WS-F800-READ                        
                                            WS-F800-SAME.                       
                                                                                
           MOVE SPACES                   TO WS-F800-KEY.                        
           MOVE F800-NUM-ALLO-CAT        TO WS-F800-NUM-ALLO-CAT                
                                            WS-F800-NUM-ALLO-CAT2.              
           MOVE F800-DTE-BALLOT          TO WS-F800-DTE-BALLOT                  
                                            WS-F800-DTE-BALLOT2.                
           MOVE F800-NUM-BTO-ZONE        TO WS-F800-NUM-NEW-TOWN.               
           PERFORM 2100-DECODE-NEW-TOWN  THRU 2100-EXIT.                        
           PERFORM 2200-DECODE-FLAT-TYPE THRU 2200-EXIT.                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-DECODE-NEW-TOWN.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                       TO WS-EOF-K813                        
                                             WS-ZONE-FND-FLAG.                  
           MOVE SPACES                    TO K813-KEY-FLD.                      
           MOVE F800-NUM-BTO-ZONE         TO K813-NUM-ZONE.                     
                                                                                
           START BP13K813 KEY >= K813-NUM-ZONE.                                 
                                                                                
           IF BP13K813-STATUS = 00 OR 02                                        
              PERFORM 2150-READNEXT-BP13K813 THRU 2150-EXIT                     
              UNTIL WS-EOF-K813 = 'Y' OR WS-ZONE-FND-FLAG = 'Y'                 
           ELSE                                                                 
              IF BP13K813-STATUS = 10 OR 23                                     
                 MOVE SPACES                 TO WS-F800-NUM-NEW-TOWN            
                 DISPLAY 'ZONE NOT FOUND IN BP13K813:' K813-KEY-FLD             
                 ADD   1     TO WS-K813-READ-ERR                                
                 GO          TO 2100-EXIT                                       
              ELSE                                                              
                DISPLAY 'START ERROR BP13K813 : ' BP13K813-STATUS               
                        ' ZONE : ' K813-NUM-ZONE                                
                PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       2150-READNEXT-BP13K813.                                                  
      *-----------------------------------------------------------*             
                                                                                
           READ BP13K813 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-EOF-K813.                                 
                                                                                
           IF BP13K813-STATUS = 0                                               
              IF K813-NUM-ZONE   = F800-NUM-BTO-ZONE                            
                 MOVE K813-NME-SITE          TO WS-F800-NUM-NEW-TOWN2           
                 ADD   1  TO WS-K813-READ-CNT                                   
                 MOVE 'Y'        TO  WS-ZONE-FND-FLAG                           
              END-IF                                                            
           ELSE                                                                 
              IF BP13K813-STATUS = 10 OR 23                                     
                 MOVE SPACES        TO WS-F800-NUM-NEW-TOWN                     
                 DISPLAY 'ZONE NOT FOUND IN BP13K813:' K813-KEY-FLD             
                 ADD   1            TO WS-K813-READ-ERR                         
                 MOVE 'Y'           TO  WS-EOF-K813                             
              ELSE                                                              
                 IF BP13K813-STATUS NOT = 0                                     
                    DISPLAY  'READ ERROR IN BP13K813 ' K813-KEY-FLD             
                    ADD   1   TO WS-K813-READ-ERR                               
                    GO TO 9000-CLOSE-ROUTINE                                    
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-DECODE-FLAT-TYPE.                                                   
      *-------------------------------------------------------------            
      *****************************************                                 
      *   HARDCODE C3 AND C4 FOR PAIR UNITS                                     
      *****************************************                                 
           EVALUATE K813-NUM-FLAT-TYPE(1:1)                                     
              WHEN '1'                                                          
                 IF K813-NUM-FLAT-TYPE(2:1) = 'A'                               
                    MOVE 'SA       ' TO WS-F800-NUM-FLAT-TYPE                   
                 ELSE                                                           
                    MOVE '1-ROOM   ' TO WS-F800-NUM-FLAT-TYPE                   
                 END-IF                                                         
              WHEN '2'                                                          
                 MOVE '2-ROOM   '    TO WS-F800-NUM-FLAT-TYPE                   
              WHEN '3'                                                          
                 IF K813-NUM-FT-PREMIUM = 'P'                                   
                    MOVE '3P-ROOM   '   TO WS-F800-NUM-FLAT-TYPE                
                 ELSE                                                           
                    MOVE '3-ROOM   '    TO WS-F800-NUM-FLAT-TYPE                
                 END-IF                                                         
              WHEN '4'                                                          
                 IF K813-NUM-ZONE = 'C3'                                        
                    IF F800-NUM-FLAT-TYPE(1:1) = '4'                            
                       MOVE '4-ROOM-SA' TO WS-F800-NUM-FLAT-TYPE                
                    ELSE                                                        
                       MOVE '4-ROOM-SP' TO WS-F800-NUM-FLAT-TYPE                
                    END-IF                                                      
                 ELSE                                                           
                    MOVE '4-ROOM   '    TO WS-F800-NUM-FLAT-TYPE                
                 END-IF                                                         
              WHEN '5'                                                          
                 IF K813-NUM-ZONE = 'C4'                                        
                    IF F800-NUM-FLAT-TYPE(1:1) = '5'                            
                       MOVE '5-ROOM-SA' TO WS-F800-NUM-FLAT-TYPE                
                    ELSE                                                        
                       MOVE '5-ROOM-SP' TO WS-F800-NUM-FLAT-TYPE                
                    END-IF                                                      
                 ELSE                                                           
                    MOVE '5-ROOM   '    TO WS-F800-NUM-FLAT-TYPE                
                 END-IF                                                         
              WHEN '6'                                                          
              WHEN 'E'                                                          
                 MOVE 'EXECUTIVE'    TO WS-F800-NUM-FLAT-TYPE                   
              WHEN OTHER                                                        
                 MOVE SPACES         TO WS-F800-NUM-FLAT-TYPE                   
                 DISPLAY 'ERROR - FLAT TYPE NOT SUPPORTED: '                    
                                        F800-NUM-FLAT-TYPE                      
                 GO TO 2200-EXIT                                                
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
            MOVE ZEROES                      TO WS-CTR3                         
                                                WS-PREV-SUPPLY.                 
                                                                                
            MOVE WS-F800-KEY                 TO WS-PREV-KEY.                    
            MOVE ZEROES                      TO WS-C21X-TABLE-DETAILS.          
                                                                                
            MOVE WS-F800-KEY2             TO   KH40-KEY-FLD.                    
            PERFORM 6000-READ-BP13KH40    THRU 6000-EXIT.                       
                                                                                
            PERFORM 4000-GET-QUEUE-START-END THRU 4000-EXIT                     
              UNTIL WS-F800-EOF = 'Y' OR                                        
                    WS-F800-KEY NOT = WS-PREV-KEY.                              
                                                                                
            IF WS-F800-EOF = 'Y'                                                
               MOVE WS-F800-SAME    TO  WS-F800-TOT                             
            ELSE                                                                
               COMPUTE WS-F800-TOT = WS-F800-SAME - 1                           
            END-IF.                                                             
                                                                                
            MOVE WS-F800-TOT       TO   WS-TOTAL-INVITED.                       
                                                                                
            IF WS-SUPPLY2-INT > WS-F800-TOT                                     
               MOVE WS-F800-TOT    TO   WS-NUM-INVITED-TAB(WS-CTR3)             
            END-IF.                                                             
                                                                                
            MOVE 1                   TO WS-F800-SAME.                           
                                                                                
            IF WS-NUM-INVITED-TAB(1) NOT = ZEROES                               
               IF BP13KH40-STATUS = 0                                           
                  PERFORM 6350-REWRITE-BP13KH40    THRU 6350-EXIT               
               END-IF                                                           
            END-IF.                                                             
                                                                                
            MOVE ZEROES            TO WS-NUM-ACCEPT-FT-FD                       
                                      WS-NUM-ACCEPT-FT  WS-NUM-ACCEPT           
                                      WS-NUM-INV-FT-FD                          
                                      WS-NUM-INV-FT WS-NUM-INVITED.             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-GET-QUEUE-START-END.                                                
      *-------------------------------------------------------------            
                                                                                
            MOVE 0              TO    WS-CTR2.                                  
                                                                                
            PERFORM 5000-PROCESS-QUEUE     THRU 5000-EXIT                       
              UNTIL WS-CTR2 = WS-SUPPLY-INT OR                                  
              WS-F800-KEY NOT = WS-PREV-KEY.                                    
                                                                                
            ADD 1               TO    WS-CTR3.                                  
                                                                                
            MOVE WS-NUM-ACCEPT  TO    WS-NUM-ACCEPT-TAB(WS-CTR3).               
            COMPUTE WS-SUPPLY2 = KH40-NUM-FLAT-SUPPLY *                         
                               (KH40-NUM-SUPPLY-PCT(WS-CTR3) / 100).            
                                                                                
            IF WS-SUPPLY2-DEC >= 50                                             
               ADD 1 TO WS-SUPPLY2-INT                                          
            END-IF.                                                             
                                                                                
            MOVE WS-SUPPLY2-INT    TO   WS-NUM-INVITED-TAB(WS-CTR3).            
                                                                                
            MOVE ZEROES            TO   WS-NUM-ACCEPT.                          
                                                                                
            IF WS-CTR3 < 80                                                     
               COMPUTE WS-PREV-SUPPLY = WS-PREV-SUPPLY + WS-SUPPLY-INT          
               COMPUTE WS-SUPPLY = KH40-NUM-FLAT-SUPPLY *                       
                              (KH40-NUM-SUPPLY-PCT(WS-CTR3 + 1) / 100) -        
                               WS-PREV-SUPPLY                                   
                                                                                
               IF WS-SUPPLY-DEC >= 50                                           
                  ADD 1 TO WS-SUPPLY-INT                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-PROCESS-QUEUE.                                                      
      *-------------------------------------------------------------            
            ADD 1 TO           WS-CTR2.                                         
                                                                                
            IF F800-NUM-NT-FT-QUEUE NOT = SPACES AND LOW-VALUES AND ZEROES      
               IF F800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                  
                  IF F800-CDE-BALLOT-HOUSEHOLD = 'T' OR 'H'                     
                     IF F800-DTE-ACCEPTANCE = KH40-DTE-START-BKAPPMT            
                        ADD 1             TO WS-NUM-ACCEPT-FT-FD                
                     ELSE                                                       
                        IF F800-DTE-BK-APPT = KH40-DTE-START-BKAPPMT            
                           ADD 1          TO WS-NUM-ACCEPT-FT-FD                
                        END-IF                                                  
                     END-IF                                                     
                     ADD 1                TO WS-NUM-ACCEPT-FT                   
                  END-IF                                                        
                  ADD 1                   TO WS-NUM-ACCEPT                      
               ELSE                                                             
                  IF F800-NUM-ALLOC-TAG = 'RI' OR 'RG'                          
                     IF F800-CDE-BALLOT-HOUSEHOLD = 'T' OR 'H'                  
                        IF F800-DTE-ACCEPTANCE = KH40-DTE-START-BKAPPMT         
                           ADD 1          TO WS-NUM-ACCEPT-FT-FD                
                        ELSE                                                    
                           IF F800-DTE-BK-APPT = KH40-DTE-START-BKAPPMT         
                              ADD 1       TO WS-NUM-ACCEPT-FT-FD                
                           END-IF                                               
                        END-IF                                                  
                        ADD 1             TO WS-NUM-ACCEPT-FT                   
                     END-IF                                                     
                     ADD 1                TO WS-NUM-ACCEPT                      
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF F800-CDE-BALLOT-HOUSEHOLD = 'T' OR 'H'                           
               IF F800-DTE-BK-APPT = KH40-DTE-START-BKAPPMT                     
                  ADD 1             TO WS-NUM-INV-FT-FD                         
               END-IF                                                           
               ADD 1                TO WS-NUM-INV-FT                            
            END-IF.                                                             
                                                                                
                                                                                
           PERFORM 2000-READ-BP13F800     THRU 2000-EXIT.                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-READ-BP13KH40.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KH40 KEY IS KH40-KEY-FLD.                                   
                                                                                
           IF BP13KH40-STATUS = 0                                               
              ADD 1    TO    WS-KH40-READ-CNT                                   
              COMPUTE WS-SUPPLY = KH40-NUM-FLAT-SUPPLY * .10                    
              IF WS-SUPPLY-DEC >= 50                                            
                 ADD 1 TO WS-SUPPLY-INT                                         
              END-IF                                                            
           ELSE                                                                 
           IF BP13KH40-STATUS = 23                                              
              DISPLAY 'RECORD NOT FOUND IN BP13KH40:' KH40-KEY-FLD              
              ADD   1     TO WS-KH40-READ-ERR                                   
              GO          TO 6000-EXIT                                          
           ELSE                                                                 
           IF BP13KH40-STATUS NOT = 0                                           
              DISPLAY  'READ ERROR IN BP13KH40 ' KH40-KEY-FLD                   
              ADD   1   TO WS-KH40-READ-ERR                                     
              GO        TO 9000-CLOSE-ROUTINE.                                  
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       6350-REWRITE-BP13KH40.                                                   
      *-----------------------------------------------------------              
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1                                   
              UNTIL WS-CTR > 80                                                 
              IF KH40-NUM-SUPPLY-PCT(WS-CTR) > ZEROES                           
                 MOVE WS-NUM-ACCEPT-TAB(WS-CTR)                                 
                                        TO  KH40-NUM-ACCEPT(WS-CTR)             
                 MOVE WS-NUM-INVITED-TAB(WS-CTR)                                
                                        TO  KH40-NUM-INVITED(WS-CTR)            
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           MOVE WS-TOTAL-INVITED  TO   KH40-NUM-TOTAL-INVITED.                  
           MOVE WS-NUM-INV-FT-FD    TO KH40-NUM-TOT-INV-FT-FD.                  
           MOVE WS-NUM-INV-FT       TO KH40-NUM-TOT-INV-FT.                     
           MOVE WS-NUM-ACCEPT-FT    TO KH40-NUM-TOT-ACCEPT-FT.                  
           MOVE WS-NUM-ACCEPT-FT-FD TO KH40-NUM-TOT-ACCEPT-FT-FD                
           MOVE 'P13C21X'         TO   KH40-NUM-USERID.                         
           ACCEPT KH40-DTE-UPDATE FROM DATE YYYYMMDD.                           
                                                                                
           REWRITE BP13KH40-REC.                                                
                                                                                
           IF BP13KH40-STATUS = 0                                               
              ADD   1  TO WS-KH40-REWRITE-CNT                                   
           ELSE                                                                 
              DISPLAY 'REWRITE ERROR, STATUS : ' BP13KH40-STATUS                
              DISPLAY ' KEY     : ' KH40-KEY-FLD                                
              ADD   1   TO WS-KH40-REWRITE-ERR                                  
              GO        TO 9000-CLOSE-ROUTINE.                                  
                                                                                
       6350-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13C21X CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'RECS READ    FROM BP13F800  : ' WS-F800-READ.               
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ    FROM BP13K813  : ' WS-K813-READ-CNT.           
           DISPLAY 'RECS NOTFND    IN BP13K813  : ' WS-K813-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ    FROM BP13KH40  : ' WS-KH40-READ-CNT.           
           DISPLAY 'RECS NOTFND    IN BP13KH40  : ' WS-KH40-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS REWRITE   IN BP13KH40  : ' WS-KH40-REWRITE-CNT.        
                                                                                
           CLOSE BP13F800                                                       
                 BP13K813                                                       
                 BP13KH40.                                                      
                                                                                
           IF BP13K813-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR CLOSING - BP13K813 : ' BP13K813-STATUS            
              MOVE BP13K813-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF BP13KH40-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR CLOSING - BP13KH40 : ' BP13KH40-STATUS            
              MOVE BP13KH40-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
