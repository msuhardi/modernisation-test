       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C784.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  08/09/2004.                                               
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *   OBJECTIVE  :  TO UPDATE TOT-MCP AND TOT-NOMCP IN BP13K012.*           
      *                                                             *           
      *   INPUT      :  BP13F012                                    *           
      *   I-O        :  BP13K012                                    *           
      *                                                             *           
      *   AMENDMENTS:                                               *           
      *    REF      DATE     BY          DESCRIPTIONS               *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP132596 17/09/2004 SD10 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F012 ASSIGN        TO BP13F012.                           
                                                                                
           SELECT BP13K012 ASSIGN        TO BP13K012                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K012-KEY-FLD                        
                           FILE   STATUS IS WS-K012-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F012                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F012.                                                           
                                                                                
       FD   BP13K012                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K012.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F012-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-K012-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-K012-UPDATE          PIC 9(8)    VALUE ZERO.                      
       01  WS-K012-NOTFND          PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-EOF-F012             PIC X       VALUE SPACE.                     
       01  WS-K012-STATUS          PIC 9(2)    VALUE ZEROES.                    
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-TOT          PIC 9(5)    VALUE ZEROES.                    
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC   PIC 9(2).                                    
                   15  WS-SYS-YY   PIC 9(2).                                    
               10  WS-SYS-MM       PIC 9(2).                                    
               10  WS-SYS-DD       PIC 9(2).                                    
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-MM    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-YY    PIC X(4)    VALUE SPACES.                    
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
             UNTIL WS-EOF-F012 = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F012                                                 
                I-O    BP13K012.                                                
           MOVE 'N' TO WS-EOF-F012.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
           IF WS-K012-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K012 OPEN ERROR, STATUS IS ' WS-K012-STATUS          
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           INITIALIZE    WS-F012-READ                                           
                         WS-K012-READ                                           
                         WS-K012-UPDATE                                         
                         WS-K012-NOTFND.                                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-PROCESS.                                                            
      *****************************************************************         
                                                                                
           PERFORM 3000-READ-F012     THRU 3000-EXIT.                           
           IF WS-EOF-F012 NOT = 'Y'                                             
              PERFORM 4000-READ-K012  THRU 4000-EXIT                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-F012.                                                          
      *****************************************************************         
                                                                                
           READ BP13F012 AT END MOVE 'Y' TO WS-EOF-F012.                        
                                                                                
           IF WS-EOF-F012 NOT = 'Y'                                             
              ADD 1 TO WS-F012-READ                                             
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-READ-K012.                                                          
      *****************************************************************         
                                                                                
           MOVE F012-KEY-FLD           TO K012-KEY-FLD.                         
                                                                                
           READ BP13K012.                                                       
           IF WS-K012-STATUS = 00                                               
              ADD 1 TO WS-K012-READ                                             
              PERFORM 4100-COMPUTE-TOTALS THRU 4100-EXIT                        
              PERFORM 5000-UPDATE-K012    THRU 5000-EXIT                        
           ELSE                                                                 
              IF WS-K012-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K012. '                       
                         'K012-CDE-NT-ZONE : ' K012-CDE-NT-ZONE '. '            
                         'K012-CDE-FLAT-TY : ' K012-CDE-FLAT-TY                 
                 ADD 1 TO WS-K012-NOTFND                                        
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K012. STATUS IS '                   
                          WS-K012-STATUS                                        
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4100-COMPUTE-TOTALS.                                                     
      *****************************************************************         
                                                                                
           IF F012-NUM-TOT-MCPH NOT NUMERIC                                     
              MOVE 0         TO F012-NUM-TOT-MCPH                               
           END-IF.                                                              
                                                                                
           IF F012-NUM-TOT-MCPG NOT NUMERIC                                     
              MOVE 0         TO F012-NUM-TOT-MCPG                               
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-NUM-TOT-MCPH + F012-NUM-TOT-MCPG.          
           MOVE WS-NUM-TOT   TO K012-NUM-TOT-MCP.                               
                                                                                
           IF F012-NUM-MAX-MCPH1 NOT NUMERIC                                    
              MOVE 0         TO F012-NUM-MAX-MCPH1                              
           END-IF.                                                              
                                                                                
           IF F012-NUM-MAX-MCPG1 NOT NUMERIC                                    
              MOVE 0         TO F012-NUM-MAX-MCPG1                              
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-NUM-MAX-MCPH1 + F012-NUM-MAX-MCPG1.        
           MOVE WS-NUM-TOT   TO K012-NUM-MAX-MCP1.                              
                                                                                
           IF F012-NUM-MAX-MCPH2 NOT NUMERIC                                    
              MOVE 0         TO F012-NUM-MAX-MCPH2                              
           END-IF.                                                              
                                                                                
           IF F012-NUM-MAX-MCPG2 NOT NUMERIC                                    
              MOVE 0         TO F012-NUM-MAX-MCPG2                              
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-NUM-MAX-MCPH2 + F012-NUM-MAX-MCPG2.        
           MOVE WS-NUM-TOT   TO K012-NUM-MAX-MCP2.                              
                                                                                
           IF F012-NUM-MAX-MCPH3 NOT NUMERIC                                    
              MOVE 0         TO F012-NUM-MAX-MCPH3                              
           END-IF.                                                              
                                                                                
           IF F012-NUM-MAX-MCPG3 NOT NUMERIC                                    
              MOVE 0         TO F012-NUM-MAX-MCPG3                              
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-NUM-MAX-MCPH3 + F012-NUM-MAX-MCPG3.        
           MOVE WS-NUM-TOT   TO K012-NUM-MAX-MCP3.                              
                                                                                
           IF F012-REG-MCPH1 NOT NUMERIC                                        
              MOVE 0         TO F012-REG-MCPH1                                  
           END-IF.                                                              
                                                                                
           IF F012-REG-MCPG1 NOT NUMERIC                                        
              MOVE 0         TO F012-REG-MCPG1                                  
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-REG-MCPH1 + F012-REG-MCPG1.                
           MOVE WS-NUM-TOT   TO K012-REG-MCP1.                                  
                                                                                
           IF F012-REG-MCPH2 NOT NUMERIC                                        
              MOVE 0         TO F012-REG-MCPH2                                  
           END-IF.                                                              
                                                                                
           IF F012-REG-MCPG2 NOT NUMERIC                                        
              MOVE 0         TO F012-REG-MCPG2                                  
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-REG-MCPH2 + F012-REG-MCPG2.                
           MOVE WS-NUM-TOT   TO K012-REG-MCP2.                                  
                                                                                
           IF F012-REG-MCPH3 NOT NUMERIC                                        
              MOVE 0         TO F012-REG-MCPH3                                  
           END-IF.                                                              
                                                                                
           IF F012-REG-MCPG3 NOT NUMERIC                                        
              MOVE 0         TO F012-REG-MCPG3                                  
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-REG-MCPH3 + F012-REG-MCPG3.                
           MOVE WS-NUM-TOT   TO K012-REG-MCP3.                                  
                                                                                
           IF F012-NUM-TOT-NOMCH NOT NUMERIC                                    
              MOVE 0         TO F012-NUM-TOT-NOMCH                              
           END-IF.                                                              
                                                                                
           IF F012-NUM-TOT-NOMCG NOT NUMERIC                                    
              MOVE 0         TO F012-NUM-TOT-NOMCG                              
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-NUM-TOT-NOMCH + F012-NUM-TOT-NOMCG.        
           MOVE WS-NUM-TOT   TO K012-NUM-TOT-NOMCP.                             
                                                                                
           IF F012-NUM-MAX-NOMCH1 NOT NUMERIC                                   
              MOVE 0         TO F012-NUM-MAX-NOMCH1                             
           END-IF.                                                              
                                                                                
           IF F012-NUM-MAX-NOMCG1 NOT NUMERIC                                   
              MOVE 0         TO F012-NUM-MAX-NOMCG1                             
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-NUM-MAX-NOMCH1 +                           
                                F012-NUM-MAX-NOMCG1.                            
           MOVE WS-NUM-TOT   TO K012-NUM-MAX-NOMCP1.                            
                                                                                
           IF F012-NUM-MAX-NOMCH2 NOT NUMERIC                                   
              MOVE 0         TO F012-NUM-MAX-NOMCH2                             
           END-IF.                                                              
                                                                                
           IF F012-NUM-MAX-NOMCG2 NOT NUMERIC                                   
              MOVE 0         TO F012-NUM-MAX-NOMCG2                             
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-NUM-MAX-NOMCH2 +                           
                                F012-NUM-MAX-NOMCG2.                            
           MOVE WS-NUM-TOT   TO K012-NUM-MAX-NOMCP2.                            
                                                                                
           IF F012-NUM-MAX-NOMCH3 NOT NUMERIC                                   
              MOVE 0         TO F012-NUM-MAX-NOMCH3                             
           END-IF.                                                              
                                                                                
           IF F012-NUM-MAX-NOMCG3 NOT NUMERIC                                   
              MOVE 0         TO F012-NUM-MAX-NOMCG3                             
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-NUM-MAX-NOMCH3 +                           
                                F012-NUM-MAX-NOMCG3.                            
           MOVE WS-NUM-TOT   TO K012-NUM-MAX-NOMCP3.                            
                                                                                
           IF F012-REG-NOMCH1 NOT NUMERIC                                       
              MOVE 0         TO F012-REG-NOMCH1                                 
           END-IF.                                                              
                                                                                
           IF F012-REG-NOMCG1 NOT NUMERIC                                       
              MOVE 0         TO F012-REG-NOMCG1                                 
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-REG-NOMCH1 + F012-REG-NOMCG1.              
           MOVE WS-NUM-TOT   TO K012-REG-NOMCP1.                                
                                                                                
           IF F012-REG-NOMCH2 NOT NUMERIC                                       
              MOVE 0         TO F012-REG-NOMCH2                                 
           END-IF.                                                              
                                                                                
           IF F012-REG-NOMCG2 NOT NUMERIC                                       
              MOVE 0         TO F012-REG-NOMCG2                                 
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-REG-NOMCH2 + F012-REG-NOMCG2.              
           MOVE WS-NUM-TOT   TO K012-REG-NOMCP2.                                
                                                                                
           IF F012-REG-NOMCH3 NOT NUMERIC                                       
              MOVE 0         TO F012-REG-NOMCH3                                 
           END-IF.                                                              
                                                                                
           IF F012-REG-NOMCG3 NOT NUMERIC                                       
              MOVE 0         TO F012-REG-NOMCG3                                 
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT = F012-REG-NOMCH3 + F012-REG-NOMCG3.              
           MOVE WS-NUM-TOT   TO K012-REG-NOMCP3.                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-UPDATE-K012.                                                        
      *****************************************************************         
                                                                                
           REWRITE BP13K012-MCPSUNIT.                                           
                                                                                
           EVALUATE WS-K012-STATUS                                              
               WHEN ZEROES                                                      
                    ADD 1         TO WS-K012-UPDATE                             
               WHEN 23                                                          
                    DISPLAY 'RECORD NOT FOUND IN BP13K012. '                    
                            'K012-CDE-NT-ZONE : ' K012-CDE-NT-ZONE '. '         
                            'K012-CDE-FLAT-TY : ' K012-CDE-FLAT-TY '. '         
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K012. STATUS IS '                
                             WS-K012-STATUS                                     
                    PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-ROUTINE.                                                      
      *****************************************************************         
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C784 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY ' F012-READ    :  ' WS-F012-READ.                            
           DISPLAY ' K012-READ    :  ' WS-K012-READ.                            
           DISPLAY ' K012-NOTFND  :  ' WS-K012-NOTFND.                          
           DISPLAY ' K012-UPDATED :  ' WS-K012-UPDATE.                          
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
           CLOSE      BP13F012                                                  
                      BP13K012.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
