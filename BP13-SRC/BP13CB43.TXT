       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB43.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  05/07/2017.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO CALCULATE THE TOTAL NUMBER OF      *                
      *                  UNITS FOR THE DIFFERENT ALLOC SCH.    *                
      *                                                        *                
      * ====================================================== *                
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                  *                
      * --------  ----  --------  ---------------------------- *                
      * BP136866  SMR2  05072017  NEW PROGRAM                  *                
      * BP137215  SMR2  14022018  GIVE FLAT SUPPLY TO NH WHEN  *                
      *                           THERE IS MORE THAN ENOUGH    *                
      *                           SUPPLY FOR UG AND NOT ENOUGH *                
      *                           SUPPLY FOR NH                *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS SEQUENTIAL                                 
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT BP13K712 ASSIGN TO BP13K712                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K712-KEY-FLD                               
                  FILE STATUS     IS WS-K712-STATUS.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K010                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BP13K010.                                                           
                                                                                
       FD  BP13K712                                                             
           RECORD CONTAINS 350 CHARACTERS.                                      
       COPY BP13K712.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-K712-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K010-STATUS              PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-COUNTER.                                                          
           05 WS-K010-READ                PIC 9(5)  VALUE ZEROS.                
           05 WS-K712-UPDATE              PIC 9(5)  VALUE ZEROS.                
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-CC                       PIC 99.                               
           05 WS-YY                       PIC 99.                               
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
       01  WS-SYS-TIME                    PIC 9(8).                             
       01  WS-NUM-FLAT-UG                 PIC 9(5)  VALUE ZEROES.               
       01  WS-ADJ-NH                      PIC 9(5)  VALUE ZEROES.               
       01  WS-ADJ-UG                      PIC 9(5)  VALUE ZEROES.               
                                                                                
       01  WS-EOF-K010                    PIC 9     VALUE 0.                    
           88 EOF-K010                    VALUE 1.                              
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-K010      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-K010   THRU 300-EXIT                             
             UNTIL EOF-K010.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
           OPEN INPUT  BP13K010                                                 
                I-O    BP13K712.                                                
                                                                                
           IF WS-K712-STATUS NOT = 00 AND 97                                    
              MOVE WS-K712-STATUS            TO RETURN-CODE                     
              DISPLAY 'ERROR OPENING BP13K712'                                  
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              MOVE WS-K010-STATUS            TO RETURN-CODE                     
              DISPLAY 'ERROR OPENING BP13K010'                                  
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           ACCEPT WS-SYS-DATE              FROM DATE YYYYMMDD.                  
           ACCEPT WS-SYS-TIME              FROM TIME.                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-K010.                                                           
      ******************************************************************        
           READ BP13K010 AT END                                                 
                MOVE 1 TO WS-EOF-K010                                           
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1  TO WS-K010-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       260-READ-K712.                                                           
      ******************************************************************        
           READ  BP13K712                                                       
           IF WS-K712-STATUS = 00 OR 23                                         
              NEXT SENTENCE                                                     
           ELSE                                                                 
              DISPLAY 'ERRO READING BP13K712, STATUS = '                        
                      WS-K712-STATUS                                            
              GO TO 999-CLOSE-FILES.                                            
       260-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-K010.                                                        
      ******************************************************************        
                                                                                
           IF K010-KEY-FLD = SPACES OR LOW-VALUES                               
                 PERFORM 200-READ-K010           THRU 200-EXIT                  
                 GO TO 300-EXIT                                                 
           END-IF.                                                              
                                                                                
           IF K010-NUM-TOT-PA > 0                                               
              MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                         
              MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                         
              MOVE 'PA'             TO K712-CDE-COHORT                          
              MOVE 'PA'            TO K712-CDE-ALLOC-SCH                        
              PERFORM 260-READ-K712 THRU 260-EXIT                               
                                                                                
              IF K712-NUM-FLAT-UG-APA3 IS NOT NUMERIC                           
                 MOVE ZEROES TO K712-NUM-FLAT-UG-APA3                           
              END-IF                                                            
                                                                                
              IF K712-NUM-FLAT-UG-APA3 > ZEROES                                 
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE K010-NUM-FLAT-UPG-APA3 TO                                 
                                  K712-NUM-FLAT-UG-APA3                         
              END-IF                                                            
                                                                                
              PERFORM 310-UPDATE-BP13K712 THRU 310-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-K010           THRU 200-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       310-UPDATE-BP13K712.                                                     
      ******************************************************************        
                                                                                
           IF WS-K712-STATUS = 00                                               
              PERFORM 400-CAL-TOT-UNIT-AVAILABLE THRU 400-EXIT                  
              PERFORM 500-CAL-FLAT-ALLOCATION    THRU 500-EXIT                  
                                                                                
              IF K712-NUM-FLAT-U3 = SPACES OR LOW-VALUES                        
                 MOVE ZEROS      TO K712-NUM-FLAT-U3                            
              END-IF                                                            
              IF K712-NUM-REG-NH = SPACES OR LOW-VALUES                         
              MOVE ZEROS        TO K712-NUM-REG-NH                              
              END-IF                                                            
              IF K712-NUM-REG-PPS = SPACES OR LOW-VALUES                        
              MOVE ZEROS        TO K712-NUM-REG-PPS                             
              END-IF                                                            
              IF K712-NUM-REG-AST = SPACES OR LOW-VALUES                        
              MOVE ZEROS        TO K712-NUM-REG-AST                             
              END-IF                                                            
              IF K712-NUM-REG-FTS = SPACES OR LOW-VALUES                        
              MOVE ZEROS        TO K712-NUM-REG-FTS                             
              END-IF                                                            
              IF K712-NUM-REG-MCPS-FT = SPACES OR LOW-VALUES                    
              MOVE ZEROS        TO K712-NUM-REG-MCPS-FT                         
              END-IF                                                            
              IF K712-NUM-REG-MCPS-ST = SPACES OR LOW-VALUES                    
              MOVE ZEROS        TO K712-NUM-REG-MCPS-ST                         
              END-IF                                                            
              IF K712-NUM-REG-UG = SPACES OR LOW-VALUES                         
              MOVE ZEROS        TO K712-NUM-REG-UG                              
              END-IF                                                            
              IF K712-NUM-REG-U3 = SPACES OR LOW-VALUES                         
                 MOVE ZEROS     TO K712-NUM-REG-U3                              
              END-IF                                                            
                                                                                
              REWRITE BP13K712-REC                                              
              IF WS-K712-STATUS = 00                                            
                 ADD 1                       TO WS-K712-UPDATE                  
              ELSE                                                              
                 MOVE WS-K712-STATUS         TO RETURN-CODE                     
                 DISPLAY 'ERROR REWRITING IN BP13K712, '                        
                          K712-KEY-FLD                                          
                          GO TO 999-CLOSE-FILES                                 
              END-IF                                                            
           ELSE                                                                 
              IF WS-K712-STATUS = 23                                            
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE WS-K712-STATUS            TO RETURN-CODE                  
                 DISPLAY 'ERROR READING BP13K712, '                             
                          K712-KEY-FLD                                          
                          GO TO 999-CLOSE-FILES                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-CAL-TOT-UNIT-AVAILABLE.                                              
      ******************************************************************        
                                                                                
           EVALUATE K712-CDE-ALLOC-SCH                                          
             WHEN 'PA '                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-PA  -                   
                                           (K010-REG-PA1    +                   
                                            K010-REG-PA2    +                   
                                            K010-REG-PA3))                      
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-CAL-FLAT-ALLOCATION.                                                 
      ******************************************************************        
                                                                                
           INITIALIZE                       WS-NUM-FLAT-UG.                     
                                                                                
           COMPUTE WS-NUM-FLAT-UG = (K712-NUM-TOT-UNIT *                        
                                     K010-NUM-UPG / 100).                       
                                                                                
           IF WS-NUM-FLAT-UG > ZERO                                             
              MOVE WS-NUM-FLAT-UG TO K712-NUM-FLAT-UG                           
           ELSE                                                                 
              MOVE ZEROES         TO K712-NUM-FLAT-UG                           
           END-IF.                                                              
                                                                                
           COMPUTE K712-NUM-FLAT-NH = K712-NUM-TOT-UNIT -                       
                                      K712-NUM-FLAT-UG.                         
                                                                                
           MOVE ZEROES                   TO K712-NUM-FLAT-FTS                   
                                            K712-NUM-FLAT-PPS                   
                                            K712-NUM-FLAT-AST                   
                                            K712-NUM-FLAT-MCPS-FT               
                                            K712-NUM-FLAT-MCPS-ST.              
                                                                                
           MOVE ZEROES                   TO WS-ADJ-NH                           
                                            WS-ADJ-UG.                          
                                                                                
           IF K712-NUM-REQ-NH < K712-NUM-FLAT-NH                                
              COMPUTE WS-ADJ-NH = K712-NUM-FLAT-NH - K712-NUM-REQ-NH            
              MOVE K712-NUM-REQ-NH       TO K712-NUM-FLAT-NH                    
              COMPUTE K712-NUM-FLAT-UG = K712-NUM-FLAT-UG + WS-ADJ-NH           
           END-IF.                                                              
                                                                                
           IF K712-NUM-REQ-UG < K712-NUM-FLAT-UG                                
              COMPUTE WS-ADJ-UG = K712-NUM-FLAT-UG - K712-NUM-REQ-UG            
              MOVE K712-NUM-REQ-UG       TO K712-NUM-FLAT-UG                    
              IF K712-NUM-REQ-NH > K712-NUM-FLAT-NH                             
                 COMPUTE K712-NUM-FLAT-NH = K712-NUM-FLAT-NH +                  
                                            WS-ADJ-UG                           
              ELSE                                                              
                 COMPUTE K712-NUM-FLAT-FTS = K712-NUM-FLAT-FTS +                
                                             WS-ADJ-UG                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE K010-NUM-NHH             TO K712-NUM-NHH.                       
           MOVE K010-NUM-UPG             TO K712-NUM-UPG.                       
           MOVE WS-SYS-DATE              TO K712-DTE-UPDATE.                    
           MOVE WS-SYS-TIME              TO K712-TME-SYSTEM.                    
           MOVE 'P13CB43'                TO K712-USERID.                        
                                                                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-FILES.                                                         
      ******************************************************************        
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CB43 ================*'.              
           DISPLAY '   NO. OF K010 RECORDS READ      = ' WS-K010-READ.          
           DISPLAY '   NO. OF K712 RECORDS REWRITTEN = ' WS-K712-UPDATE.        
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13K010                                                       
                 BP13K712.                                                      
                                                                                
           IF WS-K712-STATUS NOT = 00 AND 97                                    
              MOVE WS-K712-STATUS TO RETURN-CODE                                
              DISPLAY 'ERROR CLOSING BP13K712, ' WS-K712-STATUS.                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              MOVE WS-K010-STATUS TO RETURN-CODE                                
              DISPLAY 'ERROR CLOSING BP13K010, ' WS-K010-STATUS.                
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
