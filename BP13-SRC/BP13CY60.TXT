       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY60.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  13/07/2009.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : MATCH BP13F800 AND BM06F950                        *        
      *                                                                *        
      * INPUT  FILES : SY02F001                                        *        
      *                BM06F950                                        *        
      *                BP13F800                                        *        
      * OUTPUT FILES : P13F800A                                        *        
      *                                                                *        
      * CHG-NO   BY   ON       DESCRIPTION                             *        
      * -------------------------------------------------------------- *        
      * BP133653 JB8  13/07/09 NEW PROGRAM                             *        
      * BP133981 JB8  18/08/09 GET ACCEPTANCE DATE                     *        
      * BP134495 IMC1 29/05/12 ADD NEW DTE-UPDATE AND NUM-TRANS FIELDS *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   BM06F950      ASSIGN TO BM06F950.                           
           SELECT   BP13F800      ASSIGN TO BP13F800.                           
           SELECT   P13F800A      ASSIGN TO P13F800A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BM06F950                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F950.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   P13F800A                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13F800A-REC                    PIC X(2000).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F950-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-F800-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-F800A-WRITE              PIC 9(7)  VALUE ZEROS.              
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
       0000-MAIN.                                                               
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BM06F950       THRU 2000-EXIT.                     
           PERFORM 2010-READ-BP13F800       THRU 2010-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL F950-REGN-NO = HIGH-VALUES                                   
               AND F800-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================*                                                        
       1000-OPEN-FILES.                                                         
      *================*                                                        
                                                                                
           OPEN INPUT BM06F950                                                  
                      BP13F800                                                  
               OUTPUT P13F800A.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       2000-READ-BM06F950.                                                      
      *===================*                                                     
                                                                                
           READ BM06F950                                                        
             AT END                                                             
                MOVE HIGH-VALUES        TO F950-REGN-NO                         
                GO                      TO 2000-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO CNT-F950-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       2010-READ-BP13F800.                                                      
      *===================*                                                     
                                                                                
           READ BP13F800                                                        
             AT END                                                             
                MOVE HIGH-VALUES        TO F800-NUM-REGN                        
                GO                      TO 2010-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO CNT-F800-READ.                       
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       3000-PROCESS-RECORD.                                                     
      *===================*                                                     
                                                                                
           IF F800-NUM-REGN = F950-REGN-NO                                      
              PERFORM 3500-PROCESS-MASTER      THRU 3500-EXIT                   
              PERFORM 2000-READ-BM06F950       THRU 2000-EXIT                   
              PERFORM 2010-READ-BP13F800       THRU 2010-EXIT                   
           ELSE                                                                 
              IF F800-NUM-REGN > F950-REGN-NO                                   
                 PERFORM 2000-READ-BM06F950    THRU 2000-EXIT                   
              ELSE                                                              
                 PERFORM 2010-READ-BP13F800    THRU 2010-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       3500-PROCESS-MASTER.                                                     
      *====================*                                                    
                                                                                
           MOVE F950-SCH-ACC-NO           TO F800-NUM-SCH-ACC.                  
           MOVE F950-DTE-ACCEPTANCE       TO F800-DTE-ACCEPTANCE.               
           MOVE F950-DTE-UPDATE           TO BP13F800-MASTER(1482:8)            
           MOVE F950-NUM-TRANS            TO BP13F800-MASTER(1490:2)            
                                                                                
           WRITE P13F800A-REC FROM BP13F800-MASTER.                             
           ADD 1  TO CNT-F800A-WRITE.                                           
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       9000-CLOSE-FILES.                                                        
      *=====================*                                                   
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CY60'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BM06F950 READ     : ' CNT-F950-READ.                 
           DISPLAY 'NO. OF BP13F800 READ     : ' CNT-F800-READ.                 
           DISPLAY 'NO. OF P13F800A WRITTEN  : ' CNT-F800A-WRITE.               
                                                                                
           CLOSE BM06F950                                                       
                 BP13F800                                                       
                 P13F800A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
