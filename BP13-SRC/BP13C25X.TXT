      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C25X.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  25 APRIL 2016.                                            
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO UPDATE BP13K203 BASED ON BP13F203.       *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F203 -- SMS FILE FOR BN APPOINTMENT TYPE          *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13K203 -- SMS FILE                                  *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136192 25/04/2016 ESA1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F203 ASSIGN TO BP13F203.                                  
                                                                                
           SELECT BP13K203 ASSIGN TO BP13K203                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K203-KEY-FLD                               
                  FILE STATUS     IS BP13K203-STATUS.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F203                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  200 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F203.                                                           
                                                                                
       FD   BP13K203                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K203.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-K203-REWRITE-CNT    PIC 9(08) VALUE ZEROES.                       
       01  WS-K203-REWRITE-ERR    PIC 9(08) VALUE ZEROES.                       
       01  WS-K203-READ-ERR       PIC 9(08) VALUE ZEROES.                       
       01  WS-F203-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-K203-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-F203-EOF            PIC X     VALUE SPACES.                       
       01  BP13K203-STATUS        PIC 9(02) VALUE ZEROES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F203    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F203-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F203                                                 
                I-O    BP13K203.                                                
                                                                                
           IF BP13K203-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K203 : ' BP13K203-STATUS             
              MOVE BP13K203-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CURR-DATE FROM DATE YYYYMMDD.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F203.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F203 AT END                                                 
                MOVE HIGH-VALUES TO BP13F203-REC                                
                MOVE 'Y' TO WS-F203-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F203-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF F203-KEY-FLD NOT = SPACES AND LOW-VALUES AND ZEROES               
              PERFORM 3100-READ-BP13K203 THRU 3100-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F203        THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3100-READ-BP13K203.                                                      
      *-------------------*                                                     
           MOVE SPACES             TO BP13K203-REC.                             
           INITIALIZE                 BP13K203-REC.                             
           MOVE F203-KEY-FLD       TO K203-KEY-FLD.                             
                                                                                
           READ BP13K203.                                                       
                                                                                
           EVALUATE BP13K203-STATUS                                             
              WHEN 00                                                           
              WHEN 22                                                           
                  PERFORM 3200-REWRITE-BP13K203  THRU 3200-EXIT                 
                  ADD   1  TO WS-K203-READ-CNT                                  
              WHEN 23                                                           
                  DISPLAY 'RECORD NOT FOUND IN BP13K203:' F203-KEY-FLD          
                  ADD   1  TO WS-K203-READ-ERR                                  
              WHEN OTHER                                                        
                  DISPLAY  'READ ERROR IN BP13K203 ' F203-KEY-FLD               
                  ADD   1   TO WS-K203-READ-ERR                                 
                  GO TO 9000-CLOSE-FILES                                        
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------*                                                  
       3200-REWRITE-BP13K203.                                                   
      *----------------------*                                                  
                                                                                
           MOVE 'S'            TO K203-NUM-STATUS.                              
                                                                                
           REWRITE BP13K203-REC.                                                
                                                                                
           EVALUATE BP13K203-STATUS                                             
              WHEN 00                                                           
              WHEN 22                                                           
                  ADD   1  TO WS-K203-REWRITE-CNT                               
              WHEN OTHER                                                        
                  DISPLAY 'WRITE ERROR, STATUS : ' BP13K203-STATUS              
                  DISPLAY ' KEY     : ' K203-KEY-FLD                            
                  ADD   1   TO WS-K203-REWRITE-ERR                              
                  GO TO 9000-CLOSE-FILES                                        
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C25X  *************'.                           
           DISPLAY 'RECS READ FROM  BP13F203    : ' WS-F203-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS UPDATED  IN BP13K203   : ' WS-K203-REWRITE-CNT.        
           DISPLAY 'RECS ERROR WRITE BP13K203   : ' WS-K203-REWRITE-ERR.        
           DISPLAY 'RECS READ   FROM BP13K203   : ' WS-K203-READ-CNT.           
           DISPLAY 'RECS NOTFND   IN BP13K203   : ' WS-K203-READ-ERR.           
                                                                                
           CLOSE BP13F203                                                       
                 BP13K203.                                                      
                                                                                
           IF BP13K203-STATUS NOT = 0 AND 97                                    
             DISPLAY ' CLOSING ERROR, BP13K203-STATUS ' BP13K203-STATUS         
             MOVE BP13K203-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
