       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C239.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  OCT 12 2009.                                              
      * ============================================================            
      * SYSTEM OF COMMITMENT (SOC)                                              
      * ============================================================            
      * OBJECTIVE  - TO PROVIDE DAILY INTERFACE FILE FOR MAMS ON NPL            
      *              CASES                                                      
      *                                                                         
      * INPUT FILES:  BP13F800                                                  
      *               BM06K110                                                  
      * OUTPUT     :  BC03F818 - INTERFACE FILE FOR MAMS                        
      * ------------------------------------------------------------            
      * REF NO    DATE  BY  DESCRIPTIONS                                        
      * -------- ------ --- ------------                                        
      * BP133725 121009 ESA1 NEW PGM                                            
      * BP133997 131010 ESA1 ADD UPDATE OF SELLING-PRICE                        
      * ============================================================            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN        TO BP13F800.                           
                                                                                
           SELECT BC03F818 ASSIGN       TO BC03F818.                            
                                                                                
           SELECT BM06K110 ASSIGN        TO BM06K110                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K110-KEY-FLD                        
                           FILE STATUS   IS K110-STATUS.                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS.                                        
       COPY BP13F800.                                                           
                                                                                
       FD   BC03F818                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS.                                        
       COPY BC03F818.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F800-EOF                   PIC X      VALUE 'N'.                  
       01  K110-STATUS                   PIC 9(2)   VALUE 0.                    
       01  WS-CNT-F800-READ              PIC 9(07)  VALUE 0.                    
       01  WS-CNT-F818-WRITE             PIC 9(07)  VALUE 0.                    
       01  WS-DTE-KEY-ISSUED             PIC X(10)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F800    THRU 2000-EXIT                         
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9999-WRAP-UP          THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F800                                                
                        BM06K110                                                
                OUTPUT  BC03F818.                                               
                                                                                
            IF K110-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BM06K110,STATUS ' K110-STATUS           
               MOVE K110-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP      THRU 9999-EXIT                         
            END-IF.                                                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
            READ BP13F800 AT END                                                
                 MOVE 'Y'          TO WS-F800-EOF                               
                 GO TO 2000-EXIT                                                
            END-READ.                                                           
                                                                                
            ADD  1                   TO WS-CNT-F800-READ.                       
            PERFORM 3000-WRITE-BC03F818   THRU  3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-WRITE-BC03F818.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES                 TO BC03F818-REC.                         
           MOVE F800-NUM-REGN          TO F818-NUM-REGN.                        
                                                                                
           IF F800-NUM-SCH-ACC = SPACES OR LOW-VALUES                           
              MOVE ZEROES              TO F818-AMT-SELL-PRICE                   
              MOVE SPACES              TO WS-DTE-KEY-ISSUED                     
           ELSE                                                                 
              PERFORM 4000-READ-BM06K110  THRU 4000-EXIT                        
           END-IF.                                                              
                                                                                
           IF F800-NUM-STATUS = 'C'                                             
              MOVE F800-DTE-CANCEL     TO F818-DTE-CANCELLED                    
              MOVE SPACES              TO F818-DTE-TP                           
           ELSE                                                                 
             MOVE SPACES               TO F818-DTE-CANCELLED                    
             MOVE WS-DTE-KEY-ISSUED    TO F818-DTE-TP                           
           END-IF.                                                              
                                                                                
           WRITE BC03F818-REC.                                                  
           ADD 1  TO WS-CNT-F818-WRITE.                                         
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4000-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           MOVE F800-NUM-SCH-ACC TO   K110-KEY-FLD.                             
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE K110-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K110-CURR-SELLING-PRICE TO F818-AMT-SELL-PRICE         
                    MOVE K110-DTE-KEY-ISSUED     TO WS-DTE-KEY-ISSUED           
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE ZEROES                  TO F818-AMT-SELL-PRICE         
                    MOVE SPACES                  TO WS-DTE-KEY-ISSUED           
               WHEN OTHER                                                       
                    MOVE K110-STATUS              TO RETURN-CODE                
                    DISPLAY 'ERROR READING BM06K110, STATUS '                   
                             K110-STATUS                                        
                    PERFORM 9999-WRAP-UP    THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9999-WRAP-UP.                                                            
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY '- BP13C239 CONTROL TOTAL -------------'.                    
           DISPLAY ' TOTAL BP13F800 REC READ     : ' WS-CNT-F800-READ.          
           DISPLAY ' TOTAL BC03F818 RECS WRITE   : ' WS-CNT-F818-WRITE.         
                                                                                
           CLOSE BP13F800                                                       
                 BM06K110                                                       
                 BC03F818.                                                      
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BM06K110 ' K110-STATUS                  
              MOVE K110-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
