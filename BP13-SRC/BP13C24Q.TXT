       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C24Q.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  06 NOVEMBER 2013.                                         
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE: PRINT REPORT ON UPDATED POD/POF DUE DATE         *           
      *-------------------------------------------------------------*           
      * INPUT FILES: BM06F190                                       *           
      *              BM06K100                                       *           
      *              BM06K510                                       *           
      *              BB14K500                                       *           
      *              BP13K277                                       *           
      *                                                             *           
      * OUTPUT FILE: BP13L24Q (REPORT)                              *           
      *-------------------------------------------------------------*           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      *--------- ---  ------  --------------------------------------*           
      * BP135127 ESA1 061113  PRINT REPORT ON UPDATED POD/POF DATES *           
      * BP135177 ESA1 201213  CATER FOR OCS LAYOUT                  *           
      * BP135952 ESA1 081015  CATER FOR NEW OCS POE AND POG         *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K277  ASSIGN       TO BP13K277                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K277-KEY-FLD                        
                            FILE STATUS  IS WS-K277-STATUS.                     
                                                                                
           SELECT BM06K100  ASSIGN       TO BM06K100                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K100-KEY-FLD                        
                            FILE STATUS  IS WS-K100-STATUS.                     
                                                                                
           SELECT BM06K510  ASSIGN       TO BM06K510                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K510-ESTCDE                         
                            FILE STATUS  IS WS-K510-STATUS.                     
                                                                                
           SELECT BB14K500  ASSIGN       TO BB14K500                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K500-CDE-BLK                        
                            FILE STATUS  IS WS-K500-STATUS.                     
                                                                                
           SELECT BP13F205  ASSIGN TO BP13F205.                                 
                                                                                
           SELECT BP13L24Q  ASSIGN TO BP13L24Q.                                 
                                                                                
       DATA DIVISION.                                                           
      *--------------------------------------------------------------           
       FILE SECTION.                                                            
      *--------------------------------------------------------------           
                                                                                
       FD  BP13K277                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K277.                                                           
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K100.                                                           
                                                                                
       FD  BM06K510                                                             
           RECORD CONTAINS 60  CHARACTERS.                                      
       COPY BM06K510.                                                           
                                                                                
       FD  BB14K500                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BB14K500.                                                           
                                                                                
       FD  BP13F205                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING MODE IS F.                                                 
       COPY BP13F205.                                                           
                                                                                
       FD  BP13L24Q                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  BP13L24Q-REC                   PIC X(132).                           
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-WORK-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE            PIC X(08) VALUE SPACES.               
               10  FILLER              PIC X(13) VALUE SPACES.                  
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-MM          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-CCYY        PIC X(04) VALUE SPACES.                  
           05  WS-RETCODE              PIC -ZZZZ.                               
           05  WS-TOTAL-COST           PIC 9(08) VALUE ZEROES.                  
           05  WS-TOTAL-COST2          PIC 9(08) VALUE ZEROES.                  
           05  WS-K277-CNT             PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K277-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K100-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K510-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K500-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F205-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-F205       VALUE 'Y'.                               
           05  WS-K277-EOF             PIC X(01) VALUE 'N'.                     
           05  WS-K100-EOF             PIC X(01) VALUE 'N'.                     
           05  WS-K100-FND             PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F205-READ-CTR        PIC 9(06) VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(02) VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(04) VALUE ZEROES.                  
           05  WS-PRT-CTR              PIC 9(04) VALUE ZEROES.                  
                                                                                
       01  WS-F205-KEY.                                                         
           05  WS-KEY-ESTATE           PIC X(03) VALUE SPACES.                  
           05  WS-KEY-NEIGH            PIC X(04) VALUE SPACES.                  
           05  WS-KEY-CONTRACT         PIC X(04) VALUE SPACES.                  
                                                                                
       01  WS-F205-PREV.                                                        
           05  WS-PREV-ESTATE          PIC X(03) VALUE SPACES.                  
           05  WS-PREV-NEIGH           PIC X(04) VALUE SPACES.                  
           05  WS-PREV-CONTRACT        PIC X(04) VALUE SPACES.                  
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01 L24Q-HEADER-1.                                                        
          05 FILLER              PIC X(08)  VALUE 'BP13L24Q'.                   
          05 FILLER              PIC X(05)  VALUE SPACES .                      
          05 FILLER              PIC X(08)  VALUE 'HDBCAT3'.                    
          05 FILLER              PIC X(22)  VALUE SPACES.                       
          05 FILLER              PIC X(40)  VALUE                               
             'S Y S T E M   O F   C O M M I T M E N T'.                         
          05 FILLER              PIC X(28)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE 'DATE :'.                     
          05 L24Q-DATE           PIC X(10)  VALUE SPACES.                       
                                                                                
       01 L24Q-HEADER-2.                                                        
          05 FILLER              PIC X(42)  VALUE SPACES .                      
          05 FILLER              PIC X(50)  VALUE                               
             'REPORT ON UPDATED POD/POF/POW/POE/POG DUE DATE'.                  
          05 FILLER              PIC X(16)  VALUE SPACES .                      
          05 FILLER              PIC X(07)  VALUE 'PAGE : '.                    
          05 L24Q-PAGE           PIC ZZZ9 .                                     
                                                                                
       01 L24Q-HEADER-3.                                                        
          05 FILLER              PIC X(55)  VALUE                               
             'ESTATE                NEIGH/CONT        ECD            '.         
          05 FILLER              PIC X(62)  VALUE                               
             'POD/POS           POF             POW             POE  '.         
          05 FILLER              PIC X(07)  VALUE                               
             '    POG'.                                                         
                                                                                
       01 L24Q-HEADER-3A.                                                       
          05 FILLER              PIC X(53)  VALUE SPACES.                       
          05 FILLER              PIC X(49)  VALUE                               
             ' DUE DATE        DUE DATE        DUE DATE        '.               
          05 FILLER              PIC X(25)  VALUE                               
             'DUE DATE        DUE DATE'.                                        
                                                                                
       01 L24Q-HEADER-4.                                                        
          05 L24Q-NME-ESTATE     PIC X(20)  VALUE SPACES.                       
          05 FILLER              PIC X(02)  VALUE SPACES.                       
          05 L24Q-NEIGH-CONT     PIC X(09)  VALUE SPACES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-ECD            PIC X(10)  VALUE SPACES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-DTE-POD-DUE    PIC X(10)  VALUE SPACES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-DTE-POF-DUE    PIC X(10)  VALUE SPACES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-DTE-POW-DUE    PIC X(10)  VALUE SPACES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-DTE-POE-DUE    PIC X(10)  VALUE SPACES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-DTE-POG-DUE    PIC X(10)  VALUE SPACES.                       
                                                                                
       01 L24Q-HEADER-4A.                                                       
          05 FILLER              PIC X(53)  VALUE                               
             'BLK NO   FLAT        NUM     COST OF     COST OF'.                
          05 FILLER              PIC X(46)  VALUE                               
             'COST OF     COST OF     COST OF     COST OF   '.                  
          05 FILLER              PIC X(28)  VALUE                               
             '  TOTAL        TOTAL'.                                            
                                                                                
       01 L24Q-HEADER-4B.                                                       
          05 FILLER              PIC X(53)  VALUE                               
             '         TYPE        DOOR    DOORS/      FLOOR1 '.                
          05 FILLER              PIC X(45)  VALUE                               
             'FLOOR2      WALL        ELDERLY     GRAB BAR '.                   
          05 FILLER              PIC X(25)  VALUE                               
             '   COST         COST'.                                            
                                                                                
       01 L24Q-HEADER-4C.                                                       
          05 FILLER              PIC X(54)  VALUE                               
             '                             SANITARY           '.                
          05 FILLER              PIC X(59)  VALUE                               
             '                       PACKAGE           '.                       
          05 FILLER              PIC X(20)  VALUE                               
             ' (2-ROOM FLEXI)'.                                                 
                                                                                
       01 L24Q-DETAIL.                                                          
          05 L24Q-NUM-BLK        PIC X(05)  VALUE SPACES.                       
          05 FILLER              PIC X(04)  VALUE SPACES.                       
          05 L24Q-FLAT-TYPE      PIC X(09)  VALUE SPACES.                       
          05 FILLER              PIC X(03)  VALUE SPACES.                       
          05 L24Q-NUM-DOOR       PIC X(02)  VALUE SPACES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-AMT-POD        PIC ZZ,ZZ9 VALUE ZEROES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-AMT-POF        PIC ZZ,ZZ9 VALUE ZEROES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-AMT-POF2       PIC ZZ,ZZ9 VALUE ZEROES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-AMT-POW        PIC ZZ,ZZ9 VALUE ZEROES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-AMT-POE        PIC ZZ,ZZ9 VALUE ZEROES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-AMT-POG        PIC ZZ,ZZ9 VALUE ZEROES.                       
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-TOTAL-COST     PIC ZZZ,ZZ9 VALUE ZEROES.                      
          05 FILLER              PIC X(06)  VALUE SPACES.                       
          05 L24Q-TOTAL-COST2    PIC ZZZ,ZZ9 VALUE ZEROES.                      
                                                                                
       01 L24Q-FOOTER-1.                                                        
          05 FILLER              PIC X(40)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE                               
             'ECD AND OCS DUE DATE               '.                             
          05 FILLER              PIC X(10)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE                               
             'OCS COST                           '.                             
                                                                                
       01 L24Q-FOOTER-2.                                                        
          05 FILLER              PIC X(40)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE                               
             'UPDATED BY: _______________________'.                             
          05 FILLER              PIC X(10)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE                               
             'UPDATED BY: _______________________'.                             
                                                                                
       01 L24Q-FOOTER-3.                                                        
          05 FILLER              PIC X(40)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE                               
             'CHECKED BY: _______________________'.                             
          05 FILLER              PIC X(10)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE                               
             'CHECKED BY                         '.                             
                                                                                
       01 L24Q-FOOTER-4.                                                        
          05 FILLER              PIC X(40)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE SPACES.                       
          05 FILLER              PIC X(10)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE                               
             'TEAM HEAD: ________________________'.                             
                                                                                
       01 L24Q-FOOTER-5.                                                        
          05 FILLER              PIC X(40)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE SPACES.                       
          05 FILLER              PIC X(03)  VALUE SPACES .                      
          05 FILLER              PIC X(45)  VALUE                               
             'NOTE : COPY OF THE SALES BROCHURES'.                              
                                                                                
       01 L24Q-FOOTER-6.                                                        
          05 FILLER              PIC X(40)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE SPACES.                       
          05 FILLER              PIC X(10)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE                               
             'SPECIFICATION ATTACHED'.                                          
                                                                                
       01  L24Q-LINE             PIC X(132)    VALUE ALL '-'.                   
                                                                                
      *--------------------------------------------------------------           
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------           
                                                                                
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 2000-PROCESS-RECORDS   THRU 2000-EXIT                        
             UNTIL C-END-OF-F205.                                               
           PERFORM 4000-PRINT-FOOTER      THRU 4000-EXIT.                       
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BP13F205                                               
                         BP13K277                                               
                         BM06K510                                               
                         BM06K100                                               
                         BB14K500                                               
                OUTPUT   BP13L24Q.                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BM06K100-STATUS ' WS-K100-STATUS              
              MOVE WS-K100-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K277-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K277-STATUS ' WS-K277-STATUS              
              MOVE WS-K277-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K510-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BM06K510-STATUS ' WS-K510-STATUS              
              MOVE WS-K510-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K500-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BB14K500-STATUS ' WS-K500-STATUS              
              MOVE WS-K500-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BP13F205 AT END                                                 
                MOVE 'Y'     TO WS-END-OF-F205-SW                               
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F205-READ-CTR.                                         
                                                                                
           MOVE F205-NUM-NT-ZONE        TO WS-KEY-ESTATE.                       
           MOVE F205-NEIGHBOURHOOD      TO WS-KEY-NEIGH.                        
           MOVE F205-CONTRACT-NO        TO WS-KEY-CONTRACT.                     
                                                                                
           IF WS-F205-READ-CTR = 1                                              
              MOVE F205-NUM-NT-ZONE     TO WS-PREV-ESTATE                       
              MOVE F205-NEIGHBOURHOOD   TO WS-PREV-NEIGH                        
              MOVE F205-CONTRACT-NO     TO WS-PREV-CONTRACT                     
           END-IF.                                                              
                                                                                
           PERFORM 2100-STARTBR-BM06K100 THRU 2100-EXIT.                        
                                                                                
           IF WS-F205-KEY NOT = WS-F205-PREV AND                                
              WS-LINE-CTR NOT = 0                                               
              PERFORM 4000-PRINT-FOOTER THRU 4000-EXIT                          
              PERFORM 3110-PRINT-PAGE-BREAK   THRU 3110-EXIT                    
           END-IF.                                                              
                                                                                
           MOVE F205-NUM-NT-ZONE     TO WS-PREV-ESTATE.                         
           MOVE F205-NEIGHBOURHOOD   TO WS-PREV-NEIGH.                          
           MOVE F205-CONTRACT-NO     TO WS-PREV-CONTRACT.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       2100-STARTBR-BM06K100.                                                   
      *----------------------*                                                  
           MOVE 'N'                TO WS-K100-EOF                               
                                      WS-K100-FND.                              
           MOVE SPACES             TO K100-KEY-FLD.                             
           MOVE F205-NUM-NT-ZONE   TO K100-ESTATE.                              
           MOVE F205-NEIGHBOURHOOD TO K100-NEIGHBOURHOOD.                       
           MOVE F205-CONTRACT-NO   TO K100-CONTRACT-NO.                         
                                                                                
           START BM06K100 KEY >= K100-KEY-FLD.                                  
           EVALUATE WS-K100-STATUS                                              
           WHEN 00                                                              
                PERFORM 2110-K100-READ    THRU 2110-EXIT                        
                    UNTIL WS-K100-EOF = 'Y'                                     
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE SPACES        TO K100-REC                                  
                DISPLAY 'RECORD NOT FOUND IN BM06K100, KEY: '                   
                         K100-KEY-FLD                                           
           WHEN OTHER                                                           
                DISPLAY 'STARTBR ERROR BM06K100,STATUS ' WS-K100-STATUS         
                MOVE WS-K100-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------*                                                          
       2110-K100-READ.                                                          
      *--------------*                                                          
                                                                                
            READ BM06K100 NEXT RECORD                                           
                 AT END MOVE 'Y' TO WS-K100-EOF                                 
                        MOVE HIGH-VALUES TO K100-KEY-FLD                        
                 GO TO 2110-EXIT.                                               
                                                                                
           EVALUATE WS-K100-STATUS                                              
           WHEN 00                                                              
                IF K100-KEY-FLD(1:11) =  WS-F205-KEY                            
                   PERFORM 2400-STARTBR-BP13K277   THRU 2400-EXIT               
                ELSE                                                            
                   MOVE 'Y'      TO  WS-K100-EOF                                
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'         TO WS-K100-EOF                                 
           WHEN OTHER                                                           
                DISPLAY 'READNXT ERROR BM06K100,STATUS ' WS-K100-STATUS         
                MOVE WS-K100-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2110-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------*                                        
       2200-READ-BM06K510.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE F205-NUM-NT-ZONE      TO K510-ESTCDE.                           
           READ BM06K510.                                                       
                                                                                
           EVALUATE WS-K510-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BM06K510: ' K510-ESTCDE         
                   MOVE SPACES     TO K510-REC                                  
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BM06K510,STATUS ' WS-K510-STATUS         
                   MOVE WS-K510-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2300-READ-BB14K500.                                                      
      *--------------------------------*                                        
           MOVE K100-CDE-BLK          TO K500-CDE-BLK.                          
           READ BB14K500.                                                       
                                                                                
           EVALUATE WS-K500-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BB14K500, KEY: '                
                            K500-CDE-BLK                                        
                   MOVE SPACES         TO BB14K500-REC                          
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BB14K500,STATUS ' WS-K500-STATUS         
                   MOVE WS-K500-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       2400-STARTBR-BP13K277.                                                   
      *----------------------*                                                  
           MOVE 'N'              TO WS-K277-EOF.                                
           MOVE ZEROES           TO WS-K277-CNT.                                
           MOVE SPACES           TO K277-KEY-FLD.                               
           MOVE K100-KEY-FLD     TO K277-KEY-FLD.                               
           START BP13K277 KEY >= K277-KEY-FLD.                                  
           EVALUATE WS-K277-STATUS                                              
           WHEN 00                                                              
                PERFORM 2410-K277-READ    THRU 2410-EXIT                        
                    UNTIL WS-K277-EOF = 'Y'                                     
           WHEN 10                                                              
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K277, KEY: '                   
                         K277-KEY-FLD                                           
                GO TO 2000-EXIT                                                 
           WHEN OTHER                                                           
                DISPLAY 'STARTBR ERROR BP13K277,STATUS ' WS-K277-STATUS         
                MOVE WS-K277-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------*                                                          
       2410-K277-READ.                                                          
      *--------------*                                                          
                                                                                
            READ BP13K277 NEXT RECORD                                           
                 AT END MOVE 'Y' TO WS-K277-EOF                                 
                        MOVE HIGH-VALUES TO K277-KEY-FLD                        
                 GO TO 2410-EXIT.                                               
                                                                                
           EVALUATE WS-K277-STATUS                                              
           WHEN 00                                                              
                IF K100-KEY-FLD =  K277-KEY-FLD(1:16)                           
                   ADD   1  TO WS-K277-CNT                                      
                   PERFORM 3000-PRINT-REPORT  THRU 3000-EXIT                    
                   ADD  1   TO WS-PRT-CTR                                       
                ELSE                                                            
                   MOVE 'Y'      TO  WS-K277-EOF                                
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'         TO WS-K277-EOF                                 
           WHEN OTHER                                                           
                DISPLAY 'READNXT ERROR BP13K277,STATUS ' WS-K277-STATUS         
                MOVE WS-K277-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2410-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------*                                        
       3000-PRINT-REPORT.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 3100-CHECK-LINE-CTR        THRU 3100-EXIT.                   
           IF WS-K277-CNT = 1                                                   
              PERFORM 3200-MOVE-ESTATE-DETAILS   THRU 3200-EXIT                 
           END-IF.                                                              
           PERFORM 3300-MOVE-POD-POF-COST     THRU 3300-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3100-CHECK-LINE-CTR.                                                     
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 56  OR                                              
              WS-LINE-CTR = 0                                                   
              PERFORM 3110-PRINT-PAGE-BREAK   THRU 3110-EXIT                    
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3110-PRINT-PAGE-BREAK.                                                   
      *--------------------------------*                                        
                                                                                
           ADD   1   TO WS-PAGE-CTR.                                            
           MOVE WS-PAGE-CTR    TO L24Q-PAGE.                                    
           MOVE WS-DISP-DATE   TO L24Q-DATE.                                    
           WRITE BP13L24Q-REC  FROM L24Q-HEADER-1 AFTER PAGE.                   
           WRITE BP13L24Q-REC  FROM L24Q-HEADER-2.                              
           WRITE BP13L24Q-REC  FROM L24Q-LINE     AFTER 2.                      
           WRITE BP13L24Q-REC  FROM L24Q-HEADER-3.                              
           WRITE BP13L24Q-REC  FROM L24Q-HEADER-3A.                             
           PERFORM 3200-MOVE-ESTATE-DETAILS THRU 3200-EXIT.                     
           WRITE BP13L24Q-REC  FROM L24Q-HEADER-4.                              
           WRITE BP13L24Q-REC  FROM L24Q-LINE     AFTER 2.                      
           WRITE BP13L24Q-REC  FROM L24Q-HEADER-4A.                             
           WRITE BP13L24Q-REC  FROM L24Q-HEADER-4B.                             
           WRITE BP13L24Q-REC  FROM L24Q-HEADER-4C.                             
           WRITE BP13L24Q-REC  FROM L24Q-LINE.                                  
           MOVE SPACES           TO BP13L24Q-REC.                               
           WRITE BP13L24Q-REC.                                                  
           MOVE 9                TO WS-LINE-CTR.                                
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3200-MOVE-ESTATE-DETAILS.                                                
      *--------------------------------*                                        
           PERFORM 2200-READ-BM06K510   THRU 2200-EXIT.                         
           PERFORM 2300-READ-BB14K500   THRU 2300-EXIT.                         
           MOVE SPACES                  TO L24Q-DETAIL.                         
                                                                                
           MOVE K510-ESTATE-NAME        TO L24Q-NME-ESTATE.                     
           STRING K100-NEIGHBOURHOOD '/' K100-CONTRACT-NO                       
                  DELIMITED BY SIZE   INTO L24Q-NEIGH-CONT.                     
           MOVE K100-BLK-NO             TO L24Q-NUM-BLK.                        
                                                                                
           STRING K500-DTE-BLDG-COMPLETE (7:2) '/'                              
                  K500-DTE-BLDG-COMPLETE (5:2) '/'                              
                  K500-DTE-BLDG-COMPLETE (1:4)                                  
                  DELIMITED BY SIZE   INTO L24Q-ECD.                            
                                                                                
           STRING K100-DTE-POD-DUE (7:2) '/'                                    
                  K100-DTE-POD-DUE (5:2) '/'                                    
                  K100-DTE-POD-DUE (1:4)                                        
                  DELIMITED BY SIZE   INTO L24Q-DTE-POD-DUE.                    
                                                                                
           STRING K100-DTE-POF-DUE (7:2) '/'                                    
                  K100-DTE-POF-DUE (5:2) '/'                                    
                  K100-DTE-POF-DUE (1:4)                                        
                  DELIMITED BY SIZE   INTO L24Q-DTE-POF-DUE.                    
                                                                                
           STRING K100-DTE-POW-DUE (7:2) '/'                                    
                  K100-DTE-POW-DUE (5:2) '/'                                    
                  K100-DTE-POW-DUE (1:4)                                        
                  DELIMITED BY SIZE   INTO L24Q-DTE-POW-DUE.                    
                                                                                
           STRING K100-DTE-POE-DUE (7:2) '/'                                    
                  K100-DTE-POE-DUE (5:2) '/'                                    
                  K100-DTE-POE-DUE (1:4)                                        
                  DELIMITED BY SIZE   INTO L24Q-DTE-POE-DUE.                    
                                                                                
           STRING K100-DTE-POG-DUE (7:2) '/'                                    
                  K100-DTE-POG-DUE (5:2) '/'                                    
                  K100-DTE-POG-DUE (1:4)                                        
                  DELIMITED BY SIZE   INTO L24Q-DTE-POG-DUE.                    
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3300-MOVE-POD-POF-COST.                                                  
      *--------------------------------*                                        
                                                                                
           MOVE ZEROES                 TO L24Q-AMT-POE                          
                                          L24Q-AMT-POG.                         
                                                                                
           IF K277-NUM-POD-2RM NOT = LOW-VALUES AND ZEROES                      
              PERFORM 3100-CHECK-LINE-CTR         THRU 3100-EXIT                
              IF K277-NUM-FEATURE NOT = SPACES AND LOW-VALUES                   
                 IF K277-NUM-LAYOUT NOT = SPACES AND LOW-VALUES                 
                    STRING '2-RM(' K277-NUM-FEATURE ' '                         
                                   K277-NUM-LAYOUT ')'                          
                       DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                    
                 ELSE                                                           
                    STRING '2-RM(' K277-NUM-FEATURE ')'                         
                       DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                    
                 END-IF                                                         
              ELSE                                                              
                 IF K277-NUM-LAYOUT NOT = SPACES AND LOW-VALUES                 
                    STRING '2-RM(  '  K277-NUM-LAYOUT ')'                       
                       DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                    
                 ELSE                                                           
                    MOVE '2-RM'              TO L24Q-FLAT-TYPE                  
                 END-IF                                                         
              END-IF                                                            
              MOVE K277-NUM-POD-2RM       TO L24Q-NUM-DOOR                      
              MOVE K277-AMT-POD-2RM       TO L24Q-AMT-POD                       
              MOVE K277-AMT-POF-2RM       TO L24Q-AMT-POF                       
              MOVE K277-AMT-POF2-2RM      TO L24Q-AMT-POF2                      
              MOVE K277-AMT-POW-2RM       TO L24Q-AMT-POW                       
              MOVE K277-AMT-POE-2RM       TO L24Q-AMT-POE                       
              MOVE K277-AMT-POG-2RM       TO L24Q-AMT-POG                       
              IF K277-AMT-POD-2RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POD-2RM                   
              END-IF                                                            
              IF K277-AMT-POF-2RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POF-2RM                   
              END-IF                                                            
              IF K277-AMT-POF2-2RM NOT NUMERIC                                  
                 MOVE ZEROES              TO K277-AMT-POF2-2RM                  
              END-IF                                                            
              IF K277-AMT-POW-2RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POW-2RM                   
              END-IF                                                            
              IF K277-AMT-POE-2RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POE-2RM                   
              END-IF                                                            
              IF K277-AMT-POG-2RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POG-2RM                   
              END-IF                                                            
              COMPUTE WS-TOTAL-COST = K277-AMT-POD-2RM +                        
                                      K277-AMT-POF-2RM +                        
                                      K277-AMT-POW-2RM                          
              MOVE WS-TOTAL-COST          TO L24Q-TOTAL-COST                    
              IF K277-AMT-POF2-2RM > ZEROES OR                                  
                 K277-AMT-POE-2RM  > ZEROES OR                                  
                 K277-AMT-POG-2RM  > ZEROES                                     
                 COMPUTE WS-TOTAL-COST2 = K277-AMT-POD-2RM +                    
                                          K277-AMT-POF2-2RM +                   
                                          K277-AMT-POW-2RM +                    
                                          K277-AMT-POE-2RM +                    
                                          K277-AMT-POG-2RM                      
                 MOVE WS-TOTAL-COST2      TO L24Q-TOTAL-COST2                   
              ELSE                                                              
                 MOVE ZEROES              TO L24Q-TOTAL-COST2                   
              END-IF                                                            
              WRITE BP13L24Q-REC       FROM L24Q-DETAIL                         
              ADD 1                       TO WS-LINE-CTR                        
              MOVE SPACES                 TO L24Q-DETAIL                        
           END-IF.                                                              
                                                                                
           IF K277-NUM-POD-3RM NOT = LOW-VALUES AND ZEROES                      
              PERFORM 3100-CHECK-LINE-CTR         THRU 3100-EXIT                
              IF K277-NUM-FEATURE NOT = SPACES AND LOW-VALUES                   
                 IF K277-NUM-LAYOUT NOT = SPACES AND LOW-VALUES                 
                    STRING '3-RM(' K277-NUM-FEATURE ' '                         
                                   K277-NUM-LAYOUT  ')'                         
                     DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                      
                 ELSE                                                           
                    STRING '3-RM(' K277-NUM-FEATURE ')'                         
                     DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                      
                 END-IF                                                         
              ELSE                                                              
                 IF K277-NUM-LAYOUT NOT = SPACES AND LOW-VALUES                 
                    STRING '3-RM(  ' K277-NUM-LAYOUT  ')'                       
                       DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                    
                 ELSE                                                           
                    MOVE '3-RM'           TO L24Q-FLAT-TYPE                     
                 END-IF                                                         
              END-IF                                                            
              MOVE K277-NUM-POD-3RM       TO L24Q-NUM-DOOR                      
              MOVE K277-AMT-POD-3RM       TO L24Q-AMT-POD                       
              MOVE K277-AMT-POF-3RM       TO L24Q-AMT-POF                       
              MOVE K277-AMT-POW-3RM       TO L24Q-AMT-POW                       
              IF K277-AMT-POD-3RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POD-3RM                   
              END-IF                                                            
              IF K277-AMT-POF-3RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POF-3RM                   
              END-IF                                                            
              IF K277-AMT-POW-3RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POW-3RM                   
              END-IF                                                            
              COMPUTE WS-TOTAL-COST = K277-AMT-POD-3RM +                        
                                      K277-AMT-POF-3RM +                        
                                      K277-AMT-POW-3RM                          
              MOVE WS-TOTAL-COST          TO L24Q-TOTAL-COST                    
              MOVE ZEROES                 TO L24Q-TOTAL-COST2                   
              WRITE BP13L24Q-REC        FROM L24Q-DETAIL                        
              ADD 1                       TO WS-LINE-CTR                        
              MOVE SPACES                 TO L24Q-DETAIL                        
           END-IF.                                                              
                                                                                
           IF K277-NUM-POD-4RM NOT = LOW-VALUES AND ZEROES                      
              PERFORM 3100-CHECK-LINE-CTR         THRU 3100-EXIT                
              IF K277-NUM-FEATURE NOT = SPACES AND LOW-VALUES                   
                 IF K277-NUM-LAYOUT NOT = SPACES AND LOW-VALUES                 
                    STRING '4-RM(' K277-NUM-FEATURE ' '                         
                                   K277-NUM-LAYOUT  ')'                         
                        DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                   
                 ELSE                                                           
                    STRING '4-RM(' K277-NUM-FEATURE ')'                         
                        DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                   
                 END-IF                                                         
              ELSE                                                              
                 IF K277-NUM-LAYOUT NOT = SPACES AND LOW-VALUES                 
                    STRING '4-RM(  ' K277-NUM-LAYOUT  ')'                       
                        DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                   
                 ELSE                                                           
                    MOVE '4-RM'              TO L24Q-FLAT-TYPE                  
                 END-IF                                                         
              END-IF                                                            
              MOVE K277-NUM-POD-4RM       TO L24Q-NUM-DOOR                      
              MOVE K277-AMT-POD-4RM       TO L24Q-AMT-POD                       
              MOVE K277-AMT-POF-4RM       TO L24Q-AMT-POF                       
              MOVE K277-AMT-POW-4RM       TO L24Q-AMT-POW                       
              IF K277-AMT-POD-4RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POD-4RM                   
              END-IF                                                            
              IF K277-AMT-POF-4RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POF-4RM                   
              END-IF                                                            
              IF K277-AMT-POW-4RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POW-4RM                   
              END-IF                                                            
              COMPUTE WS-TOTAL-COST = K277-AMT-POD-4RM +                        
                                      K277-AMT-POF-4RM +                        
                                      K277-AMT-POW-4RM                          
              MOVE WS-TOTAL-COST          TO L24Q-TOTAL-COST                    
              MOVE ZEROES                 TO L24Q-TOTAL-COST2                   
              WRITE BP13L24Q-REC        FROM L24Q-DETAIL                        
              ADD 1                       TO WS-LINE-CTR                        
              MOVE SPACES                 TO L24Q-DETAIL                        
           END-IF.                                                              
                                                                                
           IF K277-NUM-POD-5RM NOT = LOW-VALUES AND ZEROES                      
              PERFORM 3100-CHECK-LINE-CTR         THRU 3100-EXIT                
              IF K277-NUM-FEATURE NOT = SPACES AND LOW-VALUES                   
                 IF K277-NUM-LAYOUT NOT = SPACES AND LOW-VALUES                 
                    STRING '5-RM(' K277-NUM-FEATURE ' '                         
                                   K277-NUM-LAYOUT  ')'                         
                       DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                    
                 ELSE                                                           
                    STRING '5-RM(' K277-NUM-FEATURE ')'                         
                       DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                    
                 END-IF                                                         
              ELSE                                                              
                 IF K277-NUM-LAYOUT NOT = SPACES AND LOW-VALUES                 
                    STRING '5-RM(  ' K277-NUM-LAYOUT  ')'                       
                       DELIMITED BY SIZE INTO L24Q-FLAT-TYPE                    
                 ELSE                                                           
                    MOVE '5-RM'              TO L24Q-FLAT-TYPE                  
                 END-IF                                                         
              END-IF                                                            
              MOVE K277-NUM-POD-5RM       TO L24Q-NUM-DOOR                      
              MOVE K277-AMT-POD-5RM       TO L24Q-AMT-POD                       
              MOVE K277-AMT-POF-5RM       TO L24Q-AMT-POF                       
              MOVE K277-AMT-POW-5RM       TO L24Q-AMT-POW                       
              IF K277-AMT-POD-5RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POD-5RM                   
              END-IF                                                            
              IF K277-AMT-POF-5RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POF-5RM                   
              END-IF                                                            
              IF K277-AMT-POW-5RM NOT NUMERIC                                   
                 MOVE ZEROES              TO K277-AMT-POW-5RM                   
              END-IF                                                            
              COMPUTE WS-TOTAL-COST = K277-AMT-POD-5RM +                        
                                      K277-AMT-POF-5RM +                        
                                      K277-AMT-POW-5RM                          
              MOVE WS-TOTAL-COST          TO L24Q-TOTAL-COST                    
              MOVE ZEROES                 TO L24Q-TOTAL-COST2                   
              WRITE BP13L24Q-REC        FROM L24Q-DETAIL                        
              ADD 1                       TO WS-LINE-CTR                        
              MOVE SPACES                 TO L24Q-DETAIL                        
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4000-PRINT-FOOTER.                                                       
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 38                                                  
              WRITE BP13L24Q-REC  FROM L24Q-FOOTER-1 AFTER PAGE                 
           ELSE                                                                 
              WRITE BP13L24Q-REC  FROM L24Q-FOOTER-1 AFTER 10                   
           END-IF.                                                              
                                                                                
           WRITE BP13L24Q-REC     FROM L24Q-FOOTER-2.                           
           WRITE BP13L24Q-REC     FROM L24Q-FOOTER-3 AFTER 2.                   
           WRITE BP13L24Q-REC     FROM L24Q-FOOTER-4.                           
           WRITE BP13L24Q-REC     FROM L24Q-FOOTER-5 AFTER 3.                   
           WRITE BP13L24Q-REC     FROM L24Q-FOOTER-6.                           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C24Q'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BP13F205 RECORDS READ       : ' WS-F205-READ-CTR.           
           DISPLAY 'NO. OF RECORDS PRINTED      : ' WS-PRT-CTR.                 
                                                                                
           CLOSE BM06K100                                                       
                 BM06K510                                                       
                 BP13K277                                                       
                 BB14K500                                                       
                 BP13F205                                                       
                 BP13L24Q.                                                      
                                                                                
            IF WS-K100-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BM06K100-STATUS ' WS-K100-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K277-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K277-STATUS ' WS-K277-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K510-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BM06K510-STATUS ' WS-K510-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K500-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BB14K500-STATUS ' WS-K500-STATUS            
            END-IF.                                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
