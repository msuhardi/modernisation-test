       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C11L.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  07/12/2016.                                               
      * ========================================================== *            
      * OBJECTIVE    :  CREATE BP13KC45 HISTORY FILE               *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13FC40                               *            
      * I-O    FILES :  1.) BP13KC45                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136525 KSJ3 07/12/2016 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC40  ASSIGN        TO BP13FC40.                          
                                                                                
           SELECT BP13KC45  ASSIGN        TO BP13KC45                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC45-KEY-FLD                       
                            FILE STATUS   IS WS-KC45-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FC40                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13FC40.                                                       
                                                                                
       FD  BP13KC45                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13KC45.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KC45-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FC40-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC45-WRITE         PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FC40-EOF               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13FC40-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-FC40-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FC40                                                 
                I-O    BP13KC45.                                                
                                                                                
           IF WS-KC45-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC45 ERROR ' WS-KC45-STATUS                  
              MOVE     WS-KC45-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13FC40-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13FC40                                                        
              AT END MOVE 'Y'              TO   WS-FC40-EOF.                    
                                                                                
           IF WS-FC40-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-FC40-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    IF NOT YET UPDATED, SKIP RECORD                                      
      *-------------------------------------------------------------            
           IF FC40-NUM-PEND-KEY-RSN = SPACES OR LOW-VALUES OR ZEROES            
              GO TO 3000-PROCEED-READ-NEXT.                                     
                                                                                
      *-------------------------------------------------------------            
      *    CREATE HISTORY FILE                                                  
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13KC45-REC.                   
           MOVE BP13FC40-REC               TO   BP13KC45-REC(1:100).            
           MOVE FC40-NUM-OIC               TO   KC45-NUM-OIC-HIST.              
           MOVE FC40-NUM-REGN              TO   KC45-NUM-REGN-HIST.             
           MOVE FUNCTION CURRENT-DATE(1:8) TO   KC45-DTE-CREATE-HIST.           
           MOVE FUNCTION CURRENT-DATE(9:8) TO   KC45-TME-CREATE-HIST.           
                                                                                
           PERFORM 3010-BP13KC45-WRITE     THRU 3010-EXIT.                      
                                                                                
       3000-PROCEED-READ-NEXT.                                                  
                                                                                
           PERFORM 2000-BP13FC40-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3010-BP13KC45-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13KC45-REC.                                                  
                                                                                
           EVALUATE WS-KC45-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-KC45-WRITE               
                                                                                
           WHEN 22                                                              
              DISPLAY 'DUPLICATE KC45 RECORD: ' FC40-NUM-REGN                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITE BP13KC45 ERROR ' WS-KC45-STATUS                    
              MOVE     WS-KC45-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FC40                                                       
                 BP13KC45.                                                      
                                                                                
           IF WS-KC45-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC45 ERROR ' WS-KC45-STATUS                  
              MOVE     WS-KC45-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C11L CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13FC40 READ      : ' WS-CNT-FC40-READ.          
           DISPLAY ' 2. NO OF BP13KC45 WRITE     : ' WS-CNT-KC45-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
