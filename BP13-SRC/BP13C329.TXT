      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C329.                                                 
      *AUTHOR.        CHYEO.                                                    
      *DATE-WRITTEN.  21/09/90.                                                 
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *    1.   THIS IS A PROGRAM TO PRINT A REPORT ON BOOKING *                
      *         FEES NOT REFUNDED 2 MONTHS                     *                
      *                                                        *                
      *    INPUT FILES :                                       *                
      *    1.   BP13K800 (SOC  MASTER)                         *                
      *    2.   BM06K110 (FLAT INVENTORY FILE)                 *                
      *                                                        *                
      *    OUTPUT LISTING :                                    *                
      *    1.   BP13L329 - SUMMARY LIST OF BOOKING FEES NOT    *                
      *                    FEFUNDED AFTER MORE THAN 3 MONTHS   *                
      *                    FROM DATE OF AO.                    *                
      *                                                        *                
      * CHG-NO.  BY  ON       DESCRIPTION                      *                
      * -------- --- -------- -----------                      *                
      * SOC-PH8B BSL 30/07/93 TO CHANGE THE MASTER FILE FROM   *                
      *                       P10K020 TO BP13K800.             *                
      * BP130015 SSS 13/09/95 CONVERT TO LASER PRINT REPORT    *                
      *                       CHG REPORT-ID POSITION TO COL 1  *                
      * BP130084 EVM 01/02/96 CREATE RECORDS IN BP13BOOK AND   *                
      *                       BP13DEBT FILE ON GIVEN CONDITIONS*                
      * BP130084 EVM 12/04/96 COMPUTE MTH MUST CONSIDER YY     *                
      * BP130130 EVM 16/07/96 TO CATER FOR COMPUTE MONTH = 0.  *                
      * BP130130 LJL 26/08/96 TO CATER FOR COMPUTATION AT WHICH*                
      *                       THE BK-MONTH = CURRENT MONTH.    *                
      * BP130275 LJL 30/04/97 TO CHANGE HANDOVER DATE TO       *                
      *                       KEY-AVAILABLE DATE.              *                
      * BP130418 SIA 980305  CHGE BM06K211 TO BM06K110         *                
      * BP130447 SRN 982312  OUTPUT BP13K142 FOR REGN CASES AND*                
      *                      CHGE BP13BOOK, BP13CDMT & BP13DEBT*                
      *                      TO   P13F132A, P13F142B & P13F142C*                
      *          SRN 990115  OUTPUT KEY-AVAIL-DATE/HANDOVER-DTE*                
      *          SRN 990226  OUTPUT CDMT ID AMT-CD NOT = 0     *                
      *          SRN 990824  GET REGN-DEPOSIT, ADDRESS,HH,ZONE *                
      *                      FROM BP13F800                     *                
      * BP130883 RAS 000807  TO CHANGE BP13C904 TO BP13C913    *                
      * BP131039 ANC 010614  ADD 'NUMBER OF CASES' AND 'GRAND  *                
      *                      TOTAL' FOOTER IN THE REPORT.      *                
      * BP135490 RJB 040215  SHORTENED TO MORE THAN 1 MONTH FROM                
      *                      DATE OF AO INSTEAD OF MORE THAN 2 *                
      * BP137337 PCL 240518  TO ADD CHECK FOR DDS SCHEME       *                
      **********************************************************                
                                                                                
      *************************                                                 
       ENVIRONMENT DIVISION.                                                    
      *************************                                                 
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *************************                                                 
       INPUT-OUTPUT SECTION.                                                    
      *************************                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800   ASSIGN TO BP13F800.                                
                                                                                
           SELECT BM06K110   ASSIGN TO BM06K110                                 
                  ACCESS MODE  IS RANDOM                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K110-KEY-FLD                                  
                  FILE STATUS  IS WS-K110-FILE-STATUS.                          
                                                                                
           SELECT BP13K130   ASSIGN TO BP13K130                                 
                  ACCESS MODE  IS DYNAMIC                                       
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K130-KEY-FLD                                  
                  FILE STATUS  IS WS-K130-FILE-STATUS.                          
                                                                                
           SELECT BP13K820   ASSIGN TO BP13K820                                 
                  ACCESS MODE  IS DYNAMIC                                       
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K820-KEY-FLD                                  
                  FILE STATUS  IS WS-K820-FILE-STATUS.                          
                                                                                
           SELECT SY02F001   ASSIGN TO SY02F001.                                
           SELECT P13F142A   ASSIGN TO P13F142A.                                
           SELECT P13F142B   ASSIGN TO P13F142B.                                
           SELECT P13F142C   ASSIGN TO P13F142C.                                
           SELECT BP13F142   ASSIGN TO BP13F142.                                
           SELECT BP13L329   ASSIGN TO BP13L329.                                
                                                                                
                                                                                
      *************************                                                 
       DATA DIVISION.                                                           
      *************************                                                 
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F142                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F142.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K130                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K130.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  P13F142A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       01  P13F142A-REC                PIC X(300).                              
                                                                                
       FD  P13F142B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       01  P13F142B-REC                PIC X(300).                              
                                                                                
       FD  P13F142C                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       01 P13F142C-REC                 PIC X(300).                              
                                                                                
       FD  BP13L329                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  L329-REC                    PIC X(132).                              
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
       01  WS-K110-FILE-STATUS         PIC 9(2)    VALUE ZEROS.                 
       01  WS-K130-FILE-STATUS         PIC 9(2)    VALUE ZEROS.                 
       01  WS-K820-FILE-STATUS         PIC 9(2)    VALUE ZEROS.                 
       01  WS-F800-READ                PIC 9(8)    VALUE ZEROES.                
       01  WS-F800-BYPASS              PIC 9(8)    VALUE ZEROES.                
       01  WS-K820-READ                PIC 9(8)    VALUE ZEROES.                
       01  WS-K130-READ                PIC 9(8)    VALUE ZEROES.                
       01  WS-K130-BYPASS              PIC 9(8)    VALUE ZEROES.                
       01  WS-F800-PROCESS             PIC 9(8)    VALUE ZEROES.                
       01  WS-K110-READ                PIC 9(8)    VALUE ZEROES.                
       01  WS-K110-BYPASS              PIC 9(8)    VALUE ZEROES.                
       01  WS-BOOK-WRITE               PIC 9(8)    VALUE ZEROES.                
       01  WS-DEBT-WRITE               PIC 9(8)    VALUE ZEROES.                
       01  WS-CDMT-WRITE               PIC 9(8)    VALUE ZEROES.                
       01  WS-F142-WRITE               PIC 9(8)    VALUE ZEROES.                
       01  WS-LINE                     PIC 9(2)    VALUE 65.                    
       01  WS-CASE                     PIC 9(4)    VALUE ZEROS.                 
       01  WS-PAGE                     PIC 9(4)    VALUE ZEROES.                
       01  WS-COMPUTE-MTH              PIC S9(2)    VALUE ZEROES.               
       01  WS-STORE-CMP-MTH            PIC S9(2)    VALUE ZEROES.               
       01  WS-GRAND-TOT1               PIC S9(6)V99 VALUE ZEROES.               
       01  WS-GRAND-TOT2               PIC S9(8)V99 VALUE ZEROES.               
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CCYY            PIC 9(4).                                
           05  WS-CURR-MM              PIC 99.                                  
           05  WS-CURR-DD              PIC 99.                                  
                                                                                
       01  WS-AGMT-DATE.                                                        
           05  WS-AGMT-CCYY            PIC X(4).                                
           05  WS-AGMT-MM              PIC X(2).                                
           05  WS-AGMT-DD              PIC X(2).                                
                                                                                
       01  WS-PROCESS-DATE.                                                     
           05  WS-PROCESS-CCYY         PIC 9(4).                                
           05  WS-PROCESS-MM           PIC 9(2).                                
           05  WS-PROCESS-DD           PIC 9(2).                                
                                                                                
       01  WS-DTE-COMPUTE.                                                      
           05  WS-COMPUTE-CCYY         PIC 9(4).                                
           05  WS-COMPUTE-MM           PIC 9(2).                                
           05  WS-COMPUTE-DD           PIC 9(2).                                
                                                                                
       01 WS-K130-EOF                  PIC X VALUE 'N'.                         
                                                                                
       01 WS-F001-EOF                  PIC X VALUE 'N'.                         
          88 EOF-F001                        VALUE 'Y'.                         
                                                                                
       01  WS-MONTH-DIFF               PIC 9(3).                                
       01  WS-MONTH-DIFF-RED REDEFINES WS-MONTH-DIFF.                           
           05  WS-MONTH-DIFF-R         PIC X(3).                                
                                                                                
      *----------------------*                                                  
      * LINKAGE FOR BP13C913 *                                                  
      *----------------------*                                                  
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH          PIC X(4).                           
               10  WS-LINK-NUM-ACC          PIC X(5).                           
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      **********************************************************                
      *    WS FOR SUMMARY LIST OF AGREEMENT ORDER RAISED       *                
      **********************************************************                
                                                                                
       01  L329-HDG1.                                                           
           05  FILLER              PIC X(8)    VALUE 'BP13L329'.                
           05  FILLER              PIC X(17)   VALUE SPACES.                    
           05  FILLER              PIC X(8)    VALUE 'HDB3    '.                
           05  FILLER              PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(16)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE: '.                  
           05  L329-PRT-DATE.                                                   
               10  L329-PRT-DD     PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  L329-PRT-MM     PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  L329-PRT-CCYY   PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'PAGE: '.                  
           05  L329-PAGE           PIC ZZZZ9.                                   
                                                                                
       01  L329-HDG2.                                                           
           05  FILLER              PIC X(49)   VALUE SPACES.                    
           05  FILLER              PIC X(30)   VALUE                            
              'SALES SECTION (AGREEMENT TEAM)'.                                 
                                                                                
       01  L329-HDG3.                                                           
           05  FILLER              PIC X(42)   VALUE SPACES.                    
           05  FILLER              PIC X(90)   VALUE                            
              '   SUMMARY LIST OF BOOKING FEES NOT REFUNDED'.                   
                                                                                
       01  L329-HDG4.                                                           
           05  FILLER              PIC X(42)   VALUE SPACES.                    
           05  FILLER              PIC X(90)   VALUE                            
              'MORE THAN 1 MONTH FROM DATE OF AGREEMENT ORDER'.                 
                                                                                
       01  L329-HDG5.                                                           
           05  FILLER              PIC X(08)   VALUE SPACES.                    
           05  FILLER              PIC X(15)   VALUE 'AGREEMENT ORDER'.         
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'CURRENT'.                 
           05  FILLER              PIC X(32)   VALUE SPACES.                    
           05  FILLER              PIC X(15)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE ' BOOKING FEE '.           
           05  FILLER              PIC X(07)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE 'SOC DEBTOR'.              
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(11)   VALUE 'DATE ACTION'.             
                                                                                
       01  L329-HDG6.                                                           
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'S/N '.                    
           05  FILLER              PIC X(06)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'DATE'.                    
           05  FILLER              PIC X(09)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'REGN NO.'.                
           05  FILLER              PIC X(19)   VALUE SPACES.                    
           05  FILLER              PIC X(19)   VALUE 'ADDRESS'.                 
           05  FILLER              PIC X(07)   VALUE SPACES.                    
           05  FILLER              PIC X(17) VALUE 'TO BE REFUNDED($)'.         
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE ' STATUS'.                 
           05  FILLER              PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'TAKEN  '.                 
                                                                                
       01  L329-DETAIL.                                                         
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  L329-NUM            PIC ZZZ9    VALUE ZEROS.                     
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  L329-AGR-ORDER-DATE.                                             
               10  L329-DD-SALE-AGR PIC X(2)   VALUE SPACES.                    
               10  FILLER           PIC X       VALUE '/'.                      
               10  L329-MM-SALE-AGR PIC X(2)   VALUE SPACES.                    
               10  FILLER           PIC X       VALUE '/'.                      
               10  L329-CCYY-SALE-AGR PIC X(4) VALUE SPACES.                    
           05  FILLER              PIC X(4)   VALUE SPACES.                     
           05  L329-CURR-REGNO     PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L329-BLK-DES        PIC X(05)   VALUE SPACES.                    
           05  L329-BLK-NO         PIC X(05).                                   
           05  L329-FLAT-NO.                                                    
               10  L329-HASH-DES   PIC X(02)   VALUE ' #'.                      
               10  L329-LEVEL-NO   PIC X(02).                                   
               10  L329-DASH-DES   PIC X(01)   VALUE '-'.                       
               10  L329-UNIT-NO    PIC X(04).                                   
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  L329-STREET         PIC X(22)   VALUE SPACES.                    
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  L329-BK-FEE-PAID    PIC Z,ZZ9.99- VALUE ZEROS.                   
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  L329-SOC-DEBTOR     PIC Z,ZZZ,ZZ9.99- VALUE ZEROS.               
           05  FILLER              PIC X(04)   VALUE SPACES.                    
                                                                                
       01  L329-FTR1.                                                           
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(18)   VALUE                            
              'NUMBER OF CASES = '.                                             
           05  L329-NOC            PIC ZZZZZ9.                                  
           05  FILLER              PIC X(38)   VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE                            
              'GRAND TOTALS =  '.                                               
           05  L329-GRAND-TOT1     PIC ZZZ,ZZ9.99- VALUE ZEROS.                 
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  L329-GRAND-TOT2     PIC ZZ,ZZZ,ZZ9.99- VALUE ZEROS.              
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      ******************************************************************        
       MAIN-ROUTINE SECTION.                                                    
      ******************************************************************        
           PERFORM 10-OPEN-FILES    THRU 10-EXIT.                               
                                                                                
           PERFORM 20-READ-BP13F800 THRU 20-EXIT                                
           PERFORM 30-PROCESS-F800  THRU 30-EXIT                                
             UNTIL  F800-NUM-REGN = HIGH-VALUE.                                 
                                                                                
           PERFORM 48-PRINT-TOTAL   THRU 48-EXIT.                               
                                                                                
           IF WS-F800-PROCESS   = ZEROS                                         
              MOVE ZEROS            TO L329-BK-FEE-PAID                         
              MOVE ZEROS            TO L329-SOC-DEBTOR                          
              PERFORM 32-PRINT-DTL  THRU 32-EXIT.                               
              PERFORM 33-PRINT-FTR  THRU 33-EXIT.                               
                                                                                
           PERFORM 50-CLOSE-ROUTINE THRU 50-EXIT.                               
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       10-OPEN-FILES.                                                           
      ******************************************************************        
           OPEN INPUT  BP13F800                                                 
                       BM06K110                                                 
                       BP13K130                                                 
                       BP13K820                                                 
                       SY02F001                                                 
                OUTPUT BP13L329  P13F142A  P13F142B P13F142C BP13F142.          
                                                                                
           IF WS-K110-FILE-STATUS NOT = 0 AND NOT = 97                          
              DISPLAY ' OPENING ERROR,WS-K110-FILE-STATUS '                     
              WS-K110-FILE-STATUS                                               
              MOVE WS-K110-FILE-STATUS TO RETURN-CODE                           
              PERFORM 50-CLOSE-ROUTINE THRU 50-EXIT.                            
                                                                                
           IF WS-K820-FILE-STATUS NOT = 0 AND NOT = 97                          
              DISPLAY ' OPENING ERROR,WS-K820-FILE-STATUS '                     
              WS-K820-FILE-STATUS                                               
              MOVE WS-K820-FILE-STATUS TO RETURN-CODE                           
              PERFORM 50-CLOSE-ROUTINE THRU 50-EXIT.                            
                                                                                
           IF WS-K130-FILE-STATUS NOT = 0 AND NOT = 97                          
              DISPLAY ' OPENING ERROR,WS-K130-FILE-STATUS '                     
              WS-K130-FILE-STATUS                                               
              MOVE WS-K130-FILE-STATUS TO RETURN-CODE                           
              PERFORM 50-CLOSE-ROUTINE THRU 50-EXIT.                            
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              GO TO 50-CLOSE-ROUTINE                                            
           ELSE                                                                 
              MOVE F001-DTE-CURRENT       TO WS-CURRENT-DATE                    
           END-IF.                                                              
                                                                                
           MOVE WS-CURR-DD   TO L329-PRT-DD.                                    
           MOVE WS-CURR-MM   TO L329-PRT-MM.                                    
           MOVE WS-CURR-CCYY TO L329-PRT-CCYY.                                  
                                                                                
       10-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       20-READ-BP13F800.                                                        
      ******************************************************************        
           READ BP13F800 AT END                                                 
                MOVE HIGH-VALUE TO F800-NUM-REGN                                
                GO TO 20-EXIT.                                                  
                                                                                
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       20-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       30-PROCESS-F800.                                                         
      ******************************************************************        
           IF F800-DTE-SALE-AGMT IS NOT NUMERIC                                 
               MOVE ZEROS TO F800-DTE-SALE-AGMT.                                
           IF F800-DTE-BK-APPT IS NOT NUMERIC                                   
               MOVE ZEROS TO F800-DTE-BK-APPT.                                  
           IF F800-DTE-CANCEL  IS NOT NUMERIC                                   
               MOVE ZEROS TO F800-DTE-CANCEL.                                   
           IF F800-NUM-ALLOC-DATE IS NOT NUMERIC                                
               MOVE ZEROS TO F800-NUM-ALLOC-DATE.                               
           IF F800-AMT-REGN-DEPOSIT-CASH IS NOT NUMERIC                         
               MOVE ZEROS TO F800-AMT-REGN-DEPOSIT-CASH.                        
           IF F800-AMT-REGN-DEPOSIT-CPF1 IS NOT NUMERIC                         
               MOVE ZEROS TO F800-AMT-REGN-DEPOSIT-CPF1.                        
           IF F800-AMT-REGN-DEPOSIT-CPF2 IS NOT NUMERIC                         
               MOVE ZEROS TO F800-AMT-REGN-DEPOSIT-CPF2.                        
           IF F800-AMT-REGN-DEPOSIT-CPF3 IS NOT NUMERIC                         
               MOVE ZEROS TO F800-AMT-REGN-DEPOSIT-CPF3.                        
           IF F800-AMT-REGN-DEPOSIT-CPF4 IS NOT NUMERIC                         
               MOVE ZEROS TO F800-AMT-REGN-DEPOSIT-CPF4.                        
           IF F800-AMT-CD IS NOT NUMERIC                                        
               MOVE ZEROS TO F800-AMT-CD.                                       
           IF F800-AMT-SOC-DEBTOR IS NOT NUMERIC                                
               MOVE ZEROS TO F800-AMT-SOC-DEBTOR.                               
           IF F800-AMT-BK-PAID IS NOT NUMERIC                                   
               MOVE ZEROS TO F800-AMT-BK-PAID.                                  
                                                                                
           IF F800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              PERFORM 31-GET-UNIT-ADDRESS THRU 31-EXIT                          
           END-IF.                                                              
                                                                                
           IF  F800-AMT-BK-PAID > ZEROES                                        
              IF F800-DTE-BK-APPT > ZEROS                                       
                 MOVE F800-DTE-BK-APPT TO WS-DTE-COMPUTE                        
                 PERFORM 35-COMPUTE-MONTH   THRU 35-EXIT                        
                 PERFORM 47-PROCESS-BOOK    THRU 47-EXIT                        
              ELSE                                                              
                 IF F800-NUM-ALLOC-DATE > ZEROS                                 
                     MOVE F800-NUM-ALLOC-DATE TO WS-DTE-COMPUTE                 
                     PERFORM 35-COMPUTE-MONTH   THRU 35-EXIT                    
                     PERFORM 47-PROCESS-BOOK    THRU 47-EXIT                    
                 ELSE                                                           
                     MOVE ZEROS TO WS-COMPUTE-MTH                               
                     PERFORM 47-PROCESS-BOOK    THRU 47-EXIT.                   
                                                                                
           IF F800-DTE-SALE-AGMT > ZEROS                                        
              MOVE F800-DTE-SALE-AGMT        TO WS-DTE-COMPUTE                  
              PERFORM 35-COMPUTE-MONTH       THRU 35-EXIT                       
           ELSE                                                                 
              MOVE ZEROS TO WS-COMPUTE-MTH.                                     
                                                                                
           IF F800-AMT-CD NOT = ZEROS                                           
              PERFORM 46-PROCESS-CDMT    THRU 46-EXIT.                          
                                                                                
           IF  F800-AMT-SOC-DEBTOR = ZEROES                                     
               NEXT SENTENCE                                                    
           ELSE                                                                 
               PERFORM 45-PROCESS-DEBT    THRU 45-EXIT.                         
                                                                                
           IF (F800-DTE-SALE-AGMT > ZEROES)  AND                                
              (F800-AMT-BK-PAID   > ZEROES)  AND                                
              (F800-NUM-HALF-IDP NOT = 'D' AND 'F')                             
                                                                                
               IF WS-COMPUTE-MTH > 1                                            
                   ADD 1 TO WS-F800-PROCESS                                     
                   PERFORM 32-PRINT-DTL        THRU 32-EXIT                     
               ELSE                                                             
                   ADD 1 TO WS-F800-BYPASS.                                     
                                                                                
           IF F800-AMT-REGN-DEPOSIT-CASH > ZEROS OR                             
              F800-AMT-REGN-DEPOSIT-CPF1 > ZEROS OR                             
              F800-AMT-REGN-DEPOSIT-CPF2 > ZEROS OR                             
              F800-AMT-REGN-DEPOSIT-CPF3 > ZEROS OR                             
              F800-AMT-REGN-DEPOSIT-CPF4 > ZEROS                                
                                                                                
              PERFORM 44-PROCESS-REGN-DEP      THRU 44-EXIT                     
                                                                                
           END-IF.                                                              
                                                                                
           MOVE ZEROES TO WS-PROCESS-DATE                                       
                          WS-DTE-COMPUTE                                        
                          WS-COMPUTE-MTH                                        
                          WS-STORE-CMP-MTH.                                     
                                                                                
           PERFORM 20-READ-BP13F800 THRU 20-EXIT.                               
                                                                                
       30-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       31-GET-UNIT-ADDRESS.                                                     
      ******************************************************************        
           MOVE SPACES                 TO BP13COMM8-REC.                        
           INITIALIZE                     BP13COMM8-REC.                        
                                                                                
           MOVE F800-NUM-SCH-ACC       TO WS-LINK-REC.                          
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR = 0                                              
              MOVE COMM8-NUM-BLK       TO L329-BLK-NO                           
              MOVE COMM8-NME-STREET    TO L329-STREET                           
              MOVE COMM8-NUM-UNIT-MAIN TO L329-UNIT-NO                          
              MOVE COMM8-NUM-LEVEL     TO L329-LEVEL-NO                         
              MOVE 'BLK  '             TO L329-BLK-DES                          
              MOVE ' #'                TO L329-HASH-DES                         
              MOVE '-'                 TO L329-DASH-DES                         
           ELSE                                                                 
              DISPLAY 'SQL CODE = ' COMM8-CDE-SYSERR                            
              MOVE SPACES TO L329-BLK-NO                                        
                             L329-STREET                                        
                             L329-UNIT-NO                                       
                             L329-LEVEL-NO                                      
                             L329-BLK-DES                                       
                             L329-HASH-DES                                      
                             L329-DASH-DES.                                     
                                                                                
       31-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       32-PRINT-DTL.                                                            
      ******************************************************************        
           ADD 1 TO WS-CASE.                                                    
                                                                                
           IF WS-LINE > 52                                                      
              ADD 1 TO WS-PAGE                                                  
              MOVE WS-PAGE TO L329-PAGE                                         
              WRITE L329-REC FROM L329-HDG1 AFTER PAGE                          
              WRITE L329-REC FROM L329-HDG2 AFTER 1                             
              WRITE L329-REC FROM L329-HDG3 AFTER 2                             
              WRITE L329-REC FROM L329-HDG4 AFTER 1                             
              WRITE L329-REC FROM L329-HDG5 AFTER 3                             
              WRITE L329-REC FROM L329-HDG6 AFTER 1                             
              MOVE SPACES TO L329-REC                                           
              WRITE L329-REC AFTER 1                                            
              MOVE 6 TO WS-LINE.                                                
                                                                                
           MOVE WS-CASE                TO L329-NUM.                             
           MOVE F800-NUM-REGN          TO L329-CURR-REGNO.                      
           MOVE F800-DTE-SALE-AGMT     TO WS-AGMT-DATE.                         
           MOVE WS-AGMT-CCYY           TO L329-CCYY-SALE-AGR.                   
           MOVE WS-AGMT-MM             TO L329-MM-SALE-AGR.                     
           MOVE WS-AGMT-DD             TO L329-DD-SALE-AGR.                     
           MOVE F800-AMT-BK-PAID       TO L329-BK-FEE-PAID.                     
           MOVE F800-AMT-SOC-DEBTOR    TO L329-SOC-DEBTOR.                      
                                                                                
           COMPUTE WS-GRAND-TOT1 = WS-GRAND-TOT1 + F800-AMT-BK-PAID.            
           COMPUTE WS-GRAND-TOT2 = WS-GRAND-TOT2 + F800-AMT-SOC-DEBTOR.         
                                                                                
           WRITE L329-REC FROM L329-DETAIL AFTER 1.                             
           ADD 1 TO WS-LINE.                                                    
                                                                                
       32-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       33-PRINT-FTR.                                                            
      ******************************************************************        
           MOVE WS-CASE                TO L329-NOC.                             
           MOVE WS-GRAND-TOT1          TO L329-GRAND-TOT1.                      
           MOVE WS-GRAND-TOT2          TO L329-GRAND-TOT2.                      
           WRITE L329-REC FROM L329-FTR1.                                       
       33-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       35-COMPUTE-MONTH.                                                        
      ******************************************************************        
           MOVE WS-CURR-MM    TO WS-PROCESS-MM.                                 
           MOVE WS-CURR-CCYY  TO WS-PROCESS-CCYY.                               
                                                                                
           IF WS-PROCESS-MM < WS-COMPUTE-MM                                     
              COMPUTE WS-PROCESS-MM = WS-PROCESS-MM + 12                        
              COMPUTE WS-PROCESS-CCYY = WS-CURR-CCYY - 1                        
           ELSE                                                                 
              IF WS-PROCESS-MM = WS-COMPUTE-MM                                  
                 MOVE 0               TO WS-PROCESS-MM.                         
                                                                                
           IF WS-PROCESS-MM = ZEROES                                            
              IF WS-PROCESS-CCYY > WS-COMPUTE-CCYY                              
              COMPUTE WS-COMPUTE-MTH = WS-PROCESS-MM +                          
                           ((WS-PROCESS-CCYY - WS-COMPUTE-CCYY) * 12)           
              ELSE                                                              
              COMPUTE WS-COMPUTE-MTH = 0                                        
           ELSE                                                                 
              COMPUTE WS-PROCESS-MM = WS-PROCESS-MM +                           
                           ((WS-PROCESS-CCYY - WS-COMPUTE-CCYY) * 12)           
           COMPUTE WS-COMPUTE-MTH = WS-PROCESS-MM - WS-COMPUTE-MM.              
                                                                                
           IF  WS-COMPUTE-MTH < 0                                               
               DISPLAY 'ERROR NEGATIVE FIGURES'.                                
                                                                                
       35-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       36-START-BR-BP13K130.                                                    
      ******************************************************************        
           MOVE SPACES             TO K130-KEY-FLD                              
                                                                                
           MOVE F800-NUM-REGN      TO K130-NUM-ORIG-REGN                        
                                                                                
           START BP13K130 KEY >= K130-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K130-FILE-STATUS                                         
             WHEN 00                                                            
               CONTINUE                                                         
             WHEN 10                                                            
             WHEN 23                                                            
               DISPLAY 'RECORD NOT FOUND IN BP13K130!!'                         
               DISPLAY 'REGN NO :- ' K130-NUM-ORIG-REGN                         
             WHEN OTHER                                                         
               DISPLAY 'ERROR READING BP13K130!!'                               
               DISPLAY 'K130-STATUS:- ' WS-K130-FILE-STATUS                     
               MOVE WS-K130-FILE-STATUS TO RETURN-CODE                          
               GO TO 50-CLOSE-ROUTINE                                           
           END-EVALUATE.                                                        
                                                                                
       36-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       37-READ-BP13K130.                                                        
      ******************************************************************        
           READ BP13K130 NEXT.                                                  
                                                                                
           EVALUATE WS-K130-FILE-STATUS                                         
             WHEN 00                                                            
             WHEN 10                                                            
             WHEN 23                                                            
               CONTINUE                                                         
             WHEN OTHER                                                         
               DISPLAY 'ERROR READING BP13K130!!'                               
               DISPLAY 'K130-STATUS:- ' WS-K130-FILE-STATUS                     
               MOVE WS-K130-FILE-STATUS TO RETURN-CODE                          
               GO TO 50-CLOSE-ROUTINE                                           
           END-EVALUATE.                                                        
                                                                                
       37-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       40-CREATE-F142-REC.                                                      
      ******************************************************************        
           MOVE F800-NUM-REGN             TO F142-NUM-REGN                      
           MOVE F800-NUM-SCH-ACC          TO F142-NUM-SCH-ACCT                  
           MOVE F800-DTE-SALE-AGMT        TO F142-DTE-SA                        
           MOVE F800-DTE-BK-APPT          TO F142-DTE-BKAPPT                    
           MOVE F800-NUM-ALLOC-DATE       TO F142-DTE-ALLOC                     
           MOVE F800-NUM-NEW-TOWN         TO F142-NUM-NT                        
           MOVE F800-NUM-ZONE             TO F142-NUM-ZONE                      
           MOVE F800-NUM-NRIC1            TO F142-NUM-NRIC1                     
           MOVE F800-NUM-NRIC2            TO F142-NUM-NRIC2                     
           MOVE F800-NUM-NRIC3            TO F142-NUM-NRIC3                     
           MOVE F800-NUM-NRIC4            TO F142-NUM-NRIC4                     
           MOVE F800-DTE-SO-APPT          TO F142-DTE-SO-APPMT                  
           MOVE F800-NUM-HOUSEHOLD        TO F142-NUM-HOUSEHOLD                 
           MOVE F800-NUM-STATUS           TO F142-NUM-STATUS                    
                                                                                
           IF F800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              IF COMM8-CDE-SYSERR = 0                                           
                                                                                
                 MOVE COMM8-NUM-BLK         TO F142-NUM-BLK                     
                 MOVE COMM8-NME-STREET      TO F142-NME-STREET                  
                 MOVE COMM8-NUM-LEVEL       TO F142-NUM-HSELVL                  
                 MOVE COMM8-NUM-UNIT-MAIN   TO F142-NUM-HSEMAIN                 
                 MOVE COMM8-NUM-UNIT-SUB    TO F142-NUM-HSESUB                  
                 MOVE COMM8-NUM-POSTAL-CODE TO F142-NUM-PD                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-COMPUTE-MTH            TO WS-MONTH-DIFF                      
           MOVE WS-MONTH-DIFF-R           TO F142-NUM-MONTH.                    
                                                                                
           IF F800-NUM-STATUS = 'C'                                             
              MOVE WS-COMPUTE-MTH         TO WS-STORE-CMP-MTH                   
                                                                                
              MOVE F800-DTE-CANCEL        TO F142-DTE-CANCEL                    
                                             WS-DTE-COMPUTE                     
                                                                                
              IF WS-DTE-COMPUTE > ZEROS                                         
                 PERFORM 35-COMPUTE-MONTH THRU 35-EXIT                          
              ELSE                                                              
                 MOVE ZEROS               TO   WS-COMPUTE-MTH                   
              END-IF                                                            
                                                                                
              MOVE WS-COMPUTE-MTH         TO WS-MONTH-DIFF                      
              MOVE WS-MONTH-DIFF-R        TO F142-NUM-MONTH-CANCEL              
                                                                                
              MOVE WS-STORE-CMP-MTH       TO WS-COMPUTE-MTH                     
           END-IF.                                                              
                                                                                
       40-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       41-READ-BP13K820.                                                        
      ******************************************************************        
           MOVE SPACES                TO K820-KEY-FLD.                          
           MOVE F800-NUM-NRIC1        TO K820-NUM-NRIC.                         
           MOVE F800-NUM-REGN         TO K820-NUM-REGN.                         
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-FILE-STATUS                                         
             WHEN 00                                                            
               MOVE K820-NME-OCCP    TO F142-NME-NAME1                          
               ADD  1                TO WS-K820-READ                            
             WHEN 23                                                            
               DISPLAY 'RECORD NOT FOUND IN BP13K820'                           
               DISPLAY 'K820-KEY-FLD :-' K820-KEY-FLD                           
             WHEN OTHER                                                         
               DISPLAY 'ERROR READING BP13K820!!'                               
               DISPLAY 'K820-STATUS:- ' WS-K820-FILE-STATUS                     
               MOVE WS-K820-FILE-STATUS TO RETURN-CODE                          
               GO TO 50-CLOSE-ROUTINE                                           
           END-EVALUATE.                                                        
                                                                                
       41-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       44-PROCESS-REGN-DEP.                                                     
      ******************************************************************        
           INITIALIZE BP13F142-REC.                                             
                                                                                
           PERFORM 36-START-BR-BP13K130 THRU 36-EXIT                            
                                                                                
           PERFORM UNTIL K130-NUM-ORIG-REGN NOT = F800-NUM-REGN                 
                       OR WS-K130-FILE-STATUS = 10 OR 23                        
                                                                                
             PERFORM 37-READ-BP13K130    THRU 37-EXIT                           
                                                                                
             IF WS-K130-FILE-STATUS = 0                                         
               IF K130-NUM-ORIG-REGN = F800-NUM-REGN                            
                  IF K130-TRANS-TYPE = '11' AND                                 
                         K130-PAYMENT-TYPE = 'RDE'                              
                     IF K130-DTE-POST IS NUMERIC                                
                        MOVE K130-DTE-POST     TO F142-DTE-PAID-CASH            
                                                  WS-DTE-COMPUTE                
                     ELSE                                                       
                        MOVE ZEROS             TO F142-DTE-PAID-CASH            
                                                  WS-DTE-COMPUTE                
                     END-IF                                                     
                     MOVE K130-NUM-RECPT-JRNO     TO F142-NUM-RECPT-CASH        
                  ELSE                                                          
                    IF K130-TRANS-TYPE = '12' AND                               
                         K130-PAYMENT-TYPE = 'RDE'                              
                       IF K130-DTE-POST IS NUMERIC                              
                         MOVE K130-DTE-POST     TO F142-DTE-PAID-CPF            
                                                  WS-DTE-COMPUTE                
                       ELSE                                                     
                         MOVE ZEROS             TO F142-DTE-PAID-CPF            
                                                   WS-DTE-COMPUTE               
                       END-IF                                                   
                       MOVE K130-NUM-RECPT-JRNO TO F142-NUM-RECPT-CPF           
                    END-IF                                                      
                  END-IF                                                        
               END-IF                                                           
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
           IF WS-DTE-COMPUTE > ZEROS                                            
              PERFORM 35-COMPUTE-MONTH    THRU 35-EXIT                          
           ELSE                                                                 
              MOVE ZEROS                  TO WS-COMPUTE-MTH                     
           END-IF                                                               
                                                                                
           MOVE F800-AMT-REGN-DEPOSIT-CASH                                      
                                          TO F142-AMT-CASH.                     
                                                                                
           MOVE ZEROS                     TO F142-AMT-CPF.                      
                                                                                
           COMPUTE F142-AMT-CPF  = F800-AMT-REGN-DEPOSIT-CPF1 +                 
                                   F800-AMT-REGN-DEPOSIT-CPF2 +                 
                                   F800-AMT-REGN-DEPOSIT-CPF3 +                 
                                   F800-AMT-REGN-DEPOSIT-CPF4.                  
                                                                                
           PERFORM 40-CREATE-F142-REC     THRU 40-EXIT                          
           PERFORM 41-READ-BP13K820       THRU 41-EXIT                          
                                                                                
           INSPECT BP13F142-REC REPLACING ALL LOW-VALUES BY SPACES              
                                                                                
           WRITE BP13F142-REC                                                   
           ADD 1     TO WS-F142-WRITE.                                          
                                                                                
       44-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       45-PROCESS-DEBT.                                                         
      ******************************************************************        
           INITIALIZE BP13F142-REC.                                             
                                                                                
           MOVE F800-AMT-SOC-DEBTOR        TO F142-AMT-CASH.                    
           PERFORM 40-CREATE-F142-REC      THRU 40-EXIT.                        
           PERFORM 41-READ-BP13K820        THRU 41-EXIT.                        
                                                                                
           INSPECT BP13F142-REC REPLACING ALL LOW-VALUES BY SPACES.             
                                                                                
           WRITE P13F142B-REC              FROM BP13F142-REC.                   
           ADD 1                           TO WS-DEBT-WRITE.                    
                                                                                
       45-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       46-PROCESS-CDMT.                                                         
      ******************************************************************        
           INITIALIZE BP13F142-REC.                                             
                                                                                
           MOVE F800-AMT-CD                TO F142-AMT-CASH.                    
                                                                                
           MOVE F800-NUM-SCH-ACC           TO K110-SCH-ACC-NO.                  
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-FILE-STATUS                                         
           WHEN 00                                                              
              IF K110-DTE-KEY-AVAIL = SPACES OR LOW-VALUES OR ZEROS             
                   MOVE 'N'            TO F142-CDE-HANDOVER                     
                ELSE                                                            
                   MOVE K110-DTE-KEY-AVAIL TO F142-DTE-KEY-AVAIL                
                   MOVE 'Y'            TO F142-CDE-HANDOVER                     
              END-IF                                                            
              ADD 1                    TO WS-K110-READ                          
           WHEN OTHER                                                           
                MOVE 'N'               TO F142-CDE-HANDOVER                     
                ADD 1                  TO WS-K110-BYPASS                        
                DISPLAY ' READ ERROR,K110-FILE-STATUS '                         
                         WS-K110-FILE-STATUS                                    
                         ' K110-KEY= ' K110-KEY-FLD                             
                         'F800-REGN= ' F800-NUM-REGN                            
           END-EVALUATE.                                                        
                                                                                
           PERFORM 40-CREATE-F142-REC      THRU 40-EXIT.                        
           INSPECT BP13F142-REC REPLACING ALL LOW-VALUES BY SPACES.             
                                                                                
           WRITE P13F142C-REC              FROM BP13F142-REC.                   
           ADD 1                           TO WS-CDMT-WRITE.                    
                                                                                
       46-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       47-PROCESS-BOOK.                                                         
      ******************************************************************        
           INITIALIZE BP13F142-REC.                                             
                                                                                
           PERFORM 36-START-BR-BP13K130    THRU 36-EXIT                         
                                                                                
           PERFORM UNTIL K130-NUM-ORIG-REGN NOT = F800-NUM-REGN                 
                   OR WS-K130-FILE-STATUS = '10' OR '23'                        
                                                                                
             PERFORM 37-READ-BP13K130      THRU 37-EXIT                         
             IF WS-K130-FILE-STATUS = 00                                        
                IF K130-NUM-ORIG-REGN = F800-NUM-REGN                           
                   IF K130-TRANS-TYPE = '11' AND                                
                         K130-PAYMENT-TYPE = 'BK '                              
                      MOVE K130-DTE-POST         TO F142-DTE-PAID-CASH          
                      MOVE K130-NUM-RECPT-JRNO   TO F142-NUM-RECPT-CASH         
                   END-IF                                                       
                END-IF                                                          
            END-IF                                                              
                                                                                
           END-PERFORM.                                                         
                                                                                
           MOVE F800-AMT-BK-PAID          TO F142-AMT-CASH.                     
           PERFORM 40-CREATE-F142-REC     THRU 40-EXIT                          
           PERFORM 41-READ-BP13K820       THRU 41-EXIT                          
                                                                                
           INSPECT BP13F142-REC REPLACING ALL LOW-VALUES BY SPACES.             
           WRITE P13F142A-REC          FROM BP13F142-REC.                       
           ADD 1                       TO WS-BOOK-WRITE.                        
                                                                                
       47-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       48-PRINT-TOTAL.                                                          
      ******************************************************************        
           DISPLAY '-- BP13C329  CONTROL COUNT --  '.                           
           DISPLAY ' '.                                                         
           DISPLAY 'F800-READ             : ' WS-F800-READ.                     
           DISPLAY 'F800-BYPASS-PRINTING  : ' WS-F800-BYPASS.                   
           DISPLAY 'F800-PROCESS-PRINTING : ' WS-F800-PROCESS.                  
           DISPLAY 'K110-READ             : ' WS-K110-READ.                     
           DISPLAY 'K110-BYPASS           : ' WS-K110-BYPASS.                   
           DISPLAY 'K820-READ             : ' WS-K820-READ.                     
           DISPLAY 'K130-READ             : ' WS-K130-READ.                     
           DISPLAY 'K130-BYPASS           : ' WS-K130-BYPASS.                   
           DISPLAY 'P13F142A - BOOK-INFO  : ' WS-BOOK-WRITE.                    
           DISPLAY 'P13F142B - SOC-DEBTOR : ' WS-DEBT-WRITE.                    
           DISPLAY 'P13F142C - CD INFO    : ' WS-CDMT-WRITE.                    
           DISPLAY 'BP13F142 - REG-DEP    : ' WS-F142-WRITE.                    
                                                                                
       48-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       50-CLOSE-ROUTINE.                                                        
      ******************************************************************        
           CLOSE BP13F800 BM06K110 BP13K130 BP13F142 BP13K820                   
                 BP13L329 P13F142A P13F142B P13F142C.                           
                                                                                
           IF WS-K110-FILE-STATUS NOT = 0 AND NOT = 97                          
              DISPLAY ' CLOSING ERROR,WS-K110-FILE-STATUS '                     
                        WS-K110-FILE-STATUS.                                    
                                                                                
           IF WS-K820-FILE-STATUS NOT = 0 AND NOT = 97                          
              DISPLAY ' CLOSING ERROR,WS-K820-FILE-STATUS '                     
                        WS-K820-FILE-STATUS.                                    
                                                                                
           IF WS-K130-FILE-STATUS NOT = 0 AND NOT = 97                          
              DISPLAY ' CLOSING ERROR,WS-K130-FILE-STATUS '                     
                        WS-K130-FILE-STATUS.                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       50-EXIT.                                                                 
           EXIT.                                                                
