       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C855.                                                 
       AUTHOR.        MTRS.                                                     
      *DATE-WRITTEN.  20/06/95.                                                 
      *REMARKS.                                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1.  TO UPDATE  BP13K800 WITH ORIGINAL PRIORITY QUEUE*                
      *        IN K800-NUM-PRIO-ORIG-QUEUE FOR PRIORITY CASES  *                
      *        EXCEPT JOINT-SELECTION SCHEME. THIS ORIGINAL    *                
      *        QUEUE IS BEFORE COMPARING THE QUEUE WITH PUBLIC *                
      *        AND BEFORE UPGRADE THEM TO GET THE BEST POSITION*                
      *                                                        *                
      *    INPUT  FILES : BP13F520                             *                
      *                                                        *                
      *    I/O   FILE   : BP13K800                             *                
      *                                                        *                
      *    OUTPUT FILES : NIL                                  *                
      *                                                        *                
      *    LISTING      : NIL                                  *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE    DETAILS                          *                
      * ======   ==  ====== ================================== *                
      * BP130418 SGK 310898 Y2K COMPLIANCE CHANGES             *                
      * BP130652 NOR 200299 EXPAND F520 RECL FROM 300 TO 500   *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    INDEXED                                       
                  ACCESS          RANDOM                                        
                  RECORD KEY      K800-NUM-REGN                                 
                  FILE STATUS     K800-FSTAT.                                   
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F520                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F520.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-F520-READ-CTR               PIC 9(7)  VALUE ZEROS.            
           05  WS-K800-UPDATE-CTR             PIC 9(7)  VALUE ZEROS.            
                                                                                
       01  WS-FILE-STATUS-INDICATORS.                                           
           05  K800-FSTAT                     PIC X(2)  VALUE SPACES.           
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-YYYY                PIC 9(4).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YYYY             PIC 9(4).                         
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-F520         THRU 2000-EXIT.                       
                                                                                
           PERFORM 4000-PROCESS-RECORDS   THRU 4000-EXIT                        
              UNTIL F520-KEY-NEW = HIGH-VALUES.                                 
                                                                                
           GO TO   9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-INITIALIZATION.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F520                                                 
                I-O    BP13K800                                                 
                                                                                
           IF K800-FSTAT NOT = '00'  AND '97'                                   
              DISPLAY 'BP13K800 OPEN ERROR : ' K800-FSTAT                       
              GO TO   9999-CLOSE-ROUTINE.                                       
                                                                                
           MOVE   FUNCTION CURRENT-DATE(1:8)                                    
                                      TO WS-SYSTEM-DATE.                        
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-YYYY         TO WS-EDITED-YYYY.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2000-READ-F520.                                                          
      ****************************************************************          
                                                                                
           READ BP13F520                                                        
              AT END      MOVE HIGH-VALUES TO F520-KEY-NEW                      
              GO TO       2000-EXIT                                             
           END-READ.                                                            
                                                                                
           ADD   +1       TO WS-F520-READ-CTR.                                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4000-PROCESS-RECORDS.                                                    
      *************************************************************             
                                                                                
           MOVE  F520-NUM-REGN   TO  K800-NUM-REGN                              
           READ BP13K800                                                        
           IF K800-FSTAT = '00'                                                 
              MOVE F520-NUM-NT-FT-QUEUE   TO  K800-NUM-PRIO-ORIG-QUEUE          
              REWRITE BP13K800-MASTER                                           
              ADD +1 TO  WS-K800-UPDATE-CTR                                     
           ELSE                                                                 
              IF K800-FSTAT = '23'                                              
                 DISPLAY 'BP13K800 RECORD NOT FOUND  : ' K800-NUM-REGN          
              ELSE                                                              
                 DISPLAY 'BP13K800 ACCESS ERROR : ' K800-FSTAT                  
                 GO TO   9999-CLOSE-ROUTINE.                                    
                                                                                
           PERFORM 2000-READ-F520  THRU 2000-EXIT.                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F520                                                       
                 BP13K800                                                       
                                                                                
           IF K800-FSTAT NOT = '00'                                             
              DISPLAY 'BP13K800 CLOSING ERROR : ' K800-FSTAT.                   
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C855 CONTROL TOTALS============*'.          
           DISPLAY '-UPDATE ORIGINAL QUEUE POSTION FOR PRIORITY CASE-'.         
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F520 RECORDS READ      = ' WS-F520-READ-CTR.            
           DISPLAY 'BP13K800 RECORDS UPDATED   = ' WS-K800-UPDATE-CTR.          
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
                                                                                
