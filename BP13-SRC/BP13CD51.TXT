       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD51.                                                 
      *AUTHOR.        ED1.                                                      
      *DATE-WRITTEN.  01/12/2006.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO CREATE REPORT FOR SURVEY FEE PAYMENTS           *        
      *             MADE FOR PREMIERE AT TAMPINES                      *        
      *                                                                *        
      * INPUT  FILES : BM06F120                                        *        
      *                BP13FD45                                        *        
      *                SY02F001                                        *        
      * OUTPUT FILES : BP13FOUT.                                       *        
      *                                                                *        
      * CHG-NO    BY    ON    - DESCRIPTION                            *        
      * ---------------------------------                              *        
      * BP133387 ED1 20080909   NEW PROGRAM                            *        
      * BP133387 JB8 20081017   ADD SCH-ACC AFTER REGN                 *        
      * BP133457 JB8 20081209   PRINT REPORT ONLY IF SURVEY-FEE > 0    *        
      * BP133387 JB8 20081218   TO CATER FOR 2 SCHEDULE                *        
      * BP133905 JB8 20100621   TO OUTPUT BP13KD85 FOR 8A CASES        *        
      * BP133905 JB8 20101013   TO OUTPUT ADDRESS TO BP13KD85          *        
      * BP133905 JB8 20101111   TO CATER FOR PP01                      *        
      * BP134500 IMC 20120424   FIX KD85 OUTPUT ISSUE WITH WRONG ADD   *        
      * BP134673 IMC 20121009   ADD SPECIAL CONDITION FOR '8A'/'8B'    *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BM06F120 ASSIGN       TO BM06F120.                            
           SELECT BP13FD45 ASSIGN       TO BP13FD45.                            
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
           SELECT BP13KD00 ASSIGN       TO BP13KD00                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KD00-NUM-REGN                        
                           FILE STATUS  IS WS-KD00-STATUS.                      
           SELECT BP13KD15 ASSIGN       TO BP13KD15                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KD15-KEY-FLD                         
                          ALTERNATE KEY IS KD15-ALT-KEY1                        
                           FILE STATUS  IS WS-KD15-STATUS.                      
           SELECT BP13KD03 ASSIGN       TO BP13KD03                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KD03-NUM-REGN                        
                           FILE STATUS  IS WS-KD03-STATUS.                      
           SELECT BP13KD85 ASSIGN       TO BP13KD85                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KD85-KEY-FLD                         
                           FILE STATUS  IS WS-KD85-STATUS.                      
           SELECT BP13FOUT ASSIGN       TO BP13FOUT.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BM06F120                RECORD CONTAINS 700 CHARACTERS              
                                    RECORDING MODE IS F.                        
       COPY BM06F120.                                                           
                                                                                
       FD   BP13FD45                RECORD CONTAINS 1550 CHARACTERS             
                                    RECORDING MODE IS F.                        
       COPY BP13FD45.                                                           
                                                                                
       FD   BP13KD15                                                            
                                    RECORD CONTAINS 300  CHARACTERS.            
       COPY BP13KD15.                                                           
                                                                                
       FD   BP13KD00                                                            
                                    RECORD CONTAINS 1600 CHARACTERS.            
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD03                                                            
                                    RECORD CONTAINS 1600 CHARACTERS.            
       COPY BP13KD03.                                                           
                                                                                
       FD   BP13KD85                                                            
                                    RECORD CONTAINS 80   CHARACTERS.            
       COPY BP13KD85.                                                           
                                                                                
       FD   BP13FOUT                RECORD CONTAINS 100 CHARACTERS              
                                    BLOCK CONTAINS 0 RECORDS                    
                                    LABEL RECORDS IS OMITTED                    
                                    RECORDING MODE IS F.                        
       01   BP13FOUT-REC.                                                       
            05  FOUT-NUM-PRJCT      PIC X(03).                                  
            05  FOUT-NUM-REGN       PIC X(08).                                  
            05  FOUT-NUM-SCH-ACC    PIC X(11).                                  
            05  FOUT-DTE-CONFIRM    PIC X(08).                                  
            05  FOUT-ADDRESS.                                                   
                10  FOUT-BLOCK-NO   PIC X(05).                                  
                10  FOUT-LEVEL      PIC X(02).                                  
                10  FOUT-MAIN-UNIT  PIC X(04).                                  
                10  FOUT-SUB-UNIT   PIC X(03).                                  
            05  FOUT-FLOOR-AREA     PIC 9(05)V99.                               
            05  FOUT-AMT-SURVEY     PIC 9(07)V99.                               
            05  FILLER              PIC X(40).                                  
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-F120-READ                 PIC 9(07) VALUE ZEROS.              
           05  WS-SURVEY                    PIC S9(7)V99  VALUE ZEROS.          
           05  WS-FEE-PD                    PIC S9(7)V99  VALUE ZEROS.          
           05  WS-FD45-READ                 PIC 9(07) VALUE ZEROS.              
           05  WS-FOUT-WRITTEN              PIC 9(07) VALUE ZEROS.              
           05  WS-UNMATCH-FD45              PIC 9(07) VALUE ZEROS.              
           05  WS-UNMATCH-F120              PIC 9(07) VALUE ZEROS.              
           05  WS-MATCH-REGN                PIC 9(07) VALUE ZEROS.              
           05  WS-PRJCT-NOTFND              PIC 9(07) VALUE ZEROS.              
           05  WS-KD85-WRITE                PIC 9(07) VALUE ZEROS.              
           05  WS-KD85-REWRITE              PIC 9(07) VALUE ZEROS.              
           05  WS-KD15-STATUS               PIC 9(02) VALUE ZEROS.              
           05  WS-KD00-STATUS               PIC 9(02) VALUE ZEROS.              
           05  WS-KD03-STATUS               PIC 9(02) VALUE ZEROS.              
           05  WS-KD85-STATUS               PIC 9(02) VALUE ZEROS.              
           05  WS-KD15-FND                  PIC X(01) VALUE SPACE.              
               88  KD15-FND                           VALUE 'Y'.                
           05  WS-KD00-FND                  PIC X(01) VALUE SPACE.              
               88  KD00-FND                           VALUE 'Y'.                
           05  WS-KD03-FND                  PIC X(01) VALUE SPACE.              
               88  KD03-FND                           VALUE 'Y'.                
           05  WS-KD85-FND                  PIC X(01) VALUE SPACE.              
               88  KD85-FND                           VALUE 'Y'.                
           05  WS-FOUT-HEADER-PRINT         PIC X(01) VALUE SPACE.              
               88  FOUT-HEADER-PRINT                  VALUE 'Y'.                
           05  CNT-FOUT                     PIC 9(03) VALUE ZEROS.              
                                                                                
       01  WS-TOP-CASE                      PIC X VALUE 'N'.                    
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-DD                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-MM                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-CCYY                 PIC 9(04) VALUE ZEROES.              
                                                                                
       01  WS-PRINT-CTRS.                                                       
           05  WS-PIDB-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-LETTER-PRINTED        PIC 9(6)   VALUE ZEROS.                 
           05  WS-FOUT-WRT-CTR          PIC 9(7)   VALUE ZEROES.                
           05  WS-SNO                   PIC 9(5)   VALUE ZEROES.                
           05  WS-PAGE-CTR              PIC 9(4)   VALUE ZEROES.                
           05  WS-LINE-CTR              PIC 9(2)   VALUE ZEROES.                
           05  WS-MAXLINE               PIC 9(2)   VALUE 60.                    
                                                                                
       01  WS-ADDRESS.                                                          
           05  WS-BLK-NO                PIC X(5)   VALUE SPACES.                
           05  WS-LEVEL-NO              PIC XX     VALUE SPACES.                
           05  WS-MAIN-UNIT             PIC X(4)   VALUE SPACES.                
           05  WS-SUB-UNIT              PIC X(3)   VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BM06F120       THRU 2000-EXIT.                     
           PERFORM 2100-READ-BP13FD45       THRU 2100-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL F120-REGN-NO  = HIGH-VALUES AND                              
                   FD45-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BM06F120                                                  
                      BP13FD45                                                  
                      SY02F001                                                  
                      BP13KD15                                                  
                      BP13KD00                                                  
                      BP13KD03                                                  
               I-O    BP13KD85                                                  
               OUTPUT BP13FOUT.                                                 
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT(1:4) TO WS-CUR-CCYY.                           
           MOVE F001-DTE-CURRENT(5:2) TO WS-CUR-MM.                             
           MOVE F001-DTE-CURRENT(7:2) TO WS-CUR-DD.                             
                                                                                
           IF WS-KD15-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD15 - OPEN ERROR (' WS-KD15-STATUS ')'              
              MOVE WS-KD15-STATUS            TO RETURN-CODE                     
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD00 - OPEN ERROR (' WS-KD00-STATUS ')'              
              MOVE WS-KD00-STATUS            TO RETURN-CODE                     
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-KD03-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD03 - OPEN ERROR (' WS-KD03-STATUS ')'              
              MOVE WS-KD03-STATUS            TO RETURN-CODE                     
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-KD85-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD85 - OPEN ERROR (' WS-KD85-STATUS ')'              
              MOVE WS-KD85-STATUS            TO RETURN-CODE                     
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BM06F120.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06F120                                                        
             AT END MOVE HIGH-VALUES TO F120-REGN-NO                            
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD  1                    TO WS-F120-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13FD45.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FD45                                                        
             AT END MOVE HIGH-VALUES TO  FD45-NUM-REGN                          
             GO TO 2100-EXIT.                                                   
                                                                                
           ADD  1                    TO  WS-FD45-READ.                          
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF FD45-NUM-REGN = F120-REGN-NO                                      
              ADD 1                                  TO WS-MATCH-REGN           
              PERFORM 3050-CHECK-AMTS              THRU 3050-EXIT               
              IF WS-SURVEY > 0 AND                                              
                 FD45-DTE-UPDATE-HIST = F001-DTE-CURRENT                        
                 PERFORM 3200-MOVE-ADDRESS         THRU 3200-EXIT               
                 PERFORM 4000-CREATE-BP13KD85      THRU 4000-EXIT               
                 PERFORM 3100-PROCESS-PRINT        THRU 3100-EXIT               
              END-IF                                                            
              PERFORM 2100-READ-BP13FD45           THRU 2100-EXIT               
           ELSE                                                                 
              IF FD45-NUM-REGN > F120-REGN-NO                                   
                 ADD   1                             TO WS-UNMATCH-F120         
                 PERFORM 2000-READ-BM06F120        THRU 2000-EXIT               
              ELSE                                                              
                 ADD   1                             TO WS-UNMATCH-FD45         
                 PERFORM 3050-CHECK-AMTS           THRU 3050-EXIT               
                 IF WS-SURVEY > 0 AND                                           
                    FD45-DTE-UPDATE-HIST = F001-DTE-CURRENT                     
                    INITIALIZE WS-ADDRESS                                       
                    PERFORM 4000-CREATE-BP13KD85   THRU 4000-EXIT               
                    PERFORM 3100-PROCESS-PRINT     THRU 3100-EXIT               
                 END-IF                                                         
                 PERFORM 2100-READ-BP13FD45        THRU 2100-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3050-CHECK-AMTS.                                                         
      *--------------------------------------------------------------           
           MOVE 'N'    TO WS-TOP-CASE.                                          
           MOVE ZEROS  TO WS-FEE-PD.                                            
           MOVE ZEROS  TO WS-SURVEY.                                            
                                                                                
           IF FD45-AMT-SURVEY-CASH          IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-SURVEY-CASH                                
           END-IF.                                                              
           IF FD45-AMT-SURVEY-CPF1          IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-SURVEY-CPF1                                
           END-IF.                                                              
           IF FD45-AMT-SURVEY-CPF2          IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-SURVEY-CPF2                                
           END-IF.                                                              
           IF FD45-AMT-SURVEY-CPF3          IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-SURVEY-CPF3                                
           END-IF.                                                              
           IF FD45-AMT-SURVEY-CPF4          IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-SURVEY-CPF4                                
           END-IF.                                                              
           IF FD45-AMT-STAMP-CASH-PD        IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-STAMP-CASH-PD                              
           END-IF.                                                              
           IF FD45-AMT-STAMP-CPF-PD         IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-STAMP-CPF-PD                               
           END-IF.                                                              
                                                                                
           IF FD45-AMT-SOLICITOR-BILL-CASH  IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-SOLICITOR-BILL-CASH                        
           END-IF.                                                              
           IF FD45-AMT-SOLICITOR-BILL-CPF1  IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-SOLICITOR-BILL-CPF1                        
           END-IF.                                                              
           IF FD45-AMT-SOLICITOR-BILL-CPF2  IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-SOLICITOR-BILL-CPF2                        
           END-IF.                                                              
           IF FD45-AMT-SOLICITOR-BILL-CPF3  IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-SOLICITOR-BILL-CPF3                        
           END-IF.                                                              
           IF FD45-AMT-SOLICITOR-BILL-CPF4  IS NOT NUMERIC                      
              MOVE ZEROS TO FD45-AMT-SOLICITOR-BILL-CPF4                        
           END-IF.                                                              
                                                                                
           MOVE FD45-NUM-REGN                 TO KD00-NUM-REGN                  
           PERFORM 8001-READ-BP13KD00       THRU 8001-EXIT                      
                                                                                
           IF KD00-NUM-PROJECT = 'D03'                                          
              IF FD45-NUM-PP-SCHDL = '8A' OR '8B'                               
                 COMPUTE WS-SURVEY = FD45-AMT-SOLICITOR-BILL                    
              END-IF                                                            
              GO TO 3050-EXIT                                                   
           END-IF                                                               
                                                                                
           IF FD45-NUM-PP-SCHDL = '8A'                                          
              COMPUTE WS-SURVEY = FD45-AMT-SURVEY                               
           ELSE                                                                 
              COMPUTE WS-SURVEY = FD45-AMT-SURVEY-CASH +                        
                                  FD45-AMT-SURVEY-CPF1 +                        
                                  FD45-AMT-SURVEY-CPF2 +                        
                                  FD45-AMT-SURVEY-CPF3 +                        
                                  FD45-AMT-SURVEY-CPF4                          
           END-IF.                                                              
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-PROCESS-PRINT.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                  TO BP13FOUT-REC.                        
           INITIALIZE                      BP13FOUT-REC.                        
                                                                                
           MOVE KD00-NUM-PROJECT        TO FOUT-NUM-PRJCT                       
           MOVE FD45-NUM-REGN           TO FOUT-NUM-REGN                        
           MOVE FD45-NUM-SCH-ACC        TO FOUT-NUM-SCH-ACC                     
           MOVE F120-BLK-NO             TO FOUT-BLOCK-NO                        
           MOVE F120-LEVEL-NO           TO FOUT-LEVEL                           
           MOVE F120-MAIN-UNIT          TO FOUT-MAIN-UNIT                       
           MOVE F120-SUB-UNIT           TO FOUT-SUB-UNIT                        
           MOVE F120-FLOOR-AREA         TO FOUT-FLOOR-AREA                      
           MOVE WS-SURVEY               TO FOUT-AMT-SURVEY                      
                                                                                
           DISPLAY KD00-NUM-PROJECT FD45-NUM-REGN                               
           MOVE KD00-NUM-PROJECT        TO KD15-NUM-PRJCT-KEY1                  
           MOVE FD45-NUM-REGN           TO KD15-NUM-REGN                        
           PERFORM 8006-READ-BP13KD16 THRU 8006-EXIT                            
           DISPLAY KD15-DTE-DBSS-CONFIRM.                                       
           MOVE KD15-DTE-DBSS-CONFIRM   TO FOUT-DTE-CONFIRM.                    
                                                                                
           WRITE BP13FOUT-REC.                                                  
           ADD 1                        TO CNT-FOUT.                            
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3200-MOVE-ADDRESS.                                                       
                                                                                
           INITIALIZE    WS-ADDRESS.                                            
                                                                                
           MOVE F120-BLK-NO                   TO WS-BLK-NO.                     
           MOVE F120-MAIN-UNIT                TO WS-MAIN-UNIT.                  
           MOVE F120-SUB-UNIT                 TO WS-SUB-UNIT.                   
           MOVE F120-LEVEL-NO                 TO WS-LEVEL-NO.                   
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
      *-------------------------------------------------------------            
       4000-CREATE-BP13KD85.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF FD45-NUM-PP-SCHDL = '8A' OR '01' OR 'TR' OR '8B'                  
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 4000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE FD45-NUM-REGN                 TO KD00-NUM-REGN                  
           PERFORM 8001-READ-BP13KD00       THRU 8001-EXIT                      
           IF NOT KD00-FND                                                      
              MOVE FD45-NUM-REGN              TO KD03-NUM-REGN                  
              PERFORM 8002-READ-BP13KD03    THRU 8002-EXIT                      
              IF KD03-FND                                                       
                 MOVE BP13KD03-REC            TO BP13KD00-REC                   
              ELSE                                                              
                 ADD 1                        TO WS-PRJCT-NOTFND                
                 DISPLAY 'PRJCT NOT FND, REGN IS ' FD45-NUM-REGN                
                 GO TO 4000-EXIT                                                
              END-IF                                                            
           END-IF                                                               
                                                                                
           MOVE KD00-NUM-PROJECT              TO KD85-NUM-PRJCT                 
           MOVE FD45-NUM-REGN                 TO KD85-NUM-REGN                  
           PERFORM 8003-READ-BP13KD85       THRU 8003-EXIT                      
                                                                                
           MOVE SPACES                        TO BP13KD85-REC                   
           INITIALIZE                            BP13KD85-REC                   
                                                                                
           MOVE KD00-NUM-PROJECT              TO KD85-NUM-PRJCT                 
           MOVE FD45-NUM-REGN                 TO KD85-NUM-REGN                  
           MOVE WS-SURVEY                     TO KD85-AMT-SURVEY-FEE            
           MOVE F001-DTE-CURRENT              TO KD85-DTE-CREATE                
           MOVE 'BP13CD51'                    TO KD85-NUM-USERID.               
                                                                                
           MOVE WS-BLK-NO                     TO KD85-NUM-BLOCK                 
           MOVE WS-MAIN-UNIT                  TO KD85-NUM-MAIN-UNIT             
           MOVE WS-SUB-UNIT                   TO KD85-NUM-SUB-UNIT              
           MOVE WS-LEVEL-NO                   TO KD85-NUM-LEVEL                 
                                                                                
           IF KD85-FND                                                          
              PERFORM 8004-REWRITE-BP13KD85 THRU 8004-EXIT                      
           ELSE                                                                 
              PERFORM 8005-WRITE-BP13KD85   THRU 8005-EXIT                      
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8001-READ-BP13KD00.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KD00.                                                       
                                                                                
           IF WS-KD00-STATUS = 00 OR 02                                         
              MOVE 'Y'                        TO WS-KD00-FND                    
           ELSE                                                                 
              IF WS-KD00-STATUS = 23                                            
                 MOVE 'N'                     TO WS-KD00-FND                    
              ELSE                                                              
                 DISPLAY 'BP13KD00 - READ ERROR (' WS-KD00-STATUS ')'           
                 MOVE WS-KD00-STATUS          TO RETURN-CODE                    
                 GO TO 9000-CLOSING-ROUTINE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8002-READ-BP13KD03.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KD03.                                                       
                                                                                
           IF WS-KD03-STATUS = 00 OR 02                                         
              MOVE 'Y'                        TO WS-KD03-FND                    
           ELSE                                                                 
              IF WS-KD03-STATUS = 23                                            
                 MOVE 'N'                     TO WS-KD03-FND                    
              ELSE                                                              
                 DISPLAY 'BP13KD03 - READ ERROR (' WS-KD03-STATUS ')'           
                 MOVE WS-KD03-STATUS          TO RETURN-CODE                    
                 GO TO 9000-CLOSING-ROUTINE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8003-READ-BP13KD85.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KD85.                                                       
                                                                                
           IF WS-KD85-STATUS = 00 OR 02                                         
              MOVE 'Y'                        TO WS-KD85-FND                    
           ELSE                                                                 
              IF WS-KD85-STATUS = 23                                            
                 MOVE 'N'                     TO WS-KD85-FND                    
              ELSE                                                              
                 DISPLAY 'BP13KD85 - READ ERROR (' WS-KD85-STATUS ')'           
                 MOVE WS-KD85-STATUS          TO RETURN-CODE                    
                 GO TO 9000-CLOSING-ROUTINE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8003-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8004-REWRITE-BP13KD85.                                                   
      *-------------------------------------------------------------            
                                                                                
           REWRITE BP13KD85-REC.                                                
                                                                                
           IF WS-KD85-STATUS = 00 OR 02                                         
              ADD 1                           TO WS-KD85-REWRITE                
           ELSE                                                                 
              DISPLAY 'BP13KD85 - REWRITE ERROR (' WS-KD85-STATUS ')'           
              MOVE WS-KD85-STATUS             TO RETURN-CODE                    
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
       8004-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8005-WRITE-BP13KD85.                                                     
      *-------------------------------------------------------------            
                                                                                
           WRITE BP13KD85-REC.                                                  
                                                                                
           IF WS-KD85-STATUS = 00 OR 02                                         
              ADD 1                           TO WS-KD85-WRITE                  
           ELSE                                                                 
              DISPLAY 'BP13KD85 - WRITE ERROR (' WS-KD85-STATUS ')'             
              MOVE WS-KD85-STATUS             TO RETURN-CODE                    
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
       8005-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8006-READ-BP13KD16.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KD15 KEY IS KD15-ALT-KEY1.                                  
                                                                                
           DISPLAY WS-KD15-STATUS                                               
           DISPLAY KD15-DTE-DBSS-CONFIRM.                                       
                                                                                
           IF WS-KD15-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-KD15-STATUS = 23                                            
                 MOVE SPACES                  TO KD15-DTE-DBSS-CONFIRM          
              ELSE                                                              
                 DISPLAY 'BP13KD15 - READ ERROR (' WS-KD15-STATUS ')'           
                 MOVE WS-KD15-STATUS          TO RETURN-CODE                    
                 GO TO 9000-CLOSING-ROUTINE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8006-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CD51'.                                
           DISPLAY 'CURRENT DATE   : ' WS-CURRENT-DATE.                         
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13FD45 READ     : ' WS-FD45-READ.                  
           DISPLAY 'NO. OF BM06F120 READ     : ' WS-F120-READ.                  
           DISPLAY 'NO. OF MATCHED           : ' WS-MATCH-REGN.                 
           DISPLAY 'NO. OF NOT MATCHED(F120) : ' WS-UNMATCH-F120.               
           DISPLAY 'NO. OF NOT MATCHED(FD45) : ' WS-UNMATCH-FD45.               
           DISPLAY 'NO. OF BP13FOUT  WRITTEN : ' CNT-FOUT.                      
           DISPLAY 'NO. OF INVALID PROJECT   : ' WS-PRJCT-NOTFND.               
           DISPLAY 'NO. OF BP13KD85 CREATED  : ' WS-KD85-WRITE.                 
           DISPLAY 'NO. OF BP13KD85 UPDATED  : ' WS-KD85-REWRITE.               
                                                                                
           CLOSE BM06F120                                                       
                 BP13FD45                                                       
                 SY02F001                                                       
                 BP13FOUT                                                       
                 BP13KD15                                                       
                 BP13KD00                                                       
                 BP13KD03                                                       
                 BP13KD85.                                                      
                                                                                
           IF WS-KD15-STATUS NOT = 00                                           
              DISPLAY 'BP13KD15 - CLOSE ERROR (' WS-KD15-STATUS ')'             
           END-IF.                                                              
                                                                                
           IF WS-KD00-STATUS NOT = 00                                           
              DISPLAY 'BP13KD00 - CLOSE ERROR (' WS-KD00-STATUS ')'             
           END-IF.                                                              
                                                                                
           IF WS-KD03-STATUS NOT = 00                                           
              DISPLAY 'BP13KD03 - CLOSE ERROR (' WS-KD03-STATUS ')'             
           END-IF.                                                              
                                                                                
           IF WS-KD85-STATUS NOT = 00                                           
              DISPLAY 'BP13KD85 - CLOSE ERROR (' WS-KD85-STATUS ')'             
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
