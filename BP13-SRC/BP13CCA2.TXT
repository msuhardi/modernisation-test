       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CCA2.                                                 
      *AUTHOR.        BO JIANG.                                                 
      *DATE-WRITTEN.  22/08/08.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   : REMINDER LETTER ATTACHED - ONE TIME         *            
      *                                                            *            
      *  INPUT FILES : 1.  BP13F800  -                             *            
      *                2.  BP13K820  -                             *            
      *                3.  BP13K830  -                             *            
      *                6.  BM06F120  -                             *            
      *                                                            *            
      *  LETTERS     : 1.  BP13PCA2  -                             *            
      *  REPORT      : 1.  BP13LCA2  -                             *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE    DESCRIPTION                         *            
      * -------- ---- -------- -----------                         *            
      * BP13     JB8  20081008 NEW LETTER.                         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
           SELECT BM06F120  ASSIGN        TO BM06F120.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13K830  ASSIGN        TO BP13K830                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K830-KEY-FLD                       
                            ALTERNATE KEY IS K830-NUM-REGN                      
                            FILE STATUS   IS WS-K830-STATUS.                    
                                                                                
           SELECT BP13K024  ASSIGN        TO BP13K024                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K024-KEY-FLD                       
                            FILE STATUS   IS WS-K024-STATUS.                    
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS WS-K022-STATUS.                    
                                                                                
           SELECT BP13PCA2  ASSIGN        TO BP13PCA2.                          
           SELECT BP13LCA2  ASSIGN        TO BP13LCA2.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BM06F120                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 700 CHARACTERS.                                     
       COPY BM06F120.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD  BP13LCA2                                                             
           RECORDING MODE  IS  F.                                               
       01  LCA2-PRTREC                      PIC X(132).                         
                                                                                
       FD  BP13PCA2                                                             
           RECORDING MODE  IS  F.                                               
       01  BP13PCA2-PRTREC                  PIC X(110).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K820-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K830-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K022-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K024-STATUS               PIC 99    VALUE ZEROS.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-F120-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-PCA2-WRT-CTR              PIC 9(06)  VALUE ZEROS.             
           05  WS-CNT-PAGE-LCA2             PIC 9(04)  VALUE ZEROS.             
           05  WS-CNT-LINE-LCA2             PIC 9(06)  VALUE ZEROS.             
           05  WS-CNT-CUR-LINE              PIC 9(02)  VALUE 56.                
           05  WS-CTR                       PIC 9(03)  VALUE ZEROS.             
           05  WS-CNT                       PIC 9(03)  VALUE ZEROS.             
           05  WS-CNT2                      PIC 9(03)  VALUE ZEROS.             
           05  WS-SKIP                      PIC 9(06)  VALUE ZEROS.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F800-EOF                  PIC X     VALUE 'N'.                
           05  WS-F800-FND                  PIC X     VALUE 'N'.                
           05  WS-F120-FND                  PIC X     VALUE 'N'.                
           05  WS-K820-NME-OCCP             PIC X(66) VALUE SPACES.             
           05  WS-K820-NME-HA1              PIC X(66) VALUE SPACES.             
           05  WS-F001-EOF                  PIC X(1)  VALUE 'N'.                
               88 EOF-F001                            VALUE 'Y'.                
           05  WS-F120-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-RETCODE                   PIC S9(8)                           
                                            SIGN IS LEADING SEPARATE.           
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE.                                                         
               10  WS-CCYY                  PIC 9(4)  VALUE 0000.               
               10  WS-MM                    PIC 99    VALUE 00.                 
               10  WS-DD                    PIC 99    VALUE 00.                 
           05  WS-CURR-DATE                 PIC 9(8)  VALUE ZEROES.             
                                                                                
       01  WS-FORM-VARIABLES.                                                   
           05  WS-NUM-REGN                  PIC X(08)    VALUE SPACES.          
           05  WS-NUM-DATE                  PIC X(10)    VALUE SPACES.          
           05  WS-NAME-HA1                  PIC X(66)    VALUE SPACES.          
           05  WS-NRIC-HA1                  PIC X(09)    VALUE SPACES.          
           05  WS-CPF-HA1                   PIC X(09)    VALUE SPACES.          
           05  WS-NAME-HA2                  PIC X(66)    VALUE SPACES.          
           05  WS-NRIC-HA2                  PIC X(09)    VALUE SPACES.          
           05  WS-CPF-HA2                   PIC X(09)    VALUE SPACES.          
           05  WS-NAME-HA3                  PIC X(66)    VALUE SPACES.          
           05  WS-NRIC-HA3                  PIC X(09)    VALUE SPACES.          
           05  WS-CPF-HA3                   PIC X(09)    VALUE SPACES.          
           05  WS-NAME-HA4                  PIC X(66)    VALUE SPACES.          
           05  WS-NRIC-HA4                  PIC X(09)    VALUE SPACES.          
           05  WS-CPF-HA4                   PIC X(09)    VALUE SPACES.          
           05  WS-K830-BLK                  PIC X(10)    VALUE SPACES.          
           05  WS-K830-FLAT                 PIC X(11)    VALUE SPACES.          
           05  WS-K830-ADDR-L2              PIC X(47)    VALUE SPACES.          
           05  WS-K830-ADDR-L3              PIC X(20)    VALUE SPACES.          
           05  WS-BAR-CODE                  PIC X(09)    VALUE SPACES.          
           05  WS-PIDB-STREET               PIC X(32)    VALUE SPACES.          
           05  WS-PIDB-ADDRESS              PIC X(51)    VALUE SPACES.          
           05  WS-PIDB-BLK                  PIC X(05)    VALUE SPACES.          
           05  WS-PIDB-LEVEL                PIC X(02)    VALUE SPACES.          
           05  WS-PIDB-UNIT                 PIC X(04)    VALUE SPACES.          
           05  WS-AMT-PREMIUM               PIC 9(03)V99 VALUE ZEROES.          
           05  WS-AMT-INSURED               PIC 9(06)V99 VALUE ZEROES.          
           05  WS-AMT-UNKNOWN               PIC 9(03)V99 VALUE ZEROES.          
           05  WS-NUM-DID                   PIC X(08)    VALUE SPACES.          
           05  WS-NME-OIC                   PIC X(30)    VALUE SPACES.          
           05  WS-CDE-MODL-DESG             PIC X(02)    VALUE SPACES.          
           05  WS-CDE-TYP-ACTLUSE           PIC X(02)    VALUE SPACES.          
                                                                                
      *-----------------------------------------------------------*             
      * COMMAREA FOR CALLING AB20C900                                           
      *-----------------------------------------------------------*             
       COPY AB20M97C.                                                           
      *-----------------------------------------------------------*             
      * BP13C913 PARAMETERS                                                     
      *-----------------------------------------------------------*             
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
       COPY P13COMM8.                                                           
      *-----------------------------------------------------------*             
      *   LINKAGE SECTION FOR AV02C015                                          
      *-----------------------------------------------------------*             
       01  WS-LINK-AV02C015.                                                    
           05  AV02C015-IN-POSTCODE             PIC X(6).                       
           05  AV02C015-OUT-POSTCODE-CHKSUM     PIC X(9).                       
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR LETTER BP13IA                           *            
      *------------------------------------------------------------*            
       01  PCA2A-LINE01.                                                        
           05  FILLER                   PIC X(72) VALUE SPACES.                 
           05  PCA2A-NUM-REGN           PIC X(08) VALUE SPACES.                 
                                                                                
       01  PCA2A-LINE02.                                                        
           05  FILLER                   PIC X(72) VALUE SPACES.                 
           05  PCA2A-DATE               PIC X(10) VALUE SPACES.                 
                                                                                
       01  PCA2A-LINE03.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PCA2A-NAME               PIC X(66) VALUE SPACES.                 
                                                                                
       01  PCA2A-LINE04.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PCA2A-NAME2              PIC X(66) VALUE SPACES.                 
                                                                                
       01  PCA2A-LINE05.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PCA2A-BLK                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  PCA2A-UNIT               PIC X(11) VALUE SPACES.                 
                                                                                
       01  PCA2A-LINE06.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PCA2A-ADDR-L2            PIC X(47) VALUE SPACES.                 
                                                                                
       01  PCA2A-LINE07.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PCA2A-ADDR-L3            PIC X(20) VALUE SPACES.                 
                                                                                
       01  PCA2A-LINE08.                                                        
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  PCA2A-BAR-CODE           PIC X(09) VALUE SPACES.                 
                                                                                
       01  PCA2A-LINE09.                                                        
           05  FILLER                   PIC X(49) VALUE SPACES.                 
           05  PCA2A-PIDB-ADDR          PIC X(50) VALUE SPACES.                 
                                                                                
       01  PCA2A-LINE10.                                                        
           05  FILLER                   PIC X(74) VALUE SPACES.                 
           05  PCA2A-AMT-INSURED        PIC ZZZ,ZZ9.99.                         
                                                                                
       01  PCA2A-LINE11.                                                        
           05  FILLER                   PIC X(22) VALUE SPACES.                 
           05  PCA2A-AMT-PREMIUM        PIC ZZ9.99.                             
                                                                                
       01  PCA2A-LINE12.                                                        
           05  FILLER                   PIC X(27) VALUE SPACES.                 
           05  PCA2A-AMT-PREMIUM2       PIC ZZ9.99.                             
                                                                                
       01  PCA2A-LINE13.                                                        
           05  FILLER                   PIC X(61) VALUE SPACES.                 
           05  PCA2A-NUM-DID            PIC X(08) VALUE SPACES.                 
                                                                                
       01  PCA2A-LINE14.                                                        
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  PCA2A-NME-OIC            PIC X(30) VALUE SPACES.                 
                                                                                
       01  PCA2A-BLANK-LINE             PIC X(100) VALUE SPACES.                
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  LCA2-HDG1.                                                           
           05  LCA2-LISTING             PIC X(14) VALUE 'BP13LCA2'.             
           05  FILLER                   PIC X(8)  VALUE 'HDB3'.                 
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  LCA2-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE ' PAGE: '.              
           05  LCA2-PAGE                PIC ZZZZ9.                              
                                                                                
       01  LCA2-HDG2.                                                           
           05  FILLER                   PIC X(49) VALUE SPACES.                 
           05  FILLER                   PIC X(33) VALUE                         
               'CONTROL LIST FOR REMINDER LETTERS'.                             
                                                                                
       01  LCA2-HDG3.                                                           
           05  FILLER                   PIC X(5)  VALUE ' S/NO'.                
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE 'REG NO  '.             
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(5)  VALUE 'BLK  '.                
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(32) VALUE                         
                                                  'STREET            '.         
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(08) VALUE 'HSE NO'.               
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(58) VALUE                         
                                                  'NAME (TRANCATED)'.           
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC'.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
                                                                                
       01  LCA2-HDG4.                                                           
           05  FILLER                   PIC X(5)  VALUE ALL '-'.                
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE ALL '-'.                
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(5)  VALUE ALL '-'.                
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(32) VALUE ALL '-'.                
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(08) VALUE ALL '-'.                
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(58) VALUE ALL '-'.                
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE ALL '-'.                
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
                                                                                
       01  LCA2-DTL.                                                            
           05  LCA2-SNO                 PIC ZZZZ9 VALUE ZEROES.                 
           05  FILLER                   PIC X(1).                               
           05  LCA2-NUM-REGN            PIC X(08) VALUE SPACES.                 
           05  FILLER                   PIC X(1).                               
           05  LCA2-NUM-BLK             PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(1).                               
           05  LCA2-NME-STREET          PIC X(32) VALUE SPACES.                 
           05  FILLER                   PIC X(1).                               
           05  LCA2-NUM-HSE-NO          PIC X(08) VALUE SPACES.                 
           05  FILLER                   PIC X(1).                               
           05  LCA2-HA1-NAME            PIC X(58) VALUE SPACES.                 
           05  FILLER                   PIC X(1).                               
           05  LCA2-HA1-NRIC            PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(1).                               
                                                                                
      *=============================================================            
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 3000-PROCESS-MATCH          THRU 3000-EXIT                   
             UNTIL WS-F800-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-FILES.                                                         
      *=============================================================            
           OPEN INPUT BP13F800                                                  
                      SY02F001                                                  
                      BP13K820                                                  
                      BM06F120                                                  
                      BP13K830                                                  
                      BP13K022                                                  
                      BP13K024                                                  
               OUTPUT BP13PCA2                                                  
                      BP13LCA2.                                                 
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K820 STATUS ' WS-K820-STATUS               
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K830 STATUS ' WS-K830-STATUS               
              MOVE     WS-K830-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K022 STATUS ' WS-K022-STATUS               
              MOVE     WS-K022-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K024 STATUS ' WS-K024-STATUS               
              MOVE     WS-K024-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              GO TO 9000-CLOSE-ROUTINE                                          
           ELSE                                                                 
              MOVE F001-DTE-CURRENT      TO   WS-DATE WS-CURR-DATE              
           END-IF.                                                              
           STRING WS-DD '/'                                                     
                  WS-MM '/'                                                     
                  WS-CCYY DELIMITED BY SIZE                                     
                  INTO WS-NUM-DATE.                                             
                                                                                
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
           PERFORM 2500-READ-BM06F120  THRU 2500-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2000-READ-BP13F800.                                                      
      *=============================================================            
                                                                                
            READ BP13F800 AT END                                                
                 MOVE 'Y'         TO WS-F800-EOF                                
                 MOVE HIGH-VALUES TO F800-NUM-REGN                              
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD  1  TO WS-F800-READ-CTR.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2500-READ-BM06F120.                                                      
      *=============================================================            
                                                                                
            READ BM06F120 AT END                                                
                 MOVE HIGH-VALUES TO F120-REGN-NO                               
                 GO TO 2500-EXIT.                                               
                                                                                
            ADD  1  TO WS-F120-READ-CTR.                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3000-PROCESS-MATCH.                                                      
      *=============================================================            
                                                                                
           IF F120-REGN-NO = F800-NUM-REGN                                      
              MOVE 'Y'                             TO WS-F120-FND               
              PERFORM 3500-PROCESS-RECORDS       THRU 3500-EXIT                 
              PERFORM 2000-READ-BP13F800         THRU 2000-EXIT                 
              PERFORM 2500-READ-BM06F120         THRU 2500-EXIT                 
           ELSE                                                                 
              IF F120-REGN-NO < F800-NUM-REGN                                   
                 PERFORM 2500-READ-BM06F120      THRU 2500-EXIT                 
              ELSE                                                              
                 MOVE 'N'                          TO WS-F120-FND               
                 PERFORM 3500-PROCESS-RECORDS    THRU 3500-EXIT                 
                 PERFORM 2000-READ-BP13F800      THRU 2000-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3500-PROCESS-RECORDS.                                                    
      *=============================================================            
                                                                                
           INITIALIZE     PCA2A-LINE01 PCA2A-LINE02 PCA2A-LINE03                
                          PCA2A-LINE04 PCA2A-LINE05 PCA2A-LINE06                
                          PCA2A-LINE07 PCA2A-LINE08 PCA2A-LINE09                
                          PCA2A-LINE10 PCA2A-LINE11 PCA2A-LINE12                
                          PCA2A-LINE13 PCA2A-LINE14.                            
                                                                                
           MOVE F800-NUM-REGN            TO WS-NUM-REGN                         
      *GET NAME OF HA1                                                          
           MOVE F800-NUM-REGN            TO K820-NUM-REGN                       
           MOVE F800-NUM-NRIC1           TO K820-NUM-NRIC WS-NRIC-HA1.          
           PERFORM 4600-READ-K820      THRU 4600-EXIT                           
           MOVE WS-K820-NME-OCCP         TO WS-NAME-HA1                         
           MOVE K820-NUM-CPF             TO WS-CPF-HA1.                         
      *GET NAME OF HA2                                                          
           IF F800-NUM-NRIC2  = SPACES OR LOW-VALUES                            
              MOVE SPACES                TO WS-NAME-HA2   WS-NRIC-HA2           
           ELSE                                                                 
              MOVE F800-NUM-NRIC2        TO K820-NUM-NRIC WS-NRIC-HA2           
              PERFORM 4600-READ-K820   THRU 4600-EXIT                           
              MOVE WS-K820-NME-OCCP      TO WS-NAME-HA2                         
              MOVE K820-NUM-CPF          TO WS-CPF-HA2                          
           END-IF.                                                              
      *GET NAME OF HA3                                                          
           IF F800-NUM-NRIC3  = SPACES OR LOW-VALUES                            
              MOVE SPACES                TO WS-NAME-HA3   WS-NRIC-HA3           
           ELSE                                                                 
              MOVE F800-NUM-NRIC3        TO K820-NUM-NRIC WS-NRIC-HA3           
              PERFORM 4600-READ-K820   THRU 4600-EXIT                           
              MOVE WS-K820-NME-OCCP      TO WS-NAME-HA3                         
              MOVE K820-NUM-CPF          TO WS-CPF-HA3                          
           END-IF.                                                              
      *GET NAME OF HA4                                                          
           IF F800-NUM-NRIC4  = SPACES OR LOW-VALUES                            
              MOVE SPACES                TO WS-NAME-HA4   WS-NRIC-HA4           
           ELSE                                                                 
              MOVE F800-NUM-NRIC4        TO K820-NUM-NRIC WS-NRIC-HA4           
              PERFORM 4600-READ-K820   THRU 4600-EXIT                           
              MOVE WS-K820-NME-OCCP      TO WS-NAME-HA4                         
              MOVE K820-NUM-CPF          TO WS-CPF-HA4                          
           END-IF.                                                              
                                                                                
           PERFORM 4800-GET-ADDR          THRU 4800-EXIT.                       
           PERFORM 6000-READ-PIDB         THRU 6000-EXIT.                       
           PERFORM 6600-GET-AMT-PREMIUM   THRU 6600-EXIT.                       
           PERFORM 6700-GET-CSM           THRU 6700-EXIT.                       
                                                                                
           PERFORM 7000-PRT-LETTER        THRU 7000-EXIT                        
           PERFORM 8000-PRN-CNTRL-LIST    THRU 8000-EXIT.                       
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4600-READ-K820.                                                          
      *=============================================================            
           MOVE    SPACES                TO  WS-K820-NME-OCCP                   
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00 OR 02                                         
              MOVE    K820-NME-OCCP      TO  WS-K820-NME-OCCP                   
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              DISPLAY 'K820 REC NOT FOUND : '   K820-KEY-FLD                    
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820 = '   WS-K820-STATUS              
                      ', K820-KEY-FLD = '   K820-KEY-FLD                        
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                      
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4800-GET-ADDR.                                                           
      *=============================================================            
                                                                                
           MOVE F800-NUM-REGN TO K830-NUM-REGN                                  
           READ BP13K830  KEY IS K830-NUM-REGN.                                 
                                                                                
           IF WS-K830-STATUS = 00                                               
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO WS-K830-BLK                       
                 IF K830-NUM-LVL-NUM = SPACES AND                               
                    K830-NUM-HSEMAIN = SPACES                                   
                    MOVE SPACES            TO WS-K830-FLAT                      
                 ELSE                                                           
                    MOVE K830-NUM-FLAT     TO WS-K830-FLAT                      
                 END-IF                                                         
                 MOVE K830-NME-STREET-BLDG(1:47) TO WS-K830-ADDR-L2             
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO WS-K830-ADDR-L3                   
                    DISPLAY ' RES POSTAL CDE IS ' K830-NUM-POSTAL               
                            ' FOR REC KEY-FLD ' K830-KEY-FLD                    
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE INTO WS-K830-ADDR-L3                   
                 END-IF                                                         
      *------ TO GET THE BAR CODE ------                                        
                 MOVE SPACES                    TO WS-LINK-AV02C015             
                 MOVE K830-NUM-POSTAL           TO AV02C015-IN-POSTCODE         
                 PERFORM 4850-CALL-AV02C015     THRU 4850-EXIT                  
                 MOVE AV02C015-OUT-POSTCODE-CHKSUM TO WS-BAR-CODE               
                                                                                
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO WS-K830-BLK                   
                 IF K830-NUM-MAIL-LVL-NUM = SPACES AND                          
                    K830-NUM-MAIL-HSEMAIN = SPACES                              
                    MOVE SPACES                TO WS-K830-FLAT                  
                 ELSE                                                           
                    MOVE K830-NUM-MAIL-FLAT    TO WS-K830-FLAT                  
                 END-IF                                                         
                 MOVE K830-NME-MAIL-STR-BLDG(1:47) TO WS-K830-ADDR-L2           
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO WS-K830-ADDR-L3               
                    DISPLAY ' MAIL POSTAL CDE IS ' K830-NUM-POSTAL              
                            ' FOR REC KEY-FLD ' K830-KEY-FLD                    
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO WS-K830-ADDR-L3               
                 END-IF                                                         
      *------ TO GET THE BAR CODE ------                                        
                 MOVE SPACES                    TO WS-LINK-AV02C015             
                 MOVE K830-NUM-MAIL-POSTAL      TO AV02C015-IN-POSTCODE         
                 PERFORM 4850-CALL-AV02C015     THRU 4850-EXIT                  
                 MOVE AV02C015-OUT-POSTCODE-CHKSUM TO WS-BAR-CODE               
              END-IF                                                            
                                                                                
           ELSE                                                                 
           IF WS-K830-STATUS = 23                                               
              MOVE SPACES    TO WS-K830-BLK      WS-K830-FLAT                   
                                WS-K830-ADDR-L2  WS-K830-ADDR-L3                
                                WS-BAR-CODE                                     
              DISPLAY 'BP13K830 RECORD NOT FOUND = ' WS-K830-STATUS             
                      ',  K830-KEY-FLD IS ' K830-NUM-REGN                       
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS                
                      ',  K830-KEY-FLD IS ' K830-NUM-REGN                       
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4850-CALL-AV02C015.                                                      
      *=============================================================            
                                                                                
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       4850-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-READ-PIDB.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                         TO WS-PIDB-ADDRESS               
                                                  WS-PIDB-BLK                   
                                                  WS-PIDB-LEVEL                 
                                                  WS-PIDB-UNIT                  
                                                  WS-CDE-TYP-ACTLUSE            
                                                  WS-CDE-MODL-DESG.             
           IF WS-F120-FND NOT = 'Y'                                             
              GO TO 6000-EXIT                                                   
           END-IF.                                                              
                                                                                
           STRING 'BLK ' F120-BLK-NO                                            
                  ' #' F120-LEVEL-NO  '-' F120-MAIN-UNIT                        
                  ' ' F120-STREET-NAME(1:30)                                    
              DELIMITED BY SIZE INTO WS-PIDB-ADDRESS                            
           MOVE F120-BLK-NO                   TO WS-PIDB-BLK                    
           MOVE F120-LEVEL-NO                 TO WS-PIDB-LEVEL                  
           MOVE F120-MAIN-UNIT                TO WS-PIDB-UNIT                   
           MOVE F120-STREET-NAME              TO WS-PIDB-STREET.                
           MOVE F120-CDE-MODL-DESG            TO WS-CDE-MODL-DESG.              
           MOVE F120-CDE-TYP-ACTUSE           TO WS-CDE-TYP-ACTLUSE.            
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       6600-GET-AMT-PREMIUM.                                                    
      *=============================================================            
                                                                                
           MOVE WS-CDE-TYP-ACTLUSE      TO  BM97-NUM-FLAT-TYPE.                 
           MOVE WS-CDE-MODL-DESG        TO  BM97-CDE-MODL-DESG.                 
           MOVE WS-CURR-DATE            TO  BM97-DTE-COMPARE.                   
                                                                                
           INITIALIZE                       BM97-NUM-ERROR                      
                                            BM97-NUM-SYSERR.                    
                                                                                
           CALL 'AB20C900' USING WS-BM97-AREA.                                  
                                                                                
           IF (BM97-NUM-ERROR = ZEROS OR SPACES) AND                            
              BM97-NUM-SYSERR = ZEROS                                           
              MOVE BM97-AMT-INSURED     TO  WS-AMT-INSURED                      
              MOVE BM97-AMT-PREMIUM     TO  WS-AMT-PREMIUM                      
           ELSE                                                                 
              MOVE BM97-NUM-SYSERR      TO  RETURN-CODE                         
              MOVE ZEROES               TO  WS-AMT-INSURED                      
              MOVE ZEROES               TO  WS-AMT-PREMIUM                      
              DISPLAY 'ERROR IN CALLING AB20C900, ' BM97-NUM-ERROR              
           END-IF.                                                              
                                                                                
       6600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       6700-GET-CSM.                                                            
      *=============================================================            
                                                                                
           MOVE SPACES                         TO BP13K022-REC                  
                                                  BP13K024-REC                  
                                                  WS-NUM-DID                    
                                                  WS-NME-OIC.                   
           INITIALIZE                             BP13K022-REC                  
                                                  BP13K024-REC.                 
      * GET NUM-OIC                                                             
           MOVE F800-NUM-REGN                  TO K022-NUM-REGN.                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
           IF WS-K022-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K022-STATUS = 23                                            
                 GO TO 6700-EXIT                                                
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K022 ,RC= ' WS-K022-STATUS          
                 MOVE WS-K022-STATUS           TO RETURN-CODE                   
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
      * GET NUM-DID , NME-OIC                                                   
           MOVE K022-NUM-OIC                   TO K024-NUM-OIC                  
           READ BP13K024.                                                       
           IF WS-K024-STATUS = 00 OR 02                                         
              MOVE K024-NUM-DID                TO WS-NUM-DID                    
              MOVE K024-NME-OIC                TO WS-NME-OIC                    
           ELSE                                                                 
              IF WS-K024-STATUS = 23                                            
                 CONTINUE                                                       
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K024 ,RC= ' WS-K024-STATUS             
                 MOVE WS-K024-STATUS           TO RETURN-CODE                   
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       7000-PRT-LETTER.                                                         
      *=============================================================            
                                                                                
            PERFORM 7500-MOVE-VALUES   THRU 7500-EXIT.                          
            ADD   1               TO    WS-PCA2-WRT-CTR.                        
                                                                                
            WRITE BP13PCA2-PRTREC FROM  PCA2A-BLANK-LINE AFTER PAGE.            
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE01.                           
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE02.                           
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE03 AFTER 4.                   
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE04.                           
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE05.                           
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE06.                           
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE07.                           
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE08 AFTER 4.                   
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE09 AFTER 6.                   
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE10 AFTER 7.                   
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE11.                           
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE12 AFTER 4.                   
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE13 AFTER 3.                   
            WRITE BP13PCA2-PRTREC FROM  PCA2A-LINE14 AFTER 14.                  
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       7500-MOVE-VALUES.                                                        
      *=============================================================            
                                                                                
           MOVE WS-NUM-REGN          TO PCA2A-NUM-REGN                          
           MOVE WS-NUM-DATE          TO PCA2A-DATE                              
           MOVE WS-NAME-HA1          TO PCA2A-NAME                              
           MOVE WS-NAME-HA2          TO PCA2A-NAME2                             
           MOVE WS-K830-BLK          TO PCA2A-BLK                               
           MOVE WS-K830-FLAT         TO PCA2A-UNIT                              
           MOVE WS-K830-ADDR-L2      TO PCA2A-ADDR-L2                           
           MOVE WS-K830-ADDR-L3      TO PCA2A-ADDR-L3                           
           MOVE WS-BAR-CODE          TO PCA2A-BAR-CODE                          
           MOVE WS-PIDB-ADDRESS      TO PCA2A-PIDB-ADDR                         
           MOVE WS-AMT-INSURED       TO PCA2A-AMT-INSURED                       
           MOVE WS-AMT-PREMIUM       TO PCA2A-AMT-PREMIUM                       
                                        PCA2A-AMT-PREMIUM2                      
           MOVE WS-NUM-DID           TO PCA2A-NUM-DID.                          
           MOVE WS-NME-OIC           TO PCA2A-NME-OIC.                          
                                                                                
       7500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=============================================================            
       8000-PRN-CNTRL-LIST.                                                     
      *=============================================================            
                                                                                
           INITIALIZE LCA2-DTL.                                                 
                                                                                
           MOVE WS-NUM-REGN               TO LCA2-NUM-REGN                      
           MOVE WS-PIDB-BLK               TO LCA2-NUM-BLK                       
           MOVE WS-PIDB-STREET            TO LCA2-NME-STREET                    
           STRING '#' WS-PIDB-LEVEL '-' WS-PIDB-UNIT                            
              DELIMITED BY SIZE INTO LCA2-NUM-HSE-NO                            
           MOVE WS-NAME-HA1               TO LCA2-HA1-NAME                      
           MOVE WS-NRIC-HA1               TO LCA2-HA1-NRIC                      
                                                                                
           ADD 1                          TO WS-CNT-LINE-LCA2.                  
           ADD 1                          TO WS-CNT-CUR-LINE.                   
           MOVE WS-CNT-LINE-LCA2          TO LCA2-SNO                           
           IF WS-CNT-CUR-LINE > 55                                              
              ADD 1                       TO WS-CNT-PAGE-LCA2                   
              MOVE WS-CNT-PAGE-LCA2       TO LCA2-PAGE                          
              MOVE WS-NUM-DATE            TO LCA2-DATE                          
              WRITE LCA2-PRTREC FROM LCA2-HDG1 AFTER PAGE                       
              WRITE LCA2-PRTREC FROM LCA2-HDG2 AFTER 2                          
              WRITE LCA2-PRTREC FROM LCA2-HDG3 AFTER 3                          
              WRITE LCA2-PRTREC FROM LCA2-HDG4                                  
              MOVE 7                      TO WS-CNT-CUR-LINE                    
           END-IF                                                               
                                                                                
           WRITE LCA2-PRTREC FROM LCA2-DTL.                                     
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       9000-CLOSE-ROUTINE.                                                      
      **************************************************************            
           DISPLAY ' '.                                                         
           DISPLAY ' ====  BP13CCA2 CONTROL TOTALS  ======'.                    
           DISPLAY ' '.                                                         
           DISPLAY ' F800 RECORDS READ          : ' WS-F800-READ-CTR.           
           DISPLAY ' NO OF CASES FOR BP13IJ     : ' WS-PCA2-WRT-CTR.            
           DISPLAY ' NO OF CASES WRITE          : ' WS-CNT-LINE-LCA2.           
           DISPLAY ' NO OF CASES FOUND IN F120  : ' WS-F120-READ-CTR.           
           DISPLAY ' '.                                                         
           DISPLAY ' ====================================='.                    
                                                                                
           CLOSE BP13F800                                                       
                 SY02F001                                                       
                 BM06F120                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13K022                                                       
                 BP13K024                                                       
                 BP13PCA2                                                       
                 BP13LCA2.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K820-STAT ' WS-K820-STATUS.            
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K830-STAT ' WS-K830-STATUS.            
                                                                                
           IF WS-K022-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K022-STAT ' WS-K022-STATUS.            
                                                                                
           IF WS-K024-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K024-STAT ' WS-K024-STATUS.            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
