       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C92G.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  08/05/2017.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *   OBJECTIVES  : UPDATE APPL DETAILS IN BP13K76A             *           
      *                                                             *           
      *   INPUT FILES : BP13F730 - RECORD TO BE PROCESSED           *           
      *                 BP13K730 - GET CHILD'S FLAT TYPE            *           
      *   I-O FILE    : BP13K76A - FILE TO BE UPDATED               *           
      *                                                             *           
      * =========================================================== *           
      * CHQ REQ   DATE      BY    DESCRIPTIONS                      *           
      * --------  --------  ----  --------------------------------- *           
      * BP136299  08052017  SMR2  NEW PROGRAM                       *           
      * BP136299  24052017  LSB1  ADD NEW FIELD, NUM-APPL           *           
      * BP136832  31052017  SMR2  CATER FOR MGPS-FT '3P'            *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13K730 ASSIGN       TO BP13K730                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K730-NUM-REGN                        
                           FILE STATUS  IS WS-K730-STATUS.                      
                                                                                
           SELECT BP13K76A ASSIGN       TO BP13K76A                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K76A-KEY-FLD                         
                           FILE STATUS  IS WS-K76A-STATUS.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   BP13K76A                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13K76A.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K730-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K76A-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(06)   VALUE ZEROES.            
           05  WS-K730-READ                PIC 9(06)   VALUE ZEROES.            
           05  WS-K76A-READ                PIC 9(06)   VALUE ZEROES.            
           05  WS-K76A-UPD                 PIC 9(06)   VALUE ZEROES.            
           05  WS-MGPS-PAIR                PIC 9(06)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-22         PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-23         PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-23P        PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-24         PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-25         PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-32         PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-33         PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-33P        PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-34         PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-35         PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-3P2        PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-3P3        PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-3P3P       PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-3P4        PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-MGPS-3P5        PIC 9(03)   VALUE ZEROES.            
           05  WS-NUM-APPL-TOTAL           PIC 9(04)   VALUE ZEROES.            
           05  WS-NUM-CANCEL               PIC 9(05)   VALUE ZEROES.            
           05  WS-NUM-BALLOTED             PIC 9(05)   VALUE ZEROES.            
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                     PIC X(08)   VALUE SPACES.            
           05  WS-DATE-EDIT                PIC X(10)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F730-KEY-FLD.                                                 
               10  WS-F730-CDE-NT           PIC X(03).                          
               10  WS-F730-CDE-FT           PIC X(02).                          
               10  WS-F730-ALLO-CAT         PIC X(03).                          
               10  WS-F730-DTE-BAL          PIC X(06).                          
           05  WS-PREV-KEY-FLD.                                                 
               10  WS-PREV-CDE-NT           PIC X(03).                          
               10  WS-PREV-CDE-FT           PIC X(02).                          
               10  WS-PREV-ALLO-CAT         PIC X(03).                          
               10  WS-PREV-DTE-BAL          PIC X(06).                          
           05  WS-MGPS-FT-PAIR.                                                 
               10  WS-MGPS-PARENT          PIC X(02)   VALUE SPACES.            
               10  WS-MGPS-CHILD           PIC X(02)   VALUE SPACES.            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K730                                                 
                I-O    BP13K76A.                                                
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K730 ERROR ' WS-K730-STATUS                  
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K76A-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K76A ERROR ' WS-K76A-STATUS                  
              MOVE WS-K76A-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                         
           STRING WS-DATE(1:4) '-' WS-DATE(5:2) '-' WS-DATE(7:2)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13F730.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                    TO WS-F730-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F730-READ.                    
                                                                                
           MOVE F730-CDE-NT1                TO WS-F730-CDE-NT.                  
           MOVE F730-CDE-FLAT-TYPE          TO WS-F730-CDE-FT.                  
           MOVE F730-NUM-ALLO-CAT           TO WS-F730-ALLO-CAT.                
           MOVE F730-DTE-BALLOT             TO WS-F730-DTE-BAL.                 
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       3000-PROCESS-RECS.                                                       
      *------------------*                                                      
                                                                                
           MOVE F730-CDE-NT1             TO WS-PREV-CDE-NT.                     
           MOVE F730-CDE-FLAT-TYPE       TO WS-PREV-CDE-FT.                     
           MOVE F730-NUM-ALLO-CAT        TO WS-PREV-ALLO-CAT.                   
           MOVE F730-DTE-BALLOT          TO WS-PREV-DTE-BAL.                    
                                                                                
           MOVE ZEROES                   TO WS-NUM-APPL-MGPS-22                 
                                            WS-NUM-APPL-MGPS-23                 
                                            WS-NUM-APPL-MGPS-23P                
                                            WS-NUM-APPL-MGPS-24                 
                                            WS-NUM-APPL-MGPS-25                 
                                            WS-NUM-APPL-MGPS-32                 
                                            WS-NUM-APPL-MGPS-33                 
                                            WS-NUM-APPL-MGPS-33P                
                                            WS-NUM-APPL-MGPS-34                 
                                            WS-NUM-APPL-MGPS-35                 
                                            WS-NUM-APPL-MGPS-3P2                
                                            WS-NUM-APPL-MGPS-3P3                
                                            WS-NUM-APPL-MGPS-3P3P               
                                            WS-NUM-APPL-MGPS-3P4                
                                            WS-NUM-APPL-MGPS-3P5                
                                            WS-NUM-APPL-TOTAL                   
                                            WS-NUM-CANCEL                       
                                            WS-NUM-BALLOTED.                    
           MOVE SPACES                   TO WS-MGPS-PARENT.                     
           MOVE SPACES                   TO WS-MGPS-CHILD.                      
                                                                                
           PERFORM 3500-COUNT-APPLICATION THRU 3500-EXIT                        
             UNTIL WS-F730-KEY-FLD > WS-PREV-KEY-FLD                            
                OR WS-F730-EOF = 'Y'.                                           
                                                                                
           PERFORM 5000-READ-BP13K76A    THRU 5000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       3500-COUNT-APPLICATION.                                                  
      *-----------------------*                                                 
                                                                                
           ADD 1                      TO WS-NUM-APPL-TOTAL.                     
           IF F730-CDE-REQUEST-STATUS NOT = 'C'                                 
              ADD 1                      TO WS-NUM-BALLOTED                     
           END-IF.                                                              
                                                                                
           IF F730-CDE-REQUEST-STATUS = 'C'                                     
              ADD 1                      TO WS-NUM-CANCEL                       
           END-IF.                                                              
                                                                                
           IF F730-NUM-MGPS = 'P'                                               
              IF F730-CDE-FLAT-TYPE = '3P'                                      
                 MOVE F730-CDE-FLAT-TYPE    TO WS-MGPS-PARENT                   
              ELSE                                                              
                 MOVE F730-CDE-FLAT-TYPE(1:1)                                   
                                            TO WS-MGPS-PARENT(1:1)              
                 MOVE SPACES TO WS-MGPS-PARENT(2:1)                             
              END-IF                                                            
              ADD 1 TO WS-MGPS-PAIR                                             
              PERFORM 4000-READ-BP13K730 THRU 4000-EXIT                         
              EVALUATE WS-MGPS-FT-PAIR                                          
              WHEN '2 2 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-22                 
              WHEN '2 3 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-23                 
              WHEN '2 3P'                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-23P                
              WHEN '2 4 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-24                 
              WHEN '2 5 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-25                 
              WHEN '3 2 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-32                 
              WHEN '3 3 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-33                 
              WHEN '3 3P'                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-33P                
              WHEN '3 4 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-34                 
              WHEN '3 5 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-35                 
              WHEN '3P2 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-3P2                
              WHEN '3P3 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-3P3                
              WHEN '3P3P'                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-3P3P               
              WHEN '3P4 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-3P4                
              WHEN '3P5 '                                                       
                   ADD 1                 TO WS-NUM-APPL-MGPS-3P5                
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730    THRU 2000-EXIT.                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4000-READ-BP13K730.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K730-REC.                    
           INITIALIZE                          BP13K730-REC.                    
                                                                                
           MOVE F730-NUM-JB-REGN            TO K730-NUM-REGN.                   
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                       TO WS-K730-READ                     
                IF K730-CDE-FLAT-TYPE = '3P'                                    
                   MOVE K730-CDE-FLAT-TYPE  TO WS-MGPS-CHILD                    
                ELSE                                                            
                   MOVE K730-CDE-FLAT-TYPE(1:1)                                 
                                            TO WS-MGPS-CHILD(1:1)               
                   MOVE SPACES TO WS-MGPS-CHILD(2:1)                            
                END-IF                                                          
                                                                                
           WHEN 23                                                              
                DISPLAY 'REGNO NOT FOUND IN BP13K730 : ' K730-NUM-REGN          
                MOVE SPACES                 TO WS-MGPS-CHILD                    
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K730 : ' WS-K730-STATUS              
                MOVE WS-K730-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5000-READ-BP13K76A.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K76A-REC.                    
           INITIALIZE                          BP13K76A-REC.                    
                                                                                
           MOVE WS-PREV-ALLO-CAT            TO K76A-NUM-SALE-MODE-HIST.         
           MOVE WS-PREV-DTE-BAL             TO K76A-DTE-BALLOT-HIST.            
           MOVE WS-PREV-CDE-NT              TO K76A-NUM-CHOICE-HIST.            
           MOVE WS-PREV-CDE-FT              TO K76A-NUM-FT-HIST.                
                                                                                
           READ BP13K76A.                                                       
                                                                                
           EVALUATE WS-K76A-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                       TO WS-K76A-READ                     
                PERFORM 6000-REWRITE-BP13K76A  THRU 6000-EXIT                   
                                                                                
           WHEN 23                                                              
                DISPLAY 'KEY-FLD NOT FOUND IN BP13K76A : ' K76A-KEY-FLD         
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K76A : ' WS-K76A-STATUS              
                MOVE WS-K76A-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       6000-REWRITE-BP13K76A.                                                   
      *----------------------*                                                  
                                                                                
           MOVE WS-NUM-APPL-MGPS-22         TO K76A-NUM-APPL-MGPS-22.           
           MOVE WS-NUM-APPL-MGPS-23         TO K76A-NUM-APPL-MGPS-23.           
           MOVE WS-NUM-APPL-MGPS-23P        TO K76A-NUM-APPL-MGPS-23P.          
           MOVE WS-NUM-APPL-MGPS-24         TO K76A-NUM-APPL-MGPS-24.           
           MOVE WS-NUM-APPL-MGPS-25         TO K76A-NUM-APPL-MGPS-25.           
           MOVE WS-NUM-APPL-MGPS-32         TO K76A-NUM-APPL-MGPS-32.           
           MOVE WS-NUM-APPL-MGPS-33         TO K76A-NUM-APPL-MGPS-33.           
           MOVE WS-NUM-APPL-MGPS-33P        TO K76A-NUM-APPL-MGPS-33P.          
           MOVE WS-NUM-APPL-MGPS-34         TO K76A-NUM-APPL-MGPS-34.           
           MOVE WS-NUM-APPL-MGPS-35         TO K76A-NUM-APPL-MGPS-35.           
           MOVE WS-NUM-APPL-MGPS-3P2        TO K76A-NUM-APPL-MGPS-3P2.          
           MOVE WS-NUM-APPL-MGPS-3P3        TO K76A-NUM-APPL-MGPS-3P3.          
           MOVE WS-NUM-APPL-MGPS-3P3P       TO K76A-NUM-APPL-MGPS-3P3P.         
           MOVE WS-NUM-APPL-MGPS-3P4        TO K76A-NUM-APPL-MGPS-3P4.          
           MOVE WS-NUM-APPL-MGPS-3P5        TO K76A-NUM-APPL-MGPS-3P5.          
           MOVE WS-NUM-APPL-TOTAL           TO K76A-NUM-APPL.                   
           MOVE WS-NUM-CANCEL               TO K76A-NUM-CANCEL.                 
           MOVE WS-NUM-BALLOTED             TO K76A-NUM-BALLOTED.               
                                                                                
           REWRITE BP13K76A-REC.                                                
                                                                                
           EVALUATE WS-K76A-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                       TO WS-K76A-UPD                      
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR WRITING TO BP13K76A. STATUS IS - '               
                        WS-K76A-STATUS                                          
                MOVE WS-K76A-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '*=============================================*'.           
           DISPLAY '  BP13C92G CONTROL TOTAL       : ' WS-DATE-EDIT.            
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '  NO OF BP13F730 RECS READ     : ' WS-F730-READ.            
           DISPLAY '  NO OF BP13K730 RECS READ     : ' WS-K730-READ.            
           DISPLAY '  NO OF BP13K730 MGPS PAIR     : ' WS-MGPS-PAIR.            
           DISPLAY '  NO OF BP13K76A RECS READ     : ' WS-K76A-READ.            
           DISPLAY '  NO OF BP13K76A RECS UPDATED  : ' WS-K76A-UPD.             
           DISPLAY '*=============================================*'.           
                                                                                
           CLOSE BP13F730                                                       
                 BP13K730                                                       
                 BP13K76A.                                                      
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K730 ERROR ' WS-K730-STATUS                  
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K76A-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K76A ERROR ' WS-K76A-STATUS                  
              MOVE WS-K76A-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
