      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13CC31.                                                 
       AUTHOR.        ARIEL HERNANDEZ.                                          
       DATE-WRITTEN.  14/09/2006.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  TO CREATE B004F556 FILE, TO RECEIVE CPF       *        
      *                  PAYMENT FROM CPFB VIA SFTP.                   *        
      *                                                                *        
      *   INPUT FILES :  1.  SY02F001                                  *        
      *                  2.  BP13F128                                  *        
      *                                                                *        
      *   OUTPUT FILES:  1.  B004F556 - VALID RECORDS                  *        
      *                  2.  P13F128A - CONTAINS HEADER & TRAILER      *        
      *                                 WHEN TRAILER REC COUNT NOT =   *        
      *                                 ACTUAL NO OF DETAIL RECS       *        
      *                  3.  BP13LC31 - IF APPLIED NOT = DEDUCT        *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP132914  BA3  14/09/2006 NEW PROGRAM                        *        
      *   BP133052  BA3  18/05/2007 INCLUDE CONDITION FOR RECORD STATUS*        
      *   BP135093  IMC1 19/12/2013 CHANGE B04F556 TO AB02F556         *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
           SELECT BP13F128  ASSIGN        TO BP13F128.                          
           SELECT AB02F556  ASSIGN        TO AB02F556.                          
           SELECT P13F128A  ASSIGN        TO P13F128A.                          
           SELECT BP13LC31  ASSIGN        TO BP13LC31.                          
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
       COPY BP13F128.                                                           
       COPY AB02F556.                                                           
                                                                                
       FD  P13F128A                                                             
           BLOCK  CONTAINS 0  RECORDS                                           
           RECORD CONTAINS 280 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  F128A-REC                   PIC X(280).                              
                                                                                
       FD  BP13LC31                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE  IS  F                                                
           DATA RECORD     IS  PRINT-REC.                                       
       01  PRINT-REC                   PIC X(132).                              
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       77  WS-RETURN-CODE             PIC 9(2)     VALUE ZERO.                  
                                                                                
       01  WS-EOF-F128                 PIC X(01)   VALUE SPACES.                
       01  WS-REC-NOT-VALID            PIC X(01)   VALUE 'N'.                   
       01  WS-F128-DETAIL-CNT          PIC 9(05)   VALUE ZEROES.                
       01  WS-F128A-WRITTEN            PIC 9(05)   VALUE ZEROES.                
       01  WS-F128-READ                PIC 9(05)   VALUE ZEROES.                
       01  WS-F556-WRITE               PIC 9(05)   VALUE ZEROES.                
       01  WS-REV-INSTAL-CNT           PIC 9(05)   VALUE ZEROES.                
       01  WS-LC31-PAGE-CNT            PIC 9(05)   VALUE ZEROES.                
       01  WS-LC31-SN-CNT              PIC 9(05)   VALUE ZEROES.                
       01  WS-LC31-LINE-CNT            PIC 9(02)   VALUE 60.                    
       01  WS-F128-COUNT-ALPHA         PIC X(05)   VALUE SPACES.                
       01  REDEFINES WS-F128-COUNT-ALPHA.                                       
           05  WS-F128-COUNT           PIC 9(05).                               
                                                                                
       01  WS-JR-VR-NS.                                                         
           05  FILLER                  PIC X(2)    VALUE 'NS'.                  
           05  WS-JR-NUM-NS.                                                    
               10  WS-JR-NUM-DD-NS     PIC X(2).                                
               10  WS-JR-NUM-MM-NS     PIC X(2).                                
           05  FILLER                  PIC X       VALUE '/'.                   
           05  WS-JR-YY-NS             PIC X(2).                                
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CC              PIC 9(2).                                
           05  WS-CURR-YYMMDD.                                                  
               10  WS-CURR-YY          PIC 9(2).                                
               10  WS-CURR-MM          PIC 9(2).                                
               10  WS-CURR-DD          PIC 9(2).                                
                                                                                
       01  LC31-HEAD-1.                                                         
           03  FILLER                  PIC X(08)    VALUE 'BP13LC31'.           
           03  FILLER                  PIC X(38)    VALUE                       
               '      HDBCAT3'.                                                 
           03  FILLER                  PIC X(51)    VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           03  FILLER                  PIC X(7)     VALUE 'DATE : '.            
           03  LC31-SYS-DATE           PIC X(10)    VALUE SPACES.               
           03  FILLER                  PIC X(11)    VALUE                       
               '    PAGE : '.                                                   
           03  LC31-PAGE-NO            PIC ZZZ9     VALUE ZEROES.               
                                                                                
       01  LC31-HEAD-2.                                                         
           03  FILLER                  PIC X(42)     VALUE SPACES.              
           03  FILLER                  PIC X(48)     VALUE                      
               'REPORT ON RETURN OF CPF WITHDRAWAL VIA SFTP (PP)'.              
                                                                                
       01  LC31-HEAD-3.                                                         
           03  FILLER                  PIC X(05)     VALUE ' S/N '.             
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  FILLER                  PIC X(09)     VALUE                      
                                                     ' SERIAL  '.               
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  FILLER                  PIC X(09)     VALUE                      
                                                    '   CPF   '.                
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  FILLER                  PIC X(13)     VALUE                      
                                                    '   HDB-REF   '.            
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  FILLER                  PIC X(12)     VALUE                      
                                                    ' LUMPSUM-REQ'.             
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  FILLER                  PIC X(12)     VALUE                      
                                                    ' LUMPSUM-REC'.             
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  FILLER                  PIC X(12)     VALUE                      
                                                    '  STAMP-REQ '.             
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  FILLER                  PIC X(12)     VALUE                      
                                                    '  STAMP-REC '.             
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  FILLER                  PIC X(40)     VALUE                      
                          '            REMARKS                     '.           
                                                                                
       01  LC31-DETAIL.                                                         
           03  LC31-SN                 PIC ZZZZ9     VALUE ZEROES.              
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  LC31-SERIAL             PIC X(09)     VALUE SPACES.              
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  LC31-CPF                PIC X(09)     VALUE SPACES.              
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  LC31-HDB-REF            PIC X(13)     VALUE SPACES.              
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  LC31-LUMPSUM-APPLY      PIC $,$$$,$$9.99                         
                                                     VALUE ZEROES.              
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  LC31-LUMPSUM-DEDUCT     PIC $,$$$,$$9.99                         
                                                     VALUE ZEROES.              
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  LC31-STAMP-APPLY        PIC $,$$$,$$9.99                         
                                                     VALUE ZEROES.              
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  LC31-STAMP-DEDUCT       PIC $,$$$,$$9.99                         
                                                     VALUE ZEROES.              
           03  FILLER                  PIC X(01)     VALUE SPACES.              
           03  LC31-REMARKS            PIC X(40)     VALUE SPACES.              
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM  1000-OPEN-FILES        THRU    1000-EXIT.                   
           PERFORM  2500-READ-F128         THRU    2500-EXIT.                   
           PERFORM  8000-CNT-NO-OF-RECS    THRU    8000-EXIT                    
                    UNTIL WS-EOF-F128 = 'Y'.                                    
           PERFORM  8100-VALIDATE-REC      THRU    8100-EXIT.                   
           PERFORM  8200-REOPEN-F128       THRU    8200-EXIT.                   
                                                                                
           PERFORM  2500-READ-F128         THRU    2500-EXIT.                   
           PERFORM  3000-PROCESS           THRU    3000-EXIT                    
                    UNTIL WS-EOF-F128 = 'Y'.                                    
                                                                                
           PERFORM  9000-CLOSE-ROUTINE     THRU    9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  SY02F001                                                 
                       BP13F128                                                 
               OUTPUT  AB02F556                                                 
                       P13F128A                                                 
                       BP13LC31.                                                
                                                                                
           READ SY02F001.                                                       
                                                                                
           MOVE F001-DTE-CURRENT TO WS-CURR-DATE.                               
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
                  DELIMITED BY SIZE INTO LC31-SYS-DATE.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       2500-READ-F128.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F128 AT END                                                 
                        MOVE HIGH-VALUES TO F128-DETAIL-REC                     
                        MOVE 'Y'    TO WS-EOF-F128                              
                        GO TO 2500-EXIT                                         
           END-READ.                                                            
                                                                                
           ADD 1                    TO WS-F128-READ.                            
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           IF WS-REC-NOT-VALID = 'Y'                                            
              IF F128-HDR-REC(1:32)  = SPACES OR                                
                 F128-NUM-HIGH-VALUE = ALL '9'                                  
                 PERFORM 3200-WRITE-F128A  THRU 3200-EXIT                       
              END-IF                                                            
              PERFORM 2500-READ-F128       THRU 2500-EXIT                       
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F128-HDR-REC(1:32) = SPACES                                       
              PERFORM 3100-PROCESS-HDR     THRU 3100-EXIT                       
              PERFORM 2500-READ-F128       THRU 2500-EXIT                       
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F128-NUM-HIGH-VALUE = ALL '9'                                     
      *       PERFORM 7000-PROCESS-TRAIL   THRU 7000-EXIT                       
              PERFORM 2500-READ-F128       THRU 2500-EXIT                       
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 4000-WRITE-F556         THRU 4000-EXIT.                      
                                                                                
           IF (F128-LUMPSUM-APPLY   NOT = F128-LUMPSUM-DEDUCT) OR               
              (F128-AMT-STAMP-APPLY NOT = F128-AMT-STAMP-DEDUCT) OR             
              (F128-NUM-STATUS          = 'R')                                  
               PERFORM 5000-WRITE-REPORT   THRU 5000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2500-READ-F128 THRU 2500-EXIT.                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3100-PROCESS-HDR.                                                        
      *----------------------------------------------------------------*        
                                                                                
           MOVE F128-DTE-TRANS(3:2)        TO WS-JR-YY-NS.                      
           MOVE F128-DTE-TRANS(5:2)        TO WS-JR-NUM-MM-NS.                  
           MOVE F128-DTE-TRANS(7:2)        TO WS-JR-NUM-DD-NS.                  
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3200-WRITE-F128A.                                                        
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                TO F128A-REC.                             
           INITIALIZE                    F128A-REC.                             
           WRITE F128A-REC          FROM F128-DETAIL-REC.                       
           ADD  1                     TO WS-F128A-WRITTEN.                      
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-WRITE-F556.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                  TO AB02F556-DET-REC.                    
           INITIALIZE                      AB02F556-DET-REC.                    
                                                                                
           MOVE 'C'                     TO F556-D-CD.                           
           MOVE F128-NUM-CPF            TO F556-NUM-CPF.                        
           MOVE WS-CURR-DD              TO F556-PROC-DD.                        
           MOVE WS-CURR-MM              TO F556-PROC-MM.                        
           MOVE WS-CURR-CC              TO F556-PROC-CC.                        
           MOVE WS-CURR-YY              TO F556-PROC-YY.                        
           MOVE 'C'                     TO F556-AMT-CODE.                       
           MOVE WS-JR-VR-NS             TO F556-JR-VR.                          
           MOVE F128-HDB-REF(2:9)       TO F556-SCHACC.                         
           MOVE F128-HDB-REF(11:2)      TO F556-LESSEE.                         
                                                                                
           MOVE 'Y'                     TO F556-TAG-SOC.                        
                                                                                
           IF F128-LUMPSUM-APPLY IS NOT NUMERIC                                 
              MOVE ZEROES               TO F128-LUMPSUM-APPLY                   
           END-IF.                                                              
           IF F128-LUMPSUM-DEDUCT IS NOT NUMERIC                                
              MOVE ZEROES               TO F128-LUMPSUM-DEDUCT                  
           END-IF.                                                              
           IF F128-AMT-STAMP-APPLY IS NOT NUMERIC                               
              MOVE ZEROES               TO F128-AMT-STAMP-APPLY                 
           END-IF.                                                              
           IF F128-AMT-STAMP-DEDUCT IS NOT NUMERIC                              
              MOVE ZEROES               TO F128-AMT-STAMP-DEDUCT                
           END-IF.                                                              
                                                                                
           IF F128-LUMPSUM-DEDUCT     > ZEROS                                   
              MOVE 'DBSS LUMPSUM'      TO F556-DESC                             
              MOVE F128-LUMPSUM-DEDUCT TO F556-AMT-REG-DEP                      
              WRITE AB02F556-DET-REC                                            
              ADD 1                    TO WS-F556-WRITE                         
           ELSE                                                                 
              IF F128-AMT-STAMP-DEDUCT > ZEROS                                  
                 MOVE 'DBSS STAMP'          TO F556-DESC                        
                 MOVE F128-AMT-STAMP-DEDUCT TO F556-AMT-REG-DEP                 
                 WRITE AB02F556-DET-REC                                         
                 ADD 1                      TO WS-F556-WRITE                    
              ELSE                                                              
                 ADD 1                      TO WS-REV-INSTAL-CNT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-WRITE-REPORT.                                                       
      *----------------------------------------------------------------*        
                                                                                
           IF WS-LC31-LINE-CNT > 50                                             
              PERFORM 5100-PRINT-HEADING THRU 5100-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE SPACES                TO LC31-DETAIL.                           
           INITIALIZE                    LC31-DETAIL.                           
                                                                                
           MOVE F128-NUM-SERIAL       TO LC31-SERIAL.                           
           MOVE F128-NUM-CPF          TO LC31-CPF.                              
           STRING F128-HDB-REF(2:4) '-'                                         
                  F128-HDB-REF(6:4) '-'                                         
                  F128-HDB-REF(10:3)                                            
                  DELIMITED BY SIZE INTO LC31-HDB-REF.                          
                                                                                
           MOVE F128-LUMPSUM-APPLY    TO LC31-LUMPSUM-APPLY.                    
           MOVE F128-LUMPSUM-DEDUCT   TO LC31-LUMPSUM-DEDUCT.                   
           MOVE F128-AMT-STAMP-APPLY  TO LC31-STAMP-APPLY.                      
           MOVE F128-AMT-STAMP-DEDUCT TO LC31-STAMP-DEDUCT.                     
           MOVE F128-REAMRK1          TO LC31-REMARKS.                          
                                                                                
           ADD 1                      TO WS-LC31-SN-CNT.                        
           ADD 2                      TO WS-LC31-LINE-CNT.                      
                                                                                
           MOVE WS-LC31-SN-CNT        TO LC31-SN.                               
                                                                                
           WRITE PRINT-REC      FROM LC31-DETAIL AFTER 2.                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5100-PRINT-HEADING.                                                      
      *----------------------------------------------------------------*        
                                                                                
           ADD 1                 TO WS-LC31-PAGE-CNT.                           
           MOVE WS-LC31-PAGE-CNT TO LC31-PAGE-NO.                               
           MOVE SPACES           TO PRINT-REC.                                  
           WRITE PRINT-REC                      AFTER PAGE.                     
           WRITE PRINT-REC     FROM LC31-HEAD-1 AFTER 3.                        
           WRITE PRINT-REC     FROM LC31-HEAD-2 AFTER 1.                        
           WRITE PRINT-REC     FROM LC31-HEAD-3 AFTER 2.                        
           MOVE 7                TO WS-LC31-LINE-CNT.                           
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
      *7000-PROCESS-TRAIL.                                                      
      *----------------------------------------------------------------*        
      *                                                                         
      *    MOVE F128-NUM-TOTAL  TO WS-F128-COUNT-ALPHA.                         
      *                                                                         
      *    IF WS-F556-WRITE NOT = WS-F128-COUNT                                 
      *       DISPLAY 'RECORD COUNT NOT TALLY!'                                 
      *       MOVE 99 TO RETURN-CODE                                            
      *       PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
      *    END-IF.                                                              
      *                                                                         
      *7000-EXIT.                                                               
      *    EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8000-CNT-NO-OF-RECS.                                                     
      *----------------------------------------------------------------*        
                                                                                
           IF F128-HDR-REC(1:32) = SPACES                                       
              PERFORM 2500-READ-F128       THRU 2500-EXIT                       
              GO TO 8000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F128-NUM-TOTAL NOT NUMERIC                                        
              MOVE ZEROES                    TO F128-NUM-TOTAL                  
           END-IF                                                               
                                                                                
           IF F128-NUM-HIGH-VALUE = ALL '9'                                     
              MOVE F128-NUM-TOTAL            TO WS-F128-COUNT-ALPHA             
              PERFORM 2500-READ-F128       THRU 2500-EXIT                       
              GO TO 8000-EXIT                                                   
           END-IF.                                                              
                                                                                
           ADD 1                             TO WS-F128-DETAIL-CNT.             
           PERFORM  2500-READ-F128         THRU 2500-EXIT.                      
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8100-VALIDATE-REC.                                                       
      *----------------------------------------------------------------*        
                                                                                
           IF WS-F128-COUNT NOT = WS-F128-DETAIL-CNT                            
              MOVE 'Y'            TO WS-REC-NOT-VALID                           
           END-IF.                                                              
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8200-REOPEN-F128.                                                        
      *----------------------------------------------------------------*        
                                                                                
           CLOSE      BP13F128.                                                 
           OPEN INPUT BP13F128.                                                 
                                                                                
           MOVE 'N'                  TO WS-EOF-F128.                            
           MOVE ZEROES               TO WS-F128-READ.                           
                                                                                
       8200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE SY02F001                                                       
                 BP13F128                                                       
                 AB02F556                                                       
                 P13F128A                                                       
                 BP13LC31.                                                      
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '******* BP13CC31 CONTROL TOTALS ******* '.                  
           DISPLAY '                                        '.                  
           DISPLAY 'NO OF RECORDS READ FROM    (BP13F128) = '                   
                    WS-F128-READ.                                               
           DISPLAY 'NO OF RECORDS WRITTEN TO   (AB02F556 ) = '                  
                    WS-F556-WRITE.                                              
           DISPLAY 'NO OF RECORDS (REVISED INSTAL)        = '                   
                    WS-REV-INSTAL-CNT.                                          
           DISPLAY 'NO OF RECORDS WRITTEN TO (P13F128A)   = '                   
                    WS-F128A-WRITTEN.                                           
           DISPLAY 'NO OF RECORDS WRITTEN TO   (BP13LC31) = '                   
                    WS-LC31-SN-CNT.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
