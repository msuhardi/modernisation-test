      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C21E.                                                 
      *AUTHOR.        ELGER DE GUZMAN.                                          
      *DATE-WRITTEN.  10 FEBRUARY 2011.                                         
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE  1.: DELETE CANCELLED CASES FROM KP00           *           
      *                  OUTPUT DELETED RECORDS TO BP13FP00         *           
      *              2.: DELETE CANCELLED CASES FROM KP01           *           
      *                  OUTPUT DELETED RECORDS TO BP13FP01         *           
      *              3.: DELETE CANCELLED CASES FROM KP02           *           
      *                  OUTPUT DELETED RECORDS TO BP13FP02         *           
      *              4.: REMOVE BP13KP01 PROCESS                    *           
      *                  OUTPUT DELETED RECORDS TO BP13FP02         *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13FQ10                                              *           
      *                                                             *           
      *   INPUT/OUTPUT:                                             *           
      *   1.  BP13KP00                                              *           
      *   2.  BP13KP02                                              *           
      *                                                             *           
      *   OUTPUT:                                                   *           
      *   1.  BP13FP00                                              *           
      *   2.  BP13FP02                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP134135 10/02/2011 ESD1 NEW PROGRAM                        *           
      * BP134513 30/11/2011 AB9  HOUSE KEEP BP13KP02 AND            *           
      *                          REMOVE BP13KP01                    *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FQ10 ASSIGN       TO BP13FQ10.                            
                                                                                
           SELECT BP13KP00 ASSIGN       TO BP13KP00                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KP00-KEY-FLD                         
                           FILE STATUS  IS WS-KP00-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT BP13KP02 ASSIGN       TO BP13KP02                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KP02-KEY-FLD                         
                           FILE STATUS  IS WS-KP02-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT BP13FP00 ASSIGN       TO BP13FP00.                            
                                                                                
           SELECT BP13FP02 ASSIGN       TO BP13FP02.                            
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FQ10                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13FQ10.                                                           
                                                                                
       FD   BP13KP00                                                            
            RECORD CONTAINS 3800 CHARACTERS.                                    
       COPY BP13KP00.                                                           
       FD   BP13KP02                                                            
            RECORD CONTAINS 200  CHARACTERS.                                    
       COPY BP13KP02.                                                           
                                                                                
       FD   BP13FP00                                                            
            RECORD CONTAINS 3800 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       01   BP13FP00-REC               PIC X(3800).                             
                                                                                
       FD   BP13FP02                                                            
            RECORD CONTAINS 200  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       01   BP13FP02-REC               PIC X(200).                              
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-KP00-STATUS           PIC 9(02)  VALUE ZEROES.                    
       01  WS-KP02-STATUS           PIC 9(02)  VALUE ZEROES.                    
       01  WS-FQ10-EOF              PIC X      VALUE SPACES.                    
       01  WS-FLAG-KP00             PIC X      VALUE SPACES.                    
       01  WS-FLAG-KP02             PIC X      VALUE SPACES.                    
       01  WS-FQ10-READ-CNT         PIC 9(08)  VALUE ZEROES.                    
       01  WS-KP00-DELETE-CNT       PIC 9(08)  VALUE ZEROES.                    
       01  WS-KP02-DELETE-CNT       PIC 9(08)  VALUE ZEROES.                    
       01  WS-FP00-WRITE-CNT        PIC 9(08)  VALUE ZEROES.                    
       01  WS-FP02-WRITE-CNT        PIC 9(08)  VALUE ZEROES.                    
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-PRE-PROCESS      THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13FQ10    THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-REC      THRU 3000-EXIT                         
                   UNTIL WS-FQ10-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-PRE-PROCESS.                                                        
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FQ10                                                 
                I-O    BP13KP00                                                 
                       BP13KP02                                                 
                OUTPUT BP13FP00                                                 
                       BP13FP02.                                                
                                                                                
           IF WS-KP00-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13KP00 - OPEN ERROR: ' WS-KP00-STATUS                  
              MOVE WS-KP00-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-KP02-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13KP02 - OPEN ERROR: ' WS-KP02-STATUS                  
              MOVE WS-KP02-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       2000-READ-BP13FQ10.                                                      
      *------------------*                                                      
                                                                                
           READ BP13FQ10 AT END                                                 
                MOVE 'Y' TO WS-FQ10-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-FQ10-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           PERFORM 3100-PROCESS-BP13KP00  THRU 3100-EXIT.                       
           PERFORM 3300-PROCESS-BP13KP02  THRU 3300-EXIT.                       
           PERFORM 2000-READ-BP13FQ10     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3100-PROCESS-BP13KP00.                                                   
      *----------------------*                                                  
                                                                                
           MOVE SPACES            TO BP13KP00-REC.                              
           INITIALIZE                BP13KP00-REC.                              
           MOVE LOW-VALUES        TO KP00-KEY-FLD                               
           MOVE FQ10-NUM-REGN     TO KP00-NUM-REGN.                             
                                                                                
           START BP13KP00 KEY >= KP00-KEY-FLD.                                  
                                                                                
           EVALUATE WS-KP00-STATUS                                              
             WHEN 00                                                            
                PERFORM 3110-WRITE-DEL-BP13KP00 THRU 3110-EXIT                  
             WHEN 23                                                            
                CONTINUE                                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13KP00 - READ ERROR: ' WS-KP00-STATUS                
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       3110-WRITE-DEL-BP13KP00.                                                 
      *-----------------------*                                                 
                                                                                
           MOVE SPACES TO WS-FLAG-KP00.                                         
                                                                                
           PERFORM UNTIL  WS-FLAG-KP00 = 'N'                                    
              READ BP13KP00 NEXT RECORD                                         
              EVALUATE WS-KP00-STATUS                                           
                 WHEN 00                                                        
                    IF KP00-NUM-REGN = FQ10-NUM-REGN                            
                       WRITE BP13FP00-REC  FROM BP13KP00-REC                    
                       ADD   1      TO     WS-FP00-WRITE-CNT                    
                       PERFORM 3120-DELETE-KP00 THRU 3120-EXIT                  
                    ELSE                                                        
                       MOVE 'N'         TO WS-FLAG-KP00                         
                    END-IF                                                      
                 WHEN 10                                                        
                 WHEN 23                                                        
                    MOVE 'N'         TO WS-FLAG-KP00                            
                 WHEN OTHER                                                     
                    DISPLAY 'ERRROR READING  BP13KP00, STATUS = '               
                            WS-KP00-STATUS ' REGN-NO ' FQ10-NUM-REGN            
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
       3110-EXIT.                                                               
                                                                                
      *-----------------------*                                                 
       3112-WRITE-DEL-BP13KP02.                                                 
      *-----------------------*                                                 
                                                                                
           MOVE SPACES   TO     WS-FLAG-KP02.                                   
                                                                                
           PERFORM UNTIL  WS-FLAG-KP02 = 'N'                                    
              READ BP13KP02 NEXT RECORD                                         
              EVALUATE WS-KP02-STATUS                                           
                 WHEN 00                                                        
                    IF KP02-NUM-REGN = FQ10-NUM-REGN                            
                       WRITE BP13FP02-REC  FROM BP13KP02-REC                    
                       ADD   1      TO     WS-FP02-WRITE-CNT                    
                       PERFORM 3140-DELETE-KP02 THRU 3140-EXIT                  
                    ELSE                                                        
                       MOVE 'N'         TO WS-FLAG-KP02                         
                    END-IF                                                      
                 WHEN 10                                                        
                 WHEN 23                                                        
                    MOVE 'N'         TO WS-FLAG-KP02                            
                 WHEN OTHER                                                     
                    DISPLAY 'ERRROR READING  BP13KP02, STATUS = '               
                            WS-KP02-STATUS ' REGN-NO ' FQ10-NUM-REGN            
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
       3112-EXIT.                                                               
                                                                                
      *----------------*                                                        
       3120-DELETE-KP00.                                                        
      *----------------*                                                        
                                                                                
           DELETE BP13KP00.                                                     
                                                                                
           EVALUATE WS-KP00-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-KP00-DELETE-CNT                                    
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13KP00, STATUS = '                  
                         WS-KP00-STATUS ' REGN-NO ' FQ10-NUM-REGN               
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3120-EXIT.                                                               
      *----------------*                                                        
       3140-DELETE-KP02.                                                        
      *----------------*                                                        
                                                                                
           DELETE BP13KP02.                                                     
                                                                                
           EVALUATE WS-KP02-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-KP02-DELETE-CNT                                    
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13KP02, STATUS = '                  
                         WS-KP02-STATUS ' REGN-NO ' FQ10-NUM-REGN               
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3140-EXIT.                                                               
                                                                                
      *----------------------*                                                  
       3300-PROCESS-BP13KP02.                                                   
      *----------------------*                                                  
                                                                                
           MOVE SPACES            TO BP13KP02-REC.                              
           INITIALIZE                BP13KP02-REC.                              
           MOVE LOW-VALUES        TO KP02-KEY-FLD                               
           MOVE FQ10-NUM-REGN     TO KP02-NUM-REGN.                             
                                                                                
           START BP13KP02 KEY >= KP02-KEY-FLD.                                  
                                                                                
           EVALUATE WS-KP02-STATUS                                              
             WHEN 00                                                            
                PERFORM 3112-WRITE-DEL-BP13KP02 THRU 3112-EXIT                  
             WHEN 23                                                            
                CONTINUE                                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13KP02 - READ ERROR: ' WS-KP02-STATUS                
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C21E  *************'.                           
           DISPLAY 'RECS READ    FROM BP13FQ10 : ' WS-FQ10-READ-CNT.            
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN IN BP13FP00   : ' WS-FP00-WRITE-CNT.           
           DISPLAY 'RECS WRITTEN IN BP13FP02   : ' WS-FP02-WRITE-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS DELETED IN BP13KP00   : ' WS-KP00-DELETE-CNT.          
           DISPLAY 'RECS DELETED IN BP13KP02   : ' WS-KP02-DELETE-CNT.          
                                                                                
           CLOSE BP13FQ10                                                       
                 BP13KP00                                                       
                 BP13KP02                                                       
                 BP13FP00                                                       
                 BP13FP02.                                                      
                                                                                
           IF WS-KP00-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13KP00 - CLOSE ERROR: ' WS-KP00-STATUS                 
              MOVE WS-KP00-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
           IF WS-KP02-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13KP02 - CLOSE ERROR: ' WS-KP02-STATUS                 
              MOVE WS-KP02-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
