      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C384.                                                 
      *AUTHOR.        EG1.                                                      
      *DATE-WRITTEN.  FEB 07 1996.                                              
                                                                                
      *****************************************************************         
      * OBJECTIVE  : TO PRINT ASSESSMENT INFORMATION.                 *         
      *                                                               *         
      * INPUT FILES:  BP13F313              AB03K700                  *         
      *               BP13K800              BB14K580                  *         
      *               BP13K820              AB03K030                  *         
      *               BP13K360              BM06K110                  *         
      *               BB14K500              BM06K100                  *         
      *               BB24k110              BB24K010                  *         
      *                                                               *         
      * REPORT:       - ASSESSMENT FORM                               *         
      * SUBROUTINES   - BP13C913                                      *         
      *---------------------------------------------------------------*         
      * REF NO   DATE        BY   AMENDMENTS/ENHANCEMENTS             *         
      * -------  ----        ---  -----------------------             *         
      * bp130068 07/02/1996  eg1  laser printing of soca letters.     *         
      * bp130171 12/29/1997  sdc  change all bb12 fields to bb24      *         
      * BP130418 03/09/1998  sia  CHGE BM06K211 TO BM06K110,          *         
      *                           CHGE BM06K210 TO BM06K100           *         
      * BP130883 07/08/2000  ras  CHANGE BP13C904 TO BP13C913         *         
      * BP132086 18/02/2002  ckk  DON'T PRINT SERS CASES. PRINT ONLY  *         
      *                           WHEN SMS SALES TYPE = BLANK         *         
      * BP132160 22/08/2002  ckk  REMOVE LETTER, BP1339               *         
      * BP132352 12/06/2003  mj16 INCLUDE FILE BP13K022 & K024 TO     *         
      *                           GET OIC NAME AND REMOVE BP13K360    *         
      * BP132435 05/11/2003  SSS2 K010 FILE EXPANSION                 *         
      * BP132485 03/03/2004  MJ16 TO INCLUDE CHECKING OF SERS TABLE   *         
      *                           TO INCLUDE 3 NEW ALLOC SCHEMES IN   *         
      *                           TABLE.('SFO','SRO','SJO')           *         
      * BP132576 04/08/2004  SSD  TO GET ESTIMATED BUILDING COMPLETED *         
      *                           DATE FROM K580-DTE-UNIT-DELIVERY    *         
      * BP132673 27/04/2005  JF4  TO GET ESTIMATED BUILDING COMPLETED *         
      *                           USING SCH ACCT FROM BP13K800        *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F313 ASSIGN        TO BP13F313.                           
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                  ACCESS MODE  IS        DYNAMIC                                
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K800-NUM-REGN                          
                  FILE STATUS  IS        BP13K800-STATUS.                       
                                                                                
           SELECT BP13K820 ASSIGN        TO BP13K820                            
                  ACCESS MODE  IS        DYNAMIC                                
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K820-KEY-FLD                           
                  FILE STATUS  IS        BP13K820-STATUS.                       
                                                                                
           SELECT BP13K022 ASSIGN         TO BP13K022                           
                  ACCESS MODE   IS        DYNAMIC                               
                  ORGANIZATION  IS        INDEXED                               
                  RECORD KEY    IS        K022-KEY-FLD                          
                  ALTERNATE RECORD KEY IS  K022-NUM-REGN                        
                  FILE STATUS   IS        BP13K022-STATUS.                      
                                                                                
           SELECT BP13K024 ASSIGN        TO BP13K024                            
                  ACCESS MODE  IS        RANDOM                                 
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K024-KEY-FLD                           
                  FILE STATUS  IS        BP13K024-STATUS.                       
                                                                                
           SELECT AB03K030 ASSIGN        TO AB03K030                            
                  ACCESS MODE  IS        RANDOM                                 
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K030-NUM-HDB-REF                       
                  FILE STATUS  IS        AB03K030-STATUS.                       
                                                                                
           SELECT AB03K700 ASSIGN        TO AB03K700                            
                  ACCESS MODE  IS        DYNAMIC                                
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K700-NUM-HDB-REF                       
                  FILE STATUS  IS        AB03K700-STATUS.                       
                                                                                
           SELECT BB14K500 ASSIGN        TO BB14K500                            
                  ACCESS MODE  IS        DYNAMIC                                
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K500-KEY-FLD                           
                  FILE STATUS  IS        BB14K500-STATUS.                       
                                                                                
           SELECT BB14K580 ASSIGN        TO BB14K580                            
                  ACCESS MODE  IS        DYNAMIC                                
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K580-KEY-FLD                           
                  FILE STATUS  IS        BB14K580-STATUS.                       
                                                                                
           SELECT BM06K100 ASSIGN        TO BM06K100                            
                  ACCESS MODE  IS        DYNAMIC                                
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K100-KEY-FLD                           
                  FILE STATUS  IS        BM06K100-STATUS.                       
                                                                                
           SELECT BM06K110 ASSIGN        TO BM06K110                            
                  ACCESS MODE  IS        DYNAMIC                                
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K110-SCH-ACC-NO                        
                  FILE STATUS  IS        BM06K110-STATUS.                       
                                                                                
           SELECT BB24K110 ASSIGN        TO BB24K110                            
                  ACCESS MODE  IS        DYNAMIC                                
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K110-KEY-FLD OF BB24K110-REC           
                  FILE STATUS  IS        BB24k110-STATUS.                       
                                                                                
           SELECT BB24K010 ASSIGN        TO BB24K010                            
                  ACCESS MODE  IS        DYNAMIC                                
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K010-KEY-FLD                           
                  FILE STATUS  IS        BB24K010-STATUS.                       
                                                                                
           SELECT BP13L384 ASSIGN        TO BP13L384.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F313                                                            
            RECORDING  MODE IS F                                                
            RECORD CONTAINS  50 CHARACTERS                                      
            BLOCK  CONTAINS   0 RECORDS.                                        
       01   BP13F313-REC.                                                       
            05 F313-NUM-REGN             PIC X(8).                              
            05 F313-NUM-NRIC             PIC X(9).                              
            05 F313-NUM-SCH-ACC          PIC X(11).                             
            05 FILLER                    PIC X(22).                             
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD   AB03K030                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY AB03K030.                                                           
                                                                                
       FD   AB03K700                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY AB03K700.                                                           
                                                                                
       FD   BB14K580                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BB14K580.                                                           
                                                                                
       FD   BB14K500                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BB14K500.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BB24K110                                                            
            RECORD CONTAINS 50 CHARACTERS.                                      
       COPY BB24K110.                                                           
                                                                                
       FD   BB24K010                                                            
            RECORD CONTAINS 5000 CHARACTERS.                                    
       COPY BB24K010.                                                           
                                                                                
       FD   BP13L384                                                            
            RECORDING  MODE IS F                                                
            RECORD CONTAINS 133 CHARACTERS                                      
            BLOCK  CONTAINS   0 RECORDS.                                        
       01   L384-PRINT-REC               PIC X(133).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       COPY SERSCH.                                                             
       01 WS-ALLOC-SCH                  PIC X(03) VALUE SPACES.                 
                                                                                
      *-------------------------------------------------------------            
      *   LITERALS                                                              
      *-------------------------------------------------------------            
       01 WS-MONTH-LITERALS.                                                    
          05 FILLER                     PIC X(09) VALUE 'JANUARY  '.            
          05 FILLER                     PIC X(09) VALUE 'FEBRUARY '.            
          05 FILLER                     PIC X(09) VALUE 'MARCH    '.            
          05 FILLER                     PIC X(09) VALUE 'APRIL    '.            
          05 FILLER                     PIC X(09) VALUE 'MAY      '.            
          05 FILLER                     PIC X(09) VALUE 'JUNE     '.            
          05 FILLER                     PIC X(09) VALUE 'JULY     '.            
          05 FILLER                     PIC X(09) VALUE 'AUGUST   '.            
          05 FILLER                     PIC X(09) VALUE 'SEPTEMBER'.            
          05 FILLER                     PIC X(09) VALUE 'OCTOBER  '.            
          05 FILLER                     PIC X(09) VALUE 'NOVEMBER '.            
          05 FILLER                     PIC X(09) VALUE 'DECEMBER '.            
       01 WS-MONTH-ARRAY REDEFINES WS-MONTH-LITERALS.                           
          05 WS-MONTH-TABLE OCCURS 12 TIMES.                                    
             10 WS-MONTH                 PIC X(9).                              
                                                                                
      *-------------------------------------------------------------            
      *   MISCELLEANEOUS VARIABLES                                              
      *-------------------------------------------------------------            
       01  WS-DAY-SUFFIX                 PIC X(2) VALUE SPACES.                 
       01  WS-DATE-SUFFIX.                                                      
           05 WS-SUFF-CC                 PIC 9(2).                              
           05 WS-DATE-SUFFIX-YYMMDD.                                            
              10 WS-SUFF-YY              PIC 9(2).                              
              10 WS-SUFF-MM              PIC 9(2).                              
              10 WS-SUFF-DD              PIC 9(2).                              
       01  WS-REFORMATTED-DATE           PIC X(20).                             
       01  WS-SQL-CODE                   PIC 9(4) VALUE 0.                      
       01  WS-STRING-POINTER             PIC 9(3) VALUE 0.                      
       01  WS-APPL-NAME-GRP.                                                    
           05 WS-NME-APPL1               PIC X(66).                             
           05 WS-NME-APPL2               PIC X(66).                             
           05 WS-NME-APPL3               PIC X(66).                             
           05 WS-NME-APPL4               PIC X(66).                             
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC                 PIC X(02).                             
           05 WS-CURR-YYMMDD.                                                   
              10 WS-CURR-YY              PIC X(02).                             
              10 WS-CURR-MM              PIC X(02).                             
              10 WS-CURR-DD              PIC X(02).                             
                                                                                
       01  WS-DISP-DATE.                                                        
           05 WS-DD                      PIC X(02).                             
           05 FILLER                     PIC X(01) VALUE '/'.                   
           05 WS-MM                      PIC X(02).                             
           05 FILLER                     PIC X(01) VALUE '/'.                   
           05 WS-YY                      PIC X(02).                             
                                                                                
       01  WS-K800-COMM8-INFO.                                                  
           05 WS-NUM-BLK                 PIC X(05).                             
           05 WS-NUM-HSELVL              PIC X(02).                             
           05 WS-NUM-HSEMAIN             PIC X(04).                             
           05 WS-NUM-HSESUB              PIC X(03).                             
           05 WS-NME-STREET              PIC X(20).                             
           05 WS-NUM-PD                  PIC X(06).                             
                                                                                
       01  WS-SMS-COMM8-INFO.                                                   
           05 WS-SMS-NUM-BLK             PIC X(05).                             
           05 WS-SMS-NUM-HSELVL          PIC X(02).                             
           05 WS-SMS-NUM-HSEMAIN         PIC X(04).                             
           05 WS-SMS-NUM-HSESUB          PIC X(03).                             
           05 WS-SMS-NME-STREET          PIC X(20).                             
           05 WS-SMS-NUM-PD              PIC X(06).                             
           05 WS-SMS-CDE-BO              PIC X(02).                             
                                                                                
      *-------------------------------------------------------------            
      *   LINGKAGE VARIABLES                                                    
      *-------------------------------------------------------------            
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(04).                            
            05 WS-LINK-NUM-ACC            PIC X(04).                            
            05 WS-LINK-NUM-CHK            PIC X(01).                            
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------------            
      *   COUNTER VARIABLES                                                     
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F313-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-FORMS-PRINT         PIC 9(07) VALUE 0.                     
                                                                                
      *-------------------------------------------------------------            
      *   FORM FORMAT                                                           
      *-------------------------------------------------------------            
       01  DJDE-LETTER-BP1338            PIC X(80) VALUE                        
           '$DJDE$ FORMS=BP1338,DUPLEX=NO,END;'.                                
                                                                                
       01  L384-INFO-LN01.                                                      
           05 FILLER                     PIC X(15) VALUE SPACES.                
           05 L384-SMS-SCH-ACC           PIC X(14).                             
           05 FILLER                     PIC X(16) VALUE SPACES.                
           05 L384-REGN-NO               PIC X(08).                             
           05 FILLER                     PIC X(01) VALUE '('.                   
           05 L384-NEW-TOWN              PIC X(03).                             
           05 FILLER                     PIC X(01) VALUE '/'.                   
           05 L384-FLAT-TYPE             PIC X(02).                             
           05 FILLER                     PIC X(01) VALUE ')'.                   
                                                                                
       01  L384-INFO-LN02.                                                      
           05 FILLER                     PIC X(76) VALUE SPACES.                
           05 L384-CURR-DATE             PIC X(10).                             
                                                                                
       01  L384-INFO-LN03.                                                      
           05 FILLER                     PIC X(17) VALUE SPACES.                
           05 L384-CDE-BO                PIC X(02).                             
           05 FILLER                     PIC X(57) VALUE SPACES.                
           05 L384-OIC-DID               PIC X(08).                             
                                                                                
       01  L384-INFO-LN04.                                                      
           05 FILLER                     PIC X(22) VALUE SPACES.                
           05 L384-NAME1                 PIC X(66).                             
                                                                                
       01  L384-INFO-LN05.                                                      
           05 FILLER                     PIC X(22) VALUE SPACES.                
           05 L384-NAME2                 PIC X(66).                             
                                                                                
       01  L384-INFO-LN06.                                                      
           05 FILLER                     PIC X(22) VALUE SPACES.                
           05 L384-NAME3                 PIC X(66).                             
                                                                                
       01  L384-INFO-LN07.                                                      
           05 FILLER                     PIC X(22) VALUE SPACES.                
           05 L384-NAME4                 PIC X(66).                             
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L384-HAS-HAVE              PIC X(04).                             
                                                                                
       01  L384-INFO-LN08.                                                      
           05 FILLER                     PIC X(32) VALUE SPACES.                
           05 L384-ADDR-LINE             PIC X(60).                             
                                                                                
       01  L384-INFO-LN09.                                                      
           05 FILLER                     PIC X(12) VALUE SPACES.                
           05 L384-DTE-ACCEPT            PIC X(20).                             
                                                                                
       01  L384-INFO-LN10.                                                      
           05 FILLER                     PIC X(07) VALUE SPACES.                
           05 L384-DTE-DPD               PIC X(20).                             
                                                                                
       01  L384-INFO-LN11.                                                      
           05 FILLER                     PIC X(07) VALUE SPACES.                
           05 L384-OIC-NAME              PIC X(40).                             
                                                                                
       01  L384-INFO-LN12.                                                      
           05 FILLER                     PIC X(15) VALUE SPACES.                
           05 L384-SMS-ADDR-LINE         PIC X(75).                             
                                                                                
       01  L384-INFO-LN13.                                                      
           05 FILLER                     PIC X(12) VALUE SPACES.                
           05 L384-SMS-NME-LESSEE1       PIC X(22).                             
                                                                                
       01  L384-INFO-LN13A.                                                     
           05 FILLER                     PIC X(17) VALUE SPACES.                
           05 L384-SMS-UIN-LESSEE1       PIC X(09).                             
                                                                                
       01  L384-INFO-LN14.                                                      
           05 FILLER                     PIC X(12) VALUE SPACES.                
           05 L384-SMS-NME-LESSEE2       PIC X(22).                             
                                                                                
       01  L384-INFO-LN14A.                                                     
           05 FILLER                     PIC X(17) VALUE SPACES.                
           05 L384-SMS-UIN-LESSEE2       PIC X(09).                             
                                                                                
       01  L384-INFO-LN15.                                                      
           05 FILLER                     PIC X(12) VALUE SPACES.                
           05 L384-SMS-NME-LESSEE3       PIC X(22).                             
                                                                                
       01  L384-INFO-LN15A.                                                     
           05 FILLER                     PIC X(17) VALUE SPACES.                
           05 L384-SMS-UIN-LESSEE3       PIC X(09).                             
                                                                                
       01  L384-INFO-LN16.                                                      
           05 FILLER                     PIC X(12) VALUE SPACES.                
           05 L384-SMS-NME-LESSEE4       PIC X(22).                             
                                                                                
       01  L384-INFO-LN16A.                                                     
           05 FILLER                     PIC X(17) VALUE SPACES.                
           05 L384-SMS-UIN-LESSEE4       PIC X(09).                             
                                                                                
      *-------------------------------------------------------------            
      *   FLAG    VARIABLES                                                     
      *-------------------------------------------------------------            
       01  WS-ERROR-FL                   PIC X(01) VALUE 'N'.                   
           88 WS-NO-ERROR                VALUE 'N'.                             
           88 WS-ERROR                   VALUE 'Y'.                             
                                                                                
       01  WS-HRU-HBO-FL                 PIC X(03) VALUE 'HBO'.                 
           88 WS-HRU-FL                  VALUE 'HRU'.                           
           88 WS-HBO-FL                  VALUE 'HBO'.                           
                                                                                
       01  WS-SERS-CASE                  PIC X(01) VALUE 'N'.                   
                                                                                
       01  WS-F313-EOF                   PIC X(01) VALUE 'N'.                   
                                                                                
       01  BP13K800-STATUS               PIC X(2)  VALUE SPACES.                
       01  BP13K820-STATUS               PIC X(2)  VALUE SPACES.                
       01  BP13K022-STATUS               PIC X(2)  VALUE SPACES.                
       01  BP13K024-STATUS               PIC X(2)  VALUE SPACES.                
       01  AB03K030-STATUS               PIC X(2)  VALUE SPACES.                
       01  AB03K700-STATUS               PIC X(2)  VALUE SPACES.                
       01  BB14K500-STATUS               PIC X(2)  VALUE SPACES.                
       01  BB14K580-STATUS               PIC X(2)  VALUE SPACES.                
       01  BB24K110-STATUS               PIC X(2)  VALUE SPACES.                
       01  BB24K010-STATUS               PIC X(2)  VALUE SPACES.                
       01  BM06K100-STATUS               PIC X(2)  VALUE SPACES.                
       01  BM06K110-STATUS               PIC X(2)  VALUE SPACES.                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
                                                                                
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                UNTIL WS-F313-EOF = 'Y'.                                        
                                                                                
           PERFORM 9999-WRAP-UP.                                                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-START-UP.                                                           
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F313                                                
                        BP13K800                                                
                        BP13K820                                                
                        AB03K030                                                
                        AB03K700                                                
                        BB14K500                                                
                        BB14K580                                                
                        BP13K022                                                
                        BP13K024                                                
                        BM06K110                                                
                        BM06K100                                                
                        BB24K110                                                
                        BB24K010                                                
                OUTPUT  BP13L384.                                               
                                                                                
            IF BP13K800-STATUS NOT = '00'                                       
               DISPLAY 'BP13K800 OPENING ERROR: ' BP13K800-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF BP13K820-STATUS NOT = '00'                                       
               DISPLAY 'BP13K820 OPENING ERROR: ' BP13K820-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF AB03K030-STATUS NOT = '00'                                       
               DISPLAY 'AB03K030 OPENING ERROR: ' AB03K030-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF AB03K700-STATUS NOT = '00'                                       
               DISPLAY 'AB03K700 OPENING ERROR: ' AB03K700-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF BB14K500-STATUS NOT = '00'                                       
               DISPLAY 'BB14K500 OPENING ERROR: ' BB14K500-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF BB14K580-STATUS NOT = '00'                                       
               DISPLAY 'BB14K580 OPENING ERROR: ' BB14K580-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF BP13K022-STATUS NOT = '00'                                       
               DISPLAY 'BP13K022 OPENING ERROR: ' BP13K022-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF BP13K024-STATUS NOT = '00'                                       
               DISPLAY 'BP13K024 OPENING ERROR: ' BP13K024-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF BM06K100-STATUS NOT = '00'                                       
               DISPLAY 'BM06K100 OPENING ERROR: ' BM06K100-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF BM06K110-STATUS NOT = '00'                                       
               DISPLAY 'BM06K110 OPENING ERROR: ' BM06K110-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF BB24k110-STATUS NOT = '00'                                       
               DISPLAY 'BB24k110 OPENING ERROR: ' BB24K110-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF BB24K010-STATUS NOT = '00'                                       
               DISPLAY 'BB24K010 OPENING ERROR: ' BB24K010-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            PERFORM 8100-READ-BP13F313   THRU 8100-EXIT.                        
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
      * GET ALL RECORDS AND PROCESS EACH ONE FOR PRINTING                       
      *-------------------------------------------------------------            
            PERFORM 2100-RETRIEVE-FILES    THRU 2100-EXIT.                      
            IF (WS-ERROR-FL = 'N') AND                                          
               (K030-CDE-SALES-TYP = SPACES) AND                                
               (WS-SERS-CASE = 'N')                                             
               PERFORM 3000-FORMAT-LETTER  THRU 3000-EXIT                       
               PERFORM 4000-PRINT-LETTER   THRU 4000-EXIT                       
            END-IF.                                                             
                                                                                
            PERFORM 8100-READ-BP13F313     THRU 8100-EXIT.                      
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-RETRIEVE-FILES.                                                     
      *-------------------------------------------------------------            
            MOVE 'N'                       TO WS-ERROR-FL.                      
            MOVE 'N'                       TO WS-SERS-CASE.                     
            MOVE F313-NUM-REGN             TO K800-NUM-REGN.                    
            PERFORM 8200-READ-BP13K800     THRU 8200-EXIT.                      
            PERFORM 2200-GET-APPL-INFO     THRU 2200-EXIT.                      
            PERFORM 2400-CHK-SERS-CASE     THRU 2400-EXIT.                      
                                                                                
            IF WS-NO-ERROR                                                      
               MOVE SPACES                 TO BP13COMM8-REC                     
               INITIALIZE                     BP13COMM8-REC                     
               MOVE K800-NUM-SCH           TO WS-LINK-NUM-SCH                   
               MOVE K800-NUM-ACC           TO WS-LINK-NUM-ACC                   
               MOVE K800-NUM-ACC-CK        TO WS-LINK-NUM-CHK                   
                                                                                
               PERFORM 8300-LINK-BP13C913  THRU 8300-EXIT                       
               PERFORM 2110-SAVE-K800-ADDR THRU 2110-EXIT                       
               MOVE K800-NUM-SCH-ACC       TO K110-SCH-ACC-NO                   
               PERFORM 8400-READ-BM06K110  THRU 8400-EXIT                       
               MOVE K110-AIX1              TO K100-KEY-FLD                      
               PERFORM 8450-READ-BM06K100  THRU 8450-EXIT                       
               MOVE K100-CDE-BLK           TO K500-KEY-FLD                      
               PERFORM 8900-READ-BB14K500  THRU 8900-EXIT                       
               MOVE K800-NUM-SCH-ACC       TO K580-KEY-FLD                      
               PERFORM 8700-READ-BB14K580  THRU 8700-EXIT                       
               MOVE K800-NUM-REGN          TO K022-NUM-REGN                     
               PERFORM 8800-READ-BP13K022  THRU 8800-EXIT                       
            END-IF.                                                             
                                                                                
            IF WS-NO-ERROR                                                      
               MOVE F313-NUM-SCH-ACC       TO K700-NUM-HDB-REF                  
               PERFORM 8500-READ-AB03K700  THRU 8500-EXIT                       
               MOVE F313-NUM-SCH-ACC       TO K030-NUM-HDB-REF                  
               PERFORM 8600-READ-AB03K030  THRU 8600-EXIT                       
               MOVE SPACES                 TO BP13COMM8-REC                     
               INITIALIZE                     BP13COMM8-REC                     
               MOVE F313-NUM-SCH-ACC(1:4)  TO WS-LINK-NUM-SCH                   
               MOVE F313-NUM-SCH-ACC(5:4)  TO WS-LINK-NUM-ACC                   
               MOVE F313-NUM-SCH-ACC(9:1)  TO WS-LINK-NUM-CHK                   
               PERFORM 8300-LINK-BP13C913  THRU 8300-EXIT                       
               PERFORM 2120-SAVE-SMS-ADDR  THRU 2120-EXIT                       
            END-IF.                                                             
                                                                                
            IF WS-NO-ERROR                                                      
               PERFORM 2300-GET-HRU-INFO   THRU 2300-EXIT                       
            END-IF.                                                             
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2110-SAVE-K800-ADDR.                                                     
      *-------------------------------------------------------------            
            MOVE SPACES                TO WS-K800-COMM8-INFO.                   
            MOVE COMM8-NUM-BLK         TO WS-NUM-BLK.                           
            MOVE COMM8-NUM-LEVEL       TO WS-NUM-HSELVL.                        
            MOVE COMM8-NUM-UNIT-MAIN   TO WS-NUM-HSEMAIN.                       
            MOVE COMM8-NUM-UNIT-SUB    TO WS-NUM-HSESUB.                        
            MOVE COMM8-NME-STREET      TO WS-NME-STREET.                        
            MOVE COMM8-NUM-POSTAL-CODE TO WS-NUM-PD.                            
                                                                                
       2110-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2120-SAVE-SMS-ADDR.                                                      
      *-------------------------------------------------------------            
            MOVE SPACES                TO WS-SMS-COMM8-INFO.                    
            MOVE COMM8-NUM-BLK         TO WS-SMS-NUM-BLK.                       
            MOVE COMM8-NUM-LEVEL       TO WS-SMS-NUM-HSELVL.                    
            MOVE COMM8-NUM-UNIT-MAIN   TO WS-SMS-NUM-HSEMAIN.                   
            MOVE COMM8-NUM-UNIT-SUB    TO WS-SMS-NUM-HSESUB.                    
            MOVE COMM8-NME-STREET      TO WS-SMS-NME-STREET.                    
            MOVE COMM8-NUM-POSTAL-CODE TO WS-SMS-NUM-PD.                        
            MOVE COMM8-NUM-MGMT        TO WS-SMS-CDE-BO.                        
                                                                                
       2120-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-GET-APPL-INFO.                                                      
      *-------------------------------------------------------------            
            MOVE SPACES               TO WS-APPL-NAME-GRP.                      
            MOVE K800-NUM-REGN        TO K820-NUM-REGN.                         
            MOVE K800-NUM-NRIC1       TO K820-NUM-NRIC.                         
            PERFORM 8250-READ-BP13K820 THRU 8250-EXIT.                          
            IF BP13K820-STATUS = '00'                                           
               MOVE K820-NME-OCCP     TO WS-NME-APPL1                           
            END-IF.                                                             
                                                                                
            IF K800-NUM-NRIC2 > SPACES                                          
               MOVE K800-NUM-REGN      TO K820-NUM-REGN                         
               MOVE K800-NUM-NRIC2     TO K820-NUM-NRIC                         
               PERFORM 8250-READ-BP13K820 THRU 8250-EXIT                        
               IF BP13K820-STATUS = '00'                                        
                  MOVE K820-NME-OCCP     TO WS-NME-APPL2                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF K800-NUM-NRIC3 > SPACES                                          
               MOVE K800-NUM-REGN      TO K820-NUM-REGN                         
               MOVE K800-NUM-NRIC3     TO K820-NUM-NRIC                         
               PERFORM 8250-READ-BP13K820 THRU 8250-EXIT                        
               IF BP13K820-STATUS = '00'                                        
                  MOVE K820-NME-OCCP     TO WS-NME-APPL3                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF K800-NUM-NRIC4 > SPACES                                          
               MOVE K800-NUM-REGN      TO K820-NUM-REGN                         
               MOVE K800-NUM-NRIC4     TO K820-NUM-NRIC                         
               PERFORM 8250-READ-BP13K820 THRU 8250-EXIT                        
               IF BP13K820-STATUS = '00'                                        
                  MOVE K820-NME-OCCP     TO WS-NME-APPL4                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-GET-HRU-INFO.                                                       
      *-------------------------------------------------------------            
           MOVE 'HBO'          TO  WS-HRU-HBO-FL.                               
           MOVE F313-NUM-NRIC  TO  K110-NUM-UIN.                                
                                                                                
           START BB24K110 KEY > K110-KEY-FLD OF BB24K110-REC                    
                 INVALID KEY                                                    
                 MOVE SPACES TO BB24K110-REC.                                   
                                                                                
           DISPLAY 'F313-NNUM-RIC : ' F313-NUM-NRIC.                            
           IF BB24K110-STATUS = '00'                                            
              PERFORM 9000-READ-BB24K110 THRU 9000-EXIT                         
                      UNTIL (F313-NUM-NRIC NOT = K110-NUM-UIN) OR               
                            (K110-NUM-UIN = SPACES) OR                          
                            (K110-CDE-STATUS = 'S')                             
           END-IF.                                                              
                                                                                
           IF F313-NUM-NRIC = K110-NUM-UIN AND                                  
              K110-CDE-STATUS = 'S'        AND                                  
              K110-CDE-ABORT NOT = 'C'                                          
              MOVE K110-NUM-CASE  TO  K010-NUM-CASE                             
              PERFORM 9050-READ-BB24K010 THRU 9050-EXIT                         
              IF K010-DTE-STATUS-UPDATE(06) IS NUMERIC AND                      
                 K010-DTE-STATUS-UPDATE(06) > '00000000'                        
                 MOVE 'HRU'      TO  WS-HRU-HBO-FL                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2400-CHK-SERS-CASE.                                                      
      *-------------------------------------------------------------            
           MOVE  K800-NUM-ALLO-SCHEME  TO  WS-ALLOC-SCH.                        
                                                                                
      *-----------------------------------*                                     
      * SEARCH SCHEME IN ALLOC SCH TABLE  *                                     
      *-----------------------------------*                                     
           IF WS-ALLOC-SCH  NOT =  LOW-VALUES AND SPACES                        
               SET WS-SERS-PTR  TO 1                                            
               SEARCH WS-SERS-SCH                                               
                   AT END                                                       
                   MOVE 'N' TO WS-SERS-CASE                                     
                   WHEN WS-SERS-SCHEME(WS-SERS-PTR) = WS-ALLOC-SCH              
                   MOVE 'Y' TO WS-SERS-CASE                                     
               END-SEARCH                                                       
           ELSE                                                                 
               MOVE 'N' TO WS-SERS-CASE                                         
           END-IF.                                                              
                                                                                
       2400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-FORMAT-LETTER.                                                      
      *-------------------------------------------------------------            
            PERFORM 3100-FORMAT-LN01-LN05  THRU 3100-EXIT.                      
            PERFORM 3200-FORMAT-LN06-LN10  THRU 3200-EXIT.                      
            PERFORM 3300-FORMAT-LN11-LN15  THRU 3300-EXIT.                      
            PERFORM 3400-FORMAT-LN16-LN20  THRU 3400-EXIT.                      
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-FORMAT-LN01-LN05.                                                   
      *-------------------------------------------------------------            
            STRING F313-NUM-SCH-ACC(1:4) '-'                                    
                   F313-NUM-SCH-ACC(5:4) '-'                                    
                   F313-NUM-SCH-ACC(9:1) '-'                                    
                   F313-NUM-SCH-ACC(10:2) DELIMITED BY SIZE                     
                   INTO L384-SMS-SCH-ACC.                                       
                                                                                
            MOVE F313-NUM-REGN           TO L384-REGN-NO.                       
                                                                                
            MOVE K800-NUM-NEW-TOWN       TO L384-NEW-TOWN.                      
            MOVE K800-NUM-FLAT-TYPE      TO L384-FLAT-TYPE.                     
                                                                                
            ACCEPT WS-CURR-YYMMDD        FROM DATE.                             
            IF WS-CURR-YY > 90                                                  
               MOVE '19'                 TO WS-CURR-CC                          
            ELSE                                                                
               MOVE '20'                 TO WS-CURR-CC                          
            END-IF.                                                             
            STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY          
                   DELIMITED BY SIZE INTO L384-CURR-DATE.                       
                                                                                
            MOVE WS-SMS-CDE-BO           TO L384-CDE-BO.                        
            MOVE WS-NME-APPL1            TO L384-NAME1.                         
            MOVE WS-NME-APPL2            TO L384-NAME2.                         
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-FORMAT-LN06-LN10.                                                   
      *-------------------------------------------------------------            
            MOVE WS-NME-APPL3            TO L384-NAME3.                         
            MOVE WS-NME-APPL4            TO L384-NAME4.                         
                                                                                
            IF WS-NME-APPL2 NOT = SPACES OR                                     
               WS-NME-APPL3 NOT = SPACES OR                                     
               WS-NME-APPL4 NOT = SPACES                                        
               MOVE 'have'               TO L384-HAS-HAVE                       
            ELSE                                                                
               MOVE ' has'               TO L384-HAS-HAVE                       
            END-IF.                                                             
                                                                                
            MOVE 1                       TO WS-STRING-POINTER.                  
            STRING WS-NUM-BLK '  #' WS-NUM-HSELVL '-' WS-NUM-HSEMAIN            
                     DELIMITED BY SIZE INTO L384-ADDR-LINE                      
                     WITH POINTER WS-STRING-POINTER.                            
                                                                                
            IF WS-NUM-HSESUB > SPACES                                           
               STRING '-' WS-NUM-HSESUB '  ' WS-NME-STREET ' ' WS-NUM-PD        
                     DELIMITED BY SIZE INTO L384-ADDR-LINE                      
                     WITH POINTER WS-STRING-POINTER                             
            ELSE                                                                
               STRING '  ' WS-NME-STREET ' ' WS-NUM-PD                          
                     DELIMITED BY SIZE INTO L384-ADDR-LINE                      
                     WITH POINTER WS-STRING-POINTER                             
            END-IF.                                                             
                                                                                
            IF K110-DTE-ACCEPTANCE NUMERIC                                      
               MOVE K110-DTE-ACCEPTANCE     TO WS-DATE-SUFFIX                   
               STRING WS-SUFF-DD ' / ' WS-SUFF-MM ' / '                         
                      WS-SUFF-CC WS-SUFF-YY DELIMITED BY SIZE                   
                                          INTO L384-DTE-ACCEPT                  
            ELSE                                                                
               MOVE SPACES                  TO L384-DTE-ACCEPT                  
            END-IF.                                                             
                                                                                
            IF K580-DTE-UNIT-DELIVERY NUMERIC                                   
               MOVE K580-DTE-UNIT-DELIVERY  TO WS-DATE-SUFFIX                   
               STRING WS-SUFF-DD ' / ' WS-SUFF-MM ' / '                         
                      WS-SUFF-CC WS-SUFF-YY DELIMITED BY SIZE                   
                                          INTO L384-DTE-DPD                     
            ELSE                                                                
               MOVE SPACES                  TO L384-DTE-DPD                     
            END-IF.                                                             
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-FORMAT-LN11-LN15.                                                   
      *-------------------------------------------------------------            
            MOVE K024-NME-OIC            TO L384-OIC-NAME.                      
            MOVE K024-NUM-DID            TO L384-OIC-DID.                       
                                                                                
            MOVE 1                       TO WS-STRING-POINTER.                  
            STRING WS-SMS-NUM-BLK '  #' WS-SMS-NUM-HSELVL '-'                   
                   WS-SMS-NUM-HSEMAIN                                           
                   DELIMITED BY SIZE INTO L384-SMS-ADDR-LINE                    
                     WITH POINTER WS-STRING-POINTER.                            
                                                                                
            IF WS-SMS-NUM-HSESUB > SPACES                                       
               STRING '-' WS-SMS-NUM-HSESUB '  '                                
                       WS-SMS-NME-STREET ' ' WS-SMS-NUM-PD                      
                     DELIMITED BY SIZE INTO L384-SMS-ADDR-LINE                  
                     WITH POINTER WS-STRING-POINTER                             
            ELSE                                                                
               STRING '  ' WS-SMS-NME-STREET ' ' WS-SMS-NUM-PD                  
                     DELIMITED BY SIZE INTO L384-SMS-ADDR-LINE                  
                     WITH POINTER WS-STRING-POINTER                             
            END-IF.                                                             
                                                                                
            MOVE K700-NME-LESSEE1        TO L384-SMS-NME-LESSEE1.               
            MOVE K700-NUM-UIN1           TO L384-SMS-UIN-LESSEE1.               
                                                                                
            MOVE K700-NME-LESSEE2        TO L384-SMS-NME-LESSEE2.               
            MOVE K700-NUM-UIN2           TO L384-SMS-UIN-LESSEE2.               
                                                                                
            MOVE K700-NME-LESSEE3        TO L384-SMS-NME-LESSEE3.               
            MOVE K700-NUM-UIN3           TO L384-SMS-UIN-LESSEE3.               
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3400-FORMAT-LN16-LN20.                                                   
      *-------------------------------------------------------------            
            MOVE K700-NME-LESSEE4        TO L384-SMS-NME-LESSEE4.               
            MOVE K700-NUM-UIN4           TO L384-SMS-UIN-LESSEE4.               
                                                                                
       3400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PRINT-LETTER.                                                       
      *-------------------------------------------------------------            
           MOVE DJDE-LETTER-BP1338      TO L384-PRINT-REC.                      
           WRITE L384-PRINT-REC      AFTER ADVANCING PAGE.                      
                                                                                
           ADD   1                      TO WS-CNT-FORMS-PRINT.                  
           WRITE L384-PRINT-REC FROM L384-INFO-LN01                             
                                                AFTER ADVANCING PAGE.           
           WRITE L384-PRINT-REC FROM L384-INFO-LN02  AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN03  AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN04  AFTER 2.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN05  AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN06  AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN07  AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN08  AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN09  AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN10  AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN11  AFTER 9.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN12  AFTER 5.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN13  AFTER 7.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN13A AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN14  AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN14A AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN15  AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN15A AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN16  AFTER 1.                   
           WRITE L384-PRINT-REC FROM L384-INFO-LN16A AFTER 1.                   
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8100-READ-BP13F313.                                                      
      *-------------------------------------------------------------            
           READ BP13F313                                                        
                AT END MOVE 'Y'  TO  WS-F313-EOF                                
                       GO TO 8100-EXIT.                                         
                                                                                
           ADD 1                 TO WS-CNT-F313-READ.                           
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8200-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           READ BP13K800                                                        
           INVALID KEY DISPLAY 'K800 NOT FND : ' F313-NUM-REGN                  
                       MOVE 'Y'             TO WS-ERROR-FL                      
                       GO TO 8200-EXIT.                                         
                                                                                
           IF BP13K800-STATUS NOT = '00'                                        
              DISPLAY 'BP13K800 READ ERROR: ' BP13K800-STATUS                   
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8250-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
           READ BP13K820                                                        
           INVALID KEY DISPLAY 'K820 NOT FND : ' F313-NUM-REGN                  
                       MOVE 'Y'             TO WS-ERROR-FL                      
                       GO TO 8250-EXIT.                                         
                                                                                
           IF BP13K820-STATUS NOT = '00'                                        
              DISPLAY 'BP13K820 READ ERROR: ' BP13K820-STATUS                   
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
       8250-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8300-LINK-BP13C913.                                                      
      *-------------------------------------------------------------            
      *    TEST                                                                 
      *    move '913711366' to ws-link-rec.                                     
           CALL 'BP13C913' USING                                                
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR  TO WS-SQL-CODE                             
              DISPLAY 'ERROR READING PIDB TABLE' WS-SQL-CODE                    
              MOVE SPACES            TO BP13COMM8-REC                           
              MOVE 'Y'               TO WS-ERROR-FL                             
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           ELSE                                                                 
              IF COMM8-NUM-BLDNG-GL    = SPACES AND                             
                 COMM8-NUM-BLK         = SPACES AND                             
                 COMM8-NUM-LEVEL       = SPACES AND                             
                 COMM8-NUM-UNIT-MAIN   = SPACES AND                             
                 COMM8-NUM-UNIT-SUB    = SPACES AND                             
                 COMM8-NME-STREET      = SPACES AND                             
                 COMM8-NUM-POSTAL-CODE = SPACES                                 
                 DISPLAY 'SCH NOT FOUND IN PIDB TABLE ' F313-NUM-REGN           
                 MOVE SPACES        TO BP13COMM8-REC                            
                 MOVE 'Y'           TO WS-ERROR-FL                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8400-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           READ BM06K110                                                        
           INVALID KEY DISPLAY 'K110 NOT FND : ' F313-NUM-REGN                  
                       MOVE 'Y'             TO WS-ERROR-FL                      
                       GO TO 8400-EXIT.                                         
                                                                                
           IF BM06K110-STATUS NOT = '00'                                        
              DISPLAY 'BM06K110 READ ERROR: ' BM06K110-STATUS                   
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
       8400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8450-READ-BM06K100.                                                      
      *-------------------------------------------------------------            
           READ BM06K100                                                        
           INVALID KEY DISPLAY 'K100 NOT FND : ' F313-NUM-REGN                  
                       MOVE 'Y'             TO WS-ERROR-FL                      
                       GO TO 8450-EXIT.                                         
                                                                                
           IF BM06K100-STATUS NOT = '00'                                        
              DISPLAY 'BM06K100 READ ERROR: ' BM06K100-STATUS                   
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
       8450-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8500-READ-AB03K700.                                                      
      *-------------------------------------------------------------            
           READ AB03K700                                                        
           INVALID KEY DISPLAY 'K700 NOT FND : ' F313-NUM-REGN                  
                       MOVE 'Y'             TO WS-ERROR-FL                      
                       GO TO 8500-EXIT.                                         
                                                                                
           IF AB03K700-STATUS NOT = '00'                                        
              DISPLAY 'AB03K700 READ ERROR: ' AB03K700-STATUS                   
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
       8500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8600-READ-AB03K030.                                                      
      *-------------------------------------------------------------            
           READ AB03K030                                                        
           INVALID KEY DISPLAY 'K030 NOT FOUND: ' F313-NUM-REGN                 
                       MOVE 'Y'        TO WS-ERROR-FL                           
                       GO TO 8600-EXIT.                                         
                                                                                
           IF AB03K030-STATUS NOT = '00'                                        
              DISPLAY 'AB03K030 READ ERROR: ' AB03K030-STATUS                   
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
       8600-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8700-READ-BB14K580.                                                      
      *-------------------------------------------------------------            
           READ BB14K580                                                        
           INVALID KEY DISPLAY 'K580 NOT FOUND: ' F313-NUM-REGN                 
                       MOVE 'Y'        TO WS-ERROR-FL                           
                       GO TO 8700-EXIT.                                         
                                                                                
           IF BB14K580-STATUS NOT = '00'                                        
              DISPLAY 'BB14K580 READ ERROR: ' BB14K580-STATUS                   
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
       8700-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8800-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
            READ  BP13K022 KEY IS K022-NUM-REGN.                                
                                                                                
            EVALUATE BP13K022-STATUS                                            
            WHEN  00                                                            
                  MOVE K022-NUM-OIC      TO K024-NUM-OIC                        
                  PERFORM 8810-READ-BP13K024  THRU 8810-EXIT                    
            WHEN  23                                                            
                  DISPLAY 'K022 RECORD NOT FND ' K022-NUM-REGN                  
            WHEN  OTHER                                                         
                  DISPLAY 'ERROR READING K022 ' BP13K022-STATUS                 
                  PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                   
            END-EVALUATE.                                                       
                                                                                
       8800-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8810-READ-BP13K024.                                                      
      *-------------------------------------------------------------            
            READ  BP13K024.                                                     
                                                                                
            EVALUATE BP13K024-STATUS                                            
            WHEN  00                                                            
                  CONTINUE                                                      
            WHEN  23                                                            
                  DISPLAY 'K024 RECORD NOT FND ' K024-NUM-OIC                   
            WHEN  OTHER                                                         
                  DISPLAY 'ERROR READING K024 ' BP13K024-STATUS                 
                  PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                   
            END-EVALUATE.                                                       
                                                                                
       8810-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8900-READ-BB14K500.                                                      
      *-------------------------------------------------------------            
           READ BB14K500                                                        
           INVALID KEY DISPLAY 'K500 NOT FOUND: ' F313-NUM-REGN                 
                       MOVE SPACES     TO BB14K500-REC                          
                       MOVE 'Y'        TO WS-ERROR-FL                           
                       GO TO 8900-EXIT.                                         
                                                                                
           IF BB14K500-STATUS NOT = '00'                                        
              DISPLAY 'BB14K500 READ ERROR: ' BB14K500-STATUS                   
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
       8900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-READ-BB24K110.                                                      
      *-------------------------------------------------------------            
           READ BB24K110 NEXT AT END                                            
                       DISPLAY 'BB24K110 NOT FOUND: ' F313-NUM-NRIC             
                       MOVE SPACES    TO BB24K110-REC                           
                       GO TO 9000-EXIT.                                         
                                                                                
           DISPLAY  F313-NUM-NRIC ' = ' K110-NUM-UIN.                           
           DISPLAY  K110-CDE-STATUS                                             
                    K110-CDE-ABORT.                                             
                                                                                
           IF K110-CDE-ABORT = 'C'                                              
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF BB24K110-STATUS NOT = '00'                                        
              DISPLAY 'BB24K110 READ ERROR: ' bb24k110-STATUS                   
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9050-READ-BB24K010.                                                      
      *-------------------------------------------------------------            
           READ BB24K010                                                        
           INVALID KEY DISPLAY 'BB24K010 NOT FOUND: ' F313-NUM-NRIC             
                       MOVE SPACES     TO BB24K010-REC                          
                       MOVE 'Y'        TO WS-ERROR-FL                           
                       GO TO 9050-EXIT.                                         
                                                                                
           IF BB24K010-STATUS NOT = '00'                                        
              DISPLAY 'BB24K010 READ ERROR: ' BB24K010-STATUS                   
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
       9050-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9900-HANDLE-ABEND.                                                       
      *-------------------------------------------------------------            
           DISPLAY  SPACES.                                                     
           DISPLAY '- BP13C384 ABEND TOTAL ----------------'.                   
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13F313 RECS READ  = ' WS-CNT-F313-READ.             
           DISPLAY 'TOTAL LETTERS WRITTEN     = ' WS-CNT-FORMS-PRINT.           
                                                                                
           CLOSE BP13F313 BP13K800 BP13K820 BM06K100 BM06K110                   
                 AB03K700 AB03K030 BB14K500 BB14K580 BP13K022 BP13K024          
                 BP13L384.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-WRAP-UP.                                                            
      *-------------------------------------------------------------            
           DISPLAY  SPACES.                                                     
           DISPLAY '- BP13C384 CONTROL TOTAL --------------'.                   
           DISPLAY  SPACES.                                                     
           DISPLAY ' TOTAL BP13F313 RECS READ  = ' WS-CNT-F313-READ.            
           DISPLAY ' TOTAL LETTERS WRITTEN     = ' WS-CNT-FORMS-PRINT.          
           DISPLAY '---------------------------------------'.                   
                                                                                
           CLOSE BP13F313 BP13K800 BP13K820 BM06K100 BM06K110                   
                 AB03K700 AB03K030 BB14K500 BB14K580 BP13K022 BP13K024          
                 BP13L384.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
