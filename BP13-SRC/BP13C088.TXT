       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C088.                                                 
      *AUTHOR.        BALAKRISHNAN S.                                           
      *DATE-WRITTEN.  12/06/2000.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE : UPD BP13K820-INFO WHICH ARE NOT UPDATED IN ORSA*            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     OIC   DESCRPTIONS                        *            
      * -------- -------- ----- -----------                        *            
      * BP130749 20000609 SB    NEW PROGRAM                        *            
      * BP130749 20000914 SSS   ADD BP13K840, DELETE BP13F310      *            
      * BP130978 20010316 LSB   IF FD IN CDB, DO NOT GET FR MHA    *            
      * BP132626 20041124 SK    CHANGE DTE-REGN TO DTE-REQUEST     *            
      * BP13XXXX 20100702 ZDD1  TO STOP READNEXT IF REGNUM IS NOT  *            
      *                         EQUAL                              *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS BP13K820-STATUS.                           
                                                                                
           SELECT BP13K825 ASSIGN TO BP13K825                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K825-KEY-FLD                               
                  FILE STATUS     IS BP13K825-STATUS.                           
                                                                                
           SELECT BP13K840  ASSIGN TO BP13K840                                  
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K840-NUM-REGN                              
                  FILE STATUS     IS BP13K840-STATUS.                           
                                                                                
           SELECT BP13L088 ASSIGN TO BP13L088.                                  
                                                                                
      ****************************************************************          
       DATA DIVISION.                                                           
      ****************************************************************          
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F500     BLOCK CONTAINS 0 RECORDS                               
                         RECORD CONTAINS 500 CHARACTERS                         
                         LABEL RECORDS ARE STANDARD                             
                         RECORDING MODE IS F.                                   
       COPY BP13F500.                                                           
                                                                                
       FD   BP13K825.                                                           
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K820.                                                           
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K840.                                                            
       COPY BP13K840.                                                           
                                                                                
       FD   BP13L088     RECORDING MODE  IS  F                                  
                         RECORD CONTAINS 132 CHARACTERS.                        
       01   L088-PRTREC             PIC X(132).                                 
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K825-STATUS                PIC 9(2)  VALUE ZEROES.               
       01  BP13K820-STATUS                PIC 9(2)  VALUE ZEROES.               
       01  BP13K840-STATUS                PIC 9(2)  VALUE ZEROES.               
                                                                                
       01  WS-DATE                        PIC X(8)  VALUE SPACES.               
       01  WS-K840-NUM-NRIC               PIC X(9)  VALUE SPACES.               
                                                                                
       01  WS-FILE-COUNTERS.                                                    
           05  BP13F500-READ              PIC 9(8)  VALUE ZEROES.               
           05  BP13K825-READ              PIC 9(8)  VALUE ZEROES.               
           05  BP13K820-UPDT              PIC 9(8)  VALUE ZEROES.               
           05  BP13K840-WRITE             PIC 9(8)  VALUE ZEROES.               
           05  BP13L088-PRTED             PIC 9(8)  VALUE ZEROES.               
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  BP13K825-EOF-FLAG          PIC X(01) VALUE SPACES.               
               88 BP13K825-EOF                      VALUE 'Y'.                  
           05  BP13K820-NRIC-UPD-FLAG     PIC X(01) VALUE SPACES.               
               88 BP13K820-NRIC-UPD                 VALUE 'Y'.                  
                                                                                
       01  WS-LINE-CNT                    PIC 9(2)  VALUE 77.                   
       01  WS-PAGE-CNT                    PIC 9(5)  VALUE ZEROES.               
       01  WS-SNO                         PIC 9(5)  VALUE ZEROES.               
                                                                                
                                                                                
      * REPORT LAYOUT                                                           
      *-------------------------------------------------------------            
       01  L088-HEADER1.                                                        
           05 FILLER                   PIC X(55)  VALUE SPACES.                 
           05 FILLER                   PIC X(20)  VALUE                         
              'SYSTEM OF COMMITMENT'.                                           
           05 FILLER                   PIC X(38)  VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE 'DATE : '.              
           05 L088-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  L088-HEADER2.                                                        
           05 FILLER                   PIC X(40)  VALUE SPACES.                 
           05 FILLER                   PIC X(50)  VALUE                         
              ' BEFORE & AFTER ON RECORD UPDATED TO BP13K820 '.                 
           05 FILLER                   PIC X(23)  VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE 'PAGE : '.              
           05 L088-PAGE                PIC ZZZ9   VALUE ZEROS.                  
                                                                                
       01  L088-HEADER3.                                                        
           05 FILLER                   PIC X(5)   VALUE 'S/NO.'.                
           05 FILLER                   PIC X(6)   VALUE SPACES.                 
           05 FILLER                   PIC X(8)   VALUE 'REGN'.                 
           05 FILLER                   PIC X(3)   VALUE SPACES.                 
           05 FILLER                   PIC X(66)  VALUE                         
              'BEFORE / AFTER  OCCUPIER NAME'.                                  
           05 FILLER                   PIC X(11)  VALUE '  ETHNIC'.             
           05 FILLER                   PIC X(13)  VALUE 'NATIONALITY'.          
           05 FILLER                   PIC X(10)  VALUE 'DOB'.                  
                                                                                
       01  L088-HEADER4.                                                        
           05 FILLER                   PIC X(130) VALUE ALL '-'.                
                                                                                
       01  L088-DETAIL1.                                                        
           05 L088-SNO                 PIC Z(5)   VALUE ZEROS.                  
           05 FILLER                   PIC X(6)   VALUE SPACES.                 
           05 L088-NUM-REGN            PIC X(8)   VALUE SPACES.                 
           05 FILLER                   PIC X(3)   VALUE SPACES.                 
           05 L088-NME-OCCP-B          PIC X(66)  VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE SPACES.                 
           05 L088-NUM-ETHNIC-B        PIC X      VALUE SPACE.                  
           05 FILLER                   PIC X(7)   VALUE SPACES.                 
           05 L088-NUM-NATIONALITY-B   PIC XX     VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE SPACES.                 
           05 L088-DTE-BIRTH-B         PIC X(8)   VALUE SPACES.                 
                                                                                
       01  L088-DETAIL2.                                                        
           05 FILLER                   PIC X(11)  VALUE SPACES.                 
           05 L088-NUM-NRIC            PIC X(9)   VALUE SPACES.                 
           05 FILLER                   PIC X(2)   VALUE SPACES.                 
           05 L088-NME-OCCP-A          PIC X(66)  VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE SPACES.                 
           05 L088-NUM-ETHNIC-A        PIC X      VALUE SPACE.                  
           05 FILLER                   PIC X(7)   VALUE SPACES.                 
           05 L088-NUM-NATIONALITY-A   PIC XX     VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE SPACES.                 
           05 L088-DTE-BIRTH-A         PIC X(8)   VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *==============================================================*          
       0000-MAIN.                                                               
      *==============================================================*          
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
                                                                                
           MOVE SPACES                    TO F500-NUM-REGN.                     
           PERFORM 2000-MAIN-ROUTINE      THRU 2000-EXIT                        
                   UNTIL F500-NUM-REGN =  HIGH-VALUES.                          
                                                                                
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==============================================================*          
       1000-OPEN-FILES.                                                         
      *==============================================================*          
                                                                                
           OPEN INPUT  BP13F500                                                 
                       BP13K825                                                 
                I-O    BP13K820                                                 
                I-O    BP13K840                                                 
               OUTPUT  BP13L088.                                                
                                                                                
           IF BP13K820-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K820 OPEN ERROR STATUS ' BP13K820-STATUS             
              MOVE BP13K820-STATUS         TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13K825-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K825 OPEN ERROR STATUS ' BP13K825-STATUS             
              MOVE BP13K825-STATUS         TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13K840-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K840 OPEN ERROR STATUS ' BP13K840-STATUS             
              MOVE BP13K840-STATUS         TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO L088-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==============================================================*          
       2000-MAIN-ROUTINE.                                                       
      *==============================================================*          
      * PROCESS :                                                    *          
      *                                                              *          
      *   1. SEQUENTIALY READ REGN ONE BY ONE IN BP13F500.           *          
      *                                                              *          
      *   2. UPDATE BP13K825 INFO INTO BP13K820 FOR EACH NRIC (BP13- *          
      *      K825) OF F500-REGN.                                     *          
      *                                                              *          
      *--------------------------------------------------------------*          
                                                                                
           READ BP13F500 AT END                                                 
                   MOVE HIGH-VALUES       TO F500-NUM-REGN                      
                   GO TO 2000-EXIT.                                             
                                                                                
           ADD  1 TO BP13F500-READ.                                             
                                                                                
           PERFORM 3000-PROCESS-RECS      THRU 3000-EXIT.                       
                                                                                
           PERFORM 5000-WRITE-BP13K840    THRU 5000-EXIT.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==============================================================*          
       3000-PROCESS-RECS.                                                       
      *==============================================================*          
      * READ ALL NRIC OF REGN AND UPDATE.                            *          
      *--------------------------------------------------------------*          
           MOVE SPACES              TO K825-KEY-FLD                             
                                       BP13K825-EOF-FLAG.                       
           MOVE F500-NUM-REGN       TO K825-NUM-REGN.                           
                                                                                
           START BP13K825  KEY >= K825-NUM-REGN.                                
                                                                                
           EVALUATE BP13K825-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
                PERFORM 3200-READNEXT-BP13K825 THRU 3200-EXIT                   
                             UNTIL BP13K825-EOF OR                              
                             K825-NUM-REGN NOT = F500-NUM-REGN                  
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY "START BP13K825 FAIL STATUS " BP13K825-STATUS           
                " KEY " F500-NUM-REGN                                           
                PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==============================================================*          
       3200-READNEXT-BP13K825.                                                  
      *==============================================================*          
                                                                                
           READ BP13K825 NEXT RECORD AT END                                     
                         MOVE 'Y' TO BP13K825-EOF-FLAG                          
                         GO TO 3200-EXIT.                                       
                                                                                
           IF K825-NUM-REGN = F500-NUM-REGN                                     
              ADD 1 TO BP13K825-READ                                            
              PERFORM 3500-PROCESS-BP13K820 THRU 3500-EXIT                      
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==============================================================*          
       3500-PROCESS-BP13K820.                                                   
      *==============================================================*          
                                                                                
           MOVE SPACES       TO BP13K820-REC.                                   
           MOVE K825-KEY-FLD TO K820-KEY-FLD.                                   
           READ BP13K820.                                                       
                                                                                
           EVALUATE BP13K820-STATUS                                             
           WHEN 00                                                              
             PERFORM 3600-UPDATE-BP13K820 THRU 3600-EXIT                        
           WHEN 23                                                              
             CONTINUE                                                           
           WHEN OTHER                                                           
             DISPLAY "BP13K820, READ FAIL STATUS " BP13K820-STATUS              
                     " KEY " K820-KEY-FLD                                       
             PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==============================================================*          
       3600-UPDATE-BP13K820.                                                    
      *==============================================================*          
           MOVE SPACES  TO BP13K820-NRIC-UPD-FLAG.                              
           MOVE SPACES  TO L088-DETAIL1                                         
                           L088-DETAIL2.                                        
                                                                                
           IF K820-NUM-ETHNIC = SPACES AND                                      
                                K825-NUM-ETHNIC NOT = SPACES                    
              MOVE K820-NUM-ETHNIC        TO L088-NUM-ETHNIC-B                  
              MOVE K825-NUM-ETHNIC        TO K820-NUM-ETHNIC                    
                                             L088-NUM-ETHNIC-A                  
              MOVE 'Y'  TO BP13K820-NRIC-UPD-FLAG                               
           END-IF.                                                              
                                                                                
           IF K820-NUM-NATIONALITY = SPACES AND                                 
                                K825-NUM-NATIONALITY NOT = SPACES               
              MOVE K820-NUM-NATIONALITY   TO L088-NUM-NATIONALITY-B             
              MOVE K825-NUM-NATIONALITY   TO K820-NUM-NATIONALITY               
                                             L088-NUM-NATIONALITY-A             
              MOVE 'Y'  TO BP13K820-NRIC-UPD-FLAG                               
           END-IF.                                                              
                                                                                
           IF K820-NME-OCCP = SPACES AND                                        
                               K825-NME-OCCP NOT = SPACES                       
              MOVE K820-NME-OCCP          TO L088-NME-OCCP-B                    
              MOVE K825-NME-OCCP          TO K820-NME-OCCP                      
                                             L088-NME-OCCP-A                    
              MOVE 'Y'  TO BP13K820-NRIC-UPD-FLAG                               
           END-IF.                                                              
                                                                                
           IF K820-DTE-BIRTH = SPACES AND                                       
                            K825-DTE-BIRTH NOT = SPACES                         
              MOVE K820-DTE-BIRTH         TO L088-DTE-BIRTH-B                   
              MOVE K825-DTE-BIRTH         TO K820-DTE-BIRTH                     
                                             L088-DTE-BIRTH-A                   
              MOVE 'Y'  TO BP13K820-NRIC-UPD-FLAG                               
           END-IF.                                                              
                                                                                
           IF BP13K820-NRIC-UPD-FLAG = 'Y'                                      
              PERFORM 6000-PRINT-BP13L088 THRU 6000-EXIT                        
              REWRITE BP13K820-REC                                              
                                                                                
              IF BP13K820-STATUS = 00                                           
                 ADD 1     TO BP13K820-UPDT                                     
              ELSE                                                              
                DISPLAY "BP13K820, REWRITE FAIL STATUS " BP13K820-STATUS        
                        " KEY " K820-KEY-FLD                                    
                PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       5000-WRITE-BP13K840.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES              TO BP13K840-REC                             
                                       WS-K840-NUM-NRIC.                        
                                                                                
           MOVE F500-NUM-REGN       TO K840-NUM-REGN.                           
                                                                                
           STRING '#' K840-NUM-REGN                                             
                  DELIMITED BY SIZE INTO WS-K840-NUM-NRIC.                      
           MOVE WS-K840-NUM-NRIC    TO K840-NUM-NRIC1                           
                                       K840-NUM-NRIC2                           
                                       K840-NUM-NRIC3                           
                                       K840-NUM-NRIC4.                          
                                                                                
           MOVE F500-CDE-ZONE       TO K840-NUM-ZONE.                           
           MOVE F500-CDE-NT1        TO K840-NUM-NEW-TOWN.                       
           MOVE F500-CDE-FLAT-TYPE  TO K840-NUM-FLAT-TYPE.                      
           MOVE F500-DTE-REQUEST    TO K840-DTE-REGN.                           
           MOVE WS-DATE             TO K840-DTE-UPDATE.                         
           MOVE F500-CDE-ALLOC-SCH  TO K840-NUM-ALLOC-SCH.                      
           MOVE 'P13C088'           TO K840-NUM-USERID.                         
                                                                                
           WRITE BP13K840-REC.                                                  
                                                                                
           IF BP13K840-STATUS = 0                                               
              ADD 1 TO BP13K840-WRITE                                           
           ELSE                                                                 
           IF BP13K840-STATUS = 22                                              
              DISPLAY ' ERROR WRITING BP13K840, STATUS '                        
                      BP13K840-STATUS '  KEY : ' K840-KEY-FLD                   
           ELSE                                                                 
              DISPLAY ' ERROR WRITING BP13K840 ' BP13K840-STATUS                
              MOVE BP13K840-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       6000-PRINT-BP13L088.                                                     
      *-------------------------------------------------------------            
           ADD  1             TO WS-SNO.                                        
           MOVE WS-SNO        TO L088-SNO.                                      
           MOVE F500-NUM-REGN TO L088-NUM-REGN.                                 
                                                                                
           MOVE K820-NUM-NRIC TO L088-NUM-NRIC.                                 
                                                                                
           IF WS-LINE-CNT > 55                                                  
              PERFORM 6500-PRINT-HEADING THRU 6500-EXIT.                        
                                                                                
           WRITE L088-PRTREC   FROM L088-DETAIL1 AFTER 2.                       
           WRITE L088-PRTREC   FROM L088-DETAIL2.                               
           MOVE  SPACES        TO   L088-PRTREC.                                
                                                                                
           ADD 1 TO BP13L088-PRTED.                                             
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       6500-PRINT-HEADING.                                                      
      *-------------------------------------------------------------            
           ADD  1 TO WS-PAGE-CNT.                                               
           MOVE 6 TO WS-LINE-CNT.                                               
           MOVE WS-PAGE-CNT TO L088-PAGE.                                       
                                                                                
           WRITE L088-PRTREC   FROM L088-HEADER1.                               
           WRITE L088-PRTREC   FROM L088-HEADER2.                               
           WRITE L088-PRTREC   FROM L088-HEADER3 AFTER 2.                       
           WRITE L088-PRTREC   FROM L088-HEADER4.                               
           MOVE  SPACES        TO   L088-PRTREC.                                
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       9000-CLOSE-FILES.                                                        
      *==============================================================*          
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' BP13C088 CONTROL TOTAL        DATE : ' WS-DATE.            
           DISPLAY '------------------------------------------------'.          
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECS READ FROM BP13F500  : ' BP13F500-READ.           
           DISPLAY 'NO OF REGN FOUND IN BP13K825   : ' BP13K825-READ.           
           DISPLAY 'NO OF RECS UPDATED IN BP13K820 : ' BP13K820-UPDT.           
           DISPLAY 'NO OF RECS UPDATED IN BP13K840 : ' BP13K840-WRITE.          
           DISPLAY 'NO OF RECS PRINTED IN BP13L088 : ' BP13L088-PRTED.          
           DISPLAY '------------------------------------------------'.          
                                                                                
           CLOSE BP13F500                                                       
                 BP13K820                                                       
                 BP13K825                                                       
                 BP13K840                                                       
                 BP13L088.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
