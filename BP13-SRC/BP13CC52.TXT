      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CC52.                                                 
      *AUTHOR.        EDEN ROSE DINGAL.                                         
      *DATE-WRITTEN.  4 DEC 2006.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (BP13)                                *           
      * =========================================================== *           
      *  OBJECTIVES : 1) TO READ BP13FD00 AND PRINT REPORT          *           
      *               2) TO PRINT 1ST LETTER REMINDERS              *           
      *                  AND CONTROL REPORT                         *           
      *               3) UPDATE ARREARS TODATE IF FOUND IN BP13KD76 *           
      *               4) CREATE/UPDATE RECORD IN BP13KD76           *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13FD00                                *           
      *                 2.  BP13K800                                *           
      *                 3.  BP13K820                                *           
      *                 4.  BP13K830                                *           
      *                 5.  AC15K010                                *           
      *                 6.  SY02F001                                *           
      *                                                             *           
      * OUTPUT       :  1.  BP13KD76                                *           
      *                 2.  BP13LC52                                *           
      *                 3.  BP13PC52                                *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE     BY   DESCRIPTION                          *           
      * -------- ------   ---  -----------                          *           
      * BP132936 20061201 ED1  NEW PGM                              *           
      * BP133051 20070511 ED1  PRINT LETTER IF MTH-INST >1 ONLY     *           
      * BP133091 20071017 RB12 EXPAND BP13FD00 FR 1200 TO 1600 BYTES*           
      * BP133209 20080220 ED1  TO PRINT LETR FOR NON-GIRO CASES ONLY*           
      * =========================================================== *           
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
         SOURCE-COMPUTER.   IBM-3090.                                           
         OBJECT-COMPUTER.   IBM-3090.                                           
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
         FILE-CONTROL.                                                          
           SELECT BP13FD00 ASSIGN TO BP13FD00.                                  
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT AC15K010 ASSIGN TO AC15K010                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K010-ACCESS-KEY                                 
                FILE STATUS  IS WS-K010-STATUS.                                 
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K800-NUM-REGN                                   
                FILE STATUS  IS WS-K800-STATUS.                                 
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K820-KEY-FLD                                    
                FILE STATUS  IS WS-K820-STATUS.                                 
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K830-KEY-FLD                                    
                ALTERNATE RECORD KEY IS K830-NUM-REGN                           
                FILE STATUS  IS WS-K830-STATUS.                                 
                                                                                
           SELECT BP13KD76 ASSIGN TO BP13KD76                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD76-KEY-FLD                                    
                FILE STATUS  IS WS-KD76-STATUS.                                 
                                                                                
           SELECT BP13LC52  ASSIGN TO BP13LC52.                                 
           SELECT BP13PC52 ASSIGN  TO BP13PC52.                                 
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD   BP13FD00    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 1600 CHARACTERS                         
                        LABEL RECORDS ARE STANDARD                              
                        RECORDING MODE IS F.                                    
       COPY BP13FD00.                                                           
                                                                                
       FD   BP13K800    RECORD CONTAINS 2000 CHARACTERS.                        
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820    RECORD CONTAINS 400 CHARACTERS.                         
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830    RECORD CONTAINS 250 CHARACTERS.                         
       COPY BP13K830.                                                           
                                                                                
       FD   BP13KD76    RECORD CONTAINS 300 CHARACTERS.                         
       COPY BP13KD76.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   AC15K010    RECORD CONTAINS 200  CHARACTERS.                        
       COPY AC15K010.                                                           
                                                                                
       FD   BP13LC52    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 132 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  LC52-PRTREC  PIC X(132).                                             
                                                                                
       FD  BP13PC52     RECORD CONTAINS 90 CHARACTERS                           
                        RECORDING MODE IS F.                                    
       01  PC52-PRTREC  PIC X(90).                                              
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-COUNTERS.                                                         
           05 WS-CNT-FD00-READ       PIC 9(7) VALUE ZEROES.                     
           05 WS-CNT-LC52-WRITE      PIC 9(7) VALUE ZEROES.                     
           05 WS-PC52-WRT-CTR        PIC 9(7) VALUE ZEROES.                     
           05 WS-SNO                 PIC 9(5) VALUE ZEROES.                     
           05 WS-PAGE-CTR            PIC 9(4) VALUE ZEROES.                     
           05 WS-LINE-CTR            PIC 9(2) VALUE ZEROES.                     
           05 WS-KD76-WRITE          PIC 9(7) VALUE ZEROES.                     
           05 WS-KD76-REWRITE        PIC 9(7) VALUE ZEROES.                     
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-K800-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K820-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K830-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K010-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-KD76-STATUS         PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-FLAGS.                                                            
           05 WS-FLG-FD00-EOF        PIC X    VALUE 'N'.                        
                                                                                
       01  WS-DATE-HDR               PIC X(10) VALUE SPACES.                    
       01  WS-MAX-LINE               PIC 99    VALUE 53.                        
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY               PIC X(4).                                  
           05  WS-MM                 PIC X(2).                                  
           05  WS-MM-R REDEFINES WS-MM.                                         
               10 WS-MTH             PIC 9(2).                                  
           05  WS-DD                 PIC X(2).                                  
                                                                                
       01  WS-TOTAL              PIC      9(7)V99 VALUE ZEROES.                 
                                                                                
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                        01010000
       01  WS-BLANK-LINE  PIC X(132)             VALUE SPACES.                  
                                                                                
       01  WS-DASH-LINE   PIC X(132)             VALUE ALL '-'.                 
                                                                                
       01  LC52-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LC52'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC52-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC52-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  LC52-HEADER02.                                                       
           05  FILLER              PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(42)     VALUE                          
               'REPORT ON ACCOUNTS WITH 1ST REMINDERS FOR '.                    
           05  FILLER              PIC X(13)     VALUE                          
               'THE MONTH OF '.                                                 
           05  LC52-MMM-HDR        PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X         VALUE SPACE.                   
           05  LC52-YR-HDR         PIC X(04)     VALUE SPACES.                  
                                                                                
       01  LC52-HEADER03.                                                       
           05  FILLER              PIC X(86)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'MTH IN'.                
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'AMT IN'.                
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'LATE PAYMENT'.          
                                                                                
       01  LC52-HEADER04.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'NAME/ADDRESS'.          
           05  FILLER              PIC X(58)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'ARREARS'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'ARREARS($)'.            
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'CHARGE($)'.             
                                                                                
       01 LC52-DETAILS.                                                         
          05 LC52-DTL-SERIAL      PIC ZZZZ9     VALUE ZEROES.                   
          05 FILLER               PIC X(01)     VALUE SPACES.                   
          05 LC52-REGNO           PIC X(08)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC52-NAME1           PIC X(66)     VALUE SPACES.                   
          05 FILLER               PIC X(04)     VALUE SPACES.                   
          05 LC52-MO-ARREAR       PIC     Z9.99 VALUE ZEROES.                   
          05 FILLER               PIC X(04)     VALUE SPACES.                   
          05 LC52-AMT-ARREAR      PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(04)     VALUE SPACES.                   
          05 LC52-LTE-PYMNT-CHRGE PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
                                                                                
       01 LC52-DETAIL2.                                                         
          05 FILLER               PIC X(16)     VALUE SPACES.                   
          05 LC52-NAME2           PIC X(60)     VALUE SPACES.                   
                                                                                
       01 LC52-DETAIL3.                                                         
          05 FILLER               PIC X(16)     VALUE SPACES.                   
          05 LC52-ADDRESS         PIC X(90)     VALUE SPACES.                   
                                                                                
                                                                                
      *--> DETAIL LINES FOR LETTER                                              
                                                                                
       01  PC52-PR-LINE-1.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC52-HDBREF              PIC X(14)  VALUE SPACES.                
                                                                                
       01  PC52-PR-LINE-2.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC52-REGNO               PIC X(08)  VALUE SPACES.                
                                                                                
       01  PC52-PR-LINE-3.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC52-DATE-HDR            PIC X(10)  VALUE SPACES.                
                                                                                
       01  PC52-PR-LINE-4.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC52-NAME1B              PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC52-PR-LINE-5.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC52-NAME2B              PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC52-PR-LINE-6.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC52-BLK                 PIC X(16)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
           05  PC52-UNIT                PIC X(11)  VALUE SPACES.                
                                                                                
       01  PC52-PR-LINE-7.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC52-ADDR1               PIC X(65)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
                                                                                
       01  PC52-PR-LINE-8.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC52-ADDR2               PIC X(65)  VALUE SPACES.                
                                                                                
       01  PC52-PR-LINE-9.                                                      
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  PC52-BAR-CODE            PIC X(09)  VALUE SPACES.                
                                                                                
       01  PC52-PR-LINE-10.                                                     
           05  FILLER                   PIC X(11)  VALUE SPACES.                
           05  PC52-FLAT-ADDR           PIC X(70)  VALUE SPACES.                
                                                                                
       01  PC52-PR-LINE-11.                                                     
           05  FILLER                   PIC X(33)  VALUE SPACES.                
           05  PC52-AS-AT-DTE           PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(07)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE '$  '.                 
           05  PC52-LOAN-INST           PIC ZZZ,ZZ9.99 VALUE ZEROES.            
                                                                                
       01  PC52-PR-LINE-12.                                                     
           05  FILLER                   PIC X(38)  VALUE SPACES.                
           05  PC52-AS-AT-DTE2          PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE '$  '.                 
           05  PC52-LTE-PYMNT-CHRGE     PIC ZZZ,ZZ9.99 VALUE ZEROES.            
                                                                                
       01  PC52-PR-LINE-13.                                                     
           05  FILLER                   PIC X(50)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE '$'.                   
           05  PC52-TOTAL               PIC Z,ZZZ,ZZ9.99 VALUE ZEROES.          
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(04).                             
           05 WS-LINK-NUM-ACC            PIC X(04).                             
           05 WS-LINK-NUM-CHK            PIC X(01).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-START-UP          THRU 1000-EXIT.                       
           PERFORM 2000-FD00-READ         THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
                   UNTIL WS-FLG-FD00-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       1000-START-UP.                                                           
      *--------------------------------------------------------------           
           OPEN INPUT  BP13FD00                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                       AC15K010                                                 
                       SY02F001                                                 
                I-O    BP13KD76                                                 
               OUTPUT  BP13LC52                                                 
                       BP13PC52.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING AC15K010 FILE ERROR ' WS-K010-STATUS             
              MOVE     WS-K010-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE     WS-K820-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K830 FILE ERROR ' WS-K830-STATUS             
              MOVE     WS-K830-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD76-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD76 FILE ERROR ' WS-KD76-STATUS             
              MOVE     WS-KD76-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT TO WS-DATE.                                    
                                                                                
           STRING WS-DD '/' WS-MM '/' WS-CCYY                                   
           DELIMITED BY SIZE                   INTO WS-DATE-HDR.                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       2000-FD00-READ.                                                          
      *--------------------------------------------------------------           
           READ BP13FD00                                                        
              AT END MOVE 'Y' TO WS-FLG-FD00-EOF                                
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1 TO WS-CNT-FD00-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3000-PROCESS-RECORDS.                                                    
      *--------------------------------------------------------------           
           IF FD00-AMT-ARR-INSTAL NOT NUMERIC                                   
              MOVE 0 TO FD00-AMT-ARR-INSTAL                                     
           END-IF.                                                              
           IF FD00-NUM-MTH-INSTAL NOT NUMERIC                                   
              MOVE 0 TO FD00-NUM-MTH-INSTAL                                     
           END-IF.                                                              
                                                                                
           IF (FD00-NUM-MTH-INSTAL  > 0) AND                                    
              (FD00-NUM-GIRO-INFO = SPACES OR LOW-VALUES OR ZEROS)              
                 PERFORM 3100-READ-BP13K800         THRU 3100-EXIT              
                                                                                
              IF WS-K800-STATUS = 00                                            
                 PERFORM 3200-INIT-PRNT-FIELDS      THRU 3200-EXIT              
                 PERFORM 4000-PRINT-LETTER          THRU 4000-EXIT              
                 PERFORM 7000-READ-UPD-BP13KD76     THRU 7000-EXIT              
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-FD00-READ                THRU 2000-EXIT.                
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3100-READ-BP13K800.                                                      
      *--------------------------------------------------------------           
      *    RETRIEVE OCCUPIERS NRIC1 & NRIC2 FROM FROM BP13K820    *             
      *-----------------------------------------------------------*             
           MOVE FD00-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              CONTINUE                                                          
                                                                                
           ELSE                                                                 
             IF WS-K800-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K800, STATUS ' WS-K800-STATUS           
                        ', REGN IS ' FD00-NUM-REGN                              
             ELSE                                                               
                DISPLAY 'ERROR READING K800, STATUS ' WS-K800-STATUS            
                        ', REGN IS ' FD00-NUM-REGN                              
                MOVE WS-K800-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3200-INIT-PRNT-FIELDS.                                                   
      *--------------------------------------------------------------           
           INITIALIZE                 PC52-NAME1B                               
                                      PC52-DATE-HDR                             
                                      PC52-NAME2B                               
                                      PC52-BLK                                  
                                      PC52-UNIT                                 
                                      PC52-ADDR1                                
                                      PC52-ADDR2                                
                                      PC52-BAR-CODE                             
                                      PC52-FLAT-ADDR                            
                                      PC52-AS-AT-DTE                            
                                      PC52-LOAN-INST                            
                                      PC52-AS-AT-DTE2                           
                                      PC52-LTE-PYMNT-CHRGE                      
                                      PC52-HDBREF                               
                                      PC52-REGNO                                
                                      PC52-TOTAL.                               
                                                                                
           MOVE SPACES               TO  LC52-DETAILS                           
                                         LC52-DETAIL2                           
                                         LC52-DETAIL3.                          
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4000-PRINT-LETTER.                                                       
      *--------------------------------------------------------------           
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC1        TO   K820-NUM-NRIC                     
              MOVE FD00-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 4010-READ-BP13K820 THRU 4010-EXIT                         
              MOVE K820-NME-OCCP         TO   LC52-NAME1                        
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC2        TO   K820-NUM-NRIC                     
              MOVE FD00-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 4010-READ-BP13K820 THRU 4010-EXIT                         
              MOVE K820-NME-OCCP         TO   LC52-NAME2                        
           END-IF.                                                              
                                                                                
           PERFORM 4100-READ-AC15K010            THRU 4100-EXIT.                
           PERFORM 4200-READ-BP13K830            THRU 4200-EXIT.                
           PERFORM 4300-GET-FLAT-ADDR            THRU 4300-EXIT.                
           PERFORM 5000-MOVE-REPORT-DTLS         THRU 5000-EXIT.                
           PERFORM 5100-MOVE-LETTER-DTLS         THRU 5100-EXIT.                
           PERFORM 6000-PRINT-1ST-REMINDERS      THRU 6000-EXIT.                
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4010-READ-BP13K820.                                                      
      *--------------------------------------------------------------           
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K820-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K820, STATUS ' WS-K820-STATUS           
                        ', KEY IS ' FD00-NUM-REGN ' ' K820-NUM-NRIC             
             ELSE                                                               
                DISPLAY 'ERROR READING K820, STATUS ' WS-K820-STATUS            
                        ', KEY IS ' FD00-NUM-REGN ' ' K820-NUM-NRIC             
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4100-READ-AC15K010.                                                      
      *--------------------------------------------------------------           
      *    RETRIEVE LATE PAYMENT CHARGE FROM AC15K010             *             
      *-----------------------------------------------------------*             
           MOVE FD00-NUM-SCH-ACCNT   TO K010-ACCESS-KEY.                        
           READ AC15K010.                                                       
                                                                                
           IF WS-K010-STATUS = 00                                               
              CONTINUE                                                          
                                                                                
           ELSE                                                                 
                                                                                
              MOVE 0 TO K010-PF-BAL-OS                                          
                                                                                
             IF WS-K010-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K010, STATUS ' WS-K010-STATUS           
                        ', REGN IS  '      FD00-NUM-REGN                        
             ELSE                                                               
                DISPLAY 'ERROR READING K010, STATUS ' WS-K010-STATUS            
                        ', REGN IS  '      FD00-NUM-REGN                        
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4200-READ-BP13K830.                                                      
      *-----------------------------------------------------------*             
      *    RETRIEVE ADDRESS FROM BP13K830 USING FD00 REGN NO      *             
      *-----------------------------------------------------------*             
           MOVE FD00-NUM-REGN          TO K830-NUM-REGN.                        
                                                                                
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           IF WS-K830-STATUS  = 00 OR 02                                        
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO PC52-BLK                          
                 MOVE K830-NUM-FLAT        TO PC52-UNIT                         
                 MOVE K830-NME-STREET-BLDG TO PC52-ADDR1                        
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PC52-ADDR2                        
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE    INTO PC52-ADDR2                     
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-POSTAL TO AV02C015-IN-POSTCODE                
                    PERFORM 4400-CALL-AV02C015     THRU 4400-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC52-BAR-CODE          
                 END-IF                                                         
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO PC52-BLK                      
                 MOVE K830-NUM-MAIL-FLAT       TO PC52-UNIT                     
                 MOVE K830-NME-MAIL-STR-BLDG   TO PC52-ADDR1                    
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PC52-ADDR2                    
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PC52-ADDR2                    
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-MAIL-POSTAL TO AV02C015-IN-POSTCODE           
                    PERFORM 4400-CALL-AV02C015     THRU 4400-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC52-BAR-CODE          
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
             IF WS-K830-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K830, STATUS ' WS-K830-STATUS           
                        ', REGN IS  '    FD00-NUM-REGN                          
             ELSE                                                               
                DISPLAY 'ERROR READING K830, STATUS ' WS-K830-STATUS            
                        ', REGN IS  '    FD00-NUM-REGN                          
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------*           
       4300-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE SPACES                TO BP13COMM8-REC.                         
           MOVE K800-NUM-SCH-ACC(1:9) TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                STRING 'BLK '  COMM8-NUM-BLK  ' '                               
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    ' ' DELIMITED BY SIZE             
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO PC52-FLAT-ADDR                                      
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         K800-NUM-SCH-ACC ', REGN : ' FD00-NUM-REGN             
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' K800-NUM-SCH-ACC                            
                        ', REGN : ' FD00-NUM-REGN                               
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *----------------------*                                                  
       4400-CALL-AV02C015.                                                      
      *----------------------*                                                  
                                                                                
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       5000-MOVE-REPORT-DTLS.                                                   
      *--------------------------------------------------------------           
           MOVE FD00-NUM-REGN        TO  LC52-REGNO.                            
           MOVE WS-MONTH(WS-MTH)     TO  LC52-MMM-HDR.                          
           MOVE WS-CCYY              TO  LC52-YR-HDR.                           
                                                                                
           MOVE FD00-NUM-MTH-INSTAL  TO  LC52-MO-ARREAR.                        
           MOVE FD00-AMT-ARR-INSTAL  TO  LC52-AMT-ARREAR.                       
           MOVE WS-DATE-HDR          TO  LC52-DATE-HDR.                         
                                                                                
           IF PC52-FLAT-ADDR NOT = SPACES                                       
              STRING 'BLK '  COMM8-NUM-BLK   ' '                                
                  '#'     COMM8-NUM-LEVEL '-'                                   
                          COMM8-NUM-UNIT-MAIN ' '   DELIMITED BY SIZE           
                          COMM8-NUM-UNIT-SUB  ' '   DELIMITED BY SIZE           
                          COMM8-NME-STREET          DELIMITED BY '   '          
                   ' S('  COMM8-NUM-POSTAL-CODE ')' DELIMITED BY SIZE           
                   INTO   LC52-ADDRESS                                          
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           IF K010-PF-BAL-OS NOT NUMERIC                                        
              MOVE 0 TO K010-PF-BAL-OS                                          
           END-IF.                                                              
                                                                                
           MOVE K010-PF-BAL-OS       TO  LC52-LTE-PYMNT-CHRGE.                  
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       5100-MOVE-LETTER-DTLS.                                                   
      *--------------------------------------------------------------           
           MOVE LC52-REGNO        TO  PC52-REGNO.                               
           MOVE WS-DATE-HDR       TO  PC52-DATE-HDR.                            
           MOVE LC52-NAME1        TO  PC52-NAME1B.                              
           MOVE LC52-NAME2        TO  PC52-NAME2B.                              
                                                                                
           STRING FD00-NUM-SCH-ACCNT(1:4) '-'                                   
                  FD00-NUM-SCH-ACCNT(5:4) '-'                                   
                  FD00-NUM-SCH-ACCNT(9:1) '-'                                   
                  FD00-NUM-SCH-ACCNT(10:2)                                      
                  DELIMITED BY SIZE INTO PC52-HDBREF.                           
                                                                                
           STRING F001-DTE-LAST-DAY-PREV(7:2) '/'                               
                  F001-DTE-LAST-DAY-PREV(5:2) '/'                               
                  F001-DTE-LAST-DAY-PREV(1:4)                                   
              DELIMITED BY SIZE    INTO  PC52-AS-AT-DTE                         
           END-STRING.                                                          
                                                                                
           STRING '08/'                                                         
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
              DELIMITED BY SIZE    INTO  PC52-AS-AT-DTE2                        
           END-STRING.                                                          
                                                                                
           MOVE LC52-AMT-ARREAR      TO  PC52-LOAN-INST.                        
           MOVE LC52-LTE-PYMNT-CHRGE TO  PC52-LTE-PYMNT-CHRGE.                  
                                                                                
           INITIALIZE  WS-TOTAL.                                                
                                                                                
           ADD  FD00-AMT-ARR-INSTAL  TO  WS-TOTAL.                              
           ADD  K010-PF-BAL-OS       TO  WS-TOTAL.                              
           MOVE WS-TOTAL             TO  PC52-TOTAL.                            
                                                                                
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       6000-PRINT-1ST-REMINDERS.                                                
      *--------------------------------------------------------------           
      *-->  PRINT REPORT                                                        
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  LC52-DTL-SERIAL.                    
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO LC52-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO LC52-PAGE-HDR                               
              PERFORM 8000-PRINT-HEADING THRU 8000-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC52-PRTREC FROM LC52-DETAILS.                                 
           WRITE LC52-PRTREC FROM LC52-DETAIL2.                                 
           WRITE LC52-PRTREC FROM LC52-DETAIL3.                                 
           WRITE LC52-PRTREC FROM WS-BLANK-LINE.                                
                                                                                
           ADD 4 TO WS-LINE-CTR.                                                
           ADD   1              TO    WS-CNT-LC52-WRITE.                        
                                                                                
      *--> PRINT LETTER                                                         
                                                                                
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-1 AFTER PAGE.                
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-2.                           
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-3.                           
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-4 AFTER 4.                   
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-5.                           
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-6.                           
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-7.                           
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-8.                           
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-9  AFTER 4.                  
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-10 AFTER 6.                  
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-11 AFTER 4.                  
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-12 AFTER 1.                  
           WRITE PC52-PRTREC    FROM  PC52-PR-LINE-13 AFTER 2.                  
                                                                                
           ADD   1              TO    WS-PC52-WRT-CTR.                          
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       7000-READ-UPD-BP13KD76.                                                  
      *----------------------*                                                  
                                                                                
           MOVE SPACES             TO KD76-NUM-REGN.                            
           MOVE FD00-NUM-REGN      TO KD76-NUM-REGN.                            
           READ BP13KD76.                                                       
                                                                                
           IF WS-KD76-STATUS = 00                                               
              PERFORM 7100-MOVE-TO-BP13KD76   THRU 7100-EXIT                    
              PERFORM 7200-REWRITE-BP13KD76   THRU 7200-EXIT                    
           ELSE                                                                 
              IF WS-KD76-STATUS = 23                                            
                 MOVE SPACES       TO BP13KD76-REC                              
                 INITIALIZE           BP13KD76-REC                              
                 PERFORM 7100-MOVE-TO-BP13KD76   THRU 7100-EXIT                 
                 PERFORM 7300-WRITE-BP13KD76     THRU 7300-EXIT                 
              ELSE                                                              
                DISPLAY 'ERROR READING KD76, STATUS ' WS-KD76-STATUS            
                        ', REGN IS  '    FD00-NUM-REGN                          
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       7100-MOVE-TO-BP13KD76.                                                   
      *--------------------------------------------------------------           
                                                                                
           MOVE FD00-NUM-REGN          TO KD76-NUM-REGN.                        
           MOVE FD00-NUM-SCH-ACCNT     TO KD76-NUM-SCH-ACCNT.                   
           MOVE FD00-CDE-LOAN-REPYMT   TO KD76-CDE-LOAN-REPYMT.                 
           MOVE FD00-AMT-LOAN-BAL      TO KD76-AMT-LOAN-BAL.                    
           MOVE FD00-AMT-INSTAL        TO KD76-AMT-INSTAL.                      
           MOVE FD00-AMT-INSTAL-CASH   TO KD76-AMT-INSTAL-CASH.                 
           MOVE FD00-AMT-INSTAL-CPF1   TO KD76-AMT-INSTAL-CPF1.                 
           MOVE FD00-AMT-INSTAL-CPF2   TO KD76-AMT-INSTAL-CPF2.                 
           MOVE FD00-AMT-INSTAL-CPF3   TO KD76-AMT-INSTAL-CPF3.                 
           MOVE FD00-AMT-INSTAL-CPF4   TO KD76-AMT-INSTAL-CPF4.                 
           MOVE FD00-AMT-CPF-RATE      TO KD76-AMT-CPF-RATE.                    
           MOVE FD00-CDE-ARREARS       TO KD76-CDE-ARREARS.                     
           MOVE FD00-DTE-INSTAL-EFF    TO KD76-DTE-INSTAL-EFF.                  
           MOVE FUNCTION CURRENT-DATE(1:8) TO KD76-DTE-UPDATE.                  
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD76-TME-UPDATE.                  
           MOVE 'BP13CC52'             TO KD76-NUM-USERID.                      
                                                                                
      *-> IF 1ST LETTER DATE IS EMPTY, UPDATE 1ST ARR & MTH                     
      *->                        ELSE, UPDATE TODATE FIELDS                     
                                                                                
           IF KD76-DTE-FIRST-LETTER = SPACES OR LOW-VALUES OR ZEROES            
              MOVE FD00-AMT-ARR-INSTAL    TO KD76-AMT-ARR-INSTAL-1ST            
              MOVE FD00-NUM-MTH-INSTAL    TO KD76-NUM-MTH-INSTAL-1ST            
              MOVE WS-DATE                TO KD76-DTE-FIRST-LETTER              
           ELSE                                                                 
              MOVE FD00-AMT-ARR-INSTAL    TO KD76-AMT-ARR-INSTAL-TODTE          
              MOVE FD00-NUM-MTH-INSTAL    TO KD76-NUM-MTH-INSTAL-TODTE          
           END-IF.                                                              
                                                                                
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       7200-REWRITE-BP13KD76.                                                   
      *--------------------------------------------------------------           
                                                                                
           REWRITE BP13KD76-REC.                                                
                                                                                
           IF WS-KD76-STATUS = 00 OR 02                                         
              ADD 1    TO WS-KD76-REWRITE                                       
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING KD76, STATUS ' WS-KD76-STATUS            
                      ', REGN IS  '    FD00-NUM-REGN                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       7300-WRITE-BP13KD76.                                                     
      *--------------------------------------------------------------           
                                                                                
           WRITE BP13KD76-REC.                                                  
                                                                                
           IF WS-KD76-STATUS = 00                                               
              ADD 1    TO WS-KD76-WRITE                                         
           ELSE                                                                 
               DISPLAY 'ERROR WRITING KD76, STATUS ' WS-KD76-STATUS             
                       ', REGN IS  '    FD00-NUM-REGN                           
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
                                                                                
       7300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       8000-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
                                                                                
            WRITE LC52-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC52-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC52-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC52-PRTREC FROM LC52-HEADER01 AFTER PAGE.                    
            WRITE LC52-PRTREC FROM LC52-HEADER02.                               
            WRITE LC52-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC52-PRTREC FROM LC52-HEADER03.                               
            WRITE LC52-PRTREC FROM LC52-HEADER04.                               
            WRITE LC52-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  9 TO WS-LINE-CTR.                                             
                                                                                
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------           
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------           
                                                                                
           CLOSE  BP13FD00                                                      
                  BP13K800                                                      
                  BP13K820                                                      
                  BP13K830                                                      
                  BP13KD76                                                      
                  AC15K010                                                      
                  SY02F001                                                      
                  BP13LC52                                                      
                  BP13PC52.                                                     
                                                                                
                                                                                
           DISPLAY ' RUN   DATE :  ' WS-DATE-HDR.                               
           DISPLAY ' *------------ BP13CC52 AUDIT TRAIL ------------*'.         
           DISPLAY '    '.                                                      
           DISPLAY 'NO. OF RECORDS READ FROM DAILY TRANS (BP13FD00) = '         
                   WS-CNT-FD00-READ.                                            
           DISPLAY 'NO. OF RECORDS WRITTEN TO            (BP13LC52) = '         
                   WS-CNT-LC52-WRITE.                                           
           DISPLAY 'NO. OF RECORDS WRITTEN TO            (BP13PC52) = '         
                   WS-PC52-WRT-CTR.                                             
           DISPLAY 'NO. OF RECORDS WRITTEN TO            (BP13KD76) = '         
                   WS-KD76-WRITE.                                               
           DISPLAY 'NO. OF RECORDS REWRITTEN TO          (BP13KD76) = '         
                   WS-KD76-REWRITE.                                             
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
              DISPLAY 'CLOSING AC15K010 FILE ERROR ' WS-K010-STATUS             
              MOVE     WS-K010-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE     WS-K820-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K830 FILE ERROR ' WS-K830-STATUS             
              MOVE     WS-K830-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD76-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13KD76 FILE ERROR ' WS-KD76-STATUS             
              MOVE     WS-KD76-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
