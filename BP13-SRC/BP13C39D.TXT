      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
                                                                                
       PROGRAM-ID.    BP13C39D.                                                 
       AUTHOR.        LIZ PATIAG.                                               
       DATE-WRITTEN.  09 JUNE 2106.                                             
       DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  THIS IS A PROGRAM TO PRINT A LIST OF          *                
      *          SOC - AUDIT TRAIL FOR SA AND FLEXI DEPENDING  *                
      *          ON PAYMENT TYPES.                             *                
      *                                                        *                
      *    INPUT FILES:                                        *                
      *      1.  BP13F180 (CASH TRANS FILE FOR VALIDATED       *                
      *          CASES FROM FIRST SYSTEM)                      *                
      *      2.  BP13K690 (AMORTISATION FILE)                  *                
      *      2.  BP13K80A (TO GET NEW TOWN)                    *                
      *                                                        *                
      *    LISTING:                                            *                
      *      1.  CONTROL LISTING    (BP13L39D)                 *                
      *                                                                         
      * CHG-NO   BY  ON        DESCRIPTION                     *                
      * -------  --- --------  --------------------------      *                
      * BP136398 FNP 09/06/16  NEW PGM                         *                
      * BP137219 FNP 19/02/18  CATER FOR FSG GRANT             *                
      * BP138083 KR13 18/01/20 ADDED EHG                     *                  
      * BP138300 KR13 28/05/20 ADD PARM FOR RPRT ID-IOM8 MIGRATN                
      * BP138560 FP6  28/12/20 ADDED LHG                     *                  
      * BP138663 FP6  26/08/21 CATER FOR CCA                 *                  
      * BP138944 FP6  14/10/21 EXPAND K690 FROM 600 TO 800   *                  
      *                        ADDED SUG                     *                  
      * BP139942 ZAR7 21/03/24 REPLACE 410101 WITH 41CZ01    *                  
      **********************************************************                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F180  ASSIGN       TO BP13F180.                           
           SELECT SY02F001  ASSIGN       TO SY02F001.                           
           SELECT BP13K690  ASSIGN       TO BP13K690                    00470000
                            ORGANIZATION IS INDEXED                     00490000
                            ACCESS MODE  IS RANDOM                      00480000
                            RECORD KEY   IS K690-KEY-FLD                00500000
                            FILE STATUS  IS K690-STATUS.                00510000
           SELECT BP13K80A  ASSIGN       TO BP13K80A                    00470000
                            ORGANIZATION IS INDEXED                     00490000
                            ACCESS MODE  IS RANDOM                      00480000
                            RECORD KEY   IS K80A-KEY-FLD                00500000
                            FILE STATUS  IS K80A-STATUS.                00510000
           SELECT BP13L39D  ASSIGN       TO BP13L39D.                           
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F180                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 120 CHARACTERS.                                     
       COPY BP13F180.                                                           
                                                                        00630000
       FD   BP13K80A                                                    00640000
            RECORD CONTAINS  100 CHARACTERS.                            00660000
       COPY BP13K80A.                                                   00680000
                                                                                
       FD   BP13K690                                                    00640000
            RECORD CONTAINS  800 CHARACTERS.                            00660000
       COPY BP13K690.                                                   00680000
                                                                                
       FD  BP13L39D                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  L39D-PRINT-REC              PIC X(132).                              
                                                                                
       COPY SY02F001.                                                           
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
       01  WS1-FILE-ACCESS-STATUS.                                              
           05  K80A-STATUS             PIC 9(2)     VALUE ZEROS.                
           05  K690-STATUS             PIC 9(2)     VALUE ZEROS.                
       01  SY02F001-EOF                PIC X(01)    VALUE 'N'.                  
      *                                                                         
       01  WS2-COUNTERS.                                                        
           05  WS2-CNT-F180-READ       PIC 9(5)     VALUE ZEROS.                
           05  WS2-CNT-K690-READ       PIC 9(5)     VALUE ZEROS.                
           05  WS2-CNT-PRINT-L39D-M    PIC 9(2)     VALUE ZEROS.                
           05  WS2-CNT-PRINT-L39D-UM   PIC 9(2)     VALUE ZEROS.                
           05  WS2-CNT-LINE-ON-L39D    PIC 9(2)     VALUE 66.                   
           05  WS2-CNT-L39D-PAGE       PIC 9(5)     VALUE ZEROS.                
      *                                                                         
       01  WS3-ACCUM-FIELDS.                                                    
           05  WS3-SUBTOT-DRCR-AMT     PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS3-SUBTOT-DR-AMT       PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS3-SUBTOT-CR-AMT       PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS3-TOT-DRCR-AMT        PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS3-TOT-CR-AMT          PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS3-TOT-DR-AMT          PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-SUB-AMT              PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-SUB-PTYPE-AMT        PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-GRD-AMT              PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-SUB-CASENO           PIC 9(3)     VALUE ZEROES.               
           05  WS-GRD-CASENO           PIC 9(3)     VALUE ZEROES.               
      *                                                                         
       01  WS4-SWITCHES-N-RANGE.                                                
           05  WS4-EOF-IND             PIC X(01)    VALUE SPACES.               
               88  WS4-EOF                          VALUE 'Y'.                  
           05  WS4-1ST-TIME-IND        PIC X(01)    VALUE 'Y'.                  
               88  WS4-1ST-TIME                     VALUE 'Y'.                  
           05  WS4-ONLY-SOC-IND        PIC X(01)    VALUE SPACES.               
               88  WS4-ONLY-SOC                     VALUE 'Y'.                  
           05  WS4-SUBTOT-PRTED-IND    PIC X(01)    VALUE SPACES.               
               88  WS4-SUBTOT-PRTED                 VALUE 'Y'.                  
           05  WS-K800-MATCH-FLAG      PIC X(1)     VALUE SPACES.               
               88  WS-K800-MATCHED     VALUE 'Y'.                               
      *                                                                         
       01  WS5-DATES-VARIABLES.                                                 
           05  WS5-CURRENT-DATE.                                                
               10  WS5-CUR-CC          PIC 99.                                  
               10  WS5-CUR-YY          PIC 99.                                  
               10  WS5-CUR-MM          PIC 99.                                  
               10  WS5-CUR-DD          PIC 99.                                  
           05  WS5-PRT-DATE.                                                    
               10  WS5-PRT-DD          PIC XX.                                  
               10  FILLER              PIC X        VALUE '/'.                  
               10  WS5-PRT-MM          PIC XX.                                  
               10  FILLER              PIC X        VALUE '/'.                  
               10  WS5-PRT-CC          PIC XX.                                  
               10  WS5-PRT-YY          PIC XX.                                  
           05  WS5-TRAN-DATE.                                                   
               10  WS5-TRAN-CCYY.                                               
                   15  WS5-TRAN-CC     PIC X(2).                                
                   15  WS5-TRAN-YY     PIC X(2).                                
               10  WS5-TRAN-MM         PIC X(2).                                
               10  WS5-TRAN-DD         PIC X(2).                                
           05  WS5-CCYY                PIC X(4).                                
           05  WS5-CCYY-NUM  REDEFINES   WS5-CCYY PIC 9(4).                     
                                                                                
       01  WS6-DEFAULT-VALUES.                                                  
           05  WS6-MAX-LINES           PIC 9(2)     VALUE  60.                  
           05  WS6-SR-SLASH            PIC X(1)     VALUE  '/'.                 
           05  WS6-L39D-RPT-ID         PIC X(14)    VALUE 'BP13L39D'.           
                                                                                
       01  WS7-SCHEME-ACCT.                                                     
           05  WS7-SCH-ACC.                                                     
               10  WS7-SCH-1-4         PIC X(4)     VALUE SPACES.               
               10  WS7-SCH-5-8         PIC X(4)     VALUE SPACES.               
               10  WS7-SCH-9-9         PIC X(1)     VALUE SPACES.               
               10  WS7-SCH-10-11       PIC X(2)     VALUE SPACES.               
                                                                                
           05  WS7-SCH-ACC-2.                                                   
               10  WS7-SCH-1-4         PIC X(4)     VALUE SPACES.               
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  WS7-SCH-5-8         PIC X(4)     VALUE SPACES.               
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  WS7-SCH-9-9         PIC X(1)     VALUE SPACES.               
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  WS7-SCH-10-11       PIC X(2)     VALUE SPACES.               
                                                                                
       01  WS8-GL-ACCT-VARS.                                                    
           05 WS-ACC-NO1.                                                       
              10 WS-ACC-LEAD              PIC X(01).                            
              10 WS-ACC-INTERNAL          PIC X(06).                            
              10 WS-ACC-FILLER            PIC X(02).                            
              10 WS-ACC-GL                PIC X(04).                            
                                                                                
      *------------------------------------------------*                        
      * TRANSACTION TO FINANCIAL DATE CONVERSION TABLE *                        
      *------------------------------------------------*                        
       01  WS9-TRAN-FIN-MTH-CONV.                                               
           05  FILLER                  PIC X(04)    VALUE '0401'.               
           05  FILLER                  PIC X(04)    VALUE '0502'.               
           05  FILLER                  PIC X(04)    VALUE '0603'.               
           05  FILLER                  PIC X(04)    VALUE '0704'.               
           05  FILLER                  PIC X(04)    VALUE '0805'.               
           05  FILLER                  PIC X(04)    VALUE '0906'.               
           05  FILLER                  PIC X(04)    VALUE '1007'.               
           05  FILLER                  PIC X(04)    VALUE '1108'.               
           05  FILLER                  PIC X(04)    VALUE '1209'.               
           05  FILLER                  PIC X(04)    VALUE '0110'.               
           05  FILLER                  PIC X(04)    VALUE '0211'.               
           05  FILLER                  PIC X(04)    VALUE '0312'.               
       01  WS9-TF-MTH-CONV-TABLE REDEFINES WS9-TRAN-FIN-MTH-CONV.               
           05  WS9-TF-MTH       OCCURS 12 TIMES INDEXED BY WS9-IDX.             
               10  WS9-TRAN-MM         PIC X(2).                                
               10  WS9-FIN-MM          PIC X(2).                                
                                                                                
       01  WS11-JOURNAL-NO.                                                     
           05  FILLER                  PIC X(02)    VALUE 'SA'.                 
           05  WS11-JOURN-MM           PIC X(02)    VALUE SPACES.               
           05  WS11-JOURN-CCYY         PIC X(04)    VALUE SPACES.               
                                                                                
       01  WS12-SOC-AMT.                                                        
           05  WS12-SOC-CR-AMT         PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS12-SOC-DR-AMT         PIC S9(9)V99 COMP-3 VALUE +0.            
                                                                                
       01  WS30-MISCLLANEOUS.                                                   
           05  WS-HD1-RPT-ID           PIC X(14)    VALUE SPACES.               
           05  WS30-PREV-PAYT-TYPE     PIC X(02)    VALUE SPACES.               
           05  WS30-PREV-GL-ACC        PIC X(18)    VALUE SPACES.               
           05  WS30-LAST-FIN-MM        PIC X(02)    VALUE SPACES.               
           05  WS30-LAST-FIN-CCYY      PIC X(04)    VALUE SPACES.               
           05  WS-PREV-DTE-TRANS       PIC X(08)    VALUE SPACES.               
           05  WS-PREV-PAYMENT-TYPE    PIC X(02)    VALUE SPACES.               
           05  WS30-PREV-GL-ACC-NO1    PIC X(13)    VALUE SPACES.               
           05  WS-NUM-REGN             PIC X(8)     VALUE SPACES.               
           05  WS-NUM-NEW-TOWN         PIC X(3)     VALUE SPACES.               
           05  WS-FSG-GRANT            PIC 9(6)V99  VALUE ZEROES.               
           05  WS-REG-GRANT            PIC 9(6)V99  VALUE ZEROES.               
                                                                                
                                                                                
      **********************************************************                
      *         REPORT LAY-OUT  FOR L39D                       *                
      **********************************************************                
                                                                                
      *========================================================*                
      *           FORMAT  FOR  HEADING  OF L39D                *                
      *========================================================*                
       01  L39D-PRT-HEAD-1.                                                     
           05  L39D-HD1-RPT-ID         PIC X(14)    VALUE 'BP13L39D'.           
           05  FILLER                  PIC X(8)     VALUE 'HDBCAT 3'.           
           05  FILLER                  PIC X(18)    VALUE SPACES.               
           05  FILLER                  PIC X(39)    VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                  PIC X(18)    VALUE SPACES.               
           05  FILLER                  PIC X(7)     VALUE 'DATE: '.             
           05  L39D-HD1-DATE           PIC X(10)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE SPACES.               
           05  FILLER                  PIC X(7)     VALUE 'PAGE: '.             
           05  L39D-HD1-PAGE-NO        PIC ZZZZ9.                               
                                                                                
       01  L39D-PRT-HEAD-2.                                                     
           05  FILLER                  PIC X(17)    VALUE SPACES.               
           05  FILLER                  PIC X(46)    VALUE                       
               'SOC - AUDIT TRAIL FOR POSTED TRANSACTIONS FOR '.                
           05  FILLER                  PIC X(43)    VALUE                       
               'STUDIO APARTMENT / LEASE BUY-BACK OLD FLATS'.                   
                                                                                
       01  L39D-PRT-HEAD-4.                                                     
           05  FILLER                  PIC X(48)    VALUE SPACES.               
           05  FILLER                  PIC X(25)    VALUE                       
              'JOURNAL NUMBER ISSUED = '.                                       
           05  L39D-HD4-JOURNAL-NO     PIC X(08)    VALUE SPACES.               
                                                                                
       01  L39D-PRT-HEAD-6.                                                     
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  FILLER                  PIC X(10)    VALUE 'DATE'.               
           05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  FILLER                  PIC X(08)    VALUE 'REGN NO'.            
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  FILLER                  PIC X(12)    VALUE 'TOWN'.               
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  FILLER                  PIC X(13)    VALUE 'GL ACCOUNT'.         
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  FILLER                  PIC X(12)  VALUE 'SCHEME-AC-NO'.         
           05  FILLER                  PIC X(09)    VALUE SPACES.               
           05  FILLER                  PIC X(08)    VALUE 'PAY TYPE'.           
           05  FILLER                  PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(16)    VALUE                       
                                                    '       AMOUNT'.            
                                                                                
      *========================================================*                
      *           FORMAT  FOR DETAIL LINE                      *                
      *========================================================*                
       01  L39D-PRT-DETAIL-1.                                                   
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  L39D-DATE               PIC X(10)    VALUE SPACES.               
           05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  L39D-REGN               PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(06)    VALUE SPACES.               
           05  L39D-TOWN               PIC X(03)    VALUE SPACES.               
           05  FILLER                  PIC X(09)    VALUE SPACES.               
           05  L39D-GL-ACC             PIC X(18)    VALUE SPACES.               
           05  FILLER                  PIC X(06)    VALUE SPACES.               
           05  L39D-SCH-ACC            PIC X(14)    VALUE SPACES.               
           05  FILLER                  PIC X(09)    VALUE SPACES.               
           05  L39D-PAYT-TYPE          PIC X(07)    VALUE SPACES.               
           05  FILLER                  PIC X(07)    VALUE SPACES.               
           05  L39D-AMOUNT             PIC ZZZ,ZZZ,ZZ9.99.                      
           05  L39D-DRCR               PIC X(02).                               
                                                                                
      *====================================================*                    
      *       FORMAT FOR SUBTOTAL HEADING, DETAIL          *                    
      *====================================================*                    
       01  L39D-PRT-SUBTOT-HEAD.                                                
           05  FILLER                  PIC X(25)    VALUE SPACES.               
           05  FILLER                  PIC X(21)    VALUE                       
              'TOTAL FOR GL ACCOUNT '.                                          
           05  L39D-PRT-SUBTOT-LINE.                                            
               10  L39D-SUBTOT-GL-ACC  PIC X(18)    VALUE SPACES.               
               10  FILLER              PIC X(37)    VALUE SPACES.               
               10  L39D-SUBTOT-DRCR-AMT PIC ZZZ,ZZZ,ZZ9.99.                     
               10  L39D-SUBTOT-DRCR    PIC X(02)    VALUE SPACES.               
                                                                                
       01  L39D-PRT-SUBTOT-DASH.                                                
           05  FILLER                  PIC X(99)    VALUE SPACES.               
           05  FILLER                  PIC X(18)    VALUE ALL '-'.              
                                                                                
      *====================================================*                    
      *        FORMAT FOR TOTAL HEADING AND DETAIL         *                    
      *====================================================*                    
       01  L39D-PRT-TOT-HEAD.                                                   
           05  FILLER                  PIC X(25)    VALUE SPACES.               
           05  FILLER                  PIC X(21)    VALUE                       
              'TOTAL FOR ALL TRANS  '.                                          
           05  L39D-PRT-TOT-LINE.                                               
               10  FILLER              PIC X(55)    VALUE SPACES.               
               10  L39D-TOT-DRCR-AMT   PIC ZZZ,ZZZ,ZZ9.99.                      
               10  L39D-TOT-DRCR       PIC X(02)    VALUE SPACES.               
                                                                                
       01  L39D-PRT-GRAND-TOTAL-HEAD-1.                                         
           05  FILLER                  PIC X(30)    VALUE SPACES.               
           05  FILLER                  PIC X(23)    VALUE                       
              'TOTAL DEBITS       =   '.                                        
           05  L39D-TOT-DR-AMT         PIC ZZZ,ZZZ,ZZ9.99.                      
                                                                                
       01  L39D-PRT-GRAND-TOTAL-HEAD-2.                                         
           05  FILLER                  PIC X(30)    VALUE SPACES.               
           05  FILLER                  PIC X(23)    VALUE                       
              'TOTAL CREDITS      =   '.                                        
           05  L39D-TOT-CR-AMT         PIC ZZZ,ZZZ,ZZ9.99.                      
                                                                                
       LINKAGE SECTION.                                                         
      *==============================================================*          
       01  PARM-DATA.                                                           
           05  PARM-STRING-LENGTH        PIC 9(4) COMP.                         
           05  PARM-STRING               PIC X(2) VALUE SPACES.                 
                                                                                
      **********************************************************                
       PROCEDURE DIVISION USING PARM-DATA.                                      
      **********************************************************                
                                                                                
      *                                                                         
       MAIN-ROUTINE SECTION.                                                    
      *====================*                                                    
            PERFORM 100-OPEN-FILES     THRU  100-EXIT.                          
            PERFORM 150-READ-BP13F180  THRU  150-EXIT  UNTIL WS4-EOF.           
            PERFORM 600-PRINT-TOTAL    THRU  600-EXIT.                          
            PERFORM 900-CLOSE-ROUTINE  THRU  900-EXIT.                          
                                                                                
       MAIN-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *                                                                         
       100-OPEN-FILES.                                                          
      *========================*                                                
           OPEN INPUT  BP13F180                                                 
                       BP13K80A                                                 
                       BP13K690                                                 
                       SY02F001                                                 
                OUTPUT BP13L39D.                                                
      *                                                                         
           IF K80A-STATUS NOT = 00 AND NOT = 97                                 
              DISPLAY ' OPENING ERROR, BP13K80A-STATUS = ' K80A-STATUS          
              MOVE K80A-STATUS  TO RETURN-CODE                                  
              PERFORM 900-CLOSE-ROUTINE.                                        
                                                                                
           IF K690-STATUS NOT = 00 AND NOT = 97                                 
              DISPLAY ' OPENING ERROR, BP13K690-STATUS = ' K690-STATUS          
              MOVE K690-STATUS  TO RETURN-CODE                                  
              PERFORM 900-CLOSE-ROUTINE.                                        
                                                                                
           READ SY02F001 AT END MOVE 'Y' TO SY02F001-EOF.                       
           IF SY02F001-EOF = 'Y'                                                
              DISPLAY 'RECORD NOT FOUND IN SY02F001.'                           
              DISPLAY 'PROCESSING TERMINATED.'                                  
              PERFORM 900-CLOSE-ROUTINE                                         
           END-IF.                                                              
      *                                                                         
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       150-READ-BP13F180.                                                       
      *===========================*                                             
      **************************************************************            
      *                                                            *            
      *   1. READ FILE BP13F180.                                   *            
      *   2. PROCESS NECESSARY DETAILS FOR PRINTING.               *            
      *   3. CALL ROUTINE TO PRINT REPORT.                         *            
      *                                                            *            
      **************************************************************            
           READ BP13F180                                                        
                         AT END MOVE 'Y' TO WS4-EOF-IND                         
                         GO TO 150-EXIT.                                        
                                                                                
           ADD  1 TO WS2-CNT-F180-READ.                                         
                                                                                
           MOVE SPACES           TO WS-ACC-NO1.                                 
           MOVE F180-ACC-NO1     TO WS-ACC-NO1.                                 
                                                                                
           PERFORM 200-MOVE-PRT-L39D   THRU 200-EXIT.                           
                                                                                
      *                                                                         
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       200-MOVE-PRT-L39D.                                                       
      *===========================*                                             
      **********************************************************                
      *                                                        *                
      *   MOVE NECESSARY DETAILS TO REPORT AND PRINT           *                
      *                                                        *                
      **********************************************************                
           PERFORM 300-READ-BP13K690   THRU 300-EXIT.                           
           PERFORM 350-READ-BP13K80A   THRU 350-EXIT.                           
                                                                                
           MOVE WS-NUM-REGN            TO  L39D-REGN.                           
           MOVE WS-NUM-NEW-TOWN        TO  L39D-TOWN.                           
           MOVE F001-DTE-CURRENT       TO  WS5-TRAN-DATE.                       
           MOVE WS5-TRAN-CC            TO  WS5-PRT-CC.                          
           MOVE WS5-TRAN-YY            TO  WS5-PRT-YY.                          
           MOVE WS5-TRAN-MM            TO  WS5-PRT-MM.                          
           MOVE WS5-TRAN-DD            TO  WS5-PRT-DD.                          
           MOVE WS5-PRT-DATE           TO  L39D-DATE.                           
           MOVE F180-NUM-SCH-ACC       TO  WS7-SCH-ACC.                         
           MOVE CORR WS7-SCH-ACC       TO  WS7-SCH-ACC-2.                       
           MOVE WS7-SCH-ACC-2          TO  L39D-SCH-ACC.                        
      *    MOVE F180-JR-SC             TO  L39D-PAYT-TYPE.                      
                                                                                
           EVALUATE WS-ACC-INTERNAL                                             
              WHEN '000000'                                                     
                 MOVE 'SA'             TO L39D-PAYT-TYPE                        
              WHEN '407600'                                                     
                 IF F180-DTE-POST(1:3) = 'CCA'                                  
                    MOVE 'CCA'         TO L39D-PAYT-TYPE                        
                 ELSE                                                           
                    MOVE 'FLX'         TO L39D-PAYT-TYPE                        
                 END-IF                                                         
              WHEN '41CZ01'                                                     
                 IF F180-DTE-POST(1:3) = 'SGP'                                  
                    MOVE 'SGP'         TO L39D-PAYT-TYPE                        
                 ELSE                                                           
                    MOVE 'SPR'         TO L39D-PAYT-TYPE                        
                 END-IF                                                         
                 IF F180-DTE-POST(1:7)  = 'CCA-SGP'                             
                    MOVE 'CCA-SGP'     TO L39D-PAYT-TYPE                        
                 ELSE                                                           
                    MOVE 'CCA-SPR'     TO L39D-PAYT-TYPE                        
                 END-IF                                                         
              WHEN '407000'                                                     
                 MOVE 'AHG'            TO L39D-PAYT-TYPE                        
              WHEN '407900'                                                     
                 IF F180-DTE-POST(1:3) = 'CCA'                                  
                    MOVE 'CCA-EHG'     TO L39D-PAYT-TYPE                        
                 ELSE                                                           
                    MOVE 'EHG'         TO L39D-PAYT-TYPE                        
                 END-IF                                                         
              WHEN '524300'                                                     
                 IF F180-DTE-POST(1:3) = 'CCA'                                  
                    MOVE 'CCA-LHG'     TO L39D-PAYT-TYPE                        
                 ELSE                                                           
                    MOVE 'LHG'         TO L39D-PAYT-TYPE                        
                 END-IF                                                         
              WHEN '407800'                                                     
                 MOVE 'SHG'            TO L39D-PAYT-TYPE                        
              WHEN '407400'                                                     
                 IF F180-DTE-POST(1:3) = 'CCA'                                  
                    MOVE 'CCA-ELD'     TO L39D-PAYT-TYPE                        
                 ELSE                                                           
                    MOVE 'ELD'         TO L39D-PAYT-TYPE                        
                 END-IF                                                         
              WHEN '407300'                                                     
                 IF F180-DTE-POST(1:3) = 'CCA'                                  
                    MOVE 'CCA-SRG'     TO L39D-PAYT-TYPE                        
                 ELSE                                                           
                    MOVE 'SRG'         TO L39D-PAYT-TYPE                        
                 END-IF                                                         
              WHEN '418200'                                                     
                 IF F180-DTE-POST(1:3) = 'CCA'                                  
                    MOVE 'CCA-SUG'     TO L39D-PAYT-TYPE                        
                 ELSE                                                           
                    MOVE 'SUG'         TO L39D-PAYT-TYPE                        
                 END-IF                                                         
              WHEN '419200'                                                     
                 IF WS-FSG-GRANT NOT = ZEROES                                   
                    MOVE 'FSG'         TO L39D-PAYT-TYPE                        
                 ELSE                                                           
                    IF F180-DTE-POST(1:3) = 'CCA'                               
                       MOVE 'CCA-REG'  TO L39D-PAYT-TYPE                        
                    ELSE                                                        
                       MOVE 'REG'      TO L39D-PAYT-TYPE                        
                    END-IF                                                      
                 END-IF                                                         
              WHEN '41FS00'                                                     
                 MOVE 'FSG GR'         TO L39D-PAYT-TYPE                        
              WHEN OTHER                                                        
                 MOVE 'INVALID'        TO L39D-PAYT-TYPE                        
           END-EVALUATE.                                                        
                                                                                
           MOVE F180-CR-AMT            TO  L39D-AMOUNT.                         
                                                                                
      *--------------------------------------------------*                      
      * GET FINANCIAL MONTH FROM TRANSACTION MONTH       *                      
      * (IF FIN. MTH = 1,2 OR 3 - BELONG TO LAST FIN. YR *                      
      *--------------------------------------------------*                      
           SET WS9-IDX TO 1.                                                    
           SEARCH WS9-TF-MTH                                                    
                  WHEN WS9-TRAN-MM(WS9-IDX) = WS5-TRAN-MM                       
                       MOVE WS9-FIN-MM(WS9-IDX) TO WS11-JOURN-MM.               
                                                                                
           IF WS5-TRAN-MM = '01' OR '02' OR '03'                                
              MOVE WS5-TRAN-CCYY   TO  WS5-CCYY                                 
              COMPUTE WS5-CCYY-NUM  =  WS5-CCYY-NUM - 1                         
              MOVE WS5-CCYY        TO  WS11-JOURN-CCYY                          
           ELSE                                                                 
              MOVE WS5-TRAN-CCYY   TO  WS11-JOURN-CCYY.                         
           MOVE WS11-JOURNAL-NO  TO  L39D-HD4-JOURNAL-NO.                       
                                                                                
      *--------------------------------------------*                            
      * FORMAT GL ACCOUNT                          *                            
      *--------------------------------------------*                            
           STRING 'A000' WS-ACC-FILLER '-'                                      
                  WS-ACC-INTERNAL  '-'                                          
                  WS-ACC-GL                                                     
                  DELIMITED BY SIZE INTO L39D-GL-ACC                            
           END-STRING.                                                          
                                                                                
      *--------------------------------------------*                            
      * IF GL ACCOUNT CHANGES - PRINT SUBTOTAL   *                              
      *--------------------------------------------*                            
                                                                                
           IF (WS-ACC-NO1  NOT =  WS30-PREV-GL-ACC-NO1) OR                      
              (F180-JR-SC  NOT =  WS30-PREV-PAYT-TYPE)                          
               MOVE F180-JR-SC     TO WS30-PREV-PAYT-TYPE                       
               MOVE WS-ACC-NO1     TO WS30-PREV-GL-ACC-NO1                      
               IF WS4-1ST-TIME                                                  
                  MOVE SPACE         TO  WS4-1ST-TIME-IND                       
                  MOVE L39D-GL-ACC   TO  WS30-PREV-GL-ACC                       
               ELSE                                                             
                  PERFORM 550-PRINT-SUBTOT THRU  550-EXIT                       
           ELSE                                                                 
              NEXT SENTENCE.                                                    
                                                                                
      *-------------------------------------------------------------*           
      * NEGATIVE AMOUNT MEANS DEBIT AND POSITIVE MEANS CREDIT                   
      *   FOR ALL PAYMENT TYPES EXCEPT FOR 'SOC'                                
      * SOC - NEGATIVE AMT MEANS CREDIT AND POSITIVE AMT MEANS DEBIT            
      *-------------------------------------------------------------*           
              MOVE SPACE TO WS4-ONLY-SOC-IND                                    
              IF F180-CR-AMT      > 0                                           
                 MOVE 'CR'             TO L39D-DRCR                             
                 ADD F180-CR-AMT       TO WS3-SUBTOT-CR-AMT                     
                                          WS3-TOT-CR-AMT                        
              ELSE                                                              
                 MOVE 'DB'             TO L39D-DRCR                             
                 ADD F180-CR-AMT       TO WS3-SUBTOT-DR-AMT                     
                                          WS3-TOT-DR-AMT.                       
      *                                                                         
           PERFORM 400-PRINT   THRU  400-EXIT.                                  
      *                                                                         
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       300-READ-BP13K690.                                                       
      *===========================*                                             
                                                                                
           MOVE F180-NUM-SCH-ACC TO K690-KEY-FLD.                               
                                                                                
           READ BP13K690.                                                       
                                                                                
           IF K690-STATUS = 00                                                  
              MOVE K690-NUM-REGN    TO WS-NUM-REGN                              
              IF K690-AMT-FSG-GRANT NOT NUMERIC                                 
                 MOVE ZEROES        TO WS-FSG-GRANT                             
              ELSE                                                              
                 MOVE K690-AMT-FSG-GRANT TO WS-FSG-GRANT                        
              END-IF                                                            
              IF K690-AMT-REG-GRANT NOT NUMERIC                                 
                 MOVE ZEROES        TO WS-REG-GRANT                             
              ELSE                                                              
                 MOVE K690-AMT-REG-GRANT TO WS-REG-GRANT                        
              END-IF                                                            
           ELSE                                                                 
           IF K690-STATUS = 23                                                  
              MOVE SPACES           TO WS-NUM-REGN                              
              MOVE ZEROES           TO WS-FSG-GRANT                             
                                       WS-REG-GRANT                             
           ELSE                                                                 
              DISPLAY ' ERROR READING, BP13K690 = ' K690-STATUS                 
              MOVE K690-STATUS  TO RETURN-CODE                                  
              PERFORM 900-CLOSE-ROUTINE   THRU  900-EXIT                        
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       350-READ-BP13K80A.                                                       
      *===========================*                                             
                                                                                
           MOVE F180-NUM-SCH-ACC TO K80A-KEY-FLD.                               
                                                                                
           READ BP13K80A.                                                       
                                                                                
           IF K80A-STATUS = 00                                                  
              MOVE K80A-NUM-TOWN    TO WS-NUM-NEW-TOWN                          
           ELSE                                                                 
           IF K80A-STATUS = 23                                                  
              MOVE SPACES           TO WS-NUM-NEW-TOWN                          
           ELSE                                                                 
              DISPLAY ' ERROR READING, BP13K80A = ' K80A-STATUS                 
              MOVE K80A-STATUS  TO RETURN-CODE                                  
              PERFORM 900-CLOSE-ROUTINE   THRU  900-EXIT                        
           END-IF.                                                              
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       400-PRINT.                                                               
      *===================*                                                     
      ******************************************************************        
      *                                                                *        
      *       PRINT DETAIL LINE FOR REPORT L39D                        *        
      *                                                                *        
      ******************************************************************        
           MOVE WS6-L39D-RPT-ID    TO    L39D-HD1-RPT-ID.               04980000
      *                                                                         
           PERFORM 500-CHECK-LINECT.                                            
      *                                                                         
           ADD 1                   TO    WS2-CNT-PRINT-L39D-M                   
           ADD 1                   TO    WS2-CNT-LINE-ON-L39D                   
           WRITE L39D-PRINT-REC    FROM  L39D-PRT-DETAIL-1 AFTER 1.             
           MOVE SPACES             TO    L39D-PRINT-REC.                        
      *                                                                         
           ADD 1                   TO    WS2-CNT-LINE-ON-L39D.                  
           MOVE SPACE              TO    WS4-SUBTOT-PRTED-IND.                  
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *                                                                         
       500-CHECK-LINECT.                                                        
      *==========================*                                              
      ******************************************************************        
      *                                                                *        
      *          CHECK LINE COUNT TO DETERMINE PAGE THROW              *        
      *                FOR REPORT                                      *        
      *                                                                *        
      ******************************************************************        
           IF (L39D-HD1-RPT-ID = WS6-L39D-RPT-ID)                       05310000
              IF (WS2-CNT-LINE-ON-L39D > WS6-MAX-LINES) OR              05320000
                 (WS30-LAST-FIN-MM NOT = WS11-JOURN-MM) OR                      
                 (WS30-LAST-FIN-CCYY NOT = WS11-JOURN-CCYY)                     
      *                                                                         
                 MOVE WS11-JOURN-MM TO WS30-LAST-FIN-MM                         
                 MOVE WS11-JOURN-CCYY TO WS30-LAST-FIN-CCYY                     
                 PERFORM 560-PRINT-RPT-HEADING   THRU 560-EXIT          05330000
                 MOVE 1  TO WS2-CNT-LINE-ON-L39D.                       05340000
      *                                                                         
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       550-PRINT-SUBTOT.                                                        
      *==========================*                                              
      ******************************************************************        
      *                                                                *        
      *       PRINT SUB-TOTAL FOR EACH PAYMENT TYPE                    *        
      *                                                                *        
      ******************************************************************        
           MOVE  WS30-PREV-GL-ACC TO   L39D-SUBTOT-GL-ACC.                      
           ADD   3                TO   WS2-CNT-LINE-ON-L39D.                    
           WRITE L39D-PRINT-REC   FROM L39D-PRT-SUBTOT-DASH AFTER 2.            
           MOVE  SPACES           TO   L39D-PRINT-REC.                          
      *                                                                         
           COMPUTE WS3-SUBTOT-DRCR-AMT = WS3-SUBTOT-DR-AMT +                    
                                         WS3-SUBTOT-CR-AMT.                     
      *                                                                         
           MOVE WS3-SUBTOT-DRCR-AMT TO  L39D-SUBTOT-DRCR-AMT.                   
           IF WS3-SUBTOT-DRCR-AMT > 0                                           
              MOVE 'CR'  TO  L39D-SUBTOT-DRCR                                   
           ELSE                                                                 
              MOVE 'DB'  TO  L39D-SUBTOT-DRCR.                                  
                                                                                
           MOVE  0              TO    WS3-SUBTOT-CR-AMT                         
                                      WS3-SUBTOT-DR-AMT                         
                                      WS3-SUBTOT-DRCR-AMT.                      
                                                                                
           WRITE L39D-PRINT-REC FROM  L39D-PRT-SUBTOT-HEAD AFTER 1.             
           ADD   4              TO    WS2-CNT-LINE-ON-L39D.                     
           MOVE  SPACES         TO    L39D-PRINT-REC.                           
           WRITE L39D-PRINT-REC AFTER 2.                                        
                                                                                
           MOVE 'Y'   TO  WS4-SUBTOT-PRTED-IND.                                 
      *                                                                         
      * TO UPDATE GL ACC WHEN IT CHANGES                                        
      *                                                                         
           IF  L39D-GL-ACC NOT = WS30-PREV-GL-ACC                               
               MOVE L39D-GL-ACC  TO WS30-PREV-GL-ACC.                           
                                                                                
      *                                                                         
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                 05660000
       560-PRINT-RPT-HEADING.                                           05670000
      *================================*                                05680000
      ****************************************************************  05690000
      *                                                              *  05700000
      * PRINT HEADING EITHER FOR L39D                                *  05710000
      *                                                              *  05720000
      ****************************************************************  05730000
           MOVE FUNCTION CURRENT-DATE TO WS5-CURRENT-DATE.                      
           MOVE WS5-CUR-DD            TO    WS5-PRT-DD.                 05760000
           MOVE WS5-CUR-MM            TO    WS5-PRT-MM.                 05770000
           MOVE WS5-CUR-CC            TO    WS5-PRT-CC.                 05780000
           MOVE WS5-CUR-YY            TO    WS5-PRT-YY.                 05780000
           MOVE WS5-PRT-DATE          TO    L39D-HD1-DATE.                      
                                                                                
      *                                                                 05800000
           IF L39D-HD1-RPT-ID = WS6-L39D-RPT-ID                         05810000
              MOVE  SPACES            TO    L39D-PRINT-REC              05820001
              ADD   1                 TO    WS2-CNT-L39D-PAGE           05740000
              MOVE  WS2-CNT-L39D-PAGE TO    L39D-HD1-PAGE-NO            05750000
              MOVE  L39D-PRT-HEAD-1   TO WS-HD1-RPT-ID                  05840001
              MOVE  PARM-STRING       TO WS-HD1-RPT-ID(9:2)             05840001
              WRITE L39D-PRINT-REC    FROM  WS-HD1-RPT-ID AFTER PAGE    05840001
              WRITE L39D-PRINT-REC    FROM  L39D-PRT-HEAD-2 AFTER 2     05850001
              WRITE L39D-PRINT-REC    FROM  L39D-PRT-HEAD-4 AFTER 2     05870001
              WRITE L39D-PRINT-REC    FROM  L39D-PRT-HEAD-6 AFTER 2     05881001
              MOVE  SPACES            TO    L39D-PRINT-REC              05890001
                                            L39D-HD1-RPT-ID             05900001
              WRITE L39D-PRINT-REC    AFTER 2                           05910000
           END-IF.                                                      05920000
                                                                        06040000
       560-EXIT.                                                        06050000
           EXIT.                                                        06060000
                                                                                
      *                                                                         
       600-PRINT-TOTAL.                                                         
      *=========================*                                               
      ******************************************************************        
      *                                                                *        
      *       PRINT TOTAL FOR ALL TRANSACTIONS AND GRAND TOTAL         *        
      *         FOR DEBTI AND CREDIT AMOUNTS.                          *        
      *                                                                *        
      ******************************************************************        
      *                                                                         
      * IF SUBTOTAL NOT YET PRINTED - PRINT SUBTOTAL FIRST *                    
      *                                                                         
           IF WS4-SUBTOT-PRTED                                                  
              NEXT SENTENCE                                                     
           ELSE                                                                 
              PERFORM 550-PRINT-SUBTOT.                                         
      *                                                                         
           PERFORM 500-CHECK-LINECT.                                            
      *                                                                         
                                                                                
      *                                                                         
      * PRINT TOTAL FOR ALL TRANSACTIONS *                                      
      *                                                                         
           COMPUTE WS3-TOT-DRCR-AMT = WS3-TOT-DR-AMT +                          
                                      WS3-TOT-CR-AMT.                           
           IF WS3-TOT-DRCR-AMT > 0                                              
              MOVE 'CR' TO L39D-TOT-DRCR                                        
           ELSE                                                                 
              MOVE 'DB' TO L39D-TOT-DRCR.                                       
                                                                                
           MOVE  WS3-TOT-DRCR-AMT TO   L39D-TOT-DRCR-AMT.                       
           ADD   3                TO   WS2-CNT-LINE-ON-L39D.                    
      *    WRITE L39D-PRINT-REC   FROM L39D-PRT-TOT-HEAD                .       
      *                                AFTER 2.                         .       
           MOVE  SPACES           TO   L39D-PRINT-REC.                          
      *                                                                         
      * PRINT GRAND TOTAL FOR DEBIT AND CREDIT AMOUNTS *                        
      *                                                                         
           ADD   4                TO   WS2-CNT-LINE-ON-L39D.                    
           MOVE  WS3-TOT-DR-AMT   TO   L39D-TOT-DR-AMT.                         
           WRITE L39D-PRINT-REC   FROM L39D-PRT-GRAND-TOTAL-HEAD-1      .       
                                       AFTER 3.                         .       
           MOVE  SPACES           TO   L39D-PRINT-REC.                          
           MOVE  WS3-TOT-CR-AMT   TO   L39D-TOT-CR-AMT.                         
           WRITE L39D-PRINT-REC   FROM L39D-PRT-GRAND-TOTAL-HEAD-2      .       
                                       AFTER 1.                         .       
           MOVE  SPACES           TO   L39D-PRINT-REC.                          
      *                                                                         
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       900-CLOSE-ROUTINE.                                                       
      *===========================*                                             
      ****************************************************************          
      *                                                              *          
      *            CLOSE FILES AND END PROCESSING                    *          
      *                                                              *          
      ****************************************************************          
           CLOSE BP13K80A                                                       
                 BP13K690                                                       
                 BP13F180                                                       
                 SY02F001                                                       
                 BP13L39D.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY ' ** BP13C39D ** '.                                          
           DISPLAY 'NO OF RECORDS READ FROM FILE (BP13F180) = '                 
                    WS2-CNT-F180-READ.                                          
           DISPLAY 'NO OF MATCHED RECORDS PRINTED           = '                 
                    WS2-CNT-PRINT-L39D-M.                                       
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
