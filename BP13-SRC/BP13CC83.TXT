      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CC83.                                                 
      *AUTHOR.        EDEN ROSE DINGAL.                                         
      *DATE-WRITTEN.  07 JUNE 2007.                                             
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   PROGRAM TO PROCESS JOURNAL VOUVHER FOR LOAN AND LUMPSUM   *           
      *   PROGRESS PAYMENT. JV GL POSTING IS DONE BY PAYMENT SYSTEM *           
      *   OUTPUT TRANSACTION TYPE '69', PAYMT=JVL (JV FOR LOAN)     *           
      *   PAYMT=JVP (JV FOR PP) FOR REPORTING ONLY. SYSTEM WILL ALSO*           
      *   UPDATE LOAN-BAL IN BP13KD00.                              *           
      *                                                             *           
      *   THIS PROGRAM IS SIMILAR WITH BP13CC06.                    *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  AG07F630 -- VOUCHER TRANSACTION FILE                  *           
      *   2.  P13FD10A -- SORTED SOC/DBSS VOUCHER OIC FILE          *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13KD10 -- SOC/DBSS VOUCHER OIC FILE                 *           
      *   2.  BP13KD00 -- PROGRESS PAYMENT MASTER                   *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13F125 -- EDITED FIRST TRANSACTION FILE             *           
      *   2.  P13FD10B -- EDITED SOC/DBSS VOUCHER OIC FILE          *           
      *   3.  BP13LC83 -- CONTROL LISTING                           *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133052 07/06/2007 ED1  NEW PROGRAM CATER FOR JV           *           
      * BP133095 22/08/2007 BA3  REMOVE ADDITION OF JV PAYMENT TO   *           
      *                          KD00-AMT-TOTL-PYMT-TODTE.          *           
      * BP133175 12/11/2007 ED1  ADD NUM-LOAN-INTR                  *           
      * BP133091 20/11/2007 RB12 EXPAND KD00 FROM 1200 TO 1600 BYTES*           
      * BP133209 29/01/2008 ED1  ADD COMP OF ARR-INTR & ARR-INSTAL  *           
      * BP133387 13/10/2008 JB8  REMOVE HARD CODE CR-DR             *           
      * BP133545 04/03/2009 JB8  TO CATER FOR DIFF NUM-LOAN-INTR    *           
      * BP133588 28/04/2009 JB8  TO GET RIGHT PROCESS DATE          *           
      * BP133588 29/04/2009 JB8  TO GET THR RIGHT SCHDL FOR SUBTRF  *           
      * BP133398 18/05/2009 JB8  EXPAND KD10 LENGTH FR 450  TO 1400 *           
      * BP133638 29/06/2009 LSB  CHG JVP, JCL, JVN, F125-DTE-TRANS  *           
      * BP133638 29/07/2009 JB8  JVI WILL NOT UPD AMT-TOT-INTR-TODTE*           
      * BP134154 29/03/2011 JB8  TO CATER FOR JVI                   *           
      * BP134154 19/04/2011 JB8  TO CREATE KIV FILE                 *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KD10 ASSIGN TO BP13KD10                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD10-KEY-FLD                               
                  FILE STATUS     IS BP13KD10-STATUS.                           
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD00-KEY-FLD                               
                  FILE STATUS     IS BP13KD00-STATUS.                           
                                                                                
           SELECT BP13K140 ASSIGN TO BP13K140                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K140-KEY-FLD                               
                  FILE STATUS     IS BP13K140-STATUS.                           
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS BM06K110-STATUS.                           
                                                                                
           SELECT AG07F630   ASSIGN       TO AG07F630.                          
                                                                                
           SELECT P13FD10A   ASSIGN       TO P13FD10A.                          
                                                                                
           SELECT P13FD10B   ASSIGN       TO P13FD10B.                          
                                                                                
           SELECT BP13F125   ASSIGN       TO BP13F125.                          
                                                                                
           SELECT BP13LC83   ASSIGN       TO BP13LC83.                          
                                                                                
           SELECT SY02F001   ASSIGN       TO SY02F001.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13KD10                                                            
            RECORD CONTAINS  1400 CHARACTERS.                                   
       COPY BP13KD10.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13K140                                                            
            RECORD CONTAINS 150  CHARACTERS.                                    
       COPY BP13K140.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   AG07F630                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  130 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY AG07F630.                                                           
                                                                                
       FD   P13FD10A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  1400 CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FD10.                                                           
                                                                                
       FD   P13FD10B                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  1400 CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01   WS-BP13FD10B-REC       PIC X(1400).                         00055007
                                                                                
       FD   BP13F125                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F125.                                                           
                                                                                
       FD   BP13LC83                                                            
            RECORD CONTAINS   132 CHARACTERS                                    
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   PRINT-REC             PIC X(132).                                   
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-NRIC                PIC X(09) VALUE SPACES.                       
       01  WS-PAYMENT-TYPE        PIC X(03) VALUE SPACES.                       
       01  WS-TOT-AMT-PAID        PIC 9(06)V99.                                 
       01  WS-BP13F125-CASHTRAN   PIC X(150).                                   
       01  WS-DTE-IBG-VALUE       PIC X(08).                                    
       01  WS-TXT-IBG-SERIAL      PIC X(08).                                    
       01  CNT-LINE-ON-PAGE       PIC 9(02) VALUE 66.                           
       01  CNT-F125-WRITE         PIC 9(05) VALUE ZEROES.                       
       01  CNT-F630-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-FD10-WRITE         PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD10-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD10-DELETE        PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD00-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD00-UPDATE        PIC 9(05) VALUE ZEROES.                       
       01  CNT-PRT-CASES          PIC 9(06) VALUE ZEROES.                       
       01  CNT-PAGE               PIC 9(03) VALUE ZEROES.                       
       01  CNT-FD10-READ          PIC 9(08) VALUE ZEROES.                       
       01  CNT-K140-KIV           PIC 9(08) VALUE ZEROES.                       
       01  WS-MATCH-REC           PIC 9(08) VALUE ZEROES.                       
       01  WS-F630-EOF            PIC X     VALUE SPACE.                        
       01  WS-FD10-EOF            PIC X     VALUE SPACE.                        
       01  BP13KD10-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KD00-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K140-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BM06K110-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  WS-K140-COUNT-HISTORY  PIC 9(02) VALUE ZEROES.                       
       01  WS-KD10-REGN-FND       PIC X     VALUE SPACE.                        
       01  WS-DATE                PIC X(08) VALUE SPACES.                       
       01  WS-NUM-PP              PIC X(2)  VALUE SPACES.                       
       01  WS-NUM-PP-NO REDEFINES WS-NUM-PP PIC 9(2).                           
       01  WS-KD00-SCHACC         PIC X(11) VALUE SPACE.                        
       01  WS-AMT-RECEIPT         PIC S9(7)V99  VALUE ZEROES.                   
       01  WS-NUM-AMOUNT          PIC S9(6)V99.                                 
       01  WS-AMT-INTEREST        PIC S9(07)V99 VALUE +0.                       
                                                                                
       01  WS-SCHEDULE-NO.                                                      
           05  WS-SCHEDULE-NUM    PIC X(08).                                    
           05  WS-DOC-NO          PIC X(06).                                    
                                                                                
       01  LC83-PR-HEAD-01.                                                     
           05  FILLER             PIC X(08) VALUE 'BP13LC83'.                   
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  FILLER             PIC X(08) VALUE 'HDBCAT 3'.                   
           05  FILLER             PIC X(24) VALUE SPACES.                       
           05  FILLER             PIC X(39) VALUE                               
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER             PIC X(17) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'DATE: '.                     
           05  LC83-DATE          PIC X(10).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'PAGE :'.                     
           05  LC83-PAGE          PIC ZZ9 .                                     
                                                                                
       01  LC83-PR-HEAD-02.                                                     
           05  FILLER             PIC X(38) VALUE SPACES.                       
           05  FILLER             PIC X(47) VALUE                               
               'REPORT ON TRANSACTIONS PASSED FROM VOUCHER FILE'.               
           05  FILLER             PIC X(33) VALUE SPACES.                       
                                                                                
       01  LC83-PR-HEAD-03.                                                     
           05  FILLER             PIC X(37)                                     
                  VALUE '  S/NO   REGN NO.   SCH ACCT         '.                
           05  FILLER             PIC X(36)                                     
                  VALUE 'AMOUNT    TRANS-TY  VOUCHER NO      '.                 
           05  FILLER             PIC X(25)                                     
                  VALUE 'BATCH NO     DATE POST   '.                            
           05  FILLER             PIC X(23)                                     
                  VALUE 'PAYMT TYPE      REMARKS'.                              
                                                                                
       01  LC83-PR-DETAILS.                                                     
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LC83-SNO           PIC ZZZZZ9 .                                  
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LC83-REG-NO        PIC X(08).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LC83-SCH-ACCT      PIC X(11).                                    
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LC83-AMT           PIC  $$,$$$,$$9.99DB.                         
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  LC83-TRANS-TY      PIC X(02).                                    
           05  FILLER             PIC X(04) VALUE SPACES.                       
           05  LC83-VR-NO         PIC X(11).                                    
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  LC83-NUM-RECEIPT   PIC X(08).                                    
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  LC83-DTE-POST      PIC X(08) VALUE SPACES.                       
           05  FILLER             PIC X(04) VALUE SPACES.                       
           05  LC83-PAYMENT-TYPE  PIC X(08) VALUE SPACES.                       
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  LC83-REMARK        PIC X(18) VALUE SPACES.                       
                                                                                
       01  LC83-CONTROL-LINE.                                                   
           05  FILLER             PIC X(20) VALUE  SPACES.                      
           05  CNTL-ITEM          PIC X(30) VALUE  SPACES.                      
           05  CNTL-CNT           PIC ZZZZ9 .                                   
           05  FILLER             PIC X(58) VALUE  SPACES.                      
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F630    THRU 2000-EXIT.                            
           PERFORM 2100-READ-FD10    THRU 2100-EXIT.                            
           MOVE ZEROES               TO CNT-PRT-CASES.                          
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F630-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  AG07F630                                                 
                       P13FD10A                                                 
                       SY02F001                                                 
                       BM06K110                                                 
                I-O    BP13KD10                                                 
                       BP13KD00                                                 
                       BP13K140                                                 
                OUTPUT BP13F125                                                 
                       P13FD10B                                                 
                       BP13LC83.                                                
                                                                                
           IF BP13KD10-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD10 : ' BP13KD10-STATUS             
              MOVE BP13KD10-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
                                                                                
           IF BP13KD00-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K140-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K140 : ' BP13K140-STATUS             
              MOVE BP13K140-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BM06K110 : ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT      TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
              DELIMITED BY SIZE INTO LC83-DATE.                                 
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F630.                                                          
      *---------------*                                                         
                                                                                
           READ AG07F630 AT END                                                 
                MOVE HIGH-VALUES TO AG07F630-REC                                
                MOVE 'Y' TO WS-F630-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-F630-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2100-READ-FD10.                                                          
      *---------------*                                                         
                                                                                
           READ P13FD10A AT END                                                 
                MOVE HIGH-VALUES TO BP13FD10-REC                                
                MOVE 'Y' TO WS-FD10-EOF                                         
                GO TO 2100-EXIT.                                                
           ADD 1 TO CNT-FD10-READ.                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF F630-NUM-VR = FD10-NUM-VR                                         
              ADD  1                            TO WS-MATCH-REC                 
              PERFORM 3100-PROCESS-REC        THRU 3100-EXIT                    
              INITIALIZE                           LC83-PR-DETAILS              
              MOVE SPACES                       TO LC83-REMARK                  
              PERFORM 4000-PRINT-REPORT       THRU 4000-EXIT                    
              PERFORM 2000-READ-F630          THRU 2000-EXIT                    
              PERFORM 2100-READ-FD10          THRU 2100-EXIT                    
           ELSE                                                                 
              IF F630-NUM-VR > FD10-NUM-VR                                      
                 PERFORM 2100-READ-FD10       THRU 2100-EXIT                    
              ELSE                                                              
                 IF F630-NUM-BATCH-TYPE = 'J'                                   
                    PERFORM 3400-WRITE-KIV-REC THRU 3400-EXIT                   
                 END-IF                                                         
                 MOVE F630-AMT-TRANS            TO WS-AMT-RECEIPT               
                 INITIALIZE                        LC83-PR-DETAILS              
                 MOVE SPACES                    TO LC83-REMARK                  
                 MOVE 'ORIG VCHR NO ERROR'      TO LC83-REMARK                  
                 MOVE ZEROES             TO KD10-AMT-CPF-GRANT-PAYEE(1)         
                                            KD10-AMT-CPF-GRANT-PAYEE(2)         
                                            KD10-AMT-CPF-GRANT-PAYEE(3)         
                                            KD10-AMT-CPF-GRANT-PAYEE(4)         
                 PERFORM 4000-PRINT-REPORT    THRU 4000-EXIT                    
                 PERFORM 2000-READ-F630       THRU 2000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3100-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE SPACES                TO WS-KD00-SCHACC.                        
           MOVE SPACES                TO KD10-KEY-FLD.                          
           MOVE FD10-NUM-TRANS        TO KD10-NUM-TRANS.                        
           MOVE FD10-NUM-REGN         TO KD10-NUM-REGN.                         
           MOVE FD10-NUM-PP           TO KD10-NUM-PP.                           
           PERFORM 3200-READ-BP13KD10 THRU 3200-EXIT.                           
           MOVE SPACES                TO WS-BP13FD10B-REC.                      
                                                                                
           IF FD10-AMT-PP-LUMPSUM NOT NUMERIC                                   
              MOVE ZEROES             TO KD10-AMT-PP-LUMPSUM                    
           END-IF.                                                              
           IF FD10-AMT-LOAN NOT NUMERIC                                         
              MOVE ZEROES             TO KD10-AMT-LOAN                          
           END-IF.                                                              
                                                                                
           MOVE F630-DTE-TRANS        TO KD10-DTE-FIN-RPOCESS.                  
           MOVE F630-NUM-BATCH        TO KD10-NUM-BATCH.                        
           MOVE AG07F630-REC(111:8)   TO KD10-DTE-IBG-VALUE.                    
           MOVE AG07F630-REC(119:12)  TO KD10-TXT-IBG-SERIAL.                   
           IF FD10-NUM-PAYMENT-MODE = 'G'                                       
              MOVE KD10-DTE-IBG-VALUE TO KD10-DTE-SEND-VOUCHER                  
           ELSE                                                                 
              MOVE FD10-DTE-DUE-PP    TO KD10-DTE-SEND-VOUCHER                  
           END-IF.                                                              
           MOVE BP13KD10-REC          TO WS-BP13FD10B-REC.                      
           WRITE WS-BP13FD10B-REC.                                              
           ADD 1                      TO CNT-FD10-WRITE.                        
                                                                                
           PERFORM 3920-READ-BP13KD00       THRU 3920-EXIT.                     
           PERFORM 3300-MOVE-DATA-BP13F125  THRU 3300-EXIT.                     
      *                                                                         
      * UPDATE BP13KD00 ONLY IF AMT-LOAN>0, AMT-LUMPSUM=0                       
      * FOR SUB-TRF, UNDER NEW REGN-NO, ONLY TAKE IN D1 FOR JVP                 
      *                               DO NOT TAKE IN D2 FOR LOAN                
      *              UNDER OLD REGN-NO, ONLY TAKE IN D2 FOR LOAN                
      *                               DO NOT TAKE IN 10 CLEARING ACCNT          
      *                                                                         
           IF BP13KD00-STATUS = 0                                               
              IF FD10-AMT-PP-LUMPSUM = ZEROS                                    
                 PERFORM 3950-REWRITE-BP13KD00 THRU 3950-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-AMT-INTEREST TO F125-AMT-INTEREST.                           
           WRITE                   BP13F125-CASHTRAN                            
           ADD  1               TO CNT-F125-WRITE.                              
                                                                                
           PERFORM 3700-DELETE-BP13KD10     THRU 3700-EXIT.                     
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3200-READ-BP13KD10.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13KD10.                                                       
           IF BP13KD10-STATUS = 0                                               
              MOVE 'Y'    TO WS-KD10-REGN-FND                                   
           ELSE                                                                 
              IF BP13KD10-STATUS = 23                                           
                 MOVE 'N' TO WS-KD10-REGN-FND                                   
                 DISPLAY 'REGNO NOT FOUND IN BP13KD10 : ' KD10-NUM-REGN         
                 MOVE BP13KD10-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD10 : ' BP13KD10-STATUS          
                 MOVE BP13KD10-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3300-MOVE-DATA-BP13F125.                                                 
      *------------------------*                                                
                                                                                
           MOVE SPACES       TO BP13F125-CASHTRAN.                              
           MOVE '69'         TO F125-CDE-TRANS-TYPE.                            
                                                                                
           MOVE FD10-NUM-REGN      TO F125-NUM-REGN.                            
                                                                                
           MOVE WS-NRIC            TO F125-NUM-NRIC.                            
           MOVE SPACES             TO F125-NME-NAME.                            
                                                                                
           MOVE F630-NUM-CPF       TO F125-CDE-CPF-APPL.                        
           MOVE F630-NUM-VR        TO F125-NUM-RECPT-JRNO.                      
           MOVE F630-NUM-VR-DDD    TO F125-NUM-VR-DDD.                          
           MOVE F630-NUM-VR-YY     TO F125-NUM-VR-YY.                           
           MOVE WS-DATE            TO F125-DTE-TRANS.                           
           MOVE F630-DTE-TRANS     TO F125-DTE-POST.                            
           MOVE SPACES             TO F125-CDE-APPLT-CPF.                       
           MOVE ZERO               TO F125-COUNT-HISTORY.                       
           IF KD00-DTE-EFF-TRANSFER NOT = SPACES AND LOW-VALUES                 
                                                 AND ZEROES                     
              MOVE 'TR'            TO F125-NUM-PP-SCHEDULE                      
           ELSE                                                                 
              MOVE FD10-NUM-PP     TO F125-NUM-PP-SCHEDULE                      
           END-IF.                                                              
           MOVE FD10-DTE-DUE-PP    TO F125-DTE-PP-DUE.                          
           MOVE WS-KD00-SCHACC     TO F125-NUM-SCH-ACCT.                        
                                                                                
           MOVE 0 TO WS-AMT-RECEIPT.                                            
                                                                                
           IF FD10-AMT-PP-LUMPSUM NOT = 0                                       
              MOVE FD10-AMT-PP-LUMPSUM TO F125-AMT-RECEIPT                      
              MOVE FD10-AMT-PP-LUMPSUM TO WS-AMT-RECEIPT                        
              MOVE 'JVP'               TO F125-CDE-PAYMENT-TYPE                 
              MOVE 'JVP'               TO WS-PAYMENT-TYPE                       
              MOVE FD10-NUM-CR-DR      TO F125-CDE-CRDR                         
           ELSE                                                                 
           IF FD10-AMT-LOAN       NOT = 0                                       
              MOVE FD10-AMT-LOAN       TO F125-AMT-RECEIPT                      
              MOVE FD10-AMT-LOAN       TO WS-AMT-RECEIPT                        
              IF FD10-NUM-LOAN-INTR = 'I'                                       
                 MOVE 'JVI'            TO F125-CDE-PAYMENT-TYPE                 
                 MOVE 'JVI'            TO WS-PAYMENT-TYPE                       
                 MOVE FD10-NUM-LOAN-INTR TO F125-NUM-LOAN-INTR                  
              ELSE                                                              
                 IF FD10-NUM-LOAN-INTR = 'L' OR 'N'                             
                    MOVE 'JVL'         TO F125-CDE-PAYMENT-TYPE                 
                    MOVE 'JVL'         TO WS-PAYMENT-TYPE                       
                    MOVE FD10-NUM-LOAN-INTR TO F125-NUM-LOAN-INTR               
                 END-IF                                                         
              END-IF                                                            
              MOVE FD10-NUM-CR-DR      TO F125-CDE-CRDR                         
           END-IF.                                                              
                                                                                
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3400-WRITE-KIV-REC.                                                      
      *------------------------*                                                
                                                                                
           MOVE F630-NUM-HDB-REF      TO K110-SCH-ACC-NO                        
           PERFORM 8001-READ-BM06K110 THRU 8001-EXIT                            
                                                                                
           MOVE K110-REGN-NO          TO K140-NUM-REGN                          
           MOVE '69'                  TO K140-CDE-TRANS-TYPE.                   
           MOVE F630-DTE-TRANS        TO K140-DTE-POST.                         
           ADD 1                      TO WS-K140-COUNT-HISTORY                  
           MOVE 'N'                   TO K140-COUNT-HIST-CHAR.                  
           MOVE WS-K140-COUNT-HISTORY TO K140-COUNT-HIST-NUM.                   
           MOVE WS-DATE               TO K140-DTE-TRANS.                        
           MOVE F630-NUM-CPF          TO K140-NUM-NRIC.                         
           MOVE 'JV '                 TO K140-CDE-PAYMENT-TYPE                  
           MOVE F630-AMT-TRANS        TO K140-AMT-RECEIPT                       
                                                                                
           MOVE F630-NUM-VR           TO K140-NUM-RECPT-JRNO.                   
           MOVE F630-NUM-HDB-REF      TO K140-NUM-SCH-ACCT.                     
           MOVE  'A'                  TO K140-CDE-ERROR.                        
                                                                                
           WRITE BP13K140-KIVTRAN.                                              
                                                                                
           IF BP13K140-STATUS = 00 OR 02                                        
              ADD 1                   TO CNT-K140-KIV                           
           ELSE                                                                 
              IF BP13K140-STATUS = 22                                           
                 DISPLAY ' BP13K140 - WRITE ERROR, DUPLICATE KEY EXISTS'        
                         ', KEY ' K140-KEY-FLD                                  
              ELSE                                                              
                 DISPLAY ' BP13K140 - WRITE ERROR, STATUS '                     
                         BP13K140-STATUS                                        
                 MOVE BP13K140-STATUS       TO   RETURN-CODE                    
                 PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3700-DELETE-BP13KD10.                                                    
      *---------------------*                                                   
                                                                                
           DELETE BP13KD10.                                                     
                                                                                
           EVALUATE BP13KD10-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO CNT-KD10-DELETE                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR DELETING BP13KD10-REC ...'                   
                    MOVE BP13KD10-STATUS TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3920-READ-BP13KD00.                                                      
      *---------------------*                                                   
                                                                                
           MOVE SPACES           TO BP13KD00-REC.                               
           MOVE KD10-NUM-REGN    TO KD00-NUM-REGN.                              
                                                                                
           READ BP13KD00.                                                       
           IF BP13KD00-STATUS = 0                                               
              ADD 1    TO CNT-KD00-READ                                         
              MOVE KD00-NUM-SCH-ACCNT   TO WS-KD00-SCHACC                       
              PERFORM 3930-INITIALIZE-AMTS THRU 3930-EXIT                       
           ELSE                                                                 
           IF BP13KD00-STATUS = 23                                              
              MOVE SPACES                    TO LC83-REMARK                     
              MOVE 'REC NOT FOUND IN KD00'   TO LC83-REMARK                     
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF                                                               
           END-IF.                                                              
                                                                                
       3920-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3930-INITIALIZE-AMTS.                                                    
      *----------------------*                                                  
           IF KD00-AMT-LOAN-BAL IS NOT NUMERIC                                  
              MOVE 0 TO KD00-AMT-LOAN-BAL                                       
           END-IF.                                                              
           IF KD00-AMT-LOAN-PAID IS NOT NUMERIC                                 
              MOVE 0 TO KD00-AMT-LOAN-PAID                                      
           END-IF.                                                              
           IF KD00-AMT-ARR-INTR  IS NOT NUMERIC                                 
              MOVE 0 TO KD00-AMT-ARR-INTR                                       
           END-IF.                                                              
           IF KD00-AMT-TOT-INTR-TODTE IS NOT NUMERIC                            
              MOVE 0 TO KD00-AMT-TOT-INTR-TODTE                                 
           END-IF.                                                              
           IF KD00-AMT-ARR-INSTAL IS NOT NUMERIC                                
              MOVE 0 TO KD00-AMT-ARR-INSTAL                                     
           END-IF.                                                              
           IF KD00-NUM-MTH-INSTAL IS NOT NUMERIC                                
              MOVE 0 TO KD00-NUM-MTH-INSTAL                                     
           END-IF.                                                              
           IF KD00-AMT-INSTAL     IS NOT NUMERIC                                
              MOVE 0 TO KD00-AMT-INSTAL                                         
           END-IF.                                                              
                                                                                
       3930-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3950-REWRITE-BP13KD00.                                                   
      *----------------------*                                                  
                                                                                
           MOVE 'BP13CC83'           TO KD00-NUM-USERID.                        
           MOVE WS-DATE              TO KD00-DTE-UPDATE.                        
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD00-TME-UPDATE.                  
           MOVE F001-DTE-CURRENT(1:6)      TO KD00-DTE-LATEST-PYMT-LOAN.        
                                                                                
      *- CATER FOR LOAN ADJUSTMENT                                              
      *-                                                                        
           IF FD10-AMT-LOAN NOT = ZEROS AND                                     
              (FD10-NUM-LOAN-INTR = 'I' OR 'L' OR 'N')                          
              COMPUTE KD00-AMT-LOAN-BAL =                                       
                      KD00-AMT-LOAN-BAL - WS-AMT-RECEIPT                        
              MOVE WS-DATE(1:6)           TO KD00-DTE-LATEST-PYMT-LOAN          
                                                                                
              IF FD10-NUM-LOAN-INTR = 'L'                                       
                 COMPUTE KD00-AMT-LOAN-PAID =                                   
                         KD00-AMT-LOAN-PAID + WS-AMT-RECEIPT                    
                 COMPUTE KD00-AMT-TOTL-PYMT-TODTE =                             
                         KD00-AMT-TOTL-PYMT-TODTE + WS-AMT-RECEIPT              
              END-IF                                                            
                                                                                
              IF FD10-NUM-LOAN-INTR = 'N'                                       
                 COMPUTE KD00-AMT-LOAN-PAID =                                   
                         KD00-AMT-LOAN-PAID + WS-AMT-RECEIPT                    
                 COMPUTE KD00-AMT-TOTL-PYMT-TODTE =                             
                         KD00-AMT-TOTL-PYMT-TODTE + WS-AMT-RECEIPT              
                 IF KD00-AMT-ARR-INSTAL > 0                                     
                    COMPUTE KD00-AMT-ARR-INSTAL =                               
                            KD00-AMT-ARR-INSTAL - WS-AMT-RECEIPT                
                    IF KD00-AMT-ARR-INSTAL > 0                                  
                       COMPUTE KD00-NUM-MTH-INSTAL ROUNDED =                    
                               KD00-AMT-ARR-INSTAL / KD00-AMT-INSTAL            
                    ELSE                                                        
                       MOVE ZEROES      TO KD00-NUM-MTH-INSTAL                  
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF FD10-NUM-LOAN-INTR = 'I'                                       
                 COMPUTE KD00-AMT-ARR-INTR = KD00-AMT-ARR-INTR                  
                                           - F125-AMT-RECEIPT                   
                 MOVE F125-AMT-RECEIPT      TO WS-AMT-INTEREST                  
                                                                                
                 MOVE WS-AMT-INTEREST       TO F125-AMT-INTEREST                
      ***                                                                       
      * JB8 29/07/2009                                                          
      ***        COMPUTE KD00-AMT-TOT-INTR-TODTE =                              
      ***                KD00-AMT-TOT-INTR-TODTE + WS-AMT-INTEREST              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           REWRITE BP13KD00-REC.                                                
                                                                                
           IF BP13KD00-STATUS = 0                                               
              ADD 1    TO CNT-KD00-UPDATE                                       
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3950-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-PRINT-REPORT.                                                       
      *------------------*                                                      
                                                                                
           IF CNT-LINE-ON-PAGE > 46                                             
              MOVE 1 TO CNT-LINE-ON-PAGE                                        
              PERFORM 4100-REPORT-HEADING THRU 4100-EXIT                        
           ELSE                                                                 
              ADD 1 TO CNT-LINE-ON-PAGE                                         
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-PRT-CASES.                                              
                                                                                
           MOVE CNT-PRT-CASES       TO LC83-SNO.                                
           IF F630-NUM-VR = FD10-NUM-VR                                         
              MOVE FD10-NUM-REGN    TO LC83-REG-NO                              
           ELSE                                                                 
              MOVE SPACES           TO LC83-REG-NO                              
           END-IF.                                                              
                                                                                
           IF F630-NUM-VR NOT = SPACES AND LOW-VALUES AND ZEROES                
              MOVE F630-NUM-VR      TO LC83-VR-NO                               
           ELSE                                                                 
              MOVE SPACES           TO LC83-VR-NO                               
           END-IF.                                                              
                                                                                
           MOVE F630-NUM-HDB-REF    TO LC83-SCH-ACCT.                           
                                                                                
           IF FD10-AMT-PP-LUMPSUM NOT NUMERIC                                   
              MOVE ZEROES           TO FD10-AMT-PP-LUMPSUM.                     
                                                                                
           IF FD10-AMT-LOAN NOT NUMERIC                                         
              MOVE ZEROES           TO FD10-AMT-LOAN.                           
                                                                                
           MOVE WS-PAYMENT-TYPE       TO LC83-PAYMENT-TYPE.                     
           MOVE '69'                  TO LC83-TRANS-TY                          
           MOVE WS-AMT-RECEIPT         TO LC83-AMT.                             
           MOVE KD10-DTE-FIN-RPOCESS   TO LC83-DTE-POST.                        
           MOVE F630-NUM-BATCH         TO LC83-NUM-RECEIPT.                     
                                                                                
           WRITE PRINT-REC          FROM LC83-PR-DETAILS.                       
                                                                                
           MOVE SPACES                 TO WS-PAYMENT-TYPE.                      
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4100-REPORT-HEADING.                                                     
      *--------------------*                                                    
                                                                                
            ADD 1           TO CNT-PAGE.                                        
            MOVE CNT-PAGE   TO LC83-PAGE.                                       
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER PAGE.                                         
            WRITE PRINT-REC FROM LC83-PR-HEAD-01 AFTER 2.                       
            WRITE PRINT-REC FROM LC83-PR-HEAD-02 AFTER 3.                       
            WRITE PRINT-REC FROM LC83-PR-HEAD-03 AFTER 3.                       
            MOVE ALL '-'    TO PRINT-REC.                                       
            WRITE PRINT-REC.                                                    
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER 1.                                            
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       8001-READ-BM06K110.                                                      
      ******************************************************************        
                                                                                
           READ BM06K110.                                                       
                                                                                
           IF BM06K110-STATUS = 00 OR 02                                        
              CONTINUE                                                          
           ELSE                                                                 
              MOVE SPACES                       TO K110-SCH-ACC-NO              
              DISPLAY 'BM06K110 - READ ERROR, KEY (' K110-KEY-FLD ')'           
                      ', STATUS (' BM06K110-STATUS ')'                          
           END-IF.                                                              
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CC83 *************'.                            
           DISPLAY 'NO OF RECS MATCHED F630/FD10    : ' WS-MATCH-REC.           
           DISPLAY 'NO OF RECS WRITTEN TO BP13F125  : ' CNT-F125-WRITE.         
           DISPLAY 'NO OF RECS READ FROM  AG07F630  : ' CNT-F630-READ.          
           DISPLAY 'NO OF RECS READ FROM  BP13FD10  : ' CNT-FD10-READ.          
           DISPLAY 'NO OF FD10 WRITTEN TO BP13FD10  : ' CNT-FD10-WRITE.         
           DISPLAY 'NO OF RECS DELETED FR BP13KD10  : ' CNT-KD10-DELETE.        
           DISPLAY 'NO OF RECS READ FROM  BP13KD00  : ' CNT-KD00-READ.          
           DISPLAY 'NO OF RECS UPDATED IN BP13KD00  : ' CNT-KD00-UPDATE.        
           DISPLAY 'NO OF RECS WRITTEN TO BP13LC83  : ' CNT-PRT-CASES.          
           DISPLAY 'NO OF RECS WRITTEN TO BP13K140  : ' CNT-K140-KIV.           
                                                                                
           CLOSE BP13KD10                                                       
                 BP13KD00                                                       
                 BP13K140                                                       
                 AG07F630                                                       
                 P13FD10A                                                       
                 P13FD10B                                                       
                 BP13F125                                                       
                 BP13LC83                                                       
                 SY02F001.                                                      
                                                                                
           IF BP13KD10-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD10-STATUS ' BP13KD10-STATUS          
            MOVE BP13KD10-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KD00-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD00-STATUS ' BP13KD00-STATUS          
            MOVE BP13KD00-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K140-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K140-STATUS ' BP13K140-STATUS          
            MOVE BP13K140-STATUS TO RETURN-CODE.                                
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BM06K110-STATUS ' BM06K110-STATUS          
            MOVE BM06K110-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
