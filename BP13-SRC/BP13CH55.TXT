       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CH55.                                                  
      *AUTHOR.       ZDD1.                                                      
      *DATE-WRITTEN. 23/11/2015.                                                
      * ========================================================== *            
      * SYSTEMS OF COMMITMENT (BP13)                               *            
      * ========================================================== *            
      *  UPDATE - BP13K859                                         *            
      *                                                            *            
      *  INPUT   :  BP13F859                                       *            
      *  OUTPUT  :  BP13K859                                       *            
      * ========================================================== *            
      * REF NO    DATE       BY     DESCRIPTIONS                   *            
      * ========================================================== *            
      * BP136307  25082016  ZDD1    CATER FOR REWRITE OF BP13K859  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F859 ASSIGN TO BP13F859.                                  
                                                                                
                                                                                
           SELECT BP13K859 ASSIGN TO BP13K859                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K859-KEY-FLD                               
                  FILE STATUS     IS WS-K859-STATUS.                            
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F859                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F859.                                                           
                                                                                
       FD   BP13K859                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K859.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F859-EOF                   PIC X    VALUE 'N'.                    
       01  WS-K859-EOF                   PIC X    VALUE 'N'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F859-READ              PIC 9(7) VALUE ZEROS.                  
           05  WS-K859-REWRITE           PIC 9(7) VALUE ZEROS.                  
           05  WS-K859-WRITE             PIC 9(7) VALUE ZEROS.                  
           05  WS-K859-READ              PIC 9(7) VALUE ZEROS.                  
           05  WS-K859-STATUS            PIC X(2) VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F859    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-F859 THRU 300-EXIT                              
                    UNTIL WS-F859-EOF = 'Y'.                                    
            PERFORM 900-CLOSE-FILES  THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F859                                                 
                I-O    BP13K859.                                                
                                                                                
           IF WS-K859-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K859 FILE, ' WS-K859-STATUS            
              PERFORM 900-CLOSE-FILES         THRU 900-EXIT.                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F859.                                                           
      *-------------------------------------------------------------            
           READ BP13F859 AT END                                                 
                MOVE 'Y' TO WS-F859-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1         TO WS-F859-READ.                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F859.                                                        
      *-------------------------------------------------------------            
                                                                                
           PERFORM 600-READ-BP13K859   THRU 600-EXIT.                           
           PERFORM 200-READ-F859       THRU 200-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-READ-BP13K859.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES               TO K859-KEY-FLD.                           
           MOVE F859-NUM-REGN        TO K859-NUM-REGN.                          
                                                                                
           READ BP13K859.                                                       
                                                                                
           IF WS-K859-STATUS = 00                                               
                ADD 1    TO WS-K859-READ                                        
                PERFORM 800-REWRITE-BP13K859  THRU 800-EXIT                     
           ELSE                                                                 
              IF WS-K859-STATUS = 23 OR 10                                      
                 PERFORM 700-WRITE-BP13K859   THRU 700-EXIT                     
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K859 : ' WS-K859-STATUS             
                 MOVE WS-K859-STATUS     TO RETURN-CODE                         
                 PERFORM 900-CLOSE-FILES THRU 900-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-WRITE-BP13K859.                                                      
      *-------------------------------------------------------------            
           MOVE BP13F859-REC         TO   BP13K859-REC.                         
                                                                                
           WRITE BP13K859-REC.                                                  
                                                                                
           IF WS-K859-STATUS = 00 OR 02                                         
              ADD 1 TO WS-K859-WRITE                                            
           ELSE                                                                 
              DISPLAY 'ERROR WRITING BP13K859 : ' K859-NUM-REGN                 
                      '  STATUS ' WS-K859-STATUS                                
              MOVE WS-K859-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-REWRITE-BP13K859.                                                    
      *-------------------------------------------------------------            
           MOVE F859-DTE-PCD-TRIGGER TO   K859-DTE-PCD-TRIGGER                  
                                                                                
           REWRITE BP13K859-REC.                                                
                                                                                
           IF WS-K859-STATUS = 00 OR 02                                         
              ADD 1 TO WS-K859-REWRITE                                          
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K859 : ' K859-NUM-REGN               
                      '  STATUS ' WS-K859-STATUS                                
              MOVE WS-K859-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-IF.                                                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '- BP13CH55 CONTROL TOTAL -----------'.                      
           DISPLAY '1) NO OF REC READ    F859  = ' WS-F859-READ.                
           DISPLAY '2) NO OF REC READ    K859  = ' WS-K859-READ.                
           DISPLAY '3) NO OF REC WRITE   K859  = ' WS-K859-WRITE.               
           DISPLAY '4) NO OF REC REWRT   K859  = ' WS-K859-REWRITE.             
                                                                                
           CLOSE BP13F859                                                       
                 BP13K859.                                                      
                                                                                
           IF WS-K859-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K859 FILE, ' WS-K859-STATUS.           
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
