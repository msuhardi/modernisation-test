       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C991.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  18/12/2001.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *   OBJECTIVE   :  PROGRAM TO UPDATE K820-NUM-CSTMR-SOURCE   *            
      *                  WHEN F915-NUM-REGN NOT IN BP13F915.       *            
      *                                                            *            
      *   INPUT  FILES:  1.BP13F915 - SOC CDB TRANSACTION FILE     *            
      *                  2.BP13F820 - SOC OCCUPIER FILE            *            
      *   I-O    FILES:  1.BP13K820 - SOC OCCUPIER FILE (VSAM)     *            
      *   OUTPUT FILES:  1.P13F820A - SOC OCCUPIER FILE            *            
      *                                                            *            
      * CHG REF  DATE      OIC   REMARKS                           *            
      * -------- --------  ----  --------------------------------- *            
      * BP130850 20011218  AS1   NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F915  ASSIGN       TO BP13F915.                           
                                                                                
           SELECT BP13F820  ASSIGN       TO BP13F820.                           
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT P13F820A  ASSIGN       TO P13F820A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F915           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 800 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BP13F915.                                                           
                                                                                
       FD   BP13F820           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 400 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BP13F820.                                                           
                                                                                
       FD   P13F820A           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 400 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       01   P13F820A-REC       PIC X(400).                                      
                                                                                
       FD   BP13K820           RECORD CONTAINS 400 CHARACTERS.                  
       COPY BP13K820.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K820-STATUS           PIC 9(2)   VALUE 00.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F915-READ             PIC 9(4)   VALUE ZEROES.                
           05  WS-F820-READ             PIC 9(4)   VALUE ZEROES.                
           05  WS-K820-REWRT            PIC 9(4)   VALUE ZEROES.                
           05  WS-K820-NOTFD            PIC 9(4)   VALUE ZEROES.                
           05  WS-F820A-WRT             PIC 9(4)   VALUE ZEROES.                
                                                                                
       01  WS-NUM-REGN                  PIC X(8)   VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BP13F915 THRU 2000-EXIT.                           
           PERFORM 3000-READ-BP13F820 THRU 3000-EXIT.                           
           PERFORM 4000-MAIN-ROUTINE  THRU 4000-EXIT                            
                       UNTIL F915-NUM-REGN = HIGH-VALUE AND                     
                             F820-NUM-REGN = HIGH-VALUE                         
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F915                                                 
                       BP13F820                                                 
                I-O    BP13K820                                                 
                OUTPUT P13F820A.                                                
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F915.                                                      
      *-------------------------------------------------------------            
           READ BP13F915 AT END MOVE HIGH-VALUE TO F915-NUM-REGN                
                            GO TO 2000-EXIT.                                    
                                                                                
           ADD  1                         TO WS-F915-READ.                      
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13F820.                                                      
      *-------------------------------------------------------------            
           READ BP13F820 AT END MOVE HIGH-VALUE TO F820-KEY-FLD                 
                            GO TO 3000-EXIT.                                    
                                                                                
           ADD  1                         TO WS-F820-READ.                      
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF F915-NUM-REGN = F820-NUM-REGN                                     
              MOVE F820-NUM-REGN               TO   WS-NUM-REGN                 
              PERFORM 3000-READ-BP13F820       THRU 3000-EXIT                   
                      UNTIL (F820-NUM-REGN NOT = WS-NUM-REGN) OR                
                            (F820-NUM-REGN     = HIGH-VALUE)                    
              PERFORM 2000-READ-BP13F915       THRU 2000-EXIT                   
           ELSE                                                                 
              IF F915-NUM-REGN < F820-NUM-REGN                                  
                 PERFORM 2000-READ-BP13F915    THRU 2000-EXIT                   
              ELSE                                                              
                 IF F915-NUM-REGN > F820-NUM-REGN                               
                    PERFORM 5000-UPDATE-BP13K820 THRU 5000-EXIT                 
                    PERFORM 3000-READ-BP13F820   THRU 3000-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5000-UPDATE-BP13K820.                                                    
      *-------------------------------------------------------------            
           MOVE SPACES                     TO K820-KEY-FLD.                     
           MOVE F820-NUM-REGN              TO K820-NUM-REGN.                    
           MOVE F820-NUM-NRIC              TO K820-NUM-NRIC.                    
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                   MOVE F820-NUM-CSTMR-SOURCE TO K820-NUM-CSTMR-SOURCE          
                   PERFORM 6000-REWRITE-BP13K820 THRU 6000-EXIT                 
                   WRITE P13F820A-REC       FROM BP13K820-REC                   
                   ADD   1                    TO WS-F820A-WRT                   
              WHEN 23                                                           
                   ADD   1                    TO WS-K820-NOTFD                  
                   DISPLAY 'RECORD NOT FOUND IN BP13K820'                       
                           ' K820-KEY-FLD : ' K820-KEY-FLD                      
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K820 ' WS-K820-STATUS             
                           ' K820-KEY-FLD : ' K820-KEY-FLD                      
                   MOVE WS-K820-STATUS TO RETURN-CODE                           
                   PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6000-REWRITE-BP13K820.                                                   
      *-------------------------------------------------------------            
                                                                                
           REWRITE BP13K820-REC.                                                
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
                ADD 1                    TO WS-K820-REWRT                       
           WHEN OTHER                                                           
                MOVE WS-K820-STATUS      TO RETURN-CODE                         
                DISPLAY 'ERROR WHILE REWRITING: ' WS-K820-STATUS                
                PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13C991 CONTROL '.                                         
           DISPLAY '-----------------------------------'.                       
           DISPLAY 'TOTAL F915  REC READ      = ' WS-F915-READ.                 
           DISPLAY 'TOTAL F820  REC READ      = ' WS-F820-READ.                 
           DISPLAY 'TOTAL K820  REC REWRITE   = ' WS-K820-REWRT.                
           DISPLAY 'TOTAL F820A REC WRITTEN   = ' WS-F820A-WRT.                 
           DISPLAY 'TOTAL K820 REC NOTFD      = ' WS-K820-NOTFD.                
                                                                                
           CLOSE      BP13F915                                                  
                      BP13F820                                                  
                      BP13K820                                                  
                      P13F820A.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
