      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13C365.                                                 
       AUTHOR.        SK.                                                       
       DATE-WRITTEN.  11/02/2003.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1. SURRENDER OF STUDIO APARTMENTS             *        
      *                     UPDATE SURRENDER DATE, SURRENDER BALANCE,  *        
      *                            ABORTIVE DATE                       *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13K690  -  AMORTISE DEFER INCOME        *        
      *                      BR05F150  -  SLS INTERFACE FILE           *        
      *                                                                *        
      *   OUTPUT FILES:  1.  BP13L365  -  UPDATED LISTING              *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP132292  SK   11/02/2003 NEW PGM                            *        
      *   BP132996  ED1  27/02/2007 CHANGED READ OF BP13K690 TO STARTBR*        
      *                             OF FIRST 9-BYTES OF SCH ACC & ADDED*        
      *                             REPORT BP13L365                    *        
      *   BP135903  ZDD1 13/08/2015 EXPAND WS-DIFF-MTH TO 4 BYTES      *        
      *   BP136398  FNP1 17/08/2016 EXPAND F690 FROM 250 TO 600        *        
      *   BP136524  FNP1 25/08/2017 REMOVE HARDCODING OF 360 MONTHS    *        
      *   BP137539  FNP1 22/10/2018 CHANGE SIZE OF DEFER INCOME        *        
      *   BP138944  FP6  13/10/2021 EXPAND F690 FROM 600 TO 800        *        
      *   BP139824  KAC1 15/04/2024 TO CATER FOR TRANSACTION COMPLETION*        
      *                             DATE (TCD) SURRENDER DATE EQUAL             
      *                             TO F150-DTE-APPLN-RECEIVED                  
      *                             WHEN CASE IS APPROVED IN SLS AND            
      *                             WHEN CASE IS COMPLETED IN SLS.              
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BR05F150  ASSIGN        TO BR05F150.                          
           SELECT BP13F690  ASSIGN        TO BP13F690.                          
           SELECT BP13L365  ASSIGN        TO BP13L365.                          
           SELECT P13F690A  ASSIGN        TO P13F690A.                          
           SELECT P13F690B  ASSIGN        TO P13F690B.                          
                                                                                
           SELECT BP13K690  ASSIGN        TO BP13K690                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K690-KEY-FLD                       
                            FILE STATUS   IS WS-K690-STATUS.                    
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BR05F150                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 150 CHARACTERS.                                      
                                                                                
       COPY BR05F150.                                                           
                                                                                
       FD  BP13F690                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
                                                                                
       COPY BP13F690.                                                           
                                                                                
       FD  BP13L365                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORD IS OMITTED                                              
           RECORDING MODE IS F.                                                 
       01  L365-PRTREC  PIC X(132).                                             
                                                                                
       FD  P13F690A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
                                                                                
       01  P13F690A-REC    PIC X(800).                                          
                                                                                
       FD  P13F690B                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
                                                                                
       01  P13F690B-REC    PIC X(800).                                          
                                                                                
       FD  BP13K690                                                             
           RECORD CONTAINS 800 CHARACTERS.                                      
                                                                                
       COPY BP13K690.                                                           
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-K690-READ          PIC 9(8)    VALUE 0.                    
           05 WS-CNT-F150-READ          PIC 9(8)    VALUE 0.                    
           05 WS-CNT-K690-UPDATE        PIC 9(8)    VALUE 0.                    
           05 WS-CNT-K690-NOTFND        PIC 9(8)    VALUE 0.                    
           05 WS-CNT-F690-WRITE         PIC 9(8)    VALUE 0.                    
           05 WS-CNT-F690A-WRITE        PIC 9(8)    VALUE 0.                    
           05 WS-CNT-F690B-WRITE        PIC 9(8)    VALUE 0.                    
           05 WS-CNT-L365-WRITE         PIC 9(8)    VALUE 0.                    
           05 WS-SNO                    PIC 9(5)    VALUE 0.                    
           05 WS-PAGE-CTR               PIC 9(4)    VALUE 0.                    
           05 WS-LINE-CTR               PIC 9(2)    VALUE 0.                    
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F150-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-K690-FOUND             PIC X(1)    VALUE 'Y'.                  
           05 WS-FLAG                   PIC X(1)    VALUE SPACES.               
                                                                                
       01  WS-TP-DATE.                                                          
           05  WS-TP-CCYY               PIC X(6)    VALUE SPACES.               
           05  WS-TP-MM                 PIC X(2)    VALUE SPACES.               
           05  WS-TP-DD                 PIC X(2)    VALUE SPACES.               
                                                                                
       01  WS-TP-DATE1                  REDEFINES WS-TP-DATE.                   
           05  WS-TP-CCYY1              PIC 9(4).                               
           05  WS-TP-MM1                PIC 9(2).                               
           05  WS-TP-DD1                PIC 9(2).                               
                                                                                
       01  WS-DTE-SURR.                                                         
           05  WS-SURR-CCYY             PIC 9(4)    VALUE ZEROS.                
           05  WS-SURR-MM               PIC 9(2)    VALUE ZEROS.                
           05  WS-SURR-DD               PIC 9(2)    VALUE ZEROS.                
                                                                                
       01  WS-EFF-DATE.                                                         
           05  WS-EFF-CCYY              PIC 9(4)    VALUE ZEROS.                
           05  WS-EFF-MM                PIC 9(2)    VALUE ZEROS.                
           05  WS-EFF-DD                PIC 9(2)    VALUE ZEROS.                
                                                                                
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-K690-STATUS            PIC 9(2)    VALUE 0.                    
           05 WS-K690-EOF               PIC X       VALUE 'N'.                  
           05 WS-DIFF-MTH               PIC 9(4)    VALUE ZEROS.                
           05 WS-LSE-MTH                PIC 9(4)    VALUE ZEROS.                
           05 WS-TENURE                 PIC 9(2)    VALUE ZEROS.                
           05 WS-SURR-AMT               PIC 9(6)V99 VALUE ZEROS.                
           05 WS-SURR-AMT-RND1          REDEFINES   WS-SURR-AMT.                
              10 WS-SURR-AMT-RND-INT    PIC 9(4).                               
              10 WS-SURR-AMT-RND-DEC    PIC 9(2).                               
              10 WS-SURR-AMT-RND-DEC1   PIC 9(2).                               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY              PIC 9(4).                               
           05  WS-SYS-MM                PIC 9(2).                               
           05  WS-SYS-DD                PIC 9(2).                               
                                                                                
       01  WS-EDITED-DATE               PIC X(10).                              
       01  WS-DATE-HDR                  PIC X(10) VALUE SPACES.                 
       01  WS-MAX-LINE                  PIC 99    VALUE 58.                     
                                                                                
       01  WS-BLANK-LINE                PIC X(132) VALUE SPACES.                
       01  WS-DASH-LINE                 PIC X(132) VALUE ALL '-'.               
                                                                                
      *---> DETAIL LINES FOR REPORT                                             
       01  L365-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L365'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L365-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L365-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L365-HEADER02.                                                       
           05  FILLER              PIC X(46)     VALUE SPACES.                  
           05  FILLER              PIC X(42)     VALUE                          
               'REPORT ON SURRENDER/ABORTIVE UPDATES '.                         
                                                                                
       01  L365-HEADER03.                                                       
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'AMORTISE'.              
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'SLS  '.                 
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'TRANS'.                 
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(25)     VALUE                          
               '------  SURRENDER  ------'.                                     
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'ABORTIVE'.              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'COMPENSATION'.          
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'COMPENSATION'.          
                                                                                
       01  L365-HEADER04.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'SCH-ACC'.               
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(01)     VALUE SPACE.                   
           05  FILLER              PIC X(07)     VALUE 'SCH-ACC'.               
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'TYPE '.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'DATE'.                  
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'AMT BAL'.               
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'AMT COMPUTED'.          
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'DATE '.                 
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'PAID DATE'.             
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'AMT'.                   
                                                                                
       01 L365-DETAILS.                                                         
          05 L365-DTL-SERIAL      PIC ZZZZ9     VALUE ZEROES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L365-SCH-ACC         PIC X(14)     VALUE SPACES.                   
          05 FILLER               PIC X(01)     VALUE SPACE.                    
          05 L365-SCH-ACC2        PIC X(14)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L365-REGNO           PIC X(08)     VALUE SPACES.                   
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 L365-TRANS-TYPE      PIC X(02)     VALUE SPACES.                   
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 L365-DTE-SURNDR      PIC X(10)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L365-SURNDR-BAL      PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L365-SURNDR-COMP     PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 L365-ABORTV-DTE      PIC X(10)      VALUE SPACES.                  
          05 FILLER               PIC X(02)      VALUE SPACES.                  
          05 L365-COMPN-DTE       PIC X(10)      VALUE SPACES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 L365-COMPN-AMT       PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM  1000-OPEN-FILES      THRU  1000-EXIT.                       
           PERFORM  2000-READ-BR05F150   THRU  2000-EXIT.                       
           PERFORM  3000-PROCESS         THRU  3000-EXIT                        
                    UNTIL WS-F150-EOF = 'Y'.                                    
           PERFORM  9000-CLOSE-ROUTINE   THRU  9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BR05F150                                                 
                I-O    BP13K690                                                 
                OUTPUT BP13F690                                                 
                       BP13L365                                                 
                       P13F690A                                                 
                       P13F690B.                                                
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K690-STATUS        TO RETURN-CODE                      
                 DISPLAY 'OPEN BP13K690 FAIL. STATUS=' WS-K690-STATUS           
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-BR05F150.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BR05F150                                                        
                AT END MOVE 'Y' TO WS-F150-EOF                                  
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F150-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 3100-STARTBR-BP13K690 THRU 3100-EXIT.                        
                                                                                
           PERFORM 2000-READ-BR05F150    THRU  2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3100-STARTBR-BP13K690.                                                   
      *----------------------------------------------------------------*        
           MOVE 'N'                   TO WS-K690-EOF.                           
           MOVE SPACES                TO K690-KEY-FLD.                          
           MOVE F150-NUM-HDB-REF(1:9) TO K690-NUM-SCH-ACC.                      
                                                                                
           START BP13K690 KEY >= K690-KEY-FLD                                   
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
                 PERFORM 3200-READNXT-BP13K690 THRU 3200-EXIT                   
                         UNTIL WS-K690-EOF = 'Y' OR                             
                 F150-NUM-HDB-REF(1:9) NOT = K690-NUM-SCH-ACC(1:9)              
                                                                                
              WHEN 23                                                           
                 DISPLAY 'REC NOT FND IN BP13K690'                              
                          ' KEY ' F150-NUM-HDB-REF                              
                 GO TO 3100-EXIT                                                
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN STARTBR BP13K690 - STATUS '                  
                 WS-K690-STATUS ' KEY ' F150-NUM-HDB-REF                        
                 MOVE        WS-K690-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       3200-READNXT-BP13K690.                                                   
      *----------------------------------------------------------------*        
           READ BP13K690 NEXT RECORD                                            
                  AT END MOVE 'Y' TO WS-K690-EOF                                
                     GO TO 3200-EXIT.                                           
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                 IF F150-NUM-HDB-REF(1:9) = K690-NUM-SCH-ACC(1:9)               
                    PERFORM 4000-UPDATE-BP13K690  THRU 4000-EXIT                
                 END-IF                                                         
                                                                                
              WHEN 10                                                           
                 MOVE 'Y' TO WS-K690-EOF                                        
                                                                                
              WHEN 23                                                           
                 MOVE 'Y' TO WS-K690-EOF                                        
                 DISPLAY 'REC NOT FND IN BP13K690'                              
                         ' KEY ' F150-NUM-HDB-REF                               
                                                                                
              WHEN OTHER                                                        
                 MOVE 'Y' TO WS-K690-EOF                                        
                 DISPLAY 'ERROR IN READNXT BP13K690 - STATUS '                  
                 WS-K690-STATUS ',KEY ' F150-NUM-HDB-REF                        
                 MOVE        WS-K690-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-UPDATE-BP13K690.                                                    
      *----------------------------------------------------------------*        
           EVALUATE F150-NUM-TRANS                                              
              WHEN 'AB'                                                         
                 PERFORM 4100-PARA-AB  THRU 4100-EXIT                           
              WHEN 'AP'                                                         
                 PERFORM 4200-PARA-AP  THRU 4200-EXIT                           
           END-EVALUATE.                                                        
                                                                                
           REWRITE K690-REC.                                                    
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN UPDATING BP13K690 - STATUS '                 
                             WS-K690-STATUS                                     
                 MOVE        WS-K690-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
                 GO TO 4000-EXIT                                                
           END-EVALUATE.                                                        
                                                                                
           ADD 1 TO WS-CNT-K690-UPDATE.                                         
                                                                                
      * WRITING AFTER IMAGE - BP13K690.                                         
                                                                                
           IF F150-NUM-TRANS = 'AP'                                             
              PERFORM 4600-WRITE-P13F690A  THRU 4600-EXIT                       
           END-IF.                                                              
                                                                                
           IF F150-NUM-HDB-REF(10:2) NOT = K690-NUM-SCH-ACC(10:2)               
              PERFORM 5000-WRITE-BP13L365     THRU 5000-EXIT                    
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4100-PARA-AB.                                                            
      *----------------------------------------------------------------*        
                                                                                
           IF (K690-AMT-COMPENSATION NOT NUMERIC)                               
               MOVE 0 TO K690-AMT-COMPENSATION                                  
           END-IF.                                                              
                                                                                
           IF (K690-DTE-SURRENDER-REQUEST = SPACES OR LOW-VALUES)               
              MOVE K690-REC  TO F690-REC                                        
              MOVE 'NS'      TO F690-TRANS-RMK                                  
              PERFORM 4700-WRITE-P13F690B  THRU 4700-EXIT                       
           ELSE                                                                 
           IF (K690-AMT-COMPENSATION > 0)                                       
              MOVE K690-REC  TO F690-REC                                        
              MOVE 'AC'      TO F690-TRANS-RMK                                  
              PERFORM 4700-WRITE-P13F690B  THRU 4700-EXIT                       
              MOVE SPACES TO K690-DTE-ABORTIVE                                  
                             K690-DTE-SURRENDER-REQUEST                         
                             K690-DTE-COMPENSATION-PD                           
              MOVE ZEROS  TO K690-AMT-SURRENDER-BAL                             
                             K690-AMT-SURRENDER-COMPUTED                        
                             K690-AMT-COMPENSATION                              
           ELSE                                                                 
               PERFORM 4500-WRITE-BP13F690  THRU 4500-EXIT                      
               MOVE SPACES TO K690-DTE-ABORTIVE                                 
                              K690-DTE-SURRENDER-REQUEST                        
               MOVE ZEROS  TO K690-AMT-SURRENDER-BAL                            
                              K690-AMT-SURRENDER-COMPUTED                       
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4200-PARA-AP.                                                            
      *----------------------------------------------------------------*        
                                                                                
           MOVE F150-DTE-APPLN-RECEIVED                                         
                             TO K690-DTE-SURRENDER-REQUEST.                     
           PERFORM 4300-GET-EFF-DTE     THRU 4300-EXIT.                         
                                                                                
           MOVE 0 TO WS-DIFF-MTH                                                
                     WS-LSE-MTH.                                                
           MOVE F150-DTE-APPLN-RECEIVED TO WS-DTE-SURR.                         
           PERFORM 4350-GET-DIFF-MTH    THRU 4350-EXIT.                         
                                                                                
           IF K690-NUM-LEASE-TENURE NOT NUMERIC                                 
              MOVE ZEROES                TO WS-TENURE                           
           ELSE                                                                 
              MOVE K690-NUM-LEASE-TENURE TO WS-TENURE                           
           END-IF                                                               
                                                                                
           COMPUTE WS-LSE-MTH = WS-TENURE * 12.                                 
                                                                                
           COMPUTE WS-SURR-AMT = (K690-AMT-DEFER-INCOME *                       
                             (WS-LSE-MTH - WS-DIFF-MTH)) / WS-LSE-MTH.          
                                                                                
           MOVE ZEROS                 TO WS-SURR-AMT-RND-DEC.                   
           MOVE WS-SURR-AMT-RND1(1:6) TO K690-AMT-SURRENDER-BAL                 
                                         K690-AMT-SURRENDER-COMPUTED.           
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4300-GET-EFF-DTE.                                                        
      *----------------------------------------------------------------*        
                                                                                
           MOVE K690-DTE-KEY-ISSUE TO WS-TP-DATE.                               
                                                                                
           IF WS-TP-DD1 < 16                                                    
              MOVE WS-TP-CCYY1  TO WS-EFF-CCYY                                  
              MOVE WS-TP-MM1    TO WS-EFF-MM                                    
              MOVE 1            TO WS-EFF-DD                                    
           ELSE                                                                 
           IF WS-TP-DD1 > 15                                                    
              COMPUTE WS-TP-MM1 =  WS-TP-MM1 + 1                                
              IF WS-TP-MM1 > 12                                                 
                 COMPUTE WS-TP-CCYY1 = WS-TP-CCYY1 + 1                          
                 MOVE 1  TO WS-TP-MM1                                           
              END-IF                                                            
                                                                                
              MOVE WS-TP-CCYY1  TO WS-EFF-CCYY                                  
              MOVE WS-TP-MM1    TO WS-EFF-MM                                    
              MOVE 1            TO WS-EFF-DD                                    
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4350-GET-DIFF-MTH.                                                       
      *----------------------------------------------------------------*        
                                                                                
           COMPUTE WS-DIFF-MTH = ((WS-SURR-CCYY * 12) + WS-SURR-MM) -           
                                 ((WS-EFF-CCYY * 12) + WS-EFF-MM).              
       4350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4500-WRITE-BP13F690.                                                     
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES             TO F690-REC.                                 
           INITIALIZE                 F690-REC.                                 
                                                                                
           MOVE K690-REC           TO F690-REC.                                 
           MOVE F150-DTE-ABORT     TO F690-DTE-ABORTIVE.                        
           MOVE WS-SYSTEM-DATE     TO F690-DTE-UPDATE.                          
                                                                                
           WRITE F690-REC.                                                      
           ADD 1 TO WS-CNT-F690-WRITE.                                          
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4600-WRITE-P13F690A.                                                     
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES          TO P13F690A-REC.                                
           INITIALIZE              P13F690A-REC.                                
                                                                                
           MOVE K690-REC        TO P13F690A-REC.                                
           MOVE WS-SYSTEM-DATE  TO P13F690A-REC(243:8).                         
                                                                                
           WRITE P13F690A-REC.                                                  
           ADD 1 TO WS-CNT-F690A-WRITE.                                         
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4700-WRITE-P13F690B.                                                     
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES          TO P13F690B-REC.                                
           INITIALIZE              P13F690B-REC.                                
                                                                                
           MOVE WS-SYSTEM-DATE  TO F690-DTE-UPDATE.                             
           MOVE F150-DTE-ABORT  TO F690-DTE-ABORTIVE.                           
                                                                                
           WRITE P13F690B-REC   FROM F690-REC.                                  
           ADD 1 TO WS-CNT-F690B-WRITE.                                         
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-WRITE-BP13L365.                                                     
      *----------------------------------------------------------------*        
      *------>  MOVE DETAILS                                                    
           INITIALIZE     L365-DETAILS.                                         
                                                                                
           STRING K690-NUM-SCH-ACC(1:4) '-'                                     
                  K690-NUM-SCH-ACC(5:4) '-'                                     
                  K690-NUM-SCH-ACC(9:1) '-'                                     
                  K690-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE INTO L365-SCH-ACC.                          
                                                                                
           STRING F150-NUM-HDB-REF(1:4) '-'                                     
                  F150-NUM-HDB-REF(5:4) '-'                                     
                  F150-NUM-HDB-REF(9:1) '-'                                     
                  F150-NUM-HDB-REF(10:2)                                        
                  DELIMITED BY SIZE INTO L365-SCH-ACC2.                         
                                                                                
           MOVE K690-NUM-REGN               TO L365-REGNO.                      
           MOVE F150-NUM-TRANS              TO L365-TRANS-TYPE.                 
           MOVE K690-AMT-SURRENDER-BAL      TO L365-SURNDR-BAL.                 
           MOVE K690-AMT-SURRENDER-COMPUTED TO L365-SURNDR-COMP.                
           MOVE K690-AMT-COMPENSATION       TO L365-COMPN-AMT.                  
                                                                                
           IF K690-DTE-SURRENDER-REQUEST NOT = SPACES AND LOW-VALUES            
                                               AND ZEROES                       
              STRING K690-DTE-SURRENDER-REQUEST(7:2) '/'                        
                     K690-DTE-SURRENDER-REQUEST(5:2) '/'                        
                     K690-DTE-SURRENDER-REQUEST(1:4)                            
                     DELIMITED BY SIZE INTO L365-DTE-SURNDR                     
           END-IF.                                                              
                                                                                
           IF K690-DTE-ABORTIVE NOT = SPACES AND LOW-VALUES AND ZEROES          
              STRING K690-DTE-ABORTIVE(7:2) '/'                                 
                     K690-DTE-ABORTIVE(5:2) '/'                                 
                     K690-DTE-ABORTIVE(1:4)                                     
                     DELIMITED BY SIZE INTO L365-ABORTV-DTE                     
           END-IF.                                                              
                                                                                
           IF K690-DTE-COMPENSATION-PD NOT = SPACES AND LOW-VALUES              
                                             AND ZEROES                         
              STRING K690-DTE-COMPENSATION-PD(7:2) '/'                          
                     K690-DTE-COMPENSATION-PD(5:2) '/'                          
                     K690-DTE-COMPENSATION-PD(1:4)                              
                     DELIMITED BY SIZE INTO L365-COMPN-DTE                      
           END-IF.                                                              
                                                                                
      *------>  PRINT REPORT                                                    
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  L365-DTL-SERIAL.                    
           MOVE  WS-EDITED-DATE         TO  L365-DATE-HDR.                      
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO L365-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO L365-PAGE-HDR                               
              PERFORM 6000-PRINT-HEADING THRU 6000-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE L365-PRTREC FROM L365-DETAILS.                                 
                                                                                
           ADD   1   TO   WS-LINE-CTR.                                          
           ADD   1   TO   WS-CNT-L365-WRITE.                                    
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       6000-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
                                                                                
            WRITE L365-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L365-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L365-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L365-PRTREC FROM L365-HEADER01 AFTER PAGE.                    
            WRITE L365-PRTREC FROM L365-HEADER02.                               
            WRITE L365-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L365-PRTREC FROM L365-HEADER03.                               
            WRITE L365-PRTREC FROM L365-HEADER04.                               
            WRITE L365-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  9 TO WS-LINE-CTR.                                             
                                                                                
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE BP13K690                                                       
                 BR05F150                                                       
                 BP13F690                                                       
                 BP13L365                                                       
                 P13F690A                                                       
                 P13F690B.                                                      
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K690-STATUS      TO RETURN-CODE                        
                 DISPLAY 'CLOSING BP13K690 ERROR. STATUS='                      
                                             WS-K690-STATUS                     
           END-EVALUATE.                                                        
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13C365 CONTROL TOTALS ***** '.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM  (BR05F150) = '                     
                    WS-CNT-F150-READ.                                           
           DISPLAY 'NO OF RECORDS UPDATED    (BP13K690) = '                     
                    WS-CNT-K690-UPDATE.                                         
           DISPLAY 'NO OF RECORDS NOT FND    (BP13K690) = '                     
                    WS-CNT-K690-NOTFND.                                         
           DISPLAY 'NO OF RECORDS WRITE      (BP13F690) = '                     
                    WS-CNT-F690-WRITE.                                          
           DISPLAY 'NO OF RECORDS WRITE (AP) (P13F690A) = '                     
                    WS-CNT-F690A-WRITE.                                         
           DISPLAY 'NO OF RECORDS WRITE-ERROR(P13F690B) = '                     
                    WS-CNT-F690B-WRITE.                                         
           DISPLAY 'NO OF RECS IN REPORT     (BP13L365) = '                     
                    WS-CNT-L365-WRITE.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
