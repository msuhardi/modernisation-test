       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD86.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  27/01/2011.                                               
      *****************************************************************         
      *                SYSTEM OF COMMITMENT (BP13)                    *         
      *****************************************************************         
      * OBJECTIVE : MATCH BP13F595 WITH PPO MASTER AND MASTER HISTORY *         
      *             (BR10K010 AND BR10K080)                           *         
      *             IF NRIC FOUND IN BR10K010, OUTPUT TO FOUND FILE   *         
      *             IF (NRIC FOUND IN BR10K080) AND                   *         
      *                (SYS-DATE - DISPOSAL DATE < 2.5 YR)            *         
      *                OUTPUT TO FOUND FILE                           *         
      *****************************************************************         
      * CHG REF  DATE     BY   DESCRIPTION                            *         
      * -------- -------- ---- -------------------------------------- *         
      * BP134247 20110127 JB8  NEW PRGM                               *         
      * BP135921 20150708 FNP  UPDATE BP13K730 WITH PPO TAG IF FOUND  *         
      * BP139003 20220401 KR13 FIX PPO TAGGING.                       *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595 ASSIGN TO BP13F595.                                  
                                                                                
           SELECT BR10K010 ASSIGN TO BR10K010                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS BR10K010-STATUS.                           
                                                                                
           SELECT BR10K080 ASSIGN TO BR10K080                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K080-KEY-FLD                               
                  FILE STATUS     IS BR10K080-STATUS.                           
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K730-KEY-NEW                               
                  FILE STATUS     IS BP13K730-STATUS.                           
                                                                                
           SELECT P13F595F ASSIGN TO P13F595F.                                  
           SELECT P13F595N ASSIGN TO P13F595N.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F595                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F595.                                                           
                                                                                
       FD   BR10K010                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BR10K010.                                                           
                                                                                
       FD   BR10K080                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BR10K080.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   P13F595F                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   P13F595F-REC     PIC X(500).                                        
                                                                                
       FD   P13F595N                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   P13F595N-REC     PIC X(500).                                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  NO-F595-READ                 PIC 9(7)  VALUE ZEROS.              
           05  NO-K010-FND                  PIC 9(7)  VALUE ZEROS.              
           05  NO-K080-FND                  PIC 9(7)  VALUE ZEROS.              
           05  NO-K730-FND                  PIC 9(7)  VALUE ZEROS.              
           05  NO-K730-NFND                 PIC 9(7)  VALUE ZEROS.              
           05  NO-PPO-A                     PIC 9(7)  VALUE ZEROS.              
           05  NO-PPO-B                     PIC 9(7)  VALUE ZEROS.              
           05  NO-PPO-C                     PIC 9(7)  VALUE ZEROS.              
           05  NO-K080-VALID                PIC 9(7)  VALUE ZEROS.              
           05  NO-F595F-WRITE               PIC 9(7)  VALUE ZEROS.              
           05  NO-F595N-WRITE               PIC 9(7)  VALUE ZEROS.              
           05  WS-K730-UPD                  PIC 9(7)  VALUE ZEROS.              
           05  WS-NUM-PPO                   PIC X(1)  VALUE SPACES.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  BR10K010-STATUS              PIC 99    VALUE ZEROES.             
           05  BR10K080-STATUS              PIC 99    VALUE ZEROES.             
           05  BP13K730-STATUS              PIC 99    VALUE ZEROES.             
           05  WS-F595-EOF                  PIC X(1)  VALUE SPACE.              
               88  F595-EOF                 VALUE 'Y'.                          
           05  WS-K010-FND                  PIC X(1)  VALUE SPACE.              
               88  K010-FND                 VALUE 'Y'.                          
           05  WS-K010-EOF                  PIC X(1)  VALUE SPACE.              
               88  K010-EOF                 VALUE 'Y'.                          
           05  WS-K080-FND                  PIC X(1)  VALUE SPACE.              
               88  K080-FND                 VALUE 'Y'.                          
           05  WS-K080-EOF                  PIC X(1)  VALUE SPACE.              
               88  K080-EOF                 VALUE 'Y'.                          
                                                                                
           05 WS-DISPOSAL               PIC X(8)    VALUE SPACES.               
           05 WS-DISPOSAL-NUM REDEFINES WS-DISPOSAL                             
                                        PIC 9(8).                               
                                                                                
           05 WS-DTE-REQ                PIC X(8)    VALUE SPACES.               
           05 WS-DTE-REQ-R    REDEFINES WS-DTE-REQ.                             
              10 WS-DTE-REQ-NUM.                                                
                 15 WS-DTE-REQ-CCYY     PIC 9(4).                               
                 15 WS-DTE-REQ-MM       PIC 9(2).                               
                 15 WS-DTE-REQ-DD       PIC 9(2).                               
                                                                                
           05 WS-PREV-REGN              PIC X(8)    VALUE SPACES.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  CNT-MTH                    PIC 9(08).                                
       01  WS-DTE-DIS.                                                          
           05  WS-DIS-CCYY            PIC 9(4).                                 
           05  WS-DIS-MM              PIC 9(2).                                 
           05  WS-DIS-DD              PIC 9(2).                                 
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
       0000-MAIN.                                                               
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F595       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL F595-EOF.                                                    
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================*                                                        
       1000-OPEN-FILES.                                                         
      *================*                                                        
                                                                                
           OPEN INPUT BP13F595                                                  
                      BR10K010                                                  
                      BR10K080                                                  
                I-O   BP13K730                                                  
               OUTPUT P13F595F                                                  
                      P13F595N.                                                 
                                                                                
           IF BR10K010-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BR10K010 (' BR10K010-STATUS ')'            
              MOVE BR10K010-STATUS           TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF BR10K080-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BR10K080 (' BR10K080-STATUS ')'            
              MOVE BR10K080-STATUS           TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF BP13K730-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BP13K730 (' BP13K730-STATUS ')'            
              MOVE BP13K730-STATUS           TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       2000-READ-BP13F595.                                                      
      *===================*                                                     
                                                                                
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                TO WS-F595-EOF                          
                GO                      TO 2000-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO NO-F595-READ.                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       3000-PROCESS-RECORD.                                                     
      *===================*                                                     
                                                                                
           MOVE SPACES                 TO WS-NUM-PPO.                           
                                                                                
           PERFORM 4100-START-K010             THRU 4100-EXIT                   
      *    IF NOT K010-FND                                                      
              PERFORM 4300-START-K080          THRU 4300-EXIT                   
      *    END-IF                                                               
                                                                                
           IF K010-FND OR K080-FND                                              
              PERFORM 6000-READ-BP13K730       THRU 6000-EXIT                   
              MOVE WS-NUM-PPO         TO       F595-NUM-PPO                     
              WRITE P13F595F-REC               FROM BP13F595-REC                
              ADD 1                              TO NO-F595F-WRITE              
           ELSE                                                                 
              WRITE P13F595N-REC               FROM BP13F595-REC                
              ADD 1                              TO NO-F595N-WRITE              
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F595          THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4100-START-K010.                                                         
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES       TO K010-KEY-FLD.                               
           MOVE 'N'              TO WS-K010-EOF                                 
                                    WS-K010-FND.                                
                                                                                
           MOVE F595-NUM-NRIC1   TO K010-NUM-UIN.                               
           START BR10K010 KEY >= K010-NUM-UIN.                                  
                                                                                
           EVALUATE BR10K010-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y'        TO WS-K010-EOF                                 
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BR10K010 STATUS - '                    
                                            BR10K010-STATUS                     
                 MOVE BR10K010-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 4200-READNXT-K010   THRU 4200-EXIT                           
              UNTIL K010-NUM-UIN NOT = F595-NUM-NRIC1                           
                 OR WS-K010-EOF = 'Y'.                                          
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4200-READNXT-K010.                                                       
      *--------------------------------------------------------*                
                                                                                
           READ BR10K010 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-K010-EOF                                  
           END-READ.                                                            
                                                                                
           IF K010-NUM-UIN = F595-NUM-NRIC1                                     
      *       IF K010-CDE-PPTY-TYPE(1:1) = 'R' AND                              
              IF (K010-CDE-PPTY-TYPE NOT = 'RXHOS' AND                          
                                           'RXOTH' AND                          
                                           'RXSTQ')                             
                 MOVE 'Y'       TO WS-K010-FND WS-K010-EOF                      
                 ADD 1          TO NO-K010-FND                                  
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'          TO WS-K010-EOF                                  
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4300-START-K080.                                                         
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES       TO K080-KEY-FLD.                               
           MOVE 'N'              TO WS-K080-EOF                                 
                                    WS-K080-FND.                                
                                                                                
           MOVE F595-NUM-NRIC1   TO K080-NUM-UIN.                               
           START BR10K080 KEY >= K080-NUM-UIN.                                  
                                                                                
           EVALUATE BR10K080-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y' TO WS-K080-EOF                                        
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BR10K080 STATUS - '                    
                                            BR10K080-STATUS                     
                 MOVE BR10K080-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 4400-READNXT-K080   THRU 4400-EXIT                           
              UNTIL K080-NUM-UIN NOT = F595-NUM-NRIC1                           
                 OR WS-K080-EOF = 'Y'.                                          
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4400-READNXT-K080.                                                       
      *--------------------------------------------------------*                
                                                                                
           READ BR10K080 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-K080-EOF                                  
           END-READ.                                                            
                                                                                
           IF K080-DTE-DISPOSAL NOT NUMERIC                                     
              MOVE ZEROES        TO K080-DTE-DISPOSAL.                          
                                                                                
           IF K080-NUM-UIN = F595-NUM-NRIC1                                     
      *       IF K080-CDE-PPTY-TYPE(1:1) = 'R' AND                              
              IF (K080-CDE-PPTY-TYPE NOT = 'RXHOS' AND                          
                                           'RXOTH' AND                          
                                           'RXSTQ')                             
                 MOVE K080-DTE-DISPOSAL TO WS-DTE-DIS                           
                 COMPUTE CNT-MTH = (WS-SYS-CCYY * 12 + WS-SYS-MM)               
                                 - (WS-DIS-CCYY * 12 + WS-DIS-MM)               
                 IF CNT-MTH < 30                                                
                    MOVE 'Y'    TO WS-K080-FND WS-K080-EOF                      
                    ADD 1       TO NO-K080-VALID                                
                 ELSE                                                           
                    IF CNT-MTH = 30                                             
                       IF WS-SYS-DD < WS-DIS-DD                                 
                          MOVE 'Y' TO WS-K080-FND WS-K080-EOF                   
                          ADD 1       TO NO-K080-VALID                          
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
              ADD 1             TO NO-K080-FND                                  
           ELSE                                                                 
              MOVE 'Y'          TO WS-K080-EOF                                  
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       6000-READ-BP13K730.                                                      
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES                TO BP13K730-REC.                          
           MOVE F595-NUM-REGN         TO K730-NUM-REGN.                         
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE BP13K730-STATUS                                             
              WHEN 00                                                           
                 IF K730-NUM-PPO = SPACES OR LOW-VALUES                         
                    IF K010-FND AND K080-FND                                    
                       MOVE 'C'        TO K730-NUM-PPO                          
                                          WS-NUM-PPO                            
                       ADD 1           TO NO-PPO-C                              
                    ELSE                                                        
                       IF K010-FND                                              
                          MOVE 'B'        TO K730-NUM-PPO                       
                                             WS-NUM-PPO                         
                          ADD 1           TO NO-PPO-A                           
                       ELSE                                                     
                          IF K080-FND                                           
                             MOVE 'A'     TO K730-NUM-PPO                       
                                             WS-NUM-PPO                         
                             ADD 1        TO NO-PPO-B                           
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                    PERFORM 6500-REWRITE-BP13K730  THRU 6500-EXIT               
                 END-IF                                                         
                 ADD 1 TO NO-K730-FND                                           
             WHEN 23                                                            
                 ADD 1 TO NO-K730-NFND                                          
                 DISPLAY 'RECORD NOT FOUND IN BP13K730: ' K730-NUM-REGN         
             WHEN OTHER                                                         
                 DISPLAY 'ERROR WITH READ OF BP13K730. STATUS IS '              
                      BP13K730-STATUS                                           
                 MOVE BP13K730-STATUS              TO RETURN-CODE               
                 PERFORM 9000-CLOSE-FILES          THRU 9000-EXIT               
             END-EVALUATE.                                                      
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       6500-REWRITE-BP13K730.                                                   
      ****************************************************************          
                                                                                
           REWRITE BP13K730-REC.                                                
                                                                                
           EVALUATE BP13K730-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
              WHEN 22                                                           
                 ADD 1 TO WS-K730-UPD                                           
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WITH UPDATING OF BP13K730. STATUS IS '          
                     BP13K730-STATUS                                            
                 MOVE BP13K730-STATUS             TO RETURN-CODE                
                 PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                
           END-EVALUATE.                                                        
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       9000-CLOSE-FILES.                                                        
      *=====================*                                                   
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CD86'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO-F595-READ            : ' NO-F595-READ.                   
           DISPLAY 'NO-K010-FND             : ' NO-K010-FND.                    
           DISPLAY 'NO-K080-FND             : ' NO-K080-FND.                    
           DISPLAY 'NO-K080-VALID           : ' NO-K080-VALID.                  
           DISPLAY 'NO-K730-FND             : ' NO-K730-FND.                    
           DISPLAY 'NO-K730-NFND            : ' NO-K730-NFND.                   
           DISPLAY 'NO-PPO-A                : ' NO-PPO-A.                       
           DISPLAY 'NO-PPO-B                : ' NO-PPO-B.                       
           DISPLAY 'NO-PPO-C                : ' NO-PPO-C.                       
           DISPLAY 'NO-F595F-WRITE          : ' NO-F595F-WRITE.                 
           DISPLAY 'NO-F595N-WRITE          : ' NO-F595N-WRITE.                 
           DISPLAY 'NO-K730-UPDATED         : ' WS-K730-UPD.                    
                                                                                
           CLOSE BP13F595                                                       
                 BR10K010                                                       
                 BR10K080                                                       
                 P13F595F                                                       
                 P13F595N.                                                      
                                                                                
           IF BR10K010-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING BR10K010 (' BR10K010-STATUS ')'            
           END-IF.                                                              
                                                                                
           IF BR10K080-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING BR10K080 (' BR10K080-STATUS ')'            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
