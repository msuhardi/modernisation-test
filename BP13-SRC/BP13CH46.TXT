      *------------------------------------------------------------*            
       IDENTIFICATION DIVISION.                                                 
      *------------------------------------------------------------*            
       PROGRAM-ID.   BP13CH46.                                                  
      *AUTHOR.       ZARITA DANE SARMIENTO.                                     
      *DATE-WRITTEN. 12/10/2017.                                                
      *------------------------------------------------------------*            
      *                   SYSTEM OF COMMITMENT  (BP13)             *            
      *------------------------------------------------------------*            
      *                                                            *            
      *  OBJECTIVE   : 1.  TO RE-ASSIGN OIC (FROM PANEL P13M021)   *            
      *                2.  ALTERNATE REASSIGNMENT                  *            
      *                3.  CAN REASSIGN CASE WITH MAX NO OF CASES  *            
      *                    OR WITH DEFAULT 9999 MAX NO             *            
      *                4.  POS OIC = 'Y'                           *            
      *                5.  TO REASSIGN INCLUDING OTHER REGN UNDER  *            
      *                    THE SAME NRIC                           *            
      *                                                            *            
      *  INPUT FILES : 1.  BP13F026  -  OIC RE-ASSIGN FILE         *            
      *                2.  BP13F022  -  OIC FILE (TO PRINT LETTER) *            
      *                3.  BP13K022  -  OIC FILE                   *            
      *                4.  BP13K028  -  OIC HISTORY FILE           *            
      *                5.  BP13K034  -  NRIC LIST                  *            
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *------------------------------------------------------------*            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F026 ASSIGN TO BP13F026.                                  
           SELECT BP13F022 ASSIGN TO BP13F022.                                  
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
           SELECT BP13K028 ASSIGN TO BP13K028                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K028-KEY-FLD                               
                  FILE STATUS     IS WS-K028-STATUS.                            
                                                                                
           SELECT BP13K034 ASSIGN TO BP13K034                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K034-KEY-FLD                               
                  ALTERNATE KEY   IS K034-NUM-NRIC                              
                  FILE STATUS     IS WS-K034-STATUS.                            
                                                                                
           SELECT P13F022A ASSIGN TO P13F022A.                                  
           SELECT BP13RPT1 ASSIGN TO BP13RPT1.                                  
                                                                                
      *------------------------------------------------------------*            
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *------------------------------------------------------------*            
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13RPT1                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   BP13RPT1-REC.                                                       
            05 RPT1-OLD-OIC    PIC X(05) VALUE SPACES.                          
            05 FILLER1         PIC X(01) VALUE SPACES.                          
            05 RPT1-NUM-REGN   PIC X(08) VALUE SPACES.                          
            05 FILLER2         PIC X(01) VALUE SPACES.                          
            05 RPT1-NEW-OIC    PIC X(05) VALUE SPACES.                          
            05 FILLER          PIC X(80) VALUE SPACES.                          
                                                                                
                                                                                
       FD   BP13K028                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K028.                                                           
                                                                                
       FD   BP13K034                                                            
            RECORD CONTAINS 30  CHARACTERS.                                     
                                                                                
       COPY BP13K034.                                                           
                                                                                
       FD   BP13F026                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       COPY BP13F026.                                                           
                                                                                
       FD   BP13F022                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       COPY BP13F022.                                                           
                                                                                
       FD   P13F022A                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       01   P13F022A-REC   PIC X(100).                                          
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
                                                                                
       01  WORK-AREAS.                                                          
           05  WS-F022-READ             PIC 9(08)  VALUE 0.                     
           05  WS-F026-READ             PIC 9(08)  VALUE 0.                     
           05  WS-K022-NFND             PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-K800-FOUND            PIC 9(08)  VALUE 0.                     
           05  WS-K800-CAN              PIC 9(08)  VALUE 0.                     
           05  WS-NOFLAT                PIC 9(08)  VALUE 0.                     
           05  WS-K800-NFND             PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-RPT1-CNT              PIC 9(08)  VALUE 0.                     
           05  WS-TOTAL-CASES           PIC 9(05)  VALUE 0.                     
           05  WS-TOTAL-CASES-CTR       PIC 9(05)  VALUE 0.                     
                                                                                
           05  WS-K022-EOF              PIC X(01)  VALUE 'N'.                   
           05  WS-F022-EOF              PIC X(01)  VALUE 'N'.                   
           05  WS-F026-EOF              PIC X(01)  VALUE 'N'.                   
           05  WS-K034-EOF-SW           PIC X(01)  VALUE 'N'.                   
                                                                                
           05  WS-K022-WRITE            PIC 9(08)  VALUE 0.                     
           05  WS-F022A-WRITE           PIC 9(08)  VALUE 0.                     
           05  WS-K028-WRITE            PIC 9(08)  VALUE 0.                     
           05  WS-EC-DBS                PIC 9(08)  VALUE 0.                     
           05  WS-OIC-APPL-UPD          PIC 9(08)  VALUE 0.                     
           05  WS-BYPASS                PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-K022-STATUS           PIC 9(02)  VALUE 0.                     
           05  WS-K028-STATUS           PIC 9(02)  VALUE 0.                     
           05  WS-K800-STATUS           PIC 9(02)  VALUE 0.                     
           05  WS-K034-STATUS           PIC 9(02)  VALUE 0.                     
                                                                                
           05  WS-COUNTER               PIC 9(03)  VALUE 1.                     
           05  WS-COUNTER1              PIC 9(03)  VALUE 0.                     
           05  WS-INTERVAL-OIC          PIC 9(03)  VALUE 0.                     
           05  WS-CTR-CHECK             PIC 9(03)  VALUE 0.                     
           05  WS-COUNT                 PIC 9(03)  VALUE 0.                     
           05  WS-COUNT1                PIC 9(03)  VALUE 0.                     
           05  WS-CTR                   PIC 9(03)  VALUE 0.                     
           05  WS-INTERVAL              PIC 9(02)   VALUE 1.                    
           05  WS-TOT                   PIC 9(04)   VALUE 0.                    
           05  WS-MAX-CASE              PIC 9(04)   VALUE 0.                    
                                                                                
           05  WS-ROUND                 PIC 9(03)  VALUE 0.                     
           05  WS-ROUND-ED              PIC Z(03).                              
                                                                                
           05  WS-LAST                  PIC X(06)  VALUE SPACES.                
           05  WS-FROM-OIC              PIC X(05)  VALUE SPACES.                
           05  WS-FLAG                  PIC X(01)  VALUE 'N'.                   
           05  WS-K022-FND              PIC X(01)  VALUE 'N'.                   
           05  WS-NEW-OIC               PIC X(05)  VALUE SPACES.                
           05  WS-NRIC                  PIC X(09)  VALUE SPACES.                
                                                                                
       01  WS-DATE-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY          PIC 9(04).                              
               10  WS-SYS-MM            PIC 9(02).                              
               10  WS-SYS-DD            PIC 9(02).                              
                                                                                
           05  WS-EDITED-DATE           PIC X(10)  VALUE SPACES.                
                                                                                
       01  WS-F026-VAR.                                                         
           05  WS-F026-KEY-FLD          PIC X(11)  VALUE SPACES.                
           05  WS-F026-CDE-COMPLETE     PIC X(01)  VALUE SPACES.                
           05  WS-F026-NUM-INTVL        PIC 9(02)  VALUE 0.                     
           05  WS-F026-NUM-MAX-INTVL    PIC 9(04)  VALUE 0.                     
           05  WS-F026-CDE-ROUND        PIC X(01)  VALUE SPACES.                
                                                                                
       01  WS-K022-KEY.                                                         
           05  WS-K022-NUM-OIC          PIC X(05)  VALUE SPACES.                
           05  WS-K022-NUM-REGN         PIC X(08)  VALUE SPACES.                
                                                                                
       01  WS-OIC-TABLE.                                                        
           05 WS-OIC-ASGN  OCCURS 30 TIMES.                                     
              10 WS-NUM-OIC             PIC X(05).                              
              10 WS-MAX-CNT             PIC 9(04).                              
              10 WS-NUM-CNT             PIC 9(04).                              
              10 WS-NUM-TOT             PIC 9(04).                              
                                                                                
       01 WS-K022-REC                   PIC X(100)   VALUE SPACES.              
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 1200-READ-F026         THRU 1200-EXIT.                       
           PERFORM UNTIL WS-F026-EOF = 'Y' OR WS-F022-EOF = 'Y'                 
             PERFORM 2000-PROCESS-TABLE     THRU 2000-EXIT                      
             PERFORM 2100-READ-F022         THRU 2100-EXIT                      
             PERFORM 2600-RE-ASSIGN         THRU 2600-EXIT                      
                   UNTIL WS-F022-EOF = 'Y' OR                                   
                         WS-TOTAL-CASES-CTR >= WS-TOTAL-CASES                   
      *      PERFORM 1200-READ-F026         THRU 1200-EXIT                      
           END-PERFORM.                                                         
           PERFORM 5000-DISPLAY-SUMMARY   THRU 5000-EXIT.                       
                                                                                
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1000-OPEN-FILES.                                                         
      *------------------------------------------------------------*            
                                                                                
           OPEN INPUT  BP13F026                                                 
                       BP13F022                                                 
                       BP13K034                                                 
                I-O    BP13K022                                                 
                       BP13K028                                                 
                OUTPUT P13F022A                                                 
                       BP13RPT1.                                                
                                                                                
                                                                                
           IF  WS-K022-STATUS NOT = 00 AND 97                                   
                 DISPLAY 'OPEN BP13K022 FAIL. STATUS - '                        
                                            WS-K022-STATUS                      
                 MOVE  WS-K022-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K028-STATUS NOT = 00 AND 97                                    
                 DISPLAY 'OPEN BP13K028 FAIL. STATUS - '                        
                                            WS-K028-STATUS                      
                 MOVE  WS-K028-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K034-STATUS NOT = 00 AND 97                                    
                 DISPLAY 'OPEN BP13K034 FAIL. STATUS - '                        
                                            WS-K034-STATUS                      
                 MOVE  WS-K034-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING  WS-SYSTEM-DATE(7:2) '/'                                      
                   WS-SYSTEM-DATE(5:2) '/'                                      
                   WS-SYSTEM-DATE(1:4)                                          
                   DELIMITED BY SIZE                                            
                   INTO WS-EDITED-DATE                                          
           END-STRING.                                                          
                                                                                
           MOVE SPACES    TO BP13RPT1-REC                                       
           MOVE  'OLD-OIC;REGN-NUM;NEW-OIC;' TO BP13RPT1-REC                    
           WRITE BP13RPT1-REC.                                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1200-READ-F026.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13F026                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F026-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-F026-READ                                        
           END-READ.                                                            
                                                                                
           IF WS-LAST = SPACES OR LOW-VALUES                                    
              MOVE F026-NUM-FROM-OIC       TO WS-LAST                           
              MOVE F026-NUM-FROM-OIC       TO WS-FROM-OIC                       
              MOVE F026-CDE-COMPLETE       TO WS-F026-CDE-COMPLETE              
              MOVE F026-NUM-INTERVAL       TO WS-F026-NUM-INTVL                 
              MOVE F026-NUM-MAX-INTERVAL   TO WS-F026-NUM-MAX-INTVL             
              MOVE F026-CDE-ROUND          TO WS-F026-CDE-ROUND                 
           END-IF.                                                              
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2000-PROCESS-TABLE.                                                      
      *------------------------------------------------------------*            
                                                                                
           MOVE 'N'  TO WS-K022-EOF.                                            
           MOVE F026-KEY-FLD              TO WS-F026-KEY-FLD.                   
           MOVE F026-NUM-FROM-OIC         TO WS-FROM-OIC.                       
           MOVE BP13F026-REC(1:5)         TO WS-LAST.                           
                                                                                
           PERFORM 2500-WRITE-TABLE       THRU 2500-EXIT.                       
                                                                                
           MOVE 'N'                 TO WS-K022-EOF.                             
           MOVE ZEROES              TO WS-MAX-CASE.                             
           MOVE ZEROES              TO WS-COUNTER.                              
           MOVE ZEROES              TO WS-TOTAL-CASES-CTR.                      
                                                                                
      *    MOVE F026-CDE-COMPLETE         TO WS-F026-CDE-COMPLETE.              
      *    MOVE F026-NUM-INTERVAL         TO WS-F026-NUM-INTVL.                 
      *    MOVE F026-NUM-MAX-INTERVAL     TO WS-F026-NUM-MAX-INTVL.             
      *    MOVE F026-CDE-ROUND            TO WS-F026-CDE-ROUND.                 
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2100-READ-F022.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13F022 NEXT RECORD                                            
                AT END MOVE 'Y' TO  WS-F022-EOF                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F022-READ.                                               
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2500-WRITE-TABLE.                                                        
      *------------------------------------------------------------*            
                                                                                
           MOVE 1 TO WS-CTR WS-COUNTER.                                         
           MOVE 0 TO WS-COUNTER1.                                               
           MOVE 0 TO WS-TOTAL-CASES.                                            
                                                                                
           INITIALIZE WS-OIC-TABLE REPLACING                                    
                      ALPHANUMERIC DATA BY SPACES                               
                      NUMERIC      DATA BY ZEROS.                               
                                                                                
           PERFORM UNTIL WS-F026-EOF = 'Y' OR                                   
                         WS-CTR > 32   OR                                       
                         BP13F026-REC(1:5) NOT = WS-LAST                        
                                                                                
              MOVE F026-NUM-TO-OIC       TO WS-NUM-OIC(WS-CTR)                  
              MOVE F026-NUM-CASES        TO WS-MAX-CNT(WS-CTR)                  
              MOVE 0                     TO WS-NUM-CNT(WS-CTR)                  
                                            WS-NUM-TOT(WS-CTR)                  
              COMPUTE WS-TOTAL-CASES = WS-TOTAL-CASES + F026-NUM-CASES          
                                                                                
              ADD 1 TO WS-CTR WS-COUNTER1                                       
              PERFORM 1200-READ-F026     THRU 1200-EXIT                         
           END-PERFORM.                                                         
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2600-RE-ASSIGN.                                                          
      *------------------------------------------------------------*            
                                                                                
           IF WS-F022-EOF NOT = 'Y'                                             
              IF WS-LAST           = F022-NUM-OIC-APPL                          
                 PERFORM 3200-REASSIGN-PROCESS THRU 3200-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-F022    THRU 2100-EXIT.                            
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3100-READ-K022.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13K022.                                                       
                                                                                
           EVALUATE WS-K022-STATUS                                              
             WHEN 00                                                            
                MOVE 'Y'   TO WS-K022-FND                                       
                CONTINUE                                                        
             WHEN 23                                                            
                MOVE 'N'   TO WS-K022-FND                                       
                MOVE SPACES    TO BP13K022-REC                                  
             WHEN OTHER                                                         
                 DISPLAY 'READ BP13K022 FAIL. STATUS - '                        
                                            WS-K022-STATUS                      
                 MOVE  WS-K022-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3150-READ-K022.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
             WHEN 00                                                            
                MOVE 'Y'   TO WS-K022-FND                                       
             WHEN 23                                                            
                MOVE 'N'   TO WS-K022-FND                                       
                MOVE SPACES    TO BP13K022-REC                                  
             WHEN OTHER                                                         
                 DISPLAY 'READ BP13K022 FAIL. STATUS - '                        
                                            WS-K022-STATUS '/'                  
                                            K022-NUM-REGN                       
                 MOVE  WS-K022-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3200-REASSIGN-PROCESS.                                                   
      *------------------------------------------------------------*            
                                                                                
           MOVE 'N'         TO    WS-FLAG.                                      
           MOVE SPACES      TO    BP13RPT1-REC                                  
                                  BP13K034-REC                                  
                                  WS-NRIC.                                      
           MOVE F022-NUM-OIC      TO  RPT1-OLD-OIC.                             
           MOVE F022-NUM-REGN     TO  RPT1-NUM-REGN                             
                                      K034-NUM-REGN.                            
           PERFORM 6000-START-K034 THRU 6000-EXIT.                              
           PERFORM 6100-READNEXT-K034 THRU 6100-EXIT.                           
           IF F022-NUM-REGN = K034-NUM-REGN                                     
              MOVE K034-NUM-NRIC  TO WS-NRIC                                    
           END-IF.                                                              
                                                                                
           DISPLAY ' BF. DEL K022. OIC-APPL/REGN: ' F022-NUM-REGN '/'           
                                        F022-NUM-OIC-APPL                       
           MOVE SPACES            TO BP13K022-REC.                              
           MOVE F022-KEY-FLD      TO K022-KEY-FLD.                              
           PERFORM 3100-READ-K022 THRU 3100-EXIT.                               
           ADD 1    TO WS-COUNTER                                               
                                                                                
           PERFORM UNTIL WS-FLAG = 'Y'                                          
                IF WS-F022-EOF NOT = 'Y'                                        
                   IF WS-COUNTER >= 32 OR                                       
                      WS-COUNTER >= WS-CTR                                      
                      MOVE 1   TO WS-COUNTER                                    
                   END-IF                                                       
                   IF WS-NUM-CNT(WS-COUNTER) < WS-MAX-CNT(WS-COUNTER)           
                      MOVE WS-NUM-OIC(WS-COUNTER)  TO                           
                                  K022-NUM-OIC-APPL                             
                      ADD 1 TO WS-NUM-TOT(WS-COUNTER)                           
                      ADD 1 TO WS-NUM-CNT(WS-COUNTER)                           
                      MOVE 'Y'   TO WS-FLAG                                     
                   ELSE                                                         
                     IF WS-NUM-CNT(WS-COUNTER) = WS-MAX-CNT(WS-COUNTER)         
                        ADD 1 TO   WS-COUNTER                                   
                     ELSE                                                       
                        MOVE 1 TO WS-COUNTER                                    
                        MOVE WS-NUM-OIC(WS-COUNTER)  TO                         
                                  K022-NUM-OIC-APPL                             
                        ADD 1 TO WS-NUM-TOT(WS-COUNTER)                         
                        ADD 1 TO WS-NUM-CNT(WS-COUNTER)                         
                        MOVE 'Y'   TO WS-FLAG                                   
                     END-IF                                                     
                   END-IF                                                       
                END-IF                                                          
           END-PERFORM.                                                         
                                                                                
           MOVE F022-NUM-OIC-APPL      TO RPT1-OLD-OIC.                         
           MOVE K022-NUM-REGN          TO RPT1-NUM-REGN.                        
           MOVE K022-NUM-OIC-APPL      TO RPT1-NEW-OIC                          
                                          WS-NEW-OIC.                           
           WRITE BP13RPT1-REC.                                                  
           ADD 1 TO    WS-RPT1-CNT.                                             
           ADD 1 TO    WS-MAX-CASE                                              
                       WS-TOTAL-CASES-CTR                                       
                                                                                
           PERFORM 4600-WRITE-K028   THRU 4600-EXIT.                            
           PERFORM 3500-WRITE-F022A  THRU 3500-EXIT.                            
                                                                                
           PERFORM 6200-REWRITE-K022  THRU 6200-EXIT.                           
           ADD 1 TO WS-OIC-APPL-UPD.                                            
           DISPLAY ' AF. K022. OIC-APPL/REGN: ' K022-NUM-REGN                   
                            '/' K022-NUM-OIC-APPL.                              
                                                                                
           IF  WS-NRIC NOT = SPACES AND LOW-VALUES                              
               DISPLAY ' NRIC - ' WS-NRIC                                       
               MOVE SPACES   TO BP13K034-REC                                    
               MOVE 'N'      TO WS-K034-EOF-SW                                  
               MOVE WS-NRIC  TO K034-NUM-NRIC                                   
               PERFORM 6050-START-K034  THRU 6050-EXIT                          
               PERFORM 6100-READNEXT-K034 THRU 6100-EXIT                        
               PERFORM UNTIL WS-K034-EOF-SW = 'Y'                               
                          OR WS-NRIC NOT = K034-NUM-NRIC                        
                 IF WS-NRIC = K034-NUM-NRIC                                     
                  IF K034-NUM-REGN NOT = F022-NUM-REGN                          
                    MOVE SPACES         TO BP13K022-REC                         
                    MOVE K034-NUM-REGN  TO K022-NUM-REGN                        
                    PERFORM 3150-READ-K022  THRU 3150-EXIT                      
                    IF WS-K022-FND = 'Y'                                        
                      IF K022-NUM-OIC-APPL  = SPACES OR  LOW-VALUES             
                         ADD 1 TO WS-BYPASS                                     
                      ELSE                                                      
                        IF K022-NUM-OIC-APPL = F026-NUM-FROM-OIC                
                      DISPLAY ' BF. K022. OICAPPL/REGN: ' K022-NUM-REGN         
                                                   '/' K022-NUM-OIC-APPL        
                         MOVE SPACES                 TO BP13RPT1-REC            
                         MOVE F022-NUM-OIC-APPL      TO RPT1-OLD-OIC            
                         MOVE K022-NUM-REGN          TO RPT1-NUM-REGN           
                         MOVE WS-NEW-OIC             TO RPT1-NEW-OIC            
                         WRITE BP13RPT1-REC                                     
                         ADD 1 TO    WS-RPT1-CNT                                
                                                                                
                         PERFORM 4600-WRITE-K028   THRU 4600-EXIT               
                         PERFORM 3500-WRITE-F022A  THRU 3500-EXIT               
                         MOVE WS-NEW-OIC           TO K022-NUM-OIC-APPL         
                         PERFORM 6200-REWRITE-K022  THRU 6200-EXIT              
                         ADD 1 TO WS-OIC-APPL-UPD                               
                     DISPLAY ' AF. K022. OIC-APPL/REGN: ' K022-NUM-REGN         
                            '/' K022-NUM-OIC-APPL                               
                         END-IF                                                 
                      END-IF                                                    
                    END-IF                                                      
                  END-IF                                                        
                 END-IF                                                         
                 PERFORM 6100-READNEXT-K034 THRU 6100-EXIT                      
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3500-WRITE-F022A.                                                        
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES         TO P13F022A-REC.                                 
           INITIALIZE             P13F022A-REC.                                 
                                                                                
           MOVE BP13K022-REC   TO P13F022A-REC.                                 
                                                                                
           WRITE P13F022A-REC.                                                  
           ADD 1 TO WS-F022A-WRITE.                                             
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4000-DELETE-K022.                                                        
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES         TO WS-K022-REC.                                  
           INITIALIZE             WS-K022-REC.                                  
                                                                                
           MOVE BP13K022-REC   TO WS-K022-REC.                                  
                                                                                
           DELETE BP13K022.                                                     
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13K022, STATUS - '                  
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4100-WRITE-K022.                                                         
      *------------------------------------------------------------*            
                                                                                
           WRITE BP13K022-REC.                                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1 TO WS-K022-WRITE                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR WRITING BP13K022, STATUS - '                   
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4600-WRITE-K028.                                                         
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES               TO BP13K028-REC.                           
           INITIALIZE                   BP13K028-REC.                           
                                                                                
           MOVE K022-NUM-OIC         TO K028-NUM-OIC.                           
           MOVE K022-NUM-REGN        TO K028-NUM-REGN.                          
           MOVE WS-SYSTEM-DATE       TO K028-DTE-HISTORY.                       
           MOVE K022-CDE-ALLO-CAT    TO K028-CDE-ALLO-CAT.                      
           MOVE K022-CDE-BANKLOAN    TO K028-CDE-BANKLOAN.                      
           MOVE K022-CDE-ALERT       TO K028-CDE-ALERT.                         
           MOVE K022-NUM-USERID      TO K028-NUM-USERID.                        
           MOVE K022-DTE-UPDATE      TO K028-DTE-UPDATE.                        
                                                                                
           WRITE  BP13K028-REC.                                                 
                                                                                
           EVALUATE WS-K028-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
              WHEN 22                                                           
                 ADD 1 TO WS-K028-WRITE                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR WRITING  BP13K028, STATUS - '                  
                                            WS-K028-STATUS                      
                 MOVE WS-K028-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       5000-DISPLAY-SUMMARY.                                                    
      *------------------------------------------------------------*            
                                                                                
           DISPLAY '*------ FINAL ASSIGNMENT -------*'.                         
           MOVE 1 TO WS-COUNT.                                                  
           PERFORM UNTIL WS-COUNT > WS-COUNTER1                                 
              DISPLAY ' OIC        : ' WS-NUM-OIC(WS-COUNT)                     
              DISPLAY ' ASSIGNED   : ' WS-NUM-TOT(WS-COUNT)                     
              ADD 1 TO WS-COUNT                                                 
           END-PERFORM.                                                         
           DISPLAY '*-------------------------------*'.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       6000-START-K034.                                                         
      ****************************************************************          
                                                                                
           START BP13K034 KEY >= K034-NUM-REGN                                  
                                                                                
           IF WS-K034-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K034-STATUS = 10 OR 23                                       
                MOVE 'Y'    TO WS-K034-EOF-SW                                   
             ELSE                                                               
              DISPLAY 'ERROR WITH START READ OF BP13K034. STATUS IS '           
                       WS-K034-STATUS '/'  K034-NUM-REGN                        
              MOVE WS-K034-STATUS              TO RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       6050-START-K034.                                                         
      ****************************************************************          
                                                                                
           START BP13K034 KEY >= K034-NUM-NRIC                                  
                                                                                
           IF WS-K034-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K034-STATUS = 10 OR 23                                       
                MOVE 'Y'    TO WS-K034-EOF-SW                                   
             ELSE                                                               
              DISPLAY 'ERROR WITH START READ OF BP13K034. STATUS IS '           
                       WS-K034-STATUS '/ ' K034-NUM-NRIC                        
              MOVE WS-K034-STATUS              TO RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
       6050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       6100-READNEXT-K034.                                                      
      ****************************************************************          
                                                                                
            READ BP13K034 NEXT RECORD                                           
               AT END MOVE 'Y' TO WS-K034-EOF-SW                                
               GO TO 6100-EXIT                                                  
            END-READ.                                                           
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       6200-REWRITE-K022.                                                       
      ****************************************************************          
                                                                                
            MOVE 'BP13CH46'            TO K022-NUM-USERID.                      
            MOVE WS-SYSTEM-DATE        TO K022-DTE-UPDATE.                      
                                                                                
            REWRITE BP13K022-REC.                                               
                                                                                
            EVALUATE WS-K022-STATUS                                             
            WHEN 00                                                             
            WHEN 02                                                             
               CONTINUE                                                         
            WHEN OTHER                                                          
               DISPLAY 'ERROR WRITING BP13K022. STATUS IS '                     
                        WS-K022-STATUS                                          
               MOVE WS-K022-STATUS              TO RETURN-CODE                  
               PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------*            
                                                                                
           CLOSE BP13K022                                                       
                 BP13K028                                                       
                 BP13F026                                                       
                 BP13F022                                                       
                 P13F022A.                                                      
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
                 DISPLAY 'CLOSE BP13K022 FAIL. STATUS-'                         
                                         WS-K022-STATUS                         
                 MOVE WS-K022-STATUS  TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF WS-K028-STATUS NOT = 00 AND 97                                    
                 DISPLAY 'CLOSE BP13K028 FAIL. STATUS-'                         
                                         WS-K028-STATUS                         
                 MOVE WS-K028-STATUS  TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
                                                                                
           DISPLAY '*----------- BP13CH46 -------------*'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY ' 1. BP13F026 READ      : '  WS-F026-READ.                   
           DISPLAY ' 2. BP13F022 READ      : '  WS-F022-READ.                   
           DISPLAY ' 3. BP13K022 NOT FOUND : '  WS-K022-NFND.                   
           DISPLAY ' 4. BP13K022 REWRITE   : '  WS-OIC-APPL-UPD.                
           DISPLAY ' 5. BP13F022 WRITE(BCK): '  WS-F022A-WRITE.                 
           DISPLAY ' 6. BP13K028 WRITE(HIS): '  WS-K022-WRITE.                  
           DISPLAY ' 7. REPORT   WRITE     : '  WS-RPT1-CNT.                    
           DISPLAY ' 8. BYPASS EC/DBS CASE : '  WS-EC-DBS.                      
           DISPLAY ' 9. BYPASS NOT POS CASE: '  WS-BYPASS.                      
           DISPLAY SPACES.                                                      
           DISPLAY '*----------------------------------*'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
