       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB52.                                                 
      *AUTHOR.        EDEN ROSE DINGAL                                          
      *DATE-WRITTEN.  03/01/07.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  MATCH BP13F716(RDB FOUND IN RDB) WITH       *            
      *                BALLOT FILE (BP13F730), IF MATCH, UPDATE    *            
      *                ROLE AND FILE STATUS-SMS (DP OR OM)         *            
      *                                                            *            
      *                ROLE IN RDB    TAG IN F730                  *            
      *                --------------------------                  *            
      *                 003             T (TENANT)                 *            
      *                 004             TO (TENANT-OCCUP)          *            
      *                 023,024         L  (LESSEE)                *            
      *                 025             O  (OCUP)                  *            
      *                                                            *            
      *  INPUT FILES:  1. BP13F716                                 *            
      *                2. BP13F730                                 *            
      *                                                            *            
      *  OUTPUT FILE:  1. P13F730A                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133111 ED1 20080103 NEW PROGRAM                          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
           SELECT P13F730A ASSIGN TO P13F730A.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F716                                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F716.                                                           
                                                                                
       FD  BP13F730                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F730.                                                           
                                                                                
       FD  P13F730A                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F730A-REC.                                                        
           05 FILLER                 PIC X(471) VALUE SPACES.                   
           05 WS-F730A-CDE-ROLE      PIC X(002) VALUE SPACES.                   
           05 WS-F730A-FILE-STAT     PIC X(002) VALUE SPACES.                   
           05 FILLER                 PIC X(025) VALUE SPACES.                   
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F716-READ        PIC 9(6)    VALUE ZEROS.                     
           05  NO-F730-READ        PIC 9(6)    VALUE ZEROS.                     
           05  NO-F730A-WRITE      PIC 9(6)    VALUE ZEROS.                     
           05  WS-MATCHED          PIC 9(6)    VALUE ZEROS.                     
                                                                                
       01  WS-CUR-DTE               PIC X(8)  VALUE SPACES.                     
       01  WS-CUR-DTE-R REDEFINES WS-CUR-DTE.                                   
           05  WS-CUR-DTE-N         PIC 9(8).                                   
       01  WS-DIFFERENCE            PIC 9(8)  VALUE 0.                          
       01  WS-CURR-DTE-J            PIC 9(8)  VALUE 0.                          
       01  WS-TOM-DTE-J             PIC 9(8)  VALUE 0.                          
       01  F730-STATUS              PIC 99    VALUE 0.                          
       01  WS-TEMP-CDE-ROLE         PIC X(02) VALUE SPACES.                     
       01  WS-TEMP-FILE-STAT        PIC X(02) VALUE SPACES.                     
       01  WS-F730-CDE-ROLE         PIC X(02) VALUE SPACES.                     
       01  WS-F730-FILE-STAT        PIC X(02) VALUE SPACES.                     
       01  WS-PREV-REGN             PIC X(08) VALUE SPACES.                     
       01  WS-EOF                   PIC X     VALUE 'N'.                        
                                                                                
       01 WS-F716-REC.                                                          
          05 FILLER             PIC X(152).                                     
          05 WS-F716-FILE-STAT  PIC X(02).                                      
          05 FILLER             PIC X(196).                                     
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F716        THRU 2000-EXIT.                        
           PERFORM 2100-READ-F730        THRU 2100-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL F716-NUM-REGN = HIGH-VALUES AND                        
                         F730-NUM-REGN = HIGH-VALUES.                           
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F716                                                 
                       BP13F730                                                 
                OUTPUT P13F730A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DTE.                            
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-F716.                                                          
      *-------------------------------------------------------------            
            READ BP13F716 AT END                                                
                 MOVE HIGH-VALUES TO F716-NUM-REGN                              
                 GO TO 2000-EXIT.                                               
                                                                                
            MOVE BP13F716-REC TO WS-F716-REC.                                   
            ADD  1            TO NO-F716-READ.                                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2100-READ-F730.                                                          
      *-------------------------------------------------------------            
            READ BP13F730 AT END                                                
                 MOVE HIGH-VALUES TO F730-NUM-REGN                              
                 GO TO 2100-EXIT.                                               
                                                                                
            ADD  1                   TO NO-F730-READ.                           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES  TO  WS-PREV-REGN.                                       
           MOVE SPACES  TO  P13F730A-REC.                                       
           MOVE SPACES  TO  WS-F730-FILE-STAT.                                  
                                                                                
           IF F716-NUM-REGN = F730-NUM-REGN                                     
              MOVE F716-NUM-REGN       TO WS-PREV-REGN                          
              MOVE BP13F730-REC(474:2) TO WS-F730-CDE-ROLE                      
                                                                                
              PERFORM 4100-DETERMINE-ROLE THRU 4100-EXIT                        
                      UNTIL F716-NUM-REGN NOT = WS-PREV-REGN                    
                         OR F716-NUM-REGN     = HIGH-VALUES                     
                                                                                
              WRITE      P13F730A-REC                                           
              ADD 1  TO  NO-F730A-WRITE                                         
              ADD 1  TO  WS-MATCHED                                             
                                                                                
              PERFORM 2100-READ-F730      THRU 2100-EXIT                        
                                                                                
           ELSE                                                                 
              IF F716-NUM-REGN > F730-NUM-REGN                                  
                 WRITE P13F730A-REC FROM BP13F730-REC                           
                 ADD 1  TO  NO-F730A-WRITE                                      
                 PERFORM 2100-READ-F730      THRU 2100-EXIT                     
              ELSE                                                              
                 PERFORM 2000-READ-F716      THRU 2000-EXIT                     
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4100-DETERMINE-ROLE.                                                     
      *-------------------------------------------------------------            
            MOVE SPACES              TO WS-TEMP-CDE-ROLE.                       
                                                                                
            EVALUATE F716-NUM-ROLE                                              
            WHEN '003'                                                          
               MOVE 'T ' TO WS-TEMP-CDE-ROLE                                    
            WHEN '004'                                                          
               MOVE 'TO' TO WS-TEMP-CDE-ROLE                                    
            WHEN '023'                                                          
            WHEN '024'                                                          
               MOVE 'L ' TO WS-TEMP-CDE-ROLE                                    
            WHEN '025'                                                          
               MOVE 'O ' TO WS-TEMP-CDE-ROLE                                    
            END-EVALUATE.                                                       
                                                                                
            IF  WS-F730-CDE-ROLE  = SPACES OR LOW-VALUES                        
                MOVE WS-TEMP-CDE-ROLE TO WS-F730-CDE-ROLE                       
            ELSE                                                                
                IF  WS-F730-CDE-ROLE  = 'L ' OR 'T '                            
                    CONTINUE                                                    
                ELSE                                                            
                    MOVE WS-TEMP-CDE-ROLE TO WS-F730-CDE-ROLE                   
                END-IF                                                          
            END-IF.                                                             
                                                                                
            EVALUATE WS-F716-FILE-STAT                                          
            WHEN 'DP'                                                           
               MOVE WS-F716-FILE-STAT    TO  WS-F730-FILE-STAT                  
            WHEN 'OM'                                                           
               IF WS-F730-FILE-STAT NOT = 'DP'                                  
                  MOVE WS-F716-FILE-STAT TO  WS-F730-FILE-STAT                  
               END-IF                                                           
            WHEN OTHER                                                          
               IF WS-F730-FILE-STAT NOT = 'DP' AND 'OM'                         
                  MOVE WS-F716-FILE-STAT TO  WS-F730-FILE-STAT                  
               END-IF                                                           
            END-EVALUATE.                                                       
                                                                                
                                                                                
            MOVE BP13F730-REC        TO P13F730A-REC.                           
            MOVE WS-F730-FILE-STAT   TO WS-F730A-FILE-STAT.                     
            MOVE WS-F730-CDE-ROLE    TO WS-F730A-CDE-ROLE.                      
                                                                                
            PERFORM 2000-READ-F716 THRU 2000-EXIT.                              
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13F716                                                       
                 BP13F730                                                       
                 P13F730A.                                                      
                                                                                
                                                                                
           DISPLAY '-----------------------------------------'.                 
           DISPLAY '          PROGRAM BP13CB52 '.                               
           DISPLAY '        RUN DATE : ' WS-CUR-DTE.                            
           DISPLAY 'NO. OF F716 REC READ     > ' NO-F716-READ.                  
           DISPLAY 'NO. OF F730 REC READ     > ' NO-F730-READ.                  
           DISPLAY 'NO. OF F730A WRITTEN     > ' NO-F730A-WRITE.                
           DISPLAY 'NO. OF MATCHED CASES     > ' WS-MATCHED.                    
           DISPLAY '-----------------------------------------'.                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
