       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C27D.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  26 OCTOBER 2016.                                          
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  :                                               *           
      *        TO CREATE DATASET ON FLATS BOOKED BY MIXED MARRIAGE  *           
      *        APPLICATION                                          *           
      *                                                             *           
      *  INPUT    :  1. BP13F800                                    *           
      *              2. BP13K820                                    *           
      *              3. BP13K757                                    *           
      *  LISTING  :  1. BP13L27D                                    *           
      *                                                             *           
      * CHG NO   BY   ON        DESCRIPTION                         *           
      * -------- ---- --------  -----------                         *           
      * BP136462 ESA1 20161026  NEW PROGRAM                         *           
      * BP139280 EAA2 06112023  EXPAND BP13K757 FROM 3000 TO 4000   *           
      * BP139898 MRR5 13022024  CHANGE CALLING OF BP13C913          *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN  TO  BP13F800.                               
                                                                                
           SELECT BP13K820  ASSIGN  TO  BP13K820                                
                  ACCESS       IS RANDOM                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K820-KEY-FLD                                  
                  FILE STATUS  IS BP13K820-STATUS.                              
                                                                                
           SELECT BP13K060  ASSIGN  TO  BP13K060                                
                  ACCESS       IS RANDOM                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K060-KEY-FLD                                  
                  FILE STATUS  IS BP13K060-STATUS.                              
                                                                                
           SELECT BP13K757  ASSIGN  TO  BP13K757                                
                  ACCESS       IS RANDOM                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K757-KEY-FLD                                  
                  FILE STATUS  IS BP13K757-STATUS.                              
                                                                                
           SELECT BP13L27D  ASSIGN  TO  BP13L27D.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F800                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS 25 CHARACTERS.                                       
       COPY BP13K060.                                                           
                                                                                
       FD  BP13K757                                                             
           RECORD CONTAINS 4000 CHARACTERS.                                     
       COPY BP13K757.                                                           
                                                                                
       FD  BP13L27D                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13L27D-REC                PIC X(500).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K820-STATUS               PIC 9(02)   VALUE 0.                   
       01  BP13K757-STATUS               PIC 9(02)   VALUE 0.                   
       01  BP13K060-STATUS               PIC 9(02)   VALUE 0.                   
                                                                                
       01  MTH                           PIC 9(02)   VALUE 1.                   
       01  WS-CTR                        PIC 9(02)   VALUE 1.                   
       01  WS-TOTAL-PRTED                PIC 9(02)   VALUE 0.                   
       01  WS-LINE-CNT                   PIC 9(02)   VALUE 60.                  
       01  WS-PAGE-CNT                   PIC 9(04)   VALUE ZERO.                
       01  WS-F800-READ                  PIC 9(04)   VALUE ZERO.                
       01  WS-F800-EOF                   PIC X       VALUE 'N'.                 
       01  WS-ETHNIC                     PIC X       VALUE SPACES.              
       01  WS-ETHNIC-DESC                PIC X(07)   VALUE SPACES.              
       01  WS-ETHNIC-APPLN               PIC X(07)   VALUE SPACES.              
       01  WS-NAME                       PIC X(66)   VALUE SPACES.              
       01  WS-GENDER                     PIC X       VALUE SPACES.              
       01  WS-GENDER-DESC                PIC X(10)   VALUE SPACES.              
       01  WS-GENDER-HH                  PIC X(10)   VALUE SPACES.              
       01  WS-ETHNIC-HH                  PIC X(07)   VALUE SPACES.              
       01  WS-ETHNIC-SOC                 PIC X(07)   VALUE SPACES.              
       01  WS-BP13C913                   PIC X(08)   VALUE 'BP13C913'.          
                                                                                
       01  WS-NRIC-DETLS.                                                       
           05 WS-NRIC-APPLN OCCURS 4  TIMES  PIC X(09)   VALUE SPACES.          
                                                                                
       01  WS-DTE-BK.                                                           
           05 WS-DTE-BK-CCYY             PIC X(4).                              
           05 WS-DTE-BK-MM               PIC X(2).                              
           05 WS-DTE-BK-DD               PIC X(2).                              
                                                                                
       01  WS-BOOK-DATE REDEFINES WS-DTE-BK.                                    
           05 WS-BOOK-CCYY           PIC 9(4).                                  
           05 WS-BOOK-MM             PIC 9(2).                                  
           05 WS-BOOK-DD             PIC 9(2).                                  
                                                                                
       01  WS-COMPUTE-MONTH              PIC 9 VALUE ZERO.                      
       01  WS-COMPUTE-YEAR               PIC 9 VALUE ZERO.                      
                                                                                
       01  WS-DATE.                                                             
           03 WS-CCYY                    PIC 9(4).                              
           03 WS-MM                      PIC 9(2).                              
           03 WS-DD                      PIC 9(2).                              
                                                                                
       01  WS-MONTH.                                                            
           03 WS-MON                     PIC X(36) VALUE                        
             'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                            
           03 FILLER REDEFINES WS-MON.                                          
              05 MON           OCCURS   12 TIMES PIC X(3).                      
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH          PIC X(04).                               
           05 WS-LINK-NUM-ACC          PIC X(04).                               
           05 WS-LINK-NUM-CHK          PIC X(01).                               
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      **************************************************************            
      *           LAY-OUT FOR CONTROL LISTING                      *            
      **************************************************************            
       01  L27D-HDG1.                                                           
           03  FILLER              PIC X(14)   VALUE 'BP13L27D'.                
           03  FILLER              PIC X(8)    VALUE 'HDB3'.                    
           03  FILLER              PIC X(18)   VALUE SPACES.                    
           03  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           03  FILLER              PIC X(28)   VALUE SPACES.                    
           03  FILLER              PIC X(7)    VALUE 'DATE : '.                 
           03  L27D-DATE           PIC X(10)   VALUE SPACES.                    
                                                                                
       01  L27D-HDG2.                                                           
           03  FILLER              PIC X(26)  VALUE SPACES.                     
           03  FILLER              PIC X(49)  VALUE                             
               'LIST OF FLATS BOOKED BY MIXED MARRIAGE APPLICANTS'.             
           03  FILLER              PIC X(4)   VALUE ' IN '.                     
           03  L27D-HDR-MONTH      PIC X(3)   VALUE SPACES.                     
           03  FILLER              PIC X      VALUE SPACES.                     
           03  L27D-HDR-YEAR       PIC X(4)   VALUE SPACES.                     
           03  FILLER              PIC X(21)  VALUE SPACES.                     
           03  FILLER              PIC X(7)   VALUE 'PAGE : '.                  
           03  L27D-PAGE           PIC ZZZZ9.                                   
                                                                                
       01  L27D-HDG3.                                                           
           03  FILLER              PIC X(07)  VALUE 'S/NO'.                     
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(09)  VALUE 'REGN NO'.                  
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(66)  VALUE 'NAME OF HA1'.              
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(07)  VALUE 'HA1 ETH'.                  
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(10)  VALUE 'HA1 GENDER'.               
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(66)  VALUE 'NAME OF HA2'.              
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(07)  VALUE 'HA2 ETH'.                  
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(10)  VALUE 'HA2 GENDER'.               
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(66)  VALUE 'NAME OF HA3'.              
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(07)  VALUE 'HA3 ETH'.                  
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(10)  VALUE 'HA3 GENDER'.               
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(66)  VALUE 'NAME OF HA4'.              
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(07)  VALUE 'HA4 ETH'.                  
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(10)  VALUE 'HA4 GENDER'.               
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(07)  VALUE 'ETH APP'.                  
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(07)  VALUE 'ETH SOC'.                  
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(07)  VALUE 'HH TYPE'.                  
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(10)  VALUE 'BALLOT QTR'.               
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(50)  VALUE                             
               'ADDRESS OF FLAT BOOKED'.                                        
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(10)  VALUE 'APPLN DATE'.               
           03  FILLER              PIC X(01)  VALUE ';'.                        
           03  FILLER              PIC X(15)  VALUE 'DATE OF BOOKING'.          
                                                                                
       01  L27D-DETAILS.                                                        
           03  L27D-SNO            PIC ZZZZZZ9.                                 
           03  FIL01               PIC X(01)  VALUE SPACES.                     
           03  L27D-REGN           PIC X(09)  VALUE SPACES.                     
           03  FIL02               PIC X(01)  VALUE SPACES.                     
           03  L27D-HA1-NAME       PIC X(66) VALUE SPACES.                      
           03  FIL03               PIC X(01)  VALUE SPACES.                     
           03  L27D-HA1-ETHNIC     PIC X(07)  VALUE SPACES.                     
           03  FIL04               PIC X(01)  VALUE SPACES.                     
           03  L27D-HA1-GENDER     PIC X(10)  VALUE SPACES.                     
           03  FIL05               PIC X(01)  VALUE SPACES.                     
           03  L27D-HA2-NAME       PIC X(66) VALUE SPACES.                      
           03  FIL06               PIC X(01)  VALUE SPACES.                     
           03  L27D-HA2-ETHNIC     PIC X(07)  VALUE SPACES.                     
           03  FIL07               PIC X(1)  VALUE SPACES.                      
           03  L27D-HA2-GENDER     PIC X(10)  VALUE SPACES.                     
           03  FIL08               PIC X(01)  VALUE SPACES.                     
           03  L27D-HA3-NAME       PIC X(66) VALUE SPACES.                      
           03  FIL09               PIC X(1)  VALUE SPACES.                      
           03  L27D-HA3-ETHNIC     PIC X(7)  VALUE SPACES.                      
           03  FIL10               PIC X(1)  VALUE SPACES.                      
           03  L27D-HA3-GENDER     PIC X(10)  VALUE SPACES.                     
           03  FIL11               PIC X(01)  VALUE SPACES.                     
           03  L27D-HA4-NAME       PIC X(66) VALUE SPACES.                      
           03  FIL12               PIC X(1)  VALUE SPACES.                      
           03  L27D-HA4-ETHNIC     PIC X(7)  VALUE SPACES.                      
           03  FIL13               PIC X(1)  VALUE SPACES.                      
           03  L27D-HA4-GENDER     PIC X(10)  VALUE SPACES.                     
           03  FIL14               PIC X(01)  VALUE SPACES.                     
           03  L27D-ETHNIC-APPLN   PIC X(7)  VALUE SPACES.                      
           03  FIL15               PIC X(1)  VALUE SPACES.                      
           03  L27D-ETHNIC-SOC     PIC X(7)  VALUE SPACES.                      
           03  FIL16               PIC X(1)  VALUE SPACES.                      
           03  L27D-HH-TYP         PIC X(7)  VALUE SPACES.                      
           03  FIL17               PIC X(1)  VALUE SPACES.                      
           03  L27D-BALLOT-QTR     PIC X(10)  VALUE SPACES.                     
           03  FIL18               PIC X(01)  VALUE SPACES.                     
           03  L27D-FLAT-ADDRESS   PIC X(50) VALUE SPACES.                      
           03  FIL19               PIC X(01)  VALUE SPACES.                     
           03  L27D-APPLN-DTE      PIC X(10) VALUE SPACES.                      
           03  FIL20               PIC X(01)  VALUE SPACES.                     
           03  L27D-BOOKING-DTE    PIC X(10) VALUE SPACES.                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-ROUTINE THRU 100-EXIT.                              
           PERFORM 200-READ-F800    THRU 200-EXIT.                              
           PERFORM 300-PROCESS-ROUTINE THRU 300-EXIT                            
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
      *-------------------------------------------------------------            
       100-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT BP13F800                                                  
                      BP13K820                                                  
                      BP13K060                                                  
                      BP13K757                                                  
               OUTPUT BP13L27D.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           MOVE WS-MM                 TO MTH.                                   
           IF MTH = 01                                                          
              MOVE 12 TO MTH                                                    
              COMPUTE WS-CCYY = WS-CCYY - 1                                     
           ELSE                                                                 
              COMPUTE MTH = MTH - 1                                             
           END-IF.                                                              
           MOVE MON(MTH)              TO L27D-HDR-MONTH.                        
           MOVE WS-CCYY               TO L27D-HDR-YEAR.                         
           STRING WS-DD '/' WS-MM '/' WS-CCYY                                   
                  DELIMITED BY SIZE INTO L27D-DATE.                             
                                                                                
           IF BP13K820-STATUS NOT = 00 AND 97                                   
              DISPLAY ' OPENING BP13K820 ERROR ' BP13K820-STATUS                
              MOVE     BP13K820-STATUS  TO  RETURN-CODE                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY ' OPENING BP13K060 ERROR ' BP13K060-STATUS                
              MOVE     BP13K060-STATUS  TO  RETURN-CODE                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K757-STATUS NOT = 00 AND 97                                   
              DISPLAY ' OPENING BP13K757 ERROR ' BP13K757-STATUS                
              MOVE     BP13K757-STATUS  TO  RETURN-CODE                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       200-READ-F800.                                                           
      *-------------------------------------------------------------            
           READ BP13F800  AT END                                                
                MOVE 'Y' TO WS-F800-EOF                                         
                GO       TO 200-EXIT.                                           
                                                                                
           ADD  1  TO WS-F800-READ.                                             
                                                                                
           MOVE F800-NUM-NRIC1   TO  WS-NRIC-APPLN(1).                          
           MOVE F800-NUM-NRIC2   TO  WS-NRIC-APPLN(2).                          
           MOVE F800-NUM-NRIC3   TO  WS-NRIC-APPLN(3).                          
           MOVE F800-NUM-NRIC4   TO  WS-NRIC-APPLN(4).                          
                                                                                
           MOVE SPACES           TO  WS-GENDER-HH                               
                                     WS-ETHNIC-HH.                              
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 4                  
              MOVE SPACES                TO K820-KEY-FLD                        
              MOVE F800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE WS-NRIC-APPLN(WS-CTR) TO K820-NUM-NRIC                       
              PERFORM 410-READ-BP13K820  THRU 410-EXIT                          
              IF K820-NUM-SEX = '1' OR 'M'                                      
                 MOVE WS-GENDER-DESC     TO WS-GENDER-HH                        
                 MOVE WS-ETHNIC-DESC     TO WS-ETHNIC-HH                        
                 MOVE 5                  TO WS-CTR                              
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       300-PROCESS-ROUTINE.                                                     
      *-------------------------------------------------------------            
           MOVE F800-DTE-ACCEPTANCE TO WS-DTE-BK.                               
                                                                                
           IF F800-DTE-ACCEPTANCE NOT NUMERIC                                   
              MOVE ZEROS TO WS-BOOK-DATE                                        
           END-IF.                                                              
                                                                                
           IF WS-BOOK-CCYY = WS-CCYY                                            
              COMPUTE WS-COMPUTE-MONTH =                                        
                    WS-MM  -  WS-BOOK-MM                                        
              IF WS-COMPUTE-MONTH = 1                                           
                 PERFORM 400-PROCESS-PARA THRU 400-EXIT                         
              END-IF                                                            
           ELSE                                                                 
              COMPUTE WS-COMPUTE-YEAR =                                         
                    WS-CCYY  -  WS-BOOK-CCYY                                    
              IF WS-COMPUTE-YEAR = 1 AND WS-MM = 1                              
                 IF WS-BOOK-MM = 12                                             
                    PERFORM 400-PROCESS-PARA THRU 400-EXIT                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F800           THRU 200-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       400-PROCESS-PARA.                                                        
      *-------------------------------------------------------------            
           PERFORM  500-READ-BP13K757    THRU 500-EXIT.                         
                                                                                
           IF WS-ETHNIC-APPLN = SPACES OR LOW-VALUES                            
              GO TO 400-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF WS-GENDER-HH NOT = SPACES AND LOW-VALUES                          
              PERFORM 425-READ-BP13K060   THRU 425-EXIT                         
              IF WS-ETHNIC-HH NOT = WS-ETHNIC-SOC                               
                 CONTINUE                                                       
              ELSE                                                              
                 GO TO 400-EXIT                                                 
              END-IF                                                            
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           IF WS-LINE-CNT = 60                                                  
              PERFORM 700-PRINT-CONTROL-HEADING THRU 700-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE  SPACES                  TO BP13L27D-REC                        
                                            L27D-DETAILS.                       
                                                                                
           MOVE ';'                      TO FIL01 FIL02 FIL03                   
                                            FIL04 FIL05 FIL06                   
                                            FIL07 FIL08 FIL09                   
                                            FIL10 FIL11 FIL12                   
                                            FIL13 FIL14 FIL15                   
                                            FIL16 FIL17 FIL18                   
                                            FIL19 FIL20.                        
                                                                                
           ADD 1                         TO WS-TOTAL-PRTED.                     
           MOVE WS-TOTAL-PRTED           TO L27D-SNO.                           
           MOVE F800-NUM-REGN            TO L27D-REGN.                          
                                                                                
           MOVE SPACES                   TO K820-KEY-FLD.                       
           MOVE F800-NUM-REGN            TO K820-NUM-REGN.                      
           MOVE F800-NUM-NRIC1           TO K820-NUM-NRIC.                      
           PERFORM 410-READ-BP13K820     THRU 410-EXIT.                         
           MOVE WS-NAME                  TO L27D-HA1-NAME.                      
           MOVE WS-ETHNIC-DESC           TO L27D-HA1-ETHNIC.                    
           MOVE WS-GENDER-DESC           TO L27D-HA1-GENDER.                    
                                                                                
           IF F800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              MOVE SPACES                TO K820-KEY-FLD                        
              MOVE F800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE F800-NUM-NRIC2        TO K820-NUM-NRIC                       
              PERFORM 410-READ-BP13K820  THRU 410-EXIT                          
              MOVE WS-NAME               TO L27D-HA2-NAME                       
              MOVE WS-ETHNIC-DESC        TO L27D-HA2-ETHNIC                     
              MOVE WS-GENDER-DESC        TO L27D-HA2-GENDER                     
           END-IF.                                                              
                                                                                
           IF F800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              MOVE SPACES                TO K820-KEY-FLD                        
              MOVE F800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE F800-NUM-NRIC3        TO K820-NUM-NRIC                       
              PERFORM 410-READ-BP13K820  THRU 410-EXIT                          
              MOVE WS-NAME               TO L27D-HA3-NAME                       
              MOVE WS-ETHNIC-DESC        TO L27D-HA3-ETHNIC                     
              MOVE WS-GENDER-DESC        TO L27D-HA3-GENDER                     
           END-IF.                                                              
                                                                                
           IF F800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              MOVE SPACES                TO K820-KEY-FLD                        
              MOVE F800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE F800-NUM-NRIC4        TO K820-NUM-NRIC                       
              PERFORM 410-READ-BP13K820  THRU 410-EXIT                          
              MOVE WS-NAME               TO L27D-HA4-NAME                       
              MOVE WS-ETHNIC-DESC        TO L27D-HA4-ETHNIC                     
              MOVE WS-GENDER-DESC        TO L27D-HA4-GENDER                     
           END-IF.                                                              
                                                                                
           MOVE WS-ETHNIC-APPLN          TO L27D-ETHNIC-APPLN.                  
                                                                                
           PERFORM  425-READ-BP13K060    THRU 425-EXIT.                         
           MOVE WS-ETHNIC-SOC            TO L27D-ETHNIC-SOC.                    
                                                                                
           MOVE F800-NUM-HOUSEHOLD       TO L27D-HH-TYP.                        
                                                                                
           STRING F800-DTE-BALLOT(1:4) '/'                                      
                  F800-DTE-BALLOT(5:2)                                          
                  DELIMITED BY SIZE INTO L27D-BALLOT-QTR                        
           END-STRING.                                                          
                                                                                
           PERFORM 600-GET-FLAT-ADDR     THRU 600-EXIT.                         
                                                                                
           STRING F800-DTE-REGN(7:2) '/' F800-DTE-REGN(5:2) '/'                 
                  F800-DTE-REGN(1:4)                                            
                  DELIMITED BY SIZE INTO L27D-APPLN-DTE                         
           END-STRING.                                                          
                                                                                
           STRING F800-DTE-ACCEPTANCE(7:2) '/'                                  
                  F800-DTE-ACCEPTANCE(5:2) '/'                                  
                  F800-DTE-ACCEPTANCE(1:4)                                      
                  DELIMITED BY SIZE INTO L27D-BOOKING-DTE                       
           END-STRING.                                                          
                                                                                
           WRITE BP13L27D-REC    FROM L27D-DETAILS.                             
           ADD  1 TO WS-LINE-CNT.                                               
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       410-READ-BP13K820.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE BP13K820-STATUS                                             
            WHEN 00                                                             
                 MOVE K820-NME-OCCP    TO WS-NAME                               
                 MOVE K820-NUM-ETHNIC  TO WS-ETHNIC                             
                 PERFORM 420-READ-BP13K060 THRU  420-EXIT                       
                 MOVE K820-NUM-SEX     TO WS-GENDER                             
                 PERFORM 427-READ-BP13K060 THRU  427-EXIT                       
            WHEN 23                                                             
                 MOVE SPACES           TO WS-NAME                               
                                          WS-ETHNIC                             
                                          WS-ETHNIC-DESC                        
                                          WS-GENDER                             
                                          WS-GENDER-DESC                        
                 DISPLAY 'RECORD NT FND IN BP13K820 : ' K820-KEY-FLD            
            WHEN OTHER                                                          
                 DISPLAY 'ERROR READING BP13K060: ' BP13K820-STATUS             
                 MOVE BP13K060-STATUS  TO  RETURN-CODE                          
                 PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       420-READ-BP13K060.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                 TO   K060-KEY-FLD.                       
           MOVE 42                     TO   K060-SERIAL-NO.                     
           MOVE WS-ETHNIC              TO   K060-CODE.                          
           READ BP13K060.                                                       
                                                                                
           EVALUATE BP13K060-STATUS                                             
            WHEN 00                                                             
                 MOVE SPACES           TO   K060-KEY-FLD                        
                 MOVE 31               TO   K060-SERIAL-NO                      
                 MOVE K060-DESC        TO   K060-CODE                           
                 READ BP13K060                                                  
                 IF BP13K060-STATUS = 00                                        
                    MOVE K060-DESC     TO   WS-ETHNIC-DESC                      
                 ELSE                                                           
                    MOVE SPACES        TO   WS-ETHNIC-DESC                      
                 END-IF                                                         
            WHEN OTHER                                                          
                 MOVE   SPACES         TO   WS-ETHNIC-DESC                      
           END-EVALUATE.                                                        
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       425-READ-BP13K060.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES                 TO   WS-ETHNIC-SOC.                      
           MOVE SPACES                 TO   K060-KEY-FLD.                       
           MOVE 09                     TO   K060-SERIAL-NO.                     
           MOVE F800-NUM-CAT           TO   K060-CODE.                          
           READ BP13K060.                                                       
                                                                                
           EVALUATE BP13K060-STATUS                                             
            WHEN 00                                                             
                 MOVE K060-DESC(1:7)   TO   WS-ETHNIC-SOC                       
            WHEN OTHER                                                          
                 MOVE   SPACES         TO   WS-ETHNIC-SOC                       
           END-EVALUATE.                                                        
                                                                                
       425-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       426-READ-BP13K060.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                 TO   K060-KEY-FLD.                       
           MOVE 31                     TO   K060-SERIAL-NO.                     
           MOVE K757-NUM-APPL-RACE     TO   K060-CODE.                          
           READ BP13K060.                                                       
                                                                                
           EVALUATE BP13K060-STATUS                                             
            WHEN 00                                                             
                 MOVE K060-DESC(1:7)   TO   WS-ETHNIC-APPLN                     
            WHEN OTHER                                                          
                 MOVE   SPACES         TO   WS-ETHNIC-APPLN                     
           END-EVALUATE.                                                        
                                                                                
       426-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       427-READ-BP13K060.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                 TO   K060-KEY-FLD.                       
           MOVE 22                     TO   K060-SERIAL-NO.                     
           MOVE WS-GENDER              TO   K060-CODE.                          
           READ BP13K060.                                                       
                                                                                
           EVALUATE BP13K060-STATUS                                             
            WHEN 00                                                             
                 MOVE K060-DESC(1:7)   TO   WS-GENDER-DESC                      
            WHEN OTHER                                                          
                 MOVE   SPACES         TO   WS-GENDER-DESC                      
           END-EVALUATE.                                                        
                                                                                
       427-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       500-READ-BP13K757.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES                 TO   WS-ETHNIC-APPLN                     
                                            WS-ETHNIC                           
                                            K757-KEY-FLD.                       
           MOVE F800-NUM-REGN          TO   K757-NUM-REGN.                      
                                                                                
           READ BP13K757.                                                       
                                                                                
           EVALUATE BP13K757-STATUS                                             
            WHEN 00                                                             
                 IF K757-NUM-APPL-RACE = SPACES OR LOW-VALUES                   
                    MOVE  SPACES         TO L27D-ETHNIC-APPLN                   
                 ELSE                                                           
                    MOVE K757-NUM-APPL-RACE  TO WS-ETHNIC                       
                    PERFORM 426-READ-BP13K060 THRU 426-EXIT                     
                 END-IF                                                         
            WHEN OTHER                                                          
                 MOVE   SPACES            TO L27D-ETHNIC-APPLN                  
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       600-GET-FLAT-ADDR.                                                       
      *-------------------------------------------------------------*           
           MOVE SPACES                TO BP13COMM8-REC.                         
           MOVE F800-NUM-SCH-ACC(1:9)   TO WS-LINK-REC.                         
                                                                                
           CALL WS-BP13C913 USING WS-LINK-REC, BP13COMM8-REC.                   
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                STRING 'BLK '  COMM8-NUM-BLK  ' '                               
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    ' ' DELIMITED BY SIZE             
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO L27D-FLAT-ADDRESS                                   
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         F800-NUM-SCH-ACC                                       
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' F800-NUM-SCH-ACC                            
           END-EVALUATE.                                                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       700-PRINT-CONTROL-HEADING.                                               
      *-------------------------------------------------------------            
           ADD  1                 TO   WS-PAGE-CNT                              
           MOVE 1                 TO   WS-LINE-CNT                              
           MOVE WS-PAGE-CNT       TO   L27D-PAGE.                               
                                                                                
           WRITE BP13L27D-REC     FROM L27D-HDG1.                               
           WRITE BP13L27D-REC     FROM L27D-HDG2.                               
           WRITE BP13L27D-REC     FROM L27D-HDG3.                               
                                                                                
           MOVE  SPACES           TO   BP13L27D-REC.                            
           WRITE BP13L27D-REC.                                                  
                                                                                
           ADD  6 TO WS-LINE-CNT.                                               
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           CLOSE BP13F800                                                       
                 BP13K820                                                       
                 BP13K060                                                       
                 BP13K757                                                       
                 BP13L27D.                                                      
                                                                                
           IF BP13K820-STATUS NOT = 00 AND 97                                   
              DISPLAY ' CLOSING BP13K820 ERROR ' BP13K820-STATUS                
              MOVE     BP13K820-STATUS  TO  RETURN-CODE                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY ' CLOSING BP13K060 ERROR ' BP13K060-STATUS                
              MOVE     BP13K060-STATUS  TO  RETURN-CODE                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K757-STATUS NOT = 00 AND 97                                   
              DISPLAY ' CLOSING BP13K757 ERROR ' BP13K757-STATUS                
              MOVE     BP13K757-STATUS  TO  RETURN-CODE                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
           DISPLAY '                                  '                         
           DISPLAY ' *----- BP13C27D CONTROL LIST ---------*'.                  
           DISPLAY '                                  '                         
           DISPLAY ' NO. OF REC READ FROM F800   = ' WS-F800-READ.              
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
