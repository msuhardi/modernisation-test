      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY42.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI                                       
       DATE-WRITTEN.  17/09/09.                                                 
      *****************************************************************         
      * OBJECTIVE -                                                   *         
      *   MATCH BP13FH13 WITH BP13F816, OUTPUT RECORDS IF             *         
      *   QUEUE-NO <= PERCENT-SUPPLY.                                 *         
      *                                                               *         
      *   PERCENT-SUPPLY IS COMPUTED BASED ON TOTAL-UNIT * %(BP13F205)*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F205                                  *         
      *                  2. BP13FH13                                  *         
      *                  3. BP13F816                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. P13FH13A                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------   --------  ---  ------------------------             *         
      * BP133712  17092009  PCL3 NEW PROGRAM.                         *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F205  ASSIGN        TO BP13F205.                         
           SELECT  BP13F816  ASSIGN        TO BP13F816.                         
           SELECT  BP13FH13  ASSIGN        TO BP13FH13.                         
           SELECT  P13FH13A  ASSIGN        TO P13FH13A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F205                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 80 CHARACTERS            
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F816                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 400 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13K816.                                                           
                                                                                
       FD   BP13FH13                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 200 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13FH13.                                                           
                                                                                
       FD   P13FH13A                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 200 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13FH13A-REC                PIC X(200).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  VARIABLES.                                                           
           05  WS-PCT-RND              PIC 9(1)V99 VALUE ZEROES.                
           05  WS-SUPPLY-RND           PIC 9(5)    VALUE ZEROES.                
           05  WS-UNIT-OFFER           PIC 9(4)    VALUE ZEROES.                
           05  WS-F205-PCT             PIC 9(5)    VALUE ZEROES.                
           05  WS-QUEUE-PCT            PIC 9(5)    VALUE ZEROES.                
           05  WS-QUEUE-RND            PIC 9(5)    VALUE ZEROES.                
           05  WS-FH13-QUEUE           PIC 9(5)    VALUE ZEROES.                
           05  WS-FH13-KEY             PIC X(3)    VALUE SPACES.                
           05  WS-F816-KEY             PIC X(3)    VALUE SPACES.                
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-F816-EOF             PIC X(1)    VALUE 'N'.                   
           05  WS-FH13-EOF             PIC X(1)    VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-BP13FH13-READ        PIC 9(7)  VALUE ZEROES.                  
           05  WS-BP13F816-READ        PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-FND              PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-NFND1            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-NFND2            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-FH13A            PIC 9(7)  VALUE ZEROES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 1500-READ-BP13F205      THRU 1500-EXIT.                      
           PERFORM 2000-READ-BP13FH13      THRU 2000-EXIT.                      
           PERFORM 3000-READ-BP13F816      THRU 3000-EXIT                       
                                                                                
           PERFORM 4000-PROCESS-RECORDS    THRU 4000-EXIT                       
             UNTIL WS-FH13-EOF = 'Y'                                            
               AND WS-F816-EOF = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FH13                                                 
                       BP13F816                                                 
                       BP13F205                                                 
                OUTPUT P13FH13A.                                                
                                                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1500-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205           AT END                                       
                GO                 TO 1500-EXIT.                                
                                                                                
           IF F205-START-QUEUE NOT NUMERIC                                      
              MOVE ZEROES          TO F205-START-QUEUE                          
           END-IF.                                                              
                                                                                
           MOVE F205-START-QUEUE   TO WS-F205-PCT.                              
                                                                                
       1500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-BP13FH13.                                                      
      ******************************************************************        
                                                                                
           READ BP13FH13           AT   END                                     
                MOVE 'Y'           TO   WS-FH13-EOF                             
                MOVE HIGH-VALUES   TO   WS-FH13-KEY                             
                GO                 TO   2000-EXIT.                              
                                                                                
           MOVE FH13-NUM-ZONE      TO   WS-FH13-KEY.                            
                                                                                
           ADD  1                  TO   WS-BP13FH13-READ.                       
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-READ-BP13F816.                                                      
      ******************************************************************        
                                                                                
           READ BP13F816           AT   END                                     
                MOVE 'Y'           TO   WS-F816-EOF                             
                MOVE HIGH-VALUES   TO   WS-F816-KEY                             
                GO                 TO   3000-EXIT.                              
                                                                                
           MOVE K816-NUM-ZONE      TO   WS-F816-KEY.                            
                                                                                
           ADD  1                  TO   WS-BP13F816-READ.                       
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF WS-FH13-KEY = WS-F816-KEY                                         
              ADD 1                                       TO WS-CNT-FND         
                                                                                
              PERFORM 5000-PROCESS-BP13FH13             THRU 5000-EXIT          
                UNTIL WS-FH13-KEY NOT = WS-F816-KEY                             
                   OR WS-FH13-EOF = 'Y'                                         
                                                                                
              PERFORM 3000-READ-BP13F816                THRU 3000-EXIT          
           ELSE                                                                 
              IF WS-FH13-KEY < WS-F816-KEY                                      
                 ADD 1                                  TO WS-CNT-NFND1         
                 PERFORM 2000-READ-BP13FH13             THRU 2000-EXIT          
              ELSE                                                              
                 ADD 1                                  TO WS-CNT-NFND2         
                 PERFORM 3000-READ-BP13F816             THRU 3000-EXIT          
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5000-PROCESS-BP13FH13.                                                   
      ******************************************************************        
                                                                                
           PERFORM 5100-CHECK-PCT-SUPPLY           THRU 5100-EXIT.              
                                                                                
           MOVE ZEROES                               TO WS-QUEUE-PCT            
                                                        WS-FH13-QUEUE.          
                                                                                
           IF K816-NUM-PERCENT-SUPPLY NOT NUMERIC                               
              MOVE ZEROES                    TO K816-NUM-PERCENT-SUPPLY         
           END-IF.                                                              
                                                                                
           MOVE K816-NUM-PERCENT-SUPPLY              TO WS-QUEUE-PCT.           
                                                                                
           IF FH13-NUM-NT-FT-QUEUE(1:1) NOT NUMERIC                             
              MOVE ZEROES                   TO FH13-NUM-NT-FT-QUEUE(1:1)        
           END-IF.                                                              
                                                                                
           IF FH13-NUM-NT-FT-QUEUE      NOT NUMERIC                             
              MOVE ZEROES                   TO FH13-NUM-NT-FT-QUEUE             
           END-IF.                                                              
                                                                                
           MOVE FH13-NUM-NT-FT-QUEUE                 TO WS-FH13-QUEUE.          
                                                                                
           IF (WS-FH13-QUEUE <= WS-QUEUE-PCT) OR                                
              (WS-QUEUE-PCT   = 0)                                              
              PERFORM 6000-WRITE-P13FH13A            THRU 6000-EXIT             
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FH13                THRU 2000-EXIT.            
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5100-CHECK-PCT-SUPPLY.                                                   
      ******************************************************************        
                                                                                
           MOVE ZEROES                           TO WS-PCT-RND                  
                                                    WS-SUPPLY-RND               
                                                    WS-UNIT-OFFER.              
                                                                                
           IF K816-NUM-UNIT-OFFER NOT NUMERIC                                   
              MOVE ZEROES                        TO K816-NUM-UNIT-OFFER         
           END-IF.                                                              
                                                                                
           MOVE K816-NUM-UNIT-OFFER              TO WS-UNIT-OFFER.              
                                                                                
           COMPUTE WS-PCT-RND = WS-F205-PCT / 100.                              
                                                                                
           COMPUTE WS-SUPPLY-RND ROUNDED                                        
                              = WS-UNIT-OFFER * WS-PCT-RND.                     
                                                                                
           MOVE WS-SUPPLY-RND               TO K816-NUM-PERCENT-SUPPLY.         
                                                                                
       5100-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       6000-WRITE-P13FH13A.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                                 TO P13FH13A-REC.         
           INITIALIZE                                     P13FH13A-REC.         
                                                                                
           WRITE P13FH13A-REC                        FROM BP13FH13-REC.         
           ADD 1                                       TO WS-CNT-FH13A.         
                                                                                
       6000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CY42'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY 'NO OF BP13FH13 RECORDS READ................. : '            
                    WS-BP13FH13-READ.                                           
           DISPLAY 'NO OF BP13F816 RECORDS READ................. : '            
                    WS-BP13F816-READ.                                           
           DISPLAY 'NO OF P13FH13A RECORDS WRITTEN ............. : '            
                    WS-CNT-FH13A.                                               
                                                                                
           CLOSE BP13FH13                                                       
                 BP13F816                                                       
                 BP13F205                                                       
                 P13FH13A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
