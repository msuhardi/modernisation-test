       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CS05.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  14/03/2007.                                               
                                                                                
      *************************************************************             
      *      SYSTEM OF COMMITMENT (BP13)                          *             
      *===========================================================*             
      *                                                           *             
      *  OBJECTIVES: 1. TO NOTIFY APPLICANTS THRU EMAIL OF THE    *             
      *                 RESULTS OF THE BALLOT AND THEIR SCHEDULED *             
      *                 APPOINTMENT TO BOOK FLAT.                 *             
      *              2. PROGRAM TO PUT MQ MESSAGE INTO QUEUE      *             
      *                 WHICH CONTAINS CUSTOMER'S EMAIL ADDRESS,  *             
      *                 QUEUE NO., APPT DATE/TIME TO BE SENT BY   *             
      *                 THE JAVA INTERFACE.                       *             
      *                                                           *             
      *   PROGRAM LOGIC:                                          *             
      *         PROCESS INPUT FILES                               *             
      *         MQCONNECT TO QUEUE MANAGER                        *             
      *         MQOPEN QUEUE                                      *             
      *         MQPUT                                             *             
      *         MQCLOSE QUEUE                                     *             
      *         MQDISCONNECT FROM QUEUE MANAGER                   *             
      *                                                           *             
      *===========================================================*             
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHG REQ#    DATE    AUTHOR REMARKS                        *             
      * -------- ---------- ------ ------------------------------ *             
      * BP133010 14/03/2007  SD10  NEW PROGRAM                    *             
      * BP132994 05/04/2007  SD10  CHANGE 12AM TO 12PM            *             
      * BP133062 30/04/2007  SD10  ADD APPT TYPE = 'AR'           *             
      *          02/05/2007  SD10  CHANGE FORMAT FOR APPT DTE     *             
      * BP133076 05/06/2007  SD10  CHANGE OCCURRENCE OF MESSAGE   *             
      *                            FROM 500 TO 2000               *             
      * BP133090 03/07/2007  SD10  CHANGE FORMAT FOR APPT DTE     *             
      *                            FR: DD/MM/YY TO: DD MMM YY.    *             
      *          04/07/2007  SD10  PASS REGN NO. TO COMMAREA.     *             
      * BP133430 10/09/2008  CT2   CHANGES FOR WIH                *             
      * BP133439 21/10/2008  CT2   ADD APPT TYPE 'AV' FOR 3R      *             
      * BP133596 04/05/2009  PCL3  TO READ SYSIN CARD             *             
      *************************************************************             
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       SPECIAL-NAMES.                                                           
           SYSIN IS PARM-INPUT.                                                 
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT BP13F203 ASSIGN TO BP13F203                                   
                  ORGANIZATION    IS SEQUENTIAL                                 
                  FILE STATUS     IS BP13F203-STAT.                             
                                                                                
           SELECT BP13K857 ASSIGN TO BP13K857                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K857-KEY-FLD                               
                  FILE STATUS     IS BP13K857-STAT.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F203                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F203.                                                           
                                                                                
       FD  BP13K857                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K857.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *----------------------------------------------------------*              
      *  IMPORTANT!                                              *              
      *  FOR PRODUCTION, QUEUE MANAGER IS 'MQP1'                 *              
      *  FOR TESTING,    QUEUE MANAGER IS 'MQT1'                 *              
      *----------------------------------------------------------*              
       01  WS-QM-NAME               PIC X(48) VALUE 'MQP1'.                     
      *01  WS-QM-NAME               PIC X(48) VALUE 'MQT1'.                     
       01  WS-BP13Q-NAME            PIC X(48) VALUE SPACES.                     
                                                                                
      *----------------------------------------------------------*              
      *  OUTPUT QUEUE NAME TO CRIMSONLOGIC                       *              
      *  THE QUEUE NAME IS THE SAME FOR PRODUCTION AND TESTING   *              
      *----------------------------------------------------------*              
       01  WS-QUEUE-NAME   PIC X(48) VALUE 'BP13.WIS.MDB.REQUESTQ'.             
                                                                                
      *----------------------------------------------------------*              
      *  MESSAGE LENGTH                                          *              
      *----------------------------------------------------------*              
       01  WS-MESSAGE-LENGTH        PIC 9(06) VALUE 220012.                     
                                                                                
      *----------------------------------------------------------*              
      *  MESSAGE                                                 *              
      *----------------------------------------------------------*              
       01  WS-SEND-MESSAGE.                                                     
           05  WS-MSG-CDE                   PIC X(03).                          
           05  WS-FILLER-01                 PIC X(01).                          
           05  WS-MSG-APPT-TYP              PIC X(02).                          
           05  WS-FILLER-02                 PIC X(01).                          
           05  WS-MSG-CTR                   PIC 9(04).                          
           05  WS-FILLER-03                 PIC X(01).                          
           05  WS-MSG OCCURS 2000 TIMES.                                        
               10  WS-MSG-EMAIL             PIC X(50).                          
               10  WS-FILLER-10             PIC X(01).                          
               10  WS-MSG-DTE-BALLOT        PIC X(14).                          
               10  WS-FILLER-11             PIC X(01).                          
               10  WS-MSG-MTH-CDE           PIC X(01).                          
      * MTH-CDE :                                                               
      * 1 = Monthly                                                             
      * 2 = Quarterly                                                           
               10  WS-FILLER-12             PIC X(01).                          
               10  WS-MSG-ALLO-MODE         PIC X(03).                          
      * ALLO-MODE :                                                             
      * 3-rm flat = 3R                                                          
      * 4-rm flat = WIA, WIB, WIC, WIH                                          
               10  WS-FILLER-13             PIC X(01).                          
               10  WS-MSG-FLAT-TYPE         PIC X(02).                          
               10  WS-FILLER-14             PIC X(01).                          
               10  WS-MSG-QUEUE-NO          PIC X(05).                          
               10  WS-FILLER-15             PIC X(01).                          
               10  WS-MSG-DTE-APPMT         PIC X(11).                          
               10  WS-FILLER-16             PIC X(01).                          
               10  WS-MSG-TME-APPMT         PIC X(07).                          
               10  WS-FILLER-17             PIC X(01).                          
               10  WS-MSG-REGN              PIC X(08).                          
               10  WS-FILLER-18             PIC X(01).                          
                                                                                
      *----------------------------------------------------------*              
      *  OTHER VARIABLES                                         *              
      *----------------------------------------------------------*              
       01  WS-VARIABLES.                                                        
           05  WS-CTR               PIC 9(04).                                  
           05  WS-START             PIC 9(03).                                  
           05  WS-MTH-BALLOT        PIC X(10).                                  
           05  WS-MTH-APPT          PIC X(04).                                  
           05  WS-DTE-BALLOT        PIC X(14).                                  
           05  WS-MTH-CDE           PIC X(01).                                  
           05  WS-FLAT-TYPE         PIC X(20).                                  
           05  WS-DTE-APPMT         PIC X(11).                                  
           05  WS-TME-APPMT         PIC X(07).                                  
           05  WS-MSG-DETAIL        PIC X(320).                                 
           05  WS-TME.                                                          
               10  WS-TME-HH        PIC 9(02).                                  
               10  WS-TME-MM        PIC 9(02).                                  
           05  WS-APPT-HH           PIC 9(02).                                  
                                                                                
       01  WS-MTH-CONV.                                                         
           05  FILLER               PIC X(05)    VALUE 'Jan01'.                 
           05  FILLER               PIC X(05)    VALUE 'Feb02'.                 
           05  FILLER               PIC X(05)    VALUE 'Mar03'.                 
           05  FILLER               PIC X(05)    VALUE 'Apr04'.                 
           05  FILLER               PIC X(05)    VALUE 'May05'.                 
           05  FILLER               PIC X(05)    VALUE 'Jun06'.                 
           05  FILLER               PIC X(05)    VALUE 'Jul07'.                 
           05  FILLER               PIC X(05)    VALUE 'Aug08'.                 
           05  FILLER               PIC X(05)    VALUE 'Sep09'.                 
           05  FILLER               PIC X(05)    VALUE 'Oct10'.                 
           05  FILLER               PIC X(05)    VALUE 'Nov11'.                 
           05  FILLER               PIC X(05)    VALUE 'Dec12'.                 
       01  WS-MTH-CONV-TABLE REDEFINES WS-MTH-CONV.                             
           05  WS-TF-MTH    OCCURS 12 TIMES INDEXED BY WS-IDX.                  
               10  WS-TRAN-MM      PIC X(3).                                    
               10  WS-NUM-MM       PIC X(2).                                    
                                                                                
       01  WS-TABLE.                                                            
           05  FILLER              PIC X(11) VALUE '01January  '.               
           05  FILLER              PIC X(11) VALUE '02February '.               
           05  FILLER              PIC X(11) VALUE '03March    '.               
           05  FILLER              PIC X(11) VALUE '04April    '.               
           05  FILLER              PIC X(11) VALUE '05May      '.               
           05  FILLER              PIC X(11) VALUE '06June     '.               
           05  FILLER              PIC X(11) VALUE '07July     '.               
           05  FILLER              PIC X(11) VALUE '08August   '.               
           05  FILLER              PIC X(11) VALUE '09September'.               
           05  FILLER              PIC X(11) VALUE '10October  '.               
           05  FILLER              PIC X(11) VALUE '11November '.               
           05  FILLER              PIC X(11) VALUE '12December '.               
       01  WS-MTH-TABLE REDEFINES WS-TABLE.                                     
           05  MONTH-TABLE OCCURS 12 TIMES INDEXED BY WS-IND.                   
               10  WS-MTH-CODE     PIC XX.                                      
               10  WS-MTH-DESC     PIC X(9).                                    
                                                                                
      *-------------------------------------------------------------            
      *  MQI - TEMPORARY VARIABLES FOR ERROR CODES                              
      *-------------------------------------------------------------            
       01  BP13CS05-REC.                                                        
           05 CS05-REASON          PIC 9(4).                                    
           05 CS05-COMPLETION-CODE PIC 9(4).                                    
           05 CS05-ERR-MSG         PIC X(66).                                   
           05 FILLER               PIC X(27).                                   
                                                                                
                                                                                
      *----------------------------------------------------------*              
      *              MQI STRUCTURE DECLARATION                   *              
      *----------------------------------------------------------*              
       01 MY-MQ-CONSTANTS.                                                      
          COPY CMQV.                                                            
                                                                                
       01 OBJECT-DESCRIPTOR.                                                    
          COPY CMQODV.                                                          
                                                                                
       01 MESSAGE-DESCRIPTOR.                                                   
          COPY CMQMDV.                                                          
                                                                                
      * PUT MESSAGE OPTIONS                                                     
       01 PMOPTIONS.                                                            
          COPY CMQPMOV.                                                         
                                                                                
      *----------------------------------------------------------*              
      * VARIABLE DESCRIPTION:                                    *              
      * (1) HCONN                                                *              
      *       - CONNECTION HANDLE - REPRESENTS THE CONNECTION TO *              
      *         THE QUEUE MANAGER. VALUE IS RETURNED BY MQCONN   *              
      *         AND SUBSEQUENTLY REFERENCED IN MQOPEN, MQPUT,    *              
      *         MQCLOSE AND MQDISC                               *              
      *                                                          *              
      * (2) Q-HANDLE                                             *              
      *       - ACCESS THAT HAS BEEN ESTABLISHED  TO THE OBJECT  *              
      *         CONNECTION HANDLE - VALUE IS RETURNED BY MQOPEN  *              
      *         AND SUBSEQUENTLY REFERENCED IN MQPUT AND MQCLOSE *              
      *                                                          *              
      * (3) OBJECT-DESCRIPTOR                                    *              
      *       - THIS IDENTIFIES THE OBJECT TO BE OPENED          *              
      *                                                          *              
      * (4) OPTIONS                                              *              
      *       WHEN USED IN MQOPEN                                *              
      *       - OPTIONS THAT CONTROL THE ACTION OF MQOPEN        *              
      *       - OPTIONS USED IN THIS PROGRAM:                    *              
      *         MQOO-OUTPUT - THE QUEUE IS OPENED FOR USE WITH   *              
      *                       SUBSEQUENT MQPUT CALL              *              
      *         MQOO-FAIL-IF-QUIESCING - FORCES MQCALL TO FAIL   *              
      *                       IF QUEUE MANAGER IS IN QUIESCING   *              
      *                       STATE (I.E. PREPARING TO SHUTDOWN) *              
      *                                                          *              
      *       WHEN USED IN MQCLOSE                               *              
      *       - OPTIONS THAT CONTROL THE ACTION OF MQCLOSE       *              
      *       - OPTIONS USED IN THIS PROGRAM:                    *              
      *         MQCO-NONE   - NO OPTIONAL CLOSE PROCESSING       *              
      *                       REQUIRED                           *              
      *                                                          *              
      * (5) MESSAGE-DESCRIPTOR                                   *              
      *       - THIS DESCRIBES THE ATTRIBUTES OF THE MESSAGE     *              
      *         BEING SENT, AND RECEIVES INFO ABOUT THE MESSAGE  *              
      *         AFTER THE PUT REQUEST IS COMPLETE                *              
      *                                                          *              
      * (6) PMOPTIONS (PUT MESSAGE OPTIONS)                      *              
      *       - OPTIONS THAT CONTROL THE ACTION OF MQPUT         *              
      *       - OPTION USED IN THIS PROGRAM:                     *              
      *         MQMD-EXPIRY - MESSAGE LIFETIME. THE MESSAGE      *              
      *                       BECOMES ELIGIBLE TO BE DISCARDED   *              
      *                       IF IT HAS NOT BEEN REMOVED FROM    *              
      *                       FROM THE QUEUE BEFORE THIS PERIOD  *              
      *                       OF TIME ELAPSED                    *              
      *                     - MUST BE SET TO MQEI_UNLIMITED      *              
      *         MQMD-PERSISTENCE = MQPER-PERSISTENT              *              
      *                                                          *              
      * (7) BUFFER-LENGTH                                        *              
      *       - LENGTH OF THE MESSAGE IN BUFFER                  *              
      *                                                          *              
      * (7) BUFFER                                               *              
      *       - MESSAGE DATA                                     *              
      *----------------------------------------------------------*              
       01 HCONN                    PIC S9(9) BINARY.                            
       01 Q-HANDLE                 PIC S9(9) BINARY.                            
       01 OPTIONS                  PIC S9(9) BINARY.                            
       01 COMPLETION-CODE          PIC S9(9) BINARY.                            
       01 OPEN-CODE                PIC S9(9) BINARY.                            
       01 CON-REASON               PIC S9(9) BINARY.                            
       01 REASON                   PIC S9(9) BINARY.                            
       01 MQMSGD                   PIC S9(9) BINARY.                            
       01 BUFFER-LENGTH            PIC S9(9) BINARY.                            
       01 BUFFER                   PIC X(220012).                               
       01 QM-NAME                  PIC X(48) VALUE SPACES.                      
       01 TARGET-QUEUE             PIC X(48) VALUE SPACES.                      
                                                                                
      *-------------------------------------------------------------            
      *  FILE STATUS                                                            
      *-------------------------------------------------------------            
       01  WS-FILE-STAT.                                                        
           05 BP13F203-STAT        PIC X(02)  VALUE SPACES.                     
           05 BP13K857-STAT        PIC X(02)  VALUE SPACES.                     
                                                                                
      *-------------------------------------------------------------            
      *  FLAGS / SWITCHES                                                       
      *-------------------------------------------------------------            
       01  WS-FLAGS.                                                            
           05 F203-EOF             PIC X(01) VALUE 'N'.                         
           05 K857-EOF             PIC X(01) VALUE 'N'.                         
           05 F203-EMPTY           PIC X(01) VALUE 'N'.                         
           05 K857-EMPTY           PIC X(01) VALUE 'N'.                         
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      ********************                                                      
       0000-MAIN-PROCESS.                                                       
      ********************                                                      
           PERFORM 0100-OPEN-FILES         THRU 0100-EXIT.                      
           PERFORM 0200-INITIALIZATION     THRU 0200-EXIT.                      
                                                                                
           ACCEPT WS-BP13Q-NAME            FROM PARM-INPUT.                     
                                                                                
           PERFORM 1000-CHECK-MESSAGE      THRU 1000-EXIT.                      
           MOVE F203-TYPE-APPMT            TO WS-MSG-APPT-TYP.                  
           PERFORM 3000-PROCESS-BP13F203   THRU 3000-EXIT                       
                   UNTIL F203-EOF = 'Y'.                                        
           DISPLAY 'WS-SEND-MESSAGE : ' WS-SEND-MESSAGE.                        
           PERFORM 5300-MQ-PUT-MESSAGE     THRU 5300-EXIT.                      
           DISPLAY ' *** APPOINTMENT NOTIFICATION SENT!'                        
           PERFORM 5400-MQ-CLOSE           THRU 5400-EXIT.                      
           PERFORM 5500-MQ-DISCONNECT      THRU 5500-EXIT.                      
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       0100-OPEN-FILES.                                                         
      ********************                                                      
           DISPLAY ' ***** STARTING PROGRAM BP13CS05 . . . '.                   
           DISPLAY ' '.                                                         
           OPEN    INPUT   BP13F203                                             
                           BP13K857.                                            
                                                                                
           IF  BP13F203-STAT     NOT  =  00                                     
               DISPLAY ' OPEN ERROR BP13F203'                                   
               DISPLAY 'BP13F203-STAT = ' BP13F203-STAT                         
               MOVE     12                   TO RETURN-CODE                     
               PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT.                    
                                                                                
           IF  BP13K857-STAT     NOT  =  00                                     
               DISPLAY ' OPEN ERROR BP13K857'                                   
               DISPLAY 'BP13K857-STAT = ' BP13K857-STAT                         
               MOVE     12                   TO RETURN-CODE                     
               PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT.                    
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       0200-INITIALIZATION.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES     TO WS-SEND-MESSAGE.                                  
           INITIALIZE         WS-SEND-MESSAGE.                                  
           MOVE ';'        TO WS-FILLER-01                                      
                              WS-FILLER-02                                      
                              WS-FILLER-03.                                     
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 2000               
              MOVE ';'     TO WS-FILLER-10(WS-CTR)                              
                              WS-FILLER-11(WS-CTR)                              
                              WS-FILLER-12(WS-CTR)                              
                              WS-FILLER-13(WS-CTR)                              
                              WS-FILLER-14(WS-CTR)                              
                              WS-FILLER-15(WS-CTR)                              
                              WS-FILLER-16(WS-CTR)                              
                              WS-FILLER-17(WS-CTR)                              
                              WS-FILLER-18(WS-CTR)                              
           END-PERFORM.                                                         
                                                                                
           MOVE 'EML'      TO WS-MSG-CDE.                                       
                                                                                
      *-----------------------------                                            
      *  INITIAL READ OF INPUT FILES                                            
      *-----------------------------                                            
           READ BP13F203                                                        
               AT END                                                           
                   MOVE 'Y' TO F203-EOF                                         
                   MOVE 'Y' TO F203-EMPTY                                       
                   DISPLAY ' *** '                                              
                   DISPLAY ' *** NO RECORDS FOUND IN SMS FILE'                  
                   DISPLAY ' *** '.                                             
                                                                                
           READ BP13K857 NEXT RECORD                                            
               AT END                                                           
                   MOVE 'Y' TO K857-EOF                                         
                   MOVE 'Y' TO K857-EMPTY                                       
                   DISPLAY ' *** '                                              
                   DISPLAY ' *** NO RECORDS FOUND IN EMAIL ADDR FILE'           
                   DISPLAY ' *** '.                                             
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       1000-CHECK-MESSAGE.                                                      
      ************************                                                  
           IF F203-EMPTY = 'Y' AND K857-EMPTY = 'Y'                             
               PERFORM 9000-CLOSE-FILES  THRU  9000-EXIT                        
           ELSE                                                                 
               PERFORM 5100-MQ-CONNECT   THRU  5100-EXIT                        
               PERFORM 5200-MQ-OPEN      THRU  5200-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO BUFFER.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       3000-PROCESS-BP13F203.                                                   
      ************************                                                  
                                                                                
           MOVE SPACES                  TO BP13K857-REC.                        
           MOVE F203-NUM-REGN           TO K857-NUM-REGN.                       
           PERFORM 3010-READ-BP13K857   THRU 3010-EXIT.                         
                                                                                
           IF F203-TYPE-APPMT = 'AP' OR 'AQ' OR 'AR' OR 'AT' OR 'AV'            
              IF K857-NUM-EMAIL NOT = SPACES AND LOW-VALUES AND ZEROES          
                 PERFORM 3200-PASS-MSG  THRU 3200-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           READ BP13F203                                                        
               AT END                                                           
                   MOVE 'Y' TO F203-EOF.                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3010-READ-BP13K857.                                                      
      ****************************************************************          
                                                                                
           READ BP13K857.                                                       
           IF BP13K857-STAT = 00                                                
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13K857-STAT = 23                                             
                 DISPLAY 'EMAIL ADDRESS NOT FOUND : ' K857-KEY-FLD              
                 MOVE BP13K857-STAT  TO RETURN-CODE                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3200-PASS-MSG.                                                           
      ****************************************************************          
                                                                                
           MOVE SPACES     TO WS-DTE-BALLOT                                     
                              WS-MTH-CDE                                        
                              WS-FLAT-TYPE                                      
                              WS-DTE-APPMT                                      
                              WS-TME-APPMT.                                     
                                                                                
           ADD 1  TO WS-MSG-CTR.                                                
                                                                                
           IF F203-DTE-BALLOT NOT = SPACES AND LOW-VALUES AND ZEROES            
              PERFORM 3300-FORMAT-BALLOT-DTE  THRU 3300-EXIT                    
           END-IF.                                                              
                                                                                
           IF F203-NUM-ALLO-MODE = '3R'                                         
              MOVE '1'               TO WS-MTH-CDE                              
           ELSE                                                                 
              MOVE '2'               TO WS-MTH-CDE                              
           END-IF.                                                              
                                                                                
           IF F203-DTE-APPMT NOT = SPACES AND LOW-VALUES AND ZEROES             
              PERFORM 3400-FORMAT-APPT-DTE  THRU 3400-EXIT                      
           END-IF.                                                              
                                                                                
           IF F203-TME-APPMT NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE F203-TME-APPMT    TO WS-TME                                  
              IF WS-TME-HH > 12                                                 
                 COMPUTE WS-APPT-HH = WS-TME-HH - 12                            
                 STRING WS-APPT-HH ':'                                          
                        WS-TME-MM  'pm'                                         
                        DELIMITED BY SIZE                                       
                 INTO WS-TME-APPMT                                              
              ELSE                                                              
                 IF WS-TME-HH = 12                                              
                    STRING WS-TME-HH  ':'                                       
                           WS-TME-MM  'pm'                                      
                           DELIMITED BY SIZE                                    
                    INTO WS-TME-APPMT                                           
                 ELSE                                                           
                    STRING WS-TME-HH  ':'                                       
                           WS-TME-MM  'am'                                      
                           DELIMITED BY SIZE                                    
                    INTO WS-TME-APPMT                                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F203-NUM-REGN        TO WS-MSG-REGN (WS-MSG-CTR).               
           MOVE K857-NUM-EMAIL       TO WS-MSG-EMAIL (WS-MSG-CTR).              
           MOVE WS-DTE-BALLOT        TO WS-MSG-DTE-BALLOT (WS-MSG-CTR).         
           MOVE WS-MTH-CDE           TO WS-MSG-MTH-CDE (WS-MSG-CTR).            
           MOVE F203-NUM-ALLO-MODE   TO WS-MSG-ALLO-MODE (WS-MSG-CTR).          
           MOVE F203-NUM-FLAT-TYPE   TO WS-MSG-FLAT-TYPE (WS-MSG-CTR).          
           MOVE F203-NUM-QUEUE       TO WS-MSG-QUEUE-NO (WS-MSG-CTR).           
           MOVE WS-DTE-APPMT         TO WS-MSG-DTE-APPMT (WS-MSG-CTR).          
           MOVE WS-TME-APPMT         TO WS-MSG-TME-APPMT (WS-MSG-CTR).          
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3300-FORMAT-BALLOT-DTE.                                                  
      ****************************************************************          
                                                                                
           SET WS-IND TO 1.                                                     
                                                                                
           SEARCH MONTH-TABLE                                                   
               AT END                                                           
                  MOVE SPACES              TO WS-MTH-BALLOT                     
               WHEN WS-MTH-CODE (WS-IND) = F203-DTE-BALLOT(5:2)                 
                  MOVE WS-MTH-DESC (WS-IND) TO WS-MTH-BALLOT                    
           END-SEARCH.                                                          
                                                                                
           STRING WS-MTH-BALLOT                                                 
                  F203-DTE-BALLOT(1:4)                                          
                  DELIMITED BY SIZE                                             
             INTO WS-DTE-BALLOT.                                                
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3400-FORMAT-APPT-DTE.                                                    
      ****************************************************************          
                                                                                
           SET WS-IDX TO 1.                                                     
                                                                                
           SEARCH WS-TF-MTH                                                     
               AT END                                                           
                  MOVE SPACES              TO WS-MTH-APPT                       
               WHEN WS-NUM-MM (WS-IDX) = F203-DTE-APPMT(5:2)                    
                  MOVE WS-TRAN-MM (WS-IDX) TO WS-MTH-APPT                       
           END-SEARCH.                                                          
                                                                                
           STRING F203-DTE-APPMT(7:2) ' '                                       
                  WS-MTH-APPT                                                   
                  F203-DTE-APPMT(1:4)                                           
                  DELIMITED BY SIZE                                             
             INTO WS-DTE-APPMT.                                                 
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5100-MQ-CONNECT.                                                         
      ****************************************************************          
      *---------------------------------------------------------------          
      *  SET THE QUEUE MANAGER NAME                                             
      *---------------------------------------------------------------          
           IF WS-BP13Q-NAME NOT = SPACES AND LOW-VALUES                         
              MOVE WS-BP13Q-NAME           TO   QM-NAME                         
           ELSE                                                                 
              MOVE WS-QM-NAME              TO   QM-NAME                         
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *   MQCONN                                                                
      *---------------------------------------------------------------          
           CALL 'MQCONN'                                                        
                USING QM-NAME, HCONN,                                           
                COMPLETION-CODE, CON-REASON.                                    
                                                                                
           IF COMPLETION-CODE  = MQCC-FAILED                                    
              MOVE CON-REASON              TO   CS05-REASON                     
              MOVE COMPLETION-CODE         TO   CS05-COMPLETION-CODE            
              MOVE 'BP13CS05 MQCONN ERROR' TO   CS05-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       5200-MQ-OPEN.                                                            
      ****************************************************************          
      *---------------------------------------------------------------          
      *  USE DEFAULT WS-QUEUE-NAME                                              
      *---------------------------------------------------------------          
           MOVE  WS-QUEUE-NAME         TO MQOD-OBJECTNAME.                      
           ADD   MQOO-OUTPUT MQOO-FAIL-IF-QUIESCING                             
                               GIVING OPTIONS.                                  
                                                                                
      *---------------------------------------------------------------          
      *   MQOPEN                                                                
      *---------------------------------------------------------------          
           CALL 'MQOPEN'                                                        
                USING HCONN, OBJECT-DESCRIPTOR,                                 
                OPTIONS, Q-HANDLE,                                              
                OPEN-CODE, REASON.                                              
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                     TO   CS05-REASON                  
              MOVE OPEN-CODE                  TO   CS05-COMPLETION-CODE         
              MOVE 'MQOPEN ENDED WITH REASON CODE'                              
                                              TO   CS05-ERR-MSG                 
              PERFORM 9100-EXIT-MQCONN        THRU 9100-EXIT                    
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       5300-MQ-PUT-MESSAGE.                                                     
      ***********************                                                   
           MOVE WS-SEND-MESSAGE     TO BUFFER.                                  
                                                                                
      *---------------------------------------------------------------          
      *  SET UP MESSAGE DESCRIPTOR                                              
      *---------------------------------------------------------------          
           MOVE MQEI-UNLIMITED      TO MQMD-EXPIRY.                             
           MOVE MQPER-PERSISTENT    TO MQMD-PERSISTENCE.                        
           MOVE MQFMT-STRING        TO MQMD-FORMAT.                             
           MOVE WS-MESSAGE-LENGTH   TO BUFFER-LENGTH.                           
      *---------------------------------------------------------------          
      *   MQPUT                                                                 
      *---------------------------------------------------------------          
           CALL 'MQPUT'                                                         
                USING HCONN, Q-HANDLE,                                          
                      MESSAGE-DESCRIPTOR, PMOPTIONS,                            
                      BUFFER-LENGTH, BUFFER,                                    
                      COMPLETION-CODE, REASON.                                  
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                  TO   CS05-REASON                     
              MOVE COMPLETION-CODE         TO   CS05-COMPLETION-CODE            
              MOVE 'BP13CS05 MQPUT ERROR'  TO   CS05-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5400-MQ-CLOSE.                                                           
      ****************************************************************          
           MOVE MQCO-NONE TO OPTIONS.                                           
                                                                                
      *---------------------------------------------------------------          
      *   MQCLOSE                                                               
      *---------------------------------------------------------------          
           CALL 'MQCLOSE'                                                       
                USING HCONN, Q-HANDLE, OPTIONS,                                 
                COMPLETION-CODE, REASON.                                        
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                   TO   CS05-REASON                    
              MOVE COMPLETION-CODE          TO   CS05-COMPLETION-CODE           
              MOVE 'BP13CS05 MQCLOSE ERROR' TO   CS05-ERR-MSG                   
              PERFORM 9100-EXIT-MQCONN      THRU 9100-EXIT                      
           END-IF.                                                              
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5500-MQ-DISCONNECT.                                                      
      ****************************************************************          
      *---------------------------------------------------------------          
      *  DISCONNECT FROM QUEUE MANAGER                                          
      *---------------------------------------------------------------          
           CALL 'MQDISC'                                                        
           USING HCONN, COMPLETION-CODE, REASON                                 
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                  TO   CS05-REASON                     
              MOVE COMPLETION-CODE         TO   CS05-COMPLETION-CODE            
              MOVE 'BP13CS05 MQDISC ERROR' TO   CS05-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-FILES.                                                        
      ****************************************************************          
                                                                                
           CLOSE BP13F203                                                       
                 BP13K857.                                                      
                                                                                
           IF    RETURN-CODE NOT = ZERO                                         
                 DISPLAY ' *** RETURN-CODE = ' RETURN-CODE ' ERROR'             
           ELSE                                                                 
                 DISPLAY ' '                                                    
                 DISPLAY ' *** SUCCESSFUL EXECUTION OF BP13CS05.'               
                 DISPLAY ' *** MESSAGE/S PUT INTO QUEUE SUCCESSFULLY!'          
                 DISPLAY ' *** Q MANAGER : ' QM-NAME                            
                 DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                      
                 DISPLAY ' '                                                    
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9100-EXIT-MQCONN.                                                        
      ****************************************************************          
           DISPLAY ' *** PROGRAM ID:  BP13CS05 '                                
           DISPLAY ' *** Q MANAGER : ' QM-NAME                                  
           DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                            
           DISPLAY ' '                                                          
           IF CS05-COMPLETION-CODE NOT = ZEROES AND LOW-VALUES                  
              DISPLAY ' *** MQ COMPLETION CODE: ' CS05-COMPLETION-CODE          
              DISPLAY ' *** MQ REASON         : ' CS05-REASON                   
              DISPLAY ' *** MQ ERROR MSG      : ' CS05-ERR-MSG                  
              MOVE 99                    TO  RETURN-CODE                        
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       9100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
      **********            END OF BP13CS05                 **********          
      ****************************************************************          
