       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C943.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  25/10/2001.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: TO EXTRACT SPOUSE NAME FROM BP13K820           *            
      *                                                            *            
      *  INPUT FILES:                                              *            
      *      1. BP13K820                                           *            
      *         FILES:                                                *         
      *      1. P13F820O                                           *            
      *      2. BP13F820                                           *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY   ON       DESCRIPTION                         *            
      * -------- ---- -------- -----------                         *            
      * BP130850 AS1  20011025 NEW PROGRAM                         *            
      * ========================================================= *             
      *--------------------*                                                    
       ENVIRONMENT DIVISION.                                                    
      *--------------------*                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K820  ASSIGN  TO  BP13K820                                
                       ACCESS       IS  RANDOM                                  
                       ORGANIZATION IS  INDEXED                                 
                       RECORD KEY   IS  K820-KEY-FLD                            
                       FILE STATUS  IS  WS-K820-STATUS.                         
                                                                                
           SELECT BP13F820  ASSIGN  TO  BP13F820.                               
           SELECT P13F820O  ASSIGN  TO  P13F820O.                               
           SELECT P13F820E  ASSIGN  TO  P13F820E.                               
                                                                                
      *--------------*                                                          
       DATA DIVISION.                                                           
      *--------------*                                                          
       FILE SECTION.                                                            
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F820                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 400  CHARACTERS                                      
           LABEL  RECORDS  ARE STANDARD                                         
           RECORDING MODE IS F.                                                 
       COPY BP13F820.                                                           
                                                                                
       FD  P13F820O                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 400  CHARACTERS                                      
           LABEL  RECORDS  ARE STANDARD                                         
           RECORDING MODE IS F.                                                 
       01  P13F820O-REC             PIC X(400).                                 
                                                                                
       FD  P13F820E                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 400  CHARACTERS                                      
           LABEL  RECORDS  ARE STANDARD                                         
           RECORDING MODE IS F.                                                 
       01  P13F820E-REC             PIC X(400).                                 
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  FILE-STATUS.                                                         
           03  WS-K820-STATUS             PIC 9(2)   VALUE 0.                   
                                                                                
       01  WS-ACCUMULATORS.                                                     
           03  WS-K820-READ               PIC 9(10)   VALUE ZERO.               
           03  WS-F820-READ               PIC 9(10)   VALUE ZERO.               
           03  WS-K820-NOTFND             PIC 9(10)   VALUE ZERO.               
           03  WS-F820-WRITE              PIC 9(10)   VALUE ZERO.               
           03  WS-F820-ERROR              PIC 9(10)   VALUE ZERO.               
                                                                                
       01  WS-F820-EOF                    PIC X       VALUE 'N'.                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
       0000-MAIN-LOGIC.                                                         
      *-------------------*                                                     
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F820   THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-ROUTINE THRU 3000-EXIT                          
                   UNTIL WS-F820-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       1000-OPEN-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           OPEN INPUT     BP13K820                                              
                          BP13F820                                              
                OUTPUT    P13F820E                                              
                          P13F820O.                                             
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY ' OPENING BP13K820  ERROR ' WS-K820-STATUS                
              MOVE WS-K820-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13F820.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13F820  AT END MOVE 'Y' TO WS-F820-EOF                        
                                           GO TO 2000-EXIT.                     
                                                                                
           ADD       1                    TO WS-F820-READ.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
      *---------------------*                                                   
       3000-PROCESS-ROUTINE.                                                    
      *---------------------*                                                   
                                                                                
            MOVE F820-NUM-REGN           TO   K820-NUM-REGN.                    
            MOVE F820-NUM-NRIC-SPOUSE    TO   K820-NUM-NRIC.                    
            READ BP13K820.                                                      
                                                                                
            EVALUATE WS-K820-STATUS                                             
                WHEN 00                                                         
                     ADD     1           TO   WS-K820-READ                      
                     PERFORM 3200-WRITE-P13F820O  THRU 3200-EXIT                
                WHEN 23                                                         
                     ADD     1           TO   WS-K820-NOTFND                    
                     PERFORM 3100-WRITE-P13F820E  THRU 3100-EXIT                
                WHEN OTHER                                                      
                     DISPLAY 'ERROR IN READING -  BP13K820'                     
                     PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT              
            END-EVALUATE.                                                       
                                                                                
           PERFORM 2000-READ-BP13F820      THRU   2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3100-WRITE-P13F820E.                                                     
      *---------------------*                                                   
                                                                                
            WRITE P13F820E-REC FROM BP13F820-REC                                
            ADD 1                TO   WS-F820-ERROR.                            
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3200-WRITE-P13F820O.                                                     
      *---------------------*                                                   
                                                                                
            MOVE K820-NME-OCCP   TO   F820-NME-SPOUSE                           
            WRITE P13F820O-REC FROM BP13F820-REC                                
            ADD 1                TO   WS-F820-WRITE.                            
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       9000-CLOSE-ROUTINE.                                                      
      *-------------------*                                                     
                                                                                
           CLOSE P13F820O                                                       
                 P13F820E                                                       
                 BP13K820.                                                      
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY ' CLOSING BP13K820 ERROR ' WS-K820-STATUS                 
              MOVE WS-K820-STATUS TO RETURN-CODE.                               
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY ' BP13C943 CONTROL LIST'.                                    
           DISPLAY ' -------------------------------------'.                    
           DISPLAY ' NO OF REC READ    IN  BP13K820= '                          
                                                  WS-K820-READ.                 
           DISPLAY ' NO OF REC READ    IN  BP13F820= '                          
                                                  WS-F820-READ.                 
           DISPLAY ' NO OF REC WRITE   TO  P13F820O= '                          
                                                  WS-F820-WRITE.                
           DISPLAY ' NO OF REC WRITE   TO  P13F820E= '                          
                                                  WS-F820-ERROR.                
           DISPLAY ' NO OF REC NOT FOUND IN K820   = '                          
                                                  WS-K820-NOTFND.               
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
