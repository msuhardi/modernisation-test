       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C42B.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  28 AUG 2013.                                              
      * ======================================================= *               
      *                                                         *               
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                   *               
      *    SYSTEM ID : BP13                                     *               
      *                                                         *               
      *    OBJECTIVE  :                                         *               
      *        1.  TO MATCH RECORDS IN BP13FD03 WITH FIS        *               
      *            BM06K110                                     *               
      *                                                         *               
      *CHG REF   BY   DATE    DESCRIPTION                       *               
      *--------  ---  ------  -----------                       *               
      *BP135027  ESA1 280813  NEW PROGRAM(PATTERN AFTER BP13C423)               
      *BP137200  FNP1 080318  ADD WRITE TO BP13F915             *               
      *BP137326  FNP1 061218  BLANK OFF OTHER DETAILS WHEN CREATING             
      *                       'TE' FOR F915                                     
      * ======================================================= *               
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD03 ASSIGN       TO BP13FD03.                            
                                                                                
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
           SELECT BM06F110 ASSIGN       TO BM06F110.                            
                                                                                
           SELECT BP13F410 ASSIGN       TO BP13F410.                            
                                                                                
           SELECT BM06K110 ASSIGN       TO BM06K110                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K110-KEY-FLD                         
                           ALTERNATE KEY   IS K110-REGN-NO                      
                           FILE STATUS  IS BM06K110-STATUS.                     
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS BP13K800-STATUS.                     
                                                                                
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS BP13K820-STATUS.                     
                                                                                
           SELECT BP13F915 ASSIGN       TO BP13F915.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13FD03           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 1600 CHARACTERS                   
                              LABEL RECORDS ARE STANDARD                        
                              RECORDING MODE IS F.                              
       COPY BP13FD03.                                                           
                                                                                
                                                                                
       FD  BM06F110           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 500 CHARACTERS                    
                              LABEL RECORDS ARE STANDARD                        
                              RECORDING MODE IS F.                              
       COPY BM06F110.                                                           
                                                                                
       FD  BP13F410           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 2000 CHARACTERS                   
                              LABEL RECORDS ARE STANDARD                        
                              RECORDING MODE IS F.                              
       COPY BP13F410.                                                           
                                                                                
       FD  BP13F915           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS  800 CHARACTERS                   
                              LABEL RECORDS ARE STANDARD                        
                              RECORDING MODE IS F.                              
       COPY BP13F915.                                                           
                                                                                
       FD  BM06K110           RECORD CONTAINS 500 CHARACTERS.                   
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K800           RECORD CONTAINS 2000 CHARACTERS.                  
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820           RECORD CONTAINS  400 CHARACTERS.                  
       COPY BP13K820.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-DTE-SY02F001          PIC X(8)  VALUE SPACES.                     
       01  WS-DTE-F001 REDEFINES WS-DTE-SY02F001.                               
           05  WS-DTE-F001-CCYY     PIC 9(4).                                   
           05  WS-DTE-F001-MM       PIC 9(2).                                   
           05  WS-DTE-F001-DD       PIC 9(2).                                   
       01  WS-EOF-F001              PIC X      VALUE 'N'.                       
       01  WS-EOF-FD03              PIC X      VALUE 'N'.                       
       01  WS-EOF-K110              PIC X      VALUE 'N'.                       
       01  WS-K110-NOKEY            PIC X      VALUE 'N'.                       
       01  WS-K110-2-TAG            PIC X      VALUE 'N'.                       
       01  WS-NUM-SCH-ACC           PIC X(9)   VALUE SPACES.                    
       01  WS-F110-REC              PIC X(500) VALUE SPACES.                    
                                                                                
       01  WS-COUNTS.                                                           
           05  WS-FD03-READ-CNT     PIC 9(6)  VALUE ZEROS.                      
           05  WS-F110-WRITE-CNT    PIC 9(6)  VALUE ZEROS.                      
           05  WS-F410-WRITE-CNT    PIC 9(6)  VALUE ZEROS.                      
           05  WS-F915-WRITE-CNT    PIC 9(6)  VALUE ZEROS.                      
           05  WS-K110-FND-CNT      PIC 9(6)  VALUE ZEROS.                      
           05  WS-K110-NTFND-CNT    PIC 9(6)  VALUE ZEROS.                      
           05  WS-K800-FND-CNT      PIC 9(6)  VALUE ZEROS.                      
           05  WS-K800-NTFND-CNT    PIC 9(6)  VALUE ZEROS.                      
           05  WS-K800-UPD-CNT      PIC 9(6)  VALUE ZEROS.                      
           05  WS-K800-FND-WSCH     PIC 9(6)  VALUE ZEROS.                      
           05  WS-K800-FND-WOSCH    PIC 9(6)  VALUE ZEROS.                      
           05  WS-FD03-DTE-BYPASS   PIC 9(6)  VALUE ZEROS.                      
           05  WS-CNT-KEY-ISSUED    PIC 9(6)  VALUE ZEROS.                      
           05  WS-K820-EOF          PIC X(01) VALUE 'N'.                        
           05  WS-K820-NTFND        PIC X(01) VALUE 'N'.                        
           05  WS-REC-PROCESS       PIC X(01) VALUE 'N'.                        
           05  WS-K820-FND          PIC 9(06) VALUE ZEROES.                     
           05  WS-K820-NOTFND       PIC 9(06) VALUE ZEROES.                     
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BM06K110-STATUS      PIC 9(2)  VALUE ZEROS.                      
           05  BP13K800-STATUS      PIC 9(2)  VALUE ZEROS.                      
           05  BP13K820-STATUS      PIC 9(2)  VALUE ZEROS.                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *********************                                                     
       0000-MAIN.                                                               
      *********************                                                     
                                                                                
           PERFORM 1000-START-PROCESS  THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13FD03  THRU 2000-EXIT.                          
           PERFORM 3000-UPDATE-PROCESS THRU 3000-EXIT                           
                   UNTIL WS-EOF-FD03 = 'Y'.                                     
           PERFORM 9999-END-PROCESS    THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *********************                                                     
       1000-START-PROCESS.                                                      
      *********************                                                     
                                                                                
           OPEN INPUT BP13FD03                                                  
                      SY02F001                                                  
                      BM06K110                                                  
                      BP13K820                                                  
                I-O   BP13K800                                                  
               OUTPUT BM06F110                                                  
                      BP13F915                                                  
                      BP13F410.                                                 
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR K110-STATUS IS ' BM06K110-STATUS           
              PERFORM 9999-END-PROCESS THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR K800-STATUS IS ' BP13K800-STATUS           
              PERFORM 9999-END-PROCESS THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K820-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR K820-STATUS IS ' BP13K820-STATUS           
              PERFORM 9999-END-PROCESS THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-EOF-F001.                       
                                                                                
           IF WS-EOF-F001 = 'Y'                                                 
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              GO TO 9999-END-PROCESS                                            
           END-IF.                                                              
                                                                                
           MOVE F001-DTE-CURRENT   TO  WS-DTE-F001.                             
                                                                                
           IF WS-DTE-F001 IS NUMERIC AND                                        
              WS-DTE-F001 > ZEROES                                              
              SUBTRACT 2 FROM WS-DTE-F001-MM                                    
              IF WS-DTE-F001-MM   <= ZEROES                                     
                 SUBTRACT 1 FROM WS-DTE-F001-CCYY                               
                 COMPUTE WS-DTE-F001-MM = WS-DTE-F001-MM + 12                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13FD03.                                                      
      *********************                                                     
                                                                                
           READ BP13FD03 AT END                                                 
                         MOVE 'Y' TO WS-EOF-FD03                                
                         GO TO 2000-EXIT.                                       
                                                                                
           ADD 1 TO WS-FD03-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       3000-UPDATE-PROCESS.                                                     
      *********************                                                     
                                                                                
           IF FD03-DTE-REPUDIATE > ZEROES                                       
              IF FD03-DTE-REPUDIATE(1:6) <= WS-DTE-F001(1:6)                    
                 PERFORM 4000-READ-BM06K110    THRU 4000-EXIT                   
                 PERFORM 5000-READ-BP13K800    THRU 5000-EXIT                   
              ELSE                                                              
                 ADD 1     TO  WS-FD03-DTE-BYPASS                               
              END-IF                                                            
           ELSE                                                                 
              IF FD03-DTE-EFF-TRANSFER > ZEROES                                 
                 IF FD03-DTE-EFF-TRANSFER(1:6) <= WS-DTE-F001(1:6)              
                    PERFORM 4000-READ-BM06K110    THRU 4000-EXIT                
                    PERFORM 5000-READ-BP13K800    THRU 5000-EXIT                
                 ELSE                                                           
                    ADD 1  TO  WS-FD03-DTE-BYPASS                               
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY 'NO DATE DTE-REPUDIATE & DTE-EFF-TRANSFER: '           
                          FD03-NUM-REGN                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD03          THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************                                                  
       4000-READ-BM06K110.                                                      
      ************************                                                  
           MOVE 'N'                       TO WS-K110-NOKEY.                     
           MOVE SPACES                    TO WS-F110-REC.                       
           MOVE FD03-NUM-REGN             TO K110-REGN-NO.                      
           READ BM06K110 KEY IS K110-REGN-NO.                                   
           IF BM06K110-STATUS = 00 OR 02                                        
              ADD 1 TO WS-K110-FND-CNT                                          
              IF K110-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR                  
                                       ZEROES                                   
                 MOVE K110-REC    TO  WS-F110-REC                               
                 MOVE 'Y'     TO  WS-K110-NOKEY                                 
                 MOVE K110-SCH-ACC-NO        TO WS-NUM-SCH-ACC                  
                 PERFORM 6000-START-BM06K110 THRU 6000-EXIT                     
                 IF WS-K110-2-TAG = 'Y'                                         
                    MOVE SPACES   TO F110-REC                                   
                    WRITE F110-REC FROM WS-F110-REC                             
                    ADD 1 TO WS-F110-WRITE-CNT                                  
                 ELSE                                                           
                    DISPLAY 'NO K110-TAG 2: '  K110-SCH-ACC-NO                  
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF BM06K110-STATUS = 23                                           
                 ADD 1 TO WS-K110-NTFND-CNT                                     
              ELSE                                                              
                DISPLAY 'READ ERROR, K110-STATUS ' BM06K110-STATUS              
                        ',  KEY-FLD = ' K110-KEY-FLD                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       5000-READ-BP13K800.                                                      
      *********************                                                     
                                                                                
           MOVE FD03-NUM-REGN  TO K800-NUM-REGN.                                
           READ BP13K800.                                                       
                                                                                
           IF BP13K800-STATUS = ZEROS OR 02                                     
              ADD 1 TO WS-K800-FND-CNT                                          
              IF WS-K110-NOKEY = 'Y'                                            
                 IF K800-AMT-BK-PAID   = ZEROES AND                             
                    K800-AMT-SOC-CASH  = ZEROES AND                             
                    K800-AMT-CPF1-PAID = ZEROES AND                             
                    K800-AMT-CPF2-PAID = ZEROES AND                             
                    K800-AMT-CPF3-PAID = ZEROES AND                             
                    K800-AMT-CPF4-PAID = ZEROES AND                             
                    K800-AMT-CD        = ZEROES                                 
                    PERFORM 5100-REWRITE-BP13K800 THRU 5100-EXIT                
                    MOVE SPACES          TO BP13F410-REC                        
                    MOVE FD03-NUM-REGN   TO F410-NUM-REGN                       
                    WRITE BP13F410-REC                                          
                    ADD 1 TO WS-F410-WRITE-CNT                                  
                 ELSE                                                           
                    DISPLAY 'PRICE > ZEROES REGN: '  K800-NUM-REGN              
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY 'SCH-ACC: ' K110-SCH-ACC-NO                            
                         '   REGN-NO KEY ISSUED: ' FD03-NUM-REGN                
                 ADD 1    TO WS-CNT-KEY-ISSUED                                  
              END-IF                                                            
              IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                        
                 ADD 1    TO  WS-K800-FND-WOSCH                                 
              ELSE                                                              
                 ADD 1    TO  WS-K800-FND-WSCH                                  
                 DISPLAY 'RECORD FND IN K800 W/ KEY: ' K800-NUM-REGN            
                         '   SCH-ACC: ' K800-NUM-SCH-ACC                        
              END-IF                                                            
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 ADD 1 TO WS-K800-NTFND-CNT                                     
              ELSE                                                              
                 DISPLAY 'READ ERROR, BP13K800 STATUS = '                       
                 BP13K800-STATUS                                                
                 MOVE BP13K800-STATUS TO RETURN-CODE                            
                 PERFORM 9999-END-PROCESS THRU 9999-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************                                                  
       5100-REWRITE-BP13K800.                                                   
      ************************                                                  
            DISPLAY ' CANCEL SOC: ' K800-NUM-REGN                               
                    ' SCH-' K800-NUM-SCH-ACC.                                   
            MOVE 'C'               TO K800-NUM-STATUS.                          
            MOVE SPACES            TO K800-NUM-SCH-ACC.                         
            MOVE 'J2'              TO K800-NUM-CANCEL-REASON.                   
            MOVE F001-DTE-CURRENT  TO K800-DTE-CANCEL                           
            MOVE 'BP13C42B'        TO K800-NUM-USERID.                          
            MOVE F001-DTE-CURRENT  TO K800-DTE-UPDATE.                          
                                                                                
            REWRITE BP13K800-MASTER.                                            
                                                                                
            EVALUATE BP13K800-STATUS                                            
               WHEN 00                                                          
               WHEN 02                                                          
                    ADD 1    TO WS-K800-UPD-CNT                                 
                    PERFORM 7000-PROCESS-CDB  THRU 7000-EXIT                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR UPDATING BP13K800. STATUS IS '               
                            BP13K800-STATUS                                     
                    PERFORM 9999-END-PROCESS    THRU 9999-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************                                                  
       6000-START-BM06K110.                                                     
      ************************                                                  
                                                                                
           MOVE 'N'                        TO WS-EOF-K110                       
                                              WS-K110-2-TAG.                    
                                                                                
           INITIALIZE K110-REC.                                                 
                                                                                
           MOVE WS-NUM-SCH-ACC             TO K110-KEY-FLD.                     
           DISPLAY '----------------- '.                                        
           START BM06K110 KEY >= K110-KEY-FLD.                                  
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                  PERFORM 6100-READNEXT-BM06K110   THRU 6100-EXIT               
                    UNTIL WS-EOF-K110 = 'Y'                                     
             WHEN 10                                                            
             WHEN 23                                                            
                  CONTINUE                                                      
             WHEN OTHER                                                         
                  DISPLAY 'ERROR STARTING BM06K110. STATUS IS '                 
                           BM06K110-STATUS                                      
                  PERFORM 9999-END-PROCESS    THRU 9999-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************                                                  
       6100-READNEXT-BM06K110.                                                  
      ************************                                                  
                                                                                
           READ BM06K110 NEXT RECORD                                            
                AT END                                                          
                   MOVE 'Y'  TO  WS-EOF-K110                                    
           END-READ.                                                            
                                                                                
           EVALUATE BM06K110-STATUS                                             
            WHEN 00                                                             
            WHEN 02                                                             
                 IF K110-SCH-ACC-NO(1:9) = WS-NUM-SCH-ACC                       
                    DISPLAY ' FIS: ' K110-SCH-ACC-NO                            
                            ' REG: ' K110-REGN-NO                               
                    IF K110-SCH-ACC-NO(10:1) = '2'                              
                       MOVE 'Y'            TO WS-K110-2-TAG                     
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'Y'       TO    WS-EOF-K110                            
                 END-IF                                                         
            WHEN 10                                                             
            WHEN 23                                                             
                 MOVE 'Y'          TO    WS-EOF-K110                            
            WHEN OTHER                                                          
                 DISPLAY 'ERROR READNEXT BM06K110. STATUS IS '                  
                          BM06K110-STATUS                                       
                 PERFORM 9999-END-PROCESS    THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       7000-PROCESS-CDB.                                                        
      *************************************************************             
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           MOVE 'N'                      TO WS-K820-EOF                         
                                            WS-K820-NTFND                       
                                            WS-REC-PROCESS.                     
                                                                                
           MOVE K800-NUM-REGN            TO K820-NUM-REGN.                      
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K820-STATUS                                             
               WHEN 00                                                          
                    PERFORM 7100-READNEXT-BP13K820 THRU 7100-EXIT               
                      UNTIL K820-NUM-REGN NOT = K800-NUM-REGN                   
                         OR WS-K820-EOF = 'Y'                                   
                         OR WS-K820-NTFND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K820-NOTFND                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 START BROWSE ERROR : '                    
                           BP13K820-STATUS                                      
                    MOVE BP13K820-STATUS TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-END-PROCESS   THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       7100-READNEXT-BP13K820.                                                  
      *************************************************************             
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           READ BP13K820 NEXT RECORD.                                           
                                                                                
           EVALUATE BP13K820-STATUS                                             
               WHEN 00                                                          
                    IF K820-NUM-REGN = K800-NUM-REGN                            
                       ADD 1             TO WS-K820-FND                         
                       MOVE 'Y'          TO WS-REC-PROCESS                      
                                                                                
                       PERFORM 7200-WRITE-BP13F915 THRU 7200-EXIT               
                    ELSE                                                        
                       IF WS-REC-PROCESS = 'N'                                  
                          ADD 1          TO WS-K820-NOTFND                      
                       END-IF                                                   
                                                                                
                       MOVE 'Y'          TO WS-K820-NTFND                       
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K820-NOTFND                      
                    MOVE 'Y'             TO WS-K820-NTFND                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 READ NEXT ERROR : '                       
                                BP13K820-STATUS                                 
                    MOVE BP13K820-STATUS TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-END-PROCESS THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       7100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       7200-WRITE-BP13F915.                                                     
      *************************************************************             
           IF K820-NUM-CSTMR-SOURCE NOT = SPACES AND LOW-VALUES                 
              MOVE SPACES                   TO BP13F915-REC                     
              INITIALIZE                       BP13F915-REC                     
                                                                                
              MOVE  'TE'                    TO F915-TXN-TYPE                    
              MOVE  'P'                     TO F915-CSTMR-TYPE                  
              MOVE  'BP13'                  TO F915-NUM-UPDATE-SOURCE           
              MOVE  'BP13C42B'              TO F915-NUM-UPDATE-USERID           
              MOVE  K820-NUM-CSTMR-SOURCE   TO F915-NUM-CSTMR-SOURCE            
                                                                                
              IF K820-NUM-NRIC = K800-NUM-NRIC1                                 
                 MOVE '011'                 TO F915-NUM-ROLE                    
              ELSE                                                              
                 IF (K820-NUM-NRIC = K800-NUM-NRIC2) OR                         
                    (K820-NUM-NRIC = K800-NUM-NRIC3) OR                         
                    (K820-NUM-NRIC = K800-NUM-NRIC4)                            
                     MOVE '012'             TO F915-NUM-ROLE                    
                 ELSE                                                           
                     MOVE '013'             TO F915-NUM-ROLE                    
                 END-IF                                                         
              END-IF                                                            
                                                                                
              MOVE  K820-NUM-REGN           TO F915-NUM-BSNS-REF                
                                                                                
              STRING K800-DTE-CANCEL(1:4) '-'                                   
                     K800-DTE-CANCEL(5:2) '-'                                   
                     K800-DTE-CANCEL(7:2)                                       
                     DELIMITED BY SIZE                                          
                INTO F915-DTE-ROLE-END                                          
                                                                                
              STRING F001-DTE-CURRENT(1:4) '-'                                  
                     F001-DTE-CURRENT(5:2) '-'                                  
                     F001-DTE-CURRENT(7:2)                                      
                     DELIMITED BY SIZE                                          
                INTO F915-DTE-UPDATE-SOURCE                                     
                                                                                
              WRITE BP13F915-REC                                                
                                                                                
              ADD 1                         TO WS-F915-WRITE-CNT                
                                                                                
           END-IF.                                                              
                                                                                
       7200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ****************************************************************          
       9999-END-PROCESS.                                                        
      ****************************************************************          
                                                                                
           DISPLAY ' BP13C42B CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13FD03 : ' WS-FD03-READ-CNT.        
           DISPLAY 'NO OF RECS BYPASS IN  BP13FD03 : '                          
                    WS-FD03-DTE-BYPASS.                                         
           DISPLAY 'NO OF RECS FOUND IN   BM06K110 : '                          
                    WS-K110-FND-CNT.                                            
           DISPLAY 'NO OF RECS NOT FOUND  BM06K110 : '                          
                    WS-K110-NTFND-CNT.                                          
           DISPLAY 'NO OF RECS WRITE   IN BM06F110 : '                          
                    WS-F110-WRITE-CNT.                                          
           DISPLAY 'NO OF RECS WRITE   IN BP13F410 : '                          
                    WS-F410-WRITE-CNT.                                          
           DISPLAY 'NO OF RECS WRITE   IN BP13F915 : '                          
                    WS-F915-WRITE-CNT.                                          
           DISPLAY 'NO OF RECS FOUND IN   BP13K800 : '                          
                    WS-K800-FND-CNT.                                            
           DISPLAY 'NO OF RECS NOT FOUND  BP13K800 : '                          
                    WS-K800-NTFND-CNT.                                          
           DISPLAY 'NO OF RECS UPDATED IN BP13K800 : '                          
                    WS-K800-UPD-CNT.                                            
           DISPLAY 'NO OF RECS W/O SCH IN BP13K800 : '                          
                    WS-K800-FND-WOSCH.                                          
           DISPLAY 'NO OF RECS W/  SCH IN BP13K800 : '                          
                    WS-K800-FND-WSCH.                                           
           DISPLAY 'NO OF RECS WITH KEY ISSUED DATE: '                          
                    WS-CNT-KEY-ISSUED.                                          
                                                                                
           CLOSE BP13FD03                                                       
                 BM06K110                                                       
                 BM06F110                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13F410                                                       
                 BP13F915.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
              DISPLAY 'CLOSING ERROR, K110-STATUS IS ' BM06K110-STATUS          
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'CLOSING ERROR, K800-STATUS IS ' BP13K800-STATUS          
           END-IF.                                                              
                                                                                
           IF BP13K820-STATUS NOT = 0 AND 97                                    
              DISPLAY 'CLOSING ERROR, K820-STATUS IS ' BP13K820-STATUS          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *======================== END OF PROGRAM ======================*          
