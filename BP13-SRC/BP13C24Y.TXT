       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C24Y.                                                 
      *AUTHOR.        KARL MAGALONA CABUG.                                      
      *DATE-WRITTEN.  16/09/14                                                  
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE   :  PROGRAM TO EXTRACT ALL MATCH K110-AIX1      *            
      *                                                            *            
      *  INPUT FILES :  1)  BP13F205                               *            
      *                 3)  BM06K110                               *            
      *                                                            *            
      *  OUTPUT FILE :  1)  BM06F110                               *            
      * ---------------------------------------------------------- *            
      * CHGE NO   OIC   DATE    DESCRIPTION                        *            
      * -------- ----- ------   ---------------------------------- *            
      * BP135377 KAM4  230914   NEW PROGRAM.                       *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN        TO  BP13F205.                         
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE RECORD KEY IS K110-AIX1                   
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT BM06F110  ASSIGN        TO  BM06F110.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80  CHARACTERS.                                     
       01 WS-F205TEMP-REC.                                                      
          05 WS-F205-EST                  PIC X(03).                            
          05 WS-F205-BLK-NO               PIC X(05).                            
          05 FILLER                       PIC X(05).                            
          05 WS-F205-OPT                  PIC X(02).                            
          05 WS-F205-NEIGH                PIC X(04).                            
          05 WS-F205-CONTR                PIC X(04).                            
          05 FILLER                       PIC X(07).                            
          05 WS-F205-DTE-BLDG-CMPLT       PIC X(08).                            
          05 FILLER                       PIC X(42).                            
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BM06F110                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06F110.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-F205                   PIC X     VALUE 'N'.               
           05  WS-EOF-K110                   PIC X     VALUE 'N'.               
           05  WS-K110-STATUS                PIC 99    VALUE  00.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-TOTAL-CNT             PIC 9(5)  VALUE ZEROS.             
           05  WS-READ-NOTFND                PIC 9(5)  VALUE ZEROS.             
           05  WS-START-READ                 PIC 9(5)  VALUE ZEROS.             
           05  WS-F110-WRITE                 PIC 9(5)  VALUE ZEROS.             
                                                                                
       01 OTHER-VARIABLES.                                                      
           05 WS-K110-AIX1                   PIC X(16) VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      **************************************************************            
       000-MAIN-ROUTINE.                                                        
      **************************************************************            
           PERFORM 100-OPEN-FILES            THRU 100-EXIT.                     
           PERFORM 200-READ-BP13F205         THRU 200-EXIT.                     
           PERFORM 300-PROCESS-RECORD        THRU 300-EXIT                      
                   UNTIL WS-EOF-F205 = 'Y'.                                     
                                                                                
           PERFORM  9999-CLOSE-ROUTINE       THRU 9999-EXIT.                    
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *------------------------------------------------------------*            
           OPEN INPUT  BP13F205                                                 
                       BM06K110                                                 
               OUTPUT  BM06F110.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - OPEN ERROR :' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS           TO RETURN-CODE                  
              PERFORM  9999-CLOSE-ROUTINE     THRU   9999-EXIT                  
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F205.                                                       
      *-------------------------------------------------------------            
           READ BP13F205                                                        
                AT END MOVE 'Y'                 TO WS-EOF-F205                  
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1                                TO WS-READ-TOTAL-CNT.           
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
      *------------------------------------------------------------*            
           MOVE SPACES                          TO K110-AIX1.                   
           MOVE WS-F205-EST                     TO K110-ESTATE.                 
           MOVE WS-F205-NEIGH                   TO K110-NEIGHBOURHOOD.          
           MOVE WS-F205-CONTR                   TO K110-CONTRACT-NO.            
           MOVE WS-F205-BLK-NO                  TO K110-BLK-NO.                 
                                                                                
           MOVE K110-AIX1                       TO K110-KEY-FLD.                
           PERFORM 400-READ-BM06K110          THRU 400-EXIT.                    
                                                                                
           PERFORM 200-READ-BP13F205          THRU 200-EXIT.                    
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-READ-BM06K110.                                                       
      *------------------------------------------------------------*            
           DISPLAY '@BM06K110 READ...'                                          
           MOVE  K110-AIX1                      TO WS-K110-AIX1.                
           MOVE ZEROES                          TO WS-START-READ.               
                                                                                
           START BM06K110 KEY >= K110-AIX1                                      
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    PERFORM 410-READNEXT-BM06K110   THRU 410-EXIT               
                      UNTIL K110-AIX1 NOT = WS-K110-AIX1                        
                                                                                
                    IF WS-START-READ = ZEROES                                   
                       ADD 1                      TO WS-READ-NOTFND             
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'KEY NOTFND (BM06K110) : ' K110-AIX1                
                    ADD    1                      TO WS-READ-NOTFND             
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 - ERROR STARTR : ' WS-K110-STATUS         
                    PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                  
           END-EVALUATE.                                                        
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-READNEXT-BM06K110.                                                   
      *------------------------------------------------------------*            
           READ BM06K110 NEXT RECORD                                            
                AT END                                                          
                   MOVE 'Y'                       TO WS-EOF-K110                
                   MOVE HIGH-VALUES               TO K110-AIX1                  
           END-READ.                                                            
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    DISPLAY '   '                                               
                    DISPLAY '++++++++@READNXT BM06K110++++++++'                 
                    DISPLAY 'WS-K110-AIX1 : ' WS-K110-AIX1                      
                    DISPLAY 'K110-AIX1    : ' K110-AIX1                         
                    DISPLAY 'K110-SCH-ACC-NO :' K110-SCH-ACC-NO                 
                                                                                
                    IF K110-AIX1 = WS-K110-AIX1                                 
                       DISPLAY '***** WRITE BM06F110 *****'                     
                       ADD  1                     TO WS-START-READ              
                       MOVE SPACES                TO F110-REC                   
                       MOVE K110-REC              TO F110-REC                   
                                                                                
                       WRITE F110-REC                                           
                                                                                
                       ADD  1                     TO WS-F110-WRITE              
                    END-IF                                                      
                                                                                
               WHEN 23                                                          
                DISPLAY 'RECORD NOT FOUND IN K110:' WS-K110-AIX1                
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110- READFILE ERROR:' WS-K110-STATUS          
                    PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                  
           END-EVALUATE.                                                        
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------*            
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C24Y SUMMARY --------------*'.             
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BP13F205 (READ)      = ' WS-READ-TOTAL-CNT.           
           DISPLAY 'NO OF BM06K110 (NOTFND)    = ' WS-READ-NOTFND.              
           DISPLAY 'NO OF BM06F110 (WRITE)     = ' WS-F110-WRITE.               
                                                                                
           CLOSE BP13F205                                                       
                 BM06K110                                                       
                 BM06F110.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BM06K110 CLOSE ERROR: ' WS-K110-STATUS                    
             MOVE  WS-K110-STATUS      TO       RETURN-CODE.                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
