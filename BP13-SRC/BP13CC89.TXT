       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC89.                                                 
      *AUTHOR.        EDEN ROSE M DINGAL.                                       
      *DATE-WRITTEN.  09/01/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *                                                            *            
      *    INFORM DEVELOPER ON THE DISBURSEMENT OF 1ST LOAN        *            
      *    FOR DBSS - USING CPF                                    *            
      *                                                            *            
      *  NOTE : THIS PROGRAM WILL BE RUN AFTER 8AM EACH DAY;       *            
      *         WHERE SYSTEM DATE IS SET TO TODAY'S DATE           *            
      *                                                            *            
      *  INPUT     :  1. BP13FD38                                  *            
      *               2. SY02F001                                  *            
      *  OUTPUT    :  1. BP13MAIL                                  *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP133051 20070427 SD10  NEW PROGRAM                        *            
      * BP133052 20070524 SD10  PRINT BODY FOOTER ONLY IF REC > 0  *            
      * BP133305 20080605 ED1   PRINT IF DTE-CREATE = DTE-PREV     *            
      * BP133509 20090115 DW5   REMOVE WLY1 ADD "JL47" , "CYM11"   *            
      * BP134269 20120620 IMC1  REMOVE LKC13 ADD "LSC13"           *            
      * BP134571 20120712 IMC1  REMOVE LSC13 ADD SU1 LKH2          *            
      * BP134919 20130516 JGO1  CHANGE TWK2 TO LFK1                *            
      * BP136258 20160607 PCL4  REMOVE JL47 & SU1                  *            
      * BP136477 20161018 RJB1  REMOVE LIM KIM HUAT                *            
      * BP137331 20180523 PCL4  TO UPDATE EMAIL RECIPIENTS         *            
      * BP137668 20190823 ZDD   To remove Watt Kee TEO (HDB) and                
      *                         replace  with IVY M BALANAG(HDB)                
      * BP139030 20220107 ZAR7  REPLACE CHUA YEE MIEN TO LIM KOK CHUN           
      * BP139276 20240405 kac1  REPLACEING LIM_Kok_Chun by         *            
      *                                    KATHIJAH_BEE_ALI_MOHAMED*            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13FD38 ASSIGN       TO BP13FD38.                            
           SELECT BP13MAIL ASSIGN       TO BP13MAIL.                            
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
           SELECT BP13KD35  ASSIGN        TO BP13KD35                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD35-KEY-FLD                       
                            FILE STATUS   IS WS-KD35-STATUS.                    
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13FD38                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13FD38.                                                           
                                                                                
       FD   BP13KD35                                                            
            RECORD CONTAINS 3500 CHARACTERS.                                    
       COPY BP13KD35.                                                           
                                                                                
       FD  BP13MAIL                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE  IS F.                                               
       01  MAIL-PRTREC                       PIC X(80).                         
                                                                                
       COPY SY02F001.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-EOF-BP13FD38          PIC X      VALUE SPACES.                    
       01  WS-FD38-EMPTY            PIC X      VALUE SPACES.                    
                                                                                
       01  WS-SNO                   PIC 9(4)   VALUE ZEROES.                    
       01  WS-FD38-READ             PIC 9(4)   VALUE ZEROES.                    
       01  WS-BYPASS-RECORD         PIC 9(4)   VALUE ZEROES.                    
       01  WS-BYPASS-NO-CHEQUE      PIC 9(4)   VALUE ZEROES.                    
       01  WS-KD35-STATUS           PIC 9(2)   VALUE 00.                        
                                                                                
       01  WS-PREV-DATE.                                                        
           05  WS-DATE              PIC X(8)   VALUE SPACES.                    
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-TOMAILID3  PIC X(50)  VALUE                                 
               'KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                          
           05  FILLER          PIC X(10)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1C.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-TOMAILID4  PIC X(50)  VALUE                                 
               'IVY_M_BALANAG@hdb.gov.sg'.                                      
           05  FILLER          PIC X(10)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1E.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-TOMAILID4  PIC X(50)  VALUE                                 
               'CHONG_YENG_MUI@hdb.gov.sg>'.                                    
           05  FILLER          PIC X(19)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5B.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAIL-TOMAILID3  PIC X(50)  VALUE                                 
               'KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                          
           05  FILLER          PIC X(10)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5C.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAIL-TOMAILID4  PIC X(50)  VALUE                                 
               'IVY_M_BALANAG@hdb.gov.sg'.                                      
           05  FILLER          PIC X(10)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5E.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAIL-TOMAILID4  PIC X(50)  VALUE                                 
               'CHONG_YENG_MUI@hdb.gov.sg>'.                                    
           05  FILLER          PIC X(10)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL7.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  FILLER          PIC X(16)  VALUE 'DISBURSEMENT OF '.             
           05  FILLER          PIC X(15)  VALUE 'FIRST LOAN FOR '.              
           05  FILLER          PIC X(22)  VALUE 'DBSS - CPF INSTALMENT'.        
                                                                                
       01  MAIL-DTL8.                                                           
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY01.                                                         
           05 FILLER                   PIC X(1)   VALUE SPACES.                 
           05 FILLER                   PIC X(50)  VALUE                         
              'S/NO   REGN NO.   SCHED    DUE DATE      AMOUNT'.                
                                                                                
       01  MAIL-BODY02.                                                         
           05 FILLER                   PIC X      VALUE SPACES.                 
           05 FILLER                   PIC X(49)  VALUE                         
              '----   --------   -----   ----------   ----------'.              
                                                                                
       01  MAIL-DETAIL.                                                         
           05 FILLER                   PIC X(1)   VALUE SPACES.                 
           05 MAIL-SNO                 PIC ZZZ9   VALUE ZEROS.                  
           05 FILLER                   PIC X(3)   VALUE SPACES.                 
           05 MAIL-NUM-REGN            PIC X(8)   VALUE SPACES.                 
           05 FILLER                   PIC X(4)   VALUE SPACES.                 
           05 MAIL-SCHED               PIC X(02)  VALUE SPACES.                 
           05 FILLER                   PIC X(5)   VALUE SPACES.                 
           05 MAIL-DUE-DATE            PIC X(10)  VALUE SPACES.                 
           05 FILLER                   PIC X(3)   VALUE SPACES.                 
           05 MAIL-AMOUNT              PIC Z,ZZZ,ZZ9.99.                        
           05 FILLER                   PIC X(38)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY03.                                                         
           05 FILLER                   PIC X(40)  VALUE                         
              'TOTAL NUMBER OF DBSS - CPF INSTALMENT : '.                       
           05 MAIL-CASES               PIC ZZZ9   VALUE ZEROS.                  
           05 FILLER                   PIC X(26)  VALUE SPACES.                 
           05 FILLER                   PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE         THRU  1000-EXIT.                  
            PERFORM 3000-PROCESS-DETAIL       THRU  3000-EXIT                   
                    UNTIL WS-EOF-BP13FD38 = 'Y'.                                
            PERFORM 4200-PRINT-EMAIL-FOOTER   THRU  4200-EXIT.                  
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN INPUT  BP13FD38                                                
                        BP13KD35                                                
                        SY02F001                                                
                 OUTPUT BP13MAIL.                                               
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-PREV      TO WS-PREV-DATE.                             
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                   DELIMITED BY SIZE INTO MAIL-DATE.                            
           MOVE ZEROES             TO WS-FD38-READ                              
                                      WS-SNO.                                   
                                                                                
      *-----------------------------                                            
      *  INITIAL READ OF INPUT FILES                                            
      *-----------------------------                                            
           READ BP13FD38                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-EOF-BP13FD38                                  
                   MOVE 'Y' TO WS-FD38-EMPTY                                    
                   DISPLAY ' *** '                                              
                   DISPLAY ' *** NO RECORDS FOUND IN PPTRANS FILE'              
                   DISPLAY ' *** '                                              
                GO TO  1000-EXIT.                                               
           ADD 1 TO WS-FD38-READ.                                               
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-READ-BP13FD38.                                                      
      *-------------------------------------------------------------            
           READ BP13FD38                                                        
                AT END MOVE 'Y' TO WS-EOF-BP13FD38                              
                GO TO  2000-EXIT.                                               
                                                                                
           ADD 1 TO WS-FD38-READ.                                               
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
           PERFORM 3100-READ-BP13KD35     THRU 3100-EXIT.                       
           PERFORM 3200-INITIALIZE-FIELDS THRU 3200-EXIT.                       
                                                                                
           MOVE SPACES TO MAIL-DETAIL.                                          
                                                                                
           IF FD38-DTE-CREATE = F001-DTE-PREV    AND                            
              (KD35-NUM-INSTAL-RATE-CPF1 >  0   OR                              
               KD35-NUM-INSTAL-RATE-CPF2 >  0   OR                              
               KD35-NUM-INSTAL-RATE-CPF3 >  0   OR                              
               KD35-NUM-INSTAL-RATE-CPF4 >  0)                                  
                                                                                
              PERFORM 4000-PRINT-DETAIL THRU 4000-EXIT                          
                                                                                
           ELSE                                                                 
              ADD 1       TO WS-BYPASS-RECORD                                   
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD38    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3100-READ-BP13KD35.                                                      
      *----------------------------------------------------------------*        
           MOVE FD38-NUM-REGN TO KD35-NUM-REGN.                                 
                                                                                
           READ BP13KD35                                                        
           EVALUATE WS-KD35-STATUS                                              
              WHEN 00                                                           
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD35 - STATUS '                  
                             WS-KD35-STATUS                                     
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3200-INITIALIZE-FIELDS.                                                  
      *-------------------------------------------------------------            
           IF FD38-AMT-LOAN-NEW        IS NOT NUMERIC                           
              MOVE 0 TO FD38-AMT-LOAN-NEW                                       
           END-IF.                                                              
           IF KD35-NUM-INSTAL-RATE-CPF1 IS NOT NUMERIC                          
              MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF1                               
           END-IF.                                                              
           IF KD35-NUM-INSTAL-RATE-CPF2 IS NOT NUMERIC                          
              MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF2                               
           END-IF.                                                              
           IF KD35-NUM-INSTAL-RATE-CPF3 IS NOT NUMERIC                          
              MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF3                               
           END-IF.                                                              
           IF KD35-NUM-INSTAL-RATE-CPF4 IS NOT NUMERIC                          
              MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF4                               
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       4000-PRINT-DETAIL.                                                       
      *-------------------------------------------------------------            
                                                                                
           ADD 1                            TO WS-SNO.                          
                                                                                
           IF WS-SNO = 1                                                        
              PERFORM 4100-PRINT-EMAIL-HEADER THRU 4100-EXIT                    
           END-IF.                                                              
                                                                                
           MOVE WS-SNO                      TO MAIL-SNO.                        
           MOVE FD38-NUM-REGN               TO MAIL-NUM-REGN.                   
           MOVE FD38-NUM-SCHEDULE           TO MAIL-SCHED.                      
           MOVE FD38-AMT-LOAN-NEW           TO MAIL-AMOUNT.                     
                                                                                
           IF FD38-DTE-DUE NOT = SPACES AND LOW-VALUES AND ZEROS                
              STRING FD38-DTE-DUE(7:2) '/'                                      
                     FD38-DTE-DUE(5:2) '/'                                      
                     FD38-DTE-DUE(1:4)                                          
              DELIMITED BY SIZE INTO MAIL-DUE-DATE                              
           ELSE                                                                 
              MOVE SPACES TO MAIL-DUE-DATE                                      
           END-IF.                                                              
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DETAIL.                                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4100-PRINT-EMAIL-HEADER.                                                 
      *-------------------------------------------------------------            
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL1B.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL1C.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL1E.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL3.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL4.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL5B.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL5C.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL5E.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL7.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL8.                                    
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY01.                                  
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY02.                                  
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4200-PRINT-EMAIL-FOOTER.                                                 
      *-------------------------------------------------------------            
                                                                                
           IF WS-FD38-EMPTY = 'Y'                                               
              GO TO 4200-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-SNO = ZEROES                                                   
              DISPLAY ' *** '                                                   
              DISPLAY ' *** NO RECORDS IN BP13FD38 WITH '                       
              DISPLAY ' *** 1ST LOAN FOR DBSS - CPF INSTALMENT'                 
              DISPLAY ' *** '                                                   
              GO TO 4200-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-SNO       TO MAIL-CASES.                                     
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY03.                                  
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-END.                                     
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
            DISPLAY ' '.                                                        
            DISPLAY ' - BP13CC89 CONTROL TOTALS -----'.                         
            DISPLAY ' '.                                                        
            DISPLAY '  NO. OF BP13FD38 RECS READ     = ' WS-FD38-READ.          
            DISPLAY '  NO. OF RECS WRITTEN IN REPORT = ' WS-SNO.                
            DISPLAY '  NO. OF RECS BYPASSED: ' WS-BYPASS-RECORD.                
            DISPLAY ' '.                                                        
                                                                                
            CLOSE BP13FD38                                                      
                  BP13KD35                                                      
                  SY02F001                                                      
                  BP13MAIL.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
