       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C209.                                                 
      *AUTHOR.        GEORGE VARGHESE.                                          
      *DATE-WRITTEN.  09/04/99.                                                 
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *    OBJECTIVE  :                                        *                
      *            TO UPDATE REFLECT THE CHANGE IN ALLOCATION  *                
      *            DATE IN THE BP13F205 CTRL-REC ON BP13K278.  *                
      *                                                        *                
      *    INPUT    :  BP13F205                                *                
      *    I-O      :  BP13K278                                *                
      *    OUTPUT   :  BP13L209 (CONTROL LIST)                 *                
      *--------------------------------------------------------*                
      *  CHG-NO    BY    ON        DESCRIPTION                 *                
      *  -------------------------------------                 *                
      * BP130641   GV  09/04/1999  NEW PGM                     *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BP13K278 ASSIGN TO BP13K278                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K278-KEY-FLD                               
                  FILE STATUS     IS BP13K278-STATUS.                           
                                                                                
           SELECT BP13L209 ASSIGN TO BP13L209.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F205                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 80  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F205.                                                           
                                                                                
       FD  BP13K278                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K278.                                                           
                                                                                
       FD  BP13L209                                                             
           BLOCK CONTAINS  0   RECORDS                                          
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC-1              PIC X(132).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K278-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  K278-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K278-UPDATE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  WS-LINE-CNT              PIC 9(2)  VALUE 99.                         
       01  WS-PRINT-PAGE            PIC 9(3)  VALUE ZEROS.                      
                                                                                
       01  WS-CTRL-KEY.                                                         
           05 CTRL-OLD-DATE.                                                    
              10 WS-CC              PIC 9(02) VALUE ZEROS.                      
              10 WS-YY              PIC 9(02) VALUE ZEROS.                      
              10 WS-MM              PIC 9(02) VALUE ZEROS.                      
              10 WS-DD              PIC 9(02) VALUE ZEROS.                      
           05 CTRL-CDE-NT-ZONE      PIC X(03) VALUE SPACES.                     
                                                                                
       01  CTRL-NEW-DATE.                                                       
              05 WS-CC              PIC 9(02) VALUE ZEROS.                      
              05 WS-YY              PIC 9(02) VALUE ZEROS.                      
              05 WS-MM              PIC 9(02) VALUE ZEROS.                      
              05 WS-DD              PIC 9(02) VALUE ZEROS.                      
                                                                                
       01  WS-K100-KEY.                                                         
           05 WS-EST                PIC X(03) VALUE SPACES.                     
           05 WS-NEIGH              PIC X(04) VALUE SPACES.                     
           05 WS-CONTR              PIC X(04) VALUE SPACES.                     
           05 WS-BLK                PIC X(05) VALUE SPACES.                     
                                                                                
       01  EOF-FLAG.                                                            
           05 EOF-F205              PIC X     VALUE 'N'.                        
           05 EOF-K278              PIC X     VALUE 'N'.                        
                                                                                
       01  WS-DATE.                                                             
           05 WS-YY                 PIC 9(2).                                   
           05 WS-MM                 PIC 9(2).                                   
           05 WS-DD                 PIC 9(2).                                   
                                                                                
       01  WS-EDITED-DATE.                                                      
           05 WS-DD                 PIC 9(2)  VALUE ZEROS.                      
           05 FILLER                PIC X     VALUE '/'.                        
           05 WS-MM                 PIC 9(2)  VALUE ZEROS.                      
           05 FILLER                PIC X     VALUE '/'.                        
           05 WS-CC                 PIC 9(2)  VALUE ZEROS.                      
           05 WS-YY                 PIC 9(2)  VALUE ZEROS.                      
                                                                                
       01  WS-TIME.                                                             
           05 WS-HH                 PIC X(2).                                   
           05 WS-MM                 PIC X(2).                                   
           05 WS-SS                 PIC X(2).                                   
                                                                                
       01  WS-EDITED-TIME.                                                      
           05 WS-SYS-TIME           PIC X(6).                                   
           05 WS-MICRO-SEC          PIC 9(1)  VALUE 0.                          
                                                                                
       01  WS-CUR-CCYYMMDD.                                                     
           05  WS-CUR-CC            PIC 9(2)  VALUE 00.                         
           05  WS-CUR-YY            PIC 9(2)  VALUE 00.                         
           05  WS-CUR-MM            PIC 9(2)  VALUE 00.                         
           05  WS-CUR-DD            PIC 9(2)  VALUE 00.                         
                                                                                
       01  L209-LINE1.                                                          
           05 FILLER                PIC X(5)  VALUE SPACES.                     
           05 FILLER                PIC X(7)  VALUE 'PAGE : '.                  
           05 PRINT-PAGE            PIC ZZ9.                                    
           05 FILLER                PIC X(28) VALUE SPACES.                     
           05 FILLER                PIC X(24) VALUE                             
             'SYSTEM   OF   COMMITMENT'.                                        
           05 FILLER                PIC X(36) VALUE SPACES.                     
           05 FILLER                PIC X(07) VALUE 'DATE : '.                  
           05 PRINT-DATE.                                                       
              10 WS-DD              PIC 9(2).                                   
              10 FILLER             PIC X     VALUE '/'.                        
              10 WS-MM              PIC 9(2).                                   
              10 FILLER             PIC X     VALUE '/'.                        
              10 WS-CC              PIC 9(2).                                   
              10 WS-YY              PIC 9(2).                                   
           05 FILLER                PIC X(5)  VALUE SPACES.                     
                                                                                
       01  L209-LINE2.                                                          
           05 FILLER                PIC X(35) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
             'UPDATE OF REVISED ALLOCATION DATE IN OCS FILE'.                   
           05 FILLER                PIC X(23) VALUE SPACES.                     
           05 FILLER                PIC X(22) VALUE                             
             'LISTING-ID  : BP13L209'.                                          
           05 FILLER                PIC X(7)  VALUE SPACES.                     
                                                                                
       01  L209-LINE3.                                                          
           05 FILLER                PIC X(08) VALUE SPACES.                     
           05 FILLER                PIC X(22) VALUE                             
             'OLD ALLOCATION DATE : '.                                          
           05 L-OLD-DATE            PIC X(10)  VALUE SPACES.                    
           05 FILLER                PIC X(27)                                   
              VALUE '     NEW ALLOCATION DATE : '.                              
           05 L-NEW-DATE            PIC X(10)  VALUE SPACES.                    
           05 FILLER                PIC X(5)  VALUE SPACES.                     
           05 FILLER                PIC X(15) VALUE 'CODE-NT-ZONE : '.          
           05 L-CDE-NT              PIC X(3)   VALUE SPACES.                    
           05 FILLER                PIC X(29) VALUE SPACES.                     
                                                                                
       01  L209-LINE22.                                                         
           05 FILLER                PIC X(24) VALUE SPACES.                     
           05 FILLER                PIC X(10) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF OCS RECORDS READ                     : '.                     
           05 L-K278-READ-CNT       PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L209-LINE23.                                                         
           05 FILLER                PIC X(34) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF OCS RECORDS UPDATED                  : '.                     
           05 L-K278-UPDATE-CNT     PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-READ-CTRL-REC THRU 200-EXIT                              
                   UNTIL EOF-F205 = 'Y'.                                        
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                OUTPUT BP13L209                                                 
                I-O    BP13K278.                                                
                                                                                
           IF BP13K278-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BP13K278 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BP13K278-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-CTRL-REC.                                                       
      *-------------------------------------------------------------            
           MOVE ZEROS TO  K278-READ-CNT,     K278-UPDATE-CNT,                   
                          CTRL-OLD-DATE,     CTRL-NEW-DATE.                     
           MOVE SPACES TO CTRL-CDE-NT-ZONE,                                     
                          EOF-FLAG.                                             
                                                                                
           READ BP13F205 AT END                                                 
                MOVE 'Y' TO EOF-F205                                            
                GO TO 200-EXIT                                                  
                END-READ.                                                       
                                                                                
           MOVE SPACES              TO BP13K278-REC.                            
           MOVE F205-NUM-NT-ZONE    TO CTRL-CDE-NT-ZONE                         
                                       K278-NUM-NT-ZONE.                        
           MOVE F205-NUM-FLAT-TYPE  TO K278-NUM-FLAT-TYPE.                      
           MOVE F205-DTE-ALLOCN     TO CTRL-OLD-DATE                            
                                       K278-DTE-ALLOCN.                         
           MOVE F205-DTE-ALLOCN-NEW TO CTRL-NEW-DATE.                           
                                                                                
           PERFORM 210-STARTBR-K278    THRU  210-EXIT.                          
                                                                                
           MOVE K278-READ-CNT       TO L-K278-READ-CNT.                         
           MOVE K278-UPDATE-CNT     TO L-K278-UPDATE-CNT.                       
                                                                                
           MOVE CTRL-CDE-NT-ZONE   TO L-CDE-NT.                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-CCYYMMDD.                
           MOVE WS-CUR-DD           TO WS-DD OF PRINT-DATE.                     
           MOVE WS-CUR-MM           TO WS-MM OF PRINT-DATE.                     
           MOVE WS-CUR-CC           TO WS-CC OF PRINT-DATE.                     
           MOVE WS-CUR-YY           TO WS-YY OF PRINT-DATE.                     
                                                                                
           MOVE CORR CTRL-OLD-DATE TO WS-EDITED-DATE.                           
           MOVE WS-EDITED-DATE     TO L-OLD-DATE.                               
           MOVE CORR CTRL-NEW-DATE TO WS-EDITED-DATE.                           
           MOVE WS-EDITED-DATE     TO L-NEW-DATE.                               
                                                                                
           IF WS-LINE-CNT > 50                                                  
              PERFORM 800-PRINT    THRU 800-EXIT                                
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC-1      FROM L209-LINE3     AFTER 5.                  
           WRITE PRINT-REC-1      FROM L209-LINE22    AFTER 2.                  
           WRITE PRINT-REC-1      FROM L209-LINE23    AFTER 2.                  
                                                                                
           ADD  9                  TO WS-LINE-CNT.                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       210-STARTBR-K278.                                                        
      *-------------------------------------------------------------            
           START BP13K278 KEY NOT < K278-KEY-FLD.                               
                                                                                
           IF BP13K278-STATUS NOT = ZEROES                                      
              DISPLAY 'ERROR IN READING K278 : ' BP13K278-STATUS                
              DISPLAY 'K278-KEY-FLD          : ' K278-KEY-FLD                   
              GO                       TO    210-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM 220-READNXT-K278        THRU  220-EXIT                       
             UNTIL EOF-K278 = 'Y'.                                              
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       220-READNXT-K278.                                                        
      *-------------------------------------------------------------            
           READ  BP13K278 NEXT AT END                                           
                          MOVE 'Y' TO EOF-K278                                  
                          GO TO  220-EXIT.                                      
                                                                                
           IF BP13K278-STATUS NOT = ZEROES                                      
              DISPLAY 'ERROR IN READING K278 : ' BP13K278-STATUS                
              DISPLAY 'K278-KEY-FLD          : ' K278-KEY-FLD                   
              GO                       TO    210-EXIT                           
           END-IF.                                                              
                                                                                
           ADD 1                       TO    K278-READ-CNT.                     
                                                                                
           IF (K278-DTE-ALLOCN = CTRL-OLD-DATE) AND                             
              (K278-NUM-NT-ZONE = F205-NUM-NT-ZONE)                             
               DELETE BP13K278                                                  
               MOVE CTRL-NEW-DATE TO K278-DTE-ALLOCN                            
               WRITE  BP13K278-REC                                              
                                                                                
               IF BP13K278-STATUS = 00                                          
                  ADD 1 TO K278-UPDATE-CNT                                      
               ELSE                                                             
                  DISPLAY 'BP13K278-WRITE ERROR ' BP13K278-STATUS               
                  DISPLAY 'BP13K278-KEY-FLD     ' K278-KEY-FLD                  
               END-IF.                                                          
                                                                                
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-PRINT.                                                               
      *-------------------------------------------------------------            
                                                                                
           ADD  1                  TO WS-PRINT-PAGE.                            
           MOVE WS-PRINT-PAGE      TO PRINT-PAGE.                               
                                                                                
           MOVE ZEROS              TO WS-LINE-CNT.                              
           WRITE PRINT-REC-1     FROM L209-LINE1 AFTER PAGE.                    
           WRITE PRINT-REC-1     FROM L209-LINE2.                               
           ADD  2                  TO WS-LINE-CNT.                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
            CLOSE      BP13F205                                                 
                       BP13L209                                                 
                       BP13K278.                                                
                                                                                
            IF   BP13K278-STATUS NOT = 00                                       
                 DISPLAY 'BP13K278-STATUS ' BP13K278-STATUS.                    
            STOP RUN.                                                           
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
