                                                                                
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C252.                                                    
       AUTHOR. JAI.                                                             
       DATE-WRITTEN. 18/11/94.                                                  
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  GET RECORDS FROM F230, CALCULATE THE TOTAL    *                
      *          NUMBER OF '02' RECORDS SENT AND WRITE TO      *                
      *          OUTPUT FILE.                                  *                
      * CHG REQ   DATE   OIC  REMARKS                          *                
      *--------------------------------------------------------*                
      * BP130956 100101  LMS1 CHANGE LRECL OF K230/K240 FROM   *                
      *                       130 TO 250.                      *                
      * BP132278 280303  JF4  CHGE BP13F230/F240 LRECL TO 450. *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT P13F230A   ASSIGN       TO P13F230A.                          
           SELECT P13F230B   ASSIGN       TO P13F230B.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   P13F230A                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F230.                                                           
                                                                                
       FD   P13F230B                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F230B-REC           PIC X(450).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  EOF-F230                PIC X(01) VALUE SPACE.                       
       01  WS-F230-READ            PIC 9(08) VALUE 0.                           
       01  WS-F230-WRITE           PIC 9(08) VALUE 0.                           
       01  WS-TOT-02-REC           PIC 9(04) VALUE 0.                           
                                                                                
       01  WS-CUR-DATE.                                                         
           05 WS-CC                PIC 9(02).                                   
           05 WS-SYS-DATE.                                                      
              10 WS-YY             PIC 9(02).                                   
              10 WS-MM             PIC 9(02).                                   
              10 WS-DD             PIC 9(02).                                   
                                                                                
       01  WS-F230-HEADER-REC.                                          00391006
           05  FILLER                      PIC X(06).                   00411010
           05  WS-HDR-DTE-ALLOC            PIC X(08).                   00410006
           05  FILLER                      PIC X(15).                   00411010
           05  WS-HDR-NUM-NEW-TOWN         PIC X(20).                   00420006
           05  WS-HDR-NUM-FLAT-TYPE        PIC X(02).                   00430006
           05  WS-HDR-NUM-BLK              PIC X(05).                   00440006
           05  FILLER                      PIC X(12).                   00450014
           05  WS-HDR-NUM-TOTAL            PIC X(04).                   00401015
           05  FILLER                      PIC X(24).                   00450014
           05  WS-HDR-NUM-ZONE             PIC X(10).                   00460014
           05  FILLER                      PIC X(344).                  00470017
                                                                                
       01  WS-F230-DETAIL-REC.                                          00391006
           05  FILLER                      PIC X(75).                   00400015
           05  WS-DETAIL-NUM-CAT           PIC X(01).                   00401015
           05  FILLER                      PIC X(374).                  00470017
                                                                                
       01  WS-F230-TRAILER-REC.                                         00391006
           05  WS-F230-CDE-INTV            PIC X(04).                   00400015
           05  WS-F230-CDE-TRAILER         PIC X(02).                   00401015
           05  WS-TRAILER-DTE-ALLOC        PIC X(08).                   00410006
           05  FILLER                      PIC X(15).                   00411010
           05  WS-TRAILER-NUM-NEW-TOWN     PIC X(20).                   00420006
           05  WS-TRAILER-NUM-FLAT-TYPE    PIC X(02).                   00430006
           05  WS-TRAILER-NUM-BLK          PIC X(05).                   00440006
           05  FILLER                      PIC X(40).                   00450014
           05  WS-TRAILER-NUM-ZONE         PIC X(10).                   00460014
           05  FILLER                      PIC X(344).                  00470017
                                                                                
      *********************                                                     
       PROCEDURE DIVISION.                                                      
      *********************                                                     
                                                                                
      ***********                                                               
       000-MAIN.                                                                
      ***********                                                               
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
                                                                                
           PERFORM 200-READ-F230-REC THRU 200-EXIT.                             
                                                                                
           PERFORM 300-PROCESS-F230  THRU 300-EXIT                              
                   UNTIL EOF-F230 = 'Y'.                                        
                                                                                
           IF WS-F230-READ > 1                                                  
              PERFORM 400-WRITE-01-AND-03-REC  THRU 400-EXIT                    
           END-IF.                                                              
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************                                                         
       100-OPEN-FILES.                                                          
      *****************                                                         
                                                                                
           OPEN INPUT  P13F230A.                                                
           OPEN OUTPUT P13F230B.                                                
                                                                                
           ACCEPT WS-SYS-DATE FROM DATE.                                        
                                                                                
           IF WS-YY > '93'                                                      
              MOVE '19'  TO WS-CC                                               
           ELSE                                                                 
              MOVE '20'  TO WS-CC                                               
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ********************                                                      
       200-READ-F230-REC.                                                       
      ********************                                                      
                                                                                
           READ P13F230A AT END                                                 
                MOVE 'Y' TO EOF-F230                                            
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F230-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       300-PROCESS-F230.                                                        
      *******************                                                       
                                                                                
           IF F230-CDE-HEADER = '01'                                            
              PERFORM 400-WRITE-01-AND-03-REC  THRU 400-EXIT                    
           ELSE                                                                 
           IF F230-CDE-DETAIL = '02'                                            
              MOVE F230-DETAIL-REC     TO  WS-F230-DETAIL-REC                   
              MOVE SPACES              TO  WS-DETAIL-NUM-CAT                    
              MOVE WS-F230-DETAIL-REC  TO  P13F230B-REC                         
              WRITE P13F230B-REC                                                
              ADD   1                  TO  WS-TOT-02-REC                        
              ADD   1                  TO  WS-F230-WRITE                        
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F230-REC  THRU 200-EXIT.                            
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************                                                  
       400-WRITE-01-AND-03-REC.                                                 
      ************************                                                  
                                                                                
           IF WS-F230-READ = 1                                                  
              CONTINUE                                                          
           ELSE                                                                 
              MOVE WS-TOT-02-REC        TO WS-HDR-NUM-TOTAL                     
              MOVE WS-F230-HEADER-REC   TO P13F230B-REC                         
              WRITE P13F230B-REC                                                
              ADD  1                    TO WS-F230-WRITE                        
              MOVE SPACES               TO WS-F230-TRAILER-REC                  
              MOVE 'INTV'               TO WS-F230-CDE-INTV                     
              MOVE '03'                 TO WS-F230-CDE-TRAILER                  
              MOVE WS-HDR-DTE-ALLOC     TO WS-TRAILER-DTE-ALLOC                 
              MOVE WS-HDR-NUM-NEW-TOWN  TO WS-TRAILER-NUM-NEW-TOWN              
              MOVE WS-HDR-NUM-FLAT-TYPE TO WS-TRAILER-NUM-FLAT-TYPE             
              MOVE WS-HDR-NUM-BLK       TO WS-TRAILER-NUM-BLK                   
              MOVE WS-HDR-NUM-ZONE      TO WS-TRAILER-NUM-ZONE                  
              MOVE WS-F230-TRAILER-REC  TO P13F230B-REC                         
              WRITE P13F230B-REC                                                
              ADD  1                    TO WS-F230-WRITE                        
           END-IF.                                                              
                                                                                
           MOVE F230-HEADER-REC         TO WS-F230-HEADER-REC.                  
           MOVE ZEROES                  TO WS-TOT-02-REC.                       
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************                                                        
       900-CLOSE-ROUTINE.                                                       
      ******************                                                        
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C252 '.                                    
           DISPLAY '                                      '.                    
           DISPLAY 'CURR-DATE  : ' WS-CUR-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM  P13F230A ' WS-F230-READ.           
           DISPLAY 'NO OF RECORDS WRITTEN TO P13F230B ' WS-F230-WRITE.          
                                                                                
           CLOSE P13F230A                                                       
                 P13F230B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
