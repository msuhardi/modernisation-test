       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF78.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  01/02/2013.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO CALCULATE THE TOTAL NUMBER OF      *                
      *                  UNITS FOR THE DIFFERENT ALLOC SCH.    *                
      *                                                        *                
      * ====================================================== *                
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                  *                
      * --------  ----  --------  ---------------------------- *                
      * BP134782  SMR2  01022013  SIMILAR TO BP13C545          *                
      * BP134857  SMR2  18032013  REMOVE HARDCODE OF '30', GET *                
      *                           VALUE FROM K010-NUM-PPS      *                
      *  BP134861  22/04/13  EAA1  EXPAND BP13K010 TO 600      *                
      * BP134914  SMR2  11062013  COMPUTE K712-NUM-FLAT-AST    *                
      * BP135028  SMR2  01082013  COMPUTE K712-NUM-FLAT-FTS    *                
      * BP135028  SMR2  20082013  REVISE COMPUTATION           *                
      * BP135100  SMR2  31102013  REVISE COMPUTATION FOR FTS   *                
      * BP135458  SMR2  04082014  EXPAND BP13K712 TO 350       *                
      * BP135492  SMR2  15082014  COMPUTE FLAT MCPS FT & ST    *                
      * BP135847  SMR2  11052015  REVISE COMPUTATION FOR FTS   *                
      * BP136096  IL5   02122015  REMOVE WRITE TO BP13K712     *                
      * BP136107  IL5   03022016  REMOVE HARDCODING OF %       *                
      * BP139411  AM25  07122022  REVISE COMPUTATION FOR UPG   *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS SEQUENTIAL                                 
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT BP13F713 ASSIGN TO BP13F713.                                  
                                                                                
           SELECT BP13K712 ASSIGN TO BP13K712                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K712-KEY-FLD                               
                  FILE STATUS     IS WS-K712-STATUS.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K010                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BP13K010.                                                           
                                                                                
       FD  BP13F713                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 20 CHARACTERS                                        
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F713.                                                           
                                                                                
       FD  BP13K712                                                             
           RECORD CONTAINS 350 CHARACTERS.                                      
       COPY BP13K712.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       COPY PRIOSCH.                                                            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-K712-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K010-STATUS              PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-COUNTER.                                                          
           05 WS-K010-READ                PIC 9(5)  VALUE ZEROS.                
           05 WS-K712-WRTN                PIC 9(5)  VALUE ZEROS.                
           05 WS-K712-UPDATE              PIC 9(5)  VALUE ZEROS.                
                                                                                
       01 WS-TOT-REQUEST                  PIC 9(6)  VALUE ZEROS.                
      *01 WS-EXTRA-FLAT                   PIC 9(5)  VALUE ZEROS.                
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-CC                       PIC 99.                               
           05 WS-YY                       PIC 99.                               
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
       01  WS-SYS-TIME                    PIC 9(8).                             
       01  WS-NUM-PPS                     PIC 9(3).                             
       01  WS-NUM-AST                     PIC 9(3).                             
       01  WS-NUM-FTS                     PIC 9(3).                             
       01  WS-NUM-MCPS-FT                 PIC 9(3).                             
       01  WS-NUM-MCPS-ST                 PIC 9(3).                             
       01  WS-NUM-FLAT-UG                 PIC 9(5)  VALUE ZEROES.               
       01  WS-NUM-FLAT-FT                 PIC 9(5)  VALUE ZEROES.               
       01  WS-TEMP                        PIC S9V9(10).                         
                                                                                
       01  WS-EOF-K712                    PIC 9     VALUE 0.                    
           88 EOF-K712                    VALUE 1.                              
       01  WS-EOF-K010                    PIC 9     VALUE 0.                    
           88 EOF-K010                    VALUE 1.                              
                                                                                
                                                                                
       01  WS-K712-KEY                    PIC X(10)  VALUE SPACES.              
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-PARM1                    PIC X(3)  VALUE SPACES.               
           05 WS-NUM-UPG                  PIC 9(2)  VALUE ZEROES.               
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 250-READ-F713      THRU 250-EXIT.                            
           PERFORM 200-READ-K010      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-K010   THRU 300-EXIT                             
             UNTIL EOF-K010.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
           OPEN INPUT  BP13K010                                                 
                       BP13F713                                                 
                I-O    BP13K712.                                                
                                                                                
           IF WS-K712-STATUS NOT = 00 AND 97                                    
              MOVE WS-K712-STATUS            TO RETURN-CODE                     
              DISPLAY 'ERROR OPENING BP13K712'                                  
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              MOVE WS-K010-STATUS            TO RETURN-CODE                     
              DISPLAY 'ERROR OPENING BP13K010'                                  
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           ACCEPT WS-PARM1.                                                     
           ACCEPT WS-SYS-DATE              FROM DATE YYYYMMDD.                  
           ACCEPT WS-SYS-TIME              FROM TIME.                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-K010.                                                           
      ******************************************************************        
           READ BP13K010 AT END                                                 
                MOVE 1 TO WS-EOF-K010                                           
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1  TO WS-K010-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       250-READ-F713.                                                           
      ******************************************************************        
           READ BP13F713 AT END                                                 
                DISPLAY 'BP13F713 EMPTY FILE '                                  
                GO TO 999-CLOSE-FILES.                                          
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       260-READ-K712.                                                           
      ******************************************************************        
           READ  BP13K712                                                       
           IF WS-K712-STATUS = 00 OR 23                                         
              NEXT SENTENCE                                                     
           ELSE                                                                 
              DISPLAY 'ERRO READING BP13K712, STATUS = '                        
                      WS-K712-STATUS                                            
              GO TO 999-CLOSE-FILES.                                            
       260-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-K010.                                                        
      ******************************************************************        
                                                                                
           IF K010-KEY-FLD = SPACES OR LOW-VALUES                               
                 PERFORM 200-READ-K010           THRU 200-EXIT                  
                 GO TO 300-EXIT                                                 
           END-IF.                                                              
                                                                                
           IF F713-CDE-COHORT = 'MT'                                            
                                                                                
              IF K010-NUM-TOT-MTF > 0                                           
                  MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                     
                  MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                     
                  MOVE 'MT'             TO K712-CDE-COHORT                      
                  MOVE 'MTF'            TO K712-CDE-ALLOC-SCH                   
                  PERFORM 260-READ-K712 THRU 260-EXIT                           
                  PERFORM 310-UPDATE-BP13K712                                   
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-CCC > 0                                           
                  MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                     
                  MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                     
                  MOVE 'CC'             TO K712-CDE-COHORT                      
                  MOVE 'CCC'            TO K712-CDE-ALLOC-SCH                   
                  PERFORM 260-READ-K712 THRU 260-EXIT                           
                  PERFORM 310-UPDATE-BP13K712                                   
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-RH > 0                                            
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'MT'             TO K712-CDE-COHORT                       
                 MOVE 'RH '            TO K712-CDE-ALLOC-SCH                    
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                 PERFORM 310-UPDATE-BP13K712                                    
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-REN > 0                                           
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'MT'             TO K712-CDE-COHORT                       
                 MOVE 'REN'            TO K712-CDE-ALLOC-SCH                    
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                 PERFORM 310-UPDATE-BP13K712                                    
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-JB > 0                                            
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'MT'             TO K712-CDE-COHORT                       
                 MOVE 'JB '            TO K712-CDE-ALLOC-SCH                    
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                 PERFORM 310-UPDATE-BP13K712                                    
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-RDC > 0                                           
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'MT'             TO K712-CDE-COHORT                       
                 MOVE 'RDC'            TO K712-CDE-ALLOC-SCH                    
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                 PERFORM 310-UPDATE-BP13K712                                    
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-TCP > 0                                           
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'MT'             TO K712-CDE-COHORT                       
                 MOVE 'TCP'            TO K712-CDE-ALLOC-SCH                    
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                 PERFORM 310-UPDATE-BP13K712                                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
           IF F713-CDE-COHORT = 'PA'                                            
              IF K010-NUM-TOT-PA > 0                                            
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'PA'             TO K712-CDE-COHORT                       
                 MOVE 'PA'            TO K712-CDE-ALLOC-SCH                     
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                                                                                
                 IF K712-NUM-FLAT-UG-APA3 IS NOT NUMERIC                        
                    MOVE ZEROES TO K712-NUM-FLAT-UG-APA3                        
                 END-IF                                                         
                                                                                
                 IF K712-NUM-FLAT-UG-APA3 > ZEROES                              
                    CONTINUE                                                    
                 ELSE                                                           
                    MOVE K010-NUM-FLAT-UPG-APA3 TO                              
                                     K712-NUM-FLAT-UG-APA3                      
                 END-IF                                                         
                                                                                
                 PERFORM 310-UPDATE-BP13K712 THRU 310-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-K010           THRU 200-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       310-UPDATE-BP13K712.                                                     
      ******************************************************************        
                                                                                
           IF WS-K712-STATUS = 00                                               
              PERFORM 400-CAL-TOT-UNIT-AVAILABLE THRU 400-EXIT                  
              PERFORM 500-CAL-FLAT-ALLOCATION    THRU 500-EXIT                  
                                                                                
              IF K712-NUM-FLAT-U3 = SPACES OR LOW-VALUES                        
                 MOVE ZEROS      TO K712-NUM-FLAT-U3                            
              END-IF                                                            
              IF K712-NUM-REG-NH = SPACES OR LOW-VALUES                         
              MOVE ZEROS        TO K712-NUM-REG-NH                              
              END-IF                                                            
              IF K712-NUM-REG-PPS = SPACES OR LOW-VALUES                        
              MOVE ZEROS        TO K712-NUM-REG-PPS                             
              END-IF                                                            
              IF K712-NUM-REG-AST = SPACES OR LOW-VALUES                        
              MOVE ZEROS        TO K712-NUM-REG-AST                             
              END-IF                                                            
              IF K712-NUM-REG-FTS = SPACES OR LOW-VALUES                        
              MOVE ZEROS        TO K712-NUM-REG-FTS                             
              END-IF                                                            
              IF K712-NUM-REG-MCPS-FT = SPACES OR LOW-VALUES                    
              MOVE ZEROS        TO K712-NUM-REG-MCPS-FT                         
              END-IF                                                            
              IF K712-NUM-REG-MCPS-ST = SPACES OR LOW-VALUES                    
              MOVE ZEROS        TO K712-NUM-REG-MCPS-ST                         
              END-IF                                                            
              IF K712-NUM-REG-UG = SPACES OR LOW-VALUES                         
              MOVE ZEROS        TO K712-NUM-REG-UG                              
              END-IF                                                            
              IF K712-NUM-REG-U3 = SPACES OR LOW-VALUES                         
                 MOVE ZEROS     TO K712-NUM-REG-U3                              
              END-IF                                                            
                                                                                
              REWRITE BP13K712-REC                                              
              IF WS-K712-STATUS = 00                                            
                 ADD 1                       TO WS-K712-UPDATE                  
              ELSE                                                              
                 MOVE WS-K712-STATUS         TO RETURN-CODE                     
                 DISPLAY 'ERROR REWRITING IN BP13K712, '                        
                          K712-KEY-FLD                                          
                          GO TO 999-CLOSE-FILES                                 
              END-IF                                                            
           ELSE                                                                 
              IF WS-K712-STATUS = 23                                            
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE WS-K712-STATUS            TO RETURN-CODE                  
                 DISPLAY 'ERROR READING BP13K712, '                             
                          K712-KEY-FLD                                          
                          GO TO 999-CLOSE-FILES                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-CAL-TOT-UNIT-AVAILABLE.                                              
      ******************************************************************        
                                                                                
           EVALUATE K712-CDE-ALLOC-SCH                                          
           WHEN 'MTF'                                                           
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-MTF -                   
                                           (K010-REG-MTF1   +                   
                                            K010-REG-MTF2   +                   
                                            K010-REG-MTF3))                     
                                                                                
             WHEN 'CCC'                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-CCC -                   
                                           (K010-REG-CCC1   +                   
                                            K010-REG-CCC2   +                   
                                            K010-REG-CCC3))                     
                                                                                
             WHEN 'JB '                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-JB  -                   
                                           (K010-REG-JB1    +                   
                                            K010-REG-JB2    +                   
                                            K010-REG-JB3))                      
                                                                                
             WHEN 'PA '                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-PA  -                   
                                           (K010-REG-PA1    +                   
                                            K010-REG-PA2    +                   
                                            K010-REG-PA3))                      
                                                                                
             WHEN 'TCP'                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-TCP -                   
                                           (K010-REG-TCP1   +                   
                                            K010-REG-TCP2   +                   
                                            K010-REG-TCP3))                     
                                                                                
             WHEN 'RH '                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-RH  -                   
                                           (K010-REG-RH1    +                   
                                            K010-REG-RH2    +                   
                                            K010-REG-RH3))                      
                                                                                
             WHEN 'RDC'                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-RDC -                   
                                           (K010-REG-RDC1   +                   
                                            K010-REG-RDC2   +                   
                                            K010-REG-RDC3))                     
                                                                                
             WHEN 'REN'                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-REN -                   
                                           (K010-REG-REN1   +                   
                                            K010-REG-REN2   +                   
                                            K010-REG-REN3))                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-CAL-FLAT-ALLOCATION.                                                 
      ******************************************************************        
                                                                                
           INITIALIZE                       WS-NUM-FLAT-UG                      
                                            WS-NUM-FLAT-FT.                     
                                                                                
           IF K010-NUM-FTS = SPACES OR LOW-VALUES                               
              MOVE ZEROES                TO WS-NUM-FTS                          
           ELSE                                                                 
              MOVE K010-NUM-FTS          TO WS-NUM-FTS                          
           END-IF.                                                              
                                                                                
           IF WS-NUM-FTS = ZEROES                                               
              PERFORM 510-COMPUTE-FOR-UPG  THRU 510-EXIT                        
           ELSE                                                                 
              PERFORM 520-COMPUTE-WITH-FTS THRU 520-EXIT                        
                                                                                
              MOVE K010-NUM-NHH        TO K712-NUM-NHH                          
              MOVE K010-NUM-UPG        TO K712-NUM-UPG                          
           END-IF.                                                              
                                                                                
           IF WS-NUM-FLAT-UG > ZERO                                             
              MOVE WS-NUM-FLAT-UG TO K712-NUM-FLAT-UG                           
           ELSE                                                                 
              MOVE ZEROES         TO K712-NUM-FLAT-UG                           
           END-IF.                                                              
                                                                                
           IF WS-NUM-FLAT-FT > ZERO                                             
              MOVE WS-NUM-FLAT-FT TO K712-NUM-FLAT-FTS                          
           ELSE                                                                 
              MOVE ZEROES         TO K712-NUM-FLAT-FTS                          
           END-IF.                                                              
                                                                                
           COMPUTE K712-NUM-FLAT-NH = K712-NUM-TOT-UNIT -                       
                                      K712-NUM-FLAT-UG.                         
                                                                                
           IF K010-NUM-PPS = SPACES OR LOW-VALUES                               
              MOVE ZEROES                TO WS-NUM-PPS                          
           ELSE                                                                 
              MOVE K010-NUM-PPS          TO WS-NUM-PPS                          
           END-IF.                                                              
                                                                                
           COMPUTE K712-NUM-FLAT-PPS = (K712-NUM-TOT-UNIT *                     
                                        WS-NUM-PPS) / 100.                      
                                                                                
           IF K010-NUM-AST = SPACES OR LOW-VALUES                               
              MOVE ZEROES                TO WS-NUM-AST                          
           ELSE                                                                 
              MOVE K010-NUM-AST          TO WS-NUM-AST                          
           END-IF.                                                              
                                                                                
           COMPUTE K712-NUM-FLAT-AST = (K712-NUM-TOT-UNIT *                     
                                        WS-NUM-AST) / 100.                      
                                                                                
           IF K010-NUM-MCPS-FT = SPACES OR LOW-VALUES                           
              MOVE ZEROES                TO WS-NUM-MCPS-FT                      
           ELSE                                                                 
              MOVE K010-NUM-MCPS-FT      TO WS-NUM-MCPS-FT                      
           END-IF.                                                              
                                                                                
           COMPUTE K712-NUM-FLAT-MCPS-FT = (K712-NUM-TOT-UNIT *                 
                                            WS-NUM-MCPS-FT) / 100.              
                                                                                
           IF K010-NUM-MCPS-ST = SPACES OR LOW-VALUES                           
              MOVE ZEROES                TO WS-NUM-MCPS-ST                      
           ELSE                                                                 
              MOVE K010-NUM-MCPS-ST      TO WS-NUM-MCPS-ST                      
           END-IF.                                                              
                                                                                
           COMPUTE K712-NUM-FLAT-MCPS-ST = (K712-NUM-TOT-UNIT *                 
                                            WS-NUM-MCPS-ST) / 100.              
                                                                                
           MOVE WS-SYS-DATE              TO K712-DTE-UPDATE.                    
           MOVE WS-SYS-TIME              TO K712-TME-SYSTEM.                    
           MOVE 'P13CF78'                TO K712-USERID.                        
                                                                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       510-COMPUTE-FOR-UPG.                                                     
      ******************************************************************        
           IF K010-NUM-CAP-UPGRADER = 'Y'                                       
              EVALUATE WS-PARM1                                                 
                 WHEN '200'                                                     
                    COMPUTE WS-NUM-UPG       =  K010-NUM-UPG / 2                
                                                                                
                    COMPUTE WS-NUM-FLAT-UG   = (K712-NUM-TOT-UNIT *             
                                                WS-NUM-UPG   / 100)             
                                                                                
                    COMPUTE K712-NUM-NHH     =  100 - WS-NUM-UPG                
                    COMPUTE K712-NUM-UPG     =  100 - K712-NUM-NHH              
                 WHEN '300'                                                     
                    MOVE ZEROES        TO WS-NUM-FLAT-UG                        
                                                                                
                    MOVE 99.9          TO K712-NUM-NHH                          
                    MOVE ZEROES        TO K712-NUM-UPG                          
                 WHEN OTHER                                                     
                    COMPUTE WS-NUM-FLAT-UG   = (K712-NUM-TOT-UNIT *             
                                                K010-NUM-UPG / 100)             
                                                                                
                    MOVE K010-NUM-NHH  TO K712-NUM-NHH                          
                    MOVE K010-NUM-UPG  TO K712-NUM-UPG                          
              END-EVALUATE                                                      
           ELSE                                                                 
              COMPUTE WS-NUM-FLAT-UG         = (K712-NUM-TOT-UNIT *             
                                                K010-NUM-UPG / 100)             
                                                                                
              MOVE K010-NUM-NHH        TO K712-NUM-NHH                          
              MOVE K010-NUM-UPG        TO K712-NUM-UPG                          
           END-IF.                                                              
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       520-COMPUTE-WITH-FTS.                                                    
      ******************************************************************        
           COMPUTE WS-NUM-FLAT-UG         = (K712-NUM-TOT-UNIT *                
                                             K010-NUM-UPG / 100).               
                                                                                
           COMPUTE WS-NUM-FLAT-FT         = (K712-NUM-TOT-UNIT *                
                                             K010-NUM-FTS / 100).               
                                                                                
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-FILES.                                                         
      ******************************************************************        
           CLOSE BP13K010                                                       
                 BP13F713                                                       
                 BP13K712.                                                      
                                                                                
           IF WS-K712-STATUS NOT = 00 AND 97                                    
              MOVE WS-K712-STATUS TO RETURN-CODE                                
              DISPLAY 'ERROR CLOSING BP13K712, ' WS-K712-STATUS.                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              MOVE WS-K010-STATUS TO RETURN-CODE                                
              DISPLAY 'ERROR CLOSING BP13K010, ' WS-K010-STATUS.                
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CF78 ================*'.              
           DISPLAY '   NO. OF K010 RECORDS READ      = ' WS-K010-READ.          
           DISPLAY '   NO. OF K712 RECORDS WRITTEN   = ' WS-K712-WRTN.          
           DISPLAY '   NO. OF K712 RECORDS REWRITTEN = ' WS-K712-UPDATE.        
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
