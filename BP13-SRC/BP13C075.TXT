       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C075.                                                    
      *AUTHOR. MCC.                                                             
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  :                                               *           
      *      TO READ MATCHED CPFIBG FILE AND PRINT REPORT           *           
      *                                                             *           
      *  INPUT FILES:                                               *           
      *      1. BB24F04A (CPFIBG FILE)                              *           
      *      2. BM06K110                                            *           
      *                                                             *           
      *  OUTPUT FILE:                                               *           
      *      1. BP13L075 (MATCHED/UNMATCHED CASES REPORT)           *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE     BY    DESCRIPTION                         *           
      * -------- -------- ----- -----------                         *           
      * BP132400 20030820 MCC   NEW PROGRAM                         *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BB24F04A ASSIGN   TO BB24F04A.                                
           SELECT BM06K110 ASSIGN   TO BM06K110                                 
                                    ACCESS MODE  IS RANDOM                      
                                    ORGANIZATION IS INDEXED                     
                                    RECORD KEY   IS K110-KEY-FLD                
                                    FILE STATUS  IS WS-K110-STATUS.             
                                                                                
           SELECT BP13L075 ASSIGN   TO BP13L075.                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BB24F04A                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BB24F04A-DETAILS.                                                   
            05 FILLER               PIC X(1).                                   
            05 F04A-NUM-HDB-REF     PIC X(11).                                  
            05 F04A-NUM-CPF         PIC X(9).                                   
            05 F04A-NAME            PIC X(66).                                  
            05 F04A-HDB-TRAN-CODE   PIC X(2).                                   
            05 FILLER               PIC X(2).                                   
            05 F04A-AMT-LUMP-SUM    PIC 9(9)V99.                                
            05 F04A-AMT-STAMP-LEGAL PIC 9(7)V99.                                
            05 F04A-AMT-LOAN-REPYMT PIC 9(9)V99.                                
            05 F04A-CDE-MATCHED     PIC X(1).                                   
            05 F04A-PAYMENT-TYPE    PIC X(3).                                   
            05 FILLER               PIC X(16).                                  
            05 F04A-DTE-VALUE       PIC X(8).                                   
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD   BP13L075                                                            
            RECORD CONTAINS 132  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   L075-PRTREC       PIC X(132).                                       
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-STATUS.                                                           
           05 WS-K110-STATUS             PIC 9(02)    VALUE ZEROES.             
           05 WS-F04A-EOF                PIC X(01)    VALUE ' '.                
           05 WS-CHK-CASE                PIC X(01)    VALUE SPACE.              
              88 MATCHED-CASE                         VALUE 'Y'.                
           05 WS-TRAN-CODE               PIC X(02)    VALUE SPACES.             
              88 SMS-CASE                             VALUE 'RF'.               
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-STORED-DTE              PIC X(08)    VALUE SPACES.             
           05 WS-SUB-AMT                 PIC 9(07)V99 VALUE ZEROES.             
           05 WS-TOTAL-AMT               PIC 9(08)V99 VALUE ZEROES.             
           05 WS-TOTAL-IBG               PIC 9(09)V99 VALUE ZEROES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F04A-READ               PIC 9(08)    VALUE ZEROES.             
           05 WS-K110-READ               PIC 9(08)    VALUE ZEROES.             
           05 WS-K110-NOTFD              PIC 9(08)    VALUE ZEROES.             
           05 WS-SNO                     PIC 9(6)     VALUE ZEROES.             
           05 WS-PAGE                    PIC 9(08)    VALUE ZEROES.             
           05 WS-LINE                    PIC 9(08)    VALUE 60.                 
           05 WS-NO-CASES                PIC 9(08)    VALUE ZEROES.             
                                                                                
      * REPORT LAYOUT BP13L075                                                  
      *-------------------------------------------------------------            
       01  L075-HDR1.                                                           
           05 FILLER                     PIC X(8)   VALUE 'BP13L075'.           
           05 FILLER                     PIC X(34)  VALUE SPACES.               
           05 FILLER                     PIC X(38)  VALUE                       
              'S Y S T E M   OF   C O M M I T M E N T'.                         
           05 FILLER                     PIC X(35)  VALUE SPACES.               
           05 FILLER                     PIC X(7)   VALUE 'DATE : '.            
           05 L075-DATE                  PIC X(10)  VALUE SPACES.               
                                                                                
       01  L075-HDR2.                                                           
           05 FILLER                     PIC X(30)  VALUE SPACES.               
           05 FILLER                     PIC X(16)  VALUE                       
                                         'CONTROL LIST OF '.                    
           05 L075-TRANS-TYPE            PIC X(23)  VALUE SPACES.               
           05 L075-TRANS-MTCH            PIC X(17)  VALUE SPACES.               
           05 FILLER                     PIC X(29)  VALUE SPACES.               
           05 FILLER                     PIC X(10) VALUE 'PAGE :    '.          
           05 L075-PAGE                  PIC ZZZ,ZZ9 VALUE ZEROES.              
                                                                                
       01  L075-HDR3-MTCH.                                                      
           05 FILLER                     PIC X(6)  VALUE ' S/NO '.              
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 FILLER                     PIC X(8)  VALUE 'REGN NO '.            
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 FILLER                     PIC X(9)  VALUE '  NRIC   '.           
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 FILLER                     PIC X(11) VALUE '  SCH ACC  '.         
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 FILLER                     PIC X(9)  VALUE 'PAYM TYPE'.           
           05 FILLER                     PIC X(26) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE 'DTE POSTED'.          
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 FILLER                     PIC X(12) VALUE '   AMOUNT   '.        
                                                                                
       01  L075-MTCH-DET.                                                       
           05 L075-MTCH-SNO              PIC Z(6).                              
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L075-MTCH-REGN             PIC X(8)  VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L075-MTCH-CPF              PIC X(9)  VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L075-MTCH-SCHACC           PIC X(11) VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L075-MTCH-PAYTYP           PIC X(9)  VALUE SPACES.                
           05 FILLER                     PIC X(26) VALUE SPACES.                
           05 L075-MTCH-DTEPOST          PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L075-MTCH-AMT              PIC Z,ZZZ,ZZ9.99.                      
                                                                                
       01  L075-HDR3-UNMTCH.                                                    
           05 FILLER                     PIC X(6)  VALUE ' S/NO '.              
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 FILLER                     PIC X(8)  VALUE 'REGN NO '.            
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 FILLER                     PIC X(9)  VALUE '  NRIC   '.           
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 FILLER                     PIC X(11) VALUE '  SCH ACC  '.         
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 FILLER                     PIC X(32) VALUE ' NAME '.              
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE 'DTE POSTED'.          
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 FILLER                     PIC X(12) VALUE '   AMOUNT   '.        
                                                                                
       01  L075-UNMTCH-DET.                                                     
           05 L075-UNMTCH-SNO            PIC Z(6).                              
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L075-UNMTCH-REGN           PIC X(8)  VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L075-UNMTCH-CPF            PIC X(9)  VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L075-UNMTCH-SCHACC         PIC X(11) VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L075-UNMTCH-NAME           PIC X(32) VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L075-UNMTCH-DTEPOST        PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L075-UNMTCH-AMT            PIC Z,ZZZ,ZZ9.99.                      
                                                                                
       01  L075-SUB-TOTAL.                                                      
           05 FILLER                     PIC X(64) VALUE SPACES.                
           05 FILLER                     PIC X(14) VALUE                        
                                        'NO OF CASES : '.                       
           05 L075-NO-CASES              PIC ZZZZ9.                             
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE '$'.                   
           05 L075-SUB-AMT               PIC Z,ZZZ,ZZ9.99.                      
                                                                                
       01  L075-GRAND-TOTAL.                                                    
           05 FILLER                     PIC X(58) VALUE SPACES.                
           05 FILLER                     PIC X(20) VALUE                        
                                        'TOTAL NO OF CASES : '.                 
           05 L075-TOT-NO-CASES          PIC ZZZZ9.                             
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE '$'.                   
           05 L075-TOTAL-AMT             PIC Z,ZZZ,ZZ9.99.                      
                                                                                
       01  L075-SUB-LINE.                                                       
           05 FILLER                     PIC X(94) VALUE SPACES.                
           05 FILLER                     PIC X(12) VALUE ALL '-'.               
                                                                                
       01  L075-TOT-LINE.                                                       
           05 FILLER                     PIC X(94) VALUE SPACES.                
           05 FILLER                     PIC X(12) VALUE ALL '='.               
                                                                                
       01  RPT-LINE.                                                            
           05 FILLER                     PIC X(132) VALUE ALL '-'.              
                                                                                
       01  EMPTY-REPORT.                                                        
           05 FILLER                     PIC X(56) VALUE SPACES.                
           05 FILLER                     PIC X(20) VALUE                        
                                        '*** EMPTY REPORT ***'.                 
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES        THRU 100-EXIT.                         
           PERFORM 200-READ-F04A         THRU 200-EXIT.                         
           PERFORM 300-PROCESS-REC       THRU 300-EXIT                          
                   UNTIL WS-F04A-EOF = 'Y'.                                     
           PERFORM 800-PRINT-GRAND-TOTAL THRU 800-EXIT.                         
           PERFORM 900-CLOSE-ROUTINE     THRU 900-EXIT.                         
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BB24F04A                                                 
                       BM06K110                                                 
               OUTPUT  BP13L075.                                                
                                                                                
           IF WS-K110-STATUS NOT = 0 AND 97                                     
              DISPLAY 'OPENING ERROR, K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           STRING FUNCTION CURRENT-DATE(7:2) '/'                                
                  FUNCTION CURRENT-DATE(5:2) '/'                                
                  FUNCTION CURRENT-DATE(1:4)                                    
           DELIMITED BY SIZE INTO L075-DATE.                                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       200-READ-F04A.                                                           
      *-------------------------------------------------------------            
           READ BB24F04A AT END                                                 
                MOVE 'Y' TO WS-F04A-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F04A-READ.                                               
                                                                                
           IF WS-F04A-READ = 1                                                  
              MOVE F04A-CDE-MATCHED   TO WS-CHK-CASE                            
              MOVE F04A-HDB-TRAN-CODE TO WS-TRAN-CODE                           
              MOVE F04A-DTE-VALUE     TO WS-STORED-DTE                          
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       300-PROCESS-REC.                                                         
      *-------------------------------------------------------------            
           PERFORM 350-READ-BM06K110         THRU 350-EXIT.                     
                                                                                
           IF F04A-DTE-VALUE = WS-STORED-DTE                                    
              PERFORM 400-PRINT-REPORT   THRU 400-EXIT                          
           ELSE                                                                 
              PERFORM 700-PRINT-SUBTOTAL THRU 700-EXIT                          
              PERFORM 400-PRINT-REPORT   THRU 400-EXIT                          
           END-IF                                                               
                                                                                
           PERFORM 200-READ-F04A THRU 200-EXIT.                                 
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       350-READ-BM06K110.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES            TO K110-REC.                                  
           INITIALIZE                K110-REC.                                  
           MOVE F04A-NUM-HDB-REF  TO K110-KEY-FLD.                              
                                                                                
           READ BM06K110.                                                       
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
                ADD 1                           TO WS-K110-READ                 
                IF K110-REGN-NO = SPACES OR LOW-VALUES                          
                   DISPLAY 'FIS REGN NO = BLANK'   K110-KEY-FLD                 
                   MOVE SPACES                  TO K110-REGN-NO                 
                END-IF                                                          
           WHEN 23                                                              
                ADD   1                         TO WS-K110-NOTFD                
                MOVE SPACES                     TO K110-REGN-NO                 
                DISPLAY 'BM06K110 NOTFOUND KEY : ' K110-KEY-FLD                 
           WHEN OTHER                                                           
                DISPLAY 'ERRROR READING BM06K110, STATUS = '                    
                       WS-K110-STATUS ',KEY = ' K110-KEY-FLD                    
                MOVE WS-K110-STATUS TO RETURN-CODE                              
                PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       400-PRINT-REPORT.                                                        
      *-------------------------------------------------------------            
           IF MATCHED-CASE                                                      
              PERFORM 500-PRINT-MTCH-CASES   THRU 500-EXIT                      
           ELSE                                                                 
              PERFORM 600-PRINT-UNMTCH-CASES THRU 600-EXIT                      
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       500-PRINT-MTCH-CASES.                                                    
      *-------------------------------------------------------------            
           IF WS-LINE > 55                                                      
              PERFORM 550-PRINT-HDG THRU 550-EXIT                               
           END-IF.                                                              
                                                                                
           MOVE SPACES              TO   L075-PRTREC.                           
           INITIALIZE                    L075-PRTREC.                           
           MOVE SPACES              TO   L075-MTCH-DET.                         
           INITIALIZE                    L075-MTCH-DET.                         
           ADD 01                   TO   WS-SNO                                 
                                         WS-NO-CASES.                           
                                                                                
           MOVE WS-SNO              TO   L075-MTCH-SNO.                         
           MOVE K110-REGN-NO        TO   L075-MTCH-REGN.                        
           MOVE F04A-NUM-CPF        TO   L075-MTCH-CPF.                         
           MOVE F04A-NUM-HDB-REF    TO   L075-MTCH-SCHACC.                      
           MOVE F04A-PAYMENT-TYPE   TO   L075-MTCH-PAYTYP.                      
           STRING F04A-DTE-VALUE(7:2) '/'                                       
                  F04A-DTE-VALUE(5:2) '/'                                       
                  F04A-DTE-VALUE(1:4) DELIMITED BY SIZE                         
                  INTO  L075-MTCH-DTEPOST.                                      
                                                                                
           IF SMS-CASE                                                          
              ADD F04A-AMT-LOAN-REPYMT  TO   WS-SUB-AMT                         
                                             WS-TOTAL-AMT                       
              MOVE F04A-AMT-LOAN-REPYMT TO   L075-MTCH-AMT                      
           ELSE                                                                 
              COMPUTE WS-TOTAL-IBG = F04A-AMT-STAMP-LEGAL +                     
                                     F04A-AMT-LUMP-SUM                          
              ADD WS-TOTAL-IBG          TO   WS-SUB-AMT                         
                                             WS-TOTAL-AMT                       
              MOVE WS-TOTAL-IBG         TO   L075-MTCH-AMT                      
           END-IF.                                                              
                                                                                
           WRITE L075-PRTREC   FROM L075-MTCH-DET AFTER 1.                      
           ADD   1             TO   WS-LINE.                                    
           MOVE  SPACES        TO   L075-MTCH-DET.                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       550-PRINT-HDG.                                                           
      *-------------------------------------------------------------            
           ADD  1                TO     WS-PAGE.                                
           MOVE WS-PAGE          TO     L075-PAGE.                              
                                                                                
           MOVE SPACES           TO     L075-PRTREC.                            
           INITIALIZE                   L075-PRTREC.                            
           IF SMS-CASE                                                          
              MOVE 'HBL2 - IBG THRU SMS    ' TO L075-TRANS-TYPE                 
           ELSE                                                                 
              MOVE 'HBL1 - IBG THRU RESALE ' TO L075-TRANS-TYPE                 
           END-IF.                                                              
                                                                                
           WRITE L075-PRTREC FROM L075-HDR1 AFTER PAGE.                         
           IF MATCHED-CASE                                                      
              MOVE '(MATCHED CASES)  ' TO   L075-TRANS-MTCH                     
              WRITE L075-PRTREC        FROM L075-HDR2        AFTER 1            
              WRITE L075-PRTREC        FROM L075-HDR3-MTCH   AFTER 2            
           ELSE                                                                 
              MOVE '(UNMATCHED CASES)' TO   L075-TRANS-MTCH                     
              WRITE L075-PRTREC        FROM L075-HDR2        AFTER 1            
              WRITE L075-PRTREC        FROM L075-HDR3-UNMTCH AFTER 2            
           END-IF.                                                              
           WRITE L075-PRTREC FROM RPT-LINE AFTER 1.                             
                                                                                
           MOVE  SPACES TO L075-PRTREC.                                         
           WRITE L075-PRTREC.                                                   
                                                                                
           MOVE 6  TO WS-LINE.                                                  
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       600-PRINT-UNMTCH-CASES.                                                  
      *-------------------------------------------------------------            
           IF WS-LINE > 55                                                      
              PERFORM 550-PRINT-HDG THRU 550-EXIT                               
           END-IF.                                                              
                                                                                
           MOVE SPACES               TO   L075-PRTREC.                          
           INITIALIZE                     L075-PRTREC.                          
           ADD 01                    TO   WS-SNO                                
                                          WS-NO-CASES.                          
                                                                                
           MOVE WS-SNO               TO   L075-UNMTCH-SNO.                      
           MOVE K110-REGN-NO         TO   L075-UNMTCH-REGN.                     
           MOVE F04A-NUM-CPF         TO   L075-UNMTCH-CPF.                      
           MOVE F04A-NUM-HDB-REF     TO   L075-UNMTCH-SCHACC.                   
           STRING F04A-DTE-VALUE(7:2) '/'                                       
                  F04A-DTE-VALUE(5:2) '/'                                       
                  F04A-DTE-VALUE(1:4) DELIMITED BY SIZE                         
                  INTO  L075-UNMTCH-DTEPOST.                                    
           MOVE F04A-NAME            TO   L075-UNMTCH-NAME.                     
                                                                                
           IF SMS-CASE                                                          
              ADD F04A-AMT-LOAN-REPYMT  TO   WS-SUB-AMT                         
                                             WS-TOTAL-AMT                       
              MOVE F04A-AMT-LOAN-REPYMT TO   L075-UNMTCH-AMT                    
           ELSE                                                                 
              COMPUTE WS-TOTAL-IBG = F04A-AMT-STAMP-LEGAL +                     
                                     F04A-AMT-LUMP-SUM                          
              ADD WS-TOTAL-IBG          TO   WS-SUB-AMT                         
                                             WS-TOTAL-AMT                       
              MOVE WS-TOTAL-IBG         TO   L075-UNMTCH-AMT                    
           END-IF.                                                              
                                                                                
           WRITE L075-PRTREC   FROM L075-UNMTCH-DET AFTER 1.                    
           ADD   1             TO   WS-LINE.                                    
           MOVE  SPACES        TO   L075-UNMTCH-DET.                            
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       700-PRINT-SUBTOTAL.                                                      
      *-------------------------------------------------------------            
           IF WS-LINE > 55                                                      
              PERFORM 550-PRINT-HDG THRU 550-EXIT                               
           END-IF.                                                              
                                                                                
           MOVE SPACES           TO     L075-PRTREC.                            
           INITIALIZE                   L075-PRTREC.                            
           MOVE WS-NO-CASES      TO     L075-NO-CASES.                          
           MOVE WS-SUB-AMT       TO     L075-SUB-AMT.                           
                                                                                
           WRITE L075-PRTREC     FROM   L075-SUB-LINE    AFTER 1.               
           WRITE L075-PRTREC     FROM   L075-SUB-TOTAL   AFTER 1.               
           MOVE SPACES TO L075-PRTREC.                                          
           WRITE L075-PRTREC.                                                   
           WRITE L075-PRTREC.                                                   
                                                                                
           ADD  4  TO WS-LINE.                                                  
           MOVE F04A-DTE-VALUE    TO   WS-STORED-DTE.                           
           MOVE ZEROES            TO   WS-SUB-AMT                               
                                       WS-NO-CASES.                             
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       800-PRINT-GRAND-TOTAL.                                                   
      *-------------------------------------------------------------            
           IF WS-F04A-READ = ZEROES                                             
              GO TO 800-EXIT.                                                   
                                                                                
           IF WS-LINE > 55                                                      
              PERFORM 550-PRINT-HDG THRU 550-EXIT                               
           END-IF.                                                              
                                                                                
           PERFORM 700-PRINT-SUBTOTAL THRU 700-EXIT.                            
                                                                                
           MOVE SPACES           TO     L075-PRTREC.                            
           INITIALIZE                   L075-PRTREC.                            
           MOVE WS-SNO           TO     L075-TOT-NO-CASES.                      
           MOVE WS-TOTAL-AMT     TO     L075-TOTAL-AMT.                         
                                                                                
           WRITE L075-PRTREC     FROM   L075-TOT-LINE    AFTER 1.               
           WRITE L075-PRTREC     FROM   L075-GRAND-TOTAL AFTER 1.               
           WRITE L075-PRTREC     FROM   L075-TOT-LINE    AFTER 1.               
           ADD  3  TO WS-LINE.                                                  
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           DISPLAY '--- BP13C075 CONTROL TOTAL ------------'.                   
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF REC READ  BB24F04A   ' WS-F04A-READ.                  
           DISPLAY 'NO OF REC READ  BM06K110   ' WS-K110-READ.                  
           DISPLAY 'NO OF REC NOTFD BM06K110   ' WS-K110-NOTFD.                 
           DISPLAY 'NO OF REC PRINTED          ' WS-SNO.                        
           DISPLAY '---------------------------------------'.                   
                                                                                
           CLOSE BB24F04A                                                       
                 BM06K110                                                       
                 BP13L075.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 0 AND 97                                     
              DISPLAY 'CLOSING ERROR, K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS TO RETURN-CODE.                               
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
