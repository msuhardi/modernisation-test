       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C42E.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  09 OCT 2017.                                              
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  OUTPUT FILE TO CARPARK SYSEM :                *                
      *        - IF DTE-SO-APPT > 1-7 FROM SYSTEM DATE         *                
      *        - KEY-ISSED CASES                               *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP136963 FNP1 091017  NEW PROGRAM                      *                
      * BP137819 KR13 220519  NEW O/P FILE F448A WITH KEY-ISS-DT*               
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13K830     ASSIGN        TO BP13K830                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K830-KEY-FLD                    
                               ALTERNATE KEY IS K830-NUM-REGN                   
                               FILE STATUS   IS WS-K830-STATUS.                 
                                                                                
           SELECT BP13K820     ASSIGN        TO BP13K820                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K820-KEY-FLD                    
                               FILE STATUS   IS WS-K820-STATUS.                 
                                                                                
           SELECT BP13F448     ASSIGN        TO BP13F448.                       
                                                                                
           SELECT P13F448A     ASSIGN        TO P13F448A.                       
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS  2000 CHARACTERS                                    
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F448                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       COPY BP13F448.                                                           
                                                                                
       FD  P13F448A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       01  F448A-REC                PIC X(300).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K830-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K820-STATUS       PIC 9(02)   VALUE ZEROES.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F800-READ          PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K820-FND          PIC 9(7)    VALUE ZEROES.                   
           05 WS-K830-READ          PIC 9(7)    VALUE ZEROES.                   
           05 WS-F448-WRITE         PIC 9(7)    VALUE ZEROES.                   
           05 WS-F448A-WRITE        PIC 9(7)    VALUE ZEROES.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F800-EOF          PIC X       VALUE 'N'.                      
           05  WS-WRITE-448A        PIC X       VALUE 'N'.                      
                                                                                
       01  WS-WORKING-FIELDS.                                                   
           05  WS-CURRENT-DATE      PIC X(8).                                   
           05  WS-CURR-DATE         PIC 9(8).                                   
           05  WS-INT-DATE          PIC 9(7).                                   
           05  WS-START-DATE        PIC 9(8).                                   
           05  WS-END-DATE          PIC 9(8).                                   
           05  WS-NRIC              PIC X(09).                                  
           05  WS-CUST-SRCE         PIC X(09).                                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-BP13F800   THRU 1100-EXIT.                         
           PERFORM 1200-GET-CUTOFF-DATE THRU 1200-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F800-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1000-OPEN-ROUTINE.                                                       
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BP13F800                                                 
                       SY02F001                                                 
                       BP13K830                                                 
                       BP13K820                                                 
                OUTPUT BP13F448                                                 
                       P13F448A.                                                
                                                                                
           IF WS-K830-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K830 OPEN ERROR ' WS-K830-STATUS                     
              MOVE WS-K830-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K820 OPEN ERROR ' WS-K820-STATUS                     
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT            TO WS-CURRENT-DATE                  
                                               WS-CURR-DATE.                    
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1100-READ-BP13F800.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F800 AT END MOVE 'Y' TO WS-F800-EOF                         
                GO TO 1100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       1200-GET-CUTOFF-DATE.                                                    
      *------------------------------------------------------------             
           IF WS-CURR-DATE NOT NUMERIC                                          
              MOVE ZEROES    TO  WS-CURR-DATE                                   
           END-IF.                                                              
                                                                                
           COMPUTE WS-INT-DATE = FUNCTION                                       
                        INTEGER-OF-DATE(WS-CURR-DATE).                          
                                                                                
           COMPUTE WS-START-DATE = FUNCTION                                     
                        DATE-OF-INTEGER (WS-INT-DATE + 1).                      
                                                                                
           COMPUTE WS-END-DATE   = FUNCTION                                     
                        DATE-OF-INTEGER (WS-INT-DATE + 7).                      
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2000-PROCESS-RECORDS.                                                    
      *------------------------------------------------------------             
           MOVE SPACES                      TO WS-WRITE-448A                    
                                                                                
           IF F800-DTE-SO-APPT > WS-START-DATE AND                              
              F800-DTE-SO-APPT < WS-END-DATE                                    
              MOVE 'N'                      TO WS-WRITE-448A                    
              PERFORM 4000-WRITE-BP13F448 THRU 4000-EXIT                        
           END-IF.                                                              
                                                                                
           IF F800-NUM-STATUS = 'K'                                             
              MOVE 'Y'                      TO WS-WRITE-448A                    
              PERFORM 4000-WRITE-BP13F448   THRU 4000-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F800   THRU  1100-EXIT.                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       3000-READ-BP13K830.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                   TO BP13K830-REC.                       
           INITIALIZE                       BP13K830-REC.                       
                                                                                
           MOVE F800-NUM-REGN            TO K830-NUM-REGN.                      
                                                                                
           READ BP13K830 KEY IS  K830-NUM-REGN.                                 
                                                                                
           EVALUATE WS-K830-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    ADD 1          TO WS-K830-READ                              
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K830 READ ERROR ' WS-K830-STATUS               
                    DISPLAY 'K830 KEY: ' K830-NUM-REGN                          
                    MOVE WS-K830-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       3600-READ-BP13K820.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                   TO K820-KEY-FLD.                       
           MOVE F800-NUM-REGN            TO K820-NUM-REGN.                      
           MOVE WS-NRIC                  TO K820-NUM-NRIC.                      
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO CNT-K820-FND                                       
                    MOVE K820-NUM-CSTMR-SOURCE  TO WS-CUST-SRCE                 
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES                 TO WS-CUST-SRCE                 
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 READ ERROR ' WS-K820-STATUS               
                    DISPLAY 'K820 KEY: ' K820-KEY-FLD                           
                    MOVE WS-K820-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------             
       4000-WRITE-BP13F448.                                                     
      *------------------------------------------------------------             
           MOVE SPACES                     TO BP13F448-REC.                     
           INITIALIZE                         BP13F448-REC.                     
                                                                                
           MOVE F800-NUM-SCH-ACC           TO F448-NUM-SCH-ACC                  
           MOVE F800-NUM-REGN              TO F448-NUM-REGN.                    
           MOVE F800-DTE-SO-APPT           TO F448-DTE-SO-APPNT                 
           MOVE F800-DTE-BKIMAGE           TO F448-DTE-KEY-ISSUE.               
           MOVE F800-NUM-NRIC1             TO F448-NUM-NRIC1                    
                                              WS-NRIC.                          
                                                                                
           PERFORM 3600-READ-BP13K820  THRU 3600-EXIT.                          
           MOVE WS-CUST-SRCE               TO F448-NUM-CSTMR1.                  
                                                                                
           IF F800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              MOVE F800-NUM-NRIC2          TO F448-NUM-NRIC2                    
                                              WS-NRIC                           
              PERFORM 3600-READ-BP13K820  THRU 3600-EXIT                        
               MOVE WS-CUST-SRCE           TO F448-NUM-CSTMR2                   
           END-IF.                                                              
                                                                                
           IF F800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              MOVE F800-NUM-NRIC3          TO F448-NUM-NRIC3                    
                                              WS-NRIC                           
              PERFORM 3600-READ-BP13K820  THRU 3600-EXIT                        
               MOVE WS-CUST-SRCE           TO F448-NUM-CSTMR3                   
           END-IF.                                                              
                                                                                
           IF F800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              MOVE F800-NUM-NRIC4          TO F448-NUM-NRIC4                    
                                              WS-NRIC                           
              PERFORM 3600-READ-BP13K820  THRU 3600-EXIT                        
               MOVE WS-CUST-SRCE           TO F448-NUM-CSTMR4                   
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13K830  THRU 3000-EXIT.                          
                                                                                
           IF WS-K830-STATUS = 00 OR 02                                         
              IF K830-NUM-MAIL-ADDR NOT = SPACES AND LOW-VALUES                 
                 MOVE K830-NUM-MAIL-ADDR   TO F448-NUM-MAIL-ADDR                
              ELSE                                                              
                 MOVE K830-NUM-RESIDENT-ADDR    TO F448-NUM-MAIL-ADDR           
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES                  TO F448-NUM-MAIL-ADDR                
           END-IF.                                                              
                                                                                
           MOVE WS-CURRENT-DATE            TO F448-DTE-UPDATE.                  
                                                                                
           IF WS-WRITE-448A = 'Y'                                               
              WRITE F448A-REC FROM BP13F448-REC                                 
              ADD  1                        TO WS-F448A-WRITE                   
           ELSE                                                                 
              WRITE BP13F448-REC                                                
              ADD  1                        TO WS-F448-WRITE.                   
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********BP13C42E END OF JOB*******************'.         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM F800    ' WS-F800-READ.             
           DISPLAY 'NO OF RECORDS FOUND IN K820     ' CNT-K820-FND.             
           DISPLAY 'NO OF RECORDS READ FROM K830    ' WS-K830-READ.             
           DISPLAY 'NO OF RECORDS WRITTEN TO F448   ' WS-F448-WRITE.            
           DISPLAY 'NO OF RECORDS WRITTEN TO F448A  ' WS-F448A-WRITE.           
                                                                                
           CLOSE BP13F800                                                       
                 BP13K830                                                       
                 BP13K820                                                       
                 BP13F448                                                       
                 P13F448A.                                                      
                                                                                
           IF WS-K830-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K830 CLOSE ERROR ' WS-K830-STATUS                    
              MOVE WS-K830-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K820 CLOSE ERROR ' WS-K820-STATUS                    
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
