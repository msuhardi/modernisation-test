       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C50G.                                                 
      *AUTHOR.        KATHRINA VALENZUELA.                                      
      *DATE-WRITTEN.  12/09/2019.                                               
      **************************************************************            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      **************************************************************            
      * OBJECTIVE : TO FORMAT AND SEND EMAIL ALERT TO CRMS THE     *            
      *             REPORT ON DEATH-CASES AND RENUNCIATION OF      *            
      *             PR CASES                                       *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- -----------                         *            
      * BP137791 20190912 KV6   NEW PGM                            *            
      **************************************************************            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT P13L904S         ASSIGN P13L904S.                             
                                                                                
           SELECT P13L904M         ASSIGN P13L904M.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   P13L904S           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 300 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       01  P13L904S-REC.                                                        
            05  L904S-NUM-SNO            PIC ZZZZZZ9.                           
            05  FILLER1                  PIC X(01).                             
            05  L904S-NUM-REGN           PIC X(08).                             
            05  FILLER2                  PIC X(01).                             
            05  L904S-HDB-REF            PIC X(11).                             
            05  FILLER18                 PIC X(01).                             
            05  L904S-NUM-ZONE           PIC X(04).                             
            05  FILLER3                  PIC X(01).                             
            05  L904S-NUM-NT             PIC X(03).                             
            05  FILLER4                  PIC X(01).                             
            05  L904S-NUM-FT             PIC X(02).                             
            05  FILLER5                  PIC X(01).                             
            05  L904S-NRIC               PIC X(09).                             
            05  FILLER6                  PIC X(01).                             
            05  L904S-NAME               PIC X(66).                             
            05  FILLER7                  PIC X(01).                             
            05  L904S-NUM-STAT           PIC X(09).                             
            05  FILLER8                  PIC X(01).                             
            05  L904S-DTE-DEATH          PIC X(10).                             
            05  FILLER9                  PIC X(01).                             
            05  L904S-DTE-RENUN          PIC X(10).                             
            05  FILLER10                 PIC X(01).                             
            05  L904S-DTE-SALE-AGMT      PIC X(10).                             
            05  FILLER11                 PIC X(01).                             
            05  L904S-DTE-FILE-RCVD      PIC X(10).                             
            05  FILLER12                 PIC X(01).                             
            05  L904S-DTE-BOOK           PIC X(10).                             
            05  FILLER12A                PIC X(01).                             
            05  L904S-DTE-BALLOT         PIC X(08).                             
            05  FILLER13                 PIC X(01).                             
            05  L904S-PRE-NUM-OIC        PIC X(07).                             
            05  FILLER14                 PIC X(01).                             
            05  L904S-PRE-NME-OIC        PIC X(30).                             
            05  FILLER15                 PIC X(01).                             
            05  L904S-POS-NUM-OIC        PIC X(08).                             
            05  FILLER16                 PIC X(01).                             
            05  L904S-POS-NME-OIC        PIC X(30).                             
            05  FILLER17                 PIC X(01).                             
            05  L904S-STATUS             PIC X(29).                             
                                                                                
                                                                                
       FD  P13L904M                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 300 CHARACTERS.                                      
       01  P13L904M-REC                PIC X(300).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FLAG-N-STATUS.                                                    
           05  WS-L904S-EOF-FLAG            PIC X(01)  VALUE 'N'.               
               88  WS-L904S-EOF             VALUE 'Y'.                          
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-PREV-PRE-OIC              PIC X(5)   VALUE SPACES.            
           05  WS-PREV-POS-OIC              PIC X(5)   VALUE SPACES.            
           05  WS-MAIL-OIC                  PIC X(5)   VALUE SPACES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-L904S-READ                PIC 9(05)  VALUE ZEROES.            
           05  WS-L904M-WRTN                PIC 9(05)  VALUE ZEROES.            
           05  WS-NUM-SNO-L904M             PIC 9(04)  VALUE ZEROES.            
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
                                                                                
      *---------------------------------------------------------------*         
      *        MAIL HEADER                                            *         
      *---------------------------------------------------------------*         
       01  MAIL-HDR-HELO.                                                       
           05  FILLER PIC X(300) VALUE 'HELO SGPHDB1'.                          
                                                                                
       01  MAIL-HDR-MAIL-FROM.                                                  
           05  FILLER PIC X(300) VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.              
                                                                                
       01  MAIL-HDR-MAIL-RCPT-TO.                                               
           05  FILLER PIC X(300) VALUE SPACES.                                  
                                                                                
       01  MAIL-HDR-MAIL-RCPT2.                                                 
           05  FILLER PIC X(300) VALUE 'RCPT TO:<SITI_N_SUMSURI@HDB.GOV.        
      -      'SG>'.                                                             
                                                                                
       01  MAIL-HDR-DATA.                                                       
           05  FILLER PIC X(300) VALUE 'DATA'.                                  
                                                                                
       01  MAIL-HDR-FROM.                                                       
           05  FILLER PIC X(300) VALUE 'FROM:Soc System - Email Alert'.         
                                                                                
       01  MAIL-HDR-MAIL-TO.                                                    
           05  FILLER PIC X(300) VALUE SPACES.                                  
                                                                                
       01  MAIL-HDR-MAIL-TO-CC1.                                                
           05  FILLER PIC X(300)                                                
               VALUE 'CC:<Siti_N_SUMSURI@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-SUBJECT-L904M.                                                  
           05  FILLER PIC X(300) VALUE 'SUBJECT : Report on death-cases         
      -        'and renunciation of PR cases (exclude cancelled and with        
      -        ' key-issued)'.                                                  
                                                                                
       01  MAIL-BODY-L904M             PIC X(300) VALUE SPACES.                 
                                                                                
       01  MAIL-END-L904M              PIC X(300) VALUE SPACES.                 
                                                                                
       01  MAIL-HDR-DATE.                                                       
           05  FILLER                  PIC X(6)   VALUE 'DATE: '.               
           05  MAIL-DATE               PIC X(10)  VALUE SPACES.                 
           05  FILLER                  PIC X(284) VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(300) VALUE SPACES.                 
                                                                                
      *---------------------------------------------------------------*         
      *                      REPORT DETAILS                           *         
      *---------------------------------------------------------------*         
       01  L904M-HEADER.                                                        
           05  FILLER               PIC X(105)   VALUE SPACES.                  
           05  FILLER               PIC X(91)   VALUE                           
                       'REPORT ON DEATH-CASES AND RENUNCIATION OF PR CAS        
      -         'ES (EXCLUDE CANCELLED AND WITH KEY ISSUED)'.                   
           05  FILLER               PIC X(88)   VALUE SPACES.                   
           05  FILLER               PIC X(6)    VALUE 'DATE: '.                 
           05  L904M-NUM-DTE        PIC X(10)   VALUE SPACES.                   
                                                                                
       01  L904M-LINE               PIC X(300)  VALUE ALL '-'.                  
                                                                                
       01  L904M-HEADER1.                                                       
           05  FILLER               PIC X(07)  VALUE 'S/NO.'.                   
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(08)  VALUE 'REGN'.                    
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(11)  VALUE 'HDB REF'.                 
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(04)  VALUE 'ZONE'.                    
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(03)  VALUE 'NT'.                      
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(02)  VALUE 'FT'.                      
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(09)  VALUE 'NRIC'.                    
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(66)  VALUE 'NAME'.                    
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(09)  VALUE 'HA/OCC   '.               
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(10)  VALUE 'DTE DEATH'.               
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(10)  VALUE 'D-RENOUNCE'.              
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(10)  VALUE 'DTE SA'.                  
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(10)  VALUE 'FILE RCVD '.              
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(10)  VALUE 'DTE BOOK  '.              
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(08)  VALUE 'DTE BAL'.                 
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(07)  VALUE 'PRE OIC'.                 
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(30)  VALUE 'NAME PRE OIC'.            
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(08)  VALUE 'POST OIC'.                
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(30)  VALUE 'NAME POST OIC'.           
           05  FILLER               PIC X(01)  VALUE ';'.                       
           05  FILLER               PIC X(10)  VALUE 'STATUS  '.                
           05  FILLER               PIC X(01)  VALUE ';'.                       
                                                                                
       01  L904M-DETAIL1.                                                       
           05  L904M-SNO            PIC ZZZ9    VALUE ZEROES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L904M-REGN           PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L904M-OIC            PIC X(05)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L904M-SCH-ACC        PIC X(11)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L904M-FLAT-TYPE      PIC X(02)   VALUE SPACES.                   
           05  FILLER               PIC X(04)   VALUE SPACES.                   
           05  L904M-DTE-BOOK       PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L904M-ELIG-SCH       PIC X(03)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  L904M-ALLO-SCH       PIC X(03)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE SPACES.                   
           05  L904M-AP-NRIC        PIC X(09)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L904M-AP-NAME        PIC X(20)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  L904M-MARITAL-ST     PIC X(01)   VALUE SPACES.                   
           05  FILLER               PIC X(07)   VALUE SPACES.                   
           05  L904M-NUM-MARRIAGE   PIC X(15)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L904M-DTE-MARRIAGE   PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L904M-SP-NRIC        PIC X(09)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L904M-SP-NAME        PIC X(20)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L904M-SP-IDTYPE      PIC X(03)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE SPACES.                   
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13CCNT                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC-CCNT.                                                    
           05  WS-LINK-CNTCT-TYPE        PIC X(3).                              
           05  WS-LINK-STAFF-NICK        PIC X(6).                              
           05  WS-LINK-STAFF-NUM         PIC X(6).                              
           05  WS-LINK-STAFF-CNTCT       PIC X(50).                             
           05  WS-LINK-SYSERR            PIC 9(4).                              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-P13L904S       THRU 2000-EXIT.                     
           PERFORM 3000-MAIN-ROUTINE        THRU 3000-EXIT                      
                   UNTIL WS-L904S-EOF.                                          
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT P13L904S                                                  
               OUTPUT P13L904M.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8               TO  WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8            TO  WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10                TO  WS-DTE-CURR-10.                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-CHECK-EMAIL.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO WS-LINK-REC-CCNT.                
           MOVE WS-MAIL-OIC                 TO WS-LINK-STAFF-NICK.              
           MOVE 'EML'                       TO WS-LINK-CNTCT-TYPE.              
                                                                                
           PERFORM 8000-CALL-BP13CCNT       THRU 8000-EXIT.                     
                                                                                
           MOVE SPACES                      TO MAIL-HDR-MAIL-RCPT-TO            
                                               MAIL-HDR-MAIL-TO.                
                                                                                
           STRING 'RCPT TO:<'          DELIMITED BY SIZE                        
                   WS-LINK-STAFF-CNTCT DELIMITED BY SPACE                       
                  '>'                  DELIMITED BY SIZE                        
                   INTO MAIL-HDR-MAIL-RCPT-TO.                                  
                                                                                
           STRING 'TO:<'               DELIMITED BY SIZE                        
                   WS-LINK-STAFF-CNTCT DELIMITED BY SPACE                       
                  '>'                  DELIMITED BY SIZE                        
                   INTO MAIL-HDR-MAIL-TO.                                       
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1200-PRINT-L904M-HEADER.                                                 
      *-------------------------------------------------------------            
           MOVE WS-DTE-CURR-10              TO MAIL-DATE                        
                                               L904M-NUM-DTE.                   
                                                                                
           WRITE P13L904M-REC               FROM MAIL-HDR-HELO.                 
           WRITE P13L904M-REC               FROM MAIL-HDR-MAIL-FROM.            
           WRITE P13L904M-REC               FROM MAIL-HDR-MAIL-RCPT-TO.         
           WRITE P13L904M-REC               FROM MAIL-HDR-MAIL-RCPT2.           
           WRITE P13L904M-REC               FROM MAIL-HDR-DATA.                 
           WRITE P13L904M-REC               FROM MAIL-HDR-FROM.                 
           WRITE P13L904M-REC               FROM MAIL-HDR-MAIL-TO.              
           WRITE P13L904M-REC               FROM MAIL-HDR-MAIL-TO-CC1.          
           WRITE P13L904M-REC               FROM MAIL-SUBJECT-L904M.            
           WRITE P13L904M-REC               FROM MAIL-HDR-DATE.                 
                                                                                
           WRITE P13L904M-REC               FROM MAIL-SPACES.                   
           WRITE P13L904M-REC               FROM MAIL-SPACES.                   
                                                                                
           WRITE P13L904M-REC               FROM L904M-HEADER.                  
           WRITE P13L904M-REC               FROM MAIL-SPACES.                   
                                                                                
           WRITE P13L904M-REC               FROM L904M-LINE.                    
           WRITE P13L904M-REC               FROM L904M-HEADER1.                 
           WRITE P13L904M-REC               FROM L904M-LINE.                    
           WRITE P13L904M-REC               FROM MAIL-SPACES.                   
                                                                                
           MOVE ZEROES                      TO WS-NUM-SNO-L904M.                
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-P13L904S.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ P13L904S                                                        
              AT END MOVE 'Y'               TO WS-L904S-EOF-FLAG.               
                                                                                
           IF WS-L904S-EOF-FLAG NOT = 'Y'                                       
              ADD 1                         TO WS-L904S-READ                    
           ELSE                                                                 
              WRITE P13L904M-REC            FROM MAIL-SPACES                    
              WRITE P13L904M-REC            FROM MAIL-SPACES                    
              WRITE P13L904M-REC            FROM MAIL-END                       
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF L904S-POS-NUM-OIC = SPACES OR LOW-VALUES OR ZEROES                
              IF L904S-PRE-NUM-OIC NOT = SPACES AND LOW-VALUES                  
                                                AND ZEROES                      
                 IF L904S-PRE-NUM-OIC NOT = WS-PREV-PRE-OIC                     
                    IF WS-PREV-PRE-OIC NOT = SPACES                             
                       WRITE P13L904M-REC           FROM MAIL-SPACES            
                       WRITE P13L904M-REC           FROM MAIL-SPACES            
                                                                                
                       WRITE P13L904M-REC           FROM MAIL-END               
                    END-IF                                                      
                    MOVE SPACES                     TO WS-MAIL-OIC              
                    MOVE L904S-PRE-NUM-OIC          TO WS-MAIL-OIC              
                    PERFORM 1100-CHECK-EMAIL        THRU 1100-EXIT              
                    PERFORM 1200-PRINT-L904M-HEADER THRU 1200-EXIT              
                    MOVE SPACES                     TO WS-PREV-PRE-OIC          
                    MOVE L904S-PRE-NUM-OIC          TO WS-PREV-PRE-OIC          
                 END-IF                                                         
                 PERFORM 3100-WRITE-L904M-REPORT    THRU 3100-EXIT              
              END-IF                                                            
           ELSE                                                                 
              IF L904S-POS-NUM-OIC NOT = WS-PREV-POS-OIC                        
                 IF WS-PREV-POS-OIC NOT = SPACES                                
                    WRITE P13L904M-REC           FROM MAIL-SPACES               
                    WRITE P13L904M-REC           FROM MAIL-SPACES               
                                                                                
                    WRITE P13L904M-REC           FROM MAIL-END                  
                 END-IF                                                         
                 MOVE SPACES                     TO WS-MAIL-OIC                 
                 MOVE L904S-POS-NUM-OIC          TO WS-MAIL-OIC                 
                 PERFORM 1100-CHECK-EMAIL        THRU 1100-EXIT                 
                 PERFORM 1200-PRINT-L904M-HEADER THRU 1200-EXIT                 
                 MOVE SPACES                     TO WS-PREV-POS-OIC             
                 MOVE L904S-POS-NUM-OIC          TO WS-PREV-POS-OIC             
              END-IF                                                            
              PERFORM 3100-WRITE-L904M-REPORT    THRU 3100-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-P13L904S  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-WRITE-L904M-REPORT.                                                 
      *---------------------------------------------------------------*         
           INITIALIZE P13L904M-REC.                                             
           MOVE SPACES                      TO P13L904M-REC.                    
           ADD 1                            TO WS-L904M-WRTN                    
                                               WS-NUM-SNO-L904M                 
                                                                                
           MOVE WS-NUM-SNO-L904M            TO L904S-NUM-SNO.                   
                                                                                
           WRITE P13L904M-REC               FROM P13L904S-REC.                  
           WRITE P13L904M-REC               FROM MAIL-SPACES.                   
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       8000-CALL-BP13CCNT.                                                      
      *------------------------------------------------------------             
                                                                                
           CALL 'BP13CCNT' USING  WS-LINK-REC-CCNT.                             
                                                                                
           EVALUATE WS-LINK-SYSERR                                              
           WHEN 000                                                             
              CONTINUE                                                          
           WHEN 100                                                             
              DISPLAY 'EMAIL ADDRESS NOT FOUND FOR ' WS-MAIL-OIC                
              STRING  WS-MAIL-OIC DELIMITED BY SPACE                            
                     '@hdb.gov.sg' DELIMITED BY SIZE                            
                                           INTO WS-LINK-STAFF-CNTCT             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13CCNT ERROR STATUS : ' WS-LINK-SYSERR                 
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '------- P13L904M -------'.                                  
           DISPLAY ' RECORDS READ    IN P13L904S : ' WS-L904S-READ.             
           DISPLAY ' RECORDS WRITTEN IN P13L904M : ' WS-L904M-WRTN.             
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE P13L904S                                                       
                 P13L904M.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
