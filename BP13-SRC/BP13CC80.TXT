       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC80.                                                 
      *AUTHOR.        ED1.                                                      
      *DATE-WRITTEN.  18/05/2007.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO MATCH CPFB REFUND REPLY FILE AND                *        
      *             DELETE CESSATION RECORDS IN SOC TRANS FILE         *        
      *                                                                *        
      * INPUT  FILES : AB02F835                                        *        
      *                BP13FD92                                        *        
      *                BP13K800                                        *        
      *                BP13K820                                        *        
      *                SY02F001                                        *        
      * I-O    FILE  : BP13KD92                                        *        
      * OUTPUT FILE  : BP13LC80.                                       *        
      *                                                                *        
      * CHG-NO    BY    ON    - DESCRIPTION                            *        
      * ---------------------------------                              *        
      * BP133052 ED1 18/05/07   NEW PROGRAM                            *        
      * BP133095 ED1 19/06/07   ADDED PRINTING OF LETTER               *        
      * BP133171 ED1 02/11/07   REMOVED UPD OF KD94-DTE&TME UPDATE     *        
      * BP133686 JB8 28/12/09   REMOVE RECTIFY DISPLAY OF ADDRESS      *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT   AB02F835      ASSIGN TO AB02F835.                           
           SELECT   BP13FD92      ASSIGN TO BP13FD92.                           
           SELECT   BP13LC80      ASSIGN TO BP13LC80.                           
           SELECT   BP13PC80      ASSIGN TO BP13PC80.                           
           SELECT   SY02F001      ASSIGN TO SY02F001.                           
                                                                                
           SELECT BP13KD92 ASSIGN TO BP13KD92                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD92-KEY-FLD                                    
                FILE STATUS  IS WS-KD92-STATUS.                                 
                                                                                
           SELECT BP13KD94 ASSIGN TO BP13KD94                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD94-KEY-FLD                                    
                FILE STATUS  IS WS-KD94-STATUS.                                 
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K800-NUM-REGN                                   
                FILE STATUS  IS WS-K800-STATUS.                                 
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K820-KEY-FLD                                    
                FILE STATUS  IS WS-K820-STATUS.                                 
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K830-KEY-FLD                                    
                ALTERNATE RECORD KEY IS K830-NUM-REGN                           
                FILE STATUS  IS WS-K830-STATUS.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   AB02F835                RECORD CONTAINS 300 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY AB02F835.                                                           
                                                                                
       FD   BP13FD92                RECORD CONTAINS 1000 CHARACTERS             
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13FD92.                                                           
                                                                                
       FD   BP13KD92    RECORD CONTAINS 1000 CHARACTERS.                        
       COPY BP13KD92.                                                           
                                                                                
       FD   BP13KD94    RECORD CONTAINS 1000 CHARACTERS.                        
       COPY BP13KD94.                                                           
                                                                                
       FD   BP13K800    RECORD CONTAINS 2000 CHARACTERS.                        
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820    RECORD CONTAINS 400  CHARACTERS.                        
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830    RECORD CONTAINS 250  CHARACTERS.                        
       COPY BP13K830.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13LC80    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 132 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  LC80-PRTREC  PIC X(132).                                             
                                                                                
       FD  BP13PC80     RECORD CONTAINS 90 CHARACTERS                           
                        RECORDING MODE IS F.                                    
       01  PC80-PRTREC  PIC X(90).                                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-KD92-STATUS                PIC 9(02) VALUE 0.                     
       01  WS-KD94-STATUS                PIC 9(02) VALUE 0.                     
       01  WS-K800-STATUS                PIC 9(02) VALUE 0.                     
       01  WS-K820-STATUS                PIC 9(02) VALUE 0.                     
       01  WS-K830-STATUS                PIC 9(02) VALUE 0.                     
       01  WS-MAX-LINE                   PIC 9(02) VALUE 58.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F835-READ                 PIC 9(07) VALUE ZEROS.              
           05  WS-FD92-READ                 PIC 9(07) VALUE ZEROS.              
           05  WS-KD92-DEL                  PIC 9(07) VALUE ZEROS.              
           05  WS-MATCH-REGN                PIC 9(07) VALUE ZEROS.              
           05 WS-SNO                        PIC 9(05) VALUE ZEROS.              
           05 WS-PAGE-CTR                   PIC 9(04) VALUE ZEROS.              
           05 WS-PRNT-CTR                   PIC 9(05) VALUE ZEROS.              
           05 WS-LINE-CTR                   PIC 9(02) VALUE ZEROS.              
           05 WS-CTR                        PIC 9     VALUE ZERO.               
                                                                                
       01  WS-NAMES.                                                            
           05  WS-NAME-X OCCURS 4 TIMES.                                        
               10  WS-NAME                 PIC X(66) VALUE SPACES.              
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-DD                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-MM                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-CCYY                 PIC 9(04) VALUE ZEROES.              
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                  PIC X(08) VALUE SPACES.             
           05  WS-SYS-TIME                  PIC X(08) VALUE SPACES.             
           05  FILLER                       PIC X(05) VALUE SPACES.             
                                                                                
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                        01010000
       01  WS-BLANK-LINE  PIC X(132)             VALUE SPACES.                  
                                                                                
       01  WS-DASH-LINE   PIC X(132)             VALUE ALL '-'.                 
                                                                                
       01  LC80-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LC80'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC80-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC80-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  LC80-HEADER02.                                                       
           05  FILLER              PIC X(45)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'REPORT ON CESSATION OF CPF DEDUCTION'.                          
                                                                                
       01  LC80-HEADER03.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'SCH-ACC'.               
           05  FILLER              PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'CPF1'.                  
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'CPF2'.                  
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'CPF3'.                  
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'CPF4'.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(16)     VALUE                          
               'CESSATION STATUS'.                                              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'EFF-DTE'.               
                                                                                
       01 LC80-DETAILS.                                                         
          05 LC80-DTL-SERIAL      PIC ZZZZ9     VALUE ZEROES.                   
          05 FILLER               PIC X(01)     VALUE SPACES.                   
          05 LC80-REGNO           PIC X(08)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC80-SCH-ACC         PIC X(14)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC80-CPF1            PIC X(09)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC80-CPF2            PIC X(09)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC80-CPF3            PIC X(09)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC80-CPF4            PIC X(09)     VALUE SPACES.                   
          05 FILLER               PIC X(05)     VALUE SPACES.                   
          05 LC80-REMARK          PIC X(09)     VALUE SPACES.                   
          05 FILLER               PIC X(05)     VALUE SPACES.                   
          05 LC80-EFF-DTE         PIC X(10)     VALUE SPACES.                   
                                                                                
      *--> DETAIL LINES FOR LETTER                                              
                                                                                
       01  PC80-PR-LINE-1.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC80-HDBREF              PIC X(14)  VALUE SPACES.                
                                                                                
       01  PC80-PR-LINE-2.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC80-REGNO               PIC X(08)  VALUE SPACES.                
                                                                                
       01  PC80-PR-LINE-3.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC80-DATE-HDR            PIC X(10)  VALUE SPACES.                
                                                                                
       01  PC80-PR-LINE-4.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC80-NAME1               PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC80-PR-LINE-5.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC80-NAME2               PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC80-PR-LINE-6.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC80-NAME3               PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC80-PR-LINE-7.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC80-NAME4               PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC80-PR-LINE-8.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC80-BLK-UNIT            PIC X(28)  VALUE SPACES.                
                                                                                
       01  PC80-PR-LINE-9.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC80-ADDR1               PIC X(65)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
                                                                                
       01  PC80-PR-LINE-10.                                                     
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC80-ADDR2               PIC X(65)  VALUE SPACES.                
                                                                                
       01  PC80-PR-LINE-11.                                                     
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  PC80-BAR-CODE            PIC X(09)  VALUE SPACES.                
                                                                                
       01  PC80-PR-LINE-12.                                                     
           05  FILLER                   PIC X(11)  VALUE SPACES.                
           05  PC80-FLAT-ADDR           PIC X(70)  VALUE SPACES.                
                                                                                
       01  PC80-PR-LINE-13.                                                     
           05  FILLER                   PIC X(51)  VALUE SPACES.                
           05  PC80-EFF-DTE             PIC X(10)  VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(04).                             
           05 WS-LINK-NUM-ACC            PIC X(04).                             
           05 WS-LINK-NUM-CHK            PIC X(01).                             
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-AB02F835       THRU 2000-EXIT.                     
           PERFORM 2100-READ-BP13FD92       THRU 2100-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL F835-NUM-HDB-REF = HIGH-VALUES AND                           
                   FD92-NUM-SCH-ACC = HIGH-VALUES.                              
           PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT AB02F835                                                  
                      BP13FD92                                                  
                      BP13K800                                                  
                      BP13K820                                                  
                      BP13K830                                                  
                      SY02F001                                                  
               I-O    BP13KD92                                                  
                      BP13KD94                                                  
               OUTPUT BP13LC80                                                  
                      BP13PC80.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
           MOVE WS-SYS-DATE(1:4)      TO WS-CUR-CCYY.                           
           MOVE WS-SYS-DATE(5:2)      TO WS-CUR-MM.                             
           MOVE WS-SYS-DATE(7:2)      TO WS-CUR-DD.                             
                                                                                
           IF WS-KD92-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD92 FILE ERROR ' WS-KD92-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-KD94-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD94 FILE ERROR ' WS-KD94-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 FILE ERROR ' WS-K800-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 FILE ERROR ' WS-K820-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K830 FILE ERROR ' WS-K830-STATUS             
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-AB02F835.                                                      
      *-------------------------------------------------------------            
           READ AB02F835                                                        
             AT END MOVE HIGH-VALUES TO F835-NUM-HDB-REF                        
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD  1                    TO WS-F835-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13FD92.                                                      
      *-------------------------------------------------------------            
           READ BP13FD92                                                        
             AT END MOVE HIGH-VALUES TO  FD92-NUM-SCH-ACC                       
             GO TO 2100-EXIT.                                                   
                                                                                
           ADD  1                    TO  WS-FD92-READ.                          
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
           IF F835-NUM-HDB-REF = FD92-NUM-SCH-ACC                               
              PERFORM 3100-PROCESS-MATCHED   THRU 3100-EXIT                     
              PERFORM 2000-READ-AB02F835     THRU 2000-EXIT                     
              PERFORM 2100-READ-BP13FD92     THRU 2100-EXIT                     
           ELSE                                                                 
              IF F835-NUM-HDB-REF > FD92-NUM-SCH-ACC                            
                 PERFORM 2100-READ-BP13FD92  THRU 2100-EXIT                     
              ELSE                                                              
                 PERFORM 2000-READ-AB02F835  THRU 2000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-PROCESS-MATCHED.                                                    
      *-------------------------------------------------------------            
           ADD 1  TO   WS-MATCH-REGN.                                           
                                                                                
           PERFORM 3200-READ-BP13KD92      THRU 3200-EXIT.                      
                                                                                
           IF F835-TXT-REJECT-REMARK = SPACES OR LOW-VALUES OR ZEROS            
              MOVE 'A'           TO  KD92-NUM-CEASE-STATUS                      
              PERFORM 3600-PROCESS-LETTER THRU 3600-EXIT                        
           ELSE                                                                 
              MOVE 'R'           TO  KD92-NUM-CEASE-STATUS                      
           END-IF.                                                              
                                                                                
           PERFORM 3300-WRITE-BP13KD94    THRU 3300-EXIT.                       
           PERFORM 3400-DELETE-BP13KD92   THRU 3400-EXIT.                       
           PERFORM 3500-WRITE-REPORT      THRU 3500-EXIT.                       
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       3200-READ-BP13KD92.                                                      
      *--------------------------------------------------------------           
           MOVE SPACES        TO BP13KD92-REC.                                  
           MOVE FD92-KEY-FLD  TO KD92-KEY-FLD.                                  
           READ BP13KD92.                                                       
                                                                                
           IF WS-KD92-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-KD92-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN KD92, STATUS ' WS-KD92-STATUS           
                        ', REGN IS ' KD92-NUM-REGN                              
             ELSE                                                               
                DISPLAY 'ERROR READING KD92, STATUS ' WS-KD92-STATUS            
                        ', REGN IS ' KD92-NUM-REGN                              
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-WRITE-BP13KD94.                                                     
      *-------------------------------------------------------------            
           INITIALIZE                  BP13KD94-REC.                            
           MOVE SPACES              TO BP13KD94-REC.                            
           MOVE BP13KD92-REC        TO BP13KD94-REC.                            
           MOVE KD92-NUM-REGN       TO KD94-NUM-REGN-LOG.                       
           MOVE KD92-NUM-TRANS      TO KD94-NUM-TRANS-LOG.                      
           MOVE 'BP13CC80'          TO KD94-NUM-USERID.                         
                                                                                
           WRITE BP13KD94-REC.                                                  
                                                                                
           EVALUATE WS-KD94-STATUS                                              
           WHEN 00                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
               DISPLAY 'BP13KD94 - ERROR IN WRITE, STATUS: '                    
                        WS-KD94-STATUS ' KEY ' KD94-KEY-FLD                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3400-DELETE-BP13KD92.                                                    
      *-------------------------------------------------------------            
           DELETE BP13KD92.                                                     
                                                                                
           EVALUATE WS-KD92-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-KD92-DEL                                            
           WHEN OTHER                                                           
               DISPLAY 'BP13KD92 - ERROR IN DELETE, STATUS: '                   
                        WS-KD92-STATUS ' KEY ' KD92-KEY-FLD                     
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-WRITE-REPORT.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES  TO LC80-DETAILS.                                        
           INITIALIZE      LC80-DETAILS.                                        
                                                                                
           STRING F835-NUM-HDB-REF(1:4) '-'                                     
                  F835-NUM-HDB-REF(5:4) '-'                                     
                  F835-NUM-HDB-REF(9:1) '-'                                     
                  F835-NUM-HDB-REF(10:2)                                        
                  DELIMITED BY SIZE INTO LC80-SCH-ACC.                          
                                                                                
           MOVE KD92-NUM-REGN               TO LC80-REGNO.                      
           MOVE FD92-NUM-CPF(1)             TO LC80-CPF1.                       
           MOVE FD92-NUM-CPF(2)             TO LC80-CPF2.                       
           MOVE FD92-NUM-CPF(3)             TO LC80-CPF3.                       
           MOVE FD92-NUM-CPF(4)             TO LC80-CPF4.                       
                                                                                
           IF F835-TXT-REJECT-REMARK = SPACES OR LOW-VALUES OR ZEROS            
              MOVE 'ACCEPTED'     TO  LC80-REMARK                               
           ELSE                                                                 
              MOVE 'REJECTED'     TO  LC80-REMARK                               
           END-IF.                                                              
                                                                                
           STRING FD92-DTE-REVSD-INSTAL-EFF(7:2) '/'                            
                  FD92-DTE-REVSD-INSTAL-EFF(5:2) '/'                            
                  FD92-DTE-REVSD-INSTAL-EFF(1:4)                                
                  DELIMITED BY SIZE INTO LC80-EFF-DTE.                          
                                                                                
           ADD   1                TO  WS-SNO.                                   
           MOVE  WS-SNO           TO  LC80-DTL-SERIAL.                          
           MOVE  WS-CURRENT-DATE  TO  LC80-DATE-HDR.                            
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO     = 1                                                    
              ADD          1   TO WS-PAGE-CTR                                   
              MOVE ZEROES      TO LC80-PAGE-HDR                                 
              MOVE WS-PAGE-CTR TO LC80-PAGE-HDR                                 
              PERFORM 3550-PRINT-HEADING THRU 3550-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC80-PRTREC FROM LC80-DETAILS.                                 
                                                                                
           ADD   1   TO   WS-LINE-CTR.                                          
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3550-PRINT-HEADING.                                                      
      *-------------------------------------------------------------            
           WRITE LC80-PRTREC FROM WS-BLANK-LINE AFTER PAGE.                     
           WRITE LC80-PRTREC FROM WS-BLANK-LINE.                                
           WRITE LC80-PRTREC FROM WS-BLANK-LINE.                                
           WRITE LC80-PRTREC FROM LC80-HEADER01.                                
           WRITE LC80-PRTREC FROM LC80-HEADER02.                                
           WRITE LC80-PRTREC FROM WS-BLANK-LINE.                                
           WRITE LC80-PRTREC FROM LC80-HEADER03.                                
           WRITE LC80-PRTREC FROM WS-DASH-LINE.                                 
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE  8 TO WS-LINE-CTR.                                              
                                                                                
       3550-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3600-PROCESS-LETTER.                                                     
      *----------------------------------- ---------------------------          
           PERFORM 3610-INIT-FIELDS       THRU 3610-EXIT.                       
           PERFORM 3700-READ-BP13K800     THRU 3700-EXIT.                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              PERFORM 3800-GET-NAMES      THRU 3800-EXIT                        
              PERFORM 3810-GET-CORR-ADDR  THRU 3810-EXIT                        
              PERFORM 3820-GET-FLAT-ADDR  THRU 3820-EXIT                        
              PERFORM 4000-PRINT-LETTER   THRU 4000-EXIT                        
           END-IF.                                                              
                                                                                
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3610-INIT-FIELDS.                                                        
      *--------------------------------------------------------------           
           MOVE SPACES  TO PC80-PR-LINE-1                                       
                           PC80-PR-LINE-2                                       
                           PC80-PR-LINE-3                                       
                           PC80-PR-LINE-4                                       
                           PC80-PR-LINE-5                                       
                           PC80-PR-LINE-6                                       
                           PC80-PR-LINE-7                                       
                           PC80-PR-LINE-8                                       
                           PC80-PR-LINE-9                                       
                           PC80-PR-LINE-10                                      
                           PC80-PR-LINE-11                                      
                           PC80-PR-LINE-12                                      
                           PC80-PR-LINE-13.                                     
       3610-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3700-READ-BP13K800.                                                      
      *--------------------------------------------------------------           
      *    RETRIEVE OCCUPIERS NRIC1 & NRIC2 FROM FROM BP13K820    *             
      *-----------------------------------------------------------*             
           MOVE SPACES        TO BP13K800-MASTER.                               
           MOVE KD92-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              CONTINUE                                                          
                                                                                
           ELSE                                                                 
             IF WS-K800-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K800, STATUS ' WS-K800-STATUS           
                        ', REGN IS ' KD92-NUM-REGN                              
             ELSE                                                               
                DISPLAY 'ERROR READING K800, STATUS ' WS-K800-STATUS            
                        ', REGN IS ' KD92-NUM-REGN                              
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3800-GET-NAMES.                                                          
      *--------------------------------------------------------------           
           MOVE 0            TO WS-CTR.                                         
           MOVE SPACES       TO WS-NAMES.                                       
                                                                                
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC1        TO   K820-NUM-NRIC                     
              MOVE KD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 3801-READ-BP13K820 THRU 3801-EXIT                         
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC2        TO   K820-NUM-NRIC                     
              MOVE KD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 3801-READ-BP13K820 THRU 3801-EXIT                         
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC3        TO   K820-NUM-NRIC                     
              MOVE KD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 3801-READ-BP13K820 THRU 3801-EXIT                         
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC4        TO   K820-NUM-NRIC                     
              MOVE KD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 3801-READ-BP13K820 THRU 3801-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-CTR = 4                                                        
              MOVE WS-NAME(1) TO PC80-NAME1                                     
              MOVE WS-NAME(2) TO PC80-NAME2                                     
              MOVE WS-NAME(3) TO PC80-NAME3                                     
              MOVE WS-NAME(4) TO PC80-NAME4                                     
           ELSE                                                                 
           IF WS-CTR = 3                                                        
              MOVE WS-NAME(1) TO PC80-NAME2                                     
              MOVE WS-NAME(2) TO PC80-NAME3                                     
              MOVE WS-NAME(3) TO PC80-NAME4                                     
           ELSE                                                                 
           IF WS-CTR = 2                                                        
              MOVE WS-NAME(1) TO PC80-NAME3                                     
              MOVE WS-NAME(2) TO PC80-NAME4                                     
           ELSE                                                                 
           IF WS-CTR = 1                                                        
              MOVE WS-NAME(1) TO PC80-NAME4                                     
           END-IF.                                                              
                                                                                
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3801-READ-BP13K820.                                                      
      *--------------------------------------------------------------           
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              ADD   1             TO WS-CTR                                     
              MOVE  K820-NME-OCCP TO WS-NAME(WS-CTR)                            
                                                                                
           ELSE                                                                 
                                                                                
             MOVE SPACES TO K820-NME-OCCP                                       
                                                                                
             IF WS-K820-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K820, STATUS ' WS-K820-STATUS           
                        ', KEY IS ' KD92-NUM-REGN ' ' K820-NUM-NRIC             
             ELSE                                                               
                DISPLAY 'ERROR READING K820, STATUS ' WS-K820-STATUS            
                        ', KEY IS ' KD92-NUM-REGN ' ' K820-NUM-NRIC             
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       3801-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3810-GET-CORR-ADDR.                                                      
      *-----------------------------------------------------------*             
           MOVE KD92-NUM-REGN          TO K830-NUM-REGN.                        
                                                                                
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           IF WS-K830-STATUS  = 00 OR 02                                        
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 STRING K830-NUM-BLK                                            
                        ' '                                                     
                        K830-NUM-FLAT DELIMITED BY SIZE                         
                        INTO PC80-BLK-UNIT                                      
                 END-STRING                                                     
                 MOVE K830-NME-STREET-BLDG TO PC80-ADDR1                        
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PC80-ADDR2                        
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE    INTO PC80-ADDR2                     
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-POSTAL TO AV02C015-IN-POSTCODE                
                    PERFORM 3811-CALL-AV02C015     THRU 3811-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC80-BAR-CODE          
                 END-IF                                                         
              ELSE                                                              
                 STRING K830-NUM-MAIL-BLK                                       
                        ' '                                                     
                        K830-NUM-MAIL-FLAT DELIMITED BY SIZE                    
                        INTO PC80-BLK-UNIT                                      
                 END-STRING                                                     
                 MOVE K830-NME-MAIL-STR-BLDG   TO PC80-ADDR1                    
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PC80-ADDR2                    
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PC80-ADDR2                    
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-MAIL-POSTAL TO AV02C015-IN-POSTCODE           
                    PERFORM 3811-CALL-AV02C015     THRU 3811-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC80-BAR-CODE          
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
             IF WS-K830-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K830, STATUS ' WS-K830-STATUS           
                        ', REGN IS  '    FD92-NUM-REGN                          
             ELSE                                                               
                DISPLAY 'ERROR READING K830, STATUS ' WS-K830-STATUS            
                        ', REGN IS  '    FD92-NUM-REGN                          
             END-IF                                                             
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       3810-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       3811-CALL-AV02C015.                                                      
      *----------------------*                                                  
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       3811-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------*           
       3820-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE SPACES                TO BP13COMM8-REC.                         
           MOVE K800-NUM-SCH-ACC(1:9) TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                STRING 'BLK '  COMM8-NUM-BLK  ' '                               
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    ' ' DELIMITED BY SIZE             
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO PC80-FLAT-ADDR                                      
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         K800-NUM-SCH-ACC ', REGN : ' K800-NUM-REGN             
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' K800-NUM-SCH-ACC                            
                        ', REGN : ' K800-NUM-REGN                               
           END-EVALUATE.                                                        
                                                                                
       3820-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PRINT-LETTER.                                                       
      *-------------------------------------------------------------            
           MOVE WS-CURRENT-DATE             TO PC80-DATE-HDR.                   
           MOVE KD92-NUM-REGN               TO PC80-REGNO.                      
                                                                                
           STRING F835-NUM-HDB-REF(1:4) '-'                                     
                  F835-NUM-HDB-REF(5:4) '-'                                     
                  F835-NUM-HDB-REF(9:1) '-'                                     
                  F835-NUM-HDB-REF(10:2)                                        
                  DELIMITED BY SIZE INTO PC80-HDBREF.                           
                                                                                
           STRING FD92-DTE-REVSD-INSTAL-EFF(7:2) '/'                            
                  FD92-DTE-REVSD-INSTAL-EFF(5:2) '/'                            
                  FD92-DTE-REVSD-INSTAL-EFF(1:4)                                
                  DELIMITED BY SIZE INTO PC80-EFF-DTE.                          
                                                                                
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-1 AFTER PAGE.                
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-2.                           
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-3.                           
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-4 AFTER 4.                   
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-5.                           
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-6.                           
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-7.                           
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-8.                           
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-9.                           
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-10.                          
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-11 AFTER 2.                  
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-12 AFTER 6.                  
           WRITE PC80-PRTREC    FROM  PC80-PR-LINE-13 AFTER 4.                  
           ADD   1   TO   WS-PRNT-CTR.                                          
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' RUN   DATE :  ' WS-CURRENT-DATE.                           
           DISPLAY ' *------------ BP13CC80 AUDIT TRAIL ------------*'.         
           DISPLAY '    '.                                                      
           DISPLAY 'NO. OF BP13FD92 READ     : ' WS-FD92-READ.                  
           DISPLAY 'NO. OF AB02F835 READ     : ' WS-F835-READ.                  
           DISPLAY 'NO. OF MATCHED  CASES    : ' WS-MATCH-REGN.                 
           DISPLAY 'NO. OF BP13KD92 DELETED  : ' WS-KD92-DEL.                   
           DISPLAY 'NO. OF LETTERS  PRINTED  : ' WS-PRNT-CTR.                   
                                                                                
           CLOSE AB02F835                                                       
                 BP13FD92                                                       
                 BP13KD92                                                       
                 BP13KD94                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13LC80                                                       
                 BP13PC80.                                                      
                                                                                
           IF WS-KD92-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13KD92 FILE ERROR ' WS-KD92-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-KD94-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13KD94 FILE ERROR ' WS-KD94-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K800 FILE ERROR ' WS-K800-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K820 FILE ERROR ' WS-K820-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K830 FILE ERROR ' WS-K830-STATUS             
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
