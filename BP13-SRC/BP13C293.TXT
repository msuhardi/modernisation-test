       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C293.                                                 
      *AUTHOR.        JULITO A. MACAPAGAL.                                      
      *DATE-WRITTEN.  21/01/2002.                                               
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      *=============================================================*           
      * OBJECTIVES : UPDATE K270-NUM-UNIT-OFFER (# OF UNITS OFFERED)*           
      *              IN BP13K270 USING FLAT FILE BP13F270 CREATED   *           
      *              BY BP13C226.                                   *           
      *-------------------------------------------------------------*           
      * CHG REF   DATE       OIC   DESCRIPTION                      *           
      * BP132075  21/01/2002 JAM1  NEW PROGRAM                      *           
      * BP132240  29/11/2002 MTD   MOVE F270-NUM-OFFER TO K270-NUM- *           
      *                            OFFER IF NOT ZEROES              *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F270 ASSIGN TO BP13F270.                                  
           SELECT BP13K270 ASSIGN TO BP13K270                                   
                           ORGANIZATION INDEXED                                 
                           ACCESS MODE  DYNAMIC                                 
                           RECORD KEY   K270-KEY-FLD                            
                           FILE STATUS  BP13K270-STATUS.                        
                                                                                
                                                                                
       DATA DIVISION.                                                           
      *************************************************************             
       FILE SECTION.                                                            
       FD  BP13F270       BLOCK CONTAINS 0 RECORDS                              
                          RECORD CONTAINS 250 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F270.                                                           
                                                                                
       FD   BP13K270      RECORD CONTAINS 250 CHARACTERS.                       
       COPY BP13K270.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *************************************************************             
       01  BP13K270-STATUS               PIC 9(2)  VALUE ZEROES.                
       01  WS-F270-CNT-READ              PIC 9(8)  VALUE ZEROES.                
       01  WS-K270-CNT-READ              PIC 9(8)  VALUE ZEROES.                
       01  WS-K270-CNT-NOTFND            PIC 9(8)  VALUE ZEROES.                
       01  WS-K270-CNT-UPD               PIC 9(8)  VALUE ZEROES.                
       01  BP13F270-EOF                  PIC X     VALUE 'N'.                   
       01  WS-DATE                       PIC X(8)  VALUE SPACES.                
       01  WS-SUB                        PIC 9(02) VALUE ZEROES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F270     THRU 2000-EXIT                        
           PERFORM 3000-MAIN-PROCESS      THRU 3000-EXIT                        
                   UNTIL BP13F270-EOF = 'Y'.                                    
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *************************************************************             
           OPEN INPUT  BP13F270                                                 
                I-O    BP13K270.                                                
                                                                                
           IF BP13K270-STATUS NOT = ZEROES AND 97                               
              DISPLAY "OPEN BP13K270 FAIL STATUS " BP13K270-STATUS              
              MOVE BP13K270-STATUS TO RETURN-CODE                               
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-BP13F270.                                                      
      *************************************************************             
           READ BP13F270 AT END                                                 
                MOVE 'Y' TO BP13F270-EOF                                        
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F270-CNT-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-MAIN-PROCESS.                                                       
      *************************************************************             
           MOVE F270-KEY-FLD   TO K270-KEY-FLD.                                 
           READ BP13K270.                                                       
                                                                                
           IF BP13K270-STATUS = 00                                              
              ADD 1   TO WS-K270-CNT-READ                                       
              PERFORM 4000-REWRITE-BP13K270 THRU 4000-EXIT                      
           ELSE                                                                 
              IF BP13K270-STATUS = 23                                           
                 DISPLAY 'BP13K270 NOT FOUND ' K270-KEY-FLD                     
                         'STATUS ' BP13K270-STATUS                              
                 ADD 1   TO WS-K270-CNT-NOTFND                                  
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K270 ' BP13K270-STATUS              
                 MOVE BP13K270-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F270 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-REWRITE-BP13K270.                                                   
      *************************************************************             
                                                                                
           IF F270-NUM-UNIT-OFFER = ZEROES                                      
              GO TO 4000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 9                  
              IF F270-NUM-OFFER (WS-SUB) NOT = ZEROES                           
                 MOVE F270-NUM-OFFER (WS-SUB)  TO                               
                      K270-NUM-OFFER (WS-SUB)                                   
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           REWRITE K270-REC.                                                    
           EVALUATE BP13K270-STATUS                                             
           WHEN 00                                                              
                ADD 1    TO WS-K270-CNT-UPD                                     
           WHEN 23                                                              
                DISPLAY 'BP13K270 REC NOT FOUND: ' K270-KEY-FLD                 
                        ', STATUS ' BP13K270-STATUS                             
           WHEN OTHER                                                           
                DISPLAY 'ERROR WRITING BP13K270 ' BP13K270-STATUS               
                        ',REC KEY IS ' K270-KEY-FLD                             
                MOVE BP13K270-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *************************************************************             
           DISPLAY ' '.                                                         
           DISPLAY ' BP13C293 CONTROL LIST,  DATE : ' WS-DATE.                  
           DISPLAY '==============================================='            
           DISPLAY ' REC READ FROM BP13F270 : ' WS-F270-CNT-READ.               
           DISPLAY ' REC READ FROM BP13K270 : ' WS-K270-CNT-READ.               
           DISPLAY ' REC UPDT INTO BP13K270 : ' WS-K270-CNT-UPD.                
           DISPLAY ' REC NOT FOUND BP13K270 : ' WS-K270-CNT-NOTFND.             
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13K270                                                       
                 BP13F270.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
