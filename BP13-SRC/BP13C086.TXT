      *------------------------------------------------------------*            
       IDENTIFICATION DIVISION.                                                 
      *------------------------------------------------------------*            
       PROGRAM-ID.    BP13C086.                                                 
      *AUTHOR.        ED.                                                       
      *DATE-WRITTEN.  12 JUL 2002.                                              
      * ========================================================== *            
      * FLAT INVENTORY SYSTEM (BP13)                               *            
      * ========================================================== *            
      *  OBJECTIVE   : TO GENERATE REPORT ON SUCCESSFUL PIGEONHOLD-*            
      *                ING OF GRO CASES.                           *            
      *  INPUT FILES : BP13F740                                    *            
      *                BP13K740                                                 
      * ---------------------------------------------------------- *            
      * CHG-NO   BY  DATE    DETAILS                               *            
      * -------- --- ------  -------                               *            
      * BP132171 ED  110702  NEW PGM                               *            
      * BP132360 SK  170103  WRITE TO BP13K825                     *            
      * BP132463 SK  301203  CATER FOR DUPLICATE IN BP13K825       *            
      * ========================================================== *            
                                                                                
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
      *------------------------------------------------------------*            
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K740-KEY-FLD                                  
                  FILE STATUS  IS WS-K740-STATUS.                               
                                                                                
           SELECT BP13K825 ASSIGN TO BP13K825                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K825-KEY-FLD                                  
                  FILE STATUS  IS WS-K825-STATUS.                               
                                                                                
           SELECT BP13L086 ASSIGN TO BP13L086.                                  
                                                                                
      *------------------------------------------------------------*            
       DATA DIVISION.                                                           
      *------------------------------------------------------------*            
                                                                                
       FILE SECTION.                                                            
       FD   BP13F740                                                            
            RECORD CONTAINS 500  CHARACTERS                                     
            BLOCK CONTAINS    0  RECORDS                                        
            RECORDING MODE   IS  F                                              
            LABEL RECORDS        STANDARD.                                      
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13K740.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200  CHARACTERS.                                    
       COPY BP13K825.                                                           
                                                                                
       FD   BP13L086                                                            
            RECORD CONTAINS 134 CHARACTERS                                      
            BLOCK CONTAINS    0  RECORDS                                        
            RECORDING MODE   IS  F                                              
            LABEL RECORDS        STANDARD.                                      
       01   BP13L086-REC    PIC X(134).                                         
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
                                                                                
       01  WS-FILE-INDICATORS.                                                  
           05  WS-F740-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K740-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K825-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K740-STATUS             PIC 9(02) VALUE ZEROS.                
           05  WS-K825-STATUS             PIC 9(02) VALUE ZEROS.                
           05  WS-F740-READ               PIC 9(06) VALUE ZEROS.                
           05  WS-CNT-WRIT-L086           PIC 9(06) VALUE ZEROS.                
           05  WS-WRITE-K825              PIC 9(06) VALUE ZEROS.                
                                                                                
       01  WS-CURRENT-DATE.                                                     
           02  WS-DATE                    PIC X(8).                             
           02  WS-CURRENT-TIME            PIC X(9).                             
           02  FILLER                     PIC X(4).                             
                                                                                
       01  WS-L086-CONTROLS.                                                    
           05  WS-L086-LINECTR                PIC 9(2)  VALUE 60.               
           05  WS-L086-PAGECTR                PIC 9(5)  VALUE ZEROS.            
           05  WS-L086-SNO                    PIC 9(5)  VALUE 0.                
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-BLANKS                      PIC X(132) VALUE SPACES.          
           05  WS-CTR                         PIC 9(2).                         
           05  WS-PREV-RANDOM-NO              PIC X(27).                        
           05  WS-TEMP-REGN                   PIC X(8).                         
           05  WS-KEY-K825                    PIC X(17).                        
           05  WS-KEY-PREV                    PIC X(17).                        
           05  WS-FLAG                        PIC X(1).                         
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
      *============================================================*            
      *         BP13L086  -  R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  L086-HEADER-01.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L086'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M   O F  C O M M I T M E N T'.                        
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L086-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L086-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  L086-HEADER-02.                                                      
           05  FILLER              PIC X(42)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
           'REPORT ON SUCCESSFUL GRO PIGEONHOLDING CASES'.                      
           05  L086-HDR-DATE       PIC X(10).                                   
                                                                                
       01  L086-HEADER-03.                                                      
           05                      PIC X(05)     VALUE 'SNO'.                   
           05                      PIC X(02)     VALUE SPACES.                  
           05                      PIC X(08)     VALUE 'REFNO'.                 
           05                      PIC X(02)     VALUE SPACES.                  
           05                      PIC X(09)     VALUE 'NRIC'.                  
           05                      PIC X(02)     VALUE SPACES.                  
           05                      PIC X(03)     VALUE 'NT'.                    
           05                      PIC X(02)     VALUE SPACES.                  
           05                      PIC X(02)     VALUE 'FT'.                    
           05                      PIC X(02)     VALUE SPACES.                  
           05                      PIC X(04)     VALUE 'WARD'.                  
           05                      PIC X(02)     VALUE SPACES.                  
           05                      PIC X(03)     VALUE 'SRV'.                   
           05                      PIC X(02)     VALUE SPACES.                  
           05                      PIC X(05)     VALUE 'QUEUE'.                 
                                                                                
       01  L086-HEADER-04.                                                      
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  L086-DETAIL.                                                         
           05  L086-SNO            PIC X(05).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L086-REFNO          PIC X(08).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L086-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L086-NT             PIC X(03).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L086-FT             PIC X(02).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L086-WARD           PIC X(04).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L086-SERVE          PIC X(03).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L086-QUEUE          PIC X(05).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L086-RANDOM         PIC X(28).                                   
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       0000-MAIN-ROUTINE.                                                       
      *------------------------------------------------------------*            
           PERFORM 1000-INITIALIZATION     THRU 1000-EXIT.                      
           PERFORM 3000-PROCESS-F740       THRU 3000-EXIT                       
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9990-CLOSE-FILES        THRU 9990-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1000-INITIALIZATION.                                                     
      *------------------------------------------------------------*            
           OPEN INPUT  BP13F740                                                 
                       BP13K740                                                 
               I-O     BP13K825                                                 
               OUTPUT  BP13L086.                                                
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740  OPEN ERROR ' WS-K740-STATUS               
              MOVE WS-K740-STATUS      TO RETURN-CODE                           
              PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K825  OPEN ERROR ' WS-K825-STATUS               
              MOVE WS-K825-STATUS      TO RETURN-CODE                           
              PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
           MOVE WS-SYS-DD                   TO WS-EDITED-DD.                    
           MOVE WS-SYS-MM                   TO WS-EDITED-MM.                    
           MOVE WS-SYS-CCYY                 TO WS-EDITED-YY.                    
           MOVE WS-EDITED-DATE              TO L086-RUN-DATE.                   
           MOVE 1                           TO WS-L086-SNO.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2000-READ-BP13F740.                                                      
      *------------------------------------------------------------*            
           READ BP13F740 AT END                                                 
                MOVE 'Y'        TO WS-F740-EOF                                  
                GO  TO 3000-EXIT                                                
           END-READ.                                                            
                                                                                
           ADD 1       TO WS-F740-READ.                                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-PROCESS-F740.                                                       
      *------------------------------------------------------------*            
                                                                                
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
           PERFORM 4000-START-K740         THRU 4000-EXIT.                      
           PERFORM 4100-READNXT-K740       THRU 4100-EXIT                       
              UNTIL F740-NUM-RANDOM(1:27) NOT = K740-NUM-RANDOM(1:27)           
                 OR WS-K740-EOF = 'Y'.                                          
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4000-START-K740.                                                         
      *------------------------------------------------------------*            
           MOVE 'T'                        TO WS-FLAG.                          
           MOVE SPACES                     TO WS-TEMP-REGN.                     
           MOVE SPACES                     TO WS-KEY-K825.                      
           MOVE SPACES                     TO K740-KEY-FLD.                     
           MOVE F740-KEY-FLD(1:27)         TO K740-KEY-FLD.                     
                                                                                
           START BP13K740   KEY >= K740-KEY-FLD                                 
           IF WS-K740-STATUS NOT = 00                                           
              DISPLAY 'START ERROR WITH BP13K740. STATUS IS - '                 
                       WS-K740-STATUS                                           
              GO TO 9990-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4100-READNXT-K740.                                                       
      *------------------------------------------------------------*            
           READ BP13K740 NEXT                                                   
              AT END   MOVE 'Y'  TO WS-K740-EOF.                                
                                                                                
           IF F740-NUM-RANDOM(1:27)   = K740-NUM-RANDOM(1:27)                   
              IF K740-NUM-RANDOM(28:1) = 'G'                                    
                 MOVE K740-NUM-REGN  TO WS-TEMP-REGN                            
              ELSE                                                              
              IF K740-NUM-RANDOM(28:1) IS NUMERIC                               
                 PERFORM 4300-STARTBR-K825 THRU 4300-EXIT                       
              END-IF                                                            
                                                                                
              PERFORM 4200-MOVE-VALUES   THRU 4200-EXIT                         
              PERFORM 5000-PRINT-REPORT  THRU 5000-EXIT                         
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4200-MOVE-VALUES.                                                        
      *------------------------------------------------------------*            
                                                                                
           MOVE WS-L086-SNO                TO L086-SNO.                         
           IF K740-NUM-RANDOM(1:27) = WS-PREV-RANDOM-NO                         
              MOVE SPACES                  TO L086-SNO                          
           ELSE                                                                 
              ADD  1    TO WS-L086-SNO                                          
              MOVE K740-NUM-RANDOM(1:27)   TO WS-PREV-RANDOM-NO                 
           END-IF.                                                              
           MOVE K740-NUM-REGN              TO L086-REFNO                        
           MOVE K740-NUM-NRIC1             TO L086-NRIC                         
           MOVE K740-CDE-SUCC-NT-ZONE      TO L086-NT                           
           MOVE K740-CDE-FLAT-TYPE         TO L086-FT                           
           MOVE K740-NUM-WARD              TO L086-WARD                         
           MOVE K740-CDE-SERVE             TO L086-SERVE                        
           MOVE K740-NUM-NT-FT-QUEUE       TO L086-QUEUE.                       
           MOVE K740-NUM-RANDOM            TO L086-RANDOM.                      
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4300-STARTBR-K825.                                                       
      *------------------------------------------------------------*            
                                                                                
           MOVE LOW-VALUES                 TO K825-KEY-FLD.                     
           MOVE K740-NUM-REGN              TO K825-NUM-REGN.                    
                                                                                
           START BP13K825   KEY >= K825-KEY-FLD.                                
           IF WS-K825-STATUS NOT = 00                                           
              DISPLAY 'START ERROR WITH BP13K825. STATUS IS - '                 
                       WS-K825-STATUS                                           
              GO TO 9990-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           MOVE 'N' TO WS-K825-EOF.                                             
           PERFORM 4400-READNEXT-K825       THRU 4400-EXIT                      
                   UNTIL K825-NUM-REGN NOT = K740-NUM-REGN                      
                         OR WS-K825-EOF = 'Y'.                                  
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4400-READNEXT-K825.                                                      
      *------------------------------------------------------------*            
           READ BP13K825 NEXT                                                   
              AT END   MOVE 'Y'  TO WS-K825-EOF.                                
           MOVE K825-KEY-FLD     TO WS-KEY-K825.                                
                                                                                
           IF K825-KEY-FLD = WS-KEY-PREV                                        
              MOVE 'Y'  TO WS-K825-EOF                                          
              GO TO 4100-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF K825-NUM-REGN = K740-NUM-REGN                                     
              PERFORM 4450-WRITE-K825    THRU 4450-EXIT                         
           ELSE                                                                 
              MOVE 'Y'  TO WS-K825-EOF                                          
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4450-WRITE-K825.                                                         
      *------------------------------------------------------------*            
                                                                                
           MOVE WS-TEMP-REGN TO K825-NUM-REGN.                                  
           WRITE BP13K825-REC.                                                  
                                                                                
           IF WS-K825-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR WRITING BP13K825'                                  
              MOVE WS-K825-STATUS TO RETURN-CODE                                
              PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE LOW-VALUES         TO K825-KEY-FLD.                             
           MOVE WS-KEY-K825        TO K825-KEY-FLD.                             
                                                                                
           START BP13K825  KEY > K825-KEY-FLD.                                  
           MOVE K825-KEY-FLD TO WS-KEY-PREV.                                    
           IF WS-K825-STATUS NOT = 00                                           
              DISPLAY 'START ERROR WITH BP13K825. STATUS IS - '                 
                       WS-K825-STATUS                                           
              GO TO 9990-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-WRITE-K825.                                              
                                                                                
       4450-EXIT.                                                               
           EXIT.                                                                
      *------------------------------------------------------------*            
       5000-PRINT-REPORT.                                                       
      *------------------------------------------------------------*            
           IF WS-L086-LINECTR >= 55                                             
              ADD                   1 TO WS-L086-PAGECTR                        
              MOVE WS-L086-PAGECTR    TO L086-PAGE-NO                           
              WRITE BP13L086-REC    FROM L086-HEADER-01  AFTER PAGE             
              WRITE BP13L086-REC    FROM L086-HEADER-02                         
              WRITE BP13L086-REC    FROM L086-HEADER-03                         
              WRITE BP13L086-REC    FROM L086-HEADER-04                         
              WRITE BP13L086-REC    FROM L086-DETAIL                            
              MOVE                  6 TO WS-L086-LINECTR                        
           ELSE                                                                 
              ADD                   1 TO WS-L086-LINECTR                        
              WRITE BP13L086-REC    FROM L086-DETAIL                            
           END-IF.                                                              
                                                                                
           ADD 1                    TO WS-CNT-WRIT-L086.                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       9990-CLOSE-FILES.                                                        
      *------------------------------------------------------------*            
           CLOSE BP13F740                                                       
                 BP13K740                                                       
                 BP13K825                                                       
                 BP13L086.                                                      
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740  CLOSE ERROR ' WS-K740-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K825  CLOSE ERROR ' WS-K825-STATUS              
           END-IF.                                                              
                                                                                
           DISPLAY '============ BP13C086 STATISTICS =============='.           
           DISPLAY ' '.                                                         
           DISPLAY 'BP13F740 RECORDS READ       ' WS-F740-READ.                 
           DISPLAY 'BP13K825 RECORDS WRITE      ' WS-WRITE-K825.                
           DISPLAY ' '.                                                         
           DISPLAY '==============================================='.           
           STOP RUN.                                                            
                                                                                
       9990-EXIT.                                                               
           EXIT.                                                                
