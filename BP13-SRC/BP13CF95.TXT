       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF95.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  18/07/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  SPLIT MATCH & NOT MATCH CASES (BE01F500)   *            
      *                                                            *            
      * INPUT FILES  :  BE01F500 - MHA INTERFACE FILE              *            
      *                 BE01F515 - OBTAIN FROM MHA VIA IDNET       *            
      * OUTPUT FILES :  P13LF95M - BE01F500 MATCH CASES            *            
      *                 P13LF95N - BE01F500 NOT MATCH CASES        *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134922  SMR2  18072013  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BE01F500 ASSIGN TO BE01F500.                                  
           SELECT BE01F515 ASSIGN TO BE01F515.                                  
           SELECT P13LF95M ASSIGN TO P13LF95M.                                  
           SELECT P13LF95N ASSIGN TO P13LF95N.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BE01F500                                                            
            RECORD CONTAINS  50 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       COPY BE01F500.                                                           
                                                                                
       FD   BE01F515                                                            
            RECORD CONTAINS 580 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       COPY BE01F515.                                                           
                                                                                
       FD   P13LF95M                                                            
            RECORD CONTAINS  50 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       01   P13LF95M-REC           PIC X(50).                                   
                                                                                
       FD   P13LF95N                                                            
            RECORD CONTAINS  50 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       01   P13LF95N-REC           PIC X(50).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-F500-READ        PIC 9(6)    VALUE ZEROES.                    
           05  WS-F515-READ        PIC 9(6)    VALUE ZEROES.                    
           05  WS-LF95M-WRITE      PIC 9(6)    VALUE ZEROES.                    
           05  WS-LF95N-WRITE      PIC 9(6)    VALUE ZEROES.                    
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F500-KEY-FLD.                                                 
               10  WS-F500-REGN    PIC X(8)    VALUE SPACES.                    
               10  WS-F500-NRIC    PIC X(9)    VALUE SPACES.                    
           05  WS-F515-KEY-FLD.                                                 
               10  WS-F515-REGN    PIC X(8)    VALUE SPACES.                    
               10  WS-F515-NRIC    PIC X(9)    VALUE SPACES.                    
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-MAIN-ROUTINE        THRU 2000-EXIT.                     
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  BE01F500                                                 
                       BE01F515                                                 
                OUTPUT P13LF95M                                                 
                       P13LF95N.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 3000-READ-BE01F500       THRU 3000-EXIT.                     
           PERFORM 4000-READ-BE01F515       THRU 4000-EXIT.                     
           PERFORM 5000-PROCESS-FILES       THRU 5000-EXIT                      
             UNTIL WS-F500-KEY-FLD = HIGH-VALUES.                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BE01F500.                                                      
      *-------------------------------------------------------------            
           READ BE01F500 AT END                                                 
                MOVE HIGH-VALUES            TO WS-F500-KEY-FLD                  
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F500-READ.                    
           MOVE BE01F500-REF-NO(1:8)        TO WS-F500-REGN.                    
           MOVE BE01F500-IDNO               TO WS-F500-NRIC.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-READ-BE01F515.                                                      
      *-------------------------------------------------------------            
           READ BE01F515 AT END                                                 
                MOVE HIGH-VALUES            TO WS-F515-KEY-FLD                  
                GO TO 4000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F515-READ.                    
           MOVE BE01F515-REF-NO(1:8)        TO WS-F515-REGN.                    
           MOVE BE01F515-IDNO               TO WS-F515-NRIC.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                      TO P13LF95M-REC                     
                                               P13LF95N-REC.                    
                                                                                
           IF WS-F500-KEY-FLD = WS-F515-KEY-FLD                                 
              WRITE P13LF95M-REC            FROM BE01F500-REC                   
              ADD 1                         TO WS-LF95M-WRITE                   
              PERFORM 3000-READ-BE01F500    THRU 3000-EXIT                      
           ELSE                                                                 
              IF WS-F500-KEY-FLD < WS-F515-KEY-FLD                              
                 WRITE P13LF95N-REC         FROM BE01F500-REC                   
                 ADD 1                      TO WS-LF95N-WRITE                   
                 PERFORM 3000-READ-BE01F500 THRU 3000-EXIT                      
              ELSE                                                              
                 PERFORM 4000-READ-BE01F515 THRU 4000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '*============== BP13CF95 ==============*'.                  
           DISPLAY '  NO. OF F500 RECS READ      =  ' WS-F500-READ.             
           DISPLAY '  NO. OF F515 RECS READ      =  ' WS-F515-READ.             
           DISPLAY '  NO. OF LF95M RECS WRITTEN  =  ' WS-LF95M-WRITE.           
           DISPLAY '  NO. OF LF95N RECS WRITTEN  =  ' WS-LF95N-WRITE.           
           DISPLAY '*======================================*'.                  
                                                                                
           CLOSE BE01F500                                                       
                 BE01F515                                                       
                 P13LF95M                                                       
                 P13LF95N.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
