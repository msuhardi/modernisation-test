      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C23T.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  FEB 17 2010.                                              
      *--------------------------------------------------------------*          
      *                                                              *          
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)                 *          
      *                                                              *          
      *    SYSTEM ID   : BP13                                        *          
      *                                                              *          
      *    OBJECTIVES :  SORT NPL CASES FROM BP13F200 WITH THE SAME  *          
      *                  BALLOT-DATE FROM BP13F205                   *          
      *                                                              *          
      *    INPUT FILES  : BP13F205                                   *          
      *                   BP13F200                                   *          
      *    OUTPUT       : P13F200A                                   *          
      *--------------------------------------------------------------*          
      * CHG-NO  BY   DATE     DETAILS                                *          
      *-------- ---- -------- ---------------------------------------*          
      *BP133813 ESA1 02172010 NEW PGM                                *          
      *-------- ---- -------- ---------------------------------------*          
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
           SELECT P13F200A ASSIGN TO P13F200A.                                  
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            RECORDING MODE  IS F.                                               
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F200                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F200.                                                           
                                                                                
       FD   P13F200A                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13F200A-REC         PIC X(300).                                    
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F205-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-F200-EOF                PIC X(1)  VALUE 'N'.                  
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-EDITED-DATE             PIC X(10) VALUE SPACES.               
           05  WS-CNT-F200A-WRITE         PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F200-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F205-READ           PIC 9(8)  VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 1100-READ-F205         THRU 1100-EXIT.                       
           PERFORM 2000-PROCESS           THRU 2000-EXIT                        
                   UNTIL WS-F205-EOF = 'Y'.                                     
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13F200                                                 
                OUTPUT P13F200A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1100-READ-F205.                                                          
      *--------------------------------------------------------------*          
           READ BP13F205                                                        
                AT END  MOVE 'Y' TO WS-F205-EOF                                 
                        MOVE 'Y' TO WS-F200-EOF                                 
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1                 TO WS-CNT-F205-READ.                           
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1200-READ-F200.                                                          
      *--------------------------------------------------------------*          
           READ BP13F200                                                        
                AT END  MOVE 'Y' TO WS-F200-EOF                                 
                GO TO 1200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F200-READ.                                           
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       1300-CLOSE-OPEN-F200.                                                    
      *--------------------------------------------------------------*          
           CLOSE      BP13F200.                                                 
           OPEN INPUT BP13F200.                                                 
                                                                                
           MOVE 'N'   TO WS-F200-EOF.                                           
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-PROCESS.                                                            
      *--------------------------------------------------------------*          
           PERFORM 4000-PROCESS           THRU 4000-EXIT                        
                   UNTIL WS-F200-EOF = 'Y'.                                     
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------         
       4000-PROCESS.                                                            
      *----------------------------------------------------------------         
           PERFORM 1200-READ-F200    THRU 1200-EXIT.                            
                                                                                
           IF WS-F200-EOF = 'Y'                                                 
              PERFORM 1100-READ-F205        THRU 1100-EXIT                      
                                                                                
              IF WS-F205-EOF = 'Y'                                              
                 GO TO 4000-EXIT                                                
              END-IF                                                            
                                                                                
              PERFORM 1300-CLOSE-OPEN-F200  THRU 1300-EXIT                      
              GO TO 4000-EXIT                                                   
           ELSE                                                                 
              IF ((F205-NUM-SELECTION = F200-NUM-SELTYPE) AND                   
                  (F205-DTE-BALLOT = F200-DTE-BALLOT))                          
                  PERFORM 5000-WRITE-P13F200A   THRU 5000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       5000-WRITE-P13F200A.                                                     
      *--------------------------------------------------------------*          
            MOVE SPACES        TO   P13F200A-REC.                               
            WRITE P13F200A-REC FROM BP13F200-REC.                               
            ADD  1  TO WS-CNT-F200A-WRITE.                                      
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
                                                                                
           CLOSE BP13F205                                                       
                 BP13F200                                                       
                 P13F200A.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '********* BP13C23T CONTROL *********'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F205) = ' WS-CNT-F205-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F200) = ' WS-CNT-F200-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF WRITE(P13F200A) = ' WS-CNT-F200A-WRITE.              
           DISPLAY SPACES.                                                      
           DISPLAY '************************************'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
