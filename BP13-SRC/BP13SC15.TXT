 /******************************************************************/           
 /*  SYSTEM      : SYSTEM OF COMMITMENT (BP13)                     */           
 /*  OBJECTIVE   : TO PRINT MONTHLY REPORT FOR TRANSACTION ON      */           
 /*                FORFEITURE, ADJUSTMANT, REFUND AND REFUND       */           
 /*  INPUT FILES : BP13F136,SY02F001.                              */           
 /*  DATE        : FEB 12 2007                                     */           
 /*  AUTHOR      : BALA AMUTHAN                                    */           
 /*================================================================*/           
 /* BP133175 BA3 12/02/2007  INCLUDE NON-CONCESSIONARY LOAN        */           
 /* BP133545 JB8 10/03/2009  TO CATER FOR SCHDULE 09               */           
 /* BP134285 JB8 17/10/2011  TO ADD SUB-TOTAL FOR GL               */           
 /*================================================================*/           
  OPTIONS  NODATE NONUMBER;                                                     
                                                                                
     PROC FORMAT;                                                               
        PICTURE CREDIT LOW-<0='000,000,000.00DR'                                
                            (FILL=' ' PREFIX='$' MULT=100.00000000001)          
                        0-HIGH='000,000,000.00CR'                               
                            (FILL=' ' PREFIX='$' MULT=100.00000000001);         
                                                                                
     DATA SY02;                                                                 
     INFILE SY02F001 END = EOF;                                                 
     INPUT  @01  DATE_CYM_CURR        YYMMDD8.                                  
            @21  DATE_CYM_PREV        $CHAR6.                                   
            @21  DATE_CY_PREV         $CHAR4.                                   
            @58  PREV_MTH_TXT         $CHAR3.;                                  
                                                                                
         CALL SYMPUT ('PREV_MTH',PREV_MTH_TXT);                                 
         CALL SYMPUT ('PREV_CY',DATE_CY_PREV);                                  
         CALL SYMPUT ('CURR_DTE',(PUT(DATE_CYM_CURR,DDMMYY10.)));               
         CALL SYMPUT ('PREVDTE', DATE_CYM_PREV);                                
         CALL SYMPUT ('PREMTXT', PREV_MTH_TXT);                                 
                                                                                
     RUN;                                                                       
                                                                                
     DATA BP13F136;                                                             
     INFILE BP13F136 END=FINAL;                                                 
     INPUT  @01  TRANCDE  $CHAR2.      @05  ORIGNO   $CHAR8.                    
            @13  POSTDTE  YYMMDD8.                                              
            @24  LNSCH    $CHAR1.                                               
            @25  NRIC     $CHAR9.                                               
            @66  DOCNO    $CHAR11.                                              
            @66  DOCNO1   $CHAR14.     @81  AMTRE    ZD9.2                      
            @90  CRDR     $CHAR1.                                               
            @91  TRANDTE  $CHAR8.      @91  TRANMY   $CHAR6.                    
            @99  PAYTYPE  $CHAR3.      @102 SCHNO    $CHAR11.                   
            @106 ACCT     $CHAR4.      @110 CHK_DG   $CHAR1.                    
            @111 LSECDE   $CHAR2.                                               
            @114 DBSTOP   $CHAR1.                                               
            @118 FT       $CHAR2.                                               
            @122 DUEMY    $CHAR6.                                               
            @122 DUEDTE   $CHAR8.                                               
            @130 PP_SHD   $CHAR2.;                                              
                                                                                
     FORMAT AMTRE CREDIT.;                                                      
                                                                                
     LENGTH WS_PREV_DATE  $8;                                                   
     RETAIN WS_PREV_DATE;                                                       
     IF _N_ = 1 THEN DO;                                                        
        WS_PREV_DATE = SUBSTR("&PREMTXT",1,3) || ' ' ||                         
                       SUBSTR("&PREV_CY",1,4);                                  
     END;                                                                       
                                                                                
     IF ((TRANCDE=13 OR TRANCDE=14 OR TRANCDE=15 OR TRANCDE=16 OR               
          TRANCDE=63) AND (PAYTYPE = 'PP ' OR PAYTYPE = 'INS' OR                
          PAYTYPE = 'OPT'));                                                    
                                                                                
     IF (TRANCDE = '63') OR (PAYTYPE = 'OPT' AND TRANCDE = '13') THEN           
        DO;                                                                     
          AMTRE = AMTRE * -1;                                                   
        END;                                                                    
                                                                                
     IF PAYTYPE = 'PP' OR PAYTYPE = 'OPT' THEN  DO;                             
        WS_GL_NO = '(' || PAYTYPE || ' - 0000000404159)';                       
        END;                                                                    
     ELSE DO;                                                                   
        IF DBSTOP = 'Y' THEN DO;                                                
           IF LNSCH = 'A' THEN DO;                                              
              WS_GL_NO = '(' || PAYTYPE || ' - 9000000545653)';                 
           END;                                                                 
           ELSE DO;                                                             
              WS_GL_NO = '(' || PAYTYPE || ' - 0000000540810)';                 
           END;                                                                 
        END;                                                                    
        ELSE DO;                                                                
           IF LNSCH = 'A' THEN DO;                                              
              WS_GL_NO = '(' || PAYTYPE || ' - 0000000540835)';                 
           END;                                                                 
           ELSE DO;                                                             
              WS_GL_NO = '(' || PAYTYPE || ' - 0000000540834)';                 
           END;                                                                 
        END;                                                                    
     END;                                                                       
  RUN;  /* END READ */                                                          
                                                                                
  /* SORT BY GL_NO, DUE_MTH, TRANS_DATE, DOCNO */                               
  PROC SORT DATA=BP13F136;                                                      
    BY WS_GL_NO DUEMY DUEDTE TRANDTE DOCNO;                                     
  RUN;                                                                          
                                                                                
  DATA _NULLS_;                                                                 
     FILE BP13LC15 HEADER=HH NOTITLES LINESLEFT=LL;                             
                                                                                
     IF TOTAL = 0 THEN DO;                                                      
        PUT _PAGE_;                                                             
        PUT // @1 '*** NO RECORDS ';                                            
     END;                                                                       
                                                                                
     SET BP13F136 END=EOF NOBS=TOTAL;                                           
     OPTIONS PAGESIZE=55 LS=132 NODATE;                                         
     BY WS_GL_NO DUEMY DUEDTE TRANDTE DOCNO;                                    
                                                                                
     IF _N_ = 1 OR FIRST.WS_GL_NO OR FIRST.DUEMY THEN DO;                       
        PUT _PAGE_;                                                             
     END;                                                                       
                                                                                
     M+1;                                                                       
     GRDTOT + AMTRE;                                                            
     GLTOT  + AMTRE;                                                            
     DUETOT + AMTRE;                                                            
     TRNTOT + AMTRE;                                                            
                                                                                
        PUT @10  M                                                              
            @17  ORIGNO                @30  DUEDTE                              
            @42  PP_SHD                                                         
            @49  SCHNO   @53  '-'      @54  ACCT   @58  '-'                     
            @59  CHK_DG  @60  '-'      @61  LSECDE                              
            @74  DOCNO1                                                         
            @89  TRANDTE               @109 AMTRE CREDIT.;                      
                                                                                
     IF LAST.WS_GL_NO THEN DO;                                                  
        PUT @50  'SUB TOTAL FOR TRANS DATE : ' TRANDTE                          
            @109 TRNTOT CREDIT.;                                                
        PUT @50  'SUB TOTAL FOR DUE DATE   : ' DUEMY                            
            @109 DUETOT CREDIT.;                                                
        PUT @50  'SUB TOTAL FOR GL ACCOUNT : ' WS_GL_NO                         
            @109 GLTOT CREDIT.;                                                 
        TRNTOT=0;                                                               
        DUETOT=0;                                                               
        GLTOT=0;                                                                
     END;                                                                       
     ELSE IF LAST.DUEMY THEN DO;                                                
        PUT @50  'SUB TOTAL FOR TRANS DATE : ' TRANDTE                          
            @109 TRNTOT CREDIT.;                                                
        PUT @50  'SUB TOTAL FOR DUE DATE   : ' DUEMY                            
            @109 DUETOT CREDIT.;                                                
        TRNTOT=0;                                                               
        DUETOT=0;                                                               
     END;                                                                       
     ELSE IF LAST.TRANDTE THEN DO;                                              
        PUT @50  'SUB TOTAL FOR TRANS DATE : ' TRANDTE                          
            @109 TRNTOT CREDIT.;                                                
        TRNTOT=0;                                                               
     END;                                                                       
                                                                                
     IF EOF THEN DO;                                                            
        PUT //@50 'GRAND-TOTAL ' @109 GRDTOT CREDIT.;                           
     END;                                                                       
  RETURN;                                                                       
                                                                                
  HH:                                                                           
     PAGE + 1;                                                                  
     CURRENT =DATE();                                                           
     PUT ///@1 'BP13LC15' @15 'HDBCAT 3'                                        
         @47  'S Y S T E M   O F   C O M M I T M E N T'                         
         @98  'DATE :' @105 CURRENT DDMMYY10.                                   
         @119 'PAGE :' @126 PAGE 3./;                                           
     PUT @26  'REPORT ON TRANSACTIONS FROM PAYABLES AND RECEIVABLES '           
         @79  'FOR ' WS_PREV_DATE/;                                             
     PUT @1   'TRANSACTION TYPE : ' WS_GL_NO//;                                 
     PUT @1   '        DUE DATE : ' DUEMY;                                      
     PUT @10  'S/N'                                                             
         @17  'REGN-NO    '                                                     
         @30  'DUE DATE'                                                        
         @42  'SHD'                                                             
         @49  'SCH A/C NO'                                                      
         @74  'VOUCHER NO'                                                      
         @89  'TRANS DATE'                                                      
         @110 'AMOUNT';                                                         
                                                                                
     PUT @10  '---'                                                             
         @17  '-------'                                                         
         @30  '--------'                                                        
         @42  '---'                                                             
         @49  '----------'                                                      
         @74  '----------'                                                      
         @89  '----------'                                                      
         @110 '------';                                                         
     RETURN;                                                                    
