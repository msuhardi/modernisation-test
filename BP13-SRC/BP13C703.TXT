       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C703.                                                 
       AUTHOR.        GD.                                                       
      *DATE-WRITTEN.  29/08/95.                                                 
      *REMARKS.                                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1.  TO COMPUTE ESTIMATED OVERALL QUEUE POSITION BY  *                
      *        ZONE AND FLAT TYPE.                             *                
      *                                                        *                
      *    INPUT       : BP13F520                              *                
      *                                                        *                
      *    OUTPUT      : BP13F500                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE    DETAILS                          *                
      * ======   ==  ====== ================================== *                
      * SOC-PH9  GD  161294 RESET COUNTER WHEN ZONE, FT OR     *                
      *                     HOUSEHOLD-TYPE CHANGES             *                
      * SOC-PH9  GD  301294 RENAME SHORTLIST TO SHORTLIST-PA   *                
      * C950651  LSB 120795 INITIALISE THE FOUR QUEUE POSITION *                
      * BP130030 MS1 280895 WRITE Q POSITION TO NUM-SHORTLIST- *                
      *                     ZNFT                               *                
      * BP130652 NOR 190299 EXPAND F500 & F520 RECL 300 TO 500 *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F520                                                            
            RECORDING MODE  F                                                   
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F520.                                                           
                                                                                
       FD   BP13F500                                                            
            RECORDING MODE  F                                                   
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F500.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F520-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-F500-EOF-SW                 PIC X(1)  VALUE 'N'.              
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-F520-READ-CTR               PIC 9(8)  VALUE ZEROS.            
           05  WS-F500-WRITE-CTR              PIC 9(8)  VALUE ZEROS.            
                                                                                
       01  WS-CONTROL-FIELDS.                                                   
           05  WS-PREV-ZONE                   PIC X(2).                         
           05  WS-PREV-FLAT-TYPE              PIC X(2).                         
                                                                                
       01  WS-QUEUE-POSITION                  PIC 9(5).                         
       01  WS-QUEUE-POSITION-X   REDEFINES    WS-QUEUE-POSITION                 
                                              PIC X(5).                         
                                                                                
       01  WS-SYSTEM-DATE                     PIC 9(8)  VALUE ZEROS.            
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
           PERFORM 2000-READ-F520      THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-F520   THRU 3000-EXIT                           
              UNTIL WS-F520-EOF-SW = 'Y'.                                       
                                                                                
                                                                                
           PERFORM 9999-CLOSE-FILES.                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-INITIALIZATION.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F520                                                 
                OUTPUT BP13F500.                                                
                                                                                
           MOVE SPACES TO WS-PREV-ZONE.                                         
           MOVE SPACES TO WS-PREV-FLAT-TYPE.                                    
           MOVE ZEROS  TO WS-QUEUE-POSITION.                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F520.                                                          
      ****************************************************************          
                                                                                
           READ BP13F520                                                        
              AT END                                                            
                 MOVE 'Y' TO WS-F520-EOF-SW                                     
                 GO  TO 2000-EXIT                                               
           END-READ.                                                            
                                                                                
           ADD  1   TO WS-F520-READ-CTR.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F520.                                                       
      ****************************************************************          
                                                                                
           IF  (F520-CDE-ZONE      = WS-PREV-ZONE)      AND                     
               (F520-CDE-FLAT-TYPE = WS-PREV-FLAT-TYPE)                         
               NEXT SENTENCE                                                    
           ELSE                                                                 
                                                                                
                                                                                
               MOVE ZEROS                  TO WS-QUEUE-POSITION                 
               MOVE F520-CDE-ZONE          TO WS-PREV-ZONE                      
               MOVE F520-CDE-FLAT-TYPE     TO WS-PREV-FLAT-TYPE                 
           END-IF.                                                              
                                                                                
           ADD +1                    TO WS-QUEUE-POSITION.                      
                                                                                
           MOVE WS-QUEUE-POSITION-X  TO   F520-NUM-SLIST-PA-ZNFT.               
                                                                                
           WRITE BP13F500-REC        FROM BP13F520-REC.                         
           ADD +1                    TO   WS-F500-WRITE-CTR.                    
                                                                                
           PERFORM 2000-READ-F520    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-FILES.                                                        
      ****************************************************************          
                                                                                
           CLOSE BP13F520                                                       
                 BP13F500.                                                      
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C703 CONTROL TOTALS============*'.          
           DISPLAY 'RUN DATE : ' WS-SYSTEM-DATE (7:2) '/'                       
                                 WS-SYSTEM-DATE (5:2) '/'                       
                                 WS-SYSTEM-DATE (1:4).                          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F520 RECORDS READ      = ' WS-F520-READ-CTR.            
           DISPLAY 'BP13F500 RECORDS WRITTEN   = ' WS-F500-WRITE-CTR.           
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
                                                                                
