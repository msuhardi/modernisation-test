       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CL15.                                                 
       AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  12/09/2023.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : PROGRAM TO GET ALL CASES THAT WERE UPDATE WITH   *        
      *               IO TRANSACTION CODE - SEND TO EACH CRE           *        
      *                                                                *        
      * OUTPUT DESCRIPTION:                                            *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F310                                  *        
      *                                                                *        
      *   OUTPUT      :  1.  BP13MAIL  - CASE WITH IO                  *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *                                                                *        
      *   BP139710  ZAR7  12/09/23 NEW PROGRAM                         *        
      *   BP139175 KAC1  23/04/24 replace Zany to Soffia_RAPHAY@hdb.gov.        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN        TO BP13F310.                          
           SELECT BP13MAIL  ASSIGN        TO BP13MAIL.                          
                                                                                
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS WS-K820-FILE-STATUS.                 
                                                                                
           SELECT SY08K010 ASSIGN TO SY08K010                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K010-NUM-USERID                            
                  FILE STATUS     IS WS-K010-FILE-STATUS.                       
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD  BP13F310                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F310.                                                           
                                                                                
       FD  BP13MAIL                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13MAIL-REC        PIC X(132).                                      
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   SY08K010                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY SY08K010.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-K820-FILE-STATUS      PIC 99       VALUE ZEROES.              
           05  WS-K010-FILE-STATUS      PIC 99       VALUE ZEROES.              
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-COUNT.                                                            
           05  WS-F310-INPUT                PIC 9(08)  VALUE 0.                 
           05  WS-F310-OUT                  PIC 9(08)  VALUE 0.                 
           05  WS-F310-WRT                  PIC 9(08)  VALUE 0.                 
           05  WS-UPD-CNT                   PIC 9(08)  VALUE 0.                 
                                                                                
       01  WS-TIME                          PIC 9(08)  VALUE 0.                 
       01  WS-F310-EOF                      PIC X(01)  VALUE SPACES.            
       01  WS-FOUND-KL00                    PIC X(01)  VALUE SPACES.            
       01  WS-SQL-CODE                   PIC 9(4).                              
       01  WS-EMAIL                         PIC X(50) VALUE SPACES.             
       01  WS-PREV-USER                     PIC X(07) VALUE SPACES.             
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTLAA.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(25)  VALUE                                 
               'LOW_Soo_Chiew@hdb.gov.sg>'.                                     
                                                                                
       01  MAIL-DTL1B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(36)  VALUE                                 
               'Kathijah_Bee_ALI_MOHAMED@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-DTL1C.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(25)  VALUE                                 
               'Soffia_RAPHAY@hdb.gov.sg>'.                                     
                                                                                
       01  MAIL-DTL1CC.                                                         
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-DTL1CCA    PIC X(50)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTLAB.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(25)  VALUE                                 
               'LOW_Soo_Chiew@hdb.gov.sg>'.                                     
                                                                                
       01  MAIL-DTL5B.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  FILLER          PIC X(36)  VALUE                                 
               'Kathijah_Bee_ALI_MOHAMED@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-DTL5C.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  FILLER          PIC X(25)  VALUE                                 
               'Soffia_RAPHAY@hdb.gov.sg>'.                                     
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5D.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  MAIL-DTL5DD     PIC X(50)  VALUE SPACES.                         
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL7.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  FILLER          PIC X(14)  VALUE ' CASES ADDED '.                
           05  FILLER          PIC X(20)  VALUE 'WITH NEW OCCUPIERS'.           
                                                                                
       01  MAIL-DTL8.                                                           
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05 F310-HDG1.                                                        
              10 FILLER                   PIC X(55)   VALUE SPACES.             
              10 FILLER                   PIC X(22)   VALUE                     
                'SYSTEM  OF  COMMITMENT'.                                       
              10 FILLER                   PIC X(55)   VALUE SPACES.             
                                                                                
           05 F310-HDG2.                                                        
              10 FILLER                   PIC X(40)   VALUE 'BP13F310'.         
              10 FILLER                   PIC X(46)   VALUE                     
                '   CASES ADDED WITH NEW OCCUPIERS '.                           
              10 FILLER                   PIC X(20)   VALUE SPACES.             
              10 FILLER                   PIC X(07)   VALUE 'DATE : '.          
              10 F310-SYS-DATE            PIC X(10).                            
              10 FILLER                   PIC X(06)   VALUE SPACES.             
                                                                                
           05 F310-HDG3.                                                        
              10 FILLER                   PIC X(06)   VALUE                     
                'S/NO. '.                                                       
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(20)   VALUE                     
                'REGISTRATION NO.    '.                                         
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(14)   VALUE                     
                 'NRIC          '.                                              
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(50)   VALUE                     
                'NAME     '.                                                    
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(09)   VALUE                     
                'CRE ID   '.                                                    
                                                                                
           05 F310-HDG4.                                                        
              10 FILLER                   PIC X(06)   VALUE                     
                '======'.                                                       
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(20)   VALUE                     
                '===================='.                                         
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(14)   VALUE                     
                 '=============='.                                              
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(50)   VALUE                     
                '========='.                                                    
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(09)   VALUE                     
                '========='.                                                    
              10 FILLER                   PIC X(05)   VALUE SPACES.             
                                                                                
           05 F310-DTL1.                                                        
              10 WS-F310-SNO              PIC ZZZZZ9  VALUE ZEROES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 WS-F310-REG-NUM          PIC X(20)   VALUE SPACES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 WS-F310-SCH-ACCT         PIC X(14)   VALUE SPACES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 WS-F310-NAME             PIC X(50)   VALUE SPACES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 WS-F310-CRE-ID           PIC X(09)   VALUE SPACES.             
                                                                                
      *=======================================================*                 
      *    LINKAGE VARIABLES                                  *                 
      *=======================================================*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13CCNT                        *        
      *----------------------------------------------------------------*        
       01  WS-BP13CCNT-LINK.                                                    
           05  LINK-CNTCT-TYPE         PIC X(3).                                
           05  LINK-STAFF-NICK         PIC X(6).                                
           05  LINK-STAFF-NUM          PIC X(6).                                
           05  LINK-STAFF-CNTCT        PIC X(50).                               
           05  LINK-SYSERR             PIC X(4).                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-F310-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13F310                                                 
                       BP13K820                                                 
                       SY08K010                                                 
                OUTPUT BP13MAIL.                                                
                                                                                
           IF WS-K820-FILE-STATUS NOT = 00 AND 97                               
              DISPLAY 'BP13K820 - OPEN ERROR ' WS-K820-FILE-STATUS              
              MOVE WS-K820-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                       
                                                                                
           IF WS-K010-FILE-STATUS NOT = 00 AND 97                               
              DISPLAY 'SY08K010 - OPEN ERROR ' WS-K010-FILE-STATUS              
              MOVE WS-K010-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BP13F310                                                       
                 AT END MOVE 'Y'  TO WS-F310-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-F310-EOF NOT = 'Y'                                            
               ADD 1 TO WS-F310-INPUT                                           
            END-IF.                                                             
            IF WS-PREV-USER = SPACES                                            
               MOVE F310-USERID   TO WS-PREV-USER                               
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           ADD 1 TO WS-F310-OUT                                                 
           PERFORM 4000-CREATE-REPORT THRU 4000-EXIT                            
                                                                                
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-CREATE-REPORT.                                                      
      *================================================================*        
                                                                                
           IF WS-F310-OUT = 1 OR                                                
              WS-PREV-USER NOT = F310-USERID                                    
              IF WS-F310-OUT NOT  = 1                                           
                WRITE BP13MAIL-REC FROM MAIL-END                                
              END-IF                                                            
              MOVE ZEROES   TO WS-F310-WRT                                      
              MOVE F310-USERID   TO WS-PREV-USER                                
              MOVE F310-USERID          TO K010-NUM-USERID                      
              PERFORM 5100-READ-SY08K010   THRU 5100-EXIT                       
              PERFORM 5200-CALL-BP13CCNT   THRU 5200-EXIT                       
              STRING WS-EMAIL '>' DELIMITED BY SPACE INTO                       
                    MAIL-DTL1CCA                                                
              STRING WS-EMAIL '>' DELIMITED BY SPACE INTO                       
                    MAIL-DTL5DD                                                 
              STRING WS-CUR-DATE(7:2) '/'                                       
                     WS-CUR-DATE(5:2) '/'                                       
                     WS-CUR-DATE(1:4)                                           
                     DELIMITED BY SIZE                                          
                     INTO F310-SYS-DATE                                         
              END-STRING                                                        
              MOVE F310-SYS-DATE TO MAIL-DATE                                   
              WRITE BP13MAIL-REC FROM MAIL-HDR1                                 
              WRITE BP13MAIL-REC FROM MAIL-HDR2                                 
              WRITE BP13MAIL-REC FROM MAIL-DTLAA                                
              WRITE BP13MAIL-REC FROM MAIL-DTL1B                                
              WRITE BP13MAIL-REC FROM MAIL-DTL1C                                
              WRITE BP13MAIL-REC FROM MAIL-DTL1CC                               
              WRITE BP13MAIL-REC FROM MAIL-DTL3                                 
              WRITE BP13MAIL-REC FROM MAIL-DTL4                                 
              WRITE BP13MAIL-REC FROM MAIL-DTLAB                                
              WRITE BP13MAIL-REC FROM MAIL-DTL5B                                
              WRITE BP13MAIL-REC FROM MAIL-DTL5C                                
              WRITE BP13MAIL-REC FROM MAIL-DTL5D                                
      *       WRITE BP13MAIL-REC FROM MAIL-DTL5F                                
              WRITE BP13MAIL-REC FROM MAIL-DTL7                                 
              WRITE BP13MAIL-REC FROM MAIL-DTL8                                 
              WRITE BP13MAIL-REC FROM MAIL-SPACES                               
              WRITE BP13MAIL-REC FROM F310-HDG1                                 
              WRITE BP13MAIL-REC FROM F310-HDG2                                 
              MOVE SPACES TO BP13MAIL-REC                                       
              WRITE BP13MAIL-REC                                                
              WRITE BP13MAIL-REC FROM F310-HDG3                                 
              WRITE BP13MAIL-REC FROM F310-HDG4                                 
              MOVE SPACES TO BP13MAIL-REC                                       
              WRITE BP13MAIL-REC                                                
           END-IF.                                                              
           ADD 1                     TO WS-F310-WRT                             
           MOVE WS-F310-WRT          TO WS-F310-SNO                             
           MOVE F310-REGN-NO         TO WS-F310-REG-NUM                         
           MOVE F310-SCH-ACC         TO WS-F310-SCH-ACCT                        
           MOVE F310-USERID          TO WS-F310-CRE-ID                          
           MOVE F310-REGN-NO         TO K820-NUM-REGN                           
           MOVE F310-SCH-ACC         TO K820-NUM-NRIC                           
           PERFORM 5000-READ-BP13K820   THRU 5000-EXIT                          
           MOVE K820-NME-OCCP        TO WS-F310-NAME.                           
           WRITE BP13MAIL-REC    FROM F310-DTL1.                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5000-READ-BP13K820.                                                      
      *================================================================*        
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-FILE-STATUS  = 00                                         
              CONTINUE                                                          
           ELSE                                                                 
              MOVE SPACES   TO BP13K820-REC                                     
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5100-READ-SY08K010.                                                      
      *================================================================*        
           READ SY08K010.                                                       
                                                                                
           IF WS-K010-FILE-STATUS  = 00                                         
              CONTINUE                                                          
           ELSE                                                                 
              MOVE SPACES   TO SY08K010-USER-REC                                
              MOVE SPACES   TO WS-EMAIL                                         
                               MAIL-DTL1CCA                                     
                               MAIL-DTL5DD                                      
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5200-CALL-BP13CCNT.                                                      
      *================================================================*        
           MOVE 'EML'                      TO LINK-CNTCT-TYPE.                  
           MOVE SPACES                     TO LINK-STAFF-NICK.                  
           MOVE K010-NUM-HDB-STAFF-ID      TO LINK-STAFF-NUM.                   
                                                                                
           CALL 'BP13CCNT' USING WS-BP13CCNT-LINK.                              
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROS                                                           
              MOVE LINK-STAFF-CNTCT        TO WS-EMAIL                          
           WHEN 100                                                             
              DISPLAY 'OIC NOT FOUND IN OFFICE_CNTCT TABLE - '                  
               K010-NUM-HDB-STAFF-ID  ', REGN : ' F310-REGN-NO                  
              MOVE  SPACES                 TO WS-EMAIL                          
                                              MAIL-DTL1CCA                      
                                              MAIL-DTL5DD                       
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OFFICE_CNTCT TABLE = '                     
                       LINK-SYSERR                                              
              MOVE  SPACES                 TO WS-EMAIL                          
                                              MAIL-DTL1CCA                      
                                              MAIL-DTL5DD                       
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           IF WS-F310-OUT > 0                                                   
              WRITE BP13MAIL-REC FROM MAIL-END                                  
           ENd-IF                                                               
                                                                                
           DISPLAY 'TOTAL INPUT RECORDS        : ' WS-F310-INPUT                
           DISPLAY 'TOTAL RECORD NOT FILED     : ' WS-F310-OUT.                 
           DISPLAY '        '                                                   
                                                                                
           CLOSE BP13F310                                                       
                 BP13MAIL.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
