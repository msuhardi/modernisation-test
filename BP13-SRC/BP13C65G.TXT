      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C65G.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  JULY 15, 2014.                                            
      *===============================================================*         
      *   OBJECTIVE:  REPORT ON BOOKED CASES AND HLE PENDING          *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F800                                  *         
      *                  3. BM06K110                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. BP13L800                                  *         
      *                  2. BP13L80A                                  *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP134824 15/07/2014 PCL4 NEW PROGRAM.                         *         
      * BP134824 11/08/2014 PCL4 TO ADD RUN-DATE ON THE REPORT        *         
      * BP135499 15/10/2014 PCL4 TO REMOVE CHECKING OF 2ND-CA-SENT    *         
      * BP138047 25/11/2019 KAC1 RECOMPILE FOR BP27 COPYBOOK UPDATE   *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F800  ASSIGN        TO BP13F800.                         
           SELECT  BP13L800  ASSIGN        TO BP13L800.                         
           SELECT  BP13L80A  ASSIGN        TO BP13L80A.                         
                                                                                
                                                                                
           SELECT  BM06K110  ASSIGN       TO BM06K110                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K110-KEY-FLD OF K110-REC           
                             FILE STATUS  IS WS-K110-STATUS.                    
                                                                                
           SELECT  BP27K800  ASSIGN       TO BP27K800                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                       RECORD KEY IS K800-NUM-HLA OF BP27K800-MASTER            
                       FILE STATUS IS WS-K800-STATUS.                           
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K800.                                                           
                                                                                
       FD   BP27K800                                                            
            RECORD CONTAINS 1200 CHARACTERS.                                    
       COPY BP27K800.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  BP13L800                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORDING MODE IS V                              
                               LABEL RECORD IS STANDARD.                        
       01  BP13L800-REC                  PIC X(200).                            
                                                                                
       FD  BP13L80A                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORDING MODE IS V                              
                               LABEL RECORD IS STANDARD.                        
       01  BP13L80A-REC                  PIC X(200).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  BP13L800-HDR.                                                        
           05  FILLER                  PIC X(05) VALUE 'S/NO'.                  
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(08) VALUE 'REGN NO.'.              
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(09) VALUE 'HLE NO'.                
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(13) VALUE 'FLAT-ACPT-DTE'.         
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(13) VALUE 'DAYS OUTSTNDG'.         
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(12) VALUE 'PROV TAG/RSN'.          
           05  FILLER                  PIC X(01).                               
                                                                                
       01  BP13L800-TITLE.                                                      
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(43) VALUE                          
                'REPORT ON CASES WITH NO DOCUMENT SENT DATE '.                  
           05  FILLER                  PIC X(41) VALUE                          
                'AND HLE STILL PENDING MORE THAN 2 MONTHS '.                    
           05  FILLER                  PIC X(17) VALUE                          
                'FROM ACCEPT DATE '.                                            
           05  WS-CURR-DATE            PIC X(10) VALUE SPACES.                  
                                                                                
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-F800-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01   BP13L800-REC1.                                                      
            05  WS-SNO                  PIC ZZZZZ.                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-REGN            PIC X(08).                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-HLE             PIC X(09).                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-DTE-ACCEPT      PIC X(13).                              
            05  FILLER                  PIC X(04).                              
            05  WS-L800-DAYS-OUT        PIC ZZZZ9.                              
            05  FILLER                  PIC X(06).                              
            05  WS-L800-PRV-TAG         PIC X(01).                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-PRV-RSN         PIC X(03).                              
            05  FILLER                  PIC X(01).                              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT                  PIC 9(2)  VALUE ZEROES.                  
           05  WS-I                    PIC 9(2)  VALUE ZEROES.                  
           05  WS-CNT-DAY              PIC 9(08) VALUE ZEROES.                  
           05  WS-CNT-BYPASS           PIC 9(08) VALUE ZEROES.                  
           05  WS-CNT-L800             PIC 9(08) VALUE ZEROES.                  
           05  WS-CNT-L80A             PIC 9(08) VALUE ZEROES.                  
           05  WS-DAY-FLT              PIC 9(05)V99 VALUE ZEROES.               
           05  WS-DAY-OUT              PIC 9(05)    VALUE ZEROES.               
                                                                                
       01  WS-DATE.                                                             
           05  WS-CURR-YY              PIC 9(4)  VALUE ZEROES.                  
           05  WS-CURR-MM              PIC 9(2)  VALUE ZEROES.                  
           05  WS-CURR-DD              PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-FLT-DATE.                                                         
           05  WS-FLT-YY               PIC 9(4)  VALUE ZEROES.                  
           05  WS-FLT-MM               PIC 9(2)  VALUE ZEROES.                  
           05  WS-FLT-DD               PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-FLT-TEMP.                                                         
           05  WS-TMP-YY               PIC 9(4)  VALUE ZEROES.                  
           05  WS-TMP-MM               PIC 9(2)  VALUE ZEROES.                  
           05  WS-TMP-DD               PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-DATE1                    PIC 9(6)  VALUE ZEROES.                  
       01  WS-FLT-DATE1                PIC 9(6)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-VALID-RECORD          PIC X(01) VALUE SPACES.                  
           05 WS-CSM-OIC               PIC X(05) VALUE SPACES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
                                                                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE            THRU 1000-EXIT.                 
           PERFORM 2000-READ-BP13F800           THRU 2000-EXIT.                 
                                                                                
           PERFORM 3000-PROCESS-RECORDS         THRU 3000-EXIT                  
             UNTIL WS-F800-EOF = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE           THRU 9999-EXIT.                 
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BM06K110                                                 
                       BP27K800                                                 
               OUTPUT  BP13L800                                                 
                       BP13L80A.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP27K800 - ERROR OPENING : ' WS-K800-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)    TO WS-DATE.                       
                                                                                
            STRING WS-DATE(7:2) '/'                                             
                   WS-DATE(5:2) '/'                                             
                   WS-DATE(1:4)                                                 
                   DELIMITED BY SIZE INTO WS-CURR-DATE.                         
                                                                                
           WRITE BP13L800-REC               FROM BP13L800-TITLE.                
           WRITE BP13L80A-REC               FROM BP13L800-TITLE.                
           MOVE SPACES                        TO BP13L800-REC.                  
           MOVE SPACES                        TO BP13L80A-REC.                  
           WRITE BP13L800-REC.                                                  
           WRITE BP13L80A-REC.                                                  
                                                                                
           WRITE BP13L800-REC  FROM BP13L800-HDR.                               
           WRITE BP13L80A-REC  FROM BP13L800-HDR.                               
                                                                                
           MOVE SPACES           TO BP13L800-REC.                               
           WRITE BP13L800-REC.                                                  
           WRITE BP13L80A-REC.                                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F800.                                                      
      ******************************************************************        
                                                                                
           READ BP13F800           AT END                                       
                MOVE 'Y'           TO WS-F800-EOF                               
                GO                 TO 2000-EXIT.                                
                                                                                
           ADD  1                  TO WS-F800-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2100-CHECK-VALID-DATES.                                                  
      ******************************************************************        
                                                                                
           MOVE 'Y'                                TO WS-VALID-RECORD.          
           MOVE K110-DTE-ACCEPTANCE                TO WS-FLT-DATE.              
                                                                                
           COMPUTE WS-FLT-MM = WS-FLT-MM + 2.                                   
                                                                                
           IF WS-FLT-MM > 12                                                    
              COMPUTE WS-FLT-MM = WS-FLT-MM - 12                                
              COMPUTE WS-FLT-YY = WS-FLT-YY + 1                                 
           END-IF.                                                              
                                                                                
           IF WS-FLT-DATE >= WS-DATE                                            
              MOVE 'N'                             TO WS-VALID-RECORD           
           ELSE                                                                 
              MOVE WS-FLT-DATE(1:6)                TO WS-FLT-DATE1              
              MOVE WS-DATE(1:6)                    TO WS-DATE1                  
                                                                                
              COMPUTE WS-DAY-FLT  = WS-DATE1 - WS-FLT-DATE1                     
                                                                                
              IF WS-DAY-FLT <= 12                                               
                 IF WS-DAY-FLT = 12                                             
                    COMPUTE WS-DAY-OUT = 365                                    
                 ELSE                                                           
                    COMPUTE WS-DAY-OUT = WS-DAY-FLT * 30                        
                 END-IF                                                         
              ELSE                                                              
                 IF WS-CURR-YY > WS-FLT-YY                                      
                    COMPUTE WS-TMP-YY = WS-CURR-YY - WS-FLT-YY                  
                                                                                
                    IF WS-TMP-YY >= 1                                           
                       COMPUTE WS-TMP-YY = WS-TMP-YY * 365                      
                    END-IF                                                      
                                                                                
                    COMPUTE WS-TMP-MM  = (12 - WS-FLT-MM) + WS-CURR-MM          
                    COMPUTE WS-DAY-OUT = (WS-TMP-MM * 30) + WS-TMP-YY           
                 END-IF                                                         
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 3100-READ-BM06K110               THRU 3100-EXIT.             
                                                                                
           IF (K110-DTE-ACCEPTANCE NOT = SPACES AND LOW-VALUES                  
                                                AND ZEROES) AND                 
              (K800-DTE-HLE-DOC-SENT = SPACES OR LOW-VALUES OR ZEROES)          
      *                                                     AND                 
      *       (K800-DTE-2ND-CA-SENT NOT = SPACES AND LOW-VALUES)                
              PERFORM 2100-CHECK-VALID-DATES         THRU 2100-EXIT             
              PERFORM 3200-READ-BP27K800             THRU 3200-EXIT             
           ELSE                                                                 
              MOVE 'N'                               TO WS-VALID-RECORD         
           END-IF.                                                              
                                                                                
           IF WS-VALID-RECORD = 'Y'                                             
              PERFORM 4000-WRITE-BP13L800            THRU 4000-EXIT             
           ELSE                                                                 
              ADD 1                                  TO WS-CNT-BYPASS           
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800                THRU 2000-EXIT.            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-BM06K110.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO K110-REC.                           
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE K800-NUM-SCH-ACC         TO K110-KEY-FLD OF K110-REC.           
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 23                                                          
                    MOVE SPACES                           TO K110-REC           
                    INITIALIZE                               K110-REC           
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K110 : ' WS-K110-STATUS          
                            ' KEY  = ' K110-KEY-FLD OF K110-REC                 
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-READ-BP27K800.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP27K800-MASTER.                    
           INITIALIZE                       BP27K800-MASTER.                    
                                                                                
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
             TO K800-NUM-HLA OF BP27K800-MASTER.                                
                                                                                
           READ BP27K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    IF K800-NUM-CASE-STATUS = 'PP ' OR 'NEW'                    
                       CONTINUE                                                 
                    ELSE                                                        
                       MOVE 'N'                TO WS-VALID-RECORD               
                    END-IF                                                      
                                                                                
               WHEN 23                                                          
                    MOVE 'N'                   TO WS-VALID-RECORD               
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP27K800 : ' WS-K800-STATUS          
                            ' KEY  = ' K800-NUM-HLA OF BP27K800-MASTER          
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-WRITE-BP13L800.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                        TO BP13L800-REC                   
                                                 BP13L80A-REC                   
                                                 BP13L800-REC1.                 
           INITIALIZE                            BP13L800-REC                   
                                                 BP13L80A-REC                   
                                                 BP13L800-REC1.                 
                                                                                
           MOVE K800-NUM-REGN                   TO WS-L800-REGN.                
           MOVE K800-NUM-HLA OF BP13K800-MASTER TO WS-L800-HLE.                 
           MOVE WS-DAY-OUT                      TO WS-L800-DAYS-OUT.            
                                                                                
           IF K110-DTE-ACCEPTANCE NOT = SPACES AND LOW-VALUES                   
              STRING K110-DTE-ACCEPTANCE(7:2) '/'                               
                     K110-DTE-ACCEPTANCE(5:2) '/'                               
                     K110-DTE-ACCEPTANCE(1:4)                                   
                       DELIMITED BY SIZE INTO WS-L800-DTE-ACCEPT                
           END-IF.                                                              
                                                                                
           IF K800-NUM-PROV-TAG NOT = SPACES AND LOW-VALUES                     
              ADD 1                        TO WS-CNT-L800                       
              MOVE WS-CNT-L800             TO WS-SNO                            
              MOVE K800-NUM-PROV-TAG       TO WS-L800-PRV-TAG                   
              MOVE K800-NUM-PROV-REASON    TO WS-L800-PRV-RSN                   
                                                                                
              WRITE BP13L800-REC         FROM BP13L800-REC1                     
           ELSE                                                                 
              ADD 1                        TO WS-CNT-L80A                       
              MOVE WS-CNT-L80A             TO WS-SNO                            
              MOVE SPACES                  TO WS-L800-PRV-TAG                   
                                              WS-L800-PRV-RSN                   
              WRITE BP13L80A-REC         FROM BP13L800-REC1                     
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           IF WS-CNT-L800 = ZEROES                                              
              MOVE SPACES           TO BP13L800-REC                             
                                       BP13L80A-REC                             
              WRITE BP13L800-REC                                                
              WRITE BP13L80A-REC                                                
                                                                                
              MOVE '              NO RECORD FOUND!           '                  
                                  TO BP13L800-REC                               
                                     BP13L80A-REC                               
              WRITE BP13L800-REC                                                
              WRITE BP13L80A-REC                                                
           END-IF.                                                              
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C65G'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1)  NO OF BP13F800 RECORDS READ............. : '           
                    WS-F800-READ.                                               
           DISPLAY '(2)  NO OF BP13F800 RECORDS BYPASS   ..... : '              
                    WS-CNT-BYPASS.                                              
           DISPLAY '(3)  NO OF BP13F800 RECORDS WRITTEN  ..... : '              
                    WS-CNT-L800.                                                
           DISPLAY '(4)  NO OF BP13F80A RECORDS WRITTEN  ..... : '              
                    WS-CNT-L80A.                                                
                                                                                
           CLOSE    BP13F800                                                    
                    BM06K110                                                    
                    BP27K800                                                    
                    BP13L80A                                                    
                    BP13L800.                                                   
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP27K800 - ERROR CLOSING : ' WS-K800-STATUS              
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
