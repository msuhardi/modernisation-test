      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13CC32.                                                 
       AUTHOR.        ARIEL HERNANDEZ.                                          
       DATE-WRITTEN.  25/09/2006.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  CHECK FOR MULTIPLE BP13FD38 FOR REVISED       *        
      *                  INSTALMENT EFFECTIVE THE FOLLOWING MONTH,     *        
      *                  TAKE THE LATEST BP13FD38 TO EFFECT NEW        *        
      *                  INSTALMENT.                                   *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13FD38                                  *        
      *   OUTPUT FILES:  1.  BP13KD38 - DELETE RECORDS, OTHER THAN THE *        
      *                                 LATEST SCHEDULE FOR MULTIPLE   *        
      *                                 BP13FD38 FOR REVISED INSTALMENT*        
      *                                 EFFECTIVE ON THE SAME MONTH.   *        
      *                  2.  P13FD38A - CASES NOT EFFECTIVE IN THE     *        
      *                                 FOLLOWING MONTH                *        
      *                  3.  P13FD38B - CASES WHERE INSTALMENT EFF THE *        
      *                                 FOLLOWING MONTH                *        
      *                                 BP13CC16 WILL UPDATE NEW INSTAL*        
      *                                 TO BP13KD00 AFTER CLOSING ACCT *        
      *                                 OR NOT DUE CASES               *        
      *                  3.  P13FD38C - DUPLICATE RECORDS THAT ARE EFF *        
      *                                 ON THE FOLLOWING MONTH         *        
      *                  3.  BP13LC32 - REPORT ON DUPLICATE TECORDS    *        
      *                                 EFF ON THE FOLLOWING MONTH     *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP132914  BA3  25/09/2006 NEW PGM                            *        
      *   BP133360  ED1  17/06/2008 UPD KD38-NUM-EFF-INSTAL FOR EFF CASE        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD38  ASSIGN        TO BP13FD38.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
           SELECT BP13KD38  ASSIGN        TO BP13KD38                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD38-KEY-FLD                       
                            FILE STATUS   IS WS-KD38-STATUS.                    
                                                                                
           SELECT P13FD38A  ASSIGN        TO P13FD38A.                          
           SELECT P13FD38B  ASSIGN        TO P13FD38B.                          
           SELECT P13FD38C  ASSIGN        TO P13FD38C.                          
           SELECT BP13LC32  ASSIGN        TO BP13LC32.                          
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13FD38                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORDS   ARE STANDARD                                         
           RECORDING MODE IS F.                                                 
                                                                                
       COPY BP13FD38.                                                           
                                                                                
       FD  BP13KD38                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
                                                                                
       COPY BP13KD38.                                                           
                                                                                
       FD  P13FD38A                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORDS   ARE STANDARD                                         
           RECORDING MODE IS F.                                                 
                                                                                
       01  P13FD38A-TRANS-REC    PIC X(400).                                    
                                                                                
       FD  P13FD38B                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORDS   ARE STANDARD                                         
           RECORDING MODE IS F.                                                 
                                                                                
       01  P13FD38B-TRANS-REC    PIC X(400).                                    
                                                                                
       FD  P13FD38C                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORDS   ARE STANDARD                                         
           RECORDING MODE IS F.                                                 
                                                                                
       01  P13FD38C-TRANS-REC    PIC X(400).                                    
                                                                                
       FD  BP13LC32                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL  RECORD    IS STANDARD                                         
           RECORDING MODE   IS F.                                               
                                                                                
       01  BP13LC32-REC          PIC X(132).                                    
                                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-KD38-STATUS               PIC 9(2)    VALUE ZERO.                 
       01  CNT-FD38-READ                PIC 9(8)    VALUE ZERO.                 
       01  CNT-FD38A-WRITE              PIC 9(8)    VALUE ZERO.                 
       01  CNT-FD38B-WRITE              PIC 9(8)    VALUE ZERO.                 
       01  CNT-FD38C-WRITE              PIC 9(8)    VALUE ZERO.                 
       01  CNT-KD38-DELETE              PIC 9(8)    VALUE ZERO.                 
       01  CNT-KD38-UPDATE              PIC 9(8)    VALUE ZERO.                 
       01  CNT-KD38-READ                PIC 9(8)    VALUE ZERO.                 
       01  CNT-KD38-NOTFOUND            PIC 9(8)    VALUE ZERO.                 
       01  CNT-DUE-VALID                PIC 9(8)    VALUE ZERO.                 
       01  CNT-DUE-DUPLICATE            PIC 9(8)    VALUE ZERO.                 
       01  CNT-NOT-DUE                  PIC 9(8)    VALUE ZERO.                 
                                                                                
       01  WS-SYS-DATE                  PIC X(8)    VALUE SPACES.               
       01  WS-NEXT-MO.                                                          
           05  WS-NEXT-MO-CCYY          PIC 9(4)    VALUE ZEROES.               
           05  WS-NEXT-MO-MM            PIC 9(2)    VALUE ZEROES.               
       01  WS-SYS-TIME                  PIC X(8)    VALUE SPACES.               
                                                                                
       01  WS-EOF-FD38                  PIC X(1)    VALUE SPACES.               
       01  WS-FIRST-VALID               PIC X(1)    VALUE 'N'.                  
       01  WS-PRINT-PREV-FD38           PIC X(1)    VALUE 'N'.                  
       01  WS-FD38-REGN                 PIC X(8)    VALUE SPACES.               
       01  WS-LINE                      PIC 9(2)    VALUE 99.                   
       01  WS-PAGE                      PIC 9(5)    VALUE ZEROS.                
       01  WS-SNO                       PIC 9(5)    VALUE ZEROS.                
                                                                                
       01  WS-BP13FD38-TRANS-REC-TMP.                                           
           05  WS-FD38-NUM-REGN         PIC X(8)    VALUE SPACES.               
           05  WS-FD38-DTE-DUE          PIC X(8)    VALUE SPACES.               
           05  WS-FD38-AMT-LOAN-NEW     PIC 9(7)V99 VALUE ZEROS.                
           05  WS-FD38-AMT-INSTAL-NEW-TOT   PIC 9(6)V99   VALUE ZEROS.          
           05  WS-FD38-NUM-SCH-ACC      PIC X(11)   VALUE SPACES.               
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  LC32-PR-HEAD-01.                                                     
           05  LC32-RPTNAME        PIC X(8)      VALUE 'BP13LC32'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LC32-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  LC32-PRT-PAGE       PIC ZZZZ9 .                                  
                                                                                
       01  LC32-PR-HEAD-02.                                                     
           05  FILLER              PIC X(37)     VALUE SPACES .                 
           05  FILLER              PIC X(60)     VALUE                          
             'MULTIPLE PROGRESS PAYMENT EFFECTIVE THE FOLLOWING MONTH'.         
           05  FILLER              PIC X(28)     VALUE SPACES.                  
                                                                                
                                                                                
       01  LC32-PR-HEAD-03.                                                     
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE                          
               ' SNO '.                                                         
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE                          
               'REGN NO'.                                                       
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE                          
               'SCH ACCT NO'.                                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(17)     VALUE                          
               'DUE-DATE '.                                                     
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE                          
               'AMT-NEW-LOAN'.                                                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE                          
               'AMT-INSTAL'.                                                    
                                                                                
       01  LC32-PR-DTL.                                                         
           05  FILLER              PIC X(25) VALUE SPACES.                      
           05  LC32-PRT-SNO        PIC ZZZZ9       VALUE ZEROES.                
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  LC32-REGN-NO        PIC X(08) VALUE SPACES.                      
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  LC32-SCH-ACCT       PIC X(11) VALUE SPACES.                      
           05  FILLER              PIC X(03) VALUE SPACES.                      
           05  LC32-DTE-DUE        PIC X(10) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE SPACES.                      
           05  LC32-AMT-NEWLOAN    PIC Z,ZZZ,ZZ9.99 VALUE ZEROES.               
           05  FILLER              PIC X(03) VALUE SPACES.                      
           05  LC32-AMT-INSTAL     PIC ZZ,ZZ9.99   VALUE  ZEROES.               
           05  FILLER              PIC X(08) VALUE SPACES.                      
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
           PERFORM  1000-OPEN-FILES        THRU    1000-EXIT.                   
           PERFORM  2000-READ-FD38         THRU    2000-EXIT.                   
           PERFORM  2500-READ-SY02F001     THRU    2500-EXIT.                   
           PERFORM  3000-PROCESS           THRU    3000-EXIT                    
                    UNTIL WS-EOF-FD38 = 'Y'.                                    
           PERFORM  9000-CLOSE-ROUTINE     THRU    9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13FD38                                                 
                       SY02F001                                                 
                  I-O  BP13KD38                                                 
               OUTPUT  P13FD38A                                                 
                       P13FD38B                                                 
                       P13FD38C                                                 
                       BP13LC32.                                                
                                                                                
           IF WS-KD38-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13KD38-STATUS ' WS-KD38-STATUS         
               MOVE     WS-KD38-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-FD38.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13FD38                                                        
                AT END MOVE 'Y' TO WS-EOF-FD38                                  
                       GO TO 2000-EXIT                                          
           END-READ.                                                            
                                                                                
           ADD 1 TO CNT-FD38-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       2500-READ-SY02F001.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT      TO WS-SYS-DATE.                           
                                                                                
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
                  DELIMITED BY SIZE                                             
           INTO LC32-DATE.                                                      
                                                                                
           MOVE F001-DTE-CURRENT(1:6) TO WS-NEXT-MO.                            
           IF WS-NEXT-MO-MM = 12                                                
              MOVE 01 TO WS-NEXT-MO-MM                                          
              ADD   1 TO WS-NEXT-MO-CCYY                                        
           ELSE                                                                 
              ADD   1 TO WS-NEXT-MO-MM                                          
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           MOVE 'N'           TO WS-FIRST-VALID                                 
                                 WS-PRINT-PREV-FD38.                            
                                                                                
           MOVE FD38-NUM-REGN TO WS-FD38-REGN.                                  
           PERFORM 4000-BYPASS-BP13FD38 THRU 4000-EXIT                          
                        UNTIL FD38-NUM-REGN NOT = WS-FD38-REGN                  
                        OR WS-EOF-FD38 = 'Y'.                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-BYPASS-BP13FD38.                                                    
      *----------------------------------------------------------------*        
                                                                                
           IF FD38-DTE-INSTAL-NEW(1:6) = WS-NEXT-MO                             
              IF FD38-NUM-REGN  = WS-FD38-REGN AND                              
                 WS-FIRST-VALID = 'N'                                           
                 ADD 1                      TO CNT-DUE-VALID                    
                                                                                
      *_______   MOVE THE FIRST RECORD TO THE TEMPORARY STORAGE                 
                 PERFORM 4500-MOVE-FD38-TO-TEMP THRU 4500-EXIT                  
                                                                                
                 PERFORM 5200-WRITE-FD38B THRU 5200-EXIT                        
                 MOVE 'Y'                   TO WS-FIRST-VALID                   
                                               WS-PRINT-PREV-FD38               
                 PERFORM 4200-READ-KD38     THRU 4200-EXIT                      
                 MOVE 'Y' TO KD38-NUM-EFF-INSTAL                                
                 PERFORM 4300-REWRITE-KD38  THRU 4300-EXIT                      
              ELSE                                                              
                 IF WS-PRINT-PREV-FD38 = 'Y'                                    
                    ADD   1                 TO CNT-DUE-DUPLICATE                
                                                                                
      *_______      PRINT THE 1ST RECORD SAVED FROM TEMPORARY STORAGE           
                    PERFORM 7000-PRINT-REC     THRU 7000-EXIT                   
                                                                                
                    MOVE 'N'                TO WS-PRINT-PREV-FD38               
                 END-IF                                                         
                 ADD 1                      TO CNT-DUE-DUPLICATE                
                 PERFORM 7000-PRINT-REC   THRU 7000-EXIT                        
                 PERFORM 4200-READ-KD38   THRU 4200-EXIT                        
                 PERFORM 5500-WRITE-FD38C THRU 5500-EXIT                        
                 PERFORM 6000-DELETE-KD38 THRU 6000-EXIT                        
              END-IF                                                            
           ELSE                                                                 
              ADD 1                         TO CNT-NOT-DUE                      
              PERFORM 5000-WRITE-FD38A    THRU 5000-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FD38  THRU 2000-EXIT.                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4200-READ-KD38.                                                          
      *----------------------------------------------------------------*        
                                                                                
           MOVE FD38-KEY-FLD TO KD38-KEY-FLD.                                   
                                                                                
           READ BP13KD38                                                        
           EVALUATE WS-KD38-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO CNT-KD38-READ                                         
              WHEN 23                                                           
                 ADD 1 TO CNT-KD38-NOTFOUND                                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD38 - STATUS '                  
                             WS-KD38-STATUS                                     
                 MOVE        WS-KD38-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4300-REWRITE-KD38.                                                       
      *----------------------------------------------------------------*        
           REWRITE BP13KD38-TRANS-REC.                                          
           EVALUATE WS-KD38-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO CNT-KD38-UPDATE                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN UPDATING BP13KD38 - STATUS '                 
                             WS-KD38-STATUS ' KEY' KD38-KEY-FLD                 
                 MOVE        WS-KD38-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4500-MOVE-FD38-TO-TEMP.                                                  
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                  TO WS-BP13FD38-TRANS-REC-TMP.           
           INITIALIZE                      WS-BP13FD38-TRANS-REC-TMP.           
                                                                                
           MOVE FD38-NUM-REGN           TO WS-FD38-NUM-REGN.                    
           MOVE FD38-DTE-DUE            TO WS-FD38-DTE-DUE.                     
           MOVE FD38-AMT-LOAN-NEW       TO WS-FD38-AMT-LOAN-NEW.                
           MOVE FD38-AMT-INSTAL-NEW-TOT TO WS-FD38-AMT-INSTAL-NEW-TOT.          
           MOVE FD38-NUM-SCH-ACC        TO WS-FD38-NUM-SCH-ACC.                 
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-WRITE-FD38A.                                                        
      *----------------------------------------------------------------*        
                                                                                
           WRITE P13FD38A-TRANS-REC FROM BP13FD38-TRANS-REC.                    
           ADD   1                  TO CNT-FD38A-WRITE.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5200-WRITE-FD38B.                                                        
      *----------------------------------------------------------------*        
                                                                                
           WRITE P13FD38B-TRANS-REC FROM BP13FD38-TRANS-REC.                    
           ADD   1                  TO CNT-FD38B-WRITE.                         
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       5500-WRITE-FD38C.                                                        
      *----------------------------------------------------------------*        
                                                                                
           IF WS-KD38-STATUS = 00                                               
              WRITE P13FD38C-TRANS-REC FROM BP13KD38-TRANS-REC                  
           ELSE                                                                 
              WRITE P13FD38C-TRANS-REC FROM BP13FD38-TRANS-REC                  
           END-IF.                                                              
           ADD   1                  TO CNT-FD38C-WRITE.                         
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       6000-DELETE-KD38.                                                        
      *----------------------------------------------------------------*        
                                                                                
           MOVE FD38-NUM-REGN     TO KD38-NUM-REGN.                             
           MOVE FD38-NUM-TRANS    TO KD38-NUM-TRANS.                            
           MOVE FD38-NUM-SCHEDULE TO KD38-NUM-SCHEDULE.                         
                                                                                
           DELETE BP13KD38.                                                     
           EVALUATE WS-KD38-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN DELETING BP13KD38 - STATUS '                 
                             WS-KD38-STATUS                                     
                 MOVE        WS-KD38-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
           ADD 1 TO CNT-KD38-DELETE.                                            
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       7000-PRINT-REC.                                                          
      *----------------------------------------------------------------*        
                                                                                
           IF WS-LINE > 50                                                      
              ADD 1                   TO WS-PAGE                                
              MOVE WS-PAGE            TO LC32-PRT-PAGE                          
              MOVE SPACES             TO BP13LC32-REC                           
              WRITE BP13LC32-REC                       AFTER PAGE               
              WRITE BP13LC32-REC FROM LC32-PR-HEAD-01  AFTER 3                  
              WRITE BP13LC32-REC FROM LC32-PR-HEAD-02  AFTER 2                  
              WRITE BP13LC32-REC FROM LC32-PR-HEAD-03  AFTER 2                  
              MOVE SPACES TO BP13LC32-REC                                       
              WRITE BP13LC32-REC                                                
              MOVE  8 TO WS-LINE                                                
           END-IF.                                                              
                                                                                
           IF WS-PRINT-PREV-FD38 = 'Y'                                          
              MOVE WS-FD38-NUM-REGN         TO LC32-REGN-NO                     
              MOVE WS-FD38-NUM-SCH-ACC      TO LC32-SCH-ACCT                    
              STRING WS-FD38-DTE-DUE(7:2) '/'                                   
                     WS-FD38-DTE-DUE(5:2) '/'                                   
                     WS-FD38-DTE-DUE(1:4)                                       
                     DELIMITED BY SIZE                                          
                INTO LC32-DTE-DUE                                               
              MOVE WS-FD38-AMT-LOAN-NEW     TO LC32-AMT-NEWLOAN                 
              MOVE WS-FD38-AMT-INSTAL-NEW-TOT                                   
                                            TO LC32-AMT-INSTAL                  
           ELSE                                                                 
              MOVE FD38-NUM-REGN         TO LC32-REGN-NO                        
              MOVE FD38-NUM-SCH-ACC      TO LC32-SCH-ACCT                       
              STRING FD38-DTE-DUE(7:2) '/'                                      
                     FD38-DTE-DUE(5:2) '/'                                      
                     FD38-DTE-DUE(1:4)                                          
                     DELIMITED BY SIZE                                          
                INTO LC32-DTE-DUE                                               
              MOVE FD38-AMT-LOAN-NEW     TO LC32-AMT-NEWLOAN                    
              MOVE FD38-AMT-INSTAL-NEW-TOT                                      
                                         TO LC32-AMT-INSTAL                     
           END-IF.                                                              
                                                                                
           ADD 1                      TO WS-SNO.                                
           MOVE WS-SNO                TO LC32-PRT-SNO.                          
           WRITE BP13LC32-REC       FROM LC32-PR-DTL.                           
           ADD 1                      TO WS-LINE.                               
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
           CLOSE BP13FD38                                                       
                 SY02F001                                                       
                 BP13KD38                                                       
                 P13FD38A                                                       
                 P13FD38B                                                       
                 P13FD38C                                                       
                 BP13LC32.                                                      
                                                                                
           IF WS-KD38-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13KD38-STATUS ' WS-KD38-STATUS.         
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13CC32 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM  (BP13FD38)   = '                   
                    CNT-FD38-READ.                                              
           DISPLAY 'NO OF RECORDS READ FROM (BP13KD38)    = '                   
                    CNT-KD38-READ.                                              
           DISPLAY 'NO OF RECORDS NOT FOUND (BP13KD38)    = '                   
                    CNT-KD38-NOTFOUND.                                          
           DISPLAY 'NO OF RECORDS DELETED FROM (BP13KD38) = '                   
                    CNT-KD38-DELETE.                                            
           DISPLAY 'NO OF RECORDS UPDATED      (BP13KD38) = '                   
                    CNT-KD38-UPDATE.                                            
           DISPLAY 'NO OF VALID-DUE-REC                   = '                   
                    CNT-DUE-VALID.                                              
           DISPLAY 'NO OF VALID-DUE-DUPLICATE             = '                   
                    CNT-DUE-DUPLICATE.                                          
           DISPLAY 'NO OF NOT DUE CASES                   = '                   
                    CNT-NOT-DUE.                                                
           DISPLAY 'NO OF RECORDS WRITTEN    (P13FD38A)   = '                   
                    CNT-FD38A-WRITE.                                            
           DISPLAY 'NO OF RECORDS WRITTEN    (P13FD38B)   = '                   
                    CNT-FD38B-WRITE.                                            
           DISPLAY 'NO OF RECORDS WRITTEN    (P13FD38C)   = '                   
                    CNT-FD38C-WRITE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
