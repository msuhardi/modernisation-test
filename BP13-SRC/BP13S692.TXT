  /*******************************************************************/         
  /*   SYSTEM OF COMMITMENT (BP13)                                   */         
  /*=================================================================*/         
  /* OBJECTIVES :                                                    */         
  /*     TO BACKUP K730 AFTER DETERMINE HOUSEHOLD TYPE AND FLAG ERROR*/         
  /*     WHERE HOUSEHOLD TYPE IS BLANK                               */         
  /*-----------------------------------------------------------------*/         
  /* MODIFICATIONS                                                   */         
  /* =============                                                   */         
  /* CHGE    DATE     OIC  DESCRIPTION                               */         
  /* ------- -------- ---- ------------                              */         
  /*BP135279 24032014 YCH2 CHECK ON DETERMINED HOUSEHOLD TYPE        */         
  /*******************************************************************/         
  OPTIONS NODATE NONUMBER;                                                      
   DATA BP13F730;                                                               
      INFILE BP13F730;                                                          
      INPUT @123 NUM_REGNO $CHAR8.                                              
            @1   F730_REC1 $CHAR250.                                            
            @251 F730_REC2 $CHAR250.;                                           
                                                                                
   PROC SORT;                                                                   
      BY NUM_REGNO;                                                             
                                                                                
   DATA BP13K730;                                                               
      INFILE BP13K730;                                                          
      INPUT @1   REC1      $CHAR250.                                            
            @251 REC2      $CHAR250.                                            
            @123 NUM_REGNO $CHAR8.                                              
            @116 NUM_HHTYPE $CHAR1.;                                            
                                                                                
   PROC SORT;                                                                   
      BY NUM_REGNO;                                                             
                                                                                
   DATA HHBLANK F730NFND;                                                       
      MERGE BP13F730 (IN=F730) BP13K730 (IN=K730);                              
      BY NUM_REGNO;                                                             
      IF F730 AND K730 THEN                                                     
         DO;                                                                    
            FILE P13F730O;                                                      
            PUT @1 REC1 $CHAR250.                                               
                @251 REC2 $CHAR250.;                                            
            IF NUM_HHTYPE = ' ' THEN                                            
               DO;                                                              
                  OUTPUT HHBLANK;                                               
                  FILE P13F730E;                                                
                  PUT @1 REC1 $CHAR250.                                         
                      @251 REC2 $CHAR250.;                                      
               END;                                                             
        END;                                                                    
      ELSE                                                                      
      IF F730 AND NOT K730 THEN                                                 
         DO;                                                                    
            OUTPUT F730NFND;                                                    
            FILE P13F730F;                                                      
            PUT @1   F730_REC1 $CHAR250.                                        
                @251 F730_REC2 $CHAR250.;                                       
         END;                                                                   
                                                                                
     /*------------------*/                                                     
     /* F730 NOT IN K730 */                                                     
     /*------------------*/                                                     
     DATA _NULL_;                                                               
                                                                                
     IF NUMBER  > 0 THEN                                                        
         ABORT RETURN 98;                                                       
                                                                                
     SET F730NFND NOBS=NUMBER;                                                  
                                                                                
     /*---------------------------*/                                            
     /* K730 HOUSEHOLD TYPE EMPTY */                                            
     /*---------------------------*/                                            
     DATA _NULL_;                                                               
                                                                                
     IF NUMBER  > 0 THEN                                                        
         ABORT RETURN 97;                                                       
                                                                                
     SET HHBLANK  NOBS=NUMBER;                                                  
                                                                                
