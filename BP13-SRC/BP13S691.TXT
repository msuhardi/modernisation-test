  /*******************************************************************/         
  /*   SYSTEM OF COMMITMENT (BP13)                                   */         
  /*=================================================================*/         
  /* CONVERT FROM BP13C691                                           */         
  /* OBJECTIVES :                                                    */         
  /*     TO PRINT REPORT ON REGISTERED HOUSEHOLD THAT RECEIVED BY    */         
  /*     SALES UNIT                                                  */         
  /*-----------------------------------------------------------------*/         
  /* MODIFICATIONS                                                   */         
  /* =============                                                   */         
  /* CHGE    DATE   BY   DESCRIPTION                                 */         
  /* ------- ------ ---- -----------                                 */         
  /* 0301    071097 YCH2 TRANSFERED REPORT                           */         
  /* 0364    071197 YCH2 CONVERT FROM COBOL TO SAS                   */         
  /*BP130652 160999 LSB1 CHANGE THE POSITION OF DTE-UPDATE           */         
  /*BP132500 010404 YCH2 INCLUDE BUY-BACK AND EXPAND FLAT TYPE       */         
  /*******************************************************************/         
    PROC FORMAT;                                                                
    VALUE $MAFRLN  '00'='RT/L        '                                          
                   '01'='GPARENT     '                                          
                   '02'='FATHER      '                                          
                   '03'='UNCLE/AUNT  '                                          
                   '04'='U/A-LAW     '                                          
                   '05'='COUSIN      '                                          
                   '06'='BRO/SIS     '                                          
                   '07'='BRO/SIS-LAW '                                          
                   '08'='NEPHEW      '                                          
                   '09'='FIANCE      '                                          
                   '10'='2ND WIFE    '                                          
                   '11'='GPARENT     '                                          
                   '12'='MOTHER      '                                          
                   '13'='UNCLE/AUNT  '                                          
                   '14'='U/A-LAW     '                                          
                   '15'='COUSIN      '                                          
                   '19'='2ND MOTHER  '                                          
                   '20'='SPOUSE      '                                          
                   '21'='GPARENT-LAW '                                          
                   '22'='FATHER-LAW  '                                          
                   '23'='U/A-LAW     '                                          
                   '24'='U/A-LAW     '                                          
                   '25'='COUSIN      '                                          
                   '26'='BRO/SIS-LAW '                                          
                   '27'='BRO/SIS-LAW '                                          
                   '28'='NEPHEW      '                                          
                   '31'='GPARENT-LAW '                                          
                   '32'='MOTHER-LAW  '                                          
                   '33'='U/A-LAW     '                                          
                   '34'='U/A-LAW     '                                          
                   '35'='COUSIN      '                                          
                   '40'='UNRELATED   '                                          
                   '41'='SON/DAU     '                                          
                   '42'='SON/DAU-LAW '                                          
                   '43'='GSON/DAU    '                                          
                   '44'='GSON/DAU-LAW'                                          
                   '45'='RELATIVE    '                                          
                   '46'='RELATIVE    '                                          
                   '47'='UNRELATED   '                                          
                   '48'='UNRELATED   '                                          
                   '49'='UNRELATED   '                                          
                   '50'='SUBTENANT   '                                          
                   '51'='GPARENT     '                                          
                   '52'='FATHER      '                                          
                   '53'='UNCLE/AUNT  '                                          
                   '54'='U/A-LAW     '                                          
                   '55'='COUSIN      '                                          
                   '56'='BRO/SIS     '                                          
                   '57'='BRO/SIS-LAW '                                          
                   '58'='NEPHEW      '                                          
                   '60'='2ND WIFE    '                                          
                   '61'='GPARENT     '                                          
                   '62'='MOTHER      '                                          
                   '63'='UNCLE/AUNT  '                                          
                   '64'='U/A-LAW     '                                          
                   '65'='COUSIN      '                                          
                   '69'='2ND MOTHER  '                                          
                   '70'='SPOUSE      '                                          
                   '71'='GPARENT-LAW '                                          
                   '72'='FATHER-LAW  '                                          
                   '73'='U/A-LAW     '                                          
                   '74'='U/A-LAW     '                                          
                   '75'='COUSIN      '                                          
                   '76'='BRO/SIS-LAW '                                          
                   '77'='BRO/SIS-LAW '                                          
                   '78'='NEPHEW      '                                          
                   '81'='GPARENT-LAW '                                          
                   '82'='MOTHER-LAW  '                                          
                   '83'='U/A-LAW     '                                          
                   '84'='U/A-LAW     '                                          
                   '85'='COUSIN      '                                          
                   '91'='SON/DAU     '                                          
                   '92'='SON/DAU-LAW '                                          
                   '93'='GSON/DAU    '                                          
                   '94'='GSON/DAU-LAW'                                          
                   '95'='RELATIVE    '                                          
                   '96'='RELATIVE    '                                          
                   '97'='UNRELATED   ';                                         
                                                                                
    VALUE $ETH      0='C'                                                       
                    1='C'                                                       
                    2='C'                                                       
                    3='C'                                                       
                    4='C'                                                       
                    5='M'                                                       
                    6='I'                                                       
                    7='O';                                                      
                                                                                
    VALUE $SEX      1='M'                                                       
                    2='F';                                                      
                                                                                
    VALUE $MAR      1='S'                                                       
                    2='M'                                                       
                    3='W'                                                       
                    4='D';                                                      
                                                                                
    VALUE $CITZEN   00='OC'                                                     
                    01='ID'                                                     
                    02='TH'                                                     
                    03='IN'                                                     
                    04='SL'                                                     
                    05='BD'                                                     
                    06='PH'                                                     
                    07='TN'                                                     
                    10='SC'                                                     
                    20='SP'                                                     
                    21='PS'                                                     
                    30='MC'                                                     
                    40='MP'                                                     
                    60='SB'                                                     
                    80='SR'                                                     
                    99='NI';                                                    
                                                                                
                                                                                
  OPTIONS NODATE NONUMBER;                                                      
   DATA BP13F830;                                                               
      INFILE BP13F830;                                                          
      INPUT @1   NUM_REF  $CHAR8.                                               
            @33  BLOCK   $CHAR5.                                                
            @33  M_BLK    $CHAR4.  @39 S_BLK   $CHAR1.                          
            @56  ST_NME   $CHAR20. @46 UNIT    $CHAR10.                         
            @46  LVL      $CHAR2.  @49 HSEMAIN $CHAR4.                          
            @53  HSESUB   $CHAR3.;                                              
                                                                                
      LENGTH HSE_NO $11.;                                                       
      IF HSESUB='   ' THEN                                                      
         HSE_NO='#'||LVL||'-'||HSEMAIN;                                         
      ELSE                                                                      
         HSE_NO='#'||LVL||'-'||HSEMAIN||'-'||HSESUB;                            
                                                                                
   PROC SORT;                                                                   
      BY NUM_REF;                                                               
                                                                                
   DATA BP13F500;                                                               
      INFILE BP13F500;                                                          
      INPUT @123 NUM_REF $CHAR8.                                                
            @41  LSE_IC1 $CHAR9.                                                
            @82  LSE_IC2 $CHAR9.                                                
            @50  LSE_IC3 $CHAR9.                                                
            @59  LSE_IC4 $CHAR9.                                                
            @78  ZONE    $CHAR2.                                                
            @187 NT      $CHAR3.                                                
            @211 FT_TYPE $CHAR2.                                                
            @213 ALL_SCH  $CHAR3.                                               
            @248 ELG_SCH  $CHAR3.                                               
            @225 CAT      $CHAR1.                                               
            @216 JB_CODE  $CHAR1.                                               
            @217 JB_REF   $CHAR6.                                               
            @423 BUYBACK  $CHAR1.                                               
            @176 JBPARENT $CHAR1.                                               
            @256 BO       $CHAR2.                                               
            @71  LFP      $CHAR1.                                               
            @485 CCYY_UPD $CHAR4.                                               
            @489 MM_UPD   $CHAR2.                                               
            @491 DD_UPD   $CHAR2.                                               
            ;                                                                   
                                                                                
       IF FT_TYPE="3 " AND BUYBACK="Y" THEN                                     
          FT_TYPE="3B";                                                         
                                                                                
       LENGTH PR_NT $3.;                                                        
       IF ZONE^=' ' THEN                                                        
          PR_NT=ZONE;                                                           
       ELSE                                                                     
          PR_NT=NT;                                                             
                                                                                
   PROC SORT;                                                                   
      BY NUM_REF;                                                               
                                                                                
   DATA BP13JOIN;                                                               
      MERGE BP13F830 (IN=A) BP13F500 (IN=B);                                    
      BY NUM_REF;                                                               
      IF A AND B;                                                               
                                                                                
   PROC SORT; BY BLOCK ST_NME UNIT;                                             
                                                                                
   DATA _NULLS_;                                                                
   SET BP13JOIN END=EOF;                                                        
      BY BLOCK ST_NME UNIT;                                                     
                                                                                
      FILE BP13L691 HEADER=H LINESLEFT=LL NOTITLES;                             
      LENGTH STATCD $3.;                                                        
                                                                                
      IF FIRST.BLOCK | FIRST.ST_NME THEN                                        
         DO;                                                                    
            PUT _PAGE_;                                                         
            SNO=0;                                                              
         END;                                                                   
                                                                                
      IF FIRST.UNIT THEN                                                        
         DO;                                                                    
           CASE+1;                                                              
           SNO+1;                                                               
           PUT @1   SNO                                                         
               @8   NUM_REF  $CHAR8.                                            
               @22  HSE_NO   $CHAR11.                                           
               @35  ALL_SCH  $CHAR3.                                            
               @40  ELG_SCH  $CHAR3.                                            
               @52  FT_TYPE  $CHAR2.                                            
               @58  DD_UPD   $CHAR2.                                            
               @60  '/'                                                         
               @61  MM_UPD   $CHAR2.                                            
               @63  '/'                                                         
               @64  CCYY_UPD $CHAR4.                                            
               @72  CAT      $CHAR1.                                            
               @76  JB_CODE  $CHAR1.                                            
               @82  JB_REF   $CHAR6.                                            
               @95  LFP      $CHAR1.                                            
               @100 PR_NT    $CHAR3.                                            
               ;                                                                
           LINK READOCC;                                                        
                                                                                
          IF LAST.UNIT THEN                                                     
             PUT /;                                                             
         END;                                                                   
                                                                                
       IF EOF THEN                                                              
          DO;                                                                   
            PUT ////@10 'TOTAL NUMBER OF CASES TRANSFERRED    : ' CASE;         
            PUT //  @10 'TOTAL NUMBER OF OCCUPIER TRANSFERRED : ' OCCUP;        
          END;                                                                  
                                                                                
   RETURN;                                                                      
                                                                                
 READOCC:                                                                       
      LENGTH K825KEY $8.;                                                       
      K825KEY=NUM_REF;                                                          
                                                                                
      INFILE BP13K825 VSAM KEY=K825KEY SKIP GENKEY FEEDBACK=SASRC;              
      INPUT @;                                                                  
                                                                                
      IF SASRC=4 | SASRC=16 THEN                                                
         DO;                                                                    
            _ERROR_=0;                                                          
            IF SASRC=4 THEN STOP;                                               
            ELSE                                                                
               DO;                                                              
                  SASRC=0;                                                      
                  FILE LOG;                                                     
                  PUT 'NO RECORD FND IN K825 : ' K825KEY;                       
                  RETURN;                                                       
               END;                                                             
         END;                                                                   
                                                                                
      INPUT @1   NUM_REF  $CHAR8.                                               
            @9   OCCP_IC  $CHAR9.                                               
            @28  OCCPNME  $CHAR66.                                              
            @94  OCCPETH  $CHAR1.                                               
            @95  OCCPSEX  $CHAR1.                                               
            @96  OCCPMS   $CHAR1.                                               
            @105 OCCPCTZ  $CHAR2.                                               
            @107 OCCPRELN $CHAR2.                                               
            ;                                                                   
                                                                                
       DO WHILE (NUM_REF EQ K825KEY);                                           
          INPUT @;                                                              
          IF SASRC=4 | SASRC=16 THEN                                            
             DO;                                                                
                _ERROR_=0;                                                      
                 SASRC=0;                                                       
             END;                                                               
                                                                                
          IF OCCP_IC = LSE_IC1 THEN                                             
             STATCD = "HA1";                                                    
          ELSE                                                                  
             IF OCCP_IC = LSE_IC2 THEN                                          
                STATCD = "HA2";                                                 
             ELSE                                                               
                IF OCCP_IC = LSE_IC3 THEN                                       
                   STATCD = "HA3";                                              
                ELSE                                                            
                   IF OCCP_IC = LSE_IC4 THEN                                    
                      STATCD = "HA4";                                           
                   ELSE                                                         
                      STATCD = "O";                                             
                                                                                
          OCCUP+1;                                                              
                                                                                
          PUT @8   OCCP_IC  $CHAR9.                                             
              @18  STATCD                                                       
              @22  OCCPNME  $CHAR66.                                            
              @93  OCCPRELN $MAFRLN.                                            
              @106 OCCPETH  $ETH.                                               
              @111 OCCPCTZ  $CITZEN.                                            
              @115 OCCPMS   $MAR.                                               
          ;                                                                     
                                                                                
      INPUT @1   NUM_REF  $CHAR8.                                               
            @9   OCCP_IC  $CHAR9.                                               
            @28  OCCPNME  $CHAR66.                                              
            @94  OCCPETH  $CHAR1.                                               
            @95  OCCPSEX  $CHAR1.                                               
            @96  OCCPMS   $CHAR1.                                               
            @105 OCCPCTZ  $CHAR2.                                               
            @107 OCCPRELN $CHAR2.                                               
            ;                                                                   
                                                                                
       END;                                                                     
                                                                                
      RETURN;                                                                   
                                                                                
 H:                                                                             
   LINE_CNT=0;                                                                  
   PAGE+1;                                                                      
   CUR_DATE=TODAY();                                                            
                                                                                
   PUT @1 'BP13L691      HDB3'                                                  
       @40 '          SYSTEM OF COMMITMENT'                                     
       @104 'DATE : ' CUR_DATE DDMMYY10.                                        
       @123 'PAGE : ' PAGE;                                                     
   PUT @40 '     REPORT OF REGISTERED HOUSEHOLD'                                
       @123 'USER : ERU';                                                       
   PUT / @1  'ADDRESS : '                                                       
         @10  'BLK ' @14 BLOCK $CHAR5. @20 ST_NME //;                           
   PUT @1  'S/NO'                                                               
       @8  'REF-NO '                                                            
       @22 'UNIT '                                                              
       @34 'ALLOC'                                                              
       @40 'ELIG'                                                               
       @50  'APPT'                                                              
       @57  'DATE-UPDATED'                                                      
       @71  'CAT'                                                               
       @75  'JB-CDE'                                                            
       @82  'JB-REF-NO '                                                        
       @94  'LFP'                                                               
       @100 'NT/ZN';                                                            
                                                                                
   PUT @8  'UIN-NO   STAT'                                                      
       @22 'LESSEES/OCCUPIERS NAME'                                             
       @51  'FT'                                                                
       @93  'RELN-SHIP'                                                         
       @105 'ETH  CITZ MS '                                                     
       ;                                                                        
                                                                                
   PUT /;                                                                       
                                                                                
   RETURN;                                                                      
                                                                                
 OPTIONS PAGESIZE=60 LINESIZE=132 NODATE MISSING='0';                           
 PROC PRINTTO PRINT=P13L691A;                                                   
 PROC TABULATE DATA=BP13JOIN FORMAT=12. MISSING;                                
                                                                                
 CLASS  PR_NT FT_TYPE JB_CODE LFP;                                              
                                                                                
 KEYLABEL   ALL =  "TOTAL"                                                      
            N   =  " " ;                                                        
                                                                                
 LABEL  PR_NT="NT/ZONE"                                                         
        FT_TYPE="FLAT TYPE";                                                    
                                                                                
 TABLE  (PR_NT*(FT_TYPE ALL)),(JB_CODE*LFP) ALL                                 
 /RTS=30.;                                                                      
                                                                                
 TITLE1 "P13L691A      HDB3                                     SOC  SYSTEM     
                                                      ";                        
 TITLE2 "                            SUMMARY REPORTS OF NT/ZONE AND FLAT TYPE   
                                       USER : SOU      ";                       
