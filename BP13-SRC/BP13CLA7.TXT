       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CLA7.                                                 
       AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  07/05/2015.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : PROGRAM TO FILTER OUT SERS CASES                 *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F300  - BP13.F300.SUCCESS             *        
      *                  2.  BP13K800  - BP13.K800.APPLN               *        
      *                  3.  BP13K410  - BP13.K410.ACCUMSO             *        
      *                  3.  BM06K110  - BM06.K110.FLATINV             *        
      *                                                                *        
      *   OUTPUT      :  1.  OUTF300A - FILTERED CASES TO BE USED AS   *        
      *                                  INPUT OF PGM BP13CL07         *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   --------  ----  -------- ------------------------------------*        
      *   BP135710  RJB1  07/05/15 NEW PROGRAM                         *        
      *   BP135710  RJB1  10/06/15 INCLUDE CHECKING OF LOAN TAG AND KEY*        
      *                            AVAILABLE DATE                      *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F300  ASSIGN        TO BP13F300.                          
           SELECT OUTF300A  ASSIGN        TO OUTF300A.                          
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K800-NUM-REGN                   
                            FILE STATUS      IS WS-K800-STATUS.                 
                                                                                
           SELECT BP13K410  ASSIGN        TO BP13K410                           
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K410-KEY-FLD                    
                            FILE STATUS      IS WS-K410-STATUS.                 
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K110-KEY-FLD                    
                            FILE STATUS      IS WS-K110-STATUS.                 
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F300                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13F300.                                                           
                                                                                
       FD  BP13K410                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K410.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  OUTF300A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       01  OUTF300A-REC      PIC X(2000).                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-COUNT.                                                            
           05  WS-F300-INPUT                PIC 9(08)  VALUE 0.                 
           05  WS-F300-OUT                  PIC 9(08)  VALUE 0.                 
           05  WS-F300-WRITE                PIC 9(08)  VALUE 0.                 
                                                                                
       01  WS-SWITCH.                                                           
           05  WS-SKIP                      PIC X(01)  VALUE 'N'.               
                                                                                
       01  WS-F300-EOF                      PIC X(01)  VALUE SPACES.            
       01  WS-FILE-STATUS.                                                      
           05 WS-K800-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K410-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K110-STATUS              PIC 99    VALUE ZEROS.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE                                            
              THRU 1000-EXIT.                                                   
                                                                                
           PERFORM 2000-READ-INPUT                                              
              THRU 2000-EXIT.                                                   
                                                                                
           PERFORM 3000-PROCESS-DATA                                            
              THRU 3000-EXIT UNTIL WS-F300-EOF = 'Y'.                           
                                                                                
           PERFORM 9000-CLOSE-ROUTINE                                           
              THRU 9000-EXIT.                                                   
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13F300                                                 
                       BP13K800                                                 
                       BP13K410                                                 
                       BM06K110                                                 
                OUTPUT OUTF300A.                                                
                                                                                
           INITIALIZE  OUTF300A-REC BP13F300-REC WS-COUNT.                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K410 ERROR ' WS-K410-STATUS                  
              MOVE     WS-K410-STATUS      TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BP13F300                                                       
                 AT END MOVE 'Y'  TO WS-F300-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-F300-EOF NOT = 'Y'                                            
               ADD 1 TO WS-F300-INPUT                                           
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           MOVE SPACES  TO OUTF300A-REC.                                        
           INITIALIZE      OUTF300A-REC.                                        
                                                                                
           MOVE 'N'     TO WS-SKIP.                                             
                                                                                
           MOVE     F300-CUR-REG        TO   K410-NUM-REGN                      
                                                                                
           PERFORM 3500-READ-BP13K410                                           
              THRU 3500-EXIT.                                                   
                                                                                
           IF WS-SKIP = 'N'                                                     
              PERFORM 4000-READ-BP13K800                                        
                 THRU 4000-EXIT                                                 
              PERFORM 4500-READ-BM06K110                                        
                 THRU 4500-EXIT                                                 
              IF WS-SKIP = 'N'                                                  
                 WRITE OUTF300A-REC  FROM BP13F300-REC                          
                 ADD 1 TO WS-F300-WRITE                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-INPUT                                              
              THRU 2000-EXIT.                                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3500-READ-BP13K410.                                                      
      *================================================================*        
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
              WHEN 00                                                           
                   DISPLAY 'RECORD FOUND IN BP13K410 : ' K410-NUM-REGN          
                   MOVE 'Y' TO WS-SKIP                                          
                   ADD 1    TO WS-F300-OUT                                      
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K410      '                       
                   DISPLAY 'KEY     : ' K410-NUM-REGN                           
                   DISPLAY 'STATUS  : ' WS-K410-STATUS                          
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4000-READ-BP13K800.                                                      
      *================================================================*        
                                                                                
           MOVE SPACES       TO BP13K800-MASTER.                                
           MOVE F300-CUR-REG TO K800-NUM-REGN.                                  
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00 OR 02                                         
              IF K800-NUM-PORT-ELIG-TAG = 'Y' OR                                
                 K800-NUM-ALLO-CAT = 'SER'                                      
                 DISPLAY K800-NUM-REGN ': K800-NUM-PORT-ELIG-TAG = '            
                         K800-NUM-PORT-ELIG-TAG                                 
                 DISPLAY K800-NUM-REGN ': K800-NUM-ALLO-CAT = '                 
                         K800-NUM-ALLO-CAT                                      
                 MOVE 'Y' TO WS-SKIP                                            
                 ADD 1    TO WS-F300-OUT                                        
              END-IF                                                            
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 DISPLAY 'BP13K800 - REC NOT FOUND - ' K800-NUM-REGN            
              ELSE                                                              
                 DISPLAY 'BP13K800 - READ ERROR (' WS-K800-STATUS ')'           
                 MOVE WS-K800-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4500-READ-BM06K110.                                                      
      *================================================================*        
                                                                                
           MOVE K800-NUM-SCH-ACC       TO K110-KEY-FLD.                         
                                                                                
           READ BM06K110.                                                       
                                                                                
           IF WS-K110-STATUS = 00 OR 02                                         
              IF (K110-DTE-KEY-AVAIL NOT = SPACES AND LOW-VALUES AND            
                 ZEROS) AND (K800-NUM-LOAN-TAG = 'B')                           
                 DISPLAY 'K110-DTE-KEY-AVAIL = ' K110-DTE-KEY-AVAIL             
                 DISPLAY 'K800-NUM-LOAN-TAG  = ' K800-NUM-LOAN-TAG              
                 MOVE 'Y' TO WS-SKIP                                            
                 ADD 1    TO WS-F300-OUT                                        
              END-IF                                                            
           ELSE                                                                 
              IF WS-K110-STATUS = 23                                            
                 DISPLAY 'BM06K110 - REC NOT FOUND - ' K110-KEY-FLD             
              ELSE                                                              
                 DISPLAY 'BM06K110 - READ ERROR (' WS-K110-STATUS ')'           
                 MOVE WS-K110-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           DISPLAY 'TOTAL INPUT RECORDS        : ' WS-F300-INPUT                
           DISPLAY 'TOTAL RECORD WRITTEN       : ' WS-F300-WRITE.               
           DISPLAY 'TOTAL RECORD SKIPPED       : ' WS-F300-OUT.                 
           DISPLAY '        '                                                   
                                                                                
           CLOSE BP13F300                                                       
                 BP13K800                                                       
                 BP13K410                                                       
                 BM06K110                                                       
                 OUTF300A.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K410 ERROR ' WS-K410-STATUS                  
              MOVE     WS-K410-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
