       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF76.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO                                     
      *DATE-WRITTEN.  04/12/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE  : EMAIL LIST OF CASES WITH HLE DEFER TAG OR     *            
      *              date                                          *            
      *                                                            *            
      * INPUT      : BP13F800                                      *            
      * OUTPUT     : BP13MAIL - OUTPUT FILE FOR SENDING EMAIL      *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTIONS                       *            
      * -------- -------- ----- ------------                       *            
      * BP134449 20121204 ZDD1  NEW PROGRAM                        *            
      * BP135537 20150303 RJB1  CHANGE THE EMAIL RECIPIENT FROM    *            
      *                         NICKNAME INTO COMPLETE NAME        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS BP13K022-STATUS.                   
                                                                                
           SELECT BP13MAIL  ASSIGN TO BP13MAIL.                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD  BP13MAIL                                                             
           RECORD CONTAINS 80  CHARACTERS                                       
           RECORDING  MODE  IS F.                                               
       01  MAIL-PRTREC                       PIC X(80).                         
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS  100 CHARACTERS.                                    
       COPY BP13K022.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-F800-EOF                    PIC X     VALUE 'N'.                   
       01 WS-BYPASS                      PIC 9(6)  VALUE ZEROS.                 
       01 WS-F800-READ                   PIC 9(6)  VALUE ZEROS.                 
       01 WS-SNO                         PIC 9(4)  VALUE ZEROS.                 
       01 WS-WRT-EMAIL                   PIC 9(4)  VALUE ZEROS.                 
       01 WS-K022-FND                    PIC 9(4)  VALUE ZEROS.                 
       01 WS-K022-NFND                   PIC 9(4)  VALUE ZEROS.                 
       01 BP13K022-STATUS                PIC X(2)  VALUE SPACES.                
                                                                                
       01 WS-DATE                        PIC X(8)  VALUE SPACES.                
                                                                                
       01  WS-6WK-VARIABLES.                                                    
           05  WS-ACCEPT-DTE              PIC 9(8) VALUE ZEROES.                
           05  WS-6WK-DTE                 PIC 9(8) VALUE ZEROES.                
                                                                                
       01  WS-NUM-FIELD.                                                00840001
           05  WS-REM                   PIC 9(3).                       00850001
           05  WS-YEAR                  PIC 9(4).                       00860001
                                                                        00880001
       01  WS-CURR-DATE                 PIC 9(08)  VALUE  ZEROES.       00840001
      *    05  WS-CURRENT-DATE.                                                 
      *        10  WS-CURR-CC           PIC 99     VALUE 00.                    
      *        10  WS-CURR-DATE.                                                
      *            15  WS-CURR-YY       PIC 99     VALUE 00.                    
      *            15  WS-CURR-MM       PIC 99     VALUE 00.                    
      *            15  WS-CURR-DD       PIC 99     VALUE 00.                    
                                                                                
       01  WS-DATE-1.                                                           
           05  WS-CURRENT-DATE.                                                 
                10  WS-DATE-CYM.                                                
                    15  WS-DATE-CCYY      PIC X(4).                             
                    15  WS-DATE-MM        PIC X(2).                             
                10  WS-DATE-DD            PIC X(2).                             
           05  WS-CURR-TIME               PIC X(9).                             
           05  WS-TIME REDEFINES WS-CURR-TIME.                                  
                10  WS-HH                 PIC 9(2).                             
                10  WS-MM                 PIC 9(2).                             
                10  WS-SS                 PIC 9(2).                             
                10  WS-MS                 PIC 9(2).                             
                10  WS-GM                 PIC 9(1).                             
           05  FILLER                     PIC X(4).                             
                                                                                
       01  WS-TMP-DATE                  PIC 9(07)  VALUE ZEROES.                
       01  WS-TEMP-DATE                 PIC X(8).                               
       01  WS-TEMP-DATE-R REDEFINES WS-TEMP-DATE.                               
           05 WS-CCYY                   PIC 9(4).                               
           05 WS-MM                     PIC 9(2).                               
           05 WS-DD                     PIC 9(2).                               
                                                                                
       01  DATE-RECORD.                                                 00890001
           05  DATE-REC                   PIC 9(8).                     00900001
           05  DATE-REC1 REDEFINES DATE-REC.                            00910001
               10 CAL-DATE.                                             00920001
                   15 CAL-YEAR            PIC 9(4).                     00930001
                       88 VALID-YEAR          VALUE 1900 THRU 2100.     00940001
                   15 CAL-MONTH           PIC 9(2).                     00950001
                       88 31-DAY-MONTH        VALUE 01, 03, 05, 07, 08, 00960001
                                                        10, 12.         00970001
                       88 30-DAY-MONTH        VALUE 04, 06, 09, 11.     00980001
                       88 28-29-DAY-MONTH     VALUE 02.                 00990001
                       88 VALID-MONTH         VALUE 01 THRU 12.         01000001
                   15 CAL-DAY             PIC 9(2).                     01010001
                       88 VALID-DAY           VALUE 01 THRU 31.         01020001
                                                                        01030001
                                                                                
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-TO         PIC X(20)  VALUE SPACES.                         
           05  FILLER          PIC X(54)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2C.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-CCMAILID   PIC X(25)  VALUE                                 
               'YEO_SIEW_GUAT@HDB.GOV.SG>'.                                     
           05  FILLER          PIC X(46)  VALUE SPACES.                         
                                                                                
                                                                                
      *  for testing purpose -------                                            
      *01  MAIL-HDR1.                                                           
      *    05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB3'.                 
      *    05  FILLER          PIC X(68)  VALUE SPACES.                         
      *                                                                         
      *01  MAIL-HDR2.                                                           
      *    05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
      *    05  MAIL-SENDID     PIC X(07)  VALUE 'CSXXXXX'.                      
      *    05  FILLER          PIC X(09)  VALUE '@SGPHDB3>'.                    
      *    05  FILLER          PIC X(53)  VALUE SPACES.                         
      *                                                                         
      *01  MAIL-DTL2B.                                                          
      *    05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
      *    05  FILLER          PIC X(17)  VALUE                                 
      *        'lkc13@hdb.gov.sg>'.                                             
      *    05  FILLER          PIC X(54)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6.                                                           
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAIL-TO1        PIC X(20)  VALUE SPACES.                         
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6C.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  MAIL-CCMAILID1  PIC X(25)  VALUE                                 
               'YEO_SIEW_GUAT@HDB.GOV.SG>'.                                     
           05  FILLER          PIC X(51)  VALUE SPACES.                         
                                                                                
       01  MAIL-SUBJ.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  MAIL-SUBJECT    PIC X(09)  VALUE                                 
               'REGN NO. '.                                                     
           05  MAIL-REGNO      PIC X(08)  VALUE SPACES.                         
           05  MAIL-SUBJECT1   PIC X(15)  VALUE                                 
               ', HLE DEF DATE '.                                               
           05  MAIL-HLE-DTE    PIC X(10)  VALUE SPACES.                         
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DETAIL1           PIC X(80)  VALUE SPACES.                      
       01  MAIL-DETAIL2           PIC X(80)  VALUE SPACES.                      
       01  MAIL-DETAIL3           PIC X(80)  VALUE SPACES.                      
                                                                                
       01  MAIL-PERIOD            PIC X(80)  VALUE '.'.                         
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE     THRU 1000-EXIT.                       
            PERFORM 2100-CHECK-DATE       THRU 2100-EXIT.                       
            PERFORM 2000-READ-BP13F800    THRU 2000-EXIT.                       
            PERFORM 3000-PROCESS-DETAIL   THRU 3000-EXIT                        
                    UNTIL WS-F800-EOF = 'Y'.                                    
            PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN  INPUT  BP13F800                                               
                         BP13K022                                               
                  OUTPUT BP13MAIL.                                              
                                                                                
           IF BP13K022-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K022 : ' BP13K022-STATUS             
              MOVE     BP13K022-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
            READ BP13F800                                                       
                 AT END MOVE 'Y' TO WS-F800-EOF                                 
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD 1   TO WS-F800-READ.                                            
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2100-CHECK-DATE.                                                         
      *-------------------------------------------------------------            
                                                                                
           INITIALIZE                    WS-6WK-VARIABLES.                      
                                                                                
           MOVE FUNCTION CURRENT-DATE TO    WS-DATE-1.                          
           MOVE WS-CURRENT-DATE          TO WS-ACCEPT-DTE.                      
                                                                                
              COMPUTE WS-6WK-DTE =                                              
                      FUNCTION INTEGER-OF-DATE (WS-ACCEPT-DTE)                  
                    + 42.                                                       
                                                                                
              COMPUTE WS-6WK-DTE =                                              
                      FUNCTION DATE-OF-INTEGER (WS-6WK-DTE).                    
                                                                                
              MOVE    WS-6WK-DTE         TO DATE-REC.                           
                                                                                
      *                                                                         
      *    COMPUTE WS-TMP-DATE =                                                
      *      FUNCTION INTEGER-OF-DATE(WS-CURRENT-DATE)                          
      *    COMPUTE WS-TMP-DATE = WS-TMP-DATE + 42                               
      *    COMPUTE WS-CURRENT-DATE =                                            
      *      FUNCTION DATE-OF-INTEGER(WS-TMP-DATE)                              
                                                                                
      *    MOVE WS-CURRENT-DATE       TO DATE-REC.                              
      *    MOVE ZEROS            TO WS-NUM-FIELD.                       02570036
      *    ADD  42               TO CAL-DAY.                            02610036
                                                                        02620001
      *    PERFORM 2200-PROCESS-DATE  THRU 2200-EXIT.                   02630001
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-PROCESS-DATE.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF 31-DAY-MONTH                                              02740001
               IF CAL-DAY > 31                                          02750002
               COMPUTE CAL-DAY    = CAL-DAY    - 31                     02760001
               COMPUTE CAL-MONTH  = CAL-MONTH  + 1                      02770002
               END-IF                                                   02771002
           END-IF.                                                      02772002
                                                                        02780001
           IF 30-DAY-MONTH                                              02790001
               IF CAL-DAY > 30                                          02800002
               COMPUTE CAL-DAY    = CAL-DAY    - 30                     02810001
               COMPUTE CAL-MONTH  = CAL-MONTH  + 1                      02820002
               END-IF                                                   02821002
           END-IF.                                                      02822002
                                                                        02830001
           IF 28-29-DAY-MONTH                                           02840001
               IF  CAL-DAY < 29                                         02850002
                   GO TO 2200-EXIT                                      02860001
               ELSE                                                     02870001
                   PERFORM 2300-FEBRUARY                                02880001
                      THRU 2300-EXIT                                    02890001
               END-IF                                                   02891002
           END-IF.                                                      02892002
                                                                        02910001
           IF CAL-MONTH  > 12                                           02920001
              MOVE 01 TO CAL-MONTH                                      02930001
              ADD 01 TO CAL-YEAR.                                       02940001
                                                                        02950001
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-FEBRUARY.                                                   03160001
      *-------------------------------------------------------------            
                                                                                
           DIVIDE 4 INTO CAL-YEAR GIVING WS-YEAR REMAINDER WS-REM.      03180001
           IF (WS-REM = ZERO) AND (CAL-DAY > 29)                        03190002
              COMPUTE CAL-DAY = CAL-DAY - 29                            03210001
              MOVE 03 TO CAL-MONTH                                      03220001
           ELSE                                                         03230001
           IF (WS-REM NOT = ZERO) AND (CAL-DAY > 28)                    03250002
              COMPUTE CAL-DAY = CAL-DAY - 28                            03270001
              MOVE 03 TO CAL-MONTH.                                     03280001
                                                                        03290001
       2300-EXIT.                                                       03300001
           EXIT.                                                        03310001
                                                                        03320001
      *-------------------------------------------------------------            
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
                                                                                
                                                                                
           DISPLAY DATE-REC ' / ' F800-DTE-HLE-DEFERMENT                        
           IF DATE-REC  =  F800-DTE-HLE-DEFERMENT                               
              CONTINUE                                                          
           ELSE                                                                 
              ADD 1 TO WS-BYPASS                                                
              PERFORM 2000-READ-BP13F800  THRU 2000-EXIT                        
              GO TO 3000-EXIT                                                   
           END-IF                                                               
                                                                                
           MOVE F800-NUM-REGN          TO MAIL-REGNO.                           
                                                                                
           STRING F800-DTE-HLE-DEFERMENT(7:2) '/'                               
                  F800-DTE-HLE-DEFERMENT(5:2) '/'                               
                  F800-DTE-HLE-DEFERMENT(1:4)                                   
                  DELIMITED BY SIZE INTO MAIL-HLE-DTE.                          
                                                                                
           MOVE SPACES    TO MAIL-TO   MAIL-TO1.                                
                                                                                
           PERFORM 4000-READ-BP13K022     THRU 4000-EXIT.                       
           STRING K022-NUM-OIC '@HDB.GOV.SG>' DELIMITED BY SPACE                
                  INTO MAIL-TO.                                                 
                                                                                
           STRING K022-NUM-OIC '@HDB.GOV.SG>' DELIMITED BY SPACE                
                  INTO MAIL-TO1.                                                
                                                                                
                                                                                
           STRING 'HLE def date is ' MAIL-HLE-DTE ' for Regn No. '              
                  F800-NUM-REGN ', for your checking and trigger'               
                  DELIMITED BY SIZE INTO MAIL-DETAIL1.                          
                                                                                
           STRING 'new HLE application if necessary. Please delete '            
                  'HLE def date if no longer '                                  
                  DELIMITED BY SIZE INTO MAIL-DETAIL2.                          
                                                                                
           STRING 'applicable. '                                                
                  DELIMITED BY SIZE INTO MAIL-DETAIL3.                          
                                                                                
           PERFORM 5000-OUTPUT-EMAIL-HDR  THRU 5000-EXIT.                       
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           ADD   1 TO WS-WRT-EMAIL.                                             
                                                                                
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES           TO  BP13K022-REC.                              
           MOVE F800-NUM-REGN    TO  K022-NUM-REGN.                             
                                                                                
           READ BP13K022  KEY IS K022-NUM-REGN.                                 
                                                                                
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
                 ADD 1 TO WS-K022-FND                                           
             WHEN 23                                                            
                 ADD 1 TO WS-K022-NFND                                          
             WHEN OTHER                                                         
              DISPLAY 'ERROR OPENING - BP13K022 : ' BP13K022-STATUS             
              MOVE     BP13K022-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-OUTPUT-EMAIL-HDR.                                                   
      *-------------------------------------------------------------            
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL2C.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL3.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL4.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL6.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL6C.                                   
           WRITE MAIL-PRTREC FROM MAIL-SUBJ.                                    
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-DETAIL1.                                 
           WRITE MAIL-PRTREC FROM MAIL-DETAIL2.                                 
           WRITE MAIL-PRTREC FROM MAIL-DETAIL3.                                 
           WRITE MAIL-PRTREC FROM MAIL-PERIOD.                                  
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
            DISPLAY ' '.                                                        
            DISPLAY '- BP13CF76 CONTROL LIST --'                                
            DISPLAY '---------------------------------------------'.            
            DISPLAY ' 1) TOT BP13F800 READ     : ' WS-F800-READ.                
            DISPLAY ' 2) TOT RECORD WRITTEN    : ' WS-WRT-EMAIL.                
            DISPLAY ' 2) TOT RECORD BYPASS     : ' WS-BYPASS.                   
            DISPLAY ' '.                                                        
                                                                                
            CLOSE  BP13F800                                                     
                   BP13K022                                                     
                   BP13MAIL.                                                    
                                                                                
           IF BP13K022-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K022 : ' BP13K022-STATUS             
              MOVE     BP13K022-STATUS      TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
