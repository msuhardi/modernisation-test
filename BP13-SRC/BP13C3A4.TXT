       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C3A4.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  15/09/2015.                                               
      * ========================================================== *            
      * OBJECTIVE    :  To generate exception cases from CPF back- *            
      *                 up file                                    *            
      * ========================================================== *            
      * NOTES        :  This program only runs on a THURSDAY. If   *            
      *                 ran on a different day, program exits      *            
      *                 immediately.                               *            
      * ========================================================== *            
      * INPUT FILES  :  1.) BP13F454                               *            
      *                 2.) BP13K680                               *            
      *                                                            *            
      * MAIL FILE    :  1.) MAILC3A4                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP135959 KSJ3 15/09/2015 NEW PROGRAM                       *            
      * BP136460 KSJ3 02/03/2017 EXPAND BP13K680 (1500-1600)       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F454  ASSIGN       TO BP13F454.                           
                                                                                
           SELECT BP13K680  ASSIGN       TO BP13K680                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K680-NUM-REGN                       
                            FILE STATUS  IS WS-K680-STATUS.                     
                                                                                
           SELECT SY02F001  ASSIGN       TO SY02F001.                           
                                                                                
           SELECT MAILC3A4  ASSIGN       TO MAILC3A4.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F454                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13F454.                                                       
                                                                                
       FD  BP13K680                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
           COPY BP13K680.                                                       
                                                                                
           COPY SY02F001.                                                       
                                                                                
       FD  MAILC3A4                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 100 CHARACTERS.                                      
       01  MAILC3A4-REC                  PIC X(100).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K680-STATUS            PIC 9(2)   VALUE 00.                   
                                                                                
       01  WS-COUNT.                                                            
           05  WS-CNT-F454-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F454-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K680-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K680-NFND          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-MAIL-PRINT         PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F454-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K680-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-FMT8-CCYY.                                            
                   15  WS-DTE-FMT8-CC    PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT8-YY    PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-FMT10-DD       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-MM       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-CCYY.                                           
                   15  WS-DTE-FMT10-CC   PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT10-YY   PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-TMP                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-INT                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-WORK               PIC 9(8)   VALUE ZEROES.               
                                                                                
      *--- MAILC3A4 REPORT -----------------------------------------            
       01  MAIL-COMMANDS.                                                       
           05  WS-MAIL-HELO              PIC X(100) VALUE                       
               'HELO SGPHDB1'.                                                  
           05  WS-MAIL-MAIL-FROM         PIC X(100) VALUE                       
               'MAIL FROM:<OPCP@SGPHDB1>'.                                      
           05  WS-MAIL-RCPT-TO-1         PIC X(100) VALUE                       
               'RCPT TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                           
           05  WS-MAIL-RCPT-TO-2         PIC X(100) VALUE                       
               'RCPT TO:<MASLINDA_OSMAN@HDB.GOV.SG>'.                           
           05  WS-MAIL-RCPT-TO-3         PIC X(100) VALUE                       
               'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                            
           05  WS-MAIL-RCPT-TO-4         PIC X(100) VALUE                       
               'RCPT TO:<TENG_SIEW_HONG@HDB.GOV.SG>'.                           
           05  WS-MAIL-DATA              PIC X(100) VALUE 'DATA'.               
           05  WS-MAIL-FROM              PIC X(100) VALUE                       
               'FROM:Soc System - Email Alert'.                                 
           05  WS-MAIL-TO-1              PIC X(100) VALUE                       
               'TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                                
           05  WS-MAIL-TO-2              PIC X(100) VALUE                       
               'TO:<MASLINDA_OSMAN@HDB.GOV.SG>'.                                
           05  WS-MAIL-TO-3              PIC X(100) VALUE                       
               'TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                                 
           05  WS-MAIL-TO-4              PIC X(100) VALUE                       
               'TO:<TENG_SIEW_HONG@HDB.GOV.SG>'.                                
           05  WS-MAIL-SUBJECT.                                                 
               10  FILLER                PIC X(100) VALUE                       
                   'SUBJECT: Exception Report for CPF Back-up'.                 
           05  WS-MAIL-DATE.                                                    
               10  FILLER                PIC X(6)   VALUE                       
                   'DATE: '.                                                    
               10  WS-MAIL-DTE-CURRENT   PIC X(10)  VALUE SPACES.               
                                                                                
       01  MAIL-RPT-HDR-1.                                                      
           05  FILLER                    PIC X(43) VALUE                        
               '     REGN         APPT DATE      APPT TIME '.                   
                                                                                
       01  MAIL-RPT-HDR-2.                                                      
           05  FILLER                    PIC X(43) VALUE                        
               '     --------     ----------     ----------'.                   
                                                                                
       01  MAIL-RPT-DETAILS.                                                    
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  MAIL-RPT-REGN             PIC X(08).                             
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  MAIL-RPT-APPT-DATE.                                              
               10  MAIL-RPT-APPT-DATE-DD PIC X(02).                             
               10  FILLER                PIC X(01) VALUE '/'.                   
               10  MAIL-RPT-APPT-DATE-MM PIC X(02).                             
               10  FILLER                PIC X(01) VALUE '/'.                   
               10  MAIL-RPT-APPT-DATE-CC PIC X(02).                             
               10  MAIL-RPT-APPT-DATE-YY PIC X(02).                             
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  MAIL-RPT-APPT-TIME.                                              
               10  MAIL-RPT-APPT-TIME-HH PIC X(02).                             
               10  FILLER                PIC X(01) VALUE ':'.                   
               10  MAIL-RPT-APPT-TIME-MM PIC X(02).                             
                                                                                
       01  MAIL-RPT-TOTALS.                                                     
           05  FILLER                    PIC X(26) VALUE                        
               '     TOTAL REGN PRINTED: '.                                     
           05  MAIL-RPT-TOTAL-REGN       PIC ZZZ9.                              
                                                                                
       01  MAIL-EMPTY-MSG.                                                      
           05  FILLER                    PIC X(12) VALUE SPACES.                
           05  FILLER                    PIC X(26) VALUE                        
               '*** NO RECORDS PRINTED ***'.                                    
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATES          THRU 1100-EXIT.                      
           PERFORM 2000-BP13F454-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
             UNTIL WS-F454-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F454                                                 
                       BP13K680                                                 
                       SY02F001                                                 
                OUTPUT MAILC3A4.                                                
                                                                                
           IF WS-K680-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K680 ERROR ' WS-K680-STATUS                  
              MOVE     WS-K680-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-GET-DATES.                                                          
      *-------------------------------------------------------------            
                                                                                
           READ SY02F001.                                                       
                                                                                
           IF F001-NUM-DAY-OF-WEEK NOT = 4                                      
              DISPLAY 'PROGRAM NOT RUN ON A THURSDAY'                           
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
      *--- CALCULATE FOR THIS SUNDAY'S DATE (WS-DTE-WORK)                       
           MOVE F001-DTE-CURRENT        TO   WS-DTE-TMP.                        
           COMPUTE WS-DTE-INT = FUNCTION INTEGER-OF-DATE                        
                                (WS-DTE-TMP).                                   
           COMPUTE WS-DTE-INT = WS-DTE-INT + 3.                                 
           COMPUTE WS-DTE-TMP = FUNCTION DATE-OF-INTEGER                        
                                (WS-DTE-INT).                                   
           MOVE WS-DTE-TMP                 TO   WS-DTE-WORK.                    
                                                                                
           DISPLAY 'CURRENT DATE = ' F001-DTE-CURRENT.                          
           DISPLAY 'SUNDAY DATE  = ' WS-DTE-WORK.                               
           DISPLAY '-----------------------'.                                   
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-BP13F454-READ.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F454                                                        
                AT END MOVE 'Y'            TO   WS-F454-EOF.                    
                                                                                
           IF WS-F454-EOF NOT = 'Y'                                             
              ADD   1                      TO   WS-CNT-F454-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
      * (1) MATCH RECS WITH APPT DATE AS SUNDAY TO TO BP13K680                  
      * (2) IF MATCHED, SKIP                                                    
      * (3) IF UNMATCHED, PRINT TO EMAIL REPORT                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                       TO   WS-K680-FND.                     
           MOVE F454-NUM-REGN             TO   K680-NUM-REGN.                   
                                                                                
           IF F454-DTE-APPT = WS-DTE-WORK                                       
              PERFORM 3100-READ-BP13K680  THRU 3100-EXIT                        
                                                                                
              IF WS-K680-FND = 'Y'                                              
                 CONTINUE                                                       
              ELSE                                                              
                 PERFORM 3200-PRINT-MAIL  THRU 3200-EXIT                        
              END-IF                                                            
           ELSE                                                                 
              ADD 1                       TO   WS-CNT-F454-SKIP                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F454-READ     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BP13K680.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K680.                                                       
                                                                                
           EVALUATE WS-K680-STATUS                                              
           WHEN 00                                                              
              ADD   1                     TO   WS-CNT-K680-READ                 
              MOVE 'Y'                    TO   WS-K680-FND                      
           WHEN 23                                                              
              ADD   1                     TO   WS-CNT-K680-NFND                 
              MOVE 'N'                    TO   WS-K680-FND                      
           WHEN OTHER                                                           
              DISPLAY 'OPENING BP13K680 ERROR ' WS-K680-STATUS                  
              MOVE     WS-K680-STATUS     TO   RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-PRINT-MAIL.                                                         
      *-------------------------------------------------------------            
                                                                                
           IF WS-CNT-MAIL-PRINT = 0                                             
              PERFORM 3300-PRINT-MAIL-HDR THRU 3300-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE F454-NUM-REGN             TO   MAIL-RPT-REGN.                   
                                                                                
           MOVE F454-DTE-APPT             TO   WS-DTE-FMT8.                     
           PERFORM 8100-CONV-DATE-TO-10   THRU 8100-EXIT.                       
           MOVE WS-DTE-FMT10              TO   MAIL-RPT-APPT-DATE.              
                                                                                
           MOVE F454-TME-SLOT-HH          TO   MAIL-RPT-APPT-TIME-HH.           
           MOVE F454-TME-SLOT-MM          TO   MAIL-RPT-APPT-TIME-MM.           
                                                                                
           WRITE MAILC3A4-REC             FROM MAIL-RPT-DETAILS.                
           ADD   1                        TO   WS-CNT-MAIL-PRINT.               
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-PRINT-MAIL-HDR.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE F001-DTE-CURRENT          TO   WS-DTE-FMT8.                     
           PERFORM 8100-CONV-DATE-TO-10   THRU 8100-EXIT.                       
           MOVE WS-DTE-FMT10              TO   WS-MAIL-DTE-CURRENT.             
                                                                                
           WRITE MAILC3A4-REC             FROM WS-MAIL-HELO.                    
           WRITE MAILC3A4-REC             FROM WS-MAIL-MAIL-FROM.               
           WRITE MAILC3A4-REC             FROM WS-MAIL-RCPT-TO-1.               
           WRITE MAILC3A4-REC             FROM WS-MAIL-RCPT-TO-2.               
           WRITE MAILC3A4-REC             FROM WS-MAIL-RCPT-TO-3.               
           WRITE MAILC3A4-REC             FROM WS-MAIL-RCPT-TO-4.               
           WRITE MAILC3A4-REC             FROM WS-MAIL-DATA.                    
           WRITE MAILC3A4-REC             FROM WS-MAIL-FROM.                    
           WRITE MAILC3A4-REC             FROM WS-MAIL-TO-1.                    
           WRITE MAILC3A4-REC             FROM WS-MAIL-TO-2.                    
           WRITE MAILC3A4-REC             FROM WS-MAIL-TO-3.                    
           WRITE MAILC3A4-REC             FROM WS-MAIL-TO-4.                    
           WRITE MAILC3A4-REC             FROM WS-MAIL-SUBJECT.                 
           WRITE MAILC3A4-REC             FROM WS-MAIL-DATE.                    
                                                                                
           MOVE SPACES                    TO   MAILC3A4-REC.                    
           WRITE MAILC3A4-REC.                                                  
                                                                                
           WRITE MAILC3A4-REC             FROM MAIL-RPT-HDR-1.                  
           WRITE MAILC3A4-REC             FROM MAIL-RPT-HDR-2.                  
                                                                                
           MOVE SPACES                    TO   MAILC3A4-REC.                    
           WRITE MAILC3A4-REC.                                                  
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8000-CONV-DATE-TO-8.                                                     
      *-------------------------------------------------------------            
      *    INPUT  - WS-DTE-FMT10                                                
      *    OUTPUT - WS-DTE-FMT8                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT10-CCYY  TO  WS-DTE-FMT8-CCYY.                        
           MOVE WS-DTE-FMT10-MM    TO  WS-DTE-FMT8-MM.                          
           MOVE WS-DTE-FMT10-DD    TO  WS-DTE-FMT8-DD.                          
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8100-CONV-DATE-TO-10.                                                    
      *-------------------------------------------------------------            
      *    INPUT  - WS-DTE-FMT8                                                 
      *    OUTPUT - WS-DTE-FMT10                                                
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT8-CCYY   TO  WS-DTE-FMT10-CCYY.                       
           MOVE WS-DTE-FMT8-MM     TO  WS-DTE-FMT10-MM.                         
           MOVE WS-DTE-FMT8-DD     TO  WS-DTE-FMT10-DD.                         
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF F001-NUM-DAY-OF-WEEK = 4                                          
              IF WS-CNT-MAIL-PRINT = 0                                          
                 PERFORM 3300-PRINT-MAIL-HDR  THRU 3300-EXIT                    
                                                                                
                 WRITE MAILC3A4-REC           FROM MAIL-EMPTY-MSG               
                 MOVE SPACES                  TO   MAILC3A4-REC                 
                 WRITE MAILC3A4-REC                                             
              ELSE                                                              
                 MOVE WS-CNT-MAIL-PRINT       TO   MAIL-RPT-TOTAL-REGN          
                                                                                
                 MOVE SPACES                  TO   MAILC3A4-REC                 
                 WRITE MAILC3A4-REC                                             
                 WRITE MAILC3A4-REC           FROM MAIL-RPT-TOTALS              
                 MOVE SPACES                  TO   MAILC3A4-REC                 
                 WRITE MAILC3A4-REC                                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           CLOSE BP13F454                                                       
                 BP13K680                                                       
                 SY02F001                                                       
                 MAILC3A4.                                                      
                                                                                
           IF WS-K680-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K680 ERROR ' WS-K680-STATUS                  
              MOVE     WS-K680-STATUS      TO   RETURN-CODE.                    
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C3A4 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F454 READ      : ' WS-CNT-F454-READ.          
           DISPLAY ' 2. NO OF BP13F454 SKIPPED   : ' WS-CNT-F454-SKIP.          
           DISPLAY ' 3. NO OF BP13K680 FOUND     : ' WS-CNT-K680-READ.          
           DISPLAY ' 4. NO OF BP13K680 NOT FOUND : ' WS-CNT-K680-NFND.          
           DISPLAY ' 5. NO OF RECS PRINTED       : ' WS-CNT-MAIL-PRINT.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
