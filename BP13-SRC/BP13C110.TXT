       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C110.                                                 
       AUTHOR.        LIEW YEE CHOONG                                           
      *DATE-WRITTEN.  15/05/89.                                                 
      *DATE-COMPILED.                                                           
      *REMARKS.                                                                 
      **************************************************************            
      *                                                            *            
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                      *            
      *                                                            *            
      *    SYSTEM ID   : BP13                                      *            
      *                                                            *            
      *    OBJECTIVE   :                                           *            
      *                  THIS PROGRAMME WILL COLLATE AND EDIT      *            
      *                  MONTHLY SOC TRANSACTION (BP13F136) FOR    *            
      *                  REPORT PRINTING PURPOSE(BP13F127).        *            
      *                                                            *            
      *     INPUT FILES:                                           *            
      *                  1. BP13F136 (MONTHLY SOC TRANS)           *            
      *                  2. BP13K800 (SOC APPLICATION MASTER)      *            
      *                  3. BP13K820 (SOC OCCUPIER FILE)           *            
      *                  4. BP13K893 (SOC APPLICATION HISTORY)     *            
      *                  5. BP13K895 (SOC OCCUPIER HISTORY FILE)   *            
      *                                                            *            
      *     OUTPUT FILES:                                          *            
      *                  1. BP13F127 (PRINTING FILE)               *            
      *                                                            *            
      *     LISTING:     NIL                                       *            
      *                                                            *            
      *                                                            *            
      *  CHG-NO   BY    NO           DESCRIPTION                   *            
      *  ------   ---   --------     ----------------------------  *            
      *  SOC-PH8  JEP   10/07/93     CHANGE OF FILE FROM           *            
      *                                     P10K020 TO   BP13K800  *            
      *                                     P10K040 TO   BP13K820  *            
      *                                                            *            
      *  BP130418 RHB   24/08/98     Y2K COMPLIANCE CHANGES        *            
      *  BP131039 SN    19/06/01     TO CHECK FOR SA FLAT          *            
      *  BP133566 ESA1  01/06/09     TO READ BP13K895 IF NOT FOUND *            
      *                              IN BP13K820 AND TO READ       *            
      *                              BP13K893 IF NOT FOUND IN      *            
      *                              BP13K800                      *            
      *  BP136098 KSJ3  02/03/16     TO CATER FOR 2 ROOM FLEXI     *            
      *  BP138565 KR13  25/01/21     TO CATER FOR FLAT-TYPE - '1F'.*            
      **************************************************************            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS K800-STATUS.                         
                                                                                
           SELECT BP13K893  ASSIGN       TO BP13K893                    00470000
                            ORGANIZATION IS INDEXED                     00490000
                            ACCESS MODE  IS DYNAMIC                     00480000
                            RECORD KEY   IS K893-KEY-FLD                00500000
                            FILE STATUS  IS K893-STATUS.                00510000
                                                                                
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS K820-STATUS.                         
                                                                                
           SELECT BP13K895 ASSIGN       TO BP13K895                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K895-KEY-FLD                         
                           FILE STATUS  IS K895-STATUS.                         
                                                                                
           SELECT BP13F136 ASSIGN       TO BP13F136.                            
                                                                                
           SELECT BP13F127 ASSIGN       TO BP13F127.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F136                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13F136.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                    00640000
            RECORD CONTAINS 2050 CHARACTERS.                            00660000
       COPY BP13K893.                                                   00680000
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K895                                                             
           RECORD CONTAINS 450 CHARACTERS.                                      
       COPY BP13K895.                                                           
                                                                                
       FD  BP13F127                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13F127.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  K800-STATUS                PIC 99         VALUE ZEROS.           
           05  K820-STATUS                PIC 99         VALUE ZEROS.           
           05  K893-STATUS                PIC 99         VALUE ZEROS.           
           05  K895-STATUS                PIC 99         VALUE ZEROS.           
                                                                                
       01  WS-SWITCHES.                                                         
           05  F136-EOF                   PIC X          VALUE 'N'.             
           05  K893-EOF                   PIC X          VALUE 'N'.             
           05  K895-EOF                   PIC X          VALUE 'N'.             
           05  WS-K895-FND-FLAG           PIC X          VALUE 'N'.             
           05  WS-K893-FND-FLAG           PIC X          VALUE 'N'.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-BP13F136-CASHTRAN       PIC 9(5)       VALUE ZEROS.           
           05  WS-BP13F136-READ           PIC 9(5)       VALUE ZEROS.           
           05  WS-BP13F127-TRANS          PIC 9(5)       VALUE ZEROS.           
           05  WS-BP13F127-WRITE          PIC 9(5)       VALUE ZEROS.           
           05  WS-K800-NT-FND             PIC 9(5)       VALUE ZEROS.           
           05  WS-K820-NT-FND             PIC 9(5)       VALUE ZEROS.           
           05  WS-K893-NT-FND             PIC 9(5)       VALUE ZEROS.           
           05  WS-K895-NT-FND             PIC 9(5)       VALUE ZEROS.           
           05  WS-K800-FND                PIC 9(5)       VALUE ZEROS.           
           05  WS-K820-FND                PIC 9(5)       VALUE ZEROS.           
           05  WS-K893-FND                PIC 9(5)       VALUE ZEROS.           
           05  WS-K895-FND                PIC 9(5)       VALUE ZEROS.           
                                                                                
       01  WS-SEQ-NO                      PIC X(3)       VALUE SPACES.          
       01  WS-F127-FT                     PIC X(2)       VALUE SPACES.          
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN PROCESS                                            *        
      ******************************************************************        
       MAIN.                                                                    
            PERFORM 100-OPEN-FILE           THRU 100-EXIT.                      
            PERFORM 200-READ-DATA-BP13F136  THRU 200-EXIT.                      
            PERFORM 300-PROCESS-UPDATE      THRU 300-EXIT                       
                        UNTIL F136-EOF = 'Y'.                                   
            PERFORM 800-CLOSE-ROUTINE.                                          
                                                                                
       MAIN-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
      *    OPENING FILES SECTION                                      *         
      *****************************************************************         
       100-OPEN-FILE.                                                           
           OPEN INPUT  BP13F136                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       BP13K820                                                 
                       BP13K895                                                 
                OUTPUT BP13F127.                                                
                                                                                
           IF K800-STATUS NOT = '00' AND '97'                                   
              DISPLAY ' OPENING ERROR, K800-STATUS '  K800-STATUS               
                                                                                
           IF K893-STATUS NOT = '00' AND '97'                                   
              DISPLAY ' OPENING ERROR, K893-STATUS '  K893-STATUS               
                                                                                
           IF K820-STATUS NOT = '00' AND '97'                                   
              DISPLAY ' OPENING ERROR, K820-STATUS '  K820-STATUS               
                                                                                
           IF K895-STATUS NOT = '00' AND '97'                                   
              DISPLAY ' OPENING ERROR, K895-STATUS '  K895-STATUS               
                                                                                
              PERFORM 800-CLOSE-ROUTINE.                                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    200-READ-DATA-BP13F136                                      *        
      ******************************************************************        
       200-READ-DATA-BP13F136.                                                  
           READ BP13F136 NEXT                                                   
                         AT END MOVE 'Y' TO F136-EOF                            
                         GO TO 200-EXIT.                                        
           ADD 1 TO WS-BP13F136-READ.                                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    300-PROCESS-UPDATE                                         *         
      *****************************************************************         
       300-PROCESS-UPDATE.                                                      
           PERFORM 400-ACCESS-BP13K820    THRU 400-EXIT.                        
           PERFORM 500-ACCESS-BP13K800    THRU 500-EXIT.                        
           PERFORM 700-WRITE-BP13F127     THRU 700-EXIT.                        
                                                                                
           PERFORM 200-READ-DATA-BP13F136 THRU 200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-ACCESS-BP13K820.                                                     
           MOVE F136-NUM-REGN  TO  K820-NUM-REGN.                               
           MOVE F136-NUM-NRIC  TO  K820-NUM-NRIC.                               
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF K820-STATUS = 00                                                  
              ADD 1              TO WS-K820-FND                                 
              MOVE K820-NME-OCCP TO F136-NME-APPL                               
           ELSE                                                                 
              IF K820-STATUS = 10 OR 23                                         
                 ADD 1           TO WS-K820-NT-FND                              
                 PERFORM 450-ACCESS-BP13K895     THRU 450-EXIT                  
                 IF WS-K895-FND-FLAG = 'Y'                                      
                    ADD 1          TO WS-K895-FND                               
                 ELSE                                                           
                    ADD 1          TO WS-K895-NT-FND                            
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY ' ERROR READING, BP13K820 = ' K820-STATUS              
                 MOVE K820-STATUS  TO RETURN-CODE                               
                 PERFORM 800-CLOSE-ROUTINE   THRU  800-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       450-ACCESS-BP13K895.                                                     
           MOVE  'N'             TO K895-EOF                                    
                                    WS-K895-FND-FLAG.                           
           INITIALIZE            BP13K895-REC.                                  
           MOVE SPACES           TO K895-KEY-FLD.                               
           MOVE F136-NUM-REGN    TO K895-NUM-REGN-HIST.                         
           MOVE F136-NUM-NRIC    TO K895-NUM-NRIC-HIST.                         
                                                                                
           START BP13K895 KEY >= K895-KEY-FLD.                                  
                                                                                
           IF K895-STATUS = 00 OR 02                                            
              PERFORM 460-READNEXT-BP13K895   THRU 460-EXIT                     
                        UNTIL K895-EOF = 'Y'                                    
           ELSE                                                                 
           IF K895-STATUS = 10 OR 23                                            
              MOVE SPACES       TO    F136-NME-APPL                             
           ELSE                                                                 
              DISPLAY 'START BROWSE ERROR BP13K895,STATUS IS '                  
                          K895-STATUS                                           
              MOVE K895-STATUS  TO RETURN-CODE                                  
              PERFORM 800-CLOSE-ROUTINE   THRU  800-EXIT.                       
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       460-READNEXT-BP13K895.                                                   
           READ BP13K895 NEXT RECORD                                            
                AT END                                                          
                   MOVE 'Y'  TO  K895-EOF                                       
           END-READ.                                                            
                                                                                
           IF K895-STATUS = 00 OR 02                                            
              IF ((F136-NUM-REGN = K895-NUM-REGN-HIST) AND                      
                  (F136-NUM-NRIC = K895-NUM-NRIC-HIST))                         
                 MOVE 'Y'          TO    WS-K895-FND-FLAG                       
                 IF K895-NUM-SCH-ACCT NOT = SPACES AND LOW-VALUES               
                    MOVE K895-NME-OCCP TO F136-NME-APPL                         
                    MOVE 'Y'       TO    K895-EOF                               
                 END-IF                                                         
              ELSE                                                              
                 MOVE SPACES       TO    F136-NME-APPL                          
                 MOVE 'Y'          TO    K895-EOF                               
              END-IF                                                            
           ELSE                                                                 
              IF K895-STATUS = 10 OR 23                                         
                 MOVE SPACES       TO    F136-NME-APPL                          
                 MOVE 'Y'          TO    K895-EOF                               
              ELSE                                                              
                 DISPLAY ' ERROR READING, BP13K895 = ' K895-STATUS              
                 MOVE K895-STATUS  TO RETURN-CODE                               
                 PERFORM 800-CLOSE-ROUTINE   THRU  800-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-ACCESS-BP13K800.                                                     
           MOVE SPACES         TO  WS-F127-FT.                                  
           MOVE F136-NUM-REGN  TO  K800-NUM-REGN.                               
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 00                                                  
              ADD 1          TO WS-K800-FND                                     
              IF K800-NUM-FLAT-TYPE = '1A'                                      
                 MOVE '1A'   TO WS-F127-FT                                      
              END-IF                                                            
              IF K800-NUM-FLAT-TYPE = '2A'                                      
                 MOVE '2A'   TO WS-F127-FT                                      
              END-IF                                                            
              IF K800-NUM-FLAT-TYPE = '2F'                                      
                 MOVE '2F'   TO WS-F127-FT                                      
              END-IF                                                            
              IF K800-NUM-FLAT-TYPE = '1F'                                      
                 MOVE '1F'   TO WS-F127-FT                                      
              END-IF                                                            
           ELSE                                                                 
              IF K800-STATUS = 10 OR 23                                         
                 ADD 1             TO WS-K800-NT-FND                            
                 PERFORM 600-ACCESS-BP13K893   THRU  600-EXIT                   
                 IF WS-K893-FND-FLAG = 'Y'                                      
                    ADD 1          TO WS-K893-FND                               
                 ELSE                                                           
                    ADD 1          TO WS-K893-NT-FND                            
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY ' ERROR READING, BP13K800 = ' K800-STATUS              
                 MOVE K800-STATUS  TO RETURN-CODE                               
                 PERFORM 800-CLOSE-ROUTINE   THRU  800-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-ACCESS-BP13K893.                                                     
           MOVE  'N'             TO K893-EOF                                    
                                    WS-K893-FND-FLAG.                           
           INITIALIZE            BP13K893-MASTER.                               
           MOVE SPACES           TO K893-KEY-FLD.                               
           MOVE F136-NUM-REGN    TO K893-NUM-REGN-HIST.                         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           IF K893-STATUS = 00 OR 02                                            
              PERFORM 650-READNEXT-BP13K893   THRU 650-EXIT                     
                        UNTIL K893-EOF = 'Y'                                    
           ELSE                                                                 
           IF K893-STATUS = 10 OR 23                                            
              MOVE 'N'          TO WS-K893-FND-FLAG                             
           ELSE                                                                 
              DISPLAY 'START BROWSE ERROR BP13K893,STATUS IS '                  
                          K893-STATUS                                           
              MOVE K893-STATUS  TO RETURN-CODE                                  
              PERFORM 800-CLOSE-ROUTINE   THRU  800-EXIT.                       
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       650-READNEXT-BP13K893.                                                   
           READ BP13K893 NEXT RECORD                                            
                AT END                                                          
                   MOVE 'Y'  TO  K893-EOF                                       
           END-READ.                                                            
                                                                                
           IF K893-STATUS = 00 OR 02                                            
              IF F136-NUM-REGN = K893-NUM-REGN-HIST                             
                 MOVE 'Y'          TO WS-K893-FND-FLAG                          
                 IF K893-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                
                    IF K893-NUM-FLAT-TYPE = '1A'                                
                       MOVE '1A'   TO WS-F127-FT                                
                    END-IF                                                      
                    IF K893-NUM-FLAT-TYPE = '2A'                                
                       MOVE '2A'   TO WS-F127-FT                                
                    END-IF                                                      
                    IF K893-NUM-FLAT-TYPE = '2F'                                
                       MOVE '2F'   TO WS-F127-FT                                
                    END-IF                                                      
                    IF K893-NUM-FLAT-TYPE = '1F'                                
                       MOVE '1F'   TO WS-F127-FT                                
                    END-IF                                                      
                    MOVE 'Y'       TO    K893-EOF                               
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'          TO    K893-EOF                               
              END-IF                                                            
           ELSE                                                                 
              IF K893-STATUS = 10 OR 23                                         
                 MOVE 'Y'          TO    K893-EOF                               
              ELSE                                                              
                 DISPLAY ' ERROR READING, BP13K893 = ' K893-STATUS              
                 MOVE K893-STATUS  TO RETURN-CODE                               
                 PERFORM 800-CLOSE-ROUTINE   THRU  800-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       700-WRITE-BP13F127.                                                      
           MOVE  BP13F136-CASHTRAN  TO  BP13F127-TRANS.                         
           MOVE  WS-F127-FT         TO  F127-NUM-FLAT-TYPE-SA.                  
           WRITE BP13F127-TRANS.                                                
           ADD 1 TO WS-BP13F127-WRITE.                                          
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***************************************************************           
      *     CLOSE ALL FILES AND STOP RUN.                           *           
      ***************************************************************           
       800-CLOSE-ROUTINE.                                                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '            ** BP13C110 ** '.                               
           DISPLAY 'NO OF RECORDS READ FROM FILE (BP13F136)  = '                
                    WS-BP13F136-READ.                                           
           DISPLAY 'NO OF RECORDS WRITTEN IN FILE(BP13F127)  = '                
                    WS-BP13F127-WRITE.                                          
           DISPLAY 'NO OF RECORDS  FOUND IN FILE   (BP13K820)  = '              
                    WS-K820-FND.                                                
           DISPLAY 'NO OF RECORDS NOT FOUND IN FILE(BP13K820)  = '              
                    WS-K820-NT-FND.                                             
           DISPLAY 'NO OF RECORDS  FOUND IN FILE   (BP13K895)  = '              
                    WS-K895-FND.                                                
           DISPLAY 'NO OF RECORDS NOT FOUND IN FILE(BP13K895)  = '              
                    WS-K895-NT-FND.                                             
           DISPLAY 'NO OF RECORDS  FOUND IN FILE   (BP13K800)  = '              
                    WS-K800-FND.                                                
           DISPLAY 'NO OF RECORDS NOT FOUND IN FILE(BP13K800)  = '              
                    WS-K800-NT-FND.                                             
           DISPLAY 'NO OF RECORDS  FOUND IN FILE   (BP13K893)  = '              
                    WS-K893-FND.                                                
           DISPLAY 'NO OF RECORDS NOT FOUND IN FILE(BP13K893)  = '              
                    WS-K893-NT-FND.                                             
                                                                                
           CLOSE BP13F136                                                       
                 BP13K800                                                       
                 BP13K893                                                       
                 BP13K820                                                       
                 BP13K895                                                       
                 BP13F127.                                                      
                                                                                
           IF K800-STATUS NOT = '00' AND '97'                                   
              DISPLAY ' CLOSING ERROR, K800-STATUS ' K800-STATUS.               
                                                                                
           IF K893-STATUS NOT = '00' AND '97'                                   
              DISPLAY ' CLOSING ERROR, K893-STATUS ' K893-STATUS.               
                                                                                
           IF K820-STATUS NOT = '00' AND '97'                                   
              DISPLAY ' CLOSING ERROR, K820-STATUS ' K820-STATUS.               
                                                                                
           IF K895-STATUS NOT = '00' AND '97'                                   
              DISPLAY ' CLOSING ERROR, K895-STATUS ' K895-STATUS.               
                                                                                
           STOP RUN.                                                            
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
