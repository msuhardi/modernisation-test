      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.    BP13C132.                                                 
      *AUTHOR.        EG1.                                                      
      *DATE-WRITTEN.  26/03/96.                                                 
      *DATE-COMPILED.                                                           
      ******************************************************************        
      * SYSTEM NAME :    SYSTEM OF COMMITMENT                          *        
      * SYSTEM ID   :    BP13                                          *        
      * OBJECTIVE   :    TO PRINT A LIST OF SOC - AUDIT TRAIL FOR SA   *        
      *                  ON PAYMENT TYPE 'CD'.                         *        
      *                                                                *        
      * FILES       :    BP13F120                                      *        
      *                  BP13L132                                      *        
      *                                                                *        
      * MODIFICATION HISTORY:                                          *        
      * ====================                                           *        
      * CHG-NO   BY   DATE    DESCRIPTION                              *        
      * -------- --- ------   ------------                             *        
      * BP130103 EG1 26031996 TO CATER FOR SUB-TRANSFER MODULE         *        
      * BP134092 ESA111022011 TO CATER FOR NEW GL ACCOUNT              *        
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *================================================================*        
       CONFIGURATION SECTION.                                                   
      *================================================================*        
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
           SELECT BP13F120   ASSIGN TO BP13F120.                                
           SELECT BP13L132   ASSIGN TO BP13L132.                                
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
       FD   BP13F120                                                            
            BLOCK  CONTAINS   0 RECORDS                                         
            RECORD CONTAINS 150 CHARACTERS                                      
            LABEL  RECORD IS STANDARD.                                          
       COPY BP13F120.                                                           
                                                                        00630000
       FD  BP13L132                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL  RECORDS  ARE OMITTED.                                         
       01  L132-REC                    PIC X(132).                              
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *    COUNTER VARIABLES                                           *        
      *----------------------------------------------------------------*        
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F120-READ        PIC 9(5)    VALUE ZEROS.                 
           05  WS-CNT-L132-WRITE       PIC 9(3)    VALUE ZEROS.                 
                                                                                
      *----------------------------------------------------------------*        
      *    FLAG     VARIABLES                                          *        
      *----------------------------------------------------------------*        
       01  WS-EOF-STATUS.                                                       
           05  WS-F120-EOF             PIC X(1)    VALUE 'N'.                   
                                                                                
      *----------------------------------------------------------------*        
      *    COMPUTE  VARIABLES                                          *        
      *----------------------------------------------------------------*        
       01  WS-ACCUM-FIELDS.                                                     
           05  WS-SUBTOT-DRCR-AMT      PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-SUBTOT-DR-AMT        PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-SUBTOT-CR-AMT        PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-TOT-DRCR-AMT         PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-TOT-CR-AMT           PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-TOT-DR-AMT           PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-JR-DRCR-AMT          PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-JR-CR-AMT            PIC S9(9)V99 COMP-3 VALUE +0.            
           05  WS-JR-DR-AMT            PIC S9(9)V99 COMP-3 VALUE +0.            
                                                                                
      *----------------------------------------------------------------*        
      *  MISCELLEANEOUS VARIABLES                                      *        
      *----------------------------------------------------------------*        
       01  LT-SUS-GL-ACCT          PIC X(18) VALUE '0 00 00 00 10 4009'.        
       01  LT-CD-GL-ACCT           PIC X(18) VALUE '0 00 00 00 10 4517'.        
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CC          PIC X(2).                                    
           05  WS-CURR-YYMMDD.                                                  
               10 WS-CURR-YY       PIC X(2).                                    
               10 WS-CURR-MM       PIC X(2).                                    
               10 WS-CURR-DD       PIC X(2).                                    
                                                                                
       01  WS-MISC-VARIABLES.                                                   
           05  WS-PREV-POST-DATE       PIC X(08)   VALUE SPACES.                
           05  WS-PREV-JRNO            PIC X(09)   VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *    PRINTING VARIABLES                                          *        
      *----------------------------------------------------------------*        
       01  WS-PRINT-VARIABLES.                                                  
           05  WS-LINE-CNT             PIC 9(2)    VALUE 66.                    
           05  WS-PAGE-CNT             PIC 9(5)    VALUE ZEROS.                 
           05  WS-SEQ-CNT              PIC 9(5)    VALUE ZEROS.                 
                                                                                
      *----------------------------------------------------------------*        
      *    PRINTING LAYOUT                                             *        
      *----------------------------------------------------------------*        
       01  L132-PRT-HDG-1.                                                      
           05  L132-RPT-ID         PIC X(14)   VALUE 'BP13L132'.                
           05  FILLER              PIC X(8)    VALUE 'HDBCAT 3'.                
           05  FILLER              PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE: '.                  
           05  L132-PRT-DATE       PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'PAGE: '.                  
           05  L132-PRT-PAGE       PIC ZZZZ9.                                   
                                                                                
       01  L132-PRT-HDG-2.                                                      
           05  FILLER              PIC X(30)   VALUE SPACES.                    
           05  FILLER              PIC X(46)   VALUE                            
               'SOC - AUDIT TRAIL FOR TRANSFER OF COMMITMENT  '.                
           05  FILLER              PIC X(30)   VALUE                            
               'DEPOSIT TO SUB-TRANSFER      '.                                 
                                                                                
       01  L132-PRT-HDG-3.                                                      
           05  FILLER              PIC X(51)   VALUE SPACES.                    
           05  FILLER              PIC X(17)   VALUE                            
              'FOR POSTING DATE '.                                              
           05  L132-POST-DATE      PIC X(08)   VALUE SPACES.                    
                                                                                
       01  L132-PRT-HDG-4.                                                      
           05  FILLER              PIC X(48)   VALUE SPACES.                    
           05  FILLER              PIC X(25)   VALUE                            
              'JOURNAL NUMBER ISSUED = '.                                       
           05  L132-JOURNAL-NO     PIC X(09)   VALUE SPACES.                    
                                                                                
       01  L132-PRT-HDG-5.                                                      
           05  FILLER              PIC X(13)   VALUE SPACES.                    
           05  FILLER              PIC X(11)   VALUE SPACES.                    
           05  FILLER              PIC X(05)   VALUE SPACES.                    
      *    05  FILLER              PIC X(12)   VALUE 'SALES'.                   
           05  FILLER              PIC X(44)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'PAYT'.                    
                                                                                
       01  L132-PRT-HDG-6.                                                      
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'DATE'.                    
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'REGN NO'.                 
           05  FILLER              PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE 'GL ACCOUNT'.              
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(12)   VALUE 'SCHEME-AC-NO'.            
           05  FILLER              PIC X(09)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'TYPE'.                    
           05  FILLER              PIC X(13)   VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE '       AMOUNT'.           
                                                                                
       01  L132-PRT-DETAIL-1.                                                   
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  L132-DATE           PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  L132-REGN           PIC X(08)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE SPACES.                    
           05  L132-GL-ACC         PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  L132-SCH-ACC        PIC X(14)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE SPACES.                    
           05  L132-PAYT-TYPE      PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE SPACES.                    
           05  L132-AMOUNT         PIC ZZZ,ZZZ,ZZ9.99.                          
           05  L132-DRCR           PIC X(02).                                   
                                                                                
       01  L132-PRT-SUBTOT-LINE-1.                                              
           05  FILLER              PIC X(88)   VALUE SPACES.                    
           05  FILLER              PIC X(18)   VALUE ALL '-'.                   
                                                                                
       01  L132-PRT-SUBTOT-LINE-2.                                              
           05  FILLER              PIC X(22)   VALUE SPACES.                    
           05  FILLER              PIC X(25)   VALUE                            
              'SUB TOTAL FOR GL ACCOUNT '.                                      
           05  L132-PRT-SUBTOT-LINE.                                            
               10  L132-SUBTOT-GL-ACC   PIC X(18)   VALUE SPACES.               
               10  FILLER               PIC X(25)   VALUE SPACES.               
               10  L132-SUBTOT-DRCR-AMT PIC ZZZ,ZZZ,ZZ9.99.                     
               10  L132-SUBTOT-DRCR     PIC X(02)   VALUE SPACES.               
                                                                                
      *====================================================*                    
      *        FORMAT FOR TOTAL HEADING AND DETAIL         *                    
      *====================================================*                    
       01  L132-PRT-TOTAL-LINE-1.                                               
           05  FILLER              PIC X(26)   VALUE SPACES.                    
           05  FILLER              PIC X(21)   VALUE                            
              'TOTAL FOR GL ACCOUNT '.                                          
           05  L132-PRT-TOTAL-LINE.                                             
               10  L132-TOTAL-GL-ACC   PIC X(18)   VALUE SPACES.                
               10  FILLER               PIC X(25)   VALUE SPACES.               
               10  L132-TOTAL-DRCR-AMT PIC ZZZ,ZZZ,ZZ9.99.                      
               10  L132-TOTAL-DRCR     PIC X(02)   VALUE SPACES.                
                                                                                
       01  L132-PRT-TOTAL-LINE-2.                                               
           05  FILLER              PIC X(22)   VALUE SPACES.                    
           05  FILLER              PIC X(24)   VALUE                            
              'TOTAL FOR PYMT TRANS CD '.                                       
           05  L132-PRT-TOT-LINE.                                               
               10  FILLER               PIC X(43)   VALUE SPACES.               
               10  L132-TOT-DRCR-AMT    PIC ZZZ,ZZZ,ZZ9.99.                     
               10  L132-TOT-DRCR        PIC X(02)   VALUE SPACES.               
                                                                                
       01  L132-PRT-GR-TOTAL-LINE-1.                                            
           05  FILLER                  PIC X(30)   VALUE SPACES.                
           05  FILLER                  PIC X(23)   VALUE                        
              'TOTAL DEBITS       =   '.                                        
           05  L132-TOT-DR-AMT         PIC ZZZ,ZZZ,ZZ9.99.                      
                                                                                
       01  L132-PRT-GR-TOTAL-LINE-2.                                            
           05  FILLER                  PIC X(30)   VALUE SPACES.                
           05  FILLER                  PIC X(23)   VALUE                        
              'TOTAL CREDITS      =   '.                                        
           05  L132-TOT-CR-AMT         PIC ZZZ,ZZZ,ZZ9.99.                      
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 2000-PROCESS-F120      THRU 2000-EXIT                        
             UNTIL WS-F120-EOF = 'Y'.                                           
           PERFORM 6500-PRINT-TOTAL       THRU 6500-EXIT.                       
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  BP13F120                                                 
                OUTPUT BP13L132.                                                
                                                                                
           ACCEPT  WS-CURR-YYMMDD FROM DATE.                                    
           IF WS-CURR-YY > '90'                                                 
              MOVE '19'            TO WS-CURR-CC                                
           ELSE                                                                 
              MOVE '20'            TO WS-CURR-CC                                
           END-IF.                                                              
           MOVE ZEROS  TO WS-PREV-POST-DATE.                                    
           MOVE SPACES TO WS-PREV-JRNO.                                         
                                                                                
           READ BP13F120                                                        
                AT END MOVE 'Y' TO WS-F120-EOF                                  
                GO TO 1000-EXIT.                                                
                                                                                
           ADD 1       TO WS-CNT-F120-READ.                                     
           MOVE 60     TO WS-LINE-CNT.                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2000-PROCESS-F120.                                                       
      *================================================================*        
           PERFORM 2100-FORMAT-L132   THRU 2100-EXIT.                           
                                                                                
           READ BP13F120                                                        
                AT END MOVE 'Y' TO WS-F120-EOF                                  
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                TO WS-CNT-F120-READ.                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2100-FORMAT-L132.                                                        
      *================================================================*        
           MOVE F120-NUM-REGN         TO  L132-REGN.                            
           STRING F120-DTE-POST(7:2) '/'                                        
                  F120-DTE-POST(5:2) '/'                                        
                  F120-DTE-POST(1:4) DELIMITED BY SIZE                          
                                    INTO  L132-DATE                             
           STRING F120-NUM-SCH-ACCT(1:4) '-'                                    
                  F120-NUM-SCH-ACCT(5:4) '-'                                    
                  F120-NUM-SCH-ACCT(9:1) '-'                                    
                  F120-NUM-SCH-ACCT(10:2) DELIMITED BY SIZE                     
                                    INTO  L132-SCH-ACC.                         
           MOVE F120-CDE-PAYMENT-TYPE TO  L132-PAYT-TYPE.                       
           INSPECT F120-AMT-RECEIPT REPLACING ALL LOW-VALUE                     
                   BY ZEROS.                                                    
           MOVE F120-AMT-RECEIPT      TO L132-AMOUNT.                           
                                                                                
           MOVE LT-SUS-GL-ACCT        TO L132-GL-ACC.                           
      *-------------------------------------------------------------*           
      * NEGATIVE AMOUNT MEANS DEBIT AND POSITIVE MEANS CREDIT                   
      *   FOR ALL PAYMENT TYPES EXCEPT FOR 'SOC'                                
      * SOC - NEGATIVE AMT MEANS CREDIT AND POSITIVE AMT MEANS DEBIT            
      *-------------------------------------------------------------*           
           IF F120-AMT-RECEIPT > 0                                              
              MOVE 'CR'            TO L132-DRCR                                 
              ADD F120-AMT-RECEIPT TO WS-SUBTOT-CR-AMT                          
                                      WS-TOT-CR-AMT                             
           ELSE                                                                 
              MOVE 'DR'            TO L132-DRCR                                 
              ADD F120-AMT-RECEIPT TO WS-SUBTOT-DR-AMT                          
                                      WS-TOT-DR-AMT                             
           END-IF.                                                              
                                                                                
           PERFORM 6000-PRINT-L132 THRU 6000-EXIT.                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       6000-PRINT-L132.                                                         
      *================================================================*        
           PERFORM 6100-CHECK-LINE-CNT THRU 6100-EXIT.                          
                                                                                
           IF F120-AMT-RECEIPT > 0                                              
              ADD F120-AMT-RECEIPT TO WS-JR-CR-AMT                              
           ELSE                                                                 
              ADD F120-AMT-RECEIPT TO WS-JR-DR-AMT                              
           END-IF.                                                              
                                                                                
           WRITE L132-REC          FROM L132-PRT-DETAIL-1 AFTER 1.              
           MOVE SPACES             TO L132-REC.                                 
                                                                                
           ADD 1                   TO WS-LINE-CNT.                              
           ADD 1                   TO WS-CNT-L132-WRITE.                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       6100-CHECK-LINE-CNT.                                                     
      *================================================================*        
           IF (WS-PREV-POST-DATE NOT = F120-DTE-POST)                           
              IF (WS-PREV-POST-DATE NOT = ZEROS)                                
                 PERFORM 6200-PRINT-HEADING   THRU 6200-EXIT            05330000
                 MOVE F120-DTE-POST TO WS-PREV-POST-DATE                        
                 MOVE F120-NUM-JRNO TO WS-PREV-JRNO                             
                 MOVE ZEROS TO WS-JR-DRCR-AMT                                   
                 MOVE ZEROS TO WS-JR-DR-AMT                                     
                 MOVE ZEROS TO WS-JR-CR-AMT                                     
              ELSE                                                              
                 MOVE F120-DTE-POST TO WS-PREV-POST-DATE                        
                 MOVE F120-NUM-JRNO TO WS-PREV-JRNO                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF (WS-LINE-CNT > 45)                                        05320000
              PERFORM 6200-PRINT-HEADING THRU 6200-EXIT                 05330000
           END-IF.                                                      05340000
                                                                        05300000
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       6200-PRINT-HEADING.                                              05670000
      *================================================================*        
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-YY              05760000
                  DELIMITED BY SIZE INTO L132-PRT-DATE.                 05770000
                                                                                
           STRING F120-DTE-POST(7:2) '/'                                        
                  F120-DTE-POST(5:2) '/'                                        
                  F120-DTE-POST(3:2) DELIMITED BY SIZE                          
                                    INTO L132-POST-DATE.                        
                                                                        05800000
           MOVE 8                     TO WS-LINE-CNT.                           
           ADD  1                     TO WS-PAGE-CNT.                   05740000
           MOVE WS-PAGE-CNT           TO L132-PRT-PAGE.                 05750000
                                                                        05800000
           WRITE L132-REC FROM  L132-PRT-HDG-1 AFTER PAGE.              05840001
           WRITE L132-REC FROM  L132-PRT-HDG-2 AFTER 2.                 05850001
           WRITE L132-REC FROM  L132-PRT-HDG-3 AFTER 3.                 05860001
           WRITE L132-REC FROM  L132-PRT-HDG-5 AFTER 2.                 05880001
           WRITE L132-REC FROM  L132-PRT-HDG-6 AFTER 1.                 05881001
           MOVE  SPACES     TO  L132-REC.                                       
           WRITE L132-REC AFTER 2.                                      05910000
                                                                        06040000
       6200-EXIT.                                                       06050000
           EXIT.                                                        06060000
                                                                        06040000
      *================================================================*        
       6400-PRINT-SUBTOT.                                                       
      *================================================================*        
      ******************************************************************        
      *                                                                *        
      *       PRINT SUB-TOTAL FOR EACH PAYMENT TYPE                    *        
      *                                                                *        
      ******************************************************************        
           MOVE LT-SUS-GL-ACCT   TO   L132-SUBTOT-GL-ACC.                       
           ADD 3                 TO   WS-LINE-CNT.                              
           WRITE L132-REC        FROM L132-PRT-SUBTOT-LINE-1 AFTER 2.           
           MOVE SPACES           TO   L132-REC.                                 
                                                                                
           COMPUTE WS-SUBTOT-DRCR-AMT = WS-SUBTOT-DR-AMT +                      
                                        WS-SUBTOT-CR-AMT.                       
                                                                                
           MOVE WS-SUBTOT-DRCR-AMT TO L132-SUBTOT-DRCR-AMT.                     
           IF WS-SUBTOT-DRCR-AMT > 0                                            
              MOVE 'CR'            TO L132-SUBTOT-DRCR                          
           ELSE                                                                 
              MOVE 'DR'            TO L132-SUBTOT-DRCR.                         
                                                                                
           WRITE L132-REC         FROM  L132-PRT-SUBTOT-LINE-2 AFTER 1.         
           ADD 4                  TO    WS-LINE-CNT.                            
           MOVE SPACES            TO    L132-REC.                               
           WRITE L132-REC AFTER 2.                                              
                                                                                
       6400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       6500-PRINT-TOTAL.                                                        
      *================================================================*        
           PERFORM 6400-PRINT-SUBTOT   THRU 6400-EXIT.                          
           PERFORM 6100-CHECK-LINE-CNT THRU 6100-EXIT.                          
                                                                                
           MOVE LT-CD-GL-ACCT      TO  L132-TOTAL-GL-ACC.                       
           ADD 3                   TO  WS-LINE-CNT.                             
                                                                                
           COMPUTE WS-SUBTOT-DRCR-AMT = WS-SUBTOT-DR-AMT +                      
                                        WS-SUBTOT-CR-AMT.                       
                                                                                
           MOVE WS-SUBTOT-DRCR-AMT TO  L132-TOTAL-DRCR-AMT.                     
           IF WS-SUBTOT-DRCR-AMT < 0                                            
              MOVE 'CR' TO L132-TOTAL-DRCR                                      
           ELSE                                                                 
              MOVE 'DR' TO L132-TOTAL-DRCR.                                     
                                                                                
           WRITE L132-REC FROM L132-PRT-TOTAL-LINE-1.                           
                                                                                
      *                                                                         
      * PRINT TOTAL FOR ALL TRANSACTIONS *                                      
      *                                                                         
           COMPUTE WS-TOT-DRCR-AMT = WS-TOT-DR-AMT +                            
                                     WS-TOT-CR-AMT.                             
           IF WS-TOT-DRCR-AMT > 0                                               
              MOVE 'CR' TO L132-TOT-DRCR                                        
           ELSE                                                                 
              MOVE 'DR' TO L132-TOT-DRCR.                                       
                                                                                
           MOVE WS-TOT-DRCR-AMT  TO  L132-TOT-DRCR-AMT.                         
           ADD 3                 TO  WS-LINE-CNT.                               
           MOVE SPACES           TO  L132-REC.                                  
                                                                                
      *                                                                         
      * PRINT GRAND TOTAL FOR DEBIT AND CREDIT AMOUNTS *                        
      *                                                                         
           ADD 4                TO   WS-LINE-CNT.                               
           MOVE WS-TOT-CR-AMT   TO   L132-TOT-DR-AMT.                           
           WRITE L132-REC FROM L132-PRT-GR-TOTAL-LINE-1                 .       
                                     AFTER 3.                           .       
           MOVE SPACES          TO   L132-REC.                                  
           MOVE WS-TOT-CR-AMT   TO   L132-TOT-CR-AMT.                           
           WRITE L132-REC FROM L132-PRT-GR-TOTAL-LINE-2                 .       
                                     AFTER 1.                           .       
           MOVE SPACES          TO   L132-REC.                                  
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9999-CLOSE-ROUTINE.                                                      
      *================================================================*        
      ****************************************************************          
      *                                                              *          
      *            CLOSE FILES AND END PROCESSING                    *          
      *                                                              *          
      ****************************************************************          
           CLOSE BP13F120                                                       
                 BP13L132.                                                      
                                                                                
           DISPLAY '**********************************'                         
           DISPLAY '**  BP13C132 CONTROL TOTALS     **'                         
           DISPLAY '**********************************'                         
           DISPLAY '** NO OF F120 RECS READ = ' WS-CNT-F120-READ.               
           DISPLAY '** NO OF L132 RECS PRINTED = ' WS-CNT-L132-WRITE.           
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
