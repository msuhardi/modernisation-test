      *------------------------------------------------------------*            
       IDENTIFICATION DIVISION.                                                 
      *------------------------------------------------------------*            
       PROGRAM-ID.   BP13CH35.                                                  
      *AUTHOR.       ZARITA DANE SARMIENTO.                                     
      *DATE-WRITTEN. 05/08/2014.                                                
      *------------------------------------------------------------*            
      *                   SYSTEM OF COMMITMENT  (BP13)             *            
      *------------------------------------------------------------*            
      *                                                            *            
      *  OBJECTIVE   : 1.  GET ALL CASES THAT CAN BE REASSIGNED    *            
      *                2.  OUTPUT CASE DEPENDING IF IT IS          *            
      *                    UNCOMPLETED OR COMPLETED                *            
      *                                                            *            
      *  INPUT FILES : 1.  BP13F026  -  OIC RE-ASSIGN FILE         *            
      *                2.  BP13K034  -  TO GET STATUS              *            
      *                3.  BP13K022  -  OIC FILE                   *            
      *                4.  BM06K110  -  TO GET DTE-HANDOVER        *            
      *                                                            *            
      * CHG REF  DATE     OIC  REMARKS                             *            
      * -------- -------- ---- ---------------------------------   *            
      * BP137941 11092019 KV6  TO DISPLAY REGN INSTEAD OF CLOSING  *            
      *                        THE FILE WHEN ERROR READING BM06K110*            
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *------------------------------------------------------------*            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F026 ASSIGN TO BP13F026.                                  
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
           SELECT BP13K034 ASSIGN TO BP13K034                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K034-KEY-FLD                               
                  FILE STATUS     IS WS-K034-STATUS.                            
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  ALTERNATE KEY   IS K110-REGN-NO                               
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
           SELECT P13F022A ASSIGN TO P13F022A.                                  
           SELECT P13F022B ASSIGN TO P13F022B.                                  
                                                                                
      *------------------------------------------------------------*            
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *------------------------------------------------------------*            
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K034                                                            
            RECORD CONTAINS 30 CHARACTERS.                                      
       COPY BP13K034.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13F026                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       COPY BP13F026.                                                           
                                                                                
       FD   P13F022A                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       01   P13F022A-REC  PIC X(100).                                           
                                                                                
       FD   P13F022B                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       01   P13F022B-REC  PIC X(100).                                           
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
                                                                                
       01  WORK-AREAS.                                                          
           05  WS-F022-READ             PIC 9(08)  VALUE 0.                     
           05  WS-K022-READ             PIC 9(08)  VALUE 0.                     
           05  WS-K034-READ             PIC 9(08)  VALUE 0.                     
           05  WS-K110-READ             PIC 9(08)  VALUE 0.                     
           05  WS-F026-READ             PIC 9(08)  VALUE 0.                     
           05  WS-K022-NFND             PIC 9(08)  VALUE 0.                     
           05  WS-K110-NFND             PIC 9(08)  VALUE 0.                     
           05  WS-K034-NFND             PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-K022-EOF              PIC X(01)  VALUE 'N'.                   
           05  WS-F026-EOF              PIC X(01)  VALUE 'N'.                   
           05  WS-K034-EOF              PIC X(01)  VALUE 'N'.                   
           05  WS-K034-FND-TAG          PIC X(01)  VALUE 'N'.                   
           05  WS-STATUS                PIC X(01)  VALUE SPACE.                 
                                                                                
           05  WS-K022-WRITE            PIC 9(08)  VALUE 0.                     
           05  WS-F022A-COMP            PIC 9(08)  VALUE 0.                     
           05  WS-F022A-NOT-COMP        PIC 9(08)  VALUE 0.                     
           05  WS-K028-WRITE            PIC 9(08)  VALUE 0.                     
           05  WS-EC-DBS                PIC 9(08)  VALUE 0.                     
           05  WS-KEY-ISSUED            PIC 9(08)  VALUE 0.                     
           05  WS-CANCEL                PIC 9(08)  VALUE 0.                     
           05  WS-F022A-MET-3           PIC 9(08)  VALUE 0.                     
           05  WS-F022A-MET-4           PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-K022-STATUS           PIC 9(02)  VALUE 0.                     
           05  WS-K034-STATUS           PIC 9(02)  VALUE 0.                     
           05  WS-K110-STATUS           PIC 9(02)  VALUE 0.                     
                                                                                
       01 WS-K022-REC                   PIC X(100)   VALUE SPACES.              
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 1200-READ-F026     THRU 1200-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
                   UNTIL WS-F026-EOF = 'Y'.                                     
                                                                                
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1000-OPEN-FILES.                                                         
      *------------------------------------------------------------*            
                                                                                
           OPEN INPUT  BP13F026                                                 
                       BP13K022                                                 
                       BP13K034                                                 
                       BM06K110                                                 
                OUTPUT P13F022A                                                 
                       P13F022B.                                                
                                                                                
           IF  WS-K022-STATUS NOT = 00 AND 97                                   
                 DISPLAY 'OPEN BP13K022 FAIL. STATUS - '                        
                                            WS-K022-STATUS                      
                 MOVE  WS-K022-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF  WS-K034-STATUS NOT = 00 AND 97                                   
                 DISPLAY 'OPEN BP13K034 FAIL. STATUS - '                        
                                            WS-K034-STATUS                      
                 MOVE  WS-K034-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
                 DISPLAY 'OPEN BM06K110 FAIL. STATUS - '                        
                                            WS-K110-STATUS                      
                 MOVE  WS-K110-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1200-READ-F026.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13F026                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F026-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-F026-READ                                        
           END-READ.                                                            
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2000-PROCESS.                                                            
      *------------------------------------------------------------*            
                                                                                
           MOVE 'N'  TO WS-K022-EOF.                                            
                                                                                
           PERFORM  3000-READ-K022    THRU 3000-EXIT.                           
                                                                                
           PERFORM UNTIL  WS-K022-EOF = 'Y'  OR                                 
                          F026-NUM-FROM-OIC NOT = K022-NUM-OIC                  
              PERFORM 3050-READNEXT-K022   THRU 3050-EXIT                       
              IF F026-NUM-FROM-OIC  = K022-NUM-OIC                              
                PERFORM 3200-GET-CASES       THRU 3200-EXIT                     
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           PERFORM 1200-READ-F026    THRU 1200-EXIT.                            
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-READ-K022.                                                          
      *------------------------------------------------------------*            
                                                                                
                                                                                
           MOVE SPACES            TO K022-KEY-FLD.                              
           MOVE F026-NUM-FROM-OIC TO K022-KEY-FLD.                              
                                                                                
           START BP13K022 KEY >= K022-KEY-FLD                                   
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 10                                                           
                 MOVE 'Y' TO WS-K022-EOF                                        
              WHEN 23                                                           
                 ADD 1 TO WS-K022-NFND                                          
                 DISPLAY 'REGN NOT FOUND IN BP13K022. REGN : '                  
                             K022-NUM-REGN                                      
                 MOVE 'Y' TO WS-K022-EOF                                        
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K022, STATUS - '                   
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3050-READNEXT-K022.                                                      
      *------------------------------------------------------------*            
                                                                                
           READ BP13K022 NEXT RECORD                                            
                 AT END MOVE 'Y' TO  WS-K022-EOF.                               
                                                                                
           ADD 1 TO WS-K022-READ.                                               
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3100-READ-K110.                                                          
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES            TO K110-REC.                                  
           INITIALIZE K110-REC.                                                 
           MOVE K022-NUM-REGN     TO K110-REGN-NO.                              
                                                                                
           READ  BM06K110 KEY IS K110-REGN-NO.                                  
                                                                                
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K110-READ                                          
              WHEN 10                                                           
                 MOVE 'Y' TO WS-K022-EOF                                        
              WHEN 23                                                           
                 ADD 1 TO WS-K110-NFND                                          
                 DISPLAY 'REGN NOT FOUND IN BM06K110. REGN : '                  
                             K110-REGN-NO                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BM06K110, STATUS - '                   
                                            WS-K110-STATUS                      
                         ' REGN=' K110-REGN-NO                                  
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3150-READ-K034.                                                          
      *------------------------------------------------------------*            
                                                                                
           MOVE 'N'               TO WS-K034-FND-TAG                            
                                     WS-K034-EOF.                               
           MOVE SPACES            TO BP13K034-REC                               
                                     WS-STATUS.                                 
           MOVE K022-NUM-REGN     TO K034-NUM-REGN.                             
                                                                                
           START BP13K034 KEY >= K034-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K034-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 3160-READNEXT-K034   THRU 3160-EXIT                    
                     UNTIL WS-K034-EOF = 'Y'   OR                               
                           WS-K034-FND-TAG = 'Y'                                
              WHEN 10                                                           
              WHEN 23                                                           
                 ADD 1 TO WS-K034-NFND                                          
                 DISPLAY 'REGN NOT FOUND IN BP13K034. REGN : '                  
                             K034-NUM-REGN                                      
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K034, STATUS - '                   
                                            WS-K034-STATUS                      
                 MOVE WS-K034-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3160-READNEXT-K034.                                                      
      *------------------------------------------------------------*            
                                                                                
           READ BP13K034 NEXT RECORD                                            
                AT END MOVE 'Y' TO  WS-K034-EOF                                 
                GO TO 3160-EXIT                                                 
           END-READ.                                                            
                                                                                
           IF K034-NUM-REGN = K022-NUM-REGN                                     
              MOVE K034-NUM-STATUS   TO WS-STATUS                               
              MOVE 'Y'               TO WS-K034-FND-TAG                         
           ELSE                                                                 
              MOVE 'Y'               TO WS-K034-EOF                             
           END-IF.                                                              
                                                                                
       3160-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3200-GET-CASES.                                                          
      *------------------------------------------------------------*            
                                                                                
           IF K022-NUM-REGN(1:1) = '5' OR '6'                                   
              ADD 1 TO WS-EC-DBS                                                
              GO TO 3200-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 3100-READ-K110        THRU 3100-EXIT.                        
           PERFORM 3150-READ-K034        THRU 3150-EXIT.                        
      *    DISPLAY ' REGN :' K022-NUM-REGN                                      
      *            ' OIC : ' K022-NUM-OIC                                       
      *            ' DTE HANDOVER: ' K110-DTE-HANDOVER                          
                                                                                
           IF F026-NUM-METHOD = 3 AND F026-NUM-POS-OIC = 'N'                    
              WRITE P13F022A-REC   FROM BP13K022-REC                            
              ADD 1 TO WS-F022A-MET-3                                           
           ELSE                                                                 
             IF F026-NUM-METHOD = 4 AND F026-NUM-POS-OIC = 'N'                  
                WRITE P13F022A-REC   FROM BP13K022-REC                          
                ADD 1 TO WS-F022A-MET-4                                         
             ELSE                                                               
                IF K110-DTE-HANDOVER NOT = SPACES                               
                                                 AND LOW-VALUES AND ZEROES      
                   IF K110-DTE-KEY-ISSUED NOT = SPACES AND LOW-VALUES           
                                                       AND ZEROES               
                      ADD 1 TO WS-KEY-ISSUED                                    
                   ELSE                                                         
                      IF F026-CDE-COMPLETE = 'Y'                                
                        WRITE P13F022A-REC   FROM BP13K022-REC                  
                        ADD 1 TO WS-F022A-COMP                                  
                      END-IF                                                    
                   END-IF                                                       
                ELSE                                                            
                  IF WS-STATUS       = 'C' AND F026-NUM-CANCEL = 'Y'            
                     DISPLAY 'CANCEL CASE : ' K022-NUM-REGN                     
                     ADD 1 TO WS-CANCEL                                         
                     WRITE P13F022B-REC FROM BP13K022-REC                       
                     ADD 1 TO WS-F022A-NOT-COMP                                 
                  ELSE                                                          
                    IF F026-CDE-COMPLETE = 'N'                                  
                       WRITE P13F022B-REC FROM BP13K022-REC                     
                       ADD 1 TO WS-F022A-NOT-COMP                               
                    END-IF                                                      
                  END-IF                                                        
                END-IF                                                          
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------*            
                                                                                
           CLOSE BP13K022                                                       
                 BM06K110                                                       
                 BP13K034                                                       
                 BP13F026                                                       
                 P13F022A                                                       
                 P13F022B.                                                      
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
                 DISPLAY 'CLOSE BP13K022 FAIL. STATUS-'                         
                                         WS-K022-STATUS                         
                 MOVE WS-K022-STATUS  TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF WS-K034-STATUS NOT = 00 AND 97                                    
                 DISPLAY 'CLOSE BP13K034 FAIL. STATUS-'                         
                                         WS-K034-STATUS                         
                 MOVE WS-K034-STATUS  TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
                 DISPLAY 'CLOSE BM06K110 FAIL. STATUS-'                         
                                         WS-K110-STATUS                         
                 MOVE WS-K110-STATUS  TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
                                                                                
           DISPLAY '*----------- BP13CH35 -------------*'.                      
           DISPLAY SPACES.                                                      
           DISPLAY ' 1. BP13F026 READ      : '  WS-F026-READ.                   
           DISPLAY ' 2. BP13K022 CASES     : '  WS-K022-READ.                   
           DISPLAY ' 3. BP13K110 NOT FOUND : '  WS-K110-NFND.                   
           DISPLAY ' 4. BP13K110 CASES FND : '  WS-K110-READ.                   
           DISPLAY ' 5. COMPLETE CASES     : '  WS-F022A-COMP.                  
           DISPLAY ' 6. NOT COMPLETE CASES : '  WS-F022A-NOT-COMP.              
           DISPLAY ' 7. DBS/EC CASES       : '  WS-EC-DBS.                      
           DISPLAY ' 8. CANCELLED CASES    : '  WS-CANCEL.                      
           DISPLAY ' 9. WITH DTE-KEY ISSUE : '  WS-KEY-ISSUED.                  
           DISPLAY '10. CASES TO UPD (3)   : '  WS-F022A-MET-3.                 
           DISPLAY '11. CASES TO UPD (4)   : '  WS-F022A-MET-4.                 
           DISPLAY SPACES.                                                      
           DISPLAY '*----------------------------------*'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
