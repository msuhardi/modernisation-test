      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C02F.                                                 
      *AUTHOR.        BALA.                                                     
      *DTE WRITTEN.   25JUL2006.                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      **********************************************************                
      *                                                        *                
      *    OBJECTIVES  :                                       *                
      *                                                        *                
      *    1.  TO CREATE FTP FILE FOR DEVELOPER,BOOKING ETHNIC *                
      *        QUOTA.                                          *                
      *                                                        *                
      *    INPUT FILES : BP13FA20 - DBS FILE                   *                
      *                                                        *                
      *    OUTPUT FILE : BP13FB61 - FTP FILE FOR SIM LIAN      *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY    DATE   DETAILS                          *                
      * ======= ====  ======  ================================ *                
      * BP132888 BA2  190706  TO CHANGE THE OUTPUT FILE NAME   *                
      * BP132914 BA2  070906  MOVE '=' TO PROJ AND BLK FIELDS  *                
      *                       INSTEAD OF SPACES.               *                
      * ====================================================== *                
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-9121.                                               
       OBJECT-COMPUTER. IBM-9121.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA20 ASSIGN    TO BP13FA20.                               
                                                                                
           SELECT BP13FB61 ASSIGN    TO BP13FB61.                               
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD   BP13FA20                                                            
            BLOCK      CONTAINS 0    RECORDS                                    
            RECORD     CONTAINS 200  CHARACTERS                                 
            LABEL      RECORDS  ARE  STANDARD                                   
            RECORDING  MODE     IS  F.                                          
       COPY BP13FA20.                                                           
                                                                                
                                                                                
       FD   BP13FB61                                                            
            BLOCK     CONTAINS 0   RECORDS                                      
            RECORD    CONTAINS 42  CHARACTERS                                   
            LABEL     RECORDS  ARE OMITTED                                      
            RECORDING MODE     IS  F.                                           
       COPY BP13FB61.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-FA20-EOF-SW             PIC X(1)    VALUE 'N'.                
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-FB61                PIC 9(5) COMP  VALUE 0.               
           05  WS-CNT-FA20                PIC 9(5) COMP  VALUE 0.               
                                                                                
       01  WS-DATES.                                                            
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD           PIC 9(2).                             
               10  FILLER                 PIC X(1)    VALUE '/'.                
               10  WS-EDITED-MM           PIC 9(2).                             
               10  FILLER                 PIC X(1)    VALUE '/'.                
               10  WS-EDITED-YY           PIC 9(4).                             
           05  WS-CURRENT-DATE.                                                 
               10  WS-CURR-CCYY.                                                
                   15  WS-CURR-YY         PIC 9(4).                             
               10  WS-CURR-MM             PIC 9(2).                             
               10  WS-CURR-DD             PIC 9(2).                             
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-TODAY-DATE.                                                   
               10  WS-SYS-YY              PIC 9(4).                             
               10  WS-SYS-MM              PIC 9(2).                             
               10  WS-SYS-DD              PIC 9(2).                             
           05  WS-TIME-NUM                PIC 9(9).                             
           05  WS-TIME REDEFINES WS-TIME-NUM.                                   
               10  WS-HH                  PIC 9(2).                             
               10  WS-MM                  PIC 9(2).                             
               10  WS-SS                  PIC 9(2).                             
               10  WS-GG                  PIC 9(2).                             
               10  WS-SG                  PIC 9(1).                             
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-FOUND-SW                PIC X(1)    VALUE 'N'.                
           05  WS-EQL-VALUE               PIC X(5)    VALUE SPACES.             
           05  WS-CTR                     PIC 9(1)    VALUE ZEROES.             
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
           PERFORM  1000-OPEN-FILES         THRU 1000-EXIT.                     
           PERFORM  2000-MAIN-ROUTINE       THRU 2000-EXIT.                     
           PERFORM  9000-CLOSE-FILES        THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13FA20                                                 
                OUTPUT BP13FB61.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE                         
           MOVE   WS-SYS-YY                TO    WS-EDITED-YY                   
                                                 WS-CURR-YY.                    
           MOVE   WS-SYS-MM                TO    WS-EDITED-MM                   
                                                 WS-CURR-MM.                    
           MOVE   WS-SYS-DD                TO    WS-EDITED-DD                   
                                                 WS-CURR-DD.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
      ******************************************************************        
           PERFORM  3000-BP13FA20-READ     THRU 3000-EXIT.                      
           PERFORM  4000-PROCESS-FA20-REC  THRU 4000-EXIT                       
              UNTIL WS-FA20-EOF-SW = 'Y'.                                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-BP13FA20-READ.                                                      
      ******************************************************************        
           READ BP13FA20                                                        
              AT END MOVE 'Y'     TO WS-FA20-EOF-SW                             
              GO TO  3000-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD +1                 TO WS-CNT-FA20.                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      **********************************************************                
       4000-PROCESS-FA20-REC.                                                   
      **********************************************************                
                                                                                
           IF FA20-NUM-PRJCT     = SPACE OR LOW-VALUES                          
              PERFORM 3000-BP13FA20-READ THRU 3000-EXIT                         
              GO TO 4000-EXIT.                                                  
                                                                                
           PERFORM 4300-FA61-WRITE          THRU 4300-EXIT.                     
           PERFORM 3000-BP13FA20-READ       THRU 3000-EXIT.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************************************************                
       4300-FA61-WRITE.                                                         
      **********************************************************                
                                                                                
           PERFORM  8000-MOVE-DELIMITER   THRU 8000-EXIT.                       
                                                                                
           MOVE FA20-NUM-PRJCT              TO                                  
                WS-EQL-VALUE.                                                   
           PERFORM  5000-MOVE-EQUALS      THRU 5000-EXIT VARYING WS-CTR         
              FROM 1 BY 1 UNTIL WS-CTR > 3.                                     
           MOVE WS-EQL-VALUE                TO                                  
                FB61-NUM-PRJCT.                                                 
                                                                                
           MOVE FA20-NUM-BLK                TO                                  
                WS-EQL-VALUE.                                                   
           PERFORM  5000-MOVE-EQUALS      THRU 5000-EXIT VARYING WS-CTR         
              FROM 1 BY 1 UNTIL WS-CTR > 5.                                     
           MOVE WS-EQL-VALUE                TO                                  
                FB61-NUM-BLK.                                                   
                                                                                
           MOVE FA20-NUM-MAX-CAT1           TO                                  
                FB61-NUM-MAX-CAT1.                                              
           MOVE FA20-NUM-MAX-CAT2           TO                                  
                FB61-NUM-MAX-CAT2.                                              
           MOVE FA20-NUM-MAX-CAT3           TO                                  
                FB61-NUM-MAX-CAT3.                                              
           MOVE FA20-NUM-TOT-CAT1           TO                                  
                FB61-NUM-TOT-CAT1.                                              
           MOVE FA20-NUM-TOT-CAT2           TO                                  
                FB61-NUM-TOT-CAT2.                                              
           MOVE FA20-NUM-TOT-CAT3           TO                                  
                FB61-NUM-TOT-CAT3.                                              
           MOVE WS-CURRENT-DATE             TO                                  
                FB61-DTE-UPDATE.                                                
                                                                                
           WRITE BP13FB61-REC.                                                  
           ADD +1                 TO WS-CNT-FB61.                               
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       5000-MOVE-EQUALS.                                                        
      *-----------------------------------------------------------*             
                                                                                
           IF WS-EQL-VALUE(WS-CTR:1) = SPACES OR LOW-VALUES                     
              MOVE '='       TO  WS-EQL-VALUE(WS-CTR:1)                         
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------*             
       8000-MOVE-DELIMITER.                                                     
      *-----------------------------------------------------------*             
                                                                                
           MOVE ';'                      TO FB61-FIL01                          
                                            FB61-FIL02                          
                                            FB61-FIL03                          
                                            FB61-FIL04                          
                                            FB61-FIL05                          
                                            FB61-FIL06                          
                                            FB61-FIL07                          
                                            FB61-FIL08.                         
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       9000-CLOSE-FILES.                                                        
      **********************************************************                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C02F'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FA20 RECORDS READ..........: '                    
                    WS-CNT-FA20.                                                
           DISPLAY 'NO OF BP13FB61 RECORDS WRITTEN.......: '                    
                    WS-CNT-FB61.                                                
                                                                                
           CLOSE BP13FA20                                                       
                 BP13FB61.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
