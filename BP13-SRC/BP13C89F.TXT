       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C89F.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  01/02/2010.                                               
      * ========================================================= *             
      *  SYSTEM OF COMMITMENT (BP13)                              *             
      * ========================================================= *             
      *  OBJECTIVE  :  PRINTING OF NPL APPLICATION FORMS          *             
      *             :  BATCH JOB TRIGGERED IN BP13M205 OPTION 11  *             
      *                                                           *             
      *  INPUT      :  1.  BP13F740 - BP13.F740.MATUREBA          *             
      *                2.  BP13K060 - BP13.K060.CODETAB           *             
      *                3.  BP13K062 - BP13.K062.CODETBL           *             
      *                4.  BP13K757 - BP13.K757.BTO               *             
      *                5.  BP13K820 - BP13.K820.OCCUP             *             
      *                                                           *             
      *  OUTPUT     :  1.  P13P89DA - APPLICATION LETTER          *             
      *                2.  P13L89DA - INTERNET CASES W/ PPO       *             
      *                3.  P13L89DB - CONTROL LIST FOR INTERNET   *             
      *                               APPLICATION AND PPO         *             
      *                                                           *             
      * ========================================================= *             
      *  CHG REF  OIC   DATE        DESCRIPTION                   *             
      *  -------- ----  ----------  ----------------------------  *             
      *  BP133813 ESA1  01/02/2010  NEW PROGRAM                   *             
      *  BP135799 ESA1  11/05/2015  TO REPLACE BP13F740 W/ BP13F200             
      *  BP139280 EAA2  07/11/2023  EXPAND BP13K757 FROM 3000 TO 4000           
      * ========================================================= *             
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F200     ASSIGN       TO BP13F200.                        
                                                                                
           SELECT BP13K060     ASSIGN       TO BP13K060                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS K060-KEY-FLD                     
                               FILE STATUS  IS WS-K060-STATUS.                  
                                                                                
           SELECT BP13K062     ASSIGN       TO BP13K062                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS K062-KEY-FLD                     
                               FILE STATUS  IS WS-K062-STATUS.                  
                                                                                
           SELECT BP13K757     ASSIGN       TO BP13K757                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS K757-KEY-FLD                     
                               FILE STATUS  IS WS-K757-STATUS.                  
                                                                                
           SELECT BP13K820     ASSIGN       TO BP13K820                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS DYNAMIC                          
                               RECORD KEY   IS K820-KEY-FLD                     
                               FILE STATUS  IS WS-K820-STATUS.                  
                                                                                
           SELECT P13P89DA     ASSIGN       TO P13P89DA.                        
                                                                                
           SELECT P13L89DA     ASSIGN       TO P13L89DA.                        
                                                                                
           SELECT P13L89DB     ASSIGN       TO P13L89DB.                        
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F200           IS GLOBAL                                        
                               RECORD CONTAINS 300 CHARACTERS                   
                               LABEL RECORDS IS STANDARD                        
                               RECORDING MODE IS F.                             
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K060           IS GLOBAL                                        
                               RECORD CONTAINS 25 CHARACTERS.                   
                                                                                
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K062           IS GLOBAL                                        
                               RECORD CONTAINS 150 CHARACTERS.                  
                                                                                
       COPY BP13K062.                                                           
                                                                                
       FD   BP13K757           IS GLOBAL                                        
                               RECORD CONTAINS 4000 CHARACTERS.                 
                                                                                
       COPY BP13K757.                                                           
                                                                                
       FD   BP13K820           IS GLOBAL                                        
                               RECORD CONTAINS 400 CHARACTERS.                  
                                                                                
       COPY BP13K820.                                                           
                                                                                
       FD   P13P89DA           IS GLOBAL                                        
                               RECORD CONTAINS 100 CHARACTERS                   
                               LABEL RECORDS IS STANDARD                        
                               RECORDING MODE IS  F.                            
       01   P13P89DA-REC                 PIC X(100) GLOBAL.                     
                                                                                
       FD   P13L89DA           IS GLOBAL                                        
                               RECORD CONTAINS 132 CHARACTERS                   
                               LABEL RECORDS IS STANDARD                        
                               RECORDING MODE IS F.                             
       01   P13L89DA-REC                 PIC X(132) GLOBAL.                     
                                                                                
       FD   P13L89DB           IS GLOBAL                                        
                               RECORD CONTAINS 132  CHARACTERS                  
                               LABEL RECORDS IS STANDARD                        
                               RECORDING MODE IS F.                             
       01   P13L89DB-REC                 PIC X(132) GLOBAL.                     
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-SWITCHES.                                                    
           05  WS-F200-EOF               PIC X(01)  VALUE 'N'.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F200-READ-CNT          PIC 9(06)  VALUE ZEROES.               
           05  WS-K757-FND-CNT           PIC 9(06)  VALUE ZEROES.               
           05  WS-K757-NFND-CNT          PIC 9(06)  VALUE ZEROES.               
                                                                                
       01  WS-FILE-STATUS-G GLOBAL.                                             
           05  WS-K060-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K062-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K757-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K820-STATUS            PIC 9(02)  VALUE ZEROES.               
                                                                                
       01  WS-VARIABLES-G GLOBAL.                                               
           05  WS-CURRENT-DATE           PIC X(08)  VALUE SPACES.               
           05  WS-CNT-WRITE-L89DA        PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-WRITE-L89DB        PIC 9(08)  VALUE ZEROES.               
           05  WS-TOT-LET-PRINT          PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-LINE-ON-PAGE-A     PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-LINE-ON-PAGE-B     PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-PRINT-L89DA        PIC 9(08)  VALUE 1.                    
           05  WS-CNT-PRINT-L89DB        PIC 9(08)  VALUE 1.                    
           05  WS-NO-OF-PAGE-A           PIC 9(08)  VALUE ZEROES.               
           05  WS-NO-OF-PAGE-B           PIC 9(08)  VALUE ZEROES.               
           05  WS-CURR-DATE              PIC X(10)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ************************************************************              
       0000-MAIN.                                                               
      ************************************************************              
           PERFORM 1000-OPEN-ROUTINE   THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F200  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-RECORD THRU 3000-EXIT                           
             UNTIL WS-F200-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ************************************************************              
       1000-OPEN-ROUTINE.                                                       
      ************************************************************              
           OPEN INPUT  BP13F200                                                 
                       BP13K060                                                 
                       BP13K820                                                 
                       BP13K757                                                 
                OUTPUT P13P89DA                                                 
                       P13L89DA                                                 
                       P13L89DB.                                                
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K060 OPEN ERROR, STATUS ' WS-K060-STATUS             
              MOVE WS-K060-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K062-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K062 OPEN ERROR, STATUS ' WS-K062-STATUS             
              MOVE WS-K062-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 OPEN ERROR, STATUS ' WS-K820-STATUS             
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K757-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K757 OPEN ERROR, STATUS ' WS-K757-STATUS             
              MOVE WS-K757-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
                                                                                
           STRING WS-CURRENT-DATE(7:2) '/'                                      
                  WS-CURRENT-DATE(5:2) '/'                                      
                  WS-CURRENT-DATE(1:4)                                          
                  DELIMITED BY SIZE                                             
             INTO WS-CURR-DATE.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       2000-READ-BP13F200.                                                      
      ************************************************************              
           READ BP13F200                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F200-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F200-READ-CNT                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       3000-PROCESS-RECORD.                                                     
      ************************************************************              
           PERFORM 4000-APPLN-HSELOAN-LET     THRU 4000-EXIT.                   
           PERFORM 2000-READ-BP13F200 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       4000-APPLN-HSELOAN-LET.                                                  
      ************************************************************              
           MOVE SPACES                   TO BP13K757-REC.                       
           INITIALIZE                       BP13K757-REC.                       
                                                                                
           MOVE F200-NUM-REGN            TO K757-NUM-REGN.                      
                                                                                
           READ BP13K757.                                                       
                                                                                
           EVALUATE WS-K757-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K757-FND-CNT                     
                    CALL 'BP13CR29'                                             
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K757-NFND-CNT                    
                    DISPLAY 'REGN NO. NOT FOUND IN BP13K757. REGN = '           
                            F200-NUM-REGN                                       
               WHEN OTHER                                                       
                    DISPLAY 'READ ERROR BP13K757, STATUS '                      
                            WS-K757-STATUS                                      
                    MOVE WS-K757-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       9000-CLOSE-ROUTINE.                                                      
      ************************************************************              
           DISPLAY ' '.                                                         
           DISPLAY '- BP13C89F CONTROL TOTAL --------------'.                   
           DISPLAY '                RUN DATE : ' WS-CURR-DATE.                  
           DISPLAY '---------------------------------------'.                   
           DISPLAY ' NO OF RECS READ        (F200): ' WS-F200-READ-CNT.         
           DISPLAY ' NO OF RECS FOUND       (K757): ' WS-K757-FND-CNT.          
           DISPLAY ' NO OF RECS NOT FOUND   (K757): ' WS-K757-NFND-CNT.         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F200                                                       
                 BP13K060                                                       
                 BP13K062                                                       
                 BP13K820                                                       
                 BP13K757                                                       
                 P13P89DA                                                       
                 P13L89DA                                                       
                 P13L89DB.                                                      
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K060, STATUS ' WS-K060-STATUS            
              MOVE WS-K060-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K062-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K062, STATUS ' WS-K062-STATUS            
              MOVE WS-K060-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K820, STATUS ' WS-K820-STATUS            
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K757-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K757, STATUS ' WS-K757-STATUS            
              MOVE WS-K757-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       COPY BP13CR29.                                                           
                                                                                
       END PROGRAM BP13C89F.                                                    
