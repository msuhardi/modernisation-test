       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C20E.                                                 
      *AUTHOR.        MTD.                                                      
      *DATE-WRITTEN.  21MAR2002.                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVE    :  1. TO PRINT INVITATION LETTERS TO           *           
      *                    APPLICANT INVITING THEM FOR A SELECTION  *           
      *                 2. FOR PUBLIC CASE NEEDS 2 COPIES, FOR RH   *           
      *                    CASE AND SITTING TENANT NEEDS 4 COPIES   *           
      *                                                             *           
      * CALLING MODULE: BP13C245                                    *           
      *                                                             *           
      * PARAMETERS: F200-RECORD                                     *           
      *             P13SC245 (COPYBOOK) - C245-PARAMETERS           *           
      *                                                             *           
      * OUTPUT LISTS :  1.  P13L245A - INVITATION LETTER            *           
      *                 2.  P13L245D - INVITATION LETTER (COPY)     *           
      *                 4.  P13L245C - ADDITIONAL RH LETTER         *           
      *                 5.  P13L245E - ADDITIONAL RH LETTER (COPY)  *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * **** THIS CALLED MODULE WAS EXTRACTED FROM BP13C245    **** *           
      * **** PRINTS WS-LETTER-TYPE = 'E' (SERS)                **** *           
      * ----------------------------------------------------------- *           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      * -------- ---  ------  ------------------------------------- *           
      * BP132160 CKK  290702  TO ADD ISO TAG                        *           
      * BP132500 JF4  290304  CATER FOR CHANGES TO LETTER           *           
      * BP132500 MTD  050504  ADD LOAN-TAG                          *           
      * BP133396 LMS  180808  ENHANCEMENT TO FORMS BP13CE AND BP13CF*           
      * BP133703 ESA1 100909  REMOVE 'COPY' IN THE LETTERS          *           
      * BP133816 ESA1 210110  TO ADD PRINTING OF REGN NUM & DATE IN *           
      *                       ANNEX 1                               *           
      * BP134838 ESA1 090513  TO CATER FOR REN INVITATION LETTER    *           
      * BP134755 ESA1 060613  TO CATER FOR WS-C245-NUM-ALLO-CAT     *           
      * BP135533 ESA1 131114  TO CATER FOR ALLOC SCHEMES            *           
      *                       'REG' & 'RNG'                         *           
      * =========================================================== *           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT P13L245A  ASSIGN       TO P13L245A.                           
           SELECT P13L245D  ASSIGN       TO P13L245D.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   P13L245A                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   P13L245A-REC                PIC X(132).                             
                                                                                
       FD   P13L245D                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   P13L245D-REC                PIC X(132).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       COPY BP13F200.                                                           
                                                                                
       01  WS-C908-PARAMETERS.                                                  
           05  WS-INPUT-PARAMETERS.                                             
               10  WS-C908-TXT-IN-SIZE       PIC 9(2).                          
               10  WS-C908-TXT-INPUT         PIC X(30).                         
           05  WS-OUTPUT-PARAMETERS.                                            
               10  WS-C908-TXT-OUT-SIZE      PIC 9(2).                          
               10  WS-C908-TXT-OUTPUT        PIC X(40).                         
           05  WS-C908-RETCODE               PIC S9(04)  COMP.                  
                                                                                
       01  WS-LOAN-TAG                  PIC X(02)  VALUE SPACES.                
       01  WS-CDE-JB                    PIC X(03)  VALUE SPACES.                
       01  WS-CDE-LOW-FLOOR             PIC X(03)  VALUE SPACES.                
                                                                                
       01  L245A-LINE-BARCODE.                                                  
           05  FILLER                   PIC X(66)  VALUE SPACES.                
           05  L245A-BARCODE-REGN       PIC X(13)  VALUE SPACES.                
                                                                                
       01  L245A-LINE-BARCODE1.                                                 
           05  FILLER                   PIC X(75)  VALUE SPACES.                
           05  L245A-BARCODE1-PREFIX    PIC X(2)   VALUE SPACES.                
           05  L245A-BARCODE1-REGN      PIC X(8)   VALUE SPACES.                
                                                                                
       01  WS-L245A-PRT-VARS.                                                   
           05  L245A-PRT-HDG-1.                                                 
               10  FILLER               PIC X(25)  VALUE SPACES.                
               10  L245A-NT             PIC X(3)   VALUE SPACES.                
               10  L245A-ROOM           PIC X(2)   VALUE SPACES.                
               10  FILLER               PIC X      VALUE '/'.                   
               10  L245A-REGN-NO        PIC X(8)   VALUE SPACES.                
               10  L245A-ALLOC-SCH      PIC X(7)   VALUE SPACES.                
               10  L245A-REF-GRP REDEFINES L245A-ALLOC-SCH.                     
                   15  L245A-REF-DATA   PIC X(7).                               
                                                                                
           05  L245A-PRT-HDG-2-QUE.                                             
               10  FILLER               PIC X(25)  VALUE SPACES.                
               10  L245A-Q-NO           PIC X(5)   VALUE SPACES.                
                                                                                
           05  L245A-PRT-HDG-2.                                                 
               10  FILLER               PIC X(25)  VALUE SPACES.                
               10  L245A-DATE           PIC X(10)  VALUE SPACES.                
                                                                                
           05  L245A-PRT-HDG-3.                                                 
               10  FILLER               PIC X(26)  VALUE SPACES.                
               10  L245A-SELECT-ROOM    PIC X(9)   VALUE SPACES.                
               10  FILLER               PIC X(6)   VALUE SPACES.                
               10  L245A-NT-ZONE-NAME   PIC X(20)  VALUE SPACES.                
               10  FILLER               PIC X(11)  VALUE SPACES.                
               10  L245A-COPY           PIC X(8).                               
                                                                                
           05  L245A-PRT-HDG-4.                                                 
               10  FILLER               PIC X(45)  VALUE SPACES.                
               10  L245A-DESC-FT        PIC X(22)  VALUE SPACES.                
                                                                                
           05  L245A-PRT-HDG-5.                                                 
               10  FILLER                    PIC X(10)   VALUE SPACES.          
               10  L245A-APPMT-DATE          PIC X(10)  VALUE SPACES.           
               10  FILLER                    PIC X(03)  VALUE SPACES.           
               10  L245A-APPMT-HR            PIC Z9.                            
               10  L245A-APPMT-HR-R      REDEFINES L245A-APPMT-HR               
                                             PIC X(2).                          
               10  FILLER                    PIC X      VALUE ':'.              
               10  L245A-APPMT-MIN           PIC X(2)   VALUE SPACES.           
               10  FILLER                    PIC X      VALUE SPACES.           
               10  L245A-AM-PM               PIC X(2)   VALUE SPACES.           
                                                                                
           05  L245A-PRT-HDG-6.                                                 
               10  FILLER               PIC X(62)  VALUE SPACES.                
               10  L245A-BOOKFEE        PIC X(14)  VALUE SPACES.                
                                                                                
           05  L245A-PRT-HDG-7.                                                 
               10  FILLER               PIC X(36)  VALUE SPACES.                
               10  L245A-DESC           PIC X(7)   VALUE SPACES.                
               10  FILLER               PIC X(14)  VALUE SPACES.                
               10  L245A-PERCENT        PIC 9(2)   VALUE ZEROES.                
                                                                                
           05  L245A-PRT-HDG-9.                                                 
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L245A-APPLN-NAME     PIC X(66)  VALUE SPACES.                
                                                                                
           05  L245A-PRT-HDG-10.                                                
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L245A-COAPPLN-NAME   PIC X(66)  VALUE SPACES.                
                                                                                
           05  L245A-PRT-HDG-11.                                                
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L245A-BLK-NO         PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(2)   VALUE SPACES.                
               10  L245A-NUM-FLAT.                                              
                   15  L245A-CHAR       PIC X      VALUE SPACE.                 
                   15  L245A-LEVEL      PIC X(2)   VALUE SPACES.                
                   15  L245A-DASH       PIC X      VALUE SPACE.                 
                   15  L245A-HSE-MAIN   PIC X(4)   VALUE SPACES.                
                   15  L245A-HSE-SUB    PIC X(3)   VALUE SPACES.                
                                                                                
           05  L245A-PRT-HDG-12.                                                
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L245A-STREET-NAME    PIC X(65)  VALUE SPACES.                
                                                                                
           05  L245A-PRT-HDG-13.                                                
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L245A-CODE           PIC X(16)  VALUE SPACES.                
                                                                                
           05  L245A-PRT-HDG-14.                                                
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(32)  VALUE                        
                   'CC TO RESETTLEMENT SECTION, LAD'.                           
                                                                                
           05  L245A-PRT-HDG-15.                                                
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(22)  VALUE                        
                   'CC TO BRANCH OFFICE - '.                                    
               10  L245A-BO             PIC X(2)   VALUE SPACES.                
                                                                                
           05  L245A-PRT-HDG-16.                                                
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(32)  VALUE                        
                   'CC TO EXEC ADMIN OFFICER, URA  '.                           
                                                                                
           05  L245A-PRT-NME-OIC.                                               
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L245A-NME-OIC        PIC X(40)  VALUE SPACES.                
                                                                                
           05  L245A-PRT-OIC-DESIG.                                             
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L245A-OIC-DESIG      PIC X(40)  VALUE SPACES.                
                                                                                
                                                                                
      *------------------------------------------------------------*            
      *  LETTER LAYOUT FOR BP13CF                                  *            
      *------------------------------------------------------------*            
          05 L245-DOC1-LN-1.                                                    
             10 FILLER                     PIC X(75)  VALUE SPACES.             
             10 L245-DOC1-REGN             PIC X(08)  VALUE SPACES.             
                                                                                
          05 L245-DOC1-LN-2.                                                    
             10 FILLER                     PIC X(67)  VALUE SPACES.             
             10 L245-DOC1-DATE             PIC X(10)  VALUE SPACES.             
                                                                                
      *------------------------------------------------------------*            
      *  SEND DJDE LINE FOR INSTRUCTION PAGE                       *            
      *------------------------------------------------------------*            
       01  DJDE-LINE-INSTRUCTION-SERS     PIC X(80)  VALUE                      
           '$DJDE$ FORMS=BP13CF,END;'.                                          
                                                                                
      *------------------------------------------------------------*            
      *  DJDE LINE FOR SERS                                        *            
      *------------------------------------------------------------*            
       01  DJDE-LINE-SERS                 PIC X(80)  VALUE                      
           '$DJDE$ JDE=13C24X,FORMS=BP13CE,COPIES=1,COLLATE=YES,DUPLEX=N        
      -    'O,JDL=BP13,END;'.                                                   
                                                                                
      *------------------------------------------------------------*            
      *  DJDE LINE FOR REN                                         *            
      *------------------------------------------------------------*            
       01  DJDE-LINE-REN                  PIC X(80)  VALUE                      
           '$DJDE$ JDE=13C24X,FORMS=BP13CG,COPIES=1,COLLATE=YES,DUPLEX=N        
      -    'O,JDL=BP13,END;'.                                                   
                                                                                
      *------------------------------------------------------------*            
       LINKAGE SECTION.                                                         
      *------------------------------------------------------------*            
       01  WS-F200-RECORD                 PIC X(300).                           
       COPY P13SC245.                                                           
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION     USING WS-F200-RECORD                              
                                    WS-C245-PARAMETERS.                         
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-FILES-ROUTINE   THRU  1000-EXIT.                  
            PERFORM 2000-INITIALIZE-ROUTINE   THRU  2000-EXIT.                  
            PERFORM 3000-MOVE-DETAILS         THRU  3000-EXIT.                  
            PERFORM 6400-PRINT-LETTER         THRU  6400-EXIT.                  
            PERFORM 9000-CLOSE-FILES-ROUTINE  THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           GOBACK.                                                              
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES-ROUTINE.                                                 
      *-------------------------------------------------------------            
           OPEN  OUTPUT P13L245A                                                
                        P13L245D.                                               
                                                                                
           IF WS-C245-FIRST-FLAG = 'Y'                                          
              MOVE SPACES TO P13L245A-REC                                       
              WRITE P13L245A-REC AFTER PAGE                                     
              MOVE SPACES TO P13L245D-REC                                       
              WRITE P13L245D-REC AFTER PAGE                                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-INITIALIZE-ROUTINE.                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO P13L245A-REC                                          
                          P13L245D-REC.                                         
                                                                                
           MOVE WS-F200-RECORD   TO BP13F200-REC.                               
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-MOVE-DETAILS.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3100-GET-REGN-CHKDIGIT  THRU  3100-EXIT.                     
                                                                                
           MOVE F200-NUM-REGN        TO L245A-BARCODE1-REGN.                    
           MOVE 'SL'                 TO L245A-BARCODE1-PREFIX.                  
                                                                                
           MOVE F200-NUM-REGN        TO L245A-REGN-NO.                          
      *    MOVE WS-C245-ALLOC-SCH    TO L245A-ALLOC-SCH.                        
           MOVE F200-NUM-NT-ZONE     TO L245A-NT.                               
           MOVE WS-C245-ROOM         TO L245A-ROOM.                             
                                                                                
           MOVE WS-C245-Q-NO           TO L245A-Q-NO.                           
           MOVE WS-C245-PRT-DATE       TO L245A-DATE.                           
                                                                                
           MOVE WS-C245-SELECT-ROOM  TO L245A-SELECT-ROOM.                      
           MOVE WS-C245-NT-ZONE-NAME TO L245A-NT-ZONE-NAME.                     
                                                                                
           MOVE WS-C245-APPMT-DATE      TO L245A-APPMT-DATE.                    
           MOVE WS-C245-APPMT-HR        TO L245A-APPMT-HR.                      
           MOVE WS-C245-APPMT-MIN       TO L245A-APPMT-MIN.                     
           MOVE WS-C245-APPMT-AM-PM     TO L245A-AM-PM.                         
                                                                                
           MOVE WS-C245-PERCENT-DESC  TO L245A-DESC.                            
           MOVE WS-C245-PERCENT       TO L245A-PERCENT.                         
                                                                                
           MOVE WS-C245-APPLN-NAME    TO L245A-APPLN-NAME.                      
           MOVE WS-C245-COAPPLN-NAME  TO L245A-COAPPLN-NAME.                    
                                                                                
           MOVE WS-C245-BLK-NO        TO L245A-BLK-NO.                          
           MOVE WS-C245-NUM-FLAT      TO L245A-NUM-FLAT.                        
           MOVE WS-C245-STREET-NAME   TO L245A-STREET-NAME.                     
           MOVE WS-C245-CODE          TO L245A-CODE.                            
                                                                                
           MOVE WS-C245-BO            TO L245A-BO.                              
           MOVE WS-C245-NME-OIC       TO L245A-NME-OIC.                         
           MOVE WS-C245-OIC-DESIG     TO L245A-OIC-DESIG.                       
                                                                                
           MOVE  SPACES               TO WS-CDE-JB                              
                                         WS-CDE-LOW-FLOOR                       
                                         WS-LOAN-TAG                            
                                         L245A-REF-DATA.                        
           IF WS-C245-NUM-LOAN-TAG = 'B'                                        
              MOVE   '/B'             TO WS-LOAN-TAG                            
           END-IF.                                                              
           IF WS-C245-NUM-JB-MTF = '8'                                          
              MOVE '/JB'              TO WS-CDE-JB                              
           END-IF.                                                              
           IF WS-C245-NUM-LOW-FLOOR = 'G'                                       
              MOVE '/G'               TO WS-CDE-LOW-FLOOR                       
           ELSE                                                                 
              IF WS-C245-NUM-LOW-FLOOR = 'Y'                                    
                 MOVE '/Y'            TO WS-CDE-LOW-FLOOR                       
              END-IF                                                            
           END-IF.                                                              
           STRING WS-LOAN-TAG                                                   
                  WS-CDE-JB                                                     
                  WS-CDE-LOW-FLOOR                                              
                  DELIMITED BY SPACE  INTO L245A-REF-DATA.                      
                                                                                
           EVALUATE WS-C245-ROOM                                                
              WHEN '2A'                                                         
                 MOVE 'STUDIO APARTMENT (SA)'  TO L245A-DESC-FT                 
                 MOVE ' 250   '                TO L245A-BOOKFEE                 
              WHEN '2 '                                                         
                 MOVE '2-RM FLAT            '  TO L245A-DESC-FT                 
                 MOVE ' 500   '                TO L245A-BOOKFEE                 
              WHEN '3 '                                                         
                 MOVE '3-RM FLAT            '  TO L245A-DESC-FT                 
                 MOVE ' 1,000 '                TO L245A-BOOKFEE                 
              WHEN '4 '                                                         
                 MOVE '4-RM FLAT            '  TO L245A-DESC-FT                 
                 MOVE ' 2,000 '                TO L245A-BOOKFEE                 
              WHEN '5 '                                                         
                 MOVE '5-RM FLAT            '  TO L245A-DESC-FT                 
                 MOVE ' 2,000 '                TO L245A-BOOKFEE                 
              WHEN OTHER                                                        
                 MOVE    SPACES                TO L245A-DESC-FT                 
                                                  L245A-BOOKFEE                 
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-GET-REGN-CHKDIGIT.                                                  
      *-------------------------------------------------------------            
                                                                                
           STRING 'SL' F200-NUM-REGN DELIMITED BY SIZE                          
             INTO  WS-C908-TXT-INPUT.                                           
                                                                                
           MOVE SPACES               TO WS-C908-TXT-OUTPUT.                     
           MOVE 10                   TO WS-C908-TXT-IN-SIZE.                    
           MOVE ZEROS                TO WS-C908-RETCODE                         
                                        WS-C908-TXT-OUT-SIZE.                   
                                                                                
           CALL 'BP13C908'    USING WS-C908-PARAMETERS.                         
                                                                                
           IF WS-C908-RETCODE = ZEROS                                           
              MOVE WS-C908-TXT-OUTPUT(1:WS-C908-TXT-OUT-SIZE) TO                
                   L245A-BARCODE-REGN                                           
           ELSE                                                                 
              DISPLAY 'ERROR IN BP13C908-REGN: ' WS-C908-TXT-INPUT              
              MOVE SPACES                 TO  L245A-BARCODE-REGN.               
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       6400-PRINT-LETTER.                                                       
      *----------------------------------------------------------*              
      *       PRINT 2 COPIES FOR PUBLIC CASES                    *              
      *----------------------------------------------------------*              
           PERFORM  6410-PR-LETTER-DETAIL-1  THRU   6410-EXIT.                  
                                                                                
      *----------------------------------------------------------*              
      *     PRINT INSTRUCTION PAGE FOR PUBLIC CASES              *              
      *----------------------------------------------------------*              
           WRITE P13L245A-REC FROM DJDE-LINE-INSTRUCTION-SERS                   
                          AFTER PAGE.                                           
           WRITE P13L245D-REC FROM DJDE-LINE-INSTRUCTION-SERS                   
                          AFTER PAGE.                                           
                                                                                
           MOVE  SPACES       TO    P13L245A-REC P13L245D-REC.                  
           MOVE WS-C245-REGN-NO     TO L245-DOC1-REGN.                          
           MOVE WS-C245-PRT-DATE    TO L245-DOC1-DATE.                          
           WRITE P13L245A-REC FROM L245-DOC1-LN-1 AFTER 5.                      
           WRITE P13L245D-REC FROM L245-DOC1-LN-1 AFTER 5.                      
           WRITE P13L245A-REC FROM L245-DOC1-LN-2 AFTER 2.                      
           WRITE P13L245D-REC FROM L245-DOC1-LN-2 AFTER 2.                      
                                                                                
                                                                                
      *----------------------------------------------------------*              
      *       FOR SERS CASES - NO LETTER FOR RESETTLEMENT AND    *              
      *               RELOCATION CASES IS PRINTED                *              
      *----------------------------------------------------------*              
                                                                                
       6400-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6410-PR-LETTER-DETAIL-1.                                                 
      *----------------------------------------------------------*              
      *     GET INFORMATION FOR PRINTING LETTER TO PUBLIC CASES  *              
      *----------------------------------------------------------*              
           IF WS-C245-NUM-ALLO-CAT = 'SER'                                      
              WRITE P13L245A-REC FROM DJDE-LINE-SERS  AFTER PAGE                
              WRITE P13L245D-REC FROM DJDE-LINE-SERS  AFTER PAGE                
           ELSE                                                                 
              WRITE P13L245A-REC FROM DJDE-LINE-REN   AFTER PAGE                
              WRITE P13L245D-REC FROM DJDE-LINE-REN   AFTER PAGE                
           END-IF.                                                              
                                                                                
           MOVE  SPACES       TO   P13L245A-REC P13L245D-REC.                   
                                                                                
      *    WRITE P13L245A-REC FROM L245A-LINE-BARCODE AFTER 1.                  
      *    WRITE P13L245D-REC FROM L245A-LINE-BARCODE AFTER 1.                  
                                                                                
      *    WRITE P13L245A-REC FROM L245A-LINE-BARCODE1 AFTER 1.                 
      *    WRITE P13L245D-REC FROM L245A-LINE-BARCODE1 AFTER 1.                 
                                                                                
           WRITE P13L245A-REC FROM L245A-PRT-HDG-1   AFTER 8.                   
           WRITE P13L245D-REC FROM L245A-PRT-HDG-1   AFTER 8.                   
                                                                                
           WRITE P13L245A-REC FROM L245A-PRT-HDG-2-QUE.                         
           WRITE P13L245D-REC FROM L245A-PRT-HDG-2-QUE.                         
                                                                                
           WRITE P13L245A-REC FROM L245A-PRT-HDG-2.                             
           WRITE P13L245D-REC FROM L245A-PRT-HDG-2.                             
                                                                                
      *----------------------------------------------------------*              
      *     PRINT NAME OF APPLICANT AND ADDRESS                  *              
      *----------------------------------------------------------*              
           WRITE P13L245A-REC FROM L245A-PRT-HDG-9 AFTER 3.                     
           WRITE P13L245D-REC FROM L245A-PRT-HDG-9 AFTER 3.                     
                                                                                
           WRITE P13L245A-REC FROM L245A-PRT-HDG-10 AFTER 1.                    
           WRITE P13L245D-REC FROM L245A-PRT-HDG-10 AFTER 1.                    
           WRITE P13L245A-REC FROM L245A-PRT-HDG-11 AFTER 1.                    
           WRITE P13L245D-REC FROM L245A-PRT-HDG-11 AFTER 1.                    
           WRITE P13L245A-REC FROM L245A-PRT-HDG-12 AFTER 1.                    
           WRITE P13L245D-REC FROM L245A-PRT-HDG-12 AFTER 1.                    
           WRITE P13L245A-REC FROM L245A-PRT-HDG-13 AFTER 1.                    
           WRITE P13L245D-REC FROM L245A-PRT-HDG-13 AFTER 1.                    
                                                                                
      *    MOVE  SPACES      TO    L245A-COPY.                                  
      *    WRITE P13L245A-REC FROM L245A-PRT-HDG-3   AFTER 7.                   
           MOVE  SPACES       TO    L245A-PRT-HDG-3.                            
           WRITE P13L245A-REC FROM L245A-PRT-HDG-3   AFTER 3.                   
           MOVE  'C O P Y'    TO    L245A-COPY.                                 
           WRITE P13L245D-REC FROM L245A-PRT-HDG-3   AFTER 3.                   
                                                                                
           WRITE P13L245A-REC FROM L245A-PRT-HDG-4    AFTER 6.                  
           WRITE P13L245D-REC FROM L245A-PRT-HDG-4    AFTER 6.                  
                                                                                
           IF WS-C245-NUM-ALLO-CAT = 'SER'                                      
              WRITE P13L245A-REC FROM L245A-PRT-HDG-5 AFTER 4                   
              WRITE P13L245D-REC FROM L245A-PRT-HDG-5 AFTER 4                   
           END-IF.                                                              
                                                                                
           IF WS-C245-NUM-ALLO-CAT = 'REN'                                      
              WRITE P13L245A-REC FROM L245A-PRT-HDG-5 AFTER 3                   
              WRITE P13L245D-REC FROM L245A-PRT-HDG-5 AFTER 3                   
              WRITE P13L245A-REC FROM L245A-PRT-HDG-6 AFTER 12                  
              WRITE P13L245D-REC FROM L245A-PRT-HDG-6 AFTER 12                  
           END-IF.                                                              
                                                                                
      *    WRITE P13L245A-REC FROM L245A-PRT-HDG-7    AFTER 15.                 
      *    WRITE P13L245D-REC FROM L245A-PRT-HDG-7    AFTER 15.                 
                                                                                
      *    WRITE P13L245A-REC  FROM L245A-PRT-NME-OIC AFTER 26.                 
      *    WRITE P13L245D-REC  FROM L245A-PRT-NME-OIC AFTER 26.                 
                                                                                
      *    WRITE P13L245A-REC  FROM L245A-PRT-OIC-DESIG AFTER 1.                
      *    WRITE P13L245D-REC  FROM L245A-PRT-OIC-DESIG AFTER 1.                
                                                                                
           IF WS-C245-NUM-ALLO-CAT = 'SER'                                      
              WRITE P13L245A-REC FROM L245A-LINE-BARCODE AFTER 29               
              WRITE P13L245D-REC FROM L245A-LINE-BARCODE AFTER 29               
           ELSE                                                                 
              WRITE P13L245A-REC FROM L245A-LINE-BARCODE AFTER 18               
              WRITE P13L245D-REC FROM L245A-LINE-BARCODE AFTER 18               
           END-IF.                                                              
                                                                                
           WRITE P13L245A-REC FROM L245A-LINE-BARCODE1 AFTER 1.                 
           WRITE P13L245D-REC FROM L245A-LINE-BARCODE1 AFTER 1.                 
                                                                                
           IF (WS-C245-NUM-ALLO-SCHEME    = 'RHO' OR 'RH1' OR 'RHA' OR          
              'RHP' OR 'REN' OR 'RHL' OR 'RHS' OR 'RJO' OR 'RJ1' OR             
              'REA' OR 'REE' OR 'REL' OR 'RES' OR 'RDC' OR 'URA' OR             
              'URL' OR 'URO' OR 'URP' OR 'URS' OR 'RED' OR 'REG' OR             
              'RNG')                                                            
               PERFORM 6430-PRINT-CC-STATEMENT-1   THRU 6430-EXIT               
           ELSE                                                                 
               MOVE SPACES  TO P13L245A-REC  P13L245D-REC                       
               WRITE P13L245A-REC                                               
               WRITE P13L245D-REC                                               
           END-IF.                                                              
                                                                                
       6410-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6430-PRINT-CC-STATEMENT-1.                                               
      *-------------------------------------------------------------            
           IF WS-C245-NUM-ALLO-SCHEME = 'RHO' OR 'RH1' OR 'RHA' OR 'RHP'        
                                 OR 'RHL' OR 'RHS' OR 'RJO' OR 'RJ1'            
              WRITE P13L245A-REC FROM L245A-PRT-HDG-14 AFTER 1                  
              WRITE P13L245D-REC FROM L245A-PRT-HDG-14 AFTER 1                  
           ELSE                                                                 
           IF WS-C245-NUM-ALLO-SCHEME = 'URA' OR 'URL' OR 'URO' OR              
                                     'URP' OR 'URS' OR 'RDC'                    
              WRITE P13L245A-REC FROM L245A-PRT-HDG-16 AFTER 1                  
              WRITE P13L245D-REC FROM L245A-PRT-HDG-16 AFTER 1                  
           ELSE                                                                 
              WRITE P13L245A-REC FROM L245A-PRT-HDG-15 AFTER 1                  
              WRITE P13L245D-REC FROM L245A-PRT-HDG-15 AFTER 1.                 
                                                                                
       6430-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES-ROUTINE.                                                
      *-------------------------------------------------------------            
            CLOSE P13L245A                                                      
                  P13L245D.                                                     
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
