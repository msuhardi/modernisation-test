       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C715.                                                 
      *AUTHOR.        YCH.                                                      
      *DATE-WRITTEN.  30/11/96.                                                 
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1.  USE EX-LESSEE NRIC TO MATCH RESALE HISTORY    *                
      *          FILE, IF FOUND, EXTRACT DATE SOLD TO SELLER   *                
      *          AND COMPUTE THE LENGTH OCCUPIED               *                
      *                                                        *                
      *     SIMILAR LOGIC AS BP13C715, EXCEPT LENGTH OF        *                
      *     OCCUPATION IS BASED ON DTE SOLD TO SELLER          *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP130219 YCH2 301196  PROFILE OF APPLN IN WAITING LIST *                
      * BP130261 YCH2 050497  BREAKDOWN OF RFS APPLN           *                
      * BP130292 YCH2 260497  INCLUDE RESALE STATUS            *                
      *                       BYPASS RESALE STATUS = 'GS '     *                
      * BP130171 SSS  070198  INCLUDE RESALE STATUS            *                
      * BP130630 NOR  140199  CHGE F715 F716 EXPAND TO 350     *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F716   ASSIGN TO BP13F716.                                
                                                                                
           SELECT BB24K510  ASSIGN TO BB24K510                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K510-KEY-FLD                              
                  FILE STATUS      IS BB24K510-STATUS.                          
                                                                                
           SELECT BB24K500  ASSIGN TO BB24K500                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS RANDOM                                    
                  RECORD KEY       IS K500-KEY-FLD                              
                  FILE STATUS      IS BB24K500-STATUS.                          
                                                                                
           SELECT P13F716O  ASSIGN TO P13F716O.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F716.                                                           
                                                                                
       FD   P13F716O                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F716O-REC            PIC X(350).                                 
                                                                                
       FD   BB24K500                                                            
            RECORD CONTAINS 1700 CHARACTERS.                                    
       COPY BB24K500.                                                           
                                                                                
       FD   BB24K510                                                            
            RECORD CONTAINS 50 CHARACTERS.                                      
       COPY BB24K510.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  BB24K500-STATUS          PIC 9(2)    VALUE ZEROES.                   
       01  BB24K510-STATUS          PIC 9(2)    VALUE ZEROES.                   
                                                                                
       01  WS-K510-READ             PIC 9(8)    VALUE 0.                        
       01  WS-K500-READ             PIC 9(8)    VALUE 0.                        
       01  WS-F716-READ             PIC 9(8)    VALUE 0.                        
       01  WS-OUTPUT-CNT            PIC 9(8)    VALUE 0.                        
       01  WS-BYPASS                PIC 9(8)    VALUE 0.                        
       01  WS-MR-NRIC               PIC 9(8)    VALUE 0.                        
       01  WS-NMR-NRIC              PIC 9(8)    VALUE 0.                        
       01  WS-BYPASS-GS             PIC 9(8)    VALUE 0.                        
       01  WS-K510-NOTFD            PIC X       VALUE 'N'.                      
       01  WS-REQUIRE-CHK           PIC X       VALUE 'N'.                      
       01  WS-OCCP-MTH              PIC 9(4)    VALUE 0.                        
                                                                                
       01  WS-HDB-REF              PIC X(11)    VALUE SPACES.                   
       01  WS-CDE-RESALE-STATUS    PIC X(3)     VALUE SPACES.                   
                                                                                
       01  WS-SOLD-DATE.                                                        
           05 WS-SOLD-YY           PIC 9999.                                    
           05 WS-SOLD-MM           PIC 99.                                      
           05 WS-SOLD-DD           PIC 99.                                      
                                                                                
       01  EOF-BB24K510            PIC X        VALUE 'N'.                      
       01  EOF-BB24K500            PIC X        VALUE 'N'.                      
       01  EOF-BP13F716            PIC X        VALUE 'N'.                      
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-YY               PIC 9999.                                    
           05  WS-MM               PIC 99.                                      
           05  WS-DD               PIC 99.                                      
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DD           PIC X(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDIT-MM           PIC X(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDIT-YY           PIC X(04).                                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
           OPEN INPUT  BB24K510                                                 
                       BB24K500                                                 
                       BP13F716                                                 
                OUTPUT P13F716O.                                                
                                                                                
           IF BB24K510-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BB24K510 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BB24K510-STATUS                        
               MOVE BB24K510-STATUS TO RETURN-CODE                              
               PERFORM 900-CLOSE-ROUTINE.                                       
                                                                                
           IF BB24K500-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BB24K500 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BB24K500-STATUS                        
               MOVE BB24K500-STATUS TO RETURN-CODE                              
               PERFORM 900-CLOSE-ROUTINE.                                       
                                                                                
      *    ACCEPT WS-CUR-DATE FROM DATE.                                        
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-CUR-DATE.                     
           MOVE WS-DD      TO WS-EDIT-DD                                        
           MOVE WS-MM      TO WS-EDIT-MM                                        
           MOVE WS-YY      TO WS-EDIT-YY                                        
                                                                                
           PERFORM 100-READ-F716-REC THRU 100-EXIT.                             
           PERFORM 200-PROCESS-REC   THRU 200-EXIT                              
                   UNTIL EOF-BP13F716 = 'Y'                                     
                     OR  F716-NUM-NRIC = HIGH-VALUE.                            
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F716-REC.                                                       
      *-------------------------------------------------------------            
           READ BP13F716 AT END MOVE 'Y' TO EOF-BP13F716                        
                MOVE HIGH-VALUE TO F716-NUM-NRIC                                
                GO TO 100-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F716-READ.                                               
                                                                                
           IF F716-NUM-RDB-DP-OM = 'D' OR 'S' OR 'M' OR 'Z'                     
              WRITE P13F716O-REC FROM BP13F716-REC                              
              ADD 1 TO WS-BYPASS                                                
              ADD 1 TO WS-OUTPUT-CNT                                            
              GO TO 100-READ-F716-REC                                           
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
      *  COMPUTE MONTH RESIDE ONLY FOR THOSE NUM-RDB-DP-OM =                    
      * 'O' =>  EX-LESSEE WITH OM FLAT IN SMS                                   
      * 'R' =>  EX-LESSEE WITH NO RECORD FND IN SMS                             
      * 'X' =>  FIRST TIME WHICH PREVIOUSLY BOUGHT FLAT FROM OM                 
      *------------------------------------------------------------*            
       200-PROCESS-REC.                                                         
      *-------------------------------------------------------------            
           MOVE 'N'           TO WS-K510-NOTFD.                                 
           MOVE 'N'           TO WS-REQUIRE-CHK.                                
           MOVE SPACES        TO K510-KEY-FLD.                                  
           MOVE F716-NUM-NRIC TO K510-NUM-UIN.                                  
                                                                                
           START BB24K510 KEY >=  K510-KEY-FLD.                                 
                                                                                
           IF  BB24K510-STATUS = ZEROES                                         
               PERFORM 210-READ-BB24K510 THRU 210-EXIT                          
                  UNTIL K510-NUM-UIN NOT = F716-NUM-NRIC                        
                    OR  WS-K510-NOTFD = 'Y'                                     
                                                                                
               PERFORM 300-COMPUTE-OCCUP-PERIOD THRU 300-EXIT                   
      *                                                                         
      *  INCLUDE THIS BY AILEEN ON 08/05/97                                     
      *    GS - NHH BOUGHT FROM OM BUT TAKE HOUSING GRANT WHICH                 
      *         TREATED IT AS UPGRADER                                          
      *         REQUESTED TO BYPASS SUCH CASES                                  
      *                                                                         
               IF K500-CDE-RESALE-STATUS = 'GS '                                
                  ADD 1 TO WS-BYPASS-GS                                         
               ELSE                                                             
                  MOVE K500-CDE-RESALE-STATUS TO F716-CDE-RESALE-STATUS         
                  WRITE P13F716O-REC FROM BP13F716-REC                          
                  ADD 1 TO WS-OUTPUT-CNT                                        
                  ADD 1 TO WS-MR-NRIC                                           
           ELSE                                                                 
               PERFORM 205-RECORD-NOTFD-RESALE THRU 205-EXIT.                   
                                                                                
       200-READ-NEXT.                                                           
      *-------------------------------------------------------------            
            PERFORM 100-READ-F716-REC THRU 100-EXIT.                            
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       205-RECORD-NOTFD-RESALE.                                                 
      *-------------------------------------------------------------            
           WRITE P13F716O-REC FROM BP13F716-REC.                                
           ADD 1 TO WS-OUTPUT-CNT.                                              
           ADD 1 TO WS-NMR-NRIC.                                                
                                                                                
       205-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       210-READ-BB24K510.                                                       
      *-------------------------------------------------------------            
           READ BB24K510 NEXT AT END MOVE HIGH-VALUE TO K510-KEY-FLD            
                                     GO TO 210-EXIT.                            
      *                                                                         
      * CHANGE IT ON 11/04/97 BECAUSE NO HDB-REF                                
      *                                                                         
           ADD 1 TO WS-K510-READ.                                               
           IF  (F716-NUM-NRIC = K510-NUM-UIN) AND                               
               (F716-NUM-SCH-ACCNT = K510-NUM-HDB-REF (1:9)) AND                
               (K510-CDE-STATUS = 'P' OR                                        
                K510-CDE-STATUS = 'S')                                          
                PERFORM 211-READ-BB24K500 THRU 211-EXIT                         
           END-IF.                                                              
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
       211-READ-BB24K500.                                                       
      *-------------------------------------------------------------            
           MOVE K510-NUM-HDB-REF   TO K500-NUM-HDB-REF.                         
           MOVE K510-DTE-APPROVAL  TO K500-DTE-APPROVAL.                        
           READ BB24K500.                                                       
                                                                                
           ADD 1 TO WS-K500-READ.                                               
           IF  BB24K500-STATUS = ZEROES                                         
               MOVE K500-DTE-SOLD-TO-SELLER TO WS-SOLD-DATE                     
               MOVE K500-CDE-RESALE-STATUS  TO F716-CDE-RESALE-STATUS           
               MOVE 'Y'                     TO WS-REQUIRE-CHK                   
           ELSE                                                                 
               MOVE 'Y' TO WS-K510-NOTFD                                        
               DISPLAY 'RECORD NOT FOUND IN BB24K500 ' F716-NUM-NRIC            
                       '--> ' K510-DTE-APPROVAL                                 
                       '--> ' K510-NUM-HDB-REF                                  
           END-IF.                                                              
                                                                                
       211-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-COMPUTE-OCCUP-PERIOD.                                                
      *-------------------------------------------------------------            
                                                                                
           IF WS-REQUIRE-CHK = 'N'                                              
              GO TO 300-EXIT.                                                   
                                                                                
           IF WS-SOLD-DATE IS NOT NUMERIC                                       
               MOVE ZEROS TO WS-SOLD-DATE.                                      
                                                                                
           IF F716-DTE-REGN IS NOT NUMERIC                                      
              MOVE ZEROES TO WS-CUR-DATE                                        
           ELSE                                                                 
              MOVE F716-DTE-REGN       TO WS-CUR-DATE                           
           END-IF.                                                              
                                                                                
           IF  WS-SOLD-DATE > ZEROS                                             
               COMPUTE WS-OCCP-MTH = (WS-YY * 12 + WS-MM) -                     
                                 (WS-SOLD-YY * 12 + WS-SOLD-MM)                 
               MOVE WS-OCCP-MTH TO F716-NUM-MTH-RESIDE                          
               MOVE K500-DTE-SOLD-TO-SELLER       TO                            
                   F716-DTE-ACCEPTED                                            
           ELSE                                                                 
               MOVE ZEROES TO F716-NUM-MTH-RESIDE                               
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
      *    ACCEPT WS-CUR-DATE FROM DATE.                                        
           MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-CUR-DATE.                    
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'BP13C715                 CURR-DATE : ' WS-CUR-DATE.         
           DISPLAY 'MATCH NRIC IN REQUEST FILE WITH RESALE FILE'.               
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F716   ' WS-F716-READ.              
           DISPLAY 'NO OF RECORDS OUTPUT BP13F716  ' WS-OUTPUT-CNT.             
           DISPLAY '      RECORDS NOT MATCH        ' WS-NMR-NRIC.               
           DISPLAY '      RECORDS MATCH            ' WS-MR-NRIC.                
           DISPLAY '      RECORDS BYPASS           ' WS-BYPASS.                 
           DISPLAY '      RECORDS BYPASS - GS      ' WS-BYPASS-GS.              
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM K500   ' WS-K500-READ.              
           DISPLAY 'NO OF RECORDS READ FROM B12K510' WS-K510-READ.              
                                                                                
           CLOSE P13F716O                                                       
                 BP13F716                                                       
                 BB24K500                                                       
                 BB24K510.                                                      
                                                                                
           IF  BB24K510-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'BB24K510-STATUS ' BB24K510-STATUS.                      
           IF  BB24K500-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'BB24K500-STATUS ' BB24K500-STATUS.                      
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
