      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C003.                                                 
      *AUTHOR.        CLT.                                                      
      *DATE-WRITTEN.  11/08/98.                                                 
                                                                                
      * ========================================================== *            
      *  SYSTEM OF COMMITMENT (BP13)                               *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *    1. TO PROCESS COLLECTION SYSTEM CASH INTERFACE          *            
      *    2. THE EDITED RECORDS ARE APPENDED TO BP13F120 WHILE    *            
      *       THE ERROR RECORDS ARE WRITTEN TO KIVTRAN             *            
      *       FILE(BP13K140).                                      *            
      *                                                            *            
      *  LISTINGS:                                                 *            
      *     1. BP13L003 - ERROR LIST                               *            
      *                                                            *            
      * ========================================================== *            
      * CHG-NO   BY  DATE    DESCRIPTION                           *            
      * -------- --- ------  -----------                           *            
      * BP130549 SGK 980811  TO READ AC28F526 FROM COLLN SYSTEM.   *            
      * BP130418 CLT 980820  Y2K COMPLIANCE CHANGES.               *            
      * BP130855 AKT 000606  TO MOVE 'N' TO WS-F800-FOUND, IF      *            
      *                      RECORD NOT FOUND IN F800              *            
      * BP130998 ED  100501  FOR STUDIO APARTMENTS, DO NOT CKH K110*            
      * BP132093 CLT 180202  CATER FOR BOOKING FEE (3 IN 1)        *            
      * BP132118 CLT 010302  CATER FOR SERS/REN STDAPT CASE        *            
      * BP132309 MCC 280303  CATER FOR SOLICITOR BILL              *            
      * BP134969 ESD1 190613 WRITE TO BP13K140-KIV FILE IF         *            
      *                      F526-NUM-HDBREF IS SPACES.            *            
      * BP136161 KSJ3 140316 CATER FOR 2 ROOM FLEXI (2F)           *            
      * BP137319 MRN1 230518 DO NOT PRINT ERROR WHEN NO K800 SCHACCT            
      * BP138565 KR13 250121 TO CATER FOR FLAT-TYPE - '1F'.        *            
      * ========================================================== *            
                                                                                
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT AC28F526 ASSIGN TO AC28F526.                                  
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13F120 ASSIGN TO BP13F120.                                  
                                                                                
           SELECT BP13K140 ASSIGN TO BP13K140                                   
                ACCESS MODE       IS RANDOM                                     
                ORGANIZATION      IS INDEXED                                    
                RECORD KEY        IS K140-KEY-FLD                               
                FILE STATUS       IS BP13K140-STATUS.                           
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                ACCESS MODE       IS RANDOM                                     
                ORGANIZATION      IS INDEXED                                    
                RECORD KEY        IS K110-KEY-FLD                               
                FILE STATUS       IS BM06K110-STATUS.                           
                                                                                
           SELECT BP13K880 ASSIGN TO BP13K880                                   
                ACCESS MODE       IS RANDOM                                     
                ORGANIZATION      IS INDEXED                                    
                RECORD KEY        IS K880-KEY-FLD                               
                FILE STATUS       IS BP13K880-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ACCESS MODE       IS RANDOM                                     
                ORGANIZATION      IS INDEXED                                    
                RECORD KEY        IS K800-NUM-REGN                              
                FILE STATUS       IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13L003 ASSIGN TO BP13L003.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
                                                                                
       FD  AC28F526                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 130 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AC28F526.                                                           
                                                                                
       FD  BP13F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F800.                                                           
                                                                                
       FD  BP13F120                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       COPY BP13F120.                                                           
                                                                                
       FD  BP13K140                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13K140.                                                           
                                                                                
       FD  BP13K880                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13K880.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13L003                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC                      PIC X(132).                           
                                                                                
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K140-STATUS         PIC 99      VALUE ZEROES.                    
       01  BP13K880-STATUS         PIC 99      VALUE ZEROES.                    
       01  BP13K800-STATUS         PIC 99      VALUE ZEROES.                    
       01  BM06K110-STATUS         PIC 99      VALUE ZEROES.                    
       01  CNT-F526-READ           PIC 9(5)    VALUE ZEROS.                     
       01  CNT-F800-READ           PIC 9(5)    VALUE ZEROS.                     
       01  CNT-F120-WRITE          PIC 9(5)    VALUE ZEROS.                     
       01  CNT-K140-WRITE          PIC 9(5)    VALUE ZEROS.                     
       01  CNT-K880-READ           PIC 9(5)    VALUE ZEROS.                     
       01  CNT-K110-READ           PIC 9(5)    VALUE ZEROS.                     
       01  WS-LINE-CNT             PIC 99      VALUE 66.                        
       01  WS-PAGE-CNT             PIC 9(5)    VALUE ZERO.                      
       01  CNT-KIV-CASES           PIC 9(4)    VALUE ZERO.                      
       01  WS-CNT-HIST             PIC 9(2)    VALUE ZERO.                      
       01  WS-F800-FOUND           PIC X       VALUE 'N'.                       
       01  WS-CDE-ERROR            PIC X       VALUE SPACES.                    
       01  WS-FIS-SOLD             PIC X       VALUE SPACES.                    
       01  WS-AMT-KIV-TOTAL        PIC 9(6)V99 VALUE ZEROES.                    
                                                                                
       01  WS-SWITCHES.                                                         
           05  COLLN-TRAN-REC-EOF  PIC X       VALUE 'N'.                       
           05  F800-EOF            PIC X       VALUE 'N'.                       
                                                                                
       01  WS-TRANS-DATE.                                                       
           05  WS-TRANS-YYYY       PIC 9(4).                                    
           05  WS-TRANS-MM         PIC 9(2).                                    
           05  WS-TRANS-DD         PIC 9(2).                                    
                                                                                
       01  WS-DTE-CUR.                                                          
           05  WS-CC               PIC 99.                                      
           05  WS-YY               PIC 99.                                      
           05  WS-MM               PIC 99.                                      
           05  WS-DD               PIC 99.                                      
                                                                                
       01  L003-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L003'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  13S1-DATE           PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  13S1-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L003-PR-HEAD-02.                                                     
           05  FILLER              PIC X(39)     VALUE SPACES .                 
           05  FILLER              PIC X(53)     VALUE                          
               ' ERROR LIST OF TRANSACTIONS IN THE COLLN-TRANS FILE'.           
           05  FILLER              PIC X(40)     VALUE                          
               '      (SALES - AGREEMENT/APPLICATION)'.                         
                                                                                
       01  L003-PR-HEAD-03.                                                     
           05  FILLER              PIC X(28)                                    
                  VALUE '   S/NO     SCH-ACCNT NO    '.                         
           05  FILLER              PIC X(34)                                    
                  VALUE 'REGN NO       AMOUNT    RECEIPT NO'.                   
           05  FILLER              PIC X(39)                                    
                  VALUE '      DATE POST   PAY TYPE       REMARK'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(6)     VALUE 'H-CNT '.                 
                                                                                
       01  L003-PR-DETAILS.                                                     
           05  13S1-SNO            PIC ZZZZZ9 .                                 
           05  FILLER              PIC X(06)     VALUE SPACES .                 
           05  13S1-NUM-SCHACCNT   PIC X(11).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  13S1-ORIG-REG-NO    PIC X(8).                                    
           05  FILLER              PIC X(4)      VALUE SPACES .                 
           05  13S1-AMT            PIC $$$$$$9.99DB.                            
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  13S1-NUM-RECEIPT    PIC X(12) .                                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  13S1-DTE-POST.                                                   
               10  13S1-POST-DD    PIC X(2)      VALUE SPACES.                  
               10  13S1-POST-FIL1  PIC X         VALUE '/'.                     
               10  13S1-POST-MM    PIC X(2)      VALUE SPACES.                  
               10  13S1-POST-FIL2  PIC X         VALUE '/'.                     
               10  13S1-POST-YYYY  PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES .                 
           05  13S1-PAYMENT-TYPE   PIC X(03).                                   
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  13S1-REMARK         PIC X(35).                                   
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  13S1-CDE-HIST       PIC X(1)      VALUE SPACES.                  
           05  13S1-CNT-HIST       PIC X(2)      VALUE SPACES.                  
                                                                                
       01  L003-CONTROL-LINE.                                                   
           05  FILLER              PIC X(19)     VALUE SPACES.                  
           05  CNTL-ITEM           PIC X(30)     VALUE SPACES.                  
           05  CNTL-CNT            PIC ZZZZ9 .                                  
           05  FILLER              PIC X(58)     VALUE SPACES.                  
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       MAIN-PROCESS.                                                            
      *-------------------------------------------------------------            
           PERFORM 000-OPEN-FILE                THRU 000-EXIT.                  
           PERFORM 100-READ-COLLN-TRAN          THRU 100-EXIT.                  
           PERFORM 200-READ-F800                THRU 200-EXIT.                  
           PERFORM 300-PROCESS-SEQUENTIAL-FILES THRU 300-EXIT                   
                               UNTIL COLLN-TRAN-REC-EOF = 'Y'                   
                               AND   F800-EOF           = 'Y'.                  
           PERFORM 2000-CLOSE-ROUTINE           THRU 2000-EXIT.                 
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      * 1) OPENING FILES .                                                      
      * 2) INITIALISING THE CURRENT DATE.                              *        
      ******************************************************************        
      *-------------------------------------------------------------            
       000-OPEN-FILE.                                                           
      *-------------------------------------------------------------            
           OPEN INPUT  AC28F526                                                 
                       BP13F800                                                 
                       BM06K110                                                 
                       BP13K880                                                 
                       BP13K800                                                 
                I-O    BP13K140                                                 
                OUTPUT BP13F120                                                 
                       BP13L003.                                                
                                                                                
           IF BP13K140-STATUS NOT = '00' AND NOT = '97'                         
              DISPLAY ' OPENING ERROR,BP13K140-STATUS' BP13K140-STATUS          
              MOVE BP13K140-STATUS TO RETURN-CODE                               
              PERFORM 2000-CLOSE-ROUTINE THRU 2000-EXIT.                        
                                                                                
           IF BM06K110-STATUS NOT = '00' AND NOT = '97'                         
              DISPLAY ' OPENING ERROR,BM06K110-STATUS' BM06K110-STATUS          
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 2000-CLOSE-ROUTINE THRU 2000-EXIT.                        
                                                                                
           IF BP13K880-STATUS NOT = '00' AND NOT = '97'                         
              DISPLAY ' OPENING ERROR,BP13K880-STATUS' BP13K880-STATUS          
              MOVE BP13K880-STATUS TO RETURN-CODE                               
              PERFORM 2000-CLOSE-ROUTINE THRU 2000-EXIT.                        
                                                                                
           IF BP13K800-STATUS NOT = '00' AND NOT = '97'                         
              DISPLAY ' OPENING ERROR,BP13K800-STATUS' BP13K800-STATUS          
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 2000-CLOSE-ROUTINE THRU 2000-EXIT.                        
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DTE-CUR.                           
            STRING WS-DD '/' WS-MM '/' WS-CC WS-YY                              
                   DELIMITED BY SIZE INTO 13S1-DATE.                            
                                                                                
            MOVE 1 TO WS-LINE-CNT                                               
            PERFORM 1100-ERROR-HEADING THRU 1100-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *       1)  SEQUENTIAL READ FILE AC28F526                        *        
      *       2)  INITIALISE RECORDS OF BP13F120 & BP13K140            *        
      *           TO SPACES.                                           *        
      ******************************************************************        
      *-------------------------------------------------------------            
       100-READ-COLLN-TRAN.                                                     
      *-------------------------------------------------------------            
           READ AC28F526                                                        
               AT END MOVE HIGH-VALUE TO AC28F526-REC                           
               MOVE 'Y' TO COLLN-TRAN-REC-EOF                                   
               GO TO 100-EXIT.                                                  
                                                                                
           ADD 1 TO CNT-F526-READ.                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *           SEQUENTIAL READ FILE  BP13F800                       *        
      ******************************************************************        
      *-------------------------------------------------------------            
       200-READ-F800.                                                           
      *-------------------------------------------------------------            
           READ BP13F800                                                        
               AT END  MOVE HIGH-VALUE TO BP13F800-MASTER                       
               MOVE 'Y' TO F800-EOF                                             
               GO TO 200-EXIT.                                                  
           ADD 1 TO CNT-F800-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *  MATCH RECORDS WITH THE SAME SCH-ACCT-NO .   PROCESSING OF     *        
      *  MATCHED RECORDS AND PRINTING & WRITTING OF ERROR RECORDS .    *        
      *                                                                *        
      *  AFTER A MATCH, NEED TO READ NEXT COLLN RECORD. READ NEXT F800 *        
      *  RECORD ONLY IF COLLN RECORD KEY > F800 RECORD KEY. THIS IS TO *        
      *  ALLOW FOR MORE THAN 1 COLLN RECORD TO BE POSTED FOR 1 F800    *        
      *  RECORD.                                                       *        
      ******************************************************************        
      *-------------------------------------------------------------            
       300-PROCESS-SEQUENTIAL-FILES.                                            
      *-------------------------------------------------------------            
           MOVE   SPACE           TO  WS-CDE-ERROR.                             
                                                                                
           IF F526-NUM-RCPTCD = '6046H'                                         
               DISPLAY 'REGN '  F526-NUM-REF                                    
               MOVE        'Y'              TO   WS-F800-FOUND                  
               PERFORM 400-VALIDATE-PAYMENT THRU 400-EXIT                       
               PERFORM 100-READ-COLLN-TRAN  THRU 100-EXIT                       
           ELSE                                                                 
           IF ((F526-NUM-HDBREF = SPACES OR LOW-VALUES) AND                     
                F526-NUM-RCPTCD = '6038G')                                      
                   MOVE 'A' TO WS-CDE-ERROR                                     
                   MOVE 'N' TO WS-F800-FOUND                                    
                   PERFORM 500-WRITE-KIV-FILE  THRU 500-EXIT                    
                   PERFORM 1000-ERROR-LIST     THRU 1000-EXIT                   
                   PERFORM 100-READ-COLLN-TRAN THRU 100-EXIT                    
           ELSE                                                                 
           IF F526-NUM-HDBREF = SPACES OR LOW-VALUES                            
              MOVE 'A' TO WS-CDE-ERROR                                          
              MOVE 'N' TO WS-F800-FOUND                                         
              PERFORM 500-WRITE-KIV-FILE  THRU 500-EXIT                         
              PERFORM 1000-ERROR-LIST     THRU 1000-EXIT                        
              PERFORM 100-READ-COLLN-TRAN THRU 100-EXIT                         
           ELSE                                                                 
           IF F800-NUM-SCH-ACC = F526-NUM-HDBREF                                
               MOVE     'Y'                 TO   WS-F800-FOUND                  
               PERFORM 400-VALIDATE-PAYMENT THRU 400-EXIT                       
               PERFORM 100-READ-COLLN-TRAN  THRU 100-EXIT                       
           ELSE                                                                 
               MOVE     'N'                 TO   WS-F800-FOUND                  
               IF  F800-NUM-SCH-ACC       <   F526-NUM-HDBREF                   
                   PERFORM 200-READ-F800    THRU 200-EXIT                       
               ELSE                                                             
                   MOVE 'A'                 TO  WS-CDE-ERROR                    
                   PERFORM 500-WRITE-KIV-FILE  THRU 500-EXIT                    
      *            PERFORM 1000-ERROR-LIST     THRU 1000-EXIT                   
                   PERFORM 100-READ-COLLN-TRAN THRU 100-EXIT.                   
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      * TO CHECK THE VALIDITY OF THE PAYMENT                   *                
      *                                                        *                
      *   1. CHECK THAT THE RECEIPTING CODE IS :               *                
      *      6182A - FOR BOOKING FEES                          *                
      *      6183C - FOR SOC DEBTOR                            *                
      *      6163D - FOR SOLICITOR BILL                        *                
      *   2. CHECK THAT THE AMT REQ MATCHES THE POSTED AMT     *                
      *   3. LAST PARA CHECK FOR DUPLICATE SCH-ACCT-NO         *                
      **********************************************************                
      *-------------------------------------------------------------            
       400-VALIDATE-PAYMENT.                                                    
      *-------------------------------------------------------------            
           IF F526-NUM-RCPTCD = '6046H'                                         
               PERFORM 1050-UPDATE-REGN-DEPOSIT  THRU  1050-EXIT                
                GO TO 400-EXIT.                                                 
                                                                                
           IF F800-NUM-IDP-SCHEME = 'SA ' OR                                    
             (F800-NUM-FLAT-TYPE  = '1A'  OR '2A' OR '2F' OR '1F') OR           
              F526-NUM-RCPTCD     = '6182A'                                     
              CONTINUE                                                          
           ELSE                                                                 
              MOVE SPACE  TO WS-FIS-SOLD                                        
              PERFORM 420-READ-FIS-BM06K110 THRU 420-EXIT                       
              IF WS-FIS-SOLD = 'Y'                                              
                   PERFORM 500-WRITE-KIV-FILE THRU 500-EXIT                     
                   PERFORM 1000-ERROR-LIST    THRU 1000-EXIT                    
                   GO TO 400-EXIT                                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F526-NUM-RCPTCD NOT = '6182A'                                     
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF F800-AMT-BK-REQ    = 0                                         
                  MOVE 'N' TO WS-CDE-ERROR                                      
                  PERFORM 500-WRITE-KIV-FILE THRU 500-EXIT                      
                  PERFORM 1000-ERROR-LIST    THRU 1000-EXIT                     
                  GO TO 400-EXIT                                                
               ELSE                                                             
                  IF F526-AMT-TXN > 9999                                        
                     MOVE 'B' TO WS-CDE-ERROR                                   
                     PERFORM 500-WRITE-KIV-FILE THRU 500-EXIT                   
                     PERFORM 1000-ERROR-LIST    THRU 1000-EXIT                  
                     GO TO 400-EXIT                                             
                  ELSE                                                          
                     DISPLAY 'XXXX' F800-NUM-REGN                               
                     PERFORM 450-WRITE-UPDATE-FILE THRU 450-EXIT                
                     GO TO 400-EXIT.                                            
                                                                                
           IF F526-NUM-RCPTCD NOT = '6163D'                                     
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF F800-AMT-SOLICIT-REQ NOT NUMERIC                               
                 MOVE ZEROES TO F800-AMT-SOLICIT-REQ                            
              END-IF                                                            
              IF F800-AMT-SOLICIT-REQ = 0                                       
                  MOVE 'L' TO WS-CDE-ERROR                                      
                  PERFORM 500-WRITE-KIV-FILE THRU 500-EXIT                      
                  PERFORM 1000-ERROR-LIST    THRU 1000-EXIT                     
                  GO TO 400-EXIT                                                
              ELSE                                                              
                  DISPLAY 'XXXX' F800-NUM-REGN                                  
                  PERFORM 450-WRITE-UPDATE-FILE THRU 450-EXIT                   
                  GO TO 400-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F800-AMT-SOC-DEBTOR IS NOT NUMERIC                                
               MOVE ZEROES TO F800-AMT-SOC-DEBTOR                               
           ELSE                                                                 
               NEXT SENTENCE.                                                   
                                                                                
           IF F526-NUM-RCPTCD = '6183C'                                         
              PERFORM 450-WRITE-UPDATE-FILE THRU 450-EXIT                       
           ELSE                                                                 
              IF F526-NUM-RCPTCD = '6038G'                                      
                 MOVE F526-NUM-REF      TO K880-KEY-FLD                         
                 READ BP13K880                                                  
                 IF BP13K880-STATUS = 00                                        
                    PERFORM 450-WRITE-UPDATE-FILE THRU 450-EXIT                 
                 ELSE                                                           
                    MOVE 'S' TO WS-CDE-ERROR                                    
                     PERFORM 500-WRITE-KIV-FILE THRU 500-EXIT                   
                    PERFORM 1000-ERROR-LIST     THRU 1000-EXIT                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       420-READ-FIS-BM06K110.                                                   
      *-------------------------------------------------------------            
           MOVE F526-NUM-HDBREF   TO K110-KEY-FLD                               
           READ BM06K110                                                        
           IF BM06K110-STATUS = 00                                              
               PERFORM 430-CHECK-FLAT-SOLD THRU 430-EXIT                        
           ELSE                                                                 
               DISPLAY 'READ ERROR,BM06K110-STATUS' BM06K110-STATUS             
               DISPLAY 'F526-NUM-HDBREF   ' F526-NUM-HDBREF.                    
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       430-CHECK-FLAT-SOLD.                                                     
      *-------------------------------------------------------------            
           ADD 1 TO CNT-K110-READ.                                              
           IF K110-DTE-KEY-ISSUED NOT = ZEROS AND SPACES AND LOW-VALUES         
                MOVE 'Y' TO WS-FIS-SOLD                                         
                MOVE 'F' TO WS-CDE-ERROR.                                       
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *        WRITING UPDATED RECORDS TO BP13F120             *                
      **********************************************************                
      *-------------------------------------------------------------            
       450-WRITE-UPDATE-FILE.                                                   
      *-------------------------------------------------------------            
           MOVE SPACES              TO BP13F120-CASHTRAN.                       
           MOVE '11'                TO F120-CDE-TRANS-TYPE.                     
           MOVE F800-NUM-REGN       TO F120-NUM-REGN.                           
           MOVE WS-DTE-CUR          TO F120-DTE-POST.                           
           MOVE '000'               TO F120-COUNT-HISTORY.                      
           MOVE F800-NUM-NRIC1      TO F120-NUM-NRIC.                           
           MOVE F526-NUM-RCPT       TO F120-NUM-RECPT-JRNO.                     
           MOVE F526-AMT-TXN        TO F120-AMT-RECEIPT.                        
           MOVE F526-DTE-TXN-DD     TO WS-TRANS-DD.                             
           MOVE F526-DTE-TXN-MM     TO WS-TRANS-MM.                             
           STRING F526-DTE-TXN-CC  F526-DTE-TXN-YY                              
           DELIMITED BY SIZE INTO      WS-TRANS-YYYY.                           
           MOVE WS-TRANS-DATE       TO F120-DTE-TRANS.                          
           MOVE F800-NUM-SCH-ACC    TO F120-NUM-SCH-ACCT.                       
                                                                                
           IF F526-NUM-RCPTCD = '6182A'                                         
              MOVE 'BK ' TO F120-CDE-PAYMENT-TYPE.                              
                                                                                
           IF F526-NUM-RCPTCD = '6038G'                                         
              MOVE 'SUS' TO F120-CDE-PAYMENT-TYPE.                              
                                                                                
           IF F526-NUM-RCPTCD = '6046H'                                         
               MOVE K800-NUM-REGN    TO F120-NUM-REGN                           
               MOVE K800-NUM-NRIC1   TO F120-NUM-NRIC                           
               MOVE 'RDE'            TO F120-CDE-PAYMENT-TYPE.                  
                                                                                
           IF F526-NUM-RCPTCD = '6183C'                                         
              MOVE 'SOC' TO F120-CDE-PAYMENT-TYPE.                              
                                                                                
           IF F526-NUM-RCPTCD = '6163D'                                         
              MOVE 'SOL' TO F120-CDE-PAYMENT-TYPE.                              
                                                                                
           WRITE BP13F120-CASHTRAN.                                             
                                                                                
           ADD 1 TO CNT-F120-WRITE.                                             
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *        WRITTING ERROR RECORDS TO BP13K140 FOR KIV      *                
      **********************************************************                
      *-------------------------------------------------------------            
       500-WRITE-KIV-FILE.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                 TO  BP13K140-KIVTRAN.                    
           ADD 1                       TO  WS-CNT-HIST.                         
                                                                                
           IF WS-F800-FOUND = 'N'                                               
               MOVE F526-NUM-REF       TO  K140-NUM-REGN                        
               MOVE SPACES             TO  K140-NUM-NRIC                        
           ELSE                                                                 
               MOVE F800-NUM-REGN      TO  K140-NUM-REGN                        
               MOVE F800-NUM-NRIC1     TO  K140-NUM-NRIC.                       
                                                                                
           MOVE '11'                   TO  K140-CDE-TRANS-TYPE.                 
           MOVE WS-DTE-CUR             TO  K140-DTE-POST.                       
           MOVE 'S'                    TO  K140-COUNT-HIST-CHAR.                
           MOVE WS-CNT-HIST            TO  K140-COUNT-HIST-NUM.                 
           MOVE F526-NUM-RCPT          TO  K140-NUM-RECPT-JRNO.                 
           MOVE F526-AMT-TXN           TO  K140-AMT-RECEIPT.                    
           MOVE F526-DTE-TXN-DD        TO  WS-TRANS-DD.                         
           MOVE F526-DTE-TXN-MM        TO  WS-TRANS-MM.                         
           STRING F526-DTE-TXN-CC  F526-DTE-TXN-YY                              
           DELIMITED BY SIZE INTO      WS-TRANS-YYYY.                           
           MOVE WS-TRANS-DATE          TO  K140-DTE-TRANS.                      
           IF F526-NUM-HDBREF = SPACES OR LOW-VALUES                            
              STRING '##' K140-NUM-REGN DELIMITED BY SIZE                       
              INTO  K140-NUM-SCH-ACCT                                           
           ELSE                                                                 
              MOVE F526-NUM-HDBREF        TO  K140-NUM-SCH-ACCT                 
           END-IF                                                               
           MOVE WS-CDE-ERROR           TO  K140-CDE-ERROR.                      
                                                                                
           IF F526-NUM-RCPTCD = '6182A'                                         
               MOVE 'BK ' TO K140-CDE-PAYMENT-TYPE.                             
                                                                                
           IF F526-NUM-RCPTCD = '6038G'                                         
              MOVE 'SUS' TO K140-CDE-PAYMENT-TYPE.                              
                                                                                
           IF F526-NUM-RCPTCD = '6183C'                                         
               MOVE 'SOC' TO K140-CDE-PAYMENT-TYPE.                             
                                                                                
           IF F526-NUM-RCPTCD = '6163D'                                         
               MOVE 'SOL' TO K140-CDE-PAYMENT-TYPE.                             
                                                                                
           IF F526-NUM-RCPTCD = '6046H'                                         
               MOVE 'RDE' TO K140-CDE-PAYMENT-TYPE                              
           END-IF.                                                              
                                                                                
           DISPLAY 'K140-KEY-FLD :'  K140-KEY-FLD.                              
           WRITE BP13K140-KIVTRAN.                                              
           IF BP13K140-STATUS > 0 AND NOT = 22 AND NOT = '02'                   
               DISPLAY ' WRITE ERROR AT BP13K140 '                              
               DISPLAY ' BP13K140-STATUS IS ' BP13K140-STATUS                   
               DISPLAY ' ORIGINAL REGN NO IS : ' F526-NUM-REF                   
               MOVE BP13K140-STATUS TO RETURN-CODE                              
               PERFORM 2000-CLOSE-ROUTINE THRU 2000-EXIT                        
           ELSE                                                                 
               IF BP13K140-STATUS  = 22                                         
                  ADD 1 TO  WS-CNT-HIST                                         
                  MOVE WS-CNT-HIST TO  K140-COUNT-HIST-NUM                      
                  WRITE BP13K140-KIVTRAN                                        
                  DISPLAY ' DUPLICATE RECORDS AT BP13K140 '                     
                  DISPLAY ' ORIGINAL REGN NO IS : '                             
                             F526-NUM-REF.                                      
                                                                                
           ADD 1 TO CNT-K140-WRITE.                                             
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *            WRITTING OUT ERROR LIST                     *                
      **********************************************************                
      *-------------------------------------------------------------            
       1000-ERROR-LIST.                                                         
      *-------------------------------------------------------------            
           IF WS-LINE-CNT > 46                                                  
               MOVE 1 TO WS-LINE-CNT                                            
               PERFORM 1100-ERROR-HEADING THRU 1100-EXIT                        
           ELSE                                                                 
               ADD  1 TO WS-LINE-CNT.                                           
                                                                                
           ADD F526-AMT-TXN      TO   WS-AMT-KIV-TOTAL.                         
           ADD 1                 TO   CNT-KIV-CASES.                            
                                                                                
           IF WS-CDE-ERROR = 'A'                                                
               MOVE 'SCH-ACCT NO ERROR'       TO 13S1-REMARK                    
           ELSE                                                                 
           IF WS-CDE-ERROR = 'N'                                                
               MOVE 'BKING FEE NOT REQ'       TO 13S1-REMARK                    
           ELSE                                                                 
           IF WS-CDE-ERROR = 'B'                                                
              MOVE 'BKING FEE > $9999'        TO 13S1-REMARK                    
           ELSE                                                                 
           IF WS-CDE-ERROR = 'F'                                                
              MOVE 'FIS FLAT SOLD    '        TO 13S1-REMARK                    
           ELSE                                                                 
           IF WS-CDE-ERROR = 'L'                                                
              MOVE 'SOLICITOR BILL REQ = $0'  TO 13S1-REMARK                    
           ELSE                                                                 
           IF WS-CDE-ERROR = 'S'                                                
              MOVE 'SHOULD BE A 4005 CASE'    TO 13S1-REMARK.                   
                                                                                
           IF WS-CDE-ERROR = 'E'                                                
              MOVE  'NON-FIANCE/FIANCEE ELIGIBLE SCHEME' TO                     
                                                  13S1-REMARK                   
           ELSE                                                                 
           IF WS-CDE-ERROR = 'D'                                                
              MOVE  'REGN - DEPOSIT  >  $5000 ' TO  13S1-REMARK.                
                                                                                
           MOVE  CNT-KIV-CASES       TO 13S1-SNO.                               
           MOVE  F526-NUM-HDBREF     TO 13S1-NUM-SCHACCNT.                      
           MOVE  F526-NUM-REF        TO 13S1-ORIG-REG-NO.                       
           MOVE  'S'                 TO 13S1-CDE-HIST.                          
           MOVE  WS-CNT-HIST         TO 13S1-CNT-HIST.                          
           MOVE  F526-AMT-TXN        TO 13S1-AMT.                               
                                                                                
           IF F526-NUM-RCPTCD = '6182A'                                         
               MOVE 'BK ' TO 13S1-PAYMENT-TYPE.                                 
           IF F526-NUM-RCPTCD = '6183C'                                         
               MOVE 'SOC' TO 13S1-PAYMENT-TYPE.                                 
           IF F526-NUM-RCPTCD = '6038G'                                         
               MOVE 'SUS' TO 13S1-PAYMENT-TYPE.                                 
           IF F526-NUM-RCPTCD = '6046H'                                         
               MOVE 'RDE' TO 13S1-PAYMENT-TYPE.                                 
           IF F526-NUM-RCPTCD = '6163D'                                         
               MOVE 'SOL' TO 13S1-PAYMENT-TYPE.                                 
                                                                                
           STRING F526-DTE-TXN-CC  F526-DTE-TXN-YY                              
                  DELIMITED BY  SIZE INTO 13S1-POST-YYYY.                       
           MOVE F526-DTE-TXN-MM      TO 13S1-POST-MM.                           
           MOVE F526-DTE-TXN-DD      TO 13S1-POST-DD.                           
           MOVE F526-NUM-RCPT        TO 13S1-NUM-RECEIPT.                       
           WRITE PRINT-REC FROM L003-PR-DETAILS.                                
                                                                                
           MOVE SPACES               TO L003-PR-DETAILS.                        
           MOVE '/'                  TO 13S1-POST-FIL1                          
                                        13S1-POST-FIL2.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1050-UPDATE-REGN-DEPOSIT.                                                
      *-------------------------------------------------------------            
              MOVE SPACES  TO     WS-CDE-ERROR.                                 
                                                                                
              MOVE  F526-NUM-REF    TO   K800-NUM-REGN.                         
              READ BP13K800.                                                    
                                                                                
              EVALUATE BP13K800-STATUS                                          
               WHEN '00'                                                        
                IF K800-NUM-ELIG-SCHEME NOT = 'FS'                              
                       MOVE 'E'     TO     WS-CDE-ERROR                         
                ELSE IF F526-AMT-TXN   > 5000                                   
                       MOVE 'D'     TO     WS-CDE-ERROR                         
                     END-IF                                                     
                END-IF                                                          
                                                                                
               WHEN  '23'                                                       
                MOVE 'N'     TO     WS-F800-FOUND                               
                MOVE 'R'     TO     WS-CDE-ERROR                                
                DISPLAY 'BP13K800-RECORD NOT FOUND, K800-NUM-REGN '             
                             K800-NUM-REGN                                      
                                                                                
               WHEN  OTHER                                                      
                MOVE 'N'     TO     WS-F800-FOUND                               
                MOVE 'R'     TO     WS-CDE-ERROR                                
                DISPLAY 'BP13K800-READ ERROR, STATUS : ' BP13K800-STATUS        
              END-EVALUATE.                                                     
                                                                                
              IF  WS-CDE-ERROR = SPACES OR LOW-VALUES                           
                  PERFORM 450-WRITE-UPDATE-FILE THRU 450-EXIT                   
              ELSE                                                              
                  PERFORM 500-WRITE-KIV-FILE    THRU 500-EXIT                   
                  PERFORM 1000-ERROR-LIST       THRU 1000-EXIT.                 
                                                                                
       1050-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *                ERROR LIST HEADING                      *                
      **********************************************************                
      *-------------------------------------------------------------            
       1100-ERROR-HEADING.                                                      
      *-------------------------------------------------------------            
            ADD 1               TO WS-PAGE-CNT.                                 
            MOVE WS-PAGE-CNT    TO 13S1-PAGENO.                                 
            MOVE SPACES         TO PRINT-REC                                    
            WRITE PRINT-REC FROM L003-PR-HEAD-01 AFTER PAGE.                    
            WRITE PRINT-REC FROM L003-PR-HEAD-02 AFTER 2.                       
            WRITE PRINT-REC FROM L003-PR-HEAD-03 AFTER 2.                       
            INSPECT PRINT-REC REPLACING ALL SPACES BY LOW-VALUES.               
            INSPECT PRINT-REC REPLACING CHARACTERS BY '-'.                      
            INSPECT PRINT-REC REPLACING ALL LOW-VALUES BY SPACES.               
            WRITE PRINT-REC .                                                   
            MOVE SPACES TO PRINT-REC.                                           
            WRITE PRINT-REC AFTER 2.                                            
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *                CLOSING ROUTINES                        *                
      **********************************************************                
      *-------------------------------------------------------------            
       2000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           ADD 1                 TO WS-PAGE-CNT.                                
           MOVE WS-PAGE-CNT      TO 13S1-PAGENO.                                
           MOVE SPACES           TO PRINT-REC                                   
           WRITE PRINT-REC FROM L003-PR-HEAD-01 AFTER PAGE.                     
           WRITE PRINT-REC FROM L003-PR-HEAD-02 AFTER 2.                        
           MOVE SPACES           TO PRINT-REC.                                  
           WRITE PRINT-REC.                                                     
           MOVE 'NO OF COLLN RECS READ (F526) : ' TO CNTL-ITEM.                 
           MOVE CNT-F526-READ    TO CNTL-CNT.                                   
           WRITE PRINT-REC FROM L003-CONTROL-LINE AFTER 3.                      
                                                                                
           MOVE 'NO OF RFF RECS READ   (F800) : ' TO CNTL-ITEM.                 
           MOVE CNT-F800-READ    TO CNTL-CNT.                                   
           WRITE PRINT-REC FROM L003-CONTROL-LINE.                              
                                                                                
           MOVE 'NO OF COLLN RECS WRITE (F120): ' TO CNTL-ITEM.                 
           MOVE CNT-F120-WRITE   TO CNTL-CNT.                                   
           WRITE PRINT-REC FROM L003-CONTROL-LINE .                             
                                                                                
           MOVE 'NO OF K110 REC READ   (K110) : ' TO CNTL-ITEM.                 
           MOVE CNT-K110-READ    TO CNTL-CNT.                                   
           WRITE PRINT-REC FROM L003-CONTROL-LINE .                             
                                                                                
           MOVE 'NO OF KIV RECS WRITE  (K140) : ' TO CNTL-ITEM.                 
           MOVE CNT-K140-WRITE   TO CNTL-CNT.                                   
           WRITE PRINT-REC FROM L003-CONTROL-LINE .                             
                                                                                
           MOVE SPACES               TO L003-PR-DETAILS.                        
           MOVE WS-AMT-KIV-TOTAL     TO 13S1-AMT.                               
           MOVE 'TOTAL :'            TO 13S1-ORIG-REG-NO.                       
           WRITE PRINT-REC FROM L003-PR-DETAILS  AFTER 2.                       
                                                                                
           DISPLAY '******  BP13C003 *************'.                            
           DISPLAY 'NO OF COLLN REC READ (F526) : ' CNT-F526-READ.              
           DISPLAY 'NO OF RFF RECS READ  (F800) : ' CNT-F800-READ.              
           DISPLAY 'NO OF F120 WRITE     (F120) : ' CNT-F120-WRITE.             
           DISPLAY 'NO OF K110 READ      (K110) : ' CNT-K110-READ.              
           DISPLAY 'NO OF K140 WRITE     (K140) : ' CNT-K140-WRITE.             
                                                                                
           CLOSE BP13F800                                                       
                 AC28F526                                                       
                 BP13F120                                                       
                 BP13K140                                                       
                 BP13K880                                                       
                 BP13K800                                                       
                 BM06K110                                                       
                 BP13L003.                                                      
                                                                                
           IF BP13K140-STATUS NOT = 0                                           
            DISPLAY ' CLOSING ERROR, BP13K140-STATUS:' BP13K140-STATUS          
            MOVE BP13K140-STATUS TO RETURN-CODE .                               
                                                                                
           IF BP13K880-STATUS NOT = 0                                           
            DISPLAY ' CLOSING ERROR, BP13K880-STATUS:' BP13K880-STATUS          
            MOVE BP13K880-STATUS TO RETURN-CODE .                               
                                                                                
           IF BP13K800-STATUS NOT = 0                                           
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS:' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE .                               
                                                                                
           IF BM06K110-STATUS NOT = 0                                           
            DISPLAY ' CLOSING ERROR, BM06K110-STATUS:' BM06K110-STATUS          
            MOVE BM06K110-STATUS TO RETURN-CODE .                               
                                                                                
           STOP RUN.                                                            
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
