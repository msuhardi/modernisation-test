       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C989.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  29/10/01.                                                 
      *===============================================================*         
      *               SYSTEM OF COMMITMENT (BP13) *                   *         
      *===============================================================*         
      *                                                               *         
      *   OBJECTIVE : FOR NEW APPLN THRU ORSA/INTERNET AF SOC CREATED *         
      *               THIS PROGRAM IS USED TO CREATE THE TRANSACTION  *         
      *               ID (AR) AND CREATE MARRIAGE RECORD (AM) , IF    *         
      *               CSTMR SOURCE IS EQUAL TO SPACES.                *         
      *1. FILE DETAILS :                                              *         
      *    INPUT   :  BP13F820  ( OCCP FILE)                          *         
      *               BP13K800  ( MASTER FILE )                       *         
      *    OUTPUT     BP13F915  ( TRANSACTION FILE)                   *         
      *                                                               *         
      *===============================================================*         
      * CHG-NO    CHG-DATE     BY   DESCRIPTION                       *         
      * ------    --------     --   -----------                       *         
      * BP130850  29/10/2001   AS1  NEW PROGRAM                       *         
      * BP132560  29/07/2004   CKK  MATCH NRIC1 TO NRIC4 FOR HA       *         
      * BP132577  03/09/2004   PGS1 TO OMIT THE CASE IF IT IS WIS AND *         
      *                             SCH IS SPACES                     *         
      * BP133285  14/05/2008   JB8  TO REMOVE THE CHECK OF SCH-ACC    *         
      *****************************************************************         
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
      *----------------------*                                                  
       CONFIGURATION SECTION.                                                   
      *----------------------*                                                  
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *----------------------*                                                  
       INPUT-OUTPUT SECTION.                                                    
      *----------------------*                                                  
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F820 ASSIGN TO BP13F820.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13F915 ASSIGN TO BP13F915.                                  
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
      *-------------*                                                           
                                                                                
       FD  BP13F820                                                             
           RECORD    CONTAINS 400  CHARACTERS                                   
           BLOCK     CONTAINS   0  RECORDS                                      
           LABEL     RECORDS  ARE  STANDARD                                     
           RECORDING MODE     IS   F.                                           
       COPY BP13F820.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD    CONTAINS 2000 CHARACTERS.                                  
       COPY BP13K800.                                                           
                                                                                
       FD  BP13F915                                                             
           RECORD    CONTAINS 800 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F915.                                                           
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  FILE-STATUS.                                                         
           03  WS-K800-STATUS          PIC 9(02)         VALUE ZERO.            
                                                                                
       01  WS-SWITCHES.                                                         
           03  F820-EOF-IND            PIC X             VALUE SPACES.          
               88 F820-EOF             VALUE 'Y'.                               
                                                                                
       01  WS-ACCUMULATORS.                                                     
           03  WS-NO-READ-K800         PIC 9(10)         VALUE ZERO.            
           03  WS-AR-WRT-F915          PIC 9(10)         VALUE ZERO.            
           03  WS-AM-WRT-F915          PIC 9(10)         VALUE ZERO.            
           03  WS-NO-READ-F820         PIC 9(10)         VALUE ZERO.            
           03  WS-CANCEL               PIC 9(10)         VALUE ZERO.            
           03  WS-CUSTID               PIC 9(10)         VALUE ZERO.            
           03  WS-NO-DETAIL            PIC 9(10)         VALUE ZERO.            
           03  WS-WITH-SPOUSE          PIC 9(10)         VALUE ZERO.            
           03  WS-NO-SP-NAME           PIC 9(10)         VALUE ZERO.            
           03  WS-EDT-NO-READ-K800     PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-AR-WRT-F915      PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-AM-WRT-F915      PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-NO-READ-F820     PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-CANCEL           PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-CUSTID           PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-NO-DETAIL        PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-WITH-SPOUSE      PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-NO-SP-NAME       PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
                                                                                
       01  WS-TEMP.                                                             
           03  WS-NUM-K800-NRIC1       PIC X(9)          VALUE SPACES.          
           03  WS-NUM-K800-NRIC2       PIC X(9)          VALUE SPACES.          
           03  WS-NUM-K800-NRIC3       PIC X(9)          VALUE SPACES.          
           03  WS-NUM-K800-NRIC4       PIC X(9)          VALUE SPACES.          
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *----------------*                                                        
       000-MAIN-PROCESS.                                                        
      *----------------*                                                        
                                                                                
             PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                   
             PERFORM 2000-READ-BP13F820       THRU 2000-EXIT.                   
             PERFORM 3000-PROCESS-ROUTINE     THRU 3000-EXIT                    
               UNTIL F820-EOF.                                                  
             PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                   
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-ROUTINE.                                                       
      *----------------*                                                        
           OPEN       INPUT   BP13F820                                          
                              BP13K800                                          
                      OUTPUT  BP13F915.                                         
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND '97'                               
              DISPLAY ' ERROR OPENING BP13K800  FILE, STATUS '                  
                                                  WS-K800-STATUS                
              MOVE WS-K800-STATUS            TO   RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2000-READ-BP13F820.                                                      
      *-----------------*                                                       
                                                                                
           READ BP13F820 AT END  MOVE 'Y' TO F820-EOF-IND                       
                         GO TO 2000-EXIT.                                       
                                                                                
           ADD 1 TO WS-NO-READ-F820.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3000-PROCESS-ROUTINE.                                                    
      *-------------------*                                                     
                                                                                
           PERFORM 4000-READ-BP13K800       THRU 4000-EXIT.                     
                                                                                
           IF WS-K800-STATUS = ZEROS AND K800-NUM-STATUS NOT = 'C'              
              IF F820-NUM-CSTMR-SOURCE = SPACES                                 
                 PERFORM 4100-AR-WRITE-F915    THRU  4100-EXIT                  
                 IF F820-NUM-NRIC-SPOUSE NOT = SPACES AND                       
                    F820-NUM-MARITAL-STATUS  = 'M' OR '2'                       
                    ADD   1    TO   WS-WITH-SPOUSE                              
                    PERFORM 4300-AM-WRITE-F915 THRU  4300-EXIT                  
                 END-IF                                                         
              ELSE                                                              
                 ADD  1  TO  WS-CUSTID                                          
              END-IF                                                            
           ELSE                                                                 
               ADD  1  TO  WS-CANCEL                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F820       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       4000-READ-BP13K800.                                                      
      *------------------*                                                      
                                                                                
            MOVE F820-NUM-REGN        TO   K800-NUM-REGN.                       
            READ BP13K800.                                                      
                                                                                
            EVALUATE WS-K800-STATUS                                             
                WHEN 00                                                         
                                                                                
                     ADD  1  TO  WS-NO-READ-K800                                
                     MOVE K800-NUM-NRIC1 TO WS-NUM-K800-NRIC1                   
                     MOVE K800-NUM-NRIC2 TO WS-NUM-K800-NRIC2                   
                     MOVE K800-NUM-NRIC3 TO WS-NUM-K800-NRIC3                   
                     MOVE K800-NUM-NRIC4 TO WS-NUM-K800-NRIC4                   
                                                                                
                WHEN 23                                                         
                     DISPLAY 'RECORD NOT FOUND IN K800 FILE , STATUS'           
                              K800-NUM-STATUS                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR IN READING - BP13K800 , STATUS'             
                              K800-NUM-STATUS                                   
                     PERFORM  9000-CLOSE-ROUTINE   THRU  9000-EXIT              
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       4100-AR-WRITE-F915.                                                      
      *-----------------*                                                       
                                                                                
           INITIALIZE  BP13F915-REC.                                            
           MOVE SPACES                   TO   BP13F915-REC.                     
                                                                                
           IF  F820-NUM-NATIONALITY      =    SPACES AND                        
               F820-NUM-CITIZENSHIP      =    SPACES AND                        
               F820-NUM-MARITAL-STATUS   =    SPACES AND                        
               F820-NUM-SEX              =    SPACES AND                        
               F820-NUM-ETHNIC           =    SPACES AND                        
               F820-DTE-BIRTH            =    SPACES                            
               ADD  1  TO  WS-NO-DETAIL                                         
               GO TO 4100-EXIT                                                  
           END-IF.                                                              
                                                                                
           MOVE  'AR'                    TO   F915-TXN-TYPE.                    
           MOVE  'P'                     TO   F915-CSTMR-TYPE.                  
           MOVE  'BP13'                  TO   F915-NUM-UPDATE-SOURCE.           
           MOVE  'BP13C989'              TO   F915-NUM-UPDATE-USERID.           
                                                                                
           MOVE  F820-NUM-NRIC           TO   F915-NUM-IDNTY-NO.                
                                                                                
           MOVE  F820-NME-OCCP           TO   F915-NME-PERSON.                  
           MOVE  F820-NUM-NATIONALITY    TO   F915-NUM-NTNLTY.                  
           MOVE  F820-NUM-CITIZENSHIP    TO   F915-NUM-PR-STATUS                
           MOVE  F820-NUM-MARITAL-STATUS TO   F915-NUM-MRTL-STATUS.             
           MOVE  F820-NUM-SEX            TO   F915-NUM-SEX.                     
           MOVE  F820-NUM-ETHNIC         TO   F915-NUM-ETHNIC-GROUP.            
                                                                                
            STRING F820-DTE-BIRTH(1:4) '-' F820-DTE-BIRTH(5:2) '-'              
                   F820-DTE-BIRTH(7:2) DELIMITED BY SIZE                        
                   INTO F915-DTE-BIRTH.                                         
                                                                                
           PERFORM 4200-CONVERT-CUSTOMER-ROLE    THRU 4200-EXIT.                
                                                                                
           MOVE    F820-NUM-REGN          TO                                    
                                          F915-NUM-BSNS-REF.                    
           MOVE    '0001-01-01'           TO                                    
                                          F915-DTE-ACPTNC.                      
                                                                                
           STRING K800-DTE-REGN(1:4) '-' K800-DTE-REGN(5:2) '-'                 
                  K800-DTE-REGN(7:2) DELIMITED BY SIZE                          
                  INTO  F915-DTE-ROLE-START.                                    
                                                                                
           WRITE BP13F915-REC.                                                  
           ADD   1    TO WS-AR-WRT-F915.                                        
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------*                                              
       4200-CONVERT-CUSTOMER-ROLE.                                              
      *--------------------------*                                              
                                                                                
            IF F820-NUM-NRIC        =   WS-NUM-K800-NRIC1                       
               MOVE '011'           TO  F915-NUM-ROLE                           
            ELSE                                                                
              IF ( F820-NUM-NRIC    =   WS-NUM-K800-NRIC2 ) OR                  
                 ( F820-NUM-NRIC    =   WS-NUM-K800-NRIC3 ) OR                  
                 ( F820-NUM-NRIC    =   WS-NUM-K800-NRIC4 )                     
                  MOVE '012'        TO  F915-NUM-ROLE                           
              ELSE                                                              
                  MOVE '013'        TO  F915-NUM-ROLE                           
              END-IF                                                            
            END-IF.                                                             
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       4300-AM-WRITE-F915.                                                      
      *------------------*                                                      
                                                                                
              IF   F820-NME-SPOUSE  =   SPACES                                  
                   ADD   1    TO   WS-NO-SP-NAME                                
                   GO TO 4300-EXIT                                              
              END-IF.                                                           
                                                                                
              MOVE SPACES                      TO BP13F915-REC.                 
              MOVE 'AM'                        TO F915-TXN-TYPE.                
              MOVE 'BP13'                      TO                               
                                               F915-NUM-UPDATE-SOURCE.          
              MOVE 'BP13C989'                  TO                               
                                               F915-NUM-UPDATE-USERID.          
                                                                                
              MOVE F820-NUM-REGN               TO F915-NUM-REGN.                
                                                                                
      ****  MOVE PERSON'S DETAIL    TO F915 PERSON'S DETAIL  ****               
                                                                                
              MOVE F820-NUM-NRIC               TO F915-NUM-IDNTY-NO-PSN.        
              MOVE F820-NME-OCCP               TO F915-NME-PSN.                 
                                                                                
      ****  MOVE SPOUSE'S DETAIL    TO F915 SPOUSE'S DETAIL  ****               
                                                                                
              MOVE F820-NUM-NRIC-SPOUSE        TO F915-NUM-IDNTY-NO-SP.         
              MOVE F820-NME-SPOUSE             TO F915-NME-SP.                  
                                                                                
              WRITE BP13F915-REC.                                               
              ADD   1    TO   WS-AM-WRT-F915.                                   
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       9000-CLOSE-ROUTINE.                                                      
      *------------------*                                                      
                                                                                
           CLOSE BP13F820                                                       
                 BP13K800                                                       
                 BP13F915.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY ' ERROR CLOSING BP13K800  FILE, STATUS '                  
              WS-K800-STATUS.                                                   
                                                                                
           MOVE  WS-NO-READ-K800       TO   WS-EDT-NO-READ-K800                 
           MOVE  WS-AR-WRT-F915        TO   WS-EDT-AR-WRT-F915                  
           MOVE  WS-AM-WRT-F915        TO   WS-EDT-AM-WRT-F915                  
           MOVE  WS-NO-READ-F820       TO   WS-EDT-NO-READ-F820                 
                                                                                
           MOVE  WS-CANCEL             TO   WS-EDT-CANCEL.                      
           MOVE  WS-CUSTID             TO   WS-EDT-CUSTID.                      
           MOVE  WS-NO-DETAIL          TO   WS-EDT-NO-DETAIL.                   
           MOVE  WS-WITH-SPOUSE        TO   WS-EDT-WITH-SPOUSE.                 
           MOVE  WS-NO-SP-NAME         TO   WS-EDT-NO-SP-NAME.                  
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY " BP13C989  CONTROL LIST  :  ".                              
           DISPLAY "----------------------------".                              
           DISPLAY SPACES.                                                      
           DISPLAY "NO. OF REC READ FROM BP13F820 :" WS-EDT-NO-READ-F820        
           DISPLAY "NO. OF REC READ FROM BP13K800 :" WS-EDT-NO-READ-K800        
           DISPLAY SPACES.                                                      
           DISPLAY "NO. OF REC WRITE TO  BP13F915 }" WS-EDT-AR-WRT-F915         
           DISPLAY " CDB TABLE   ( 'AR' TRANS )   }"                            
           DISPLAY SPACES.                                                      
           DISPLAY "NO. OF REC CANCELLED APPL     :" WS-EDT-CANCEL.             
           DISPLAY "NO. OF REC BYPASS WITH CUSTID :" WS-EDT-CUSTID.             
           DISPLAY "NO. OF REC BYPASS NO DETAIL   :" WS-EDT-NO-DETAIL.          
           DISPLAY "NO. OF REC WITH SPOUSE-NRIC   :" WS-EDT-WITH-SPOUSE.        
           DISPLAY "NO. OF REC SPOUSE-NAME=BLANK  :" WS-EDT-NO-SP-NAME.         
           DISPLAY SPACES.                                                      
           DISPLAY "NO. OF REC WRITE TO  BP13F915 }" WS-EDT-AM-WRT-F915         
           DISPLAY " MARRIED FILE ('AM' TRANS )   }"                            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
