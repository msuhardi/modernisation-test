      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID. BP13C049.                                                    
      *AUTHOR. SK.                                                              
      *DATE-WRITTEN.   12/07/2004.                                              
      *--------------------------------------------------------*                
      *  OBJECTIVE :                                           *                
      *    MATCH F800 AND F595 (TO FIND THE CANCELATION CASES) *                
      *    WRITE ONLY IF NOT FOUND IN BP13F800 (F730)          *                
      *                                                        *                
      * INPUT  : BP13F800                                      *                
      *          BP13F595                                      *                
      *                                                        *                
      * OUTPUT : P13F595A                                      *                
      *-------------- --------- -------------------------------*                
      * CHG REQ# BY   ON        DESCRIPTION                    *                
      *-------------- --------- -------------------------------*                
      * BP132558 SK   12072004  NEW PGM                        *                
      *--------------------------------------------------------*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800   ASSIGN TO BP13F800.                                
           SELECT BP13F595   ASSIGN TO BP13F595.                                
           SELECT P13F595A   ASSIGN TO P13F595A.                                
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F595                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F595.                                                           
                                                                                
       FD   P13F595A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F595A-REC.                                                       
            05 FILLER              PIC X(500).                                  
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-READ-F800              PIC 9(8)    VALUE 0.                    
           05 WS-READ-F595              PIC 9(8)    VALUE 0.                    
           05 WS-MATCH                  PIC 9(8)    VALUE 0.                    
           05 WS-NOT-MATCH              PIC 9(8)    VALUE 0.                    
           05 WS-COUNT                  PIC 9(1)    VALUE 0.                    
           05 WS-WRITE-F595A            PIC 9(8)    VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  WS-EDITED-DATE             PIC X(10).                                
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F800     THRU 2000-EXIT.                           
           PERFORM 3000-READ-F595     THRU 3000-EXIT.                           
                                                                                
           PERFORM 4000-COMPARE       THRU 4000-EXIT                            
                   UNTIL F800-NUM-REGN = HIGH-VALUES                            
                     AND F595-NUM-REGN = HIGH-VALUES.                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13F595                                                 
           OUTPUT      P13F595A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F800.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F800                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F800-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F800                                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-READ-F595.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F595                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F595-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F595                                        
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-COMPARE.                                                            
      *--------------------------------------------------------*                
                                                                                
           IF F800-NUM-REGN  = F595-NUM-REGN                                    
              ADD 1 TO WS-MATCH                                                 
              PERFORM 3000-READ-F595 THRU 3000-EXIT                             
              PERFORM 2000-READ-F800   THRU 2000-EXIT                           
           ELSE                                                                 
              IF F800-NUM-REGN > F595-NUM-REGN                                  
                 PERFORM 4800-WRITE-F595A THRU 4800-EXIT                        
                 PERFORM 3000-READ-F595 THRU 3000-EXIT                          
              ELSE                                                              
                 PERFORM 2000-READ-F800  THRU 2000-EXIT                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4800-WRITE-F595A.                                                        
      *--------------------------------------------------------*                
                                                                                
           WRITE P13F595A-REC FROM BP13F595-REC.                                
           ADD 1 TO WS-WRITE-F595A.                                             
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '*********** BP13C049 **************'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS READ (BP13F800) :' WS-READ-F800.             
           DISPLAY 'NO. OF RECORDS READ (BP13F595) :' WS-READ-F595.             
           DISPLAY 'NO. OF RECORDS MATCH           :' WS-MATCH.                 
           DISPLAY 'NO. OF RECORDS WRITE(P13F595A) :' WS-WRITE-F595A.           
           DISPLAY '***********************************'.                       
                                                                                
           CLOSE       BP13F800                                                 
                       BP13F595                                                 
                       P13F595A.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
