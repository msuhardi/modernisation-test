       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC99.                                                 
      *AUTHOR.        IRVIN CHING.                                              
      *DATE-WRITTEN.  09/09/2012.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVE    :  CREATE LONG DOS REPORT                      *           
      *                                                             *           
      * INPUT FILES  :                                              *           
      *                 1.  BP13K800 - SOC APPLICATION MASTER       *           
      *                 2.  BP13K820 - SOC OCCUPIER FILE            *           
      *                                                             *           
      * OUTPUT       :  1.  BP13FD99 - SUPER OUTPUT REPORT          *           
      *                                              CASH           *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  OIC  DATE     DESCRIPTION                          *           
      * -------- ---  -------- ------------------------------------ *           
      * BP134650 IMC1 20120827 NEW PROGRAM                          *           
      * BP134990 IMC1 20130709 ADD BP13K757                         *           
      * BP134990 IMC1 20130813 ADD BP13KB78 FOR DBSS CASES          *           
      * BP134990 IMC1 20130820 ADD BM06K110 TO RETRIEVE DTE-ACCEPT  *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800  ASSIGN       TO BP13F800.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT BP13K757  ASSIGN       TO BP13K757                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K757-KEY-FLD                        
                            FILE STATUS  IS WS-K757-STATUS.                     
                                                                                
           SELECT BP13K060  ASSIGN       TO BP13K060                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K060-KEY-FLD                        
                            FILE STATUS  IS WS-K060-STATUS.                     
                                                                                
           SELECT BP13KB78  ASSIGN       TO BP13KB78                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS KB78-KEY-FLD                        
                            FILE STATUS  IS WS-KB78-STATUS.                     
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT BP13FD99  ASSIGN       TO BP13FD99.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORDING MODE  IS    F                                             
            LABEL RECORDS   ARE   STANDARD                                      
            BLOCK  CONTAINS    0  RECORDS                                       
            RECORD CONTAINS 2000  CHARACTERS.                                   
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K757                                                            
            RECORD CONTAINS 3000 CHARACTERS.                                    
       COPY BP13K757.                                                           
                                                                                
       FD   BP13KB78                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB78.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BP13FD99                                                            
            RECORD CONTAINS 1200 CHARACTERS                                     
            RECORDING MODE  IS  F.                                              
       COPY BP13FD99.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-K800-STATUS           PIC 99     VALUE 00.                    
           05  WS-K820-STATUS           PIC 99     VALUE 00.                    
           05  WS-K830-STATUS           PIC 99     VALUE 00.                    
           05  WS-K060-STATUS           PIC 99     VALUE 00.                    
           05  WS-K757-STATUS           PIC 99     VALUE 00.                    
           05  WS-KB78-STATUS           PIC 99     VALUE 00.                    
           05  WS-K110-STATUS           PIC 99     VALUE 00.                    
           05  WS-EOF-F800              PIC X      VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-F800-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-K800-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-CNT-KB78              PIC 9(6)   VALUE ZEROS.                 
           05  WS-CNT-KB78-NOTFD        PIC 9(6)   VALUE ZEROS.                 
           05  WS-CNT-K757              PIC 9(6)   VALUE ZEROS.                 
           05  WS-CNT-K757-NOTFD        PIC 9(6)   VALUE ZEROS.                 
           05  WS-PIDB-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-FD99-WRT-CTR          PIC 9(7)   VALUE ZEROES.                
           05  WS-SNO                   PIC 9(5)   VALUE ZEROES.                
           05  WS-CNT                   PIC 9(5)   VALUE ZEROES.                
           05  WS-INIT                  PIC 9(5)   VALUE ZEROES.                
           05  WS-PAGE-CTR              PIC 9(4)   VALUE ZEROES.                
           05  WS-LINE-CTR              PIC 9(2)   VALUE ZEROES.                
           05  WS-K757-FND              PIC X(1)   VALUE 'N'.                   
           05  WS-KB78-FND              PIC X(1)   VALUE 'N'.                   
           05  WS-K820-EOF              PIC X(1)   VALUE 'N'.                   
           05  WS-K820-FOUND            PIC X(1)   VALUE SPACE.                 
               88  K820-FND              VALUE 'Y'.                             
                                                                                
       01  WS-STORAGE.                                                          
           10 WS-NRIC                   PIC X(9) VALUE SPACES.                  
           10 WS-CODE-RELATIONSHIP      PIC X(2) VALUE SPACES.                  
           10 WS-CODE-MARITAL-STATUS    PIC X(1) VALUE SPACES.                  
           10 WS-CODE-EDUCATION         PIC X(1) VALUE SPACES.                  
           10 WS-NUM-RELATIONSHIP       PIC X(20) VALUE SPACES.                 
           10 WS-NUM-MARITAL-STATUS     PIC X(1) VALUE SPACES.                  
           10 WS-NUM-EDUCATION          PIC X(20) VALUE SPACES.                 
           10 WS-NUM-PRIO-SCHEME        PIC X(5) VALUE SPACES.                  
           10 WS-NUM-NRIC-SPOUSE        PIC X(9) VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-FILES-ROUTINE   THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13F800        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-RECORD       THRU  3000-EXIT                   
                    UNTIL WS-EOF-F800 =  'Y'.                                   
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES-ROUTINE.                                                 
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F800                                                
                        BP13K800                                                
                        BP13K820                                                
                        BP13K757                                                
                        BP13KB78                                                
                        BP13K060                                                
                        BM06K110                                                
                 OUTPUT BP13FD99.                                               
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K800-STATUS ' WS-K800-STATUS           
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K820-STATUS ' WS-K820-STATUS           
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF WS-K757-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K757-STATUS ' WS-K757-STATUS           
              MOVE     WS-K757-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF WS-KB78-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-KB78-STATUS ' WS-KB78-STATUS           
              MOVE     WS-KB78-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K060-STATUS ' WS-K060-STATUS           
              MOVE     WS-K060-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K110-STATUS ' WS-K110-STATUS           
              MOVE     WS-K110-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
           READ BP13F800                                                        
                AT END MOVE 'Y' TO WS-EOF-F800                                  
                GO TO  2000-EXIT.                                               
                                                                                
           ADD  1  TO   WS-F800-READ.                                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
           PERFORM 3100-INITIALIZE-FIELDS THRU  3100-EXIT.                      
                                                                                
           PERFORM 4000-READ-BP13K800     THRU 4000-EXIT.                       
                                                                                
           IF WS-K800-STATUS    = 00                                            
              MOVE K800-NUM-REGN         TO FD99-NUM-REGN                       
              MOVE K800-NUM-SCH-ACC      TO FD99-NUM-SCH-ACC                    
                                                                                
              IF K800-DTE-ACCEPTANCE = SPACES OR LOW-VALUES                     
                 PERFORM 5300-READ-BM06K110   THRU 5300-EXIT                    
              ELSE                                                              
                 MOVE K800-DTE-ACCEPTANCE     TO FD99-DTE-ACCEPTANCE            
              END-IF                                                            
                                                                                
              IF K800-NUM-ALLO-SCHEME = 'PA'                                    
                 MOVE 'N'                TO FD99-NUM-PRIOR-SCH                  
              ELSE                                                              
                 MOVE 'Y'                TO FD99-NUM-PRIOR-SCH                  
              END-IF                                                            
                                                                                
              PERFORM 5000-GET-NAMES        THRU 5000-EXIT                      
                                                                                
              IF K800-NUM-ALLO-SCHEME = 'MCP'                                   
                 IF K800-NUM-REGN(1:1) = '5'                                    
                    PERFORM 3300-READ-BP13KB78    THRU 3300-EXIT                
                 ELSE                                                           
                    PERFORM 3200-READ-BP13K757    THRU 3200-EXIT                
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF K800-NUM-MGPS NOT = SPACES AND LOW-VALUES                      
                 MOVE K800-NUM-MGPS           TO FD99-NUM-TYPE-MGPS             
                 MOVE K800-NUM-JT-BALLOT-REGN TO FD99-NUM-REGN-MGPS             
              END-IF                                                            
                                                                                
              PERFORM 8300-PRINT-REPORT     THRU 8300-EXIT                      
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800       THRU 2000-EXIT.                     
                                                                                
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3100-INITIALIZE-FIELDS.                                                  
      *-------------------------------------------------------------            
                                                                                
           INITIALIZE        BP13FD99-REC.                                      
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3200-READ-BP13K757.                                                      
      *************************************************************             
                                                                                
            MOVE SPACES                              TO BP13K757-REC.           
            INITIALIZE                                  BP13K757-REC.           
            MOVE 'N'                                 TO WS-K757-FND.            
                                                                                
            MOVE K800-NUM-REGN                       TO K757-NUM-REGN.          
                                                                                
            READ BP13K757.                                                      
                                                                                
            EVALUATE WS-K757-STATUS                                             
                WHEN 00                                                         
                     IF K757-NUM-SPEC-SCHEME = 'P' OR 'C'                       
                        MOVE K757-NUM-PRSN1-UIN    TO FD99-NUM-PRSN1-UIN        
                        MOVE K757-NME-PRSN1        TO FD99-NME-PRSN1            
                        MOVE K757-NUM-PRSN1-RLTNSP                              
                          TO FD99-NUM-PRSN1-RLTNSP                              
                        MOVE K757-NUM-PRSN1-MARSTAT                             
                          TO FD99-NUM-PRSN1-MARSTAT                             
                                                                                
                        MOVE K757-NUM-PRSN2-UIN    TO FD99-NUM-PRSN2-UIN        
                        MOVE K757-NME-PRSN2        TO FD99-NME-PRSN2            
                        MOVE K757-NUM-PRSN2-RLTNSP                              
                          TO FD99-NUM-PRSN2-RLTNSP                              
                        MOVE K757-NUM-PRSN2-MARSTAT                             
                          TO FD99-NUM-PRSN2-MARSTAT                             
                                                                                
                        MOVE K757-NUM-MCPS-BLK   TO FD99-NUM-MCPS-BLK           
                        MOVE K757-NUM-MCPS-LEVEL TO FD99-NUM-MCPS-LEVEL         
                        MOVE K757-NUM-MCPS-MAIN-UNIT                            
                          TO FD99-NUM-MCPS-MAIN-UNIT                            
                        MOVE K757-NUM-MCPS-SUB-UNIT                             
                          TO FD99-NUM-MCPS-SUB-UNIT                             
                        MOVE K757-NME-MCPS-STREET                               
                          TO FD99-NME-MCPS-STREET                               
                        MOVE K757-NUM-MCPS-POSTAL                               
                          TO FD99-NUM-MCPS-POSTAL                               
                     END-IF                                                     
                     MOVE 'Y'             TO WS-K757-FND                        
                     ADD 1 TO WS-CNT-K757                                       
                WHEN 23                                                         
                     MOVE 'N'             TO WS-K757-FND                        
                     ADD 1 TO WS-CNT-K757-NOTFD                                 
                     DISPLAY 'BP13K757 NOT FOUND: ' K757-NUM-REGN               
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K757 : ' WS-K757-STATUS         
                             ' REGN NO = ' K757-NUM-REGN                        
                     PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT               
            END-EVALUATE.                                                       
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3300-READ-BP13KB78.                                                      
      *************************************************************             
                                                                                
            MOVE SPACES                              TO BP13KB78-REC.           
            INITIALIZE                                  BP13KB78-REC.           
            MOVE 'N'                                 TO WS-KB78-FND.            
                                                                                
            MOVE K800-NUM-REGN                       TO KB78-NUM-REGN.          
                                                                                
            READ BP13KB78.                                                      
                                                                                
            EVALUATE WS-KB78-STATUS                                             
                WHEN 00                                                         
                     IF KB78-NUM-CHILD-PARENT-TAG = 'P' OR 'C'                  
                        MOVE KB78-NUM-CHILD1-PARENT-UIN                         
                          TO FD99-NUM-PRSN1-UIN                                 
                        MOVE KB78-NME-CHILD1-PARENT                             
                          TO FD99-NME-PRSN1                                     
                        MOVE KB78-NUM-CHILD1-PARENT-RLTNSP                      
                          TO FD99-NUM-PRSN1-RLTNSP                              
                        MOVE KB78-NUM-CHILD1-PARENT-MARSTAT                     
                          TO FD99-NUM-PRSN1-MARSTAT                             
                                                                                
                        MOVE KB78-NUM-CHILD2-PARENT-UIN                         
                          TO FD99-NUM-PRSN2-UIN                                 
                        MOVE KB78-NME-CHILD2-PARENT                             
                          TO FD99-NME-PRSN2                                     
                        MOVE KB78-NUM-CHILD1-PARENT-RLTNSP                      
                          TO FD99-NUM-PRSN2-RLTNSP                              
                        MOVE KB78-NUM-CHILD1-PARENT-MARSTAT                     
                          TO FD99-NUM-PRSN2-MARSTAT                             
                                                                                
                        MOVE KB78-NUM-CP-BLK                                    
                          TO FD99-NUM-MCPS-BLK                                  
                        MOVE KB78-NUM-CP-LEVEL                                  
                          TO FD99-NUM-MCPS-LEVEL                                
                        MOVE KB78-NUM-CP-MAIN-UNIT                              
                          TO FD99-NUM-MCPS-MAIN-UNIT                            
                        MOVE KB78-NUM-CP-SUB-UNIT                               
                          TO FD99-NUM-MCPS-SUB-UNIT                             
                        MOVE KB78-NME-CP-STREET                                 
                          TO FD99-NME-MCPS-STREET                               
                        MOVE KB78-NUM-CP-POST                                   
                          TO FD99-NUM-MCPS-POSTAL                               
                     END-IF                                                     
                     MOVE 'Y'             TO WS-KB78-FND                        
                     ADD 1 TO WS-CNT-KB78                                       
                WHEN 23                                                         
                     MOVE 'N'             TO WS-KB78-FND                        
                     ADD 1 TO WS-CNT-KB78-NOTFD                                 
                     DISPLAY 'BP13KB78 NOT FOUND: ' KB78-NUM-REGN               
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13KB78 : ' WS-KB78-STATUS         
                             ' REGN NO = ' KB78-NUM-REGN                        
                     PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT               
            END-EVALUATE.                                                       
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           MOVE F800-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD  1  TO   WS-K800-READ                                       
           WHEN 23                                                              
                DISPLAY 'BP13K800 REC NOTFND, KEY ' K800-NUM-REGN               
           WHEN OTHER                                                           
                DISPLAY 'BP13K800 READ ERR, STATUS ' WS-K800-STATUS             
                MOVE WS-K800-STATUS           TO RETURN-CODE                    
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=====================*                                                   
       4500-PROCESS-BP13K820.                                                   
      *=====================*                                                   
                                                                                
           MOVE   K820-NUM-NRIC                TO WS-NRIC                       
           MOVE   K820-NUM-RELATIONSHIP        TO WS-CODE-RELATIONSHIP          
           MOVE   K820-NUM-MARITAL-STATUS      TO WS-CODE-MARITAL-STATUS        
           MOVE   K820-NUM-EDUCATION           TO WS-CODE-EDUCATION             
           MOVE   K820-NUM-NRIC-SPOUSE         TO WS-NUM-NRIC-SPOUSE            
                                                                                
           PERFORM 4700-DECODE-MARITAL      THRU 4700-EXIT                      
           PERFORM 4800-DECODE-EDUCATION    THRU 4800-EXIT                      
                                                                                
           IF K820-NUM-RELATIONSHIP = 00                                        
              MOVE WS-NRIC               TO FD99-NUM-NRIC-APPLN                 
              MOVE WS-NUM-MARITAL-STATUS TO FD99-NUM-MS-APPLN                   
              MOVE WS-NUM-EDUCATION      TO FD99-NUM-NRIC-APPLN-EDU             
              MOVE WS-NUM-NRIC-SPOUSE    TO FD99-NUM-NRIC-APPLN-SP              
           ELSE                                                                 
              MOVE WS-NRIC               TO FD99-NUM-NRIC-PRSN(WS-CNT)          
              MOVE WS-CODE-RELATIONSHIP  TO FD99-NUM-RELATIONSHIP               
                                            (WS-CNT)                            
              MOVE WS-NUM-MARITAL-STATUS TO FD99-NUM-MARITAL-STATUS             
                                            (WS-CNT)                            
              MOVE WS-NUM-EDUCATION      TO FD99-NUM-PRSN-EDU(WS-CNT)           
              MOVE WS-NUM-NRIC-SPOUSE    TO FD99-NUM-NRIC-PRSN-SP               
                                            (WS-CNT)                            
              ADD 1                         TO WS-CNT                           
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=====================*                                                   
       4600-DECODE-RELATIONSHIP.                                                
      *=====================*                                                   
                                                                                
           MOVE 35                             TO K060-SERIAL-NO                
           MOVE WS-CODE-RELATIONSHIP           TO K060-CODE                     
           PERFORM 4900-READ-BP13K060        THRU 4900-EXIT                     
           MOVE K060-DESC(1:20)                TO WS-NUM-RELATIONSHIP.          
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=====================*                                                   
       4700-DECODE-MARITAL.                                                     
      *=====================*                                                   
                                                                                
           MOVE 44                             TO K060-SERIAL-NO                
           MOVE WS-CODE-MARITAL-STATUS         TO K060-CODE                     
           PERFORM 4900-READ-BP13K060        THRU 4900-EXIT                     
           MOVE K060-DESC(1:1)                 TO WS-NUM-MARITAL-STATUS.        
                                                                                
       4700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=====================*                                                   
       4800-DECODE-EDUCATION.                                                   
      *=====================*                                                   
                                                                                
           MOVE 52                             TO K060-SERIAL-NO                
           MOVE WS-CODE-EDUCATION              TO K060-CODE                     
           PERFORM 4900-READ-BP13K060        THRU 4900-EXIT                     
           MOVE K060-DESC(1:20)                TO WS-NUM-EDUCATION.             
                                                                                
       4800-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=====================*                                                   
       4900-READ-BP13K060.                                                      
      *=====================*                                                   
                                                                                
           READ BP13K060.                                                       
                                                                                
           IF WS-K060-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K060-STATUS = 23                                            
                 MOVE SPACES                     TO K060-DESC                   
              ELSE                                                              
                 DISPLAY 'BP13K060 - ERROR READING , '                          
                         ' STATUS : ' WS-K060-STATUS                            
                         ' KEY : ' K060-KEY-FLD                                 
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-GET-NAMES.                                                          
      *-------------------------------------------------------------            
           MOVE 'N'                                 TO WS-K820-FOUND.           
           MOVE 1                                   TO WS-CNT.                  
           PERFORM 5100-START-BP13K820            THRU 5100-EXIT.               
           IF K820-FND                                                          
              PERFORM UNTIL WS-K820-EOF = 'Y'                                   
                         OR WS-CNT > 09                                         
                 PERFORM 5200-READNEXT-BP13K820   THRU 5200-EXIT                
                 IF NOT WS-K820-EOF = 'Y'                                       
                    PERFORM 4500-PROCESS-BP13K820 THRU 4500-EXIT                
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       5100-START-BP13K820.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                            TO BP13K820-REC               
           INITIALIZE                                BP13K820-REC.              
           MOVE 'N'                               TO WS-K820-EOF                
                                                     WS-K820-FOUND.             
                                                                                
           MOVE F800-NUM-REGN                     TO K820-NUM-REGN              
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           IF WS-K820-STATUS = 00 OR 02                                         
              MOVE 'Y'                            TO WS-K820-FOUND              
           ELSE                                                                 
              IF WS-K820-STATUS = 23 OR 10                                      
                 MOVE 'Y'                         TO WS-K820-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K820 - ERROR STARTING : ' WS-K820-STATUS          
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       5200-READNEXT-BP13K820.                                                  
      *===============================================================*         
                                                                                
           READ BP13K820 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K820-EOF                                      
                GO          TO 5200-EXIT                                        
           END-READ.                                                            
                                                                                
           IF WS-K820-STATUS = 00 OR 02                                         
              IF K820-NUM-REGN = F800-NUM-REGN                                  
                 MOVE 'Y'                         TO WS-K820-FOUND              
              ELSE                                                              
                 MOVE 'Y'                         TO WS-K820-EOF                
              END-IF                                                            
           ELSE                                                                 
              IF WS-K820-STATUS = 23 OR 10                                      
                 MOVE 'Y'                         TO WS-K820-FOUND              
              ELSE                                                              
                 DISPLAY 'BP13K820 - ERROR READ NEXT : ' WS-K820-STATUS         
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       5300-READ-BM06K110.                                                      
      *=====================*                                                   
                                                                                
           MOVE SPACES                                TO K110-REC.              
           INITIALIZE                                    K110-REC.              
                                                                                
           MOVE K800-NUM-SCH-ACC                      TO K110-KEY-FLD.          
                                                                                
           READ BM06K110.                                                       
                                                                                
           IF WS-K110-STATUS = 00                                               
              MOVE K110-DTE-ACCEPTANCE     TO FD99-DTE-ACCEPTANCE               
           ELSE                                                                 
              IF WS-K110-STATUS = 23                                            
                 MOVE SPACES               TO FD99-DTE-ACCEPTANCE               
              ELSE                                                              
                 DISPLAY 'BM06K110 - ERROR READING , '                          
                         ' STATUS : ' WS-K110-STATUS                            
                         ' KEY : ' K110-KEY-FLD                                 
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       8300-PRINT-REPORT.                                                       
      *--------------------------------------------------------------           
                                                                                
           WRITE BP13FD99-REC.                                                  
                                                                                
           ADD   1              TO    WS-FD99-WRT-CTR.                          
           INITIALIZE BP13FD99-REC.                                             
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY ' -- BP13CC99 CONTROL TOTAL -------'.                        
           DISPLAY ' '.                                                         
           DISPLAY ' TOTAL READ FROM F800 : ' WS-F800-READ.                     
           DISPLAY ' TOTAL READ FROM K800 : ' WS-K800-READ.                     
           DISPLAY ' TOTAL READ FND  K757 : ' WS-CNT-K757.                      
           DISPLAY ' TOTAL READ NFND K757 : ' WS-CNT-K757-NOTFD.                
           DISPLAY ' TOTAL REPORT PRINTED : ' WS-FD99-WRT-CTR.                  
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F800                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K060                                                       
                 BP13K757                                                       
                 BP13KB78                                                       
                 BP13FD99.                                                      
                                                                                
           IF WS-K800-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K800, STAT ' WS-K800-STATUS.           
                                                                                
           IF WS-K820-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K820, STAT ' WS-K820-STATUS.           
                                                                                
           IF WS-K757-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K757, STAT ' WS-K757-STATUS.           
                                                                                
           IF WS-KB78-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13KB78, STAT ' WS-KB78-STATUS.           
                                                                                
           IF WS-K060-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K060, STAT ' WS-K060-STATUS.           
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
