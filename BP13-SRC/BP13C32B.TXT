       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C32A.                                                 
      *AUTHOR.        JGO1.                                                     
      *DATE-WRITTEN.  09/09/13.                                                 
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *    1.   TO UPDATE SOC MASTER (BP13K893) THAT CASES HAD     *            
      *           BEEN STAMPED FOR SOCA AFTER TP.                  *            
      *                                                            *            
      *  INPUT      :  BP13F315  (TRANSACTION FILE)                *            
      *                                                            *            
      *  I-O        :  BP13K893  (SOC APPLICATION MASTER FILE)     *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY   DATE    DESCRIPTION                          *            
      * -------- ---- ------  -----------                          *            
      * BP134998 JGO1 090913  NEW PROGRAM COPEID FROM BP13C324     *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F315  ASSIGN       TO BP13F315.                           
                                                                                
           SELECT BP13K893  ASSIGN       TO BP13K893                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K893-KEY-FLD                        
                            FILE STATUS  IS WS-K893-STATUS.                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F315                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F315.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-K893-STATUS                PIC 99    VALUE 00.                
           05  WS-EOF-F315                   PIC X     VALUE 'N'.               
               88  EOF-OF-F315                         VALUE 'Y'.               
           05  WS-K893-FND             PIC X(01) VALUE SPACE.                   
               88 K893-FND                      VALUE 'Y'.                      
           05  WS-K893-EOF             PIC X(01) VALUE SPACE.                   
               88 K893-EOF                      VALUE 'Y'.                      
                                                                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-F315-CNT              PIC 9(6)  VALUE 0.                 
           05  WS-READ-K893-CNT              PIC 9(6)  VALUE 0.                 
           05  WS-UPDT-K893-CNT              PIC 9(6)  VALUE 0.                 
                                                                                
           05  WS-EDITED-READ-F315           PIC ZZZZZ9.                        
           05  WS-EDITED-READ-K893           PIC ZZZZZ9.                        
           05  WS-EDITED-UPDT-K893           PIC ZZZZZ9.                        
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-ROUTINE          THRU   100-EXIT.                  
           PERFORM  200-READ-INPUT-BP13F315   THRU   200-EXIT.                  
           PERFORM  300-PROCESS-RECORD        THRU   300-EXIT                   
                        UNTIL   EOF-OF-F315.                                    
           PERFORM  600-DISPLAY-STATISTICS    THRU   600-EXIT.                  
           PERFORM  900-CLOSE-ROUTINE         THRU   900-EXIT.                  
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT   BP13F315                                                
                I-O     BP13K893.                                               
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K893, STATUS = ' WS-K893-STATUS        
              MOVE     WS-K893-STATUS    TO    RETURN-CODE                      
              PERFORM  900-CLOSE-ROUTINE THRU  900-EXIT                         
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
      * READ TRANSACTION FILE (BP13F315) AT END MOVE 'Y' TO EOF-IND  *          
      ****************************************************************          
       200-READ-INPUT-BP13F315.                                                 
      *-------------------------------------------------------------            
           READ BP13F315                                                        
                AT END                                                          
                   MOVE  'Y'         TO WS-EOF-F315                             
                   GO TO 200-EXIT.                                              
                                                                                
           ADD  1  TO WS-READ-F315-CNT.                                         
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
      *  CHECK FOR F315-CDE-TRANS = 'SA' THEN READ SOC MASTER FILE   *          
      ****************************************************************          
       300-PROCESS-RECORD.                                                      
      *-------------------------------------------------------------            
           IF F315-CDE-TRANS = 'SA' AND                                         
              F315-IND-TP-SA = 'Y'                                              
              PERFORM  400-READ-BP13K893-RECORD   THRU   400-EXIT               
           END-IF.                                                              
                                                                                
           PERFORM     200-READ-INPUT-BP13F315    THRU   200-EXIT.              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
      *  CHECK F315-REGN-NO IF EXISTING IN SOC MASTER FILE           *          
      ****************************************************************          
       400-READ-BP13K893-RECORD.                                                
      *-------------------------------------------------------------            
           MOVE SPACES                           TO BP13K893-MASTER.            
           INITIALIZE                               BP13K893-MASTER.            
                                                                                
           MOVE    F315-REGN-NO       TO    K893-NUM-REGN-HIST.                 
                                                                                
           MOVE 'N'                              TO WS-K893-EOF                 
                                                    WS-K893-FND                 
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K893-STATUS                                              
              WHEN 00                                                           
                 PERFORM 425-READNEXT-BP13K893 THRU 425-EXIT                    
                   UNTIL K893-NUM-REGN-HIST NOT = F315-REGN-NO                  
                      OR WS-K893-EOF = 'Y'                                      
              WHEN 10                                                           
              WHEN 23                                                           
              DISPLAY 'RECORD NOT FOUND IN K893, STAT = ' WS-K893-STATUS        
              DISPLAY '           K893-NUM-REGN       = ' F315-REGN-NO          
              WHEN OTHER                                                        
              DISPLAY 'ERROR READING BP13K893, STATUS = ' WS-K893-STATUS        
              DISPLAY '           K893-NUM-REGN       = ' F315-REGN-NO          
              MOVE     WS-K893-STATUS      TO     RETURN-CODE                   
              PERFORM  900-CLOSE-ROUTINE   THRU   900-EXIT                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
           IF K893-FND                                                          
              PERFORM 500-UPDATE-BP13K893  THRU 500-EXIT                        
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       425-READNEXT-BP13K893.                                                   
      *-------------------------------------------------------------*           
                                                                                
           READ BP13K893 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                         TO WS-K893-EOF                 
           END-READ.                                                            
                                                                                
           EVALUATE WS-K893-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K893-NUM-REGN-HIST = F315-REGN-NO                           
                    MOVE 'Y'            TO  WS-K893-FND                         
                    MOVE 'Y'            TO  WS-K893-EOF                         
                    ADD  1              TO  WS-READ-K893-CNT                    
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
              DISPLAY 'RECORD NOT FOUND IN K893, STAT = ' WS-K893-STATUS        
              DISPLAY '           K893-NUM-REGN       = ' F315-REGN-NO          
              MOVE 'N'            TO  WS-K893-FND                               
              WHEN OTHER                                                        
              DISPLAY 'ERROR READING BP13K893, STATUS = ' WS-K893-STATUS        
              DISPLAY '           K893-NUM-REGN       = ' F315-REGN-NO          
              MOVE     WS-K893-STATUS      TO     RETURN-CODE                   
              PERFORM  900-CLOSE-ROUTINE   THRU   900-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       425-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
      ****************************************************************          
      *  REWRITE SOC APPLICATION MASTER RECORD (BP13K893)            *          
      ****************************************************************          
       500-UPDATE-BP13K893.                                                     
      *-------------------------------------------------------------            
           MOVE   FUNCTION CURRENT-DATE(1:8)   TO   K893-DTE-UPDATE.            
           MOVE   'Y'  TO   K893-NUM-RELEASE-PAYMENT.                           
                                                                                
           REWRITE BP13K893-MASTER.                                             
                                                                                
           IF WS-K893-STATUS =  00                                              
              ADD     1   TO WS-UPDT-K893-CNT                                   
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING BP13K893, STAT = ' WS-K893-STATUS         
              DISPLAY '               K893-NUM-REGN  = ' K893-NUM-REGN          
              MOVE     WS-K893-STATUS      TO     RETURN-CODE                   
              PERFORM  900-CLOSE-ROUTINE   THRU   900-EXIT                      
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-DISPLAY-STATISTICS.                                                  
      *-------------------------------------------------------------            
           MOVE  WS-READ-F315-CNT   TO   WS-EDITED-READ-F315.                   
           MOVE  WS-READ-K893-CNT   TO   WS-EDITED-READ-K893.                   
           MOVE  WS-UPDT-K893-CNT   TO   WS-EDITED-UPDT-K893.                   
                                                                                
           DISPLAY '*--------- BP13C32A CONTROL TOTALS ---------*'.             
           DISPLAY '  '.                                                        
           DISPLAY ' NO. OF RECORDS READ  FROM BP13F315  = '                    
                     WS-EDITED-READ-F315.                                       
           DISPLAY '  '.                                                        
           DISPLAY ' NO. OF RECORDS READ  FROM BP13K893  = '                    
                     WS-EDITED-READ-K893.                                       
           DISPLAY ' NO. OF RECORDS UPDATED TO BP13K893  = '                    
                     WS-EDITED-UPDT-K893.                                       
           DISPLAY '*-------------------------------------------*'.             
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           CLOSE   BP13F315                                                     
                   BP13K893.                                                    
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K893, STATUS = ' WS-K893-STATUS        
              MOVE     WS-K893-STATUS    TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
