      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C129.                                                 
      *AUTHOR.        ERWIN GAW.                                                
      *DATE-WRITTEN.  22/03/96.                                                 
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME  :    SYSTEM OF COMMITMENT   (SOC)               *        
      *                                                                *        
      *   SYSTEM ID    :    BP13                                       *        
      *                                                                *        
      *   OBJECTIVE    :    TO PRINT PAYMENT VOUCHER FOR SUB-TRANSFER  *        
      *                                                                *        
      *   INPUT FILES  :    BP13F885                                   *        
      *                     BP13K800                                   *        
      *                     BP13K130                                   *        
      *                                                                *        
      *   REPORT       :    1. BP13L129                                *        
      *                                                                *        
      *   MODIFICATION HISTORY:                                        *        
      *   =====================                                        *        
      *   CHGE-NO  BY    DATE   DESCRIPTION                            *        
      *   -------  ---  ------  -------------------------------------  *        
      * BP130094   EG1  22031996 TO CATER FOR SUB-TRANSFER MODULE.     *        
      * BP130418   RHB  05061998 Y2K                                   *        
      * BP130883   RAS  03082000 TO CHANGE BP13C904 TO BP13C913        *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT    DIVISION.                                                 
      ******************************************************************        
      *================================================================*        
       CONFIGURATION  SECTION.                                                  
      *================================================================*        
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
                                                                                
           SELECT BP13F885  ASSIGN       TO BP13F885.                           
           SELECT BP13K885  ASSIGN       TO BP13K885                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K885-KEY-FLD                        
                            FILE STATUS  IS K885-STATUS.                        
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS K800-STATUS.                        
           SELECT BP13K130  ASSIGN       TO BP13K130                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K130-KEY-FLD                        
                            FILE STATUS  IS K130-STATUS.                        
           SELECT BP13L129  ASSIGN       TO BP13L129.                           
                                                                                
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
                                                                                
       FD   BP13F885                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13F885.                                                           
                                                                                
       FD   BP13K885                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K885.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K130                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K130.                                                           
                                                                                
       FD   BP13L129                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   L129-REC            PIC X(132).                                     
                                                                                
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *     LINK  VARIABLES                                            *        
      *----------------------------------------------------------------*        
      *----------------------------------------------------------------*        
      *---LINKAGE-VARIABLES-PASS-TO-SUB-PROGRAM--BP13C913--------------*        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH      PIC X(4)  VALUE SPACES.                 
               10  WS-LINK-NUM-ACC      PIC X(5)  VALUE SPACES.                 
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *     ERROR VARIABLES                                            *        
      *----------------------------------------------------------------*        
       01  WS-ERR-MSG                     PIC X(80).                            
                                                                                
      *----------------------------------------------------------------*        
      *     FLAG VARIABLES                                             *        
      *----------------------------------------------------------------*        
       01  WS-FLAGS.                                                            
           05  WS-F885-EOF                PIC X(1)   VALUE 'N'.                 
           05  WS-K130-EOF                PIC X(1)   VALUE 'N'.                 
           05  WS-K885-EOF                PIC X(1)   VALUE 'N'.                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  K885-STATUS                PIC X(2).                             
           05  K800-STATUS                PIC X(2).                             
           05  K130-STATUS                PIC X(2).                             
                                                                                
      *----------------------------------------------------------------*        
      *    COUNTER VARIABLES                                           *        
      *----------------------------------------------------------------*        
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F885-READ           PIC 9(8)   VALUE ZEROES.              
           05  WS-CNT-K885-REWRITE        PIC 9(8)   VALUE ZEROES.              
           05  WS-CNT-L129-WRITE          PIC 9(8)   VALUE ZEROES.              
                                                                                
      *----------------------------------------------------------------*        
      *    PRINTING VARIABLES                                          *        
      *----------------------------------------------------------------*        
       01  WS-PRINT-VARIABLES.                                                  
           05  WS-SEQ-CNT                 PIC 9(2)   VALUE ZEROES.              
           05  WS-PAGE-CNT                PIC 9(5)   VALUE 0.                   
           05  WS-LINE-CNT                PIC 9(2)   VALUE 0.                   
                                                                                
      *----------------------------------------------------------------*        
      *  MISCELLEANEOUS VARIABLES                                      *        
      *----------------------------------------------------------------*        
       01  WS-STRING-POINTER              PIC 9(2).                             
       01  WS-COMPUTE-VARIABLES.                                                
           05  WS-AMT-SUSPENSE            PIC S9(8)V99 VALUE +0.                
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CC                 PIC X(2).                             
           05  WS-CURR-YY                 PIC X(2).                             
           05  WS-CURR-MM                 PIC X(2).                             
           05  WS-CURR-DD                 PIC X(2).                             
                                                                                
      *----------------------------------------------------------------*        
      *    REPORT LAYOUT OF  VOUCHER LIST.                             *        
      *----------------------------------------------------------------*        
       01  L129-PRT-HDR-1.                                                      
           05  FILLER                     PIC X      VALUE SPACE.               
           05  FILLER                     PIC X(8)   VALUE 'BP13L129'.          
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  FILLER                     PIC X(4)   VALUE 'HDB3'.              
           05  FILLER                     PIC X(28)  VALUE SPACES.              
           05  FILLER                     PIC X(39)                             
               VALUE 'S Y S T E M   O F   C O M M I T M E N T'.                 
           05  FILLER                     PIC X(05)  VALUE SPACES.              
           05  FILLER                     PIC X(9)   VALUE 'PAGE   : '.         
           05  L129-PRT-PAGE              PIC ZZZZ9.                            
           05  FILLER                     PIC X(1)   VALUE SPACES.              
           05  FILLER                     PIC X(7)   VALUE 'DATE : '.           
           05  L129-PRT-DATE              PIC X(10)  VALUE SPACES.              
                                                                                
       01  L129-PRT-HDR-2.                                                      
           05  FILLER                     PIC X(90)  VALUE SPACES.              
           05  FILLER                     PIC X(23)                             
               VALUE 'SEND TO: AGREEMENT TEAM'.                                 
                                                                                
       01  L129-PRT-HDR-3.                                                      
           05  FILLER                     PIC X(43)  VALUE SPACES.              
           05  FILLER                     PIC X(46)                             
               VALUE 'LIST FOR REFUND TO TRANSFEROR FOR SUB-TRANSFER'.          
                                                                                
       01  L129-PRT-HDR-4.                                                      
           05  FILLER                     PIC X(05)  VALUE SPACES.              
           05  FILLER                     PIC X(14)                             
               VALUE 'REGN        : '.                                          
           05  L129-NEW-TOWN              PIC X(02).                            
           05  FILLER                     PIC X(01)  VALUE '/'.                 
           05  L129-FLAT-TYPE             PIC X(02).                            
           05  FILLER                     PIC X(01)  VALUE '/'.                 
           05  L129-REGN-NO               PIC X(08).                            
           05  FILLER                     PIC X(20)  VALUE SPACES.              
           05  FILLER                     PIC X(14)                             
               VALUE 'SCHEME ACCT : '.                                          
           05  L129-SCH-ACC               PIC X(14).                            
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(19)                             
               VALUE 'SUB-TRANSFER DATE: '.                                     
           05  L129-DTE-SUB-TRANS         PIC X(10).                            
                                                                                
       01  L129-PRT-HDR-5.                                                      
           05  FILLER                     PIC X(05)  VALUE SPACES.              
           05  FILLER                     PIC X(14)                             
               VALUE 'FLAT ADDRESS: '.                                          
           05  L129-ADDRESS-STRING        PIC X(80).                            
                                                                                
       01  L129-PRT-HDR-6.                                                      
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE 'S/N'.               
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(66)                             
               VALUE '               NAME OF TRANSFEROR'.                       
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(08)                             
               VALUE 'CPF NO. '.                                                
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(12)                             
               VALUE ' AMT REFUND '.                                            
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(15)                             
               VALUE 'NCN NO (T ACCT)'.                                         
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(13)                             
               VALUE 'T''REE CPF NO.'.                                          
                                                                                
       01  L129-PRT-HDR-7.                                                      
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE '==='.               
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(66)  VALUE ALL '='.             
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(09)                             
               VALUE '========='.                                               
           05  FILLER                     PIC X(01)  VALUE SPACES.              
           05  FILLER                     PIC X(12)                             
               VALUE '============'.                                            
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(15)                             
               VALUE '==============='.                                         
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(13)                             
               VALUE '============='.                                           
                                                                                
       01  L129-PRT-DETAIL.                                                     
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L129-SER-NO                PIC ZZ9.                              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L129-TROR-NAME             PIC X(66).                            
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L129-TROR-CPF              PIC X(09).                            
           05  FILLER                     PIC X(01)  VALUE SPACES.              
           05  L129-AMT-REFUND            PIC $,$$$,$$9.99.                     
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L129-NCN-NO-T-ACCT         PIC X(15).                            
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L129-TREE-CPF              PIC X(09).                            
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
           PERFORM 1000-INITIALIZATION      THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-ROUTINE     THRU 2000-EXIT                      
                   UNTIL WS-F885-EOF = 'Y'.                                     
           PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  BP13F885                                                 
                       BP13K800                                                 
                       BP13K130                                                 
                I-O    BP13K885                                                 
                OUTPUT BP13L129.                                                
                                                                                
           IF K885-STATUS NOT EQUAL 00 AND 97                                   
              STRING 'BP13K885 OPENING ERROR (' K885-STATUS ')'                 
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT EQUAL 00 AND 97                                   
              STRING 'BP13K800 OPENING ERROR (' K800-STATUS ')'                 
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           IF K130-STATUS NOT EQUAL 00 AND 97                                   
              STRING 'BP13K130 OPENING ERROR (' K130-STATUS ')'                 
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
           READ  BP13F885 AT END                                                
                 MOVE 'Y'          TO  WS-F885-EOF.                             
                                                                                
           IF WS-F885-EOF = 'Y'                                                 
              STRING 'NO INPUT F885 RECORDS FOUND'                              
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT                   
           ELSE                                                                 
              ADD 1                TO WS-CNT-F885-READ                          
           END-IF.                                                              
           MOVE 60                 TO WS-LINE-CNT.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       2000-PROCESS-ROUTINE.                                                    
      *================================================================*        
           PERFORM 3000-GET-GEN-INFO     THRU 3000-EXIT.                        
           PERFORM 3100-START-FILES      THRU 3100-EXIT.                        
                                                                                
           IF WS-K130-EOF NOT = 'Y' OR WS-K885-EOF NOT = 'Y'                    
              PERFORM 2300-FMT-HDR-INFO  THRU 2300-EXIT                         
              PERFORM 6050-PRT-HEADING   THRU 6050-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE 0                     TO WS-SEQ-CNT.                            
           PERFORM UNTIL WS-K130-EOF = 'Y' AND WS-K885-EOF = 'Y'                
              MOVE SPACES             TO L129-PRT-DETAIL                        
              ADD  1                  TO WS-SEQ-CNT                             
              MOVE WS-SEQ-CNT         TO L129-SER-NO                            
              IF WS-K885-EOF NOT = 'Y'                                          
                 PERFORM 2100-FMT-K885-INFO     THRU 2100-EXIT                  
              END-IF                                                            
              IF WS-K130-EOF NOT = 'Y'                                          
                 PERFORM 2200-FMT-K130-INFO     THRU 2200-EXIT                  
              END-IF                                                            
              PERFORM 6000-PRT-L129-LIST        THRU 6000-EXIT                  
              IF WS-K885-EOF NOT = 'Y'                                          
                 MOVE 'Y'                TO K885-NUM-PRINT-STATUS               
                 MOVE WS-CURRENT-DATE    TO K885-DTE-UPDATE                     
                 PERFORM 8400-REWRITE-BP13K885  THRU 8400-EXIT                  
                 PERFORM 8150-READNEXT-BP13K885 THRU 8150-EXIT                  
              END-IF                                                            
              IF WS-K130-EOF NOT = 'Y'                                          
                 PERFORM 8250-READNEXT-BP13K130 THRU 8250-EXIT                  
                   WITH  TEST AFTER                                             
                   UNTIL WS-K130-EOF = 'Y' OR                                   
                         K130-PAYMENT-TYPE = 'SUS'                              
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           READ  BP13F885 AT END                                                
                 MOVE 'Y'        TO WS-F885-EOF                                 
                 GO  TO 2000-EXIT                                               
                 END-READ.                                                      
                                                                                
           ADD   1   TO  WS-CNT-F885-READ.                                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       2100-FMT-K885-INFO.                                                      
      *================================================================*        
           COMPUTE WS-AMT-SUSPENSE = K885-AMT-REFUND + K885-AMT-INTEREST        
           IF K885-AMT-SUSPENSE NOT = WS-AMT-SUSPENSE                           
              STRING 'SUSPENSE AMT DOES NOT TALLY (' F885-NUM-REGN ')'          
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE K885-NME-NAME              TO L129-TROR-NAME.                   
           MOVE K885-NUM-CPF               TO L129-TROR-CPF.                    
           MOVE K885-AMT-SUSPENSE          TO L129-AMT-REFUND.                  
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       2200-FMT-K130-INFO.                                                      
      *================================================================*        
           MOVE K130-NUM-RECPT-JRNO        TO L129-NCN-NO-T-ACCT.               
           MOVE K130-NUM-CPF-APPL          TO L129-TREE-CPF.                    
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       2300-FMT-HDR-INFO.                                                       
      *================================================================*        
           ADD  1                          TO WS-PAGE-CNT.                      
           MOVE 8                          TO WS-LINE-CNT.                      
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY           
                  DELIMITED BY SIZE      INTO L129-PRT-DATE.                    
           MOVE WS-PAGE-CNT                TO L129-PRT-PAGE.                    
           MOVE K800-NUM-NEW-TOWN          TO L129-NEW-TOWN.                    
           MOVE K800-NUM-FLAT-TYPE         TO L129-FLAT-TYPE.                   
           MOVE K800-NUM-REGN              TO L129-REGN-NO.                     
                                                                                
           STRING K800-NUM-SCH '-' K800-NUM-ACC '-' K800-NUM-ACC-CK             
                  '-' K800-NUM-LESSEE-CODE DELIMITED BY SIZE                    
                                         INTO L129-SCH-ACC.                     
                                                                                
           STRING K800-DTE-SUB-TRANSFER(7:2) '/'                                
                  K800-DTE-SUB-TRANSFER(5:2) '/'                                
                  K800-DTE-SUB-TRANSFER(1:4) DELIMITED BY SIZE                  
                                         INTO L129-DTE-SUB-TRANS.               
                                                                                
           MOVE 1                    TO WS-STRING-POINTER.                      
           STRING 'BLK. ' COMM8-NUM-BLK '  '                                    
                  DELIMITED BY SIZE INTO L129-ADDRESS-STRING                    
                       WITH POINTER WS-STRING-POINTER.                          
                                                                                
           STRING '#' COMM8-NUM-LEVEL  '-' COMM8-NUM-UNIT-MAIN                  
                  DELIMITED BY SIZE INTO L129-ADDRESS-STRING                    
                       WITH POINTER WS-STRING-POINTER.                          
                                                                                
           IF COMM8-NUM-UNIT-SUB > SPACES                                       
              STRING '-' COMM8-NUM-UNIT-SUB ' '                                 
                  DELIMITED BY SIZE INTO L129-ADDRESS-STRING                    
                       WITH POINTER WS-STRING-POINTER                           
           END-IF.                                                              
                                                                                
           STRING COMM8-NME-STREET ' S(' COMM8-NUM-POSTAL-CODE ')'              
                  DELIMITED BY SIZE INTO L129-ADDRESS-STRING                    
                       WITH POINTER WS-STRING-POINTER.                          
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3000-GET-GEN-INFO.                                                       
      *================================================================*        
           MOVE F885-NUM-REGN              TO K800-NUM-REGN.                    
           PERFORM  8300-READ-BP13K800     THRU 8300-EXIT.                      
                                                                                
           MOVE SPACES                     TO BP13COMM8-REC.                    
           INITIALIZE                         BP13COMM8-REC.                    
                                                                                
           MOVE K800-NUM-SCH-ACC           TO WS-LINK-REC.                      
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC BP13COMM8-REC.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3100-START-FILES.                                                        
      *================================================================*        
           MOVE LOW-VALUES                 TO K885-KEY-FLD.                     
           MOVE F885-NUM-REGN              TO K885-NUM-REGN.                    
           PERFORM  8100-START-BP13K885    THRU 8100-EXIT.                      
           PERFORM  8150-READNEXT-BP13K885 THRU 8150-EXIT.                      
                                                                                
           MOVE LOW-VALUES                 TO K130-KEY-FLD.                     
           MOVE F885-NUM-REGN              TO K130-NUM-ORIG-REGN.               
           MOVE K800-DTE-SUB-TRANSFER      TO K130-DTE-POST.                    
           PERFORM  8200-START-BP13K130    THRU 8200-EXIT.                      
           PERFORM  8250-READNEXT-BP13K130 THRU 8250-EXIT                       
             UNTIL  WS-K130-EOF = 'Y' OR                                        
                    K130-PAYMENT-TYPE = 'SUS'.                                  
                                                                                
           IF WS-K885-EOF NOT = 'Y'                                             
              IF K885-NUM-PRINT-STATUS = 'Y'                                    
                 STRING 'VOUCHER ALREADY ISSUED  FOR (' K885-NUM-REGN           
                        ')'  DELIMITED BY SIZE INTO WS-ERR-MSG                  
                 PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       6000-PRT-L129-LIST.                                                      
      *================================================================*        
           WRITE L129-REC      FROM L129-PRT-DETAIL AFTER 2.                    
           ADD   2             TO   WS-LINE-CNT.                                
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       6050-PRT-HEADING.                                                        
      *================================================================*        
           WRITE L129-REC    FROM L129-PRT-HDR-1   AFTER PAGE.                  
           WRITE L129-REC    FROM L129-PRT-HDR-2   AFTER 1.                     
           WRITE L129-REC    FROM L129-PRT-HDR-3   AFTER 1.                     
           WRITE L129-REC    FROM L129-PRT-HDR-4   AFTER 2.                     
           WRITE L129-REC    FROM L129-PRT-HDR-5   AFTER 1.                     
           WRITE L129-REC    FROM L129-PRT-HDR-6   AFTER 2.                     
           WRITE L129-REC    FROM L129-PRT-HDR-7   AFTER 1.                     
                                                                                
           ADD 1                     TO WS-CNT-L129-WRITE.                      
                                                                                
       6050-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8100-START-BP13K885.                                                     
      *================================================================*        
            MOVE 'N'                 TO WS-K885-EOF.                            
            START BP13K885 KEY >= K885-KEY-FLD                                  
                  INVALID KEY  MOVE 'Y'    TO WS-K885-EOF                       
                               GO TO 8100-EXIT.                                 
                                                                                
            IF K885-STATUS NOT = '00'                                           
               STRING 'BP13K885 START ERROR CODE(' K885-STATUS ')'              
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8150-READNEXT-BP13K885.                                                  
      *================================================================*        
            READ  BP13K885 NEXT                                                 
                  AT END   MOVE 'Y'        TO WS-K885-EOF                       
                           GO TO 8150-EXIT.                                     
                                                                                
            IF K885-STATUS NOT = '00'                                           
               STRING 'BP13K885 READ NEXT ERROR CODE(' K885-STATUS ')'          
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF K885-NUM-REGN NOT = F885-NUM-REGN                                
               MOVE 'Y'            TO WS-K885-EOF                               
            END-IF.                                                             
                                                                                
       8150-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8200-START-BP13K130.                                                     
      *================================================================*        
            MOVE 'N'                 TO WS-K130-EOF.                            
            START BP13K130 KEY >= K130-KEY-FLD                                  
                  INVALID KEY  MOVE 'Y'    TO WS-K130-EOF                       
                               GO TO 8200-EXIT.                                 
                                                                                
            IF K130-STATUS NOT = '00'                                           
               STRING 'BP13K130 START ERROR CODE(' K130-STATUS ')'              
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8250-READNEXT-BP13K130.                                                  
      *================================================================*        
            READ  BP13K130 NEXT                                                 
                  AT END   MOVE 'Y'        TO WS-K130-EOF                       
                           GO TO 8250-EXIT.                                     
                                                                                
            IF K130-STATUS NOT = '00'                                           
               STRING 'BP13K130 READ NEXT ERROR CODE(' K130-STATUS ')'          
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF K130-NUM-ORIG-REGN NOT = F885-NUM-REGN                           
               MOVE 'Y'            TO WS-K130-EOF                               
            END-IF.                                                             
                                                                                
       8250-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8300-READ-BP13K800.                                                      
      *================================================================*        
            READ  BP13K800.                                                     
                                                                                
            IF K800-STATUS NOT = '00'                                           
               STRING 'BP13K800 READ ERROR CODE(' K800-STATUS ')'               
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
       8300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8400-REWRITE-BP13K885.                                                   
      *================================================================*        
            REWRITE BP13K885-REC.                                               
                                                                                
            IF K885-STATUS NOT = '00'                                           
               STRING 'BP13K885 REWRITE ERROR CODE(' K885-STATUS ')'            
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            ELSE                                                                
               ADD  1                     TO WS-CNT-K885-REWRITE                
            END-IF.                                                             
                                                                                
       8400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9900-HANDLE-ABEND.                                                       
      *================================================================*        
            CLOSE   BP13F885                                                    
                    BP13K800                                                    
                    BP13K885                                                    
                    BP13K130                                                    
                    BP13L129.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C129  ABEND TOTAL            *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF F885 READ : ' WS-CNT-F885-READ.                   
            DISPLAY '* NO. OF K885 REWR.: ' WS-CNT-K885-REWRITE.                
            DISPLAY '* NO. OF L129 CASES: ' WS-CNT-L129-WRITE.                  
            DISPLAY '***************************************'                   
            DISPLAY '* ABEND DESC: ' WS-ERR-MSG.                                
            MOVE '4095'               TO RETURN-CODE.                           
                                                                                
            STOP RUN.                                                           
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9999-CLOSE-FILES.                                                        
      *================================================================*        
            CLOSE   BP13F885                                                    
                    BP13K800                                                    
                    BP13K885                                                    
                    BP13K130                                                    
                    BP13L129.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C129  CONTROL TOTAL          *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF F885 READ : ' WS-CNT-F885-READ.                   
            DISPLAY '* NO. OF K885 REWR.: ' WS-CNT-K885-REWRITE.                
            DISPLAY '* NO. OF L129 CASES: ' WS-CNT-L129-WRITE.                  
            DISPLAY '* ABEND DESC: ' WS-ERR-MSG.                                
            DISPLAY '***************************************'                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
