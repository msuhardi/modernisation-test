       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB9H.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  26 JUL 2016.                                              
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  MATCH BP13F740 (8AM) WITH BP13F740(11AM),     *                
      *          OUTPUT NEW CASES CAME IN AFTER 8AM            *                
      *          OUTPUT CASES FOUND IN 8AM FILE WITH CHANGES   *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP136363 FNP1 260716  NEW PROGRAM                      *                
      * BP136902 SMR2 110817  CHECK THE DIFFERENCE OF NTIMER/  *                
      *                       ELDERLY/ALLOC-SCH/ELIG-SCH/PPO   *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
           SELECT P13F730A ASSIGN TO P13F730A.                                  
           SELECT P13F730B ASSIGN TO P13F730B.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F740.                                                           
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD  P13F730A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       01  P13F730A-REC        PIC X(500).                                      
                                                                                
       FD  P13F730B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       01  P13F730B-REC        PIC X(500).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F740-READ          PIC 9(7)    VALUE ZEROES.                   
           05 WS-F730-READ          PIC 9(7)    VALUE ZEROES.                   
           05 WS-F730-WRITE-NEW     PIC 9(7)    VALUE ZEROES.                   
           05 WS-F730-WRITE-OLD     PIC 9(7)    VALUE ZEROES.                   
           05 WS-F730-NO-CHANGE     PIC 9(7)    VALUE ZEROES.                   
           05 WS-F730-WITH-CHANGE   PIC 9(7)    VALUE ZEROES.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF          PIC X       VALUE 'N'.                      
           05  WS-F740-EOF          PIC X       VALUE 'N'.                      
           05  WS-DIFF              PIC X       VALUE 'N'.                      
                                                                                
       01  WS-WORKING-FIELDS.                                                   
           05  WS-F730-MATCHED      PIC 9(8)    VALUE ZEROES.                   
           05  WS-F730-NOMATCH      PIC 9(8)    VALUE ZEROES.                   
           05  WS-F740-NOMATCH      PIC 9(8)    VALUE ZEROES.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-BP13F740   THRU 1100-EXIT.                         
           PERFORM 1200-READ-BP13F730   THRU 1200-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL F740-NUM-REGN   = HIGH-VALUES AND                            
                   F730-NUM-REGN   = HIGH-VALUES.                               
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1000-OPEN-ROUTINE.                                                       
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F740                                                 
                OUTPUT P13F730A                                                 
                       P13F730B.                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1100-READ-BP13F740.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F740 AT END                                                 
              MOVE HIGH-VALUES TO F740-NUM-REGN                                 
                GO TO 1100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F740-READ.                                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       1200-READ-BP13F730.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F730 AT END                                                 
              MOVE HIGH-VALUES TO F730-NUM-REGN                                 
                GO TO 1200-EXIT.                                                
                                                                                
           ADD 1 TO WS-F730-READ.                                               
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2000-PROCESS-RECORDS.                                                    
      *------------------------------------------------------------             
                                                                                
                                                                                
           IF F730-NUM-REGN    = F740-NUM-REGN                                  
              ADD 1 TO WS-F730-MATCHED                                          
              PERFORM 3000-CHECK-DIFFERENCE   THRU 3000-EXIT                    
              IF WS-DIFF = 'Y'                                                  
                 WRITE P13F730A-REC FROM BP13F730-REC                           
                 ADD 1 TO WS-F730-WRITE-NEW                                     
                 ADD 1 TO WS-F730-WITH-CHANGE                                   
              ELSE                                                              
                 WRITE P13F730B-REC FROM BP13F740-REC                           
                 ADD 1 TO WS-F730-NO-CHANGE                                     
                 ADD 1 TO WS-F730-WRITE-OLD                                     
              END-IF                                                            
              PERFORM 1200-READ-BP13F730    THRU 1200-EXIT                      
              PERFORM 1100-READ-BP13F740    THRU 1100-EXIT                      
           ELSE                                                                 
              IF F730-NUM-REGN    < F740-NUM-REGN                               
                 ADD 1 TO WS-F730-NOMATCH                                       
                 WRITE P13F730A-REC FROM BP13F730-REC                           
                 ADD 1 TO WS-F730-WRITE-NEW                                     
                 PERFORM 1200-READ-BP13F730    THRU 1200-EXIT                   
              ELSE                                                              
                 ADD 1 TO WS-F740-NOMATCH                                       
                 PERFORM 1100-READ-BP13F740     THRU 1100-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       3000-CHECK-DIFFERENCE.                                                   
      *------------------------------------------------------------             
           MOVE 'N'    TO WS-DIFF.                                              
                                                                                
           IF F730-CDE-HOUSEHOLD NOT = F740-CDE-HOUSEHOLD                       
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-FT2T = SPACES                                            
              MOVE 'N' TO F730-NUM-FT2T                                         
           END-IF.                                                              
           IF F740-NUM-FT2T = SPACES                                            
              MOVE 'N' TO F740-NUM-FT2T                                         
           END-IF.                                                              
                                                                                
           IF F730-NUM-FT2T NOT = F740-NUM-FT2T                                 
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-APPS NOT = F740-NUM-APPS                                 
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-APPS-ELIG-TAG NOT = F740-NUM-APPS-ELIG-TAG               
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-PPS-ELIG-TAG NOT = F740-NUM-PPS-ELIG-TAG                 
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-GEN3          NOT = F740-NUM-GEN3                        
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-AST           NOT = F740-NUM-AST                         
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-YOUNG-CHILD-AGE-SC NOT =                                 
                                 F740-NUM-YOUNG-CHILD-AGE-SC                    
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-YOUNG-CHILD-AGE-NSC NOT =                                
                                 F740-NUM-YOUNG-CHILD-AGE-NSC                   
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-PPS-SUCC   NOT = F740-NUM-PPS-SUCC                       
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-MGPS       NOT = F740-NUM-MGPS                           
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-MCPS       NOT = F740-NUM-MCPS                           
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-CDE-RESIDENT-HA1 NOT = F740-CDE-RESIDENT-HA1                 
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-CDE-RESIDENT-HA2 NOT = F740-CDE-RESIDENT-HA2                 
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-AST-SUCC      NOT = F740-NUM-AST-SUCC                    
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-NTIMER NOT = F740-NUM-NTIMER                             
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-ELDERLY NOT = F740-NUM-ELDERLY                           
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-CDE-ALLOC-SCH NOT = F740-CDE-ALLOC-SCH                       
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-CDE-ELIG-SCH NOT = F740-CDE-ELIG-SCH                         
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-PPO NOT = F740-NUM-PPO                                   
              MOVE 'Y' TO WS-DIFF                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********BP13CB9H END OF JOB*******************'.         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF REC READ FROM F730A    ' WS-F730-READ.                
           DISPLAY 'NO OF REC READ FROM F740     ' WS-F740-READ.                
           DISPLAY 'NO OF REC MATCHED W/ F740    ' WS-F730-MATCHED.             
           DISPLAY 'FOUND F730 NOTMTCH NEW CASE  ' WS-F730-NOMATCH.             
           DISPLAY 'FOUND F740 NOTMTCH ERROR     ' WS-F740-NOMATCH.             
           DISPLAY ' '.                                                         
           DISPLAY 'MATCHED NO CHANGE, NO OUTPUT ' WS-F730-NO-CHANGE.           
           DISPLAY 'MATCHED WITH CHANGE,  OUTPUT ' WS-F730-WITH-CHANGE.         
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF OUPUT, MATCH W DIFF    ' WS-F730-WRITE-OLD.           
           DISPLAY 'NO OF OUPUT, NEW CASES       ' WS-F730-WRITE-NEW.           
                                                                                
                                                                                
           CLOSE BP13F730                                                       
                 BP13F740                                                       
                 P13F730A                                                       
                 P13F730B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
