       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY50.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  10/06/2009.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : MATCH BP13F800 & BM06F120 TO GET ALL OCCUPIERS INFO*        
      *                                                                *        
      * INPUT  FILES : SY02F001                                        *        
      *                BM06F120                                        *        
      *                BP13F800                                        *        
      *                BP13K820                                        *        
      *                BP13K895                                        *        
      *                BP13K060                                        *        
      *                BP13K813                                        *        
      * OUTPUT FILES : P13F557A (BK)                                   *        
      *                P13F557B (TP)                                   *        
      *                P13F557C (CAN)                                  *        
      *                BP13F558                                        *        
      *                                                                *        
      * CHG-NO   BY    ON    - DESCRIPTION                             *        
      * -------------------------------------------------------------- *        
      * BP133653 JB8  10/06/09 NEW PROGRAM                             *        
      * BP133641 JB8  18/08/09 GET ACCEPANCE DATE FOR CANCEL CASES     *        
      * BP133891 PCL3 15/07/10 TO ADD F557-CDE-CITIZENSHIP-HOUSEHOLD   *        
      * BP134097 JB8  13/12/10 TO OUTPUT PRE-YEAR BOOKED CASES         *        
      * BP134495 IMC1 29/05/12 TO ADD DTE-UPDATE AND NUM-TRANS TO CANCEL        
      * BP13     IMC1 03/07/12 REPLACE BP13K816 WITH BP13K813                   
      * BP134803 IMC1 21/02/13 TO ADD CHILD INFO,APPS,MCPS,MGPS,PPS    *        
      * BP134512 IMC1 24/04/13 TO ADD NEW F800 OUTPUT FOR BOOKED CASES *        
      * BP135190 SMR2 04/12/13 EXPAND BP13K813 TO 1000                 *        
      * BP135615 IMC1 27/11/14 INCLUDE PROJECT NAME                    *        
      * BP136005 FNP1 301015 TO INCLUDE PCD QTR AND TENURE             *        
      * BP136320 ZDD1 270416 TO CATER FOR BLK NO. FT STATUS & PROTECTED*        
      * BP137214 ZDD1 120218 TO CHANGE PRE-YEAR TO 2 YRS.              *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   SY02F001      ASSIGN TO SY02F001.                           
           SELECT   BM06F120      ASSIGN TO BM06F120.                           
           SELECT   BP13F800      ASSIGN TO BP13F800.                           
                                                                                
           SELECT   BP13K060      ASSIGN TO BP13K060                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS RANDOM                              
                              RECORD KEY IS K060-KEY-FLD                        
                             FILE STATUS IS K060-STATUS.                        
                                                                                
           SELECT   BP13K813      ASSIGN TO BP13K813                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS RANDOM                              
                              RECORD KEY IS K813-KEY-FLD                        
                             FILE STATUS IS K813-STATUS.                        
                                                                                
           SELECT   BP13K800      ASSIGN TO BP13K800                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K800-NUM-REGN                       
                             FILE STATUS IS K800-STATUS.                        
                                                                                
           SELECT   BP13K820      ASSIGN TO BP13K820                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K820-KEY-FLD                        
                           ALTERNATE KEY IS K820-NUM-NRIC                       
                             FILE STATUS IS K820-STATUS.                        
                                                                                
           SELECT   BP13K893      ASSIGN TO BP13K893                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K893-KEY-FLD                        
                             FILE STATUS IS K893-STATUS.                        
                                                                                
           SELECT   BP13K895      ASSIGN TO BP13K895                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K895-KEY-FLD                        
                             FILE STATUS IS K895-STATUS.                        
                                                                                
           SELECT BM06K100        ASSIGN TO BM06K100                            
                             ACCESS MODE IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                              RECORD KEY IS K100-KEY-FLD                        
                             FILE STATUS IS K100-STATUS.                        
                                                                                
           SELECT   P13F557A      ASSIGN TO P13F557A.                           
           SELECT   P13F557B      ASSIGN TO P13F557B.                           
           SELECT   P13F557C      ASSIGN TO P13F557C.                           
           SELECT   P13F800A      ASSIGN TO P13F800A.                           
           SELECT   BP13F558      ASSIGN TO BP13F558.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BM06F120                                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F120.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K895                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K895.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   P13F557A                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   P13F557A-REC        PIC X(400).                                     
                                                                                
       FD   P13F557B                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   P13F557B-REC        PIC X(400).                                     
                                                                                
       FD   P13F557C                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   P13F557C-REC        PIC X(400).                                     
                                                                                
       FD   BP13F558                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       COPY BP13F558.                                                           
                                                                                
       FD   P13F800A                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   P13F800A-REC        PIC X(2000).                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       COPY DSGNTAB.                                                            
       COPY BP13F557.                                                           
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F120-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-F800-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-K820-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-K895-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-FND-K100                 PIC 9(7)  VALUE ZEROS.              
           05  CNT-F557-WRITE-BK            PIC 9(7)  VALUE ZEROS.              
           05  CNT-F557-WRITE-TP            PIC 9(7)  VALUE ZEROS.              
           05  CNT-F557-WRITE-CAN           PIC 9(7)  VALUE ZEROS.              
           05  CNT-F558-WRITE               PIC 9(7)  VALUE ZEROS.              
           05  CNT-F120-MATCH               PIC 9(7)  VALUE ZEROS.              
           05  CNT-F120-NOTMAT              PIC 9(7)  VALUE ZEROS.              
           05  CNT-CAN-NOT-CUR-YR           PIC 9(7)  VALUE ZEROS.              
           05  CNT-BK-NOT-CUR-YR            PIC 9(7)  VALUE ZEROS.              
           05  CNT-TP-NOT-CUR-YR            PIC 9(7)  VALUE ZEROS.              
           05  CNT-CHILD                    PIC 9(2)  VALUE ZEROS.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  K060-STATUS                  PIC 99    VALUE ZEROES.             
           05  K800-STATUS                  PIC 99    VALUE ZEROES.             
           05  K813-STATUS                  PIC 99    VALUE ZEROES.             
           05  K820-STATUS                  PIC 99    VALUE ZEROES.             
           05  K893-STATUS                  PIC 99    VALUE ZEROES.             
           05  K895-STATUS                  PIC 99    VALUE ZEROES.             
           05  K100-STATUS                  PIC 99    VALUE ZEROES.             
           05  WS-F120-MATCH                PIC X(1)  VALUE SPACE.              
               88  F120-MATCH               VALUE 'Y'.                          
           05  WS-K820-EOF                  PIC X(1)  VALUE SPACE.              
               88  K820-EOF                 VALUE 'Y'.                          
           05  WS-F800-FND                  PIC X(01) VALUE SPACE.              
               88  F800-FND                 VALUE 'Y'.                          
           05  WS-F120-FND                  PIC X(01) VALUE SPACE.              
               88  F120-FND                 VALUE 'Y'.                          
           05  WS-K820-FND                  PIC X(01) VALUE SPACE.              
               88  K820-FND                 VALUE 'Y'.                          
           05  WS-K800-FND                  PIC X(01) VALUE SPACE.              
               88  K800-FND                 VALUE 'Y'.                          
           05  WS-K895-EOF                  PIC X(1)  VALUE SPACE.              
               88  K895-EOF                 VALUE 'Y'.                          
           05  WS-K895-FND                  PIC X(01) VALUE SPACE.              
               88  K895-FND                 VALUE 'Y'.                          
           05  WS-HH-NRICS.                                                     
               10  WS-HH-NRIC OCCURS 20 TIMES                                   
                                            PIC X(09) VALUE SPACES.             
           05  WS-HH-SIZE                   PIC 99    VALUE ZEROES.             
           05  WS-NUM-AGE                   PIC 9(08) VALUE ZEROES.             
           05  WS-AGE REDEFINES WS-NUM-AGE.                                     
               10  WS-AGE-CY                PIC 9(04).                          
               10  WS-AGE-MM                PIC 9(02).                          
               10  WS-AGE-DD                PIC 9(02).                          
           05  WS-HA1-ETHNIC                PIC X(1)  VALUE SPACE.              
           05  WS-CDE-RACE                  PIC X(1)  VALUE SPACE.              
           05  WS-CHILD-BABY                PIC X(1)  VALUE SPACE.              
           05  WS-HA1-MS                    PIC X(01) VALUE SPACE.              
           05  WS-HA1-SEX                   PIC X(01) VALUE ZEROES.             
           05  WS-HA1-BIRTH                 PIC X(08) VALUE ZEROES.             
           05  WS-HA1-CTZN                  PIC X(02) VALUE ZEROES.             
           05  WS-NUM-REGN                  PIC 9(08) VALUE ZEROES.             
           05  WS-NUM-BIRTH                 PIC 9(08) VALUE ZEROES.             
           05  WS-CTR                       PIC 9(02) VALUE ZEROES.             
           05  WS-NEW-FLAT.                                                     
               10  WS-NF-NUM-REGN           PIC X(08).                          
               10  WS-NF-DTE-BALLOT         PIC X(06).                          
               10  WS-NF-NUM-NT             PIC X(03).                          
               10  WS-NF-NUM-FT             PIC X(02).                          
               10  WS-NF-SCH-ACC            PIC X(11).                          
           05  WS-PRE-YEAR                  PIC 9(04) VALUE ZEROES.             
           05  WS-PRE-YEAR-2                PIC 9(04) VALUE ZEROES.             
           05  WS-NUM-CUR-YEAR              PIC X(01) VALUE SPACES.             
           05  WS-PREV-AGE                  PIC 9(04) VALUE ZEROES.             
           05  WS-PREV-AGE-43               PIC S9(02) VALUE ZEROES.            
                                                                                
       01  WS-DTE-PACD.                                                         
           05 WS-DTE-PACD-YR            PIC X(04).                              
           05 WS-DTE-PACD-QTR           PIC X(02).                              
           05 WS-DTE-PACD-DD            PIC X(02)  VALUE '01'.                  
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
       0000-MAIN.                                                               
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BM06F120       THRU 2000-EXIT.                     
           PERFORM 2010-READ-BP13F800       THRU 2010-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL F120-SCH-ACC-NO = HIGH-VALUES AND                            
                   F800-NUM-SCH-ACC = HIGH-VALUES.                              
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================*                                                        
       1000-OPEN-FILES.                                                         
      *================*                                                        
                                                                                
           OPEN INPUT SY02F001                                                  
                      BM06F120                                                  
                      BP13F800                                                  
                      BP13K060                                                  
                      BP13K800                                                  
                      BP13K813                                                  
                      BP13K820                                                  
                      BP13K893                                                  
                      BP13K895                                                  
                      BM06K100                                                  
               OUTPUT P13F557A                                                  
                      P13F557B                                                  
                      P13F557C                                                  
                      P13F800A                                                  
                      BP13F558.                                                 
                                                                                
           READ SY02F001.                                                       
                                                                                
           MOVE F001-DTE-CURRENT(1:4)        TO WS-PRE-YEAR                     
           COMPUTE WS-PRE-YEAR = WS-PRE-YEAR  - 1                               
                                                                                
           MOVE F001-DTE-CURRENT(1:4)        TO WS-PRE-YEAR-2                   
           COMPUTE WS-PRE-YEAR-2 = WS-PRE-YEAR-2  - 2                           
                                                                                
           IF K060-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K060 , STATUS = ' K060-STATUS          
              MOVE K060-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K800 , STATUS = ' K800-STATUS          
              MOVE K800-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K813 , STATUS = ' K813-STATUS          
              MOVE K813-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K820 , STATUS = ' K820-STATUS          
              MOVE K820-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF K893-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K893 , STATUS = ' K893-STATUS          
              MOVE K893-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF K895-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K895 , STATUS = ' K895-STATUS          
              MOVE K895-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BM06K100 , STATUS = ' K100-STATUS          
              MOVE K100-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       2000-READ-BM06F120.                                                      
      *===================*                                                     
                                                                                
           READ BM06F120                                                        
             AT END                                                             
                MOVE HIGH-VALUES        TO F120-SCH-ACC-NO                      
                GO                      TO 2000-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO CNT-F120-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       2010-READ-BP13F800.                                                      
      *===================*                                                     
                                                                                
           READ BP13F800                                                        
             AT END                                                             
                MOVE HIGH-VALUES        TO F800-NUM-SCH-ACC                     
                GO                      TO 2010-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO CNT-F800-READ.                       
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       3000-PROCESS-RECORD.                                                     
      *===================*                                                     
                                                                                
           IF F800-NUM-SCH-ACC = F120-SCH-ACC-NO                                
              ADD 1                              TO CNT-F120-MATCH              
              PERFORM 3500-GENERATE-OUTPUT     THRU 3500-EXIT                   
                UNTIL F800-NUM-SCH-ACC NOT = F120-SCH-ACC-NO                    
                   OR F800-NUM-SCH-ACC = HIGH-VALUES                            
              PERFORM 2000-READ-BM06F120       THRU 2000-EXIT                   
           ELSE                                                                 
              IF F800-NUM-SCH-ACC > F120-SCH-ACC-NO                             
                 PERFORM 2000-READ-BM06F120    THRU 2000-EXIT                   
              ELSE                                                              
                 ADD   1                         TO CNT-F120-NOTMAT             
                 PERFORM 2010-READ-BP13F800    THRU 2010-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       3500-GENERATE-OUTPUT.                                                    
      *=====================*                                                   
                                                                                
           MOVE SPACES                           TO BP13F557-REC                
                                                    BP13F558-REC.               
           INITIALIZE                               BP13F557-REC                
                                                    BP13F558-REC.               
           MOVE SPACES                           TO WS-NUM-CUR-YEAR             
                                                    WS-CHILD-BABY.              
           MOVE ZEROES                           TO CNT-CHILD                   
                                                    WS-PREV-AGE                 
                                                    WS-PREV-AGE-43.             
                                                                                
           IF F800-NUM-ALLO-CAT = 'BTO'                                         
              PERFORM 7450-GET-PROJECT  THRU 7450-EXIT                          
           END-IF                                                               
                                                                                
           IF F800-NUM-STATUS = 'C'                                             
              IF F800-DTE-CANCEL(1:4) = F001-DTE-CURRENT(1:4)                   
                 PERFORM 3600-PROCESS-CAN      THRU 3600-EXIT                   
              END-IF                                                            
           ELSE                                                                 
              IF F120-DTE-KEY-ISSUED(1:4) = F001-DTE-CURRENT(1:4)               
                 PERFORM 3700-PROCESS-TP       THRU 3700-EXIT                   
              END-IF                                                            
              IF F120-DTE-ACCEPTANCE(1:4) = F001-DTE-CURRENT(1:4)               
                 MOVE 'Y'                        TO WS-NUM-CUR-YEAR             
                 PERFORM 3800-PROCESS-BK       THRU 3800-EXIT                   
              END-IF                                                            
              IF F120-DTE-ACCEPTANCE(1:4) = WS-PRE-YEAR OR                      
                 F120-DTE-ACCEPTANCE(1:4) = WS-PRE-YEAR-2                       
                 MOVE 'N'                        TO WS-NUM-CUR-YEAR             
                 PERFORM 3800-PROCESS-BK       THRU 3800-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2010-READ-BP13F800          THRU 2010-EXIT.                  
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       3600-PROCESS-CAN.                                                        
      *******************                                                       
                                                                                
           MOVE 'C'                       TO F557-NUM-STATUS.                   
           PERFORM 4000-READ-OCCUPIERS  THRU 4000-EXIT.                         
           PERFORM 5000-READ-APPLICANT  THRU 5000-EXIT.                         
           PERFORM 6000-READ-FIS        THRU 6000-EXIT                          
           PERFORM 7000-READ-NEW-FLAT   THRU 7000-EXIT                          
                                                                                
           MOVE F800-AMT-HSE-INCOME     TO   F557-AMT-INCOME-HH.                
                                                                                
      * MOVE REJECTION DATE AND REASON                                          
           MOVE BP13F800-MASTER(1482:8)   TO F557-DTE-SALE-EFF-RSL-HA1.         
           MOVE BP13F800-MASTER(1490:2)   TO F557-NUM-FT-RSL-HA1.               
                                                                                
           IF F557-NUM-YOUNG-CHILD-AGE = SPACES OR ZEROES OR LOW-VALUES         
              IF WS-PREV-AGE-43 < 0                                             
                 MOVE 'BB'                  TO F557-NUM-YOUNG-CHILD-AGE         
              ELSE                                                              
                 MOVE WS-PREV-AGE-43        TO F557-NUM-YOUNG-CHILD-AGE         
              END-IF                                                            
           END-IF                                                               
                                                                                
           MOVE F800-NUM-LEASE-TENURE     TO F557-NUM-LEASE-TENURE.             
           MOVE F800-NUM-ELDERLY          TO F557-NUM-ELDERLY.                  
           PERFORM 7700-READ-BM06K100     THRU 7700-EXIT.                       
                                                                                
           ADD 1                          TO CNT-F557-WRITE-CAN.                
           WRITE P13F557C-REC FROM BP13F557-REC.                                
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       3700-PROCESS-TP.                                                         
      ******************                                                        
                                                                                
           MOVE 'T'                       TO F557-NUM-STATUS.                   
                                                                                
           PERFORM 4000-READ-OCCUPIERS  THRU 4000-EXIT.                         
           PERFORM 5000-READ-APPLICANT  THRU 5000-EXIT.                         
           PERFORM 6000-READ-FIS        THRU 6000-EXIT                          
                                                                                
           MOVE F800-AMT-HSE-INCOME     TO   F557-AMT-INCOME-HH.                
                                                                                
           IF F557-NUM-YOUNG-CHILD-AGE = SPACES OR ZEROES OR LOW-VALUES         
              IF WS-PREV-AGE-43 < 0                                             
                 MOVE 'BB'                  TO F557-NUM-YOUNG-CHILD-AGE         
              ELSE                                                              
                 MOVE WS-PREV-AGE-43        TO F557-NUM-YOUNG-CHILD-AGE         
              END-IF                                                            
           END-IF                                                               
                                                                                
           MOVE F800-NUM-LEASE-TENURE     TO F557-NUM-LEASE-TENURE.             
           MOVE F800-NUM-ELDERLY          TO F557-NUM-ELDERLY.                  
           PERFORM 7700-READ-BM06K100     THRU 7700-EXIT.                       
                                                                                
           ADD 1                          TO CNT-F557-WRITE-TP.                 
           WRITE P13F557B-REC FROM BP13F557-REC.                                
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       3800-PROCESS-BK.                                                         
      ******************                                                        
                                                                                
           MOVE 'B'                       TO F557-NUM-STATUS.                   
           MOVE WS-NUM-CUR-YEAR           TO F557-NUM-CURR-YEAR.                
                                                                                
           PERFORM 4000-READ-OCCUPIERS  THRU 4000-EXIT.                         
           PERFORM 5000-READ-APPLICANT  THRU 5000-EXIT.                         
           PERFORM 6000-READ-FIS        THRU 6000-EXIT.                         
           PERFORM 3850-OUTPUT-BP13F558 THRU 3850-EXIT.                         
                                                                                
           IF F800-NUM-APPS-ELIG-TAG = 'P' OR 'H' OR 'M'                        
              MOVE SPACES                 TO F557-NUM-APPS                      
           ELSE                                                                 
              MOVE F800-NUM-APPS-ELIG-TAG TO F557-NUM-APPS                      
           END-IF                                                               
           IF F800-CDE-MCPS-REQ(2:1) = 'P' OR 'H' OR 'M'                        
              MOVE SPACES                 TO F557-NUM-MCPS-ELIG                 
           ELSE                                                                 
              MOVE F800-CDE-MCPS-REQ(2:1) TO F557-NUM-MCPS-ELIG                 
           END-IF                                                               
           MOVE F800-NUM-MGPS           TO F557-NUM-MGPS                        
           MOVE F800-NUM-PPS-ELIG       TO F557-NUM-PPS                         
           MOVE F800-NUM-FT2T           TO F557-NUM-FT2T                        
           MOVE F800-NUM-PPS-QUOTA      TO F557-NUM-PPS-QUOTA                   
                                                                                
           MOVE F800-AMT-HSE-INCOME     TO F557-AMT-INCOME-HH.                  
           ADD 1                        TO CNT-F557-WRITE-BK.                   
                                                                                
           IF F557-NUM-YOUNG-CHILD-AGE = SPACES OR ZEROES OR LOW-VALUES         
              IF WS-PREV-AGE-43 < 0                                             
                 MOVE 'BB'                  TO F557-NUM-YOUNG-CHILD-AGE         
              ELSE                                                              
                 MOVE WS-PREV-AGE-43        TO F557-NUM-YOUNG-CHILD-AGE         
              END-IF                                                            
           END-IF                                                               
                                                                                
           MOVE F800-NUM-LEASE-TENURE     TO F557-NUM-LEASE-TENURE.             
           MOVE F800-NUM-ELDERLY          TO F557-NUM-ELDERLY.                  
           PERFORM 7700-READ-BM06K100     THRU 7700-EXIT.                       
                                                                                
           WRITE P13F557A-REC FROM BP13F557-REC.                                
                                                                                
           WRITE P13F800A-REC FROM BP13F800-MASTER.                             
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       3850-OUTPUT-BP13F558.                                                    
      ***********************                                                   
      ** OUTPUT F558 FOR BP13U702                                               
      ** ONLY FOR BOOKED & NOT-KEY-ISSUED CASES                                 
      **                                                                        
           MOVE SPACES                    TO BP13F558-REC.                      
           INITIALIZE                        BP13F558-REC.                      
                                                                                
           MOVE F800-NUM-REGN             TO F558-NUM-REGN                      
           MOVE F800-NUM-NRIC1            TO F558-NUM-NRIC                      
           MOVE 'Y'                       TO F558-NUM-APPLN                     
           MOVE '00'                      TO F558-CDE-RELATIONSHIP              
           MOVE 'Y'                       TO F558-CDE-LESSEE-CHECK              
           MOVE WS-HA1-ETHNIC             TO F558-NUM-ETHNIC                    
           MOVE WS-HA1-MS                 TO F558-NUM-MARITAL-STATUS            
           MOVE WS-HA1-SEX                TO F558-NUM-SEX                       
           MOVE WS-HA1-BIRTH              TO F558-DTE-BIRTH                     
           MOVE WS-HA1-CTZN               TO F558-NUM-CITIZENSHIP               
           MOVE F800-NUM-ZONE             TO F558-NUM-ZONE-NT-SOC               
           MOVE F800-NUM-FLAT-TYPE        TO F558-NUM-FLAT-TY-SOC               
                                                                                
           ADD 1                          TO CNT-F558-WRITE.                    
           WRITE BP13F558-REC.                                                  
                                                                                
       3850-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       4000-READ-OCCUPIERS.                                                     
      *====================*                                                    
                                                                                
           MOVE 0                                 TO WS-HH-SIZE                 
                                                     WS-AGE.                    
           MOVE SPACES                            TO WS-HH-NRICS                
                                                     WS-HA1-ETHNIC.             
                                                                                
           PERFORM 4100-START-BP13K820          THRU 4100-EXIT.                 
           IF K820-FND                                                          
              PERFORM UNTIL K820-EOF                                            
                 PERFORM 4200-READNEXT-BP13K820 THRU 4200-EXIT                  
                 IF NOT K820-EOF                                                
                    PERFORM 4300-COMPUTE-AGE    THRU 4300-EXIT                  
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           IF WS-HH-SIZE > 0                                                    
              MOVE WS-HH-SIZE                     TO F557-NUM-HH-SIZE           
              MOVE CNT-CHILD                      TO F557-NUM-CHILD             
              IF CNT-CHILD > 0 AND                                              
                 WS-CHILD-BABY = 'Y'                                            
                 IF F557-NUM-YOUNG-CHILD-CITIZEN = 'SC'                         
                    MOVE 'BB'  TO F557-NUM-YOUNG-CHILD-AGE                      
                 END-IF                                                         
              ELSE                                                              
                 IF CNT-CHILD = 0                                               
                    MOVE 'NC'  TO F557-NUM-YOUNG-CHILD-AGE                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE 0                              TO WS-HH-SIZE                 
                                                     WS-AGE                     
              MOVE SPACES                         TO WS-HH-NRICS                
                                                                                
              PERFORM 4400-START-BP13K895       THRU 4400-EXIT                  
              IF K895-FND                                                       
                 PERFORM UNTIL K895-EOF                                         
                    PERFORM 4500-READNEXT-BP13K895 THRU 4500-EXIT               
                    IF NOT K895-EOF                                             
                       PERFORM 4600-COMPUTE-AGE THRU 4600-EXIT                  
                    END-IF                                                      
                 END-PERFORM                                                    
              END-IF                                                            
                                                                                
              MOVE WS-HH-SIZE                     TO F557-NUM-HH-SIZE           
              MOVE CNT-CHILD                      TO F557-NUM-CHILD             
                                                                                
              IF CNT-CHILD > 0 AND                                              
                 WS-CHILD-BABY = 'Y'                                            
                 MOVE 'BB'  TO F557-NUM-YOUNG-CHILD-AGE                         
              ELSE                                                              
                 IF CNT-CHILD = 0                                               
                    MOVE 'NC'  TO F557-NUM-YOUNG-CHILD-AGE                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       4100-START-BP13K820.                                                     
      *====================*                                                    
                                                                                
           MOVE SPACES                            TO BP13K820-REC               
           INITIALIZE                                BP13K820-REC.              
           MOVE 'N'                               TO WS-K820-EOF                
                                                     WS-K820-FND.               
                                                                                
           MOVE F800-NUM-REGN                     TO K820-NUM-REGN              
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           IF K820-STATUS = 00 OR 02                                            
              MOVE 'Y'                            TO WS-K820-FND                
           ELSE                                                                 
              IF K820-STATUS = 23 OR 10                                         
                 MOVE 'N'                         TO WS-K820-FND                
              ELSE                                                              
                 DISPLAY 'BP13K820 - ERROR STARTING : ' K820-STATUS             
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================*                                                 
       4200-READNEXT-BP13K820.                                                  
      *=======================*                                                 
                                                                                
           READ BP13K820 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K820-EOF                                      
                GO          TO 4200-EXIT                                        
           END-READ.                                                            
                                                                                
           IF K820-STATUS = 00 OR 02                                            
              IF K820-NUM-REGN = F800-NUM-REGN                                  
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE 'Y'                         TO WS-K820-EOF                
              END-IF                                                            
           ELSE                                                                 
              IF K820-STATUS = 23 OR 10                                         
                 MOVE 'Y'                         TO WS-K820-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K820 - ERROR READING NEXT : ' K820-STATUS         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==================*                                                      
       4300-COMPUTE-AGE.                                                        
      *==================*                                                      
           INITIALIZE WS-NUM-AGE.                                               
                                                                                
           IF F800-NUM-ALLO-CAT = 'BTO'                                         
              IF K813-DTE-OPEN-BTO NOT NUMERIC                                  
                 MOVE F800-DTE-REGN               TO WS-NUM-REGN                
              ELSE                                                              
                 MOVE K813-DTE-OPEN-BTO           TO WS-NUM-REGN                
              END-IF                                                            
           ELSE                                                                 
              MOVE F800-DTE-REGN                  TO WS-NUM-REGN                
           END-IF.                                                              
                                                                                
           IF K820-DTE-BIRTH NOT NUMERIC                                        
              MOVE ZEROES                         TO WS-NUM-BIRTH               
           ELSE                                                                 
              MOVE K820-DTE-BIRTH                 TO WS-NUM-BIRTH               
           END-IF.                                                              
           IF K820-AMT-INCOME NOT NUMERIC                                       
              MOVE ZEROES                         TO K820-AMT-INCOME            
           END-IF.                                                              
                                                                                
           ADD 1                                  TO WS-HH-SIZE.                
                                                                                
           IF K820-NUM-RELATIONSHIP = 41                                        
              ADD 1                 TO CNT-CHILD                                
           END-IF.                                                              
                                                                                
           IF WS-NUM-REGN > WS-NUM-BIRTH                                        
              COMPUTE WS-NUM-AGE = WS-NUM-REGN - WS-NUM-BIRTH                   
           ELSE                                                                 
              MOVE ZEROES       TO WS-AGE-CY                                    
              MOVE 'Y'          TO WS-CHILD-BABY                                
           END-IF.                                                              
                                                                                
           IF WS-AGE-MM > 0                                                     
              COMPUTE WS-AGE-CY = WS-AGE-CY + 1                                 
           END-IF.                                                              
                                                                                
           IF K820-NUM-RELATIONSHIP = 41                                        
              IF K820-NUM-MARITAL-STATUS = 1                                    
              IF K820-NUM-CITIZENSHIP = '10'                                    
                 IF WS-PREV-AGE = 0                                             
                    MOVE WS-AGE-CY        TO  WS-PREV-AGE                       
                 ELSE                                                           
                    IF WS-AGE-CY        < WS-PREV-AGE AND                       
                      (WS-AGE-CY NOT = SPACES AND LOW-VALUES AND ZEROES)        
                       MOVE WS-AGE-CY          TO WS-PREV-AGE                   
                    END-IF                                                      
                 END-IF                                                         
                 IF WS-CHILD-BABY = 'Y'                                         
                    MOVE 'BB'      TO F557-NUM-YOUNG-CHILD-AGE                  
                 ELSE                                                           
                    MOVE WS-PREV-AGE(3:2) TO F557-NUM-YOUNG-CHILD-AGE           
                 END-IF                                                         
              END-IF                                                            
              MOVE 'SC'           TO F557-NUM-YOUNG-CHILD-CITIZEN               
              END-IF                                                            
           ELSE                                                                 
              IF K820-NUM-RELATIONSHIP = 43                                     
              IF K820-NUM-CITIZENSHIP = '10'                                    
                 IF WS-PREV-AGE-43 = 0                                          
                    MOVE WS-AGE-CY        TO  WS-PREV-AGE-43                    
                 ELSE                                                           
                    IF WS-AGE-CY        < WS-PREV-AGE-43 AND                    
                      (WS-AGE-CY NOT = SPACES AND LOW-VALUES AND ZEROES)        
                       MOVE WS-AGE-CY     TO WS-PREV-AGE-43                     
                    END-IF                                                      
                 END-IF                                                         
                 IF WS-CHILD-BABY = 'Y'                                         
                    MOVE  -01      TO WS-PREV-AGE-43                            
                 END-IF                                                         
              END-IF                                                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
           EVALUATE K820-NUM-NRIC                                               
               WHEN F800-NUM-NRIC1                                              
                    MOVE WS-AGE-CY                TO F557-NUM-AGE-HA1           
                    MOVE K820-AMT-INCOME          TO F557-AMT-INCOME-HA1        
                    MOVE K820-NUM-ETHNIC          TO WS-HA1-ETHNIC              
                    MOVE K820-NUM-MARITAL-STATUS  TO WS-HA1-MS                  
                    MOVE K820-NUM-SEX             TO WS-HA1-SEX                 
                    MOVE K820-DTE-BIRTH           TO WS-HA1-BIRTH               
                    MOVE K820-NUM-CITIZENSHIP     TO WS-HA1-CTZN                
                    IF K820-NUM-MARITAL-STATUS IS NOT ALPHABETIC                
                       MOVE 44                      TO K060-SERIAL-NO           
                       MOVE K820-NUM-MARITAL-STATUS TO K060-CODE                
                       PERFORM 8000-READ-BP13K060   THRU 8000-EXIT              
                       MOVE K060-DESC(1:1)  TO F557-NUM-MARITAL-STATUS          
                    ELSE                                                        
                       MOVE K820-NUM-MARITAL-STATUS TO                          
                                      F557-NUM-MARITAL-STATUS                   
                    END-IF                                                      
               WHEN F800-NUM-NRIC2                                              
                    MOVE WS-AGE-CY                TO F557-NUM-AGE-HA2           
                    MOVE K820-AMT-INCOME          TO F557-AMT-INCOME-HA2        
               WHEN F800-NUM-NRIC3                                              
                    MOVE WS-AGE-CY                TO F557-NUM-AGE-HA3           
                    MOVE K820-AMT-INCOME          TO F557-AMT-INCOME-HA3        
               WHEN F800-NUM-NRIC4                                              
                    MOVE WS-AGE-CY                TO F557-NUM-AGE-HA4           
                    MOVE K820-AMT-INCOME          TO F557-AMT-INCOME-HA4        
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       4400-START-BP13K895.                                                     
      *====================*                                                    
                                                                                
           MOVE SPACES                            TO BP13K895-REC               
           INITIALIZE                                BP13K895-REC.              
           MOVE 'N'                               TO WS-K895-EOF                
                                                     WS-K895-FND.               
                                                                                
           MOVE F800-NUM-REGN                     TO K895-NUM-REGN-HIST         
                                                                                
           START BP13K895 KEY >= K895-KEY-FLD.                                  
                                                                                
           IF K895-STATUS = 00 OR 02                                            
              MOVE 'Y'                            TO WS-K895-FND                
           ELSE                                                                 
              IF K895-STATUS = 23 OR 10                                         
                 MOVE 'N'                         TO WS-K895-FND                
              ELSE                                                              
                 DISPLAY 'BP13K895 - ERROR STARTING : ' K895-STATUS             
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================*                                                 
       4500-READNEXT-BP13K895.                                                  
      *=======================*                                                 
                                                                                
           READ BP13K895 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K895-EOF                                      
                GO          TO 4500-EXIT                                        
           END-READ.                                                            
                                                                                
           IF K895-STATUS = 00 OR 02                                            
              IF K895-NUM-REGN-HIST = F800-NUM-REGN                             
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE 'Y'                         TO WS-K895-EOF                
              END-IF                                                            
           ELSE                                                                 
              IF K895-STATUS = 23 OR 10                                         
                 MOVE 'Y'                         TO WS-K895-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K895 - ERROR READING NEXT : ' K895-STATUS         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==================*                                                      
       4600-COMPUTE-AGE.                                                        
      *==================*                                                      
           INITIALIZE WS-NUM-AGE.                                               
                                                                                
           IF K813-DTE-OPEN-BTO NOT NUMERIC                                     
              MOVE F800-DTE-REGN                  TO WS-NUM-REGN                
           ELSE                                                                 
              MOVE K813-DTE-OPEN-BTO              TO WS-NUM-REGN                
           END-IF.                                                              
           IF K895-DTE-BIRTH NOT NUMERIC                                        
              MOVE ZEROES                         TO WS-NUM-BIRTH               
           ELSE                                                                 
              MOVE K895-DTE-BIRTH                 TO WS-NUM-BIRTH               
           END-IF.                                                              
           IF K895-AMT-INCOME NOT NUMERIC                                       
              MOVE ZEROES                         TO K895-AMT-INCOME            
           END-IF.                                                              
                                                                                
      ** CHECK MULTIPLE NRIC                                                    
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > WS-HH-SIZE         
              IF K895-NUM-NRIC-HIST = WS-HH-NRIC(WS-CTR)                        
                 GO TO 4600-EXIT                                                
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           ADD 1                             TO WS-HH-SIZE.                     
                                                                                
           IF K895-NUM-RELATIONSHIP = 41                                        
              ADD 1                 TO CNT-CHILD                                
           END-IF.                                                              
           MOVE K895-NUM-NRIC-HIST           TO WS-HH-NRIC(WS-HH-SIZE).         
                                                                                
           IF WS-NUM-REGN > WS-NUM-BIRTH                                        
              COMPUTE WS-NUM-AGE = WS-NUM-REGN - WS-NUM-BIRTH                   
           ELSE                                                                 
              MOVE ZEROES       TO WS-AGE-CY                                    
              MOVE 'Y'          TO WS-CHILD-BABY                                
           END-IF.                                                              
                                                                                
           IF WS-AGE-MM > 0                                                     
              COMPUTE WS-AGE-CY = WS-AGE-CY + 1                                 
           END-IF.                                                              
                                                                                
           IF K895-NUM-RELATIONSHIP = 41                                        
              IF K895-NUM-MARITAL-STATUS = 1                                    
              IF K895-NUM-CITIZENSHIP = '10'                                    
                 IF WS-PREV-AGE = 0                                             
                    MOVE WS-AGE-CY        TO  WS-PREV-AGE                       
                 ELSE                                                           
                    IF WS-AGE-CY        < WS-PREV-AGE                           
                       MOVE WS-AGE-CY          TO WS-PREV-AGE                   
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF WS-CHILD-BABY = 'Y'                                         
                    MOVE 'BB'       TO F557-NUM-YOUNG-CHILD-AGE                 
                 ELSE                                                           
                    MOVE WS-PREV-AGE(3:2) TO F557-NUM-YOUNG-CHILD-AGE           
                 END-IF                                                         
              END-IF                                                            
              MOVE 'SC'          TO F557-NUM-YOUNG-CHILD-CITIZEN                
              END-IF                                                            
           ELSE                                                                 
              IF K895-NUM-RELATIONSHIP = 43                                     
              IF K895-NUM-CITIZENSHIP = '10'                                    
                 IF WS-PREV-AGE-43 = 0                                          
                    MOVE WS-AGE-CY        TO  WS-PREV-AGE-43                    
                 ELSE                                                           
                    IF WS-AGE-CY        < WS-PREV-AGE-43 AND                    
                      (WS-AGE-CY NOT = SPACES AND LOW-VALUES AND ZEROES)        
                       MOVE WS-AGE-CY     TO WS-PREV-AGE-43                     
                    END-IF                                                      
                 END-IF                                                         
                 IF WS-CHILD-BABY = 'Y'                                         
                    MOVE -01       TO WS-PREV-AGE-43                            
                 END-IF                                                         
              END-IF                                                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           EVALUATE K895-NUM-NRIC-HIST                                          
               WHEN F800-NUM-NRIC1                                              
                    MOVE WS-AGE-CY                TO F557-NUM-AGE-HA1           
                    MOVE K895-AMT-INCOME          TO F557-AMT-INCOME-HA1        
                    MOVE K895-NUM-ETHNIC          TO WS-HA1-ETHNIC              
                    MOVE K895-NUM-MARITAL-STATUS  TO WS-HA1-MS                  
                    MOVE K895-NUM-SEX             TO WS-HA1-SEX                 
                    MOVE K895-DTE-BIRTH           TO WS-HA1-BIRTH               
                    MOVE K895-NUM-CITIZENSHIP     TO WS-HA1-CTZN                
                    IF K895-NUM-MARITAL-STATUS IS NOT ALPHABETIC                
                       MOVE 44                      TO K060-SERIAL-NO           
                       MOVE K895-NUM-MARITAL-STATUS TO K060-CODE                
                       PERFORM 8000-READ-BP13K060   THRU 8000-EXIT              
                       MOVE K060-DESC(1:1)  TO F557-NUM-MARITAL-STATUS          
                    ELSE                                                        
                       MOVE K895-NUM-MARITAL-STATUS TO                          
                                      F557-NUM-MARITAL-STATUS                   
                    END-IF                                                      
               WHEN F800-NUM-NRIC2                                              
                    MOVE WS-AGE-CY                TO F557-NUM-AGE-HA2           
                    MOVE K895-AMT-INCOME          TO F557-AMT-INCOME-HA2        
               WHEN F800-NUM-NRIC3                                              
                    MOVE WS-AGE-CY                TO F557-NUM-AGE-HA3           
                    MOVE K895-AMT-INCOME          TO F557-AMT-INCOME-HA3        
               WHEN F800-NUM-NRIC4                                              
                    MOVE WS-AGE-CY                TO F557-NUM-AGE-HA4           
                    MOVE K895-AMT-INCOME          TO F557-AMT-INCOME-HA4        
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       5000-READ-APPLICANT.                                                     
      *====================*                                                    
                                                                                
           MOVE F800-NUM-REGN                  TO F557-NUM-REGN.                
           MOVE F800-NUM-SCH-ACC               TO F557-NUM-SCH-ACCNT.           
           MOVE F800-NUM-NRIC1                 TO F557-NUM-NRIC1.               
           MOVE F800-DTE-CANCEL                TO F557-DTE-CANCEL.              
           MOVE F800-NUM-ALLO-CAT              TO F557-CDE-SALES-MODE.          
           MOVE F800-DTE-BALLOT                TO F557-DTE-QUARTER.             
           MOVE F800-NUM-NEW-TOWN              TO F557-NUM-TOWN.                
           MOVE F800-NUM-FLAT-TYPE             TO F557-NUM-FLAT-TYPE.           
           MOVE F800-NUM-ALLOC-TAG             TO F557-NUM-ALLOC-TAG.           
           MOVE F800-NUM-HOUSEHOLD             TO F557-NUM-HH.                  
           MOVE F800-NUM-ALLO-SCHEME           TO F557-NUM-ALLO-SCHEME.         
           MOVE F800-NUM-ELIG-SCHEME           TO F557-NUM-ELIG-SCHEME.         
           MOVE F800-CDE-CITIZENSHIP-HOUSEHOLD                                  
                                     TO F557-CDE-CITIZENSHIP-HOUSEHOLD.         
           IF F557-NUM-STATUS = 'C'                                             
              MOVE F800-DTE-ACCEPTANCE         TO F557-DTE-ACCEPTANCE           
           END-IF.                                                              
      **                                                                        
      ** DECODE ETHNIC                                                          
      **                                                                        
           IF WS-HA1-ETHNIC = 0 OR 1 OR 2 OR 3 OR 4                             
              MOVE 2                           TO WS-CDE-RACE                   
           ELSE IF WS-HA1-ETHNIC = 5                                            
              MOVE 1                           TO WS-CDE-RACE                   
           ELSE IF WS-HA1-ETHNIC = 6                                            
              MOVE 3                           TO WS-CDE-RACE                   
           ELSE IF WS-HA1-ETHNIC = 7 OR 9 OR ' '                                
              MOVE 4                           TO WS-CDE-RACE                   
           END-IF.                                                              
                                                                                
           MOVE 09                             TO K060-SERIAL-NO                
           MOVE WS-CDE-RACE                    TO K060-CODE                     
           PERFORM 8000-READ-BP13K060        THRU 8000-EXIT                     
           MOVE K060-DESC(1:10)                TO F557-NUM-ETHNIC.              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==============*                                                          
       6000-READ-FIS.                                                           
      *==============*                                                          
                                                                                
           IF F557-NUM-STATUS NOT = 'C'                                         
              MOVE F120-DTE-ACCEPTANCE          TO F557-DTE-ACCEPTANCE          
           END-IF.                                                              
           MOVE F120-NEIGHBOURHOOD  TO  F557-NEIGHBOURHOOD-CONTRACT.            
           MOVE F120-CONTRACT-NO    TO  F557-CONTRACT.                          
           MOVE F120-BLK-NO         TO  F557-NUM-BLOCK.                         
           MOVE F120-FLAT-STATUS    TO  F557-NUM-FLAT-STATUS.                   
           MOVE F120-CDE-PROTECTED  TO  F557-NUM-PROTECTED.                     
                                                                                
           MOVE F120-DTE-HANDOVER               TO F557-DTE-HANDOVER.           
           MOVE F120-CDE-TYP-DESG               TO F557-CDE-TYP-DESG            
           MOVE F120-DTE-KEY-ISSUED             TO F557-DTE-TP.                 
           MOVE F120-CURR-SELLING-PRICE     TO F557-AMT-SELLING-PRICE           
      **                                                                        
      ** DECODE MODL-DESG                                                       
      **                                                                        
           IF F120-CDE-MODL-DESG NOT = SPACES AND LOW-VALUES                    
              SET WS-IND                        TO 1                            
              SEARCH WS-MODL-DESG-TAB                                           
                  AT END                                                        
                     MOVE SPACES                TO F557-DESC-MODL-DESG          
                WHEN WS-DESG-CDE(WS-IND)      = F120-CDE-MODL-DESG              
                     MOVE WS-DESG-DESC(WS-IND)  TO F557-DESC-MODL-DESG          
              END-SEARCH                                                        
           END-IF.                                                              
      **                                                                        
      ** DECODE ESTATE                                                          
      **                                                                        
           MOVE 03                              TO K060-SERIAL-NO               
           MOVE F120-ESTATE                     TO K060-CODE                    
           PERFORM 8000-READ-BP13K060         THRU 8000-EXIT                    
           MOVE K060-DESC                       TO F557-DESC-ESTATE.            
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       7000-READ-NEW-FLAT.                                                      
      *=====================*                                                   
                                                                                
           PERFORM 7100-START-BP13K821       THRU 7100-EXIT.                    
           PERFORM 7200-READ-NEXT-BP13K821   THRU 7200-EXIT                     
             UNTIL K820-EOF OR K820-FND.                                        
                                                                                
           IF K820-FND                                                          
              PERFORM 7300-MOVE-BP13K820     THRU 7300-EXIT                     
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       7100-START-BP13K821.                                                     
      *=====================*                                                   
                                                                                
           MOVE SPACES                         TO WS-K820-FND                   
                                                  WS-K820-EOF                   
                                                  WS-NF-DTE-BALLOT              
                                                  WS-NF-NUM-REGN                
                                                  WS-NF-NUM-NT                  
                                                  WS-NF-NUM-FT                  
                                                  WS-NF-SCH-ACC.                
                                                                                
           MOVE SPACES                         TO BP13K820-REC.                 
           INITIALIZE                             BP13K820-REC.                 
                                                                                
           MOVE F800-NUM-NRIC1                 TO K820-NUM-NRIC.                
                                                                                
           START BP13K820 KEY >= K820-NUM-NRIC.                                 
                                                                                
           IF K820-STATUS = 00 OR 02                                            
              CONTINUE                                                          
           ELSE                                                                 
              IF K820-STATUS = 23 OR 10                                         
                 MOVE 'Y'                      TO WS-K820-EOF                   
              ELSE                                                              
                 DISPLAY 'BP13K820 - ERROR STARTING , '                         
                         'STATUS = ' K820-STATUS                                
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *========================*                                                
       7200-READ-NEXT-BP13K821.                                                 
      *========================*                                                
                                                                                
           READ BP13K820 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K820-EOF                                      
                GO          TO 7200-EXIT                                        
           END-READ.                                                            
                                                                                
           IF K820-STATUS = 00 OR 02                                            
              IF K820-NUM-NRIC = F800-NUM-NRIC1                                 
                 MOVE K820-NUM-REGN               TO K800-NUM-REGN              
                 PERFORM 7500-READ-BP13K800     THRU 7500-EXIT                  
                 IF K800-FND                                                    
                    IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES             
                       IF K800-NUM-REGN NOT = F800-NUM-REGN                     
                          MOVE 'Y'                TO WS-K820-FND                
                          MOVE K800-DTE-BALLOT    TO WS-NF-DTE-BALLOT           
                          MOVE K800-NUM-REGN      TO WS-NF-NUM-REGN             
                          MOVE K800-NUM-NEW-TOWN  TO WS-NF-NUM-NT               
                          MOVE K800-NUM-FLAT-TYPE TO WS-NF-NUM-FT               
                          MOVE K800-NUM-SCH-ACC   TO WS-NF-SCH-ACC              
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'                     TO WS-K820-EOF                    
              END-IF                                                            
           ELSE                                                                 
              IF K820-STATUS = 23 OR 10                                         
                 MOVE 'Y'                     TO WS-K820-EOF                    
              ELSE                                                              
                 DISPLAY 'BP13K820 - ERROR READING NEXT , '                     
                         'K820-STATUS = ' K820-STATUS                           
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       7300-MOVE-BP13K820.                                                      
      *=====================*                                                   
                                                                                
      *    IF                                                                   
      *       PERFORM 7400-DECODE-NT        THRU 7400-EXIT                      
      *    END-IF.                                                              
                                                                                
           MOVE WS-NF-NUM-REGN             TO F557-NUM-REGN-NEW-FLAT            
           MOVE WS-NF-DTE-BALLOT           TO F557-DTE-QTR-NEW-FLAT             
           MOVE WS-NF-NUM-NT               TO F557-NUM-NT-NEW-FLAT              
           MOVE WS-NF-NUM-FT               TO F557-NUM-FT-NEW-FLAT.             
           MOVE WS-NF-SCH-ACC              TO F557-NUM-SCHACC-NEW-FLAT.         
                                                                                
       7300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============*                                                         
       7400-DECODE-NT.                                                          
      *===============*                                                         
                                                                                
           MOVE SPACES                         TO BP13K813-REC.                 
           INITIALIZE                             BP13K813-REC.                 
                                                                                
           MOVE WS-NF-NUM-NT                   TO K813-NUM-ZONE.                
           MOVE WS-NF-NUM-FT                   TO K813-NUM-FLAT-TYPE.           
           MOVE WS-NF-DTE-BALLOT               TO K813-DTE-BALLOT.              
                                                                                
           PERFORM 8100-READ-BP13K813        THRU 8100-EXIT                     
                                                                                
           IF K813-CDE-NT NOT = SPACES                                          
              MOVE K813-CDE-NT                 TO WS-NF-NUM-NT                  
           END-IF.                                                              
                                                                                
       7400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============*                                                         
       7450-GET-PROJECT.                                                        
      *===============*                                                         
                                                                                
           MOVE SPACES                         TO BP13K813-REC.                 
           INITIALIZE                             BP13K813-REC.                 
                                                                                
           MOVE F800-NUM-BTO-ZONE              TO K813-NUM-ZONE.                
           IF F800-NUM-BTO-ZONE = '034'                                         
              MOVE '4 '                        TO K813-NUM-FLAT-TYPE            
           ELSE                                                                 
           IF F800-NUM-BTO-ZONE = '036'                                         
              MOVE '3 '                        TO K813-NUM-FLAT-TYPE            
           ELSE                                                                 
           IF F800-NUM-BTO-ZONE = '0C3'                                         
              MOVE '4 '                        TO K813-NUM-FLAT-TYPE            
           ELSE                                                                 
           IF F800-NUM-BTO-ZONE = '0C4'                                         
              MOVE '5 '                        TO K813-NUM-FLAT-TYPE            
           ELSE                                                                 
              IF F800-NUM-BTO-FLAT-TYPE = '1A' OR '2A'                          
                 MOVE F800-NUM-BTO-FLAT-TYPE      TO K813-NUM-FLAT-TYPE         
              ELSE                                                              
                 MOVE F800-NUM-BTO-FLAT-TYPE(1:1) TO K813-NUM-FLAT-TYPE         
              END-IF                                                            
           END-IF                                                               
           END-IF                                                               
           END-IF                                                               
           END-IF.                                                              
                                                                                
           MOVE F800-DTE-BALLOT                TO K813-DTE-BALLOT.              
                                                                                
           PERFORM 8100-READ-BP13K813        THRU 8100-EXIT                     
                                                                                
           IF K813-NME-PROJECT-LTR NOT = SPACES                                 
              MOVE K813-NME-PROJECT-LTR        TO F557-NUM-PROJECT              
           END-IF.                                                              
                                                                                
       7450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       7500-READ-BP13K800.                                                      
      *=====================*                                                   
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 00                                                  
              MOVE 'Y'                         TO WS-K800-FND                   
           ELSE                                                                 
              IF K800-STATUS = 23                                               
                 MOVE 'N'                      TO WS-K800-FND                   
                 DISPLAY 'BP13K800 - RECORD NOT FOUND : ' K800-NUM-REGN         
              ELSE                                                              
                 DISPLAY 'BP13K800 - ERROR READING , '                          
                         ' STATUS : ' K800-STATUS                               
                         ' KEY : ' K800-NUM-REGN                                
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=====================*                                                   
       7600-DECODE-PACD.                                                        
      *=====================*                                                   
                                                                                
           MOVE SPACES           TO WS-DTE-PACD.                                
           INITIALIZE               WS-DTE-PACD.                                
           MOVE K100-DTE-PACD    TO WS-DTE-PACD.                                
                                                                                
           IF K100-NUM-PACD = 'Q'                                               
              EVALUATE WS-DTE-PACD-QTR                                          
                WHEN '01'                                                       
                 MOVE '01'   TO WS-DTE-PACD-QTR                                 
                WHEN '02'                                                       
                 MOVE '04'   TO WS-DTE-PACD-QTR                                 
                WHEN '03'                                                       
                 MOVE '07'   TO WS-DTE-PACD-QTR                                 
                WHEN '04'                                                       
                 MOVE '10'   TO WS-DTE-PACD-QTR                                 
                WHEN OTHER                                                      
                 CONTINUE                                                       
               END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
           IF K100-NUM-PACD = 'M'                                               
               MOVE K100-DTE-PACD    TO WS-DTE-PACD                             
           END-IF.                                                              
                                                                                
           MOVE WS-DTE-PACD        TO F557-DTE-PACD.                            
                                                                                
       7600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       7700-READ-BM06K100.                                                      
      *=====================*                                                   
                                                                                
           MOVE SPACES      TO K100-REC.                                        
           INITIALIZE          K100-REC.                                        
                                                                                
           MOVE F120-ESTATE        TO K100-ESTATE                               
           MOVE F120-NEIGHBOURHOOD TO K100-NEIGHBOURHOOD                        
           MOVE F120-CONTRACT-NO   TO K100-CONTRACT-NO                          
           MOVE F120-BLK-NO        TO K100-BLK-NO                               
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE K100-STATUS                                                 
              WHEN 00                                                           
                 ADD 1 TO  CNT-FND-K100                                         
                 PERFORM 7600-DECODE-PACD   THRU 7600-EXIT                      
              WHEN 23                                                           
                 MOVE SPACES     TO K100-REC                                    
                 INITIALIZE         K100-REC                                    
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BM06K100 ' K100-STATUS                  
                 MOVE K100-STATUS        TO RETURN-CODE                         
                 PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       7700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       8000-READ-BP13K060.                                                      
      *=====================*                                                   
                                                                                
           READ BP13K060.                                                       
                                                                                
           IF K060-STATUS = 00                                                  
              CONTINUE                                                          
           ELSE                                                                 
              IF K060-STATUS = 23                                               
                 MOVE SPACES                     TO K060-DESC                   
              ELSE                                                              
                 DISPLAY 'BP13K060 - ERROR READING , '                          
                         ' STATUS : ' K060-STATUS                               
                         ' KEY : ' K060-KEY-FLD                                 
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       8100-READ-BP13K813.                                                      
      *=====================*                                                   
                                                                                
           READ BP13K813.                                                       
                                                                                
           IF K813-STATUS = 00                                                  
              CONTINUE                                                          
           ELSE                                                                 
              IF K813-STATUS = 23                                               
                 DISPLAY 'BP13K813 - NOT FOUND,'                                
                         ' KEY : ' K813-KEY-FLD                                 
                         ' REG : ' F800-NUM-REGN                                
                 MOVE SPACES                     TO BP13K813-REC                
              ELSE                                                              
                 DISPLAY 'BP13K813 - ERROR READING , '                          
                         ' STATUS : ' K813-STATUS                               
                         ' KEY : ' K813-KEY-FLD                                 
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       9000-CLOSE-FILES.                                                        
      *=====================*                                                   
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CY50'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BM06F120 READ     : ' CNT-F120-READ.                 
           DISPLAY 'NO. OF BP13F800 READ     : ' CNT-F800-READ.                 
           DISPLAY 'NO. OF F120 MATCHED      : ' CNT-F120-MATCH.                
           DISPLAY 'NO. OF F120 UNMATCHED    : ' CNT-F120-NOTMAT.               
           DISPLAY 'NO. OF BK CASES WRITTEN  : ' CNT-F557-WRITE-BK.             
           DISPLAY 'NO. OF TP CASES WRITTEN  : ' CNT-F557-WRITE-TP.             
           DISPLAY 'NO. OF CAN CASES WRITTEN : ' CNT-F557-WRITE-CAN.            
           DISPLAY 'NO. OF BP13F558 WRITTEN  : ' CNT-F558-WRITE.                
           DISPLAY 'NO. OF CANCEL CASES NOT CURRENT YEAR : '                    
                   CNT-CAN-NOT-CUR-YR.                                          
           DISPLAY 'NO. OF BOOK CASES NOT CURRENT YEAR : '                      
                   CNT-BK-NOT-CUR-YR.                                           
           DISPLAY 'NO. OF TP CASES NOT CURRENT YEAR : '                        
                   CNT-TP-NOT-CUR-YR.                                           
                                                                                
           CLOSE SY02F001                                                       
                 BM06F120                                                       
                 BP13F800                                                       
                 BP13K060                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K813                                                       
                 BP13K893                                                       
                 BP13K895                                                       
                 BM06K100                                                       
                 P13F557A                                                       
                 P13F557B                                                       
                 P13F557C                                                       
                 P13F800A                                                       
                 BP13F558.                                                      
                                                                                
           IF K060-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K060 , STATUS = ' K060-STATUS          
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K800 , STATUS = ' K800-STATUS          
           END-IF.                                                              
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K813 , STATUS = ' K813-STATUS          
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K820 , STATUS = ' K820-STATUS          
           END-IF.                                                              
                                                                                
           IF K893-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K893 , STATUS = ' K893-STATUS          
           END-IF.                                                              
                                                                                
           IF K895-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K895 , STATUS = ' K895-STATUS          
           END-IF.                                                              
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BM06K100 , STATUS = ' K100-STATUS          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
