       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF12.                                                 
      *AUTHOR.        ZARITA DANE. D. SARMIENTO.                                
      *DATE-WRITTEN.  JUN 02 2010.                                              
                                                                                
      * SYSTEM OF COMMITMENT (SOC)                                              
      * ============================================================            
      * OBJECTIVE  - PRINT LETTERS ON NOTIFICATION OF                           
      *              HAND-OVER CEREMONY AT THE TELOK BLANGAH                    
      *                                                                         
      * FORM USED:                                                              
      *                                                                         
      * INPUT FILES:  BM06F120                                                  
      *               BP13K800                                                  
      *               BP13K820                                                  
      *               BP13K830                                                  
      * REPORT:       BP13GEY                                                   
      *               BP13LGEY                                                  
      *               P13LGEYA                                                  
      * ------------------------------------------------------------            
      * REF NO    DATE  BY  DESCRIPTIONS                                        
      * -------- ------ --- ------------                                        
      * BP133925 020510 ZDD1 NEW PGM                                            
      * BP135963 180915 NST1 TO CHANGE DATE FOR GOLDEN KISMIS                   
      * BP135983 191015 RJB1 TO REMOVE THE HARDCODED '21/09/2015' DATE          
      *                      AND CHANGED FORM NAMES INTO PROD VERSION           
      * ============================================================            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F120 ASSIGN       TO BM06F120.                            
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS K800-STATUS.                        
                                                                                
           SELECT BP13K820 ASSIGN        TO BP13K820                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K820-KEY-FLD                        
                           FILE STATUS   IS K820-STATUS.                        
                                                                                
           SELECT BP13K830 ASSIGN        TO BP13K830                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K830-KEY-FLD                        
                           ALTERNATE KEY IS K830-NUM-REGN                       
                           FILE STATUS   IS K830-STATUS.                        
                                                                                
           SELECT BP13GEY ASSIGN        TO BP13GEY.                             
                                                                                
           SELECT BP13LGEY ASSIGN       TO BP13LGEY.                            
                                                                                
           SELECT P13LGEYA ASSIGN       TO P13LGEYA.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BM06F120                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 700 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS.                                        
       COPY BM06F120.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13GEY            RECORDING MODE IS F                              
                               RECORD CONTAINS 100 CHARACTERS                   
                               BLOCK  CONTAINS 0   RECORDS.                     
       01   GEY-PRTREC         PIC X(100).                                      
                                                                                
       FD   BP13LGEY           RECORDING MODE IS F                              
                               RECORD CONTAINS 132 CHARACTERS                   
                               BLOCK  CONTAINS 0   RECORDS.                     
       01   LGEY-PRTREC        PIC X(132).                                      
                                                                                
       FD   P13LGEYA           RECORDING MODE IS F                              
                               RECORD CONTAINS 132 CHARACTERS                   
                               BLOCK  CONTAINS 0   RECORDS.                     
       01   LGEYA-PRTREC       PIC X(132).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  K800-STATUS                   PIC 99    VALUE 0.                     
       01  K820-STATUS                   PIC 99    VALUE 0.                     
       01  K830-STATUS                   PIC 99    VALUE 0.                     
       01  WS-F120-EOF                   PIC X     VALUE 'N'.                   
       01  WS-SURVEY-NAME1               PIC X(66) VALUE SPACES.                
       01  WS-SURVEY-NAME2               PIC X(66) VALUE SPACES.                
       01  WS-SURVEY-NAME3               PIC X(66) VALUE SPACES.                
       01  WS-SURVEY-NAME4               PIC X(66) VALUE SPACES.                
       01  WS-SURVEY-CONTACT1            PIC X(08) VALUE SPACES.                
       01  WS-SURVEY-CONTACT2            PIC X(08) VALUE SPACES.                
       01  WS-SURVEY-CONTACT3            PIC X(08) VALUE SPACES.                
       01  WS-SURVEY-CONTACT4            PIC X(08) VALUE SPACES.                
       01  WS-OVERSEA                    PIC X     VALUE SPACES.                
                                                                                
                                                                                
       01  WS-DATE                       PIC X(8)  VALUE SPACES.                
       01  WS-POSTAL                     PIC X(6)  VALUE SPACES.                
                                                                                
       01  WS-PREV-BLK-NO                PIC X(5).                              
       01  WS-OCCP-PTR                   PIC 9(2).                              
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F120-READ           PIC 9(7)  VALUE 0.                     
           05 WS-CNT-LETTER              PIC 9(7)  VALUE 0.                     
           05 WS-CNT-K800-MISS           PIC 9(7)  VALUE 0.                     
           05 WS-CNT-K820-MISS           PIC 9(7)  VALUE 0.                     
           05 WS-CNT-K830-MISS           PIC 9(7)  VALUE 0.                     
           05 WS-IDX                     PIC 9(2)  VALUE 0.                     
           05 WS-CTR                     PIC 9(2)  VALUE 0.                     
           05 WS-BLK-CNT                 PIC 9(7)  VALUE 0.                     
           05 WS-TOTAL-CNT               PIC 9(7)  VALUE 0.                     
           05 WS-LINE-CNT                PIC 9(7)  VALUE 0.                     
           05 WS-LINE-CNT-A              PIC 9(7)  VALUE 0.                     
           05 WS-PAGE-CNT                PIC 9(7)  VALUE 0.                     
           05 WS-REC-CNT                 PIC 9(7)  VALUE 0.                     
           05 WS-RECORD-CNT              PIC ZZZ9.                              
                                                                                
           05 WS-ERROR-FL                PIC X(1) VALUE 'N'.                    
              88 WS-NO-ERROR             VALUE 'N'.                             
              88 WS-ERROR                VALUE 'Y'.                             
                                                                                
       01 LGEYA-LINE1.                                                          
           05  LGEYA-REGN-NO             PIC X(08).                             
           05  LGEYA-OVERSEA             PIC X(01).                             
           05  FIL01                     PIC X(01).                             
           05  LGEYA-GEY-ADDRESS         PIC X(50).                             
           05  FIL02                     PIC X(01).                             
           05  FILLLER                   PIC X(71).                             
       01 LGEYA-LINE2.                                                          
           05  FILLER                    PIC X(10).                             
           05  LGEYA-NUM-NRIC1           PIC X(09).                             
           05  FIL03                     PIC X(01).                             
           05  LGEYA-NME-APPLICANT1      PIC X(66).                             
           05  FIL04                     PIC X(01).                             
           05  FILLER                    PIC X(45).                             
       01 LGEYA-LINE3.                                                          
           05  FILLER                    PIC X(10).                             
           05  LGEYA-NUM-NRIC2           PIC X(09).                             
           05  FIL05                     PIC X(01).                             
           05  LGEYA-NME-APPLICANT2      PIC X(66).                             
           05  FIL06                     PIC X(01).                             
           05  LGEYA-ETHNIC-GROUP        PIC X(01).                             
           05  FIL07                     PIC X(01).                             
           05  LGEYA-FT-ST               PIC X(01).                             
           05  FIL08                     PIC X(01).                             
           05  LGEYA-ALLOC-MODE          PIC X(03).                             
           05  FIL09                     PIC X(01).                             
           05  FILLER                    PIC X(37).                             
                                                                                
       01  WS-ADDRESS.                                                          
           05 LGEY-BLK-NO                PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X     VALUE SPACES.                
           05 LGEY-FLAT-NO.                                                     
              10 FILLER                  PIC X     VALUE '#'.                   
              10 LGEY-LVL-NO             PIC X(2)  VALUE SPACES.                
              10 FILLER                  PIC X     VALUE '-'.                   
              10 LGEY-UNIT-NO            PIC X(4)  VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 LGEY-STREET                PIC X(47) VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 LGEY-POSTAL-CODE           PIC X(6)  VALUE SPACES.                
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(90).                          
           05 WS-PARSED-STRING              PIC X(90).                          
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
       01  DJDE-LINE-PAGE1                PIC X(80)  VALUE                      
           '$DJDE$ JDE=13POT2,JDL=BP13,FORMS=BP13KE,NUMBER=(0000,29,65,3        
      -    '),END;'.                                                            
       01  DJDE-LINE-PAGE2A               PIC X(80)  VALUE                      
           '$DJDE$ JDE=13POT5,JDL=BP13T,FORMS=BP13K6,NUMBER=NO,END;'.           
       01  DJDE-BLANKF                   PIC X(90) VALUE                        
           '$DJDE$ PMODE=PORTRAIT,FORMS=BLANKF,SHIFT=(0,0),NUMBER=NO,END        
      -    ';'.                                                                 
       01  DJDE-LINE-PAGE3A               PIC X(80)  VALUE                      
           '$DJDE$ JDE=13POT2,JDL=BP13T,FORMS=BP13KF,NUMBER=NO,END;'.           
                                                                                
                                                                                
      *=======================================================*                 
      *    LINKAGE VARIABLES                                  *                 
      *=======================================================*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------------            
      *    LETTER LAYOUT                                                        
      *-------------------------------------------------------------            
       01  GEY-LINE01.                                                          
           05 FILLER                     PIC X(66) VALUE SPACES.                
           05 GEY-REGN-NO                PIC X(8)  VALUE SPACES.                
                                                                                
      *01  GEY-LINE02.                                                          
      *    05 FILLER                     PIC X(66) VALUE SPACES.                
      *    05 GEY-DATE                   PIC X(10) VALUE '21/09/2015'.          
                                                                                
       01  GEY-LINE03.                                                          
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 GEY-NAME1                  PIC X(66)  VALUE SPACES.               
                                                                                
       01  GEY-LINE03B.                                                         
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 GEY-NAME2                  PIC X(66)  VALUE SPACES.               
                                                                                
       01  GEY-LINE03C.                                                         
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 GEY-NAME3                  PIC X(66)  VALUE SPACES.               
                                                                                
       01  GEY-LINE03D.                                                         
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 GEY-NAME4                  PIC X(66)  VALUE SPACES.               
                                                                                
       01  GEY-LINE04.                                                          
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 GEY-BLK                    PIC X(10)  VALUE SPACES.               
           05 FILLER                     PIC X(2)   VALUE SPACES.               
           05 GEY-FLAT-NO.                                                      
              15  GEY-CHAR               PIC X      VALUE SPACE.                
              15  GEY-LEVEL              PIC X(2)   VALUE SPACES.               
              15  GEY-DASH               PIC X      VALUE SPACE.                
              15  GEY-HSE-MAIN           PIC X(4)   VALUE SPACES.               
              15  GEY-HSE-SUB            PIC X(3)   VALUE SPACES.               
                                                                                
       01  GEY-LINE04B.                                                         
           05 FILLER                     PIC X(11) VALUE SPACES.                
           05 GEY-STREET                 PIC X(47) VALUE SPACES.                
                                                                                
       01  GEY-LINE04C.                                                         
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  GEY-ADDR                 PIC X(20) VALUE SPACES.                 
                                                                                
       01  GEY-LINE05.                                                          
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  GEY-BAR-CODE              PIC X(09) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      *    SURVEY ON INTEREST LAYOUT                                            
      *-------------------------------------------------------------            
       01  SURVEY-LINE01.                                                       
           05 FILLER                     PIC X(24) VALUE SPACES.                
           05 SURVEY-REGN-NO             PIC X(8)  VALUE SPACES.                
                                                                                
       01  SURVEY-LINE02.                                                       
           05 FILLER                     PIC X(15) VALUE SPACES.                
           05 FILLER                     PIC X(4)  VALUE 'BLK '.                
           05 SURVEY-BLK                 PIC X(05) VALUE SPACES.                
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 SURVEY-FLAT-NO.                                                   
              10 FILLER                  PIC X     VALUE '#'.                   
              10 SURVEY-LVL-NO           PIC X(2)  VALUE SPACES.                
              10 FILLER                  PIC X     VALUE '-'.                   
              10 SURVEY-UNIT-NO          PIC X(4)  VALUE SPACES.                
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 SURVEY-STREET              PIC X(47) VALUE SPACES.                
                                                                                
       01  SURVEY-UNDERLINE.                                                    
           05  FILLER                     PIC X(11)  VALUE SPACES.              
           05  FILLER                     PIC X(25)                             
               VALUE '_________________________'.                               
                                                                                
       01  SURVEY-LINE03.                                                       
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 FILLER                     PIC X(11)  VALUE 'Signed by '.         
           05 SURVEY-NAME1               PIC X(66)  VALUE SPACES.               
                                                                                
       01  SURVEY-LINE04.                                                       
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 FILLER                     PIC X(15)  VALUE                       
                                              'Contact Number '.                
           05 SURVEY-CONTACT1            PIC X(08)  VALUE SPACES.               
                                                                                
       01  SURVEY-LINE05.                                                       
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 FILLER                     PIC X(11)  VALUE 'Signed by '.         
           05 SURVEY-NAME2               PIC X(66)  VALUE SPACES.               
                                                                                
       01  SURVEY-LINE06.                                                       
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 FILLER                     PIC X(15)  VALUE                       
                                              'Contact Number '.                
           05 SURVEY-CONTACT2            PIC X(08)  VALUE SPACES.               
                                                                                
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT (HARD COPY)                                            
      *-------------------------------------------------------------            
       01  LGEY-HDR01.                                                          
           05 FILLER                     PIC X(14) VALUE 'BP13LGEY'.            
           05 FILLER                     PIC X(30) VALUE 'HDB3'.                
           05 FILLER                     PIC X(42) VALUE                        
             'S Y S T E M   O F   C O M M I T M E N T'.                         
           05 FILLER                     PIC X(13) VALUE SPACES.                
           05 FILLER                     PIC X(7)  VALUE 'DATE: '.              
           05 LGEY-DATE                  PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE '   PAGE: '.           
           05 LGEY-PAGE                  PIC ZZZZ9.                             
                                                                                
       01  LGEY-HDR02.                                                          
           05 FILLER                     PIC X(40) VALUE SPACES.                
           05 FILLER                     PIC X(80) VALUE                        
             'LETTERS PRINTED FOR NOTIFICATION OF HAND-OVER CEREMONY'.          
                                                                                
       01  LGEY-HDR02A.                                                         
           05 FILLER                     PIC X(49) VALUE SPACES.                
           05 FILLER                     PIC X(25) VALUE                        
             'FOR THE TELOK BLANGAH    '.                                       
                                                                                
       01  LGEY-HDR03.                                                          
           05 FILLER                     PIC X(10) VALUE '  BLOCK: '.           
           05 LGEY-BLOCK                 PIC X(5)  VALUE SPACES.                
           05 FILLER                     PIC X(4)  VALUE SPACES.                
           05 LGEY-STREET-NAME           PIC X(47) VALUE SPACES.                
                                                                                
       01  LGEY-HDR04.                                                          
           05 FILLER                     PIC X(9)  VALUE '   NO.   '.           
           05 FILLER                     PIC X(14) VALUE 'SCH-ACC NO'.          
           05 FILLER                     PIC X(10) VALUE '  REGN NO.'.          
           05 FILLER                     PIC X(01) VALUE SPACE.                 
           05 FILLER                     PIC X(34)                              
                    VALUE '  NAME            '.                                 
           05 FILLER                     PIC X(10) VALUE '  NRIC NO.'.          
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 FILLER                     PIC X(22)                              
                    VALUE 'CORRESPONDENCE ADDRESS'.                             
                                                                                
       01  LGEY-DETAIL.                                                         
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 LGEY-NO                    PIC ZZZZ9.                             
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 LGEY-SCH-ACC               PIC X(14) VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 LGEY-REGN-NO               PIC X(8)  VALUE SPACES.                
           05 LGEY-OVERSEA               PIC X(1)  VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 LGEY-NAME                  PIC X(32) VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 LGEY-NRIC-NO               PIC X(9)  VALUE SPACES.                
           05 FILLER                     PIC X(4)  VALUE SPACES.                
           05 LGEY-CORR-ADDR             PIC X(50) VALUE SPACES.                
                                                                                
       01  LGEY-FOOTER.                                                         
           05  FILLER                   PIC X(90) VALUE                         
               '  "*" CASES WITH OVERSEA ADDRESS.'.                             
                                                                                
       01  LGEY-PRT-TOTAL.                                                      
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(30)                              
              VALUE 'TOTAL NUMBER OF LETTERS: '.                                
           05 LGEY-TOTAL                 PIC Z,ZZZ,ZZ9.                         
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                   UNTIL WS-F120-EOF = 'Y'.                                     
           PERFORM 9999-WRAP-UP.                                                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-START-UP.                                                           
      *-------------------------------------------------------------            
            OPEN INPUT  BM06F120                                                
                        BP13K800                                                
                        BP13K820                                                
                        BP13K830                                                
                OUTPUT  BP13GEY                                                 
                        BP13LGEY                                                
                        P13LGEYA.                                               
                                                                                
            IF K800-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K800,STATUS ' K800-STATUS           
               MOVE K800-STATUS TO RETURN-CODE                                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            IF K820-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K820,STATUS ' K820-STATUS           
               MOVE K820-STATUS TO RETURN-CODE                                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            IF K830-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K830,STATUS ' K830-STATUS           
               MOVE K830-STATUS TO RETURN-CODE                                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            READ BM06F120                                                       
                 AT END  DISPLAY 'NO RECORDS FOUND!'                            
                         MOVE 'Y'          TO WS-F120-EOF                       
                         GO TO 1000-EXIT                                        
            END-READ.                                                           
                                                                                
            MOVE 60                  TO WS-LINE-CNT.                            
            ADD  1                   TO WS-CNT-F120-READ.                       
            MOVE F120-BLK-NO         TO WS-PREV-BLK-NO.                         
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DATE.                              
                                                                                
            STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)               
                   DELIMITED BY SIZE INTO LGEY-DATE.                            
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           SET WS-NO-ERROR           TO TRUE.                                   
      *-------------------------------------------------------------*           
      * READS BM06F120 AND PROCESS EACH RECORD UNTIL END OF FILE    *           
      *-------------------------------------------------------------*           
                                                                                
           PERFORM 2100-RETRIEVE-DATA   THRU 2100-EXIT.                         
           PERFORM 3000-FORMAT-LETTER   THRU 3000-EXIT.                         
           PERFORM 3100-PRINT-LETTER    THRU 3100-EXIT.                         
           PERFORM 3200-PRINT-AUDIT     THRU 3200-EXIT.                         
                                                                                
           READ BM06F120 AT END                                                 
                MOVE 'Y' TO WS-F120-EOF                                         
                GO TO 2000-EXIT                                                 
                END-READ.                                                       
                                                                                
           ADD  1                          TO WS-CNT-F120-READ.                 
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2100-RETRIEVE-DATA.                                                      
      *-------------------------------------------------------------            
           MOVE F120-REGN-NO               TO K800-NUM-REGN.                    
           PERFORM 6000-READ-K800          THRU 6000-EXIT.                      
           IF WS-NO-ERROR                                                       
              PERFORM 2200-GET-NRIC-NAMES  THRU 2200-EXIT                       
              PERFORM 2300-GET-NRIC-ADDR   THRU 2300-EXIT                       
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2200-GET-NRIC-NAMES.                                                     
      *-------------------------------------------------------------            
           MOVE ZEROES                     TO   WS-CTR.                         
           MOVE SPACES                     TO   WS-SURVEY-NAME1                 
                                                WS-SURVEY-NAME2                 
                                                WS-SURVEY-CONTACT1              
                                                WS-SURVEY-CONTACT2.             
                                                                                
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES                        
              IF K800-NUM-NRIC1(1:2) NOT = '##'                                 
                 MOVE K800-NUM-REGN           TO   K820-NUM-REGN                
                 MOVE K800-NUM-NRIC1          TO   K820-NUM-NRIC                
                 PERFORM 7000-READ-K820       THRU 7000-EXIT                    
                 MOVE K820-NME-OCCP           TO   WS-SURVEY-NAME1              
                                                                                
                 IF K820-NUM-PHONE-OFF NOT = SPACES AND LOW-VALUES              
                    MOVE K820-NUM-PHONE-OFF TO   WS-SURVEY-CONTACT1             
                 ELSE                                                           
                   IF K820-NUM-HP-PGR NOT = SPACES AND LOW-VALUES               
                      MOVE K820-NUM-HP-PGR TO   WS-SURVEY-CONTACT1              
                   ELSE                                                         
                      MOVE SPACES          TO   WS-SURVEY-CONTACT1              
                   END-IF                                                       
                 END-IF                                                         
                 MOVE 1 TO WS-CTR                                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              IF K800-NUM-NRIC2(1:2) NOT = '##'                                 
                 MOVE K800-NUM-REGN           TO   K820-NUM-REGN                
                 MOVE K800-NUM-NRIC2          TO   K820-NUM-NRIC                
                 PERFORM 7000-READ-K820       THRU 7000-EXIT                    
                 MOVE K820-NME-OCCP           TO   WS-SURVEY-NAME2              
                                                                                
                 IF K820-NUM-PHONE-OFF NOT = SPACES AND LOW-VALUES              
                    MOVE K820-NUM-PHONE-OFF TO   WS-SURVEY-CONTACT2             
                 ELSE                                                           
                   IF K820-NUM-HP-PGR NOT = SPACES AND LOW-VALUES               
                      MOVE K820-NUM-HP-PGR TO   WS-SURVEY-CONTACT2              
                   ELSE                                                         
                      MOVE SPACES          TO   WS-SURVEY-CONTACT2              
                   END-IF                                                       
                 END-IF                                                         
                 MOVE 2 TO WS-CTR                                               
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2300-GET-NRIC-ADDR.                                                      
      *------------------------------------------------------                   
                                                                                
           MOVE K800-NUM-REGN              TO  K830-NUM-REGN.                   
           PERFORM 8000-READ-K830          THRU 8000-EXIT.                      
                                                                                
       2300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-FORMAT-LETTER.                                                      
      *------------------------------------------------------                   
           MOVE SPACES                   TO  GEY-PRTREC.                        
           MOVE K800-NUM-REGN            TO  GEY-REGN-NO                        
                                             SURVEY-REGN-NO.                    
                                                                                
                                                                                
           MOVE F120-BLK-NO         TO        SURVEY-BLK.                       
           MOVE F120-LEVEL-NO       TO        SURVEY-LVL-NO.                    
           MOVE F120-MAIN-UNIT      TO        SURVEY-UNIT-NO.                   
           MOVE F120-STREET-NAME    TO        SURVEY-STREET.                    
                                                                                
                                                                                
           MOVE WS-SURVEY-NAME1          TO  SURVEY-NAME1                       
                                             GEY-NAME1.                         
           MOVE WS-SURVEY-CONTACT1       TO  SURVEY-CONTACT1.                   
           MOVE WS-SURVEY-NAME2          TO  SURVEY-NAME2                       
                                             GEY-NAME2.                         
           MOVE WS-SURVEY-CONTACT2       TO  SURVEY-CONTACT2.                   
                                                                                
           IF K830-NUM-MAIL-ADDR NOT = SPACES AND LOW-VALUES                    
              PERFORM 3050-FORMAT-MAIL-ADDR THRU 3050-EXIT                      
           ELSE                                                                 
              PERFORM 3055-FORMAT-HOME-ADDR THRU 3055-EXIT                      
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3010-PARSE-ADDRESS.                                                      
      *--------------------------------------------------------------           
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 3020-GET-CHAR          THRU 3020-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3020-GET-CHAR.                                                           
      *---------------------------------------------------------------          
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3050-FORMAT-MAIL-ADDR.                                                   
      *------------------------------------------------------                   
           IF K830-NUM-MAIL-ADDR-TYPE  = 'H'                                    
              MOVE K830-NME-MAIL-STR-BLDG TO GEY-STREET                         
              MOVE K830-NUM-MAIL-LVL-CHAR TO GEY-CHAR                           
              MOVE K830-NUM-MAIL-LVL-NUM  TO GEY-LEVEL                          
              MOVE K830-NUM-MAIL-LVL-DASH TO GEY-DASH                           
              MOVE K830-NUM-MAIL-HSEMAIN  TO GEY-HSE-MAIN                       
              MOVE K830-NUM-MAIL-HSESUB   TO GEY-HSE-SUB                        
           ELSE                                                                 
              MOVE K830-NUM-MAIL-FLAT     TO GEY-FLAT-NO                        
              MOVE K830-NME-MAIL-STR-BLDG TO GEY-STREET                         
           END-IF.                                                              
                                                                                
           MOVE K830-NUM-MAIL-BLK         TO GEY-BLK.                           
           MOVE K830-NUM-MAIL-POSTAL      TO WS-POSTAL.                         
                                                                                
           IF K830-NUM-MAIL-POSTAL = SPACES OR ZEROS                            
              MOVE SPACES            TO GEY-ADDR                                
              DISPLAY '- MAIL POSTAL EMPTY ' K830-NUM-MAIL-POSTAL               
                      ', REGN IS ' K800-NUM-REGN                                
           ELSE                                                                 
              STRING 'SINGAPORE '  K830-NUM-MAIL-POSTAL                         
                     DELIMITED BY SIZE INTO GEY-ADDR                            
           END-IF.                                                              
           IF K830-NUM-MAIL-POSTAL = ZEROES                                     
              MOVE '*'                    TO LGEY-OVERSEA                       
                                             WS-OVERSEA                         
           ELSE                                                                 
              MOVE SPACE                  TO LGEY-OVERSEA                       
                                             WS-OVERSEA                         
           END-IF.                                                              
           MOVE SPACES                    TO WS-LINK-AV02C015.                  
           MOVE K830-NUM-MAIL-POSTAL      TO AV02C015-IN-POSTCODE.              
           PERFORM 5000-CALL-AV02C015     THRU 5000-EXIT.                       
           MOVE AV02C015-OUT-POSTCODE-CHKSUM TO GEY-BAR-CODE.                   
                                                                                
       3050-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3055-FORMAT-HOME-ADDR.                                                   
      *------------------------------------------------------                   
           IF K830-NUM-ADDR-TYPE  = 'H'                                         
              MOVE K830-NME-STREET      TO GEY-STREET                           
              MOVE K830-NUM-LVL-CHAR    TO GEY-CHAR                             
              MOVE K830-NUM-LVL-NUM     TO GEY-LEVEL                            
              MOVE K830-NUM-LVL-DASH    TO GEY-DASH                             
              MOVE K830-NUM-HSEMAIN     TO GEY-HSE-MAIN                         
              MOVE K830-NUM-HSESUB      TO GEY-HSE-SUB                          
           ELSE                                                                 
              MOVE K830-NUM-FLAT        TO GEY-FLAT-NO                          
              MOVE K830-NME-STREET-BLDG TO GEY-STREET                           
           END-IF.                                                              
                                                                                
           MOVE K830-NUM-BLK            TO GEY-BLK.                             
           MOVE K830-NUM-POSTAL         TO WS-POSTAL.                           
                                                                                
           IF K830-NUM-POSTAL = SPACES OR ZEROS                                 
              DISPLAY '- RES POSTAL EMPTY ' K830-NUM-POSTAL                     
                      ', REGN IS ' K800-NUM-REGN                                
           ELSE                                                                 
              STRING 'SINGAPORE '  K830-NUM-POSTAL                              
                     DELIMITED BY SIZE INTO GEY-ADDR                            
           END-IF.                                                              
           IF K830-NUM-POSTAL = ZEROES                                          
              MOVE '*'                    TO LGEY-OVERSEA                       
                                             WS-OVERSEA                         
           ELSE                                                                 
              MOVE SPACE                  TO LGEY-OVERSEA                       
                                             WS-OVERSEA                         
           END-IF.                                                              
           MOVE SPACES                  TO WS-LINK-AV02C015.                    
           MOVE K830-NUM-POSTAL         TO AV02C015-IN-POSTCODE.                
           PERFORM 5000-CALL-AV02C015   THRU 5000-EXIT.                         
           MOVE AV02C015-OUT-POSTCODE-CHKSUM TO GEY-BAR-CODE.                   
                                                                                
                                                                                
       3055-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3100-PRINT-LETTER.                                                       
      *-------------------------------------------------------------            
                                                                                
      * LETTER TO BE SENT OUT                                                   
      *-------------------------------------------------------------            
           MOVE  SPACES              TO GEY-PRTREC.                             
                                                                                
           ADD   1                   TO WS-CNT-LETTER.                          
           ADD   1                   TO WS-REC-CNT.                             
           IF WS-PREV-BLK-NO  NOT = F120-BLK-NO                                 
              MOVE 1                 TO WS-REC-CNT                              
           END-IF.                                                              
           MOVE WS-REC-CNT           TO WS-RECORD-CNT.                          
           MOVE WS-RECORD-CNT        TO DJDE-LINE-PAGE1(49:4).                  
           WRITE GEY-PRTREC FROM DJDE-LINE-PAGE1 AFTER PAGE                     
           WRITE GEY-PRTREC FROM GEY-LINE01 AFTER PAGE.                         
      *    WRITE GEY-PRTREC FROM GEY-LINE02.                                    
                                                                                
           WRITE GEY-PRTREC FROM GEY-LINE03 AFTER 6.                            
           WRITE GEY-PRTREC FROM GEY-LINE03B.                                   
           WRITE GEY-PRTREC FROM GEY-LINE03C.                                   
           WRITE GEY-PRTREC FROM GEY-LINE03D.                                   
                                                                                
           WRITE GEY-PRTREC FROM GEY-LINE04.                                    
           WRITE GEY-PRTREC FROM GEY-LINE04B.                                   
           WRITE GEY-PRTREC FROM GEY-LINE04C.                                   
           WRITE GEY-PRTREC FROM GEY-LINE05 AFTER 2.                            
                                                                                
      *------------------------*                                                
      *SURVEY ON INTEREST FORM *                                                
      *------------------------*                                                
           WRITE GEY-PRTREC FROM DJDE-LINE-PAGE2A AFTER PAGE.                   
           MOVE SPACES        TO   GEY-PRTREC.                                  
                                                                                
           WRITE GEY-PRTREC FROM SURVEY-LINE01 AFTER 8.                         
           WRITE GEY-PRTREC FROM SURVEY-LINE02 AFTER 16.                        
           IF WS-CTR = 1                                                        
             WRITE GEY-PRTREC FROM SURVEY-UNDERLINE AFTER 14                    
             WRITE GEY-PRTREC FROM SURVEY-LINE03 AFTER 1                        
             WRITE GEY-PRTREC FROM SURVEY-LINE04                                
           ELSE                                                                 
             WRITE GEY-PRTREC FROM SURVEY-UNDERLINE AFTER 14                    
             WRITE GEY-PRTREC FROM SURVEY-LINE03 AFTER 1                        
             WRITE GEY-PRTREC FROM SURVEY-LINE04                                
             WRITE GEY-PRTREC FROM SURVEY-UNDERLINE AFTER 5                     
             WRITE GEY-PRTREC FROM SURVEY-LINE05 AFTER 1                        
             WRITE GEY-PRTREC FROM SURVEY-LINE06                                
           END-IF.                                                              
                                                                                
           MOVE SPACES        TO   GEY-PRTREC                                   
                                   DJDE-LINE-PAGE1(49:4).                       
           MOVE SPACES           TO GEY-PRTREC                                  
           WRITE GEY-PRTREC      FROM DJDE-LINE-PAGE3A    AFTER PAGE.           
           MOVE SPACES           TO GEY-PRTREC.                                 
           WRITE GEY-PRTREC AFTER PAGE.                                         
      *    WRITE GEY-PRTREC.                                                    
                                                                                
                                                                                
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3200-PRINT-AUDIT.                                                        
      *-------------------------------------------------------------            
           IF WS-LINE-CNT > 55                                                  
           OR WS-PREV-BLK-NO NOT = F120-BLK-NO                                  
              PERFORM 4000-PRINT-HEADING THRU 4000-EXIT                         
           END-IF.                                                              
                                                                                
           ADD 2                       TO WS-LINE-CNT.                          
           ADD   1                     TO WS-BLK-CNT.                           
                                                                                
           MOVE SPACES                 TO LGEY-PRTREC                           
                                          LGEYA-LINE1                           
                                          LGEYA-LINE2                           
                                          LGEYA-LINE3                           
                                          LGEYA-PRTREC.                         
           MOVE WS-BLK-CNT             TO LGEY-NO.                              
           STRING F120-SCHEME '-' F120-ACC '-' F120-CHK '-'                     
                  F120-ACC-TYPE                                                 
                  DELIMITED BY SIZE  INTO LGEY-SCH-ACC.                         
                                                                                
           MOVE F120-REGN-NO           TO LGEY-REGN-NO                          
                                          LGEYA-REGN-NO.                        
           MOVE WS-OVERSEA             TO LGEYA-OVERSEA.                        
                                                                                
           MOVE K800-NUM-NRIC1         TO LGEY-NRIC-NO                          
                                          LGEYA-NUM-NRIC1.                      
           MOVE WS-SURVEY-NAME1        TO LGEYA-NME-APPLICANT1                  
                                          LGEY-NAME.                            
           MOVE K800-NUM-NRIC2         TO LGEYA-NUM-NRIC2.                      
           MOVE WS-SURVEY-NAME2        TO LGEYA-NME-APPLICANT2.                 
           MOVE K800-NUM-CAT           TO LGEYA-ETHNIC-GROUP.                   
           MOVE K800-NUM-HOUSEHOLD     TO LGEYA-FT-ST.                          
           MOVE K800-NUM-ALLO-CAT      TO LGEYA-ALLOC-MODE.                     
           MOVE GEY-BLK                TO LGEY-BLK-NO.                          
           MOVE GEY-STREET             TO LGEY-STREET.                          
           MOVE GEY-FLAT-NO            TO LGEY-FLAT-NO.                         
           MOVE WS-POSTAL              TO LGEY-POSTAL-CODE.                     
                                                                                
           MOVE SPACES                 TO WS-STRING-TO-PARSE.                   
           MOVE WS-ADDRESS             TO WS-STRING-TO-PARSE.                   
           MOVE LENGTH OF WS-ADDRESS                                            
                                       TO WS-STRING-LENGTH.                     
           MOVE 'Y'                    TO WS-INSERT-SPACE-FLAG.                 
           PERFORM 3010-PARSE-ADDRESS  THRU 3010-EXIT.                          
           MOVE WS-PARSED-STRING       TO  LGEY-CORR-ADDR.                      
                                                                                
           MOVE SPACES                 TO WS-STRING-TO-PARSE.                   
           MOVE SURVEY-LINE02          TO WS-STRING-TO-PARSE.                   
           MOVE LENGTH OF SURVEY-LINE02                                         
                                       TO WS-STRING-LENGTH.                     
           MOVE 'Y'                    TO WS-INSERT-SPACE-FLAG.                 
           PERFORM 3010-PARSE-ADDRESS  THRU 3010-EXIT.                          
           MOVE WS-PARSED-STRING       TO  LGEYA-GEY-ADDRESS.                   
                                                                                
           MOVE ';'                    TO  FIL01 FIL02 FIL03                    
                                           FIL04 FIL05 FIL06                    
                                           FIL07 FIL08 FIL09.                   
                                                                                
           WRITE LGEY-PRTREC           FROM LGEY-DETAIL AFTER 2.                
                                                                                
           IF WS-LINE-CNT-A < 50                                                
              IF WS-LINE-CNT-A = ZEROES                                         
                 MOVE SPACES TO LGEYA-PRTREC                                    
                 WRITE LGEYA-PRTREC       AFTER PAGE                            
              END-IF                                                            
              WRITE LGEYA-PRTREC  FROM LGEYA-LINE1                              
              WRITE LGEYA-PRTREC  FROM LGEYA-LINE2                              
              WRITE LGEYA-PRTREC  FROM LGEYA-LINE3                              
           ELSE                                                                 
              WRITE LGEYA-PRTREC  FROM LGEYA-LINE1                              
              WRITE LGEYA-PRTREC  FROM LGEYA-LINE2                              
              WRITE LGEYA-PRTREC  FROM LGEYA-LINE3                              
              MOVE SPACES TO LGEYA-PRTREC                                       
              WRITE LGEYA-PRTREC       AFTER PAGE                               
              MOVE 4  TO  WS-LINE-CNT-A                                         
           END-IF                                                               
                                                                                
           ADD  3  TO WS-LINE-CNT-A.                                            
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4000-PRINT-HEADING.                                                      
      *-------------------------------------------------------------            
           IF WS-PAGE-CNT  NOT = 0                                              
              WRITE LGEY-PRTREC FROM LGEY-FOOTER AFTER 2                        
           END-IF.                                                              
                                                                                
           ADD  1                    TO WS-PAGE-CNT.                            
           MOVE WS-PAGE-CNT          TO LGEY-PAGE.                              
           MOVE F120-BLK-NO          TO LGEY-BLOCK.                             
           MOVE F120-STREET-NAME     TO LGEY-STREET-NAME.                       
                                                                                
           MOVE  8                   TO WS-LINE-CNT.                            
                                                                                
           IF WS-PREV-BLK-NO  NOT = F120-BLK-NO                                 
              ADD WS-BLK-CNT         TO WS-TOTAL-CNT                            
              INITIALIZE                WS-BLK-CNT                              
           END-IF.                                                              
           MOVE F120-BLK-NO          TO WS-PREV-BLK-NO.                         
                                                                                
           MOVE SPACES               TO LGEY-PRTREC.                            
           WRITE LGEY-PRTREC         FROM LGEY-HDR01      AFTER PAGE.           
           WRITE LGEY-PRTREC         FROM LGEY-HDR02.                           
           WRITE LGEY-PRTREC         FROM LGEY-HDR02A.                          
           WRITE LGEY-PRTREC         FROM LGEY-HDR03      AFTER 2.              
           WRITE LGEY-PRTREC         FROM LGEY-HDR04      AFTER 2.              
           MOVE ALL '='              TO LGEY-PRTREC.                            
           WRITE LGEY-PRTREC            AFTER 1.                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-CALL-AV02C015.                                                      
      *-------------------------------------------------------------            
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       6000-READ-K800.                                                          
      *-------------------------------------------------------------            
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           EVALUATE K800-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD  1                  TO WS-CNT-K800-MISS                 
                    DISPLAY 'K800 REC NOT FOUND : ' F120-REGN-NO                
                            ' ; ' F120-SCH-ACC-NO                               
                    SET WS-ERROR            TO TRUE                             
                    GO TO 6000-EXIT                                             
               WHEN OTHER                                                       
                    MOVE K800-STATUS              TO RETURN-CODE                
                    DISPLAY 'ERROR READING BP13K800, STATUS '                   
                             K800-STATUS                                        
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       7000-READ-K820.                                                          
      *-------------------------------------------------------------            
           READ BP13K820 KEY IS K820-KEY-FLD.                                   
                                                                                
           EVALUATE K820-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD  1                  TO WS-CNT-K820-MISS                 
                    MOVE SPACES             TO K820-NME-OCCP                    
                    DISPLAY 'K820 REC NOT FOUND : ' K800-NUM-REGN               
                    GO TO 7000-EXIT                                             
               WHEN OTHER                                                       
                    MOVE K820-STATUS              TO RETURN-CODE                
                    DISPLAY 'ERROR READING BP13K820, STATUS '                   
                             K820-STATUS                                        
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8000-READ-K830.                                                          
      *-------------------------------------------------------------            
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE K830-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD  1                  TO WS-CNT-K830-MISS                 
                    DISPLAY 'REGN-NUM' K830-NUM-REGN                            
                    DISPLAY 'K830 REC NOT FOUND : ' K800-NUM-NRIC1              
                                                    K800-NUM-NRIC2              
                    SET WS-ERROR            TO TRUE                             
                    GO TO 8000-EXIT                                             
               WHEN OTHER                                                       
                    MOVE K830-STATUS              TO RETURN-CODE                
                    DISPLAY 'ERROR READING BP13K830, STATUS '                   
                             K830-STATUS                                        
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9900-HANDLE-ABEND.                                                       
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY '- EDTGEY CONTROL TOTAL -------------'.                      
           DISPLAY 'TOTAL BM06F120 RECS READ  = ' WS-CNT-F120-READ.             
           DISPLAY 'TOTAL LETTERS PRINTED     = ' WS-CNT-LETTER.                
           DISPLAY 'TOTAL MISSING K800 REC    = ' WS-CNT-K800-MISS.             
           DISPLAY 'TOTAL MISSING K820 REC    = ' WS-CNT-K820-MISS.             
           DISPLAY 'TOTAL MISSING K830 REC    = ' WS-CNT-K830-MISS.             
                                                                                
           CLOSE BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BM06F120                                                       
                 BP13GEY                                                        
                 BP13LGEY                                                       
                 P13LGEYA.                                                      
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              MOVE K800-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K800 ' K800-STATUS                  
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              MOVE K820-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K820 ' K820-STATUS                  
           END-IF.                                                              
                                                                                
           IF K830-STATUS NOT = 00 AND 97                                       
              MOVE K830-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K830 ' K830-STATUS                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9999-WRAP-UP.                                                            
      *-------------------------------------------------------------            
           WRITE LGEY-PRTREC FROM LGEY-FOOTER AFTER 2.                          
           ADD WS-BLK-CNT                        TO WS-TOTAL-CNT.               
           MOVE WS-TOTAL-CNT                     TO LGEY-TOTAL.                 
           WRITE LGEY-PRTREC FROM LGEY-PRT-TOTAL AFTER 2.                       
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '-  CONTROL TOTAL -------------'.                            
           DISPLAY ' TOTAL BM06F120 REC READ : ' WS-CNT-F120-READ.              
           DISPLAY ' TOTAL LETTERS PRINTED   : ' WS-CNT-LETTER.                 
           DISPLAY ' TOTAL MISSING K800 REC  : ' WS-CNT-K800-MISS.              
           DISPLAY ' TOTAL MISSING K820 REC  : ' WS-CNT-K820-MISS.              
           DISPLAY ' TOTAL MISSING K830 REC  : ' WS-CNT-K830-MISS.              
                                                                                
           CLOSE BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BM06F120                                                       
                 BP13GEY                                                        
                 BP13LGEY                                                       
                 P13LGEYA.                                                      
                                                                                
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BP13K820 ' K820-STATUS                  
              MOVE K820-STATUS TO RETURN-CODE.                                  
                                                                                
           IF K830-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BP13K830 ' K830-STATUS                  
              MOVE K830-STATUS TO RETURN-CODE.                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
