       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C901.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
       DATE-WRITTEN.  29/07/92.                                                 
       REMARKS.                                                                 
      * ====================================================== *                
      *    OBJECTIVE  :                                        *                
      *        1.  A STANDARD ROUTINE TO CALCULATE THE INSTAL  *                
      *            AND SUBSEQUENT INSTAL                       *                
      *            FOR NORMAL SCHEMES                          *                
      *             &  ERL SCH B, C, D                         *                
      *        2.  THIS SUB-RTN IS USED IN SALES ORDER PANEL.  *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-RECORD.                                                           
           05  WS-INSTAL              PIC S9(5)V9(8)    VALUE ZERO.             
           05  WS-INSTAL1             PIC S9(5)V9(8)    VALUE ZERO.             
           05  WS-INSTAL2             PIC S9(5)V9(8)    VALUE ZERO.             
           05  WS-SUB-INSTAL          PIC S9(5)V9(8)    VALUE ZERO.             
           05  WS-INSTAL-DOL          PIC S9(5)         VALUE ZERO.             
           05  WS-INT                 PIC S9(2)V9(8)    VALUE ZERO.             
           05  WS-LOANAMT             PIC S9(7)V9(8)    VALUE ZERO.             
           05  WS-ERL-LOANAMT         PIC S9(7)V9(8)    VALUE ZERO.             
           05  WS-REPAY               PIC 99            VALUE ZERO.             
           05  WS-TEMP1               PIC S9(7)V9(8)    VALUE ZERO.             
           05  WS-TEMP2               PIC S9(7)V9(8)    VALUE ZERO.             
           05  WS-DIV                 PIC S9(2)V9(8)    VALUE ZERO.             
           05  PTR                    PIC 99            VALUE ZERO.             
           05  WS-INTDATE-MTH         PIC 9(5)          VALUE ZERO.             
           05  WS-ERLDATE-MTH         PIC 9(5)          VALUE ZERO.             
           05  WS-LOANDATE-MTH        PIC 9(5)          VALUE ZERO.             
           05  WS-LOANINT-MTH         PIC S9(5)         VALUE ZERO.             
           05  WS-REMAIN-MTH          PIC 9(5)          VALUE ZERO.             
           05  WS-REMAIN-YR           PIC 9(2)V99       VALUE ZERO.             
           05  WS-M                   PIC 9(2)V99       VALUE ZERO.             
           05  WS-REPAY-MTH           PIC S9(5)         VALUE ZERO.             
           05  WS-REPAY-YR            PIC S9(2)         VALUE ZERO.             
           05  WS-NUM-YR              PIC S9(2)         VALUE ZERO.             
           05  WS-FAC                 PIC S9(2)         VALUE ZERO.             
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
      **-----------------------------------------------------------*            
      *  01) NEW-INT-RATE         02) NEW INT RATE EFF DATE        *            
      *  03) ERL-CODES            04) ERL-COMM-DATE                *            
      *  05) LOAN-COMM-DATE       06) ORIG-LOAN                    *            
      *  07) ORIG-LOAN-REPAY      08) REV-LOAN                     *            
      *  09) REV-LOAN-REPAY       10) OS-LOAN - 2(INSTAL)          *            
      *  11) MTHLY INSTAL         12) SUBSEQ/INCR-INSTAL           *            
      *  13) ROOM                 14) FLAT-DESIGN-TYPE.            *            
      *------------------------------------------------------------*            
       01  DFHCOMMAREA.                                                         
           05  LNK-INT                 PIC 9V9(4).                              
           05  LNK-FAC                 PIC 9(2).                                
           05  LNK-ERL                 PIC X.                                   
           05  LNK-LOAN-AMT            PIC 9(7)V99.                             
           05  LNK-ERL-LOAN-AMT        PIC 9(7)V99.                             
           05  LNK-REPAY-PD            PIC 99.                                  
           05  LNK-RM                  PIC X(2).                                
           05  LNK-DESIGN-TYPE.                                                 
               10  LNK-CDE-MODL-DESG   PIC X(2).                                
               10  LNK-CDE-CONFIG-DESG PIC X(1).                                
           05  FILLER                  PIC X(1).                                
           05  LNK-MTHINST             PIC S9(5)V99.                            
           05  LNK-SUBINST             PIC S9(5)V99.                            
                                                                                
           EJECT                                                                
                                                                                
       PROCEDURE DIVISION USING DFHCOMMAREA.                                    
                                                                                
       0000-HANDLE-COND.                                                        
                                                                                
           MOVE LNK-LOAN-AMT      TO WS-LOANAMT.                                
           MOVE LNK-ERL-LOAN-AMT  TO WS-ERL-LOANAMT.                            
           MOVE LNK-REPAY-PD      TO WS-REPAY.                                  
           MOVE LNK-INT           TO WS-INT.                                    
           MOVE LNK-FAC           TO WS-FAC.                                    
                                                                                
       100-MAIN-ROUTINE.                                                        
           MOVE ZEROS TO  LNK-MTHINST LNK-SUBINST.                              
           IF LNK-ERL = '1' OR 'A' OR 'M'                                       
              PERFORM 105-SCHA-INSTAL-ROUTINE THRU 105-EXIT                     
           ELSE                                                                 
           IF LNK-ERL = '2' OR 'B'                                              
              PERFORM 200-SCHB-INSTAL-ROUTINE THRU 200-EXIT                     
           ELSE                                                                 
           IF LNK-ERL = '3' OR 'C'                                              
              PERFORM 300-SCHC-INSTAL-ROUTINE THRU 300-EXIT                     
           ELSE                                                                 
           IF LNK-ERL = '4' OR '6' OR 'D'                                       
              PERFORM 400-SCHD-INSTAL-ROUTINE THRU 400-EXIT                     
           ELSE                                                                 
              PERFORM 500-NORMAL-INSTAL-ROUTINE THRU 500-EXIT.                  
                                                                                
           PERFORM 600-ROUNDING-ROUTINE THRU 600-EXIT.                          
                                                                                
       100-END-ROUTINE.                                                         
           GOBACK.                                                              
                                                                                
       105-SCHA-INSTAL-ROUTINE.                                                 
      *----------------------------------------------------------*              
      *  SCHEME A -- BALLOONING;                                 *              
      *      A) FOR SALE SECTION IN THEIR CONVERSION OF LOAN     *              
      *      B) FOR CALCULATION OF INSTALMENT USE SAME FORMULA   *              
      *         AS FOR NORMAL SCHEME, BUT USE BASE LOAN INSTEAD  *              
      *         OF LOAN GRANTED.                                 *              
      *      C) SUBSEQUENT YEAR, INSTALMENT WILL BE BASED ON     *              
      *         A NEW FORMULA. BUT USE LOAN GRANTED.             *              
      *----------------------------------------------------------*              
                                                                                
           COMPUTE WS-DIV = WS-INT / WS-FAC.                                    
                                                                                
           COMPUTE WS-INSTAL = (WS-ERL-LOANAMT * (WS-DIV))/                     
               (1 - (1 / ((1 + (WS-DIV)) ** (WS-FAC * WS-REPAY)))).             
                                                                                
           COMPUTE WS-INSTAL = WS-INSTAL * (WS-FAC / 12).                       
                                                                                
           COMPUTE WS-TEMP1 = WS-LOANAMT * (( 1 + WS-DIV ) ** 10) -             
           ( (12 / WS-FAC) * WS-INSTAL * (( 1 + WS-DIV ) ** 10 - 1) /           
                      WS-DIV).                                                  
                                                                                
           COMPUTE WS-REPAY = WS-REPAY - 5.                                     
                                                                                
           COMPUTE WS-INSTAL1 =                                                 
           (WS-TEMP1 * WS-DIV * ((1 + WS-DIV) **                                
           (WS-FAC * WS-REPAY))).                                               
           COMPUTE WS-INSTAL2 = ((1 + WS-DIV) **                                
           (WS-FAC * WS-REPAY)) - 1.                                            
           COMPUTE WS-SUB-INSTAL = (WS-INSTAL1 / WS-INSTAL2) *                  
                                   (WS-FAC / 12).                               
                                                                                
       105-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-SCHB-INSTAL-ROUTINE.                                                 
      *----------------------------------------------------------*              
      *  SCHEME B -- BALLOONING;                                 *              
      *      A) FOR PURCHASE OF UP TO 4S FLATS ONLY              *              
      *      B) 25 YEARS REPAYMENT ONLY                          *              
      *      C) FIRST YEAR, INSTALMENT CALCULATED BASED ON LOAN  *              
      *         TAKEN                                            *              
      *      D) SUBSEQUENT YEAR, INSTALMENT BASED WILL BE        *              
      *         INCREASE OF FIXED AMOUNT OF $10/=                *              
      *----------------------------------------------------------*              
                                                                                
      *  PUBLIC INTEREST RATE, HALF YEARLY REST                                 
                                                                                
           COMPUTE WS-DIV = WS-INT / WS-FAC.                                    
           COMPUTE WS-INSTAL = (WS-LOANAMT * (WS-FAC / 12)) *                   
             (WS-DIV / (1 - (1 / (WS-DIV + 1) **                                
             (WS-FAC * WS-REPAY)))) -                                           
             10 * ((1 / (WS-DIV * (WS-DIV + 2))) -                              
             (WS-REPAY / ((WS-DIV + 1) ** (WS-FAC * WS-REPAY) - 1))).           
                                                                                
      *  SUBSEQUENT INCREASE OF $10 PER YEAR                                    
                                                                                
           MOVE 10 TO WS-SUB-INSTAL.                                            
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-SCHC-INSTAL-ROUTINE.                                                 
      *----------------------------------------------------------*              
      *  SCHEME C -- BALLOONING;                                 *              
      *      A) FOR PURCHASE OF UP TO 4S FLATS ONLY              *              
      *      B) 25 YEARS REPAYMENT ONLY                          *              
      *      C) INSTALMENT FOR 1ST 5 YEARS BASED ON ROOM-TYPE    *              
      *         3RM                : $  75                       *              
      *         4I, 4S, 4NG, 4A    : $ 150                       *              
      *         5I, 5A, MG TYPE  C : $ 150                       *              
      *         EXEC, MG TYPE A, B : $ 300                       *              
      *  RM-TYPE = 8 , CDE-MODL-DESG-TYPE = '07' ==> MG TYPE A   *              
      *  RM-TYPE = 8 , CDE-MODL-DESG-TYPE = '08' ==> MG TYPE B   *              
      *  RM-TYPE = 8 , CDE-MODL-DESG-TYPE = '09' ==> MG TYPE C   *              
      *      D) SUBSEQUENT 5 YEARS,FIXED INCREASE                *              
      *----------------------------------------------------------*              
                                                                                
      *  INSTALMENT FOR 1ST 5 YEARS BASED ON ROOM-TYPE                          
                                                                                
           IF LNK-RM = '03'                                                     
              MOVE 75 TO WS-INSTAL                                              
           ELSE                                                                 
           IF LNK-RM = '04'                                                     
              MOVE 150 TO WS-INSTAL                                             
           ELSE                                                                 
           IF LNK-RM = '05'                                                     
              MOVE 250 TO WS-INSTAL                                             
           ELSE                                                                 
           IF LNK-RM = '06'                                                     
              MOVE 300 TO WS-INSTAL                                             
           ELSE                                                                 
           IF LNK-RM = '08'                                                     
              IF LNK-CDE-MODL-DESG = '07' OR '08'                               
                 MOVE 300 TO WS-INSTAL                                          
              ELSE                                                              
                 IF LNK-CDE-MODL-DESG = '09'                                    
                    MOVE 250 TO WS-INSTAL.                                      
                                                                                
      *  PUBLIC INTEREST RATE, HALF YEARLY REST                                 
                                                                                
           COMPUTE WS-DIV = WS-INT / WS-FAC.                                    
           MOVE 0 TO WS-TEMP1, WS-TEMP2.                                        
           PERFORM 310-SCHC-SUMMATION                                           
                   VARYING PTR FROM 0 BY 1                                      
                     UNTIL PTR > (WS-REPAY / 5 - 1).                            
                                                                                
      *  CALC SUBS INCREASE FOR NEXT 5 YEARS BASED ON LOAN-AMT                  
                                                                                
           COMPUTE WS-SUB-INSTAL = (WS-FAC / 12) *                              
                                   (WS-LOANAMT * WS-DIV *                       
                   ((1 + WS-DIV) ** (WS-REPAY / 5 * 10))) /                     
                   ((((1 + WS-DIV) ** 10 - 1)) *                                
                   WS-TEMP1) - (WS-INSTAL * WS-TEMP2 / WS-TEMP1).               
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       310-SCHC-SUMMATION.                                                      
           COMPUTE WS-TEMP1 = WS-TEMP1 + PTR * ((1 + WS-DIV) **                 
                              (10 * (WS-REPAY / 5 - 1 - PTR))).                 
           COMPUTE WS-TEMP2 = WS-TEMP2 + (1 + WS-DIV) ** (10 * PTR).            
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-SCHD-INSTAL-ROUTINE.                                                 
      *------------------------------------------------------------*            
      *   NO.OF YRS FROM ERL EFF-DATE TO INT-REV-DATE < 5 YRS      *            
      *------------------------------------------------------------*            
      *   PUBLIC INTEREST RATE, HALF YEARLY REST -- NORMAL INSTAL  *            
      *------------------------------------------------------------*            
      *                          {     R   (    R ) NF  }          *            
      *                          { P * - * (1 + - )     }          *            
      *                       F  {     F   (    F )     }          *            
      *   INSTALMENT= 0.75 * --- {----------------------}          *            
      *                      12  {    (      R ) NF     }          *            
      *                          {    ( 1 +  - )    - 1 }          *            
      *                          {    (      F )        }          *            
      *------------------------------------------------------------*            
      *    P = LOAN GRANTED/REVISED LOAN                           *            
      *    R = REVISED INTEREST RATE IN %                          *            
      *    F = 2:HALF YEARLY                                       *            
      *    N = LOAN REPAYMENT PERIOD/REVISED LOAN PERIOD           *            
      *------------------------------------------------------------*            
           MOVE ZEROES TO WS-DIV                                                
                          WS-INSTAL                                             
                          WS-INSTAL1                                            
                          WS-INSTAL2                                            
                          WS-TEMP1                                              
                          WS-SUB-INSTAL.                                        
                                                                                
           IF WS-LOANAMT NOT > 0                                                
           OR WS-REPAY NOT > 0                                                  
              GO TO 400-EXIT.                                                   
                                                                                
           COMPUTE WS-DIV = WS-INT / WS-FAC.                                    
                                                                                
           COMPUTE WS-INSTAL1 =                                                 
           WS-LOANAMT * WS-DIV * ((1 + WS-DIV) ** (WS-FAC * WS-REPAY)).         
                                                                                
           COMPUTE WS-INSTAL2 =                                                 
                   ((1 + WS-DIV) ** (WS-FAC * WS-REPAY)) - 1.                   
                                                                                
           COMPUTE WS-INSTAL = 0.75 * (WS-INSTAL1 / WS-INSTAL2) *               
                               (WS-FAC / 12).                                   
                                                                                
      *------------------------------------------------------------*            
      *                           { (    R ) MF     }              *            
      *                           { (1 + - )    - 1 }              *            
      *         (     R )MF   12I { (    F )        }              *            
      *   E = L ( 1 + - )   - --- {-----------------}              *            
      *         (     F )      F  {       R         }              *            
      *                           {       -         }              *            
      *                           {       F         }              *            
      *------------------------------------------------------------*            
      *    L = O/S LOAN BALANCE                                    *            
      *    M = NO OF YRS FR DATE OF INT REVISION TO END OF 5  YRS  *            
      *    I = REVISED INSTALMENT                                  *            
      *------------------------------------------------------------*            
                                                                                
           COMPUTE WS-M = 2 * 5.                                                
                                                                                
           COMPUTE WS-TEMP1 =                                                   
             WS-LOANAMT * (( 1 + WS-DIV) ** WS-M) -                             
             ((12 / WS-FAC) * WS-INSTAL) *                                      
             ((((1 + WS-DIV) ** WS-M) - 1) / WS-DIV).                           
                                                                                
      *------------------------------------------------------------*            
      *  COMPUTE BALANCE OF REPAYMENT PERIOD AFTER 5 YEARS         *            
      *  LOAN REPAYMENT PERIOD SHOULD BE ORIGINAL REPAYMENT PERIOD *            
      *------------------------------------------------------------*            
      *    B = LOAN REPAYMENT PERIOD - 5 YRS                       *            
      *------------------------------------------------------------*            
                                                                                
                                                                                
           COMPUTE WS-REPAY-YR = WS-REPAY - 5.                                  
                                                                                
      *----------------------------------------------------------*              
      *  COMPUTE SUBQ INSTALMENT                                 *              
      *                           {     R   (    R ) BF  }       *              
      *                           { E * - * (1 + - )     }       *              
      *                        F  {     F   (    F )     }       *              
      *   SUBSEQUENT INSTAL = --- {----------------------}       *              
      *                       12  {    (      R ) BF     }       *              
      *                           {    ( 1 +  - )    - 1 }       *              
      *                           {    (      F )        }       *              
      *----------------------------------------------------------*              
           IF WS-REPAY-YR NOT > 0                                               
              GO TO 400-EXIT.                                                   
                                                                                
           COMPUTE WS-SUB-INSTAL = (WS-FAC / 12) *                              
           ((WS-TEMP1 * WS-DIV * ((1 + WS-DIV) **                               
           (WS-FAC * WS-REPAY-YR)))                                             
           / ((1 + WS-DIV) ** (WS-FAC * WS-REPAY-YR) - 1)).                     
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-NORMAL-INSTAL-ROUTINE.                                               
      *----------------------------------------------------------*              
      *  SCHEME N -- NORMAL CASE                                 *              
      *      A) ALL PURCHASES                                    *              
      *      B) 5,10,15,20, OR 25 YEARS REPAYMENT                *              
      *----------------------------------------------------------*              
           MOVE 0 TO WS-INSTAL.                                                 
           MOVE 0 TO WS-SUB-INSTAL.                                             
      *----------------------------------------------------------*              
      *  PUBLIC INTEREST RATE, HALF YEARLY REST                  *              
      *----------------------------------------------------------*              
                                                                                
           COMPUTE WS-DIV = WS-INT / WS-FAC.                                    
           COMPUTE WS-INSTAL = (WS-LOANAMT * (WS-DIV))/                         
           (1 - (1 / (1 + WS-DIV) **                                            
           (WS-FAC * WS-REPAY))).                                               
           COMPUTE WS-INSTAL = (WS-INSTAL) * (WS-FAC / 12).                     
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------*              
      *   ROUNDING FOR MTHLY INSTALMENT & SUBSEQUENT INSTALMENT  *              
      *      LAST 2 DIGITS NOT 0 , ROUND UP, REGARDLESS OF       *              
      *      POSITIVE OR NEGATIVE                                *              
      *----------------------------------------------------------*              
       600-ROUNDING-ROUTINE.                                                    
           COMPUTE WS-INSTAL-DOL = WS-INSTAL + 0.99.                            
           MOVE WS-INSTAL-DOL TO LNK-MTHINST.                                   
                                                                                
           COMPUTE WS-INSTAL-DOL = WS-SUB-INSTAL + 0.99.                        
           MOVE WS-INSTAL-DOL TO LNK-SUBINST.                                   
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
