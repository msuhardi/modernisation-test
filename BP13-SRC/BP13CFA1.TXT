       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CFA1.                                                 
       AUTHOR.        RJB1.                                                     
       DATE-WRITTEN.  27/04/17.                                                 
      *****************************************************************         
      * OBJECTIVE - BP13K482 HOUSEKEEPING TO SEPARATE ACCOUNTS WITH             
      *             COMPLETED PAYMENT WITH MORE THAN 1 YEAR                     
      *----------------------------------------------------------------         
      * REF NO     DATE      BY   AMENDMENTS/ENHANCEMENTS                       
      * --------  ---------- ---- -----------------------                       
      * BP135315  27/04/2017 RJB1 NEW PGM COPY OF AB35315A OF AB03              
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F482  ASSIGN TO BP13F482.                                 
           SELECT SY02F001  ASSIGN TO SY02F001.                                 
           SELECT BP13K482  ASSIGN TO BP13K482                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K482-KEY-FLD                              
                  FILE STATUS      IS K482-STATUS                               
                  ACCESS           IS SEQUENTIAL.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K482                   RECORD CONTAINS 1000 CHARACTERS.          
       COPY BP13K482.                                                           
                                                                                
       FD  SY02F001                   RECORD CONTAINS 80 CHARACTERS.            
       01  F001-RECORD.                                                         
           05 F001-SYS-DTE.                                                     
              10 F001-DTE-CC                 PIC 99.                            
              10 F001-DTE-YY                 PIC 99.                            
              10 F001-DTE-MM                 PIC 99.                            
              10 F001-DTE-DD                 PIC 99.                            
           05 FILLER                         PIC X(72).                         
                                                                                
       FD  BP13F482                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 100 CHARACTERS            
                                      LABEL RECORDS ARE STANDARD                
                                      RECORDING MODE IS F.                      
       01  F482-RECORD.                                                         
           05 F482-NUM-HDBREF-NEW            PIC X(11).                         
           05 F482-NUM-HDBREF-DISPOSAL       PIC X(11).                         
           05 F482-DTE-COMPLETE              PIC X(08).                         
           05 F482-CDE-SMS-TAG               PIC X(01).                         
           05 FILLER                         PIC X(69).                         
                                                                                
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-STATUS.                                                           
           05 K482-STATUS                    PIC 9(2)  VALUE ZERO.              
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-K482-RD                 PIC 9(6)  VALUE ZERO.              
           05 WS-CNT-K482-VAL                PIC 9(6)  VALUE ZERO.              
           05 WS-CNT-K482-INV                PIC 9(6)  VALUE ZERO.              
           05 WS-CNT-F482-WRT                PIC 9(6)  VALUE ZERO.              
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-EOF-SW                      PIC X     VALUE 'N'.               
              88 K482-EOF                              VALUE 'Y'.               
           05 WS-VAL-SW                      PIC X     VALUE 'N'.               
              88 K482-VAL                              VALUE 'Y'.               
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-HDB-REF-NUM                 PIC X(11).                         
           05 WS-REC-CTR                     PIC 9(02) VALUE ZERO.              
                                                                                
       01  WS-TABLE.                                                            
           05 WS-K482-TABLE OCCURS 0 TO 50 TIMES                                
                            DEPENDING ON WS-REC-CTR                             
                            INDEXED BY TIX.                                     
              10 WS-TAB-NUM-HDBREF-NEW       PIC X(11).                         
              10 WS-TAB-NUM-HDBREF-DISPOSAL  PIC X(11).                         
              10 WS-TAB-DTE-COMPLETE         PIC X(08).                         
              10 WS-TAB-CDE-SMS-TAG          PIC X(01).                         
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP       THRU 1000-EXIT.                          
           PERFORM 2000-PROC-BP13K482  THRU 2000-EXIT                           
                   UNTIL K482-EOF.                                              
           PERFORM 9999-WRAP-UP.                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13K482                                                 
                       SY02F001                                                 
                OUTPUT BP13F482.                                                
                                                                                
           IF  K482-STATUS NOT = 0 AND 97                                       
               DISPLAY 'ERROR OPEN BP13K482 : ' K482-STATUS                     
               MOVE K482-STATUS TO RETURN-CODE                                  
               GO TO 9999-WRAP-UP                                               
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           IF F001-DTE-YY = 00                                                  
              SUBTRACT 1  FROM F001-DTE-CC                                      
              MOVE 99     TO   F001-DTE-YY                                      
           ELSE                                                                 
              SUBTRACT 1  FROM F001-DTE-YY                                      
           END-IF.                                                              
                                                                                
           MOVE SPACES    TO   WS-HDB-REF-NUM.                                  
                                                                                
       1000-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ******************************************************************        
       2000-PROC-BP13K482.                                                      
      ******************************************************************        
                                                                                
           READ BP13K482 AT END                                                 
                MOVE 'Y' TO WS-EOF-SW                                           
                IF WS-CNT-K482-RD NOT = 0 AND                                   
                   WS-HDB-REF-NUM NOT = SPACES                                  
                   PERFORM 4000-WRITE-OUTPUT THRU 4000-EXIT                     
                END-IF                                                          
                GO TO 2000-EXIT.                                                
           ADD  1  TO WS-CNT-K482-RD.                                           
                                                                                
           IF (K482-NUM-HDBREF-NEW NOT = WS-HDB-REF-NUM)                        
              IF WS-HDB-REF-NUM NOT = SPACES                                    
                 PERFORM 4000-WRITE-OUTPUT THRU 4000-EXIT                       
              END-IF                                                            
              MOVE ZERO                TO WS-REC-CTR                            
              MOVE K482-NUM-HDBREF-NEW TO WS-HDB-REF-NUM                        
              MOVE 'N'                 TO WS-VAL-SW                             
              MOVE SPACES              TO WS-TABLE                              
           END-IF.                                                              
                                                                                
           PERFORM 3000-CHECK-RECORDS THRU 3000-EXIT.                           
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3000-CHECK-RECORDS.                                                      
      ******************************************************************        
      *    VALIDATE SMS-TAG IF COMPLETED AND CHECK WHETHER             *        
      *    DTE-COMPLETE IS MORE THAN 1 YEAR AGO.                       *        
      *----------------------------------------------------------------*        
                                                                                
           IF COMPLETED AND K482-DTE-COMPLETE > SPACES                          
              IF K482-DTE-COMPLETE < F001-SYS-DTE                               
                 MOVE 'Y'    TO WS-VAL-SW                                       
              END-IF                                                            
           ELSE                                                                 
              IF COMPLETED AND K482-DTE-COMPLETE = SPACES                       
                 MOVE 'Y'    TO WS-VAL-SW                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1             TO WS-REC-CTR.                                     
           SET TIX           TO WS-REC-CTR.                                     
                                                                                
           MOVE K482-NUM-HDBREF-NEW      TO                                     
                WS-TAB-NUM-HDBREF-NEW(TIX).                                     
           MOVE K482-NUM-HDBREF-DISPOSAL TO                                     
                WS-TAB-NUM-HDBREF-DISPOSAL(TIX).                                
           MOVE K482-DTE-COMPLETE        TO                                     
                WS-TAB-DTE-COMPLETE(TIX).                                       
           MOVE K482-CDE-SMS-TAG         TO                                     
                WS-TAB-CDE-SMS-TAG(TIX).                                        
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       4000-WRITE-OUTPUT.                                                       
      ******************************************************************        
                                                                                
           IF K482-VAL                                                          
              PERFORM VARYING TIX FROM 1 BY 1 UNTIL TIX > WS-REC-CTR            
                 MOVE WS-K482-TABLE(TIX) TO F482-RECORD                         
                 WRITE F482-RECORD                                              
                 ADD 1                   TO WS-CNT-F482-WRT                     
              END-PERFORM                                                       
              ADD 1 TO WS-CNT-K482-VAL                                          
           ELSE                                                                 
              ADD 1 TO WS-CNT-K482-INV                                          
           END-IF.                                                              
                                                                                
       4000-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ******************************************************************        
       9999-WRAP-UP.                                                            
      ******************************************************************        
           DISPLAY  SPACES.                                                     
           DISPLAY '********* CONTROL   COUNTS **********'.                     
           DISPLAY 'PROGRAM-ID : BP13CFA1'                                      
           DISPLAY 'NO. OF BP13K482 RECS READ    : ' WS-CNT-K482-RD.            
           DISPLAY 'NO. OF BP13K482 ACCT VALID   : ' WS-CNT-K482-VAL.           
           DISPLAY 'NO. OF BP13K482 ACCT INVALID : ' WS-CNT-K482-INV.           
           DISPLAY 'NO. OF BP13F482 RECS WRITTEN : ' WS-CNT-F482-WRT.           
           DISPLAY '*************************************'.                     
                                                                                
           CLOSE    BP13K482                                                    
                    SY02F001                                                    
                    BP13F482.                                                   
                                                                                
           IF  K482-STATUS NOT = 0 AND 97                                       
               DISPLAY 'ERROR CLOSE BP13K482 : ' K482-STATUS                    
               MOVE K482-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
      * ------------  END OF SOURCE  ------------ *                             
