      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C774.                                                 
      *AUTHOR.        JK.                                                       
                                                                                
      * ========================================================= *             
      *                SYSTEM OF COMMITMENT (BP13)                *             
      * ========================================================= *             
      *                                                           *             
      *  OBJECTIVE : TO REFORMAT MHA MOBILITY RECORD FOR          *             
      *              SUB-TENANT RPS (BP13F725 ==> BP13F716)       *             
      *                                                           *             
      *              BASED ON THE BLK/CDE_ST IN BP13F725, RGM     *             
      *              WILL ACCESS PBF TO GET EST-CDE WHICH WILL    *             
      *              THEN BE USED TO GET NEW TOWN CODE FROM       *             
      *              BM06K510 ESTATE TABLE                        *             
      *                                                           *             
      *  REMARKS   : THIS PROGRAM IS AVAILABLE ONLY FOR MATURE    *             
      *              NEW TOWN THEREFORE NUM-REF REMAIN NO CHANGE  *             
      *                                                           *             
      * CHGE-NO  BY   DATE   DESCRIPTION                          *             
      * -------  --- ------  -------------------------------      *             
      * C930335  JK  250293  CONVERT MHA RECS TO F716 FORMAT      *             
      * N930012  JK  240993  EXPAND NRIC & F725 FILE FORMAT       *             
      * C9309B4  JK  041193  'CLEAN'/FORMAT F725                  *             
      * N940006  JK  220394  CLEAN UP FILE FORMATS                *             
      * SOC-PH9  GD  111094  TO CHANGE BP13K558 TO BP13K825       *             
      * N950112  YCH 200195  EXTRACT NEW TOWN ALL BY ADDRESS      *             
      * C950199  YCH 170295  CHANGE USING JOINT TABLE             *             
      * BP130062 LSB 091295  RELEASE CTRL ON IDP, ALLOC & ELIG SCH*             
      * BP130180 SSS 100996  INCLUDE IA COMMENTS                  *             
      * BP130418 YCH 181098  Y2K                                  *             
      * BP130630 NOR 140199  CHGE F715 F716 EXPAND TO 350         *             
      * BP130652 LSB 170999  EXPAND K730 RECL FROM 300 TO 500     *             
      * BP130883 CLT 061100  READ PIDB INTERFACE TABLE            *             
      * BP131064 CKK 201201  OUTPUT MATCHED RESIDENT FILE WHEN    *             
      *                      NT-RESIDE = NT-REQUEST               *             
      * BP132376 MJ16290703  USE REGN NO INSTEAD OF REFNO OF FILE *             
      *                      BP13F725                             *             
      * BP132538 SK  270504  INCLUDE BP13K767                     *             
      * BP133610 ESA1110509  TO REPLACE BP13K767 W/ BP13K816      *             
      * BP135254 SMR 170414  TO REPLACE BP13K816 W/ BP13K813      *             
      * BP135944 SMR 150915  REMOVE REFERENCE TO CDE-NT6/7/8      *             
      * ========================================================= *             
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F725 ASSIGN TO BP13F725.                                  
                                                                                
           SELECT P13F716A ASSIGN TO P13F716A.                                  
                                                                                
           SELECT P13F716B ASSIGN TO P13F716B.                                  
                                                                                
           SELECT BM06K510 ASSIGN TO BM06K510                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS RANDOM                                
                           RECORD KEY IS K510-ESTCDE                            
                           FILE STATUS IS WS-K510-STAT.                         
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS RANDOM                                
                           RECORD KEY IS K730-KEY-NEW                           
                           FILE STATUS IS WS-K730-STAT.                         
                                                                                
           SELECT BP13K825 ASSIGN TO BP13K825                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS RANDOM                                
                           RECORD KEY IS K825-KEY-FLD                           
                           FILE STATUS IS WS-K825-STAT.                         
                                                                                
           SELECT BP13K813 ASSIGN  TO  BP13K813                                 
                     ORGANIZATION  IS  INDEXED                                  
                     ACCESS MODE   IS  RANDOM                                   
                     RECORD KEY    IS  K813-KEY-FLD                             
                     FILE STATUS   IS  WS-K813-STAT.                            
                                                                                
           SELECT BP13F716  ASSIGN TO BP13F716.                                 
           SELECT BP13OUT   ASSIGN TO BP13OUT.                                  
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD  BP13F725                                                             
           BLOCK CONTAINS   0  RECORDS                                          
           RECORDING MODE F                                                     
           RECORD CONTAINS 211 CHARACTERS                                       
           LABEL RECORD IS STANDARD.                                            
       COPY BP13F725.                                                           
                                                                                
       FD  BP13K730                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K730.                                                           
                                                                                
       FD  BP13K825                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K825.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD  BP13F716                                                             
           BLOCK CONTAINS   0  RECORDS                                          
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       COPY BP13F716.                                                           
                                                                                
       FD  P13F716A                                                             
           BLOCK CONTAINS   0  RECORDS                                          
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       01  P13F716A-REC         PIC X(350).                                     
                                                                                
       FD  P13F716B                                                             
           BLOCK CONTAINS   0  RECORDS                                          
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       01  P13F716B-REC         PIC X(350).                                     
                                                                                
       FD  BP13OUT                                                              
           BLOCK CONTAINS   0  RECORDS                                          
           RECORD CONTAINS 211 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       01  BP13OUT-REC          PIC X(211).                                     
                                                                                
       FD  BM06K510                                                             
           RECORD CONTAINS 60 CHARACTERS.                                       
       COPY BM06K510.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-ALLOCSCH-TYPE                 PIC X(3).                           
       COPY PRIOSCH.                                                            
                                                                                
       01  WS-K510-STAT                     PIC 99        VALUE 0.              
       01  WS-K730-STAT                     PIC 99        VALUE 0.              
       01  WS-K825-STAT                     PIC 99        VALUE 0.              
       01  WS-K813-STAT                     PIC 99        VALUE 0.              
       01  WS-F725-EOF                      PIC X         VALUE 'N'.            
       01  WS-CHK-FLG                       PIC X         VALUE 'Y'.            
                                                                                
       01  WS-CONTROL-COUNT.                                                    
           05  WS-CNT                       PIC 9         VALUE ZERO.           
           05  WS-CNT-F725-READ             PIC 9(8)      VALUE ZERO.           
           05  WS-CNT-OUT-WRT               PIC 9(8)      VALUE ZERO.           
           05  WS-CNT-NO-NAME               PIC 9(8)      VALUE ZERO.           
           05  WS-CNT-UNMATCH               PIC 9(8)      VALUE ZERO.           
           05  WS-CNT-UNMTCH-BLK            PIC 9(8)      VALUE ZERO.           
           05  WS-CNT-UNMTCH-ST             PIC 9(8)      VALUE ZERO.           
           05  WS-CNT-BLNK-BLK-CDE-ST       PIC 9(8)      VALUE ZERO.           
           05  WS-CNT-TOKENS                PIC 9(2)      VALUE ZERO.           
           05  WS-CNT-MTCH-RES              PIC 9(8)      VALUE ZERO.           
           05  WS-CNT-UNMTCH-RES            PIC 9(8)      VALUE ZERO.           
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND                 PIC S9(4) COMP VALUE +000.          
           05  WS-SQL-NOT-FOUND             PIC S9(4) COMP VALUE +100.          
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-BLK-NO                    PIC X(5).                           
           05  WS-POSTAL-CODE               PIC X(6).                           
           05  WS-STREET                    PIC X(32).                          
           05  WS-CDE-EST.                                                      
               10  WS-CDE-EST-NUM           PIC 9(3).                           
           05  WS-NT                        PIC X(3) VALUE SPACES.              
                                                                                
       01  WS-STR-TOKENS.                                                       
           05  WS-TOKEN1                    PIC X(30).                          
           05  WS-TOKEN2                    PIC X(30).                          
           05  WS-TOKEN3                    PIC X(30).                          
           05  WS-TOKEN4                    PIC X(30).                          
           05  WS-TOKEN5                    PIC X(30).                          
           05  WS-TOKEN6                    PIC X(30).                          
           05  WS-TOKEN7                    PIC X(30).                          
           05  WS-TOKEN8                    PIC X(30).                          
           05  WS-TOKEN9                    PIC X(30).                          
           05  WS-TOKEN10                   PIC X(30).                          
                                                                                
       01  WS-DATES.                                                            
           05  WS-DTE-BALLOT.                                                   
               10  WS-YEAR                  PIC X(4).                           
               10  WS-MONTH                 PIC X(2).                           
           05  WS-SYSTEM-DATE.                                                  
               10  WS-YY                    PIC X(4).                           
               10  WS-MM                    PIC X(2).                           
               10  WS-DD                    PIC X(2).                           
                                                                                
       01   WS-F716-REC.                                                        
            05  WS-F716-NUM-REGN                PIC X(08).                      
            05  WS-F716-NUM-REFERENCE REDEFINES WS-F716-NUM-REGN.               
                10  WS-F716-NUM-REF             PIC X(06).                      
                10  FILLER                      PIC X(02).                      
            05  WS-F716-NUM-NRIC                PIC X(09).                      
            05  WS-F716-NUM-RDB-DP-OM           PIC X(01).                      
            05  WS-F716-NUM-HHSIZE              PIC X(02).                      
            05  WS-F716-NUM-RELATION-APPLN      PIC X(02).                      
            05  WS-F716-NUM-HA1-AGE             PIC X(03).                      
            05  WS-F716-NUM-CCC                 PIC X(02).                      
            05  WS-F716-DTE-REGN                PIC X(08).                      
            05  FILLER                       PIC X(04).                         
            05  WS-F716-CDE-NT-REQ              PIC X(03).                      
            05  WS-F716-NUM-PRIORITY-SCH        PIC X(03).                      
            05  WS-F716-CDE-FLAT-TYPE-REQ       PIC X(02).                      
            05  WS-F716-CDE-PARENT-CHILD        PIC X(01).                      
            05  WS-F716-CDE-SOLE-LESSEE-SMS     PIC X(01).                      
            05  WS-F716-NUM-CATEGORY            PIC X(01).                      
            05  WS-F716-NUM-HDB-REF.                                            
                10  WS-F716-NUM-SCH-ACCNT       PIC X(09).                      
                10  WS-F716-NUM-LSE-CDE         PIC X(02).                      
            05  FILLER                       PIC X(13).                         
            05  WS-F716-NUM-ROLE                PIC X(03).                      
            05  WS-F716-NUM-MTH-RESIDE          PIC X(04).                      
            05  WS-F716-CDE-FLAT-TYPE-RESIDE    PIC X(02).                      
            05  WS-F716-CDE-ED-CURR             PIC X(02).                      
            05  WS-F716-CDE-NT-RESIDE           PIC X(03).                      
            05  WS-F716-CDE-OCCUPIER            PIC X(01).                      
            05  WS-F716-NUM-SUB-TENANT          PIC X(01).                      
            05  WS-F716-CDE-FILE                PIC X(01).                      
            05  WS-F716-CDE-HOUSEHOLD           PIC X(01).                      
            05  WS-F716-RES-STATUS              PIC X(02).                      
            05  WS-F716-CDE-RESALE-STATUS       PIC X(03).                      
            05  FILLER                       PIC X(01).                         
            05  WS-F716-CDE-RELATIONSHIP        PIC X(02).                      
            05  WS-F716-DTE-BALLOT              PIC X(06).                      
            05  WS-F716-DTE-ACCEPTED            PIC X(08).                      
            05  WS-F716-DTE-MOVING-IN           PIC X(08).                      
            05  WS-F716-DTE-MOVING-OUT          PIC X(08).                      
            05  WS-F716-CDE-ED-GEOG             PIC X(02).                      
            05  FILLER                          PIC X(166).                     
            05  WS-F716-ADDRESS.                                                
                10  WS-F716-NUM-BLK             PIC X(5).                       
                10  WS-F716-NME-STREET          PIC X(32).                      
                10  WS-F716-NUM-HSE.                                            
                    15  WS-F716-NUM-HSELVL      PIC X(2).                       
                    15  WS-F716-NUM-HSEMAIN     PIC X(4).                       
                    15  WS-F716-NUM-HSESUB      PIC X(3).                       
                10  WS-F716-NUM-POSTAL-DSTRCT   PIC X(6).                       
                                                                                
           EXEC SQL INCLUDE P13VINTF END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      *================*                                                        
       0000-MAIN-LOGIC.                                                         
      *================*                                                        
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F725   THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-ROUTINE    THRU 3000-EXIT                          
                   UNTIL  WS-F725-EOF = 'Y'.                                    
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
                                                                                
      *================*                                                        
       1000-OPEN-FILES.                                                         
      *================*                                                        
           OPEN INPUT  BP13F725                                                 
                       BM06K510                                                 
                       BP13K730                                                 
                       BP13K825                                                 
                       BP13K813                                                 
                OUTPUT BP13F716                                                 
                       P13F716A                                                 
                       P13F716B                                                 
                       BP13OUT.                                                 
                                                                                
           IF WS-K510-STAT NOT = ZEROES AND 97                                  
              MOVE WS-K510-STAT            TO RETURN-CODE                       
              DISPLAY 'OPEN BM06K510 FAIL STATUS = ' WS-K510-STAT               
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           IF WS-K730-STAT NOT = ZEROES AND 97                                  
              MOVE WS-K510-STAT            TO RETURN-CODE                       
              DISPLAY 'OPEN BP13K730 FAIL STATUS = ' WS-K730-STAT               
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           IF WS-K825-STAT NOT = ZEROES AND 97                                  
              MOVE WS-K825-STAT            TO RETURN-CODE                       
              DISPLAY 'OPEN BP13K825 FAIL STATUS = ' WS-K825-STAT               
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           IF WS-K813-STAT NOT = ZEROES AND 97                                  
              MOVE WS-K813-STAT            TO RETURN-CODE                       
              DISPLAY 'OPEN BP13K813 FAIL STATUS = ' WS-K813-STAT               
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       2000-READ-BP13F725.                                                      
      *===================*                                                     
           READ BP13F725 AT END                                                 
                        MOVE 'Y' TO WS-F725-EOF                                 
                        GO TO 2000-EXIT.                                        
                                                                                
           ADD 1          TO WS-CNT-F725-READ.                                  
           MOVE SPACES    TO BP13F716-REC.                                      
           INITIALIZE     WS-F716-REC.                                          
           MOVE SPACES    TO WS-F716-REC.                                       
           MOVE 'Y'       TO WS-CHK-FLG.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==================*                                                      
       3000-MAIN-ROUTINE.                                                       
      *==================*                                                      
           PERFORM 3100-EDIT-INPUT-FIELDS THRU 3100-EXIT.                       
           PERFORM 3300-READ-PIDB         THRU 3300-EXIT.                       
           PERFORM 3400-GET-NT-REQ        THRU 3400-EXIT.                       
           PERFORM 3500-GET-NEWTOWN-CODE  THRU 3500-EXIT.                       
                                                                                
           IF WS-CHK-FLG = 'Y'                                                  
              PERFORM 4000-MOVE-DATES      THRU 4000-EXIT                       
              PERFORM 5000-WRITE-BP13F716  THRU 5000-EXIT.                      
              PERFORM 5500-WRITE-P13F716A  THRU 5500-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F725   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=======================*                                                 
       3100-EDIT-INPUT-FIELDS.                                                  
      *=======================*                                                 
                                                                                
           MOVE F725-NME-STREET        TO WS-STREET                             
           MOVE F725-NUM-BLK-HSE       TO WS-BLK-NO                             
           MOVE F725-CDE-POSTAL        TO WS-POSTAL-CODE                        
                                                                                
           PERFORM 3110-UNSTRING-STNAME THRU 3110-EXIT.                         
                                                                                
           PERFORM 3120-TRIM-BLK        THRU 3120-EXIT                          
                VARYING WS-CNT FROM 1 BY 1                                      
                UNTIL WS-CNT > 4.                                               
                                                                                
           IF WS-BLK-NO (4:1) IS ALPHABETIC                                     
              PERFORM 3130-SHIFT-ALPHA-BLK THRU 3130-EXIT.                      
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===============================================================*         
      * UNSTRING WS-STREET TO REMOVE EXTRA SPACES AND STRING IT BACK  *         
      * WITH ONLY ONE SPACE BETWEEN EACH WORD.                        *         
      *===============================================================*         
      *=======================*                                                 
       3110-UNSTRING-STNAME.                                                    
      *=======================*                                                 
           MOVE SPACES TO WS-STR-TOKENS.                                        
           UNSTRING WS-STREET   DELIMITED BY ALL SPACES                         
                 INTO WS-TOKEN1 WS-TOKEN2 WS-TOKEN3 WS-TOKEN4                   
                      WS-TOKEN5 WS-TOKEN6 WS-TOKEN7 WS-TOKEN8                   
                      WS-TOKEN9 WS-TOKEN10                                      
                 TALLYING IN WS-CNT-TOKENS.                                     
                                                                                
           IF WS-TOKEN1 = 'TOA' AND WS-TOKEN2 = 'PAYOH' AND                     
              WS-TOKEN3 = 'LORONG'                                              
              MOVE SPACES TO WS-STREET                                          
              STRING WS-TOKEN3 DELIMITED BY SPACE                               
                      ' '      DELIMITED BY SIZE                                
                     WS-TOKEN4 DELIMITED BY SPACE                               
                      ' '      DELIMITED BY SIZE                                
                     WS-TOKEN1 DELIMITED BY SPACE                               
                      ' '      DELIMITED BY SIZE                                
                     WS-TOKEN2 DELIMITED BY SPACE                               
                               INTO WS-STREET                                   
           ELSE                                                                 
              MOVE SPACES TO WS-STREET                                          
              STRING WS-TOKEN1 DELIMITED BY SPACE                               
                     ' '      DELIMITED BY SIZE                                 
                     WS-TOKEN2 DELIMITED BY SPACE                               
                     ' '      DELIMITED BY SIZE                                 
                     WS-TOKEN3 DELIMITED BY SPACE                               
                     ' '      DELIMITED BY SIZE                                 
                     WS-TOKEN4 DELIMITED BY SPACE                               
                     ' '      DELIMITED BY SIZE                                 
                     WS-TOKEN5 DELIMITED BY SPACE                               
                     ' '      DELIMITED BY SIZE                                 
                     WS-TOKEN6 DELIMITED BY SPACE                               
                     ' '      DELIMITED BY SIZE                                 
                     WS-TOKEN7 DELIMITED BY SPACE                               
                     ' '      DELIMITED BY SIZE                                 
                     WS-TOKEN8 DELIMITED BY SPACE                               
                     ' '      DELIMITED BY SIZE                                 
                     WS-TOKEN9 DELIMITED BY SPACE                               
                     ' '      DELIMITED BY SIZE                                 
                     WS-TOKEN10 DELIMITED BY SPACE                              
                     INTO WS-STREET.                                            
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=======================*                                                 
       3120-TRIM-BLK.                                                           
      *=======================*                                                 
           IF WS-BLK-NO (4:1) = SPACES OR LOW-VALUES                            
              MOVE WS-BLK-NO (3:1) TO WS-BLK-NO (4:1)                           
              MOVE SPACES          TO WS-BLK-NO (3:1)                           
           END-IF.                                                              
                                                                                
           IF WS-BLK-NO (3:1) = SPACES OR LOW-VALUES                            
              MOVE WS-BLK-NO (2:1) TO WS-BLK-NO (3:1)                           
              MOVE SPACES          TO WS-BLK-NO (2:1)                           
           END-IF.                                                              
                                                                                
           IF WS-BLK-NO (2:1) = SPACES OR LOW-VALUES                            
              MOVE WS-BLK-NO (1:1) TO WS-BLK-NO (2:1)                           
              MOVE SPACES          TO WS-BLK-NO (1:1)                           
           END-IF.                                                              
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=======================*                                                 
       3130-SHIFT-ALPHA-BLK.                                                    
      *=======================*                                                 
           IF WS-BLK-NO (5:1) = SPACES OR LOW-VALUES                            
              MOVE WS-BLK-NO (4:1) TO WS-BLK-NO (5:1)                           
              MOVE SPACES          TO WS-BLK-NO (4:1)                           
           END-IF.                                                              
                                                                                
           IF WS-BLK-NO (4:1) = SPACES OR LOW-VALUES                            
              MOVE WS-BLK-NO (3:1) TO WS-BLK-NO (4:1)                           
              MOVE SPACES          TO WS-BLK-NO (3:1)                           
           END-IF.                                                              
                                                                                
           IF WS-BLK-NO (3:1) = SPACES OR LOW-VALUES                            
              MOVE WS-BLK-NO (2:1) TO WS-BLK-NO (3:1)                           
              MOVE SPACES          TO WS-BLK-NO (2:1)                           
           END-IF.                                                              
                                                                                
           IF WS-BLK-NO (2:1) = SPACES OR LOW-VALUES                            
              MOVE WS-BLK-NO (1:1) TO WS-BLK-NO (2:1)                           
              MOVE SPACES          TO WS-BLK-NO (1:1)                           
           END-IF.                                                              
                                                                                
       3130-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=================*                                                       
       3300-READ-PIDB.                                                          
      *=================*                                                       
           EXEC SQL SELECT NUM_ESTATE_CIM, NUM_ED_GL                            
                INTO :DCLPIDB-INTERFACE.NUM-ESTATE-CIM,                         
                     :DCLPIDB-INTERFACE.NUM-ED-GL                               
                FROM  PIDB_INTERFACE                                            
                WHERE NUM_BLK         = :WS-BLK-NO                              
                AND   NUM_POSTAL_CODE = :WS-POSTAL-CODE                         
           END-EXEC.                                                            
                                                                                
           DISPLAY 'SQL CODE =  ' SQLCODE.                                      
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
           WHEN -811                                                            
              DISPLAY 'PIDB INFO FOUND !!'                                      
              MOVE NUM-ESTATE-CIM          TO WS-CDE-EST                        
              MOVE NUM-ED-GL               TO F716-CDE-ED-CURR                  
                                              WS-F716-CDE-ED-CURR               
              DISPLAY 'CDE EST  =  ' WS-CDE-EST                                 
              DISPLAY 'ED  GL   =  ' F716-CDE-ED-CURR                           
           WHEN 100                                                             
              DISPLAY 'NO MATCH: BLK ' WS-BLK-NO ' PST CDE'                     
                      WS-POSTAL-CODE ' ' F725-NUM-REGN                          
              MOVE SPACES                  TO F716-CDE-ED-CURR                  
                                              WS-F716-CDE-ED-CURR               
              MOVE SPACES                  TO F716-CDE-NT-RESIDE                
                                              WS-F716-CDE-NT-RESIDE             
                                              WS-CDE-EST                        
              ADD  1                       TO WS-CNT-UNMTCH-BLK                 
              GO TO 3300-EXIT                                                   
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READING PIDB INTERFACE TABLE '                  
              PERFORM 9900-ABORT-PROCESS THRU 9900-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       3400-GET-NT-REQ.                                                         
      *===================*                                                     
           MOVE SPACES        TO K730-NUM-REGN.                                 
           MOVE F725-NUM-REGN TO K730-NUM-REGN.                                 
                                                                                
           READ BP13K730.                                                       
                                                                                
           IF WS-K730-STAT = 0                                                  
              PERFORM 3420-CHECK-NT     THRU 3420-EXIT                          
              MOVE WS-ALLOCSCH-TYPE     TO F716-NUM-PRIORITY-SCH                
                                           WS-F716-NUM-PRIORITY-SCH             
              MOVE K730-CDE-FLAT-TYPE   TO F716-CDE-FLAT-TYPE-REQ               
                                           WS-F716-CDE-FLAT-TYPE-REQ            
           ELSE                                                                 
              IF WS-K730-STAT = 23                                              
                 PERFORM 6000-WRITE-BP13OUT THRU 6000-EXIT                      
                 MOVE SPACES        TO F716-CDE-NT-REQ WS-CHK-FLG               
                                       WS-F716-CDE-NT-REQ                       
              ELSE                                                              
                 MOVE WS-K730-STAT            TO RETURN-CODE                    
                 DISPLAY 'ERROR READING BP13K730 FAIL STATUS = '                
                                         WS-K730-STAT                           
                 GO TO 9999-CLOSE-FILES.                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============*                                                          
       3420-CHECK-NT.                                                           
      *==============*                                                          
           SET WS-ALLOC-PTR TO 1.                                               
           SEARCH WS-ALLOC-SCH                                                  
              AT END MOVE 'XXX' TO WS-ALLOCSCH-TYPE                             
              WHEN K730-CDE-ALLOC-SCH =                                         
                                WS-ALLOC-SCH-DESP(WS-ALLOC-PTR)                 
              MOVE WS-MAIN-SCH-CD(WS-ALLOC-PTR) TO                              
                                      WS-ALLOCSCH-TYPE.                         
                                                                                
           MOVE SPACES TO F716-CDE-NT-REQ                                       
                          WS-F716-CDE-NT-REQ.                                   
                                                                                
           IF WS-ALLOCSCH-TYPE = 'REN' AND                                      
              (F716-CDE-NT-RESIDE NOT = SPACES AND LOW-VALUES)                  
               PERFORM 3421-CHECK-REN-NT THRU 3421-EXIT                         
           ELSE                                                                 
               MOVE K730-CDE-NT1 TO F716-CDE-NT-REQ                             
                                    WS-F716-CDE-NT-REQ.                         
                                                                                
       3420-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=================*                                                       
       3421-CHECK-REN-NT.                                                       
      *=================*                                                       
                                                                                
           MOVE K730-CDE-NT1          TO WS-NT.                                 
           PERFORM 6500-READ-BP13K813 THRU 6500-EXIT.                           
                                                                                
           IF F716-CDE-NT-RESIDE = WS-NT                                        
              MOVE K730-CDE-NT1 TO F716-CDE-NT-REQ                              
                                   WS-F716-CDE-NT-REQ                           
           ELSE                                                                 
           IF F716-CDE-NT-RESIDE = K730-CDE-NT2                                 
              MOVE K730-CDE-NT2 TO F716-CDE-NT-REQ                              
                                   WS-F716-CDE-NT-REQ                           
           ELSE                                                                 
           IF F716-CDE-NT-RESIDE = K730-CDE-NT3                                 
              MOVE K730-CDE-NT3 TO F716-CDE-NT-REQ                              
                                   WS-F716-CDE-NT-REQ                           
           ELSE                                                                 
           IF F716-CDE-NT-RESIDE = K730-CDE-NT4                                 
              MOVE K730-CDE-NT4 TO F716-CDE-NT-REQ                              
                                   WS-F716-CDE-NT-REQ                           
           ELSE                                                                 
           IF F716-CDE-NT-RESIDE = K730-CDE-NT5                                 
              MOVE K730-CDE-NT5 TO F716-CDE-NT-REQ                              
                                   WS-F716-CDE-NT-REQ                           
           ELSE                                                                 
              MOVE K730-CDE-NT1 TO F716-CDE-NT-REQ                              
                                   WS-F716-CDE-NT-REQ.                          
                                                                                
       3421-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=====================*                                                   
       3500-GET-NEWTOWN-CODE.                                                   
      *=====================*                                                   
      *==================================================*                      
      * RETRIEVE THE NEWTOWN CODE FROM THE BM06K510 TABLE*                      
      * USING THE ESTATE CODE FROM THE BUILDING TABLE    *                      
      *==================================================*                      
           MOVE WS-CDE-EST-NUM   TO K510-ESTCDE.                                
                                                                                
           READ BM06K510.                                                       
           IF WS-K510-STAT = 0                                                  
              MOVE K510-ALPHA-NTCDE  TO F716-CDE-NT-RESIDE                      
                                        WS-F716-CDE-NT-RESIDE                   
           ELSE                                                                 
              IF WS-K510-STAT = 23                                              
                 MOVE SPACES            TO F716-CDE-NT-RESIDE                   
                                           WS-F716-CDE-NT-RESIDE                
              ELSE                                                              
                 MOVE WS-K510-STAT            TO RETURN-CODE                    
                 DISPLAY 'ERROR READING BM06K510 FAIL STATUS = '                
                                     WS-K510-STAT                               
                 GO TO 9999-CLOSE-FILES.                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       4000-MOVE-DATES.                                                         
      *===================*                                                     
           MOVE '99999999'              TO F716-DTE-MOVING-OUT                  
                                             WS-F716-DTE-MOVING-OUT.            
           MOVE F725-DTE-1ST-REG        TO F716-DTE-MOVING-IN                   
                                              WS-F716-DTE-MOVING-IN.            
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
                                                                                
      *    IF WS-MM < '04'                                                      
      *       MOVE '01' TO WS-MONTH                                             
      *    ELSE IF WS-MM < '07'                                                 
      *       MOVE '04' TO WS-MONTH                                             
      *    ELSE IF WS-MM < '10'                                                 
      *       MOVE '07' TO WS-MONTH                                             
      *    ELSE IF WS-MM < '13'                                                 
      *       MOVE '10' TO WS-MONTH.                                            
                                                                                
      *    MOVE WS-YY         TO WS-YEAR.                                       
           MOVE K730-DTE-BALLOT  TO F716-DTE-BALLOT                             
                                 WS-F716-DTE-BALLOT.                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       5000-WRITE-BP13F716.                                                     
      *===================*                                                     
           MOVE F725-NUM-REGN      TO F716-NUM-REGN                             
                                      WS-F716-NUM-REGN.                         
           MOVE F725-NUM-NRIC      TO F716-NUM-NRIC                             
                                      WS-F716-NUM-NRIC.                         
                                                                                
           WRITE BP13F716-REC.                                                  
           ADD 1 TO WS-CNT-OUT-WRT.                                             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       5500-WRITE-P13F716A.                                                     
      *===================*                                                     
                                                                                
           MOVE WS-F716-CDE-NT-REQ TO WS-NT.                                    
           PERFORM 6500-READ-BP13K813 THRU 6500-EXIT.                           
                                                                                
           IF WS-F716-CDE-NT-RESIDE = WS-NT                                     
              MOVE SPACES             TO P13F716A-REC                           
              MOVE 'N'                TO WS-F716-NUM-SUB-TENANT                 
              MOVE WS-F716-REC        TO P13F716A-REC                           
              WRITE P13F716A-REC                                                
              ADD 1 TO WS-CNT-MTCH-RES                                          
           ELSE                                                                 
              MOVE SPACES             TO P13F716B-REC                           
              MOVE WS-F716-REC        TO P13F716B-REC                           
              WRITE P13F716B-REC                                                
              ADD 1 TO WS-CNT-UNMTCH-RES                                        
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       6000-WRITE-BP13OUT.                                                      
      *===================*                                                     
           MOVE BP13F725-REC TO BP13OUT-REC                                     
                                                                                
           WRITE BP13OUT-REC.                                                   
           ADD 1 TO WS-CNT-UNMATCH.                                             
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6500-READ-BP13K813.                                                      
      *===================*                                                     
                                                                                
           MOVE SPACES                TO BP13K813-REC.                          
           INITIALIZE                    BP13K813-REC.                          
           MOVE WS-NT                 TO K813-NUM-ZONE.                         
           MOVE K730-CDE-FLAT-TYPE    TO K813-NUM-FLAT-TYPE.                    
           MOVE K730-DTE-BALLOT       TO K813-DTE-BALLOT.                       
                                                                                
           READ BP13K813.                                                       
           IF WS-K813-STAT = 00                                                 
              MOVE K813-CDE-NT              TO WS-NT                            
           ELSE                                                                 
              IF WS-K813-STAT = 23                                              
                 CONTINUE                                                       
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K813,STATUS ' WS-K813-STAT             
                 MOVE WS-K813-STAT       TO RETURN-CODE                         
                 GO TO 9999-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================*                        
      *TERMINATE PROCESSING IF THERE IS A SEVERE ERROR *                        
      *================================================*                        
      *===================*                                                     
       9900-ABORT-PROCESS.                                                      
      *===================*                                                     
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE                 TO RETURN-CODE.                         
           MOVE '99'                    TO RETURN-CODE.                         
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
       9900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=================*                                                       
       9999-CLOSE-FILES.                                                        
      *=================*                                                       
           DISPLAY ' '.                                                         
           DISPLAY ' **************** BP13C774 ******************** '.          
           DISPLAY ' '.                                                         
           DISPLAY 'TOTAL F725 REC READ     = ' WS-CNT-F725-READ.               
           DISPLAY 'TOTAL F716 REC CREATED  = ' WS-CNT-OUT-WRT.                 
           DISPLAY 'TOTAL MATCHED RESIDENT  = ' WS-CNT-MTCH-RES.                
           DISPLAY 'TOTAL UNMATCHED RES     = ' WS-CNT-UNMTCH-RES.              
           DISPLAY 'UNMATCHED - K730 BALLOT = ' WS-CNT-UNMATCH.                 
           DISPLAY 'UNMATCHED - K825 OCCUP  = ' WS-CNT-NO-NAME.                 
           DISPLAY 'UNMATCHED - BLOCK NO.   = ' WS-CNT-UNMTCH-BLK.              
           DISPLAY 'UNMATCHED - STREET NAME = ' WS-CNT-UNMTCH-ST.               
           DISPLAY 'BLANK - BLK / CDE_ST    = ' WS-CNT-BLNK-BLK-CDE-ST.         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE      BP13F725                                                  
                      BM06K510                                                  
                      BP13K730                                                  
                      BP13K825                                                  
                      BP13K813                                                  
                      BP13F716                                                  
                      P13F716A                                                  
                      P13F716B                                                  
                      BP13OUT.                                                  
                                                                                
           IF WS-K510-STAT NOT = ZEROES                                         
              DISPLAY ' FILE CLOSING ERROR , WS-K510-STAT :',                   
                                  WS-K510-STAT                                  
              MOVE WS-K510-STAT            TO RETURN-CODE.                      
                                                                                
           IF WS-K730-STAT NOT = ZEROES                                         
              DISPLAY ' FILE CLOSING ERROR , WS-K730-STAT :',                   
                                  WS-K730-STAT                                  
              MOVE WS-K730-STAT            TO RETURN-CODE.                      
                                                                                
           IF WS-K825-STAT NOT = ZEROES                                         
              DISPLAY ' FILE CLOSING ERROR , WS-K825-STAT :',                   
                                  WS-K825-STAT                                  
              MOVE WS-K825-STAT            TO RETURN-CODE.                      
                                                                                
           IF WS-K813-STAT NOT = ZEROES                                         
              DISPLAY ' FILE CLOSING ERROR , WS-K813-STAT :',                   
                                  WS-K813-STAT                                  
              MOVE WS-K813-STAT            TO RETURN-CODE.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
