      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C38Y.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  MAY 07, 2015.                                             
      *===============================================================*         
      * OBJECTIVE  :  PRINT BUY FIRST CASES                           *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F800                                     *         
      *                                                                         
      * OUTPUT FILE:  1. BP13L38Y                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP136548 30/12/2016 PCL4 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F800  ASSIGN        TO BP13F800.                         
           SELECT  MAILC38Y  ASSIGN        TO MAILC38Y.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   MAILC38Y                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  100 CHARACTERS.                                    
       01   MAILC38Y-REC               PIC X(100).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F800             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-ADDRESS.                                                          
           05 WS-BLK-NO                PIC X(05) VALUE SPACES.                  
           05 FILLER                   PIC X     VALUE SPACES.                  
           05 WS-FLAT-NO.                                                       
              10 FILLER                PIC X     VALUE '#'.                     
              10 WS-LVL-NO             PIC X(2)  VALUE SPACES.                  
              10 FILLER                PIC X     VALUE '-'.                     
              10 WS-UNIT-NO            PIC X(4)  VALUE SPACES.                  
           05 FILLER                   PIC X(1)  VALUE SPACES.                  
           05 WS-STREET                PIC X(25) VALUE SPACES.                  
           05 WS-SINGAPORE             PIC X(01) VALUE 'S'.                     
           05 FILLER                   PIC X(1)  VALUE SPACES.                  
           05 WS-OPEN                  PIC X(01) VALUE '('.                     
           05 WS-POSTAL-CODE           PIC X(6)  VALUE SPACES.                  
           05 WS-CLOSE                 PIC X(01) VALUE ')'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-F800-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L38Y-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(5)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-2NDCA-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-NUM-DTE-CA            PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-NUM-NRIC              PIC X(9)  VALUE SPACES.                  
           05 WS-NUM-FTS               PIC X(2)  VALUE SPACES.                  
           05 WS-NUM-RELN              PIC X(20) VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
                                                                                
       01  L38Y-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L38Y'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L38Y-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L38Y-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L38Y-HEADER02.                                                       
           05  FILLER              PIC X(22)    VALUE SPACES.                   
           05  FILLER              PIC X(32)    VALUE                           
                   'Approval obtained for change in '.                          
           05  FILLER              PIC X(30) VALUE                              
                   'cash proceeds (Buy 1st)'.                                   
                                                                                
       01  L38Y-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE 'NRIC No(s)'.             
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  FILLER              PIC X(50)    VALUE 'FLAT ADDRESS'.           
                                                                                
       01  L38Y-HEADER04.                                                       
           05  FILLER              PIC X(22)    VALUE SPACES.                   
           05  FILLER              PIC X(31)     VALUE                          
                   'Approval has been obtained for '.                           
           05  FILLER              PIC X(30) VALUE                              
                   'the following applications.'.                               
                                                                                
                                                                                
       01  L38Y-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38Y-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38Y-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38Y-NRIC1          PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(03)        VALUE SPACES.               
           05  L38Y-FLAT-ADDRESS   PIC X(50)        VALUE SPACES.               
                                                                                
       01  L38Y-DETAIL-NRIC2.                                                   
           05  FILLER              PIC X(16)        VALUE SPACES.               
           05  L38Y-NRIC2          PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
                                                                                
       01  L38Y-DETAIL-NRIC3.                                                   
           05  FILLER              PIC X(16)        VALUE SPACES.               
           05  L38Y-NRIC3          PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
                                                                                
       01  L38Y-DETAIL-NRIC4.                                                   
           05  FILLER              PIC X(16)        VALUE SPACES.               
           05  L38Y-NRIC4          PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-EMPTY-RPT.                                                    
               10 FILLER                  PIC X(30) VALUE SPACES.               
               10 FILLER                  PIC X(21)                             
                                          VALUE '<   EMPTY  REPORT   >'.        
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(55)                               
               VALUE 'RCPT TO:<RSMAIL@hdb.gov.sg>'.                             
           05  WS-MAIL-RPT4             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT5             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT6             PIC X(55)                               
               VALUE 'TO:<RSMAIL@hdb.gov.sg>'.                                  
           05  WS-MAIL-RPT7             PIC X(100) VALUE                        
                   'SUBJECT: Approval obtained for change in cash '.            
                                                                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01  WS-LINK-BP13C913.                                                    
           05  LINK-NUM-SCH-ACC.                                                
               10  LINK-NUM-SCH        PIC X(4).                                
               10  LINK-NUM-ACC        PIC X(5).                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F800      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F800 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F800                                                 
              OUTPUT   MAILC38Y.                                                
                                                                                
           PERFORM 6000-PRINT-HEADER       THRU 6000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F800.                                                      
      ******************************************************************        
                                                                                
           READ BP13F800           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F800                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F800-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F800-DTE-UPDATE-SUBTAG = WS-SYSTEM-DATE                           
              IF F800-NUM-BUY-SELL-TAG = 'B'                                    
                 PERFORM 4000-PRINT-REPORT         THRU 4000-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800              THRU 2000-EXIT.              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           MOVE SPACES                    TO L38Y-DETAIL01.                     
           INITIALIZE                        L38Y-DETAIL01.                     
                                                                                
           ADD 1                          TO WS-SNO                             
           MOVE WS-SNO                    TO WS-SNO-ZZZZ9                       
           MOVE WS-SNO-ZZZZ9              TO L38Y-SN                            
           MOVE F800-NUM-REGN             TO L38Y-REGN                          
           MOVE F800-NUM-NRIC1            TO L38Y-NRIC1.                        
                                                                                
           IF F800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              MOVE SPACES                       TO BP13COMM8-REC                
              INITIALIZE                           BP13COMM8-REC                
                                                                                
              MOVE SPACES                       TO WS-BLK-NO                    
                                                   WS-LVL-NO                    
                                                   WS-UNIT-NO                   
                                                   WS-STREET                    
                                                   WS-POSTAL-CODE               
                                                                                
              MOVE F800-NUM-SCH-ACC             TO LINK-NUM-SCH-ACC             
              PERFORM 5000-CALL-BP13C913      THRU 5000-EXIT                    
                                                                                
              MOVE COMM8-NUM-BLK                TO WS-BLK-NO                    
              MOVE COMM8-NUM-LEVEL              TO WS-LVL-NO                    
              MOVE COMM8-NUM-UNIT-MAIN          TO WS-UNIT-NO                   
              MOVE COMM8-NME-STREET             TO WS-STREET                    
              MOVE COMM8-NUM-POSTAL-CODE        TO WS-POSTAL-CODE               
                                                                                
              MOVE WS-ADDRESS                   TO L38Y-FLAT-ADDRESS            
           END-IF.                                                              
                                                                                
           WRITE MAILC38Y-REC                 FROM L38Y-DETAIL01.               
           ADD 1                                TO WS-CNT-L38Y-WRITE.           
                                                                                
           IF F800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE F800-NUM-NRIC2               TO L38Y-NRIC2                   
              WRITE MAILC38Y-REC              FROM L38Y-DETAIL-NRIC2            
           END-IF.                                                              
                                                                                
           IF F800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE F800-NUM-NRIC3               TO L38Y-NRIC3                   
              WRITE MAILC38Y-REC              FROM L38Y-DETAIL-NRIC3            
           END-IF.                                                              
                                                                                
           IF F800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE F800-NUM-NRIC4               TO L38Y-NRIC4                   
              WRITE MAILC38Y-REC              FROM L38Y-DETAIL-NRIC4            
           END-IF.                                                              
                                                                                
           MOVE  SPACES                         TO   MAILC38Y-REC.              
           WRITE MAILC38Y-REC.                                                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-CALL-BP13C913.                                                      
      ******************************************************************        
                                                                                
           CALL 'BP13C913' USING WS-LINK-BP13C913,                              
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       6000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-SYSTEM-DATE.              
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L38Y-DATE-HDR.                             
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L38Y-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L38Y-PAGE-HDR.                
                                                                                
           WRITE MAILC38Y-REC FROM WS-MAIL-RPT1.                                
           WRITE MAILC38Y-REC FROM WS-MAIL-RPT2.                                
           WRITE MAILC38Y-REC FROM WS-MAIL-RPT3.                                
           WRITE MAILC38Y-REC FROM WS-MAIL-RPT4.                                
           WRITE MAILC38Y-REC FROM WS-MAIL-RPT5.                                
           WRITE MAILC38Y-REC FROM WS-MAIL-RPT6.                                
                                                                                
           STRING 'SUBJECT: Approval obtained for change in cash '              
                  'proceeds (Buy 1st)'                                          
               DELIMITED BY SIZE INTO WS-MAIL-RPT7.                             
                                                                                
           WRITE MAILC38Y-REC FROM WS-MAIL-RPT7.                                
                                                                                
           MOVE SPACES TO MAILC38Y-REC.                                         
           WRITE          MAILC38Y-REC.                                         
                                                                                
           WRITE MAILC38Y-REC FROM L38Y-HEADER01.                               
           WRITE MAILC38Y-REC FROM L38Y-HEADER02.                               
                                                                                
           MOVE SPACES          TO MAILC38Y-REC.                                
           WRITE MAILC38Y-REC.                                                  
                                                                                
           WRITE MAILC38Y-REC FROM L38Y-HEADER03.                               
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           MOVE  SPACES                         TO   MAILC38Y-REC.              
           WRITE MAILC38Y-REC.                                                  
                                                                                
           IF WS-CNT-L38Y-WRITE > 0                                             
              WRITE MAILC38Y-REC                 FROM L38Y-HEADER04             
           ELSE                                                                 
              MOVE '      No record for the day. ' TO MAILC38Y-REC              
              WRITE MAILC38Y-REC                                                
           END-IF.                                                              
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C38Y'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F800 RECORDS READ............. : '            
                    WS-F800-READ.                                               
           DISPLAY '(2) NO OF BP13L38Y RECORDS WRITTEN.......... : '            
                    WS-CNT-L38Y-WRITE.                                          
                                                                                
           CLOSE    BP13F800                                                    
                    MAILC38Y.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
