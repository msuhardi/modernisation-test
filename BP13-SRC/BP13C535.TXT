       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C535.                                                 
      *AUTHOR.        LJL.                                                      
      *DATE-WRITTEN.  31/03/95.                                                 
      *DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  PROGRAM TO MATCH BP13F520 WITH BP13F800 AND   *                
      *          UPDATE DTE-BALLOT AND TME-PIGEONHOLING        *                
      *                                                        *                
      *     INPUT :                                            *                
      *      1. BP13F520 (TO BE SORTED BY KEY-FLD NUM-REGN).   *                
      *      2. BP13F713                                       *                
      *      3. BP13F800 (TO BE SORTED BY KEY-FLD NUM-REGN).   *                
      *                                                        *                
      *     OUTPUT :                                           *                
      *      1. P13F800A                                      *                 
      *                                                        *                
      *  CHG-NO  BY  ON      DESCRIPTION                       *                
      *-----------------------------------                     *                
      * BP130418 SIA 070798  CATER FOR YR2000                  *                
      * BP130652 NOR 200299  EXPAND F520 RECL FROM 300 TO 500  *                
      * BP130703 LSB 100799  1)PRINT RPT-WAITING TIME 2)SC/CC  *                
      *                      TAG=Y 3)CTRL TRANSFER REQ         *                
      * BP130790 SCK 020200  TO ADD IN IN-SOURCE DOCUMENTATION *                
      * BP130823 AKT 140400  TO INCLUDE COHORT FILE.           *                
      * BP130836 AKT 030500  TO CHANGE BP13K800 TO BP13F800 &  *                
      *                      TO MATCH F520 AND F800.           *                
      **********************************************************                
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
           SELECT BP13F713 ASSIGN TO BP13F713.                                  
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT P13F800A ASSIGN TO P13F800A.                                  
                                                                                
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD   BP13F520                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F520.                                                           
                                                                                
       FD   BP13F713                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 20 CHARACTERS                                       
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F713.                                                           
      *                                                                         
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F800.                                                           
      *                                                                         
       FD   P13F800A                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F                                                 
            LABEL RECORDS IS STANDARD.                                          
       01   P13F800A-MASTER.                                                    
            05 FILLER                  PIC X(03).                               
            05 F800A-NUM-REGN          PIC X(8).                                
            05 FILLER                  PIC X(1989).                             
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01  WS-VARIABLE.                                                         
           05  WS-F520-EOF                PIC X(1)    VALUE 'N'.                
           05  WS-F713-EOF                PIC X(1)    VALUE 'N'.                
           05  WS-F520-READ               PIC 9(5)    VALUE 0.                  
           05  WS-F713-READ               PIC 9(5)    VALUE 0.                  
           05  WS-F800-READ               PIC 9(5)    VALUE 0.                  
           05  WS-F800A-WRITE             PIC 9(5)    VALUE 0.                  
           05  WS-REC-MATCH               PIC 9(5)    VALUE 0.                  
           05  WS-MATCH-IND               PIC X       VALUE 'N'.                
                                                                                
       01  WS-DTE-BALLOT.                                                       
           05  WS-BAL-YYMM.                                                     
               10 WS-BAL-YY               PIC 9(4)    VALUE ZEROES.             
               10 WS-BAL-MM               PIC 9(2)    VALUE ZEROES.             
                                                                                
                                                                                
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
      ***************************************************************           
       0000-MAIN-ROUTINE.                                                       
      ***************************************************************           
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 1500-READ-BP13F713      THRU 1500-EXIT.                      
           PERFORM 2000-READ-BP13F520      THRU 2000-EXIT.                      
           PERFORM 2500-READ-BP13F800      THRU 2500-EXIT.                      
           PERFORM 3000-PROCESS-F520       THRU 3000-EXIT                       
             UNTIL F800-NUM-REGN = HIGH-VALUES OR                               
                   F520-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       1000-OPEN-FILES.                                                         
      ***************************************************************           
                                                                                
           OPEN INPUT  BP13F520                                                 
                       BP13F713                                                 
                       BP13F800                                                 
                OUTPUT P13F800A.                                                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       1500-READ-BP13F713.                                                      
      ***************************************************************           
                                                                                
           READ BP13F713                                                        
                AT END MOVE 'Y' TO WS-F713-EOF                                  
                GO TO 1500-EXIT.                                                
                                                                                
           ADD 1                         TO WS-F713-READ.                       
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************************************************           
       2000-READ-BP13F520.                                                      
      ***************************************************************           
                                                                                
           READ BP13F520                                                        
                AT END MOVE HIGH-VALUES TO F520-NUM-REGN                        
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                         TO WS-F520-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       2500-READ-BP13F800.                                                      
      ***************************************************************           
                                                                                
           READ BP13F800                                                        
                AT END MOVE HIGH-VALUES TO F800-NUM-REGN                        
                GO TO 2500-EXIT.                                                
                                                                                
           ADD 1                         TO WS-F800-READ.                       
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       3000-PROCESS-F520.                                                       
      ***************************************************************           
                                                                                
           IF F520-TME-QUEUE = SPACES OR ZEROS                                  
              MOVE  60   TO F520-TME-QUEUE                                      
           END-IF.                                                              
                                                                                
           IF F520-NUM-REGN = F800-NUM-REGN                                     
              PERFORM 4000-WRITE-P13F800A       THRU 4000-EXIT                  
              PERFORM 2000-READ-BP13F520        THRU 2000-EXIT                  
              PERFORM 2500-READ-BP13F800        THRU 2500-EXIT                  
           ELSE                                                                 
              IF F520-NUM-REGN < F800-NUM-REGN                                  
                 PERFORM 2000-READ-BP13F520     THRU 2000-EXIT                  
              ELSE                                                              
                 IF F520-NUM-REGN > F800-NUM-REGN                               
                    PERFORM 2500-READ-BP13F800 THRU 2500-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       4000-WRITE-P13F800A.                                                     
      ***************************************************************           
                                                                                
             EVALUATE F713-CDE-COHORT                                           
                WHEN 'PA'                                                       
                      MOVE F520-TME-PIGEONHOLE TO F800-NUM-WAIT-TIME            
                WHEN 'QT'                                                       
                      MOVE F520-TME-QUEUE      TO F800-NUM-WAIT-TIME            
                WHEN OTHER                                                      
                     DISPLAY 'INVALID CDE-COHORT'                               
             END-EVALUATE.                                                      
             IF F520-CDE-SUCC-NT-ZONE = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                MOVE SPACES              TO WS-DTE-BALLOT                       
             ELSE                                                               
                MOVE F520-DTE-BALLOT     TO WS-DTE-BALLOT                       
             END-IF.                                                            
             MOVE WS-DTE-BALLOT       TO F800-DTE-BALLOT.                       
             WRITE P13F800A-MASTER FROM BP13F800-MASTER.                        
             ADD 1                      TO WS-F800A-WRITE.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F520                                                       
                 BP13F713                                                       
                 BP13F800                                                       
                 P13F800A.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '  NO. OF F520 RECORDS READ = ' WS-F520-READ.                
           DISPLAY '  NO. OF F713 RECORDS READ = ' WS-F713-READ.                
           DISPLAY '  NO. OF F800 RECORDS READ = ' WS-F800-READ.                
           DISPLAY '  NO. OF RECORDS WRITTEN   = ' WS-F800A-WRITE.              
           DISPLAY SPACES.                                                      
           DISPLAY '============================='.                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
