       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB3F.                                                 
      *AUTHOR.        SANDEEP REDDY.                                            
      *DATE-WRITTEN.  03/06/2019.                                               
      * ========================================================== *            
      *             SYSTEM OF COMMITMENT  (BP13)                   *            
      * ========================================================== *            
      *                                                            *            
      *  DESCRIPTION :  TO MATCH AB02F940 AND AB02F945             *            
      *  INPUT FILE  :  AB02F945                                   *            
      *                 AB02F940                                   *            
      *  OUTPUT FILES:  P13F945A --- MATCHED FROM F945             *            
      *                 P13F945B --- UNMATHD FROM F945             *            
      *                 P13F940A --- MATCHED FROM F940             *            
      *                 P13F940B --- UNMATHD FROM F940             *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY     ON          DESCRIPTION                    *            
      * -------- ---    ----------  -----------                    *            
      * BP137965 SRG1   03/06/2019  NEW PROGRAM                    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB02F945  ASSIGN          TO AB02F945.                        
           SELECT AB02F940  ASSIGN          TO AB02F940.                        
                                                                                
           SELECT P13F945A  ASSIGN          TO P13F945A.                        
           SELECT P13F945B  ASSIGN          TO P13F945B.                        
           SELECT P13F940A  ASSIGN          TO P13F940A.                        
           SELECT P13F940B  ASSIGN          TO P13F940B.                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *---------------------------------------------------------------*         
       FD  AB02F945                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AB02F945.                                                           
                                                                                
       FD  AB02F940                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AB02F940.                                                           
                                                                                
       FD  P13F945A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F945A-REC                     PIC X(300).                         
                                                                                
       FD  P13F945B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F945B-REC                     PIC X(300).                         
                                                                                
       FD  P13F940A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F940A-REC                     PIC X(200).                         
                                                                                
       FD  P13F940B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F940B-REC                     PIC X(200).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-F945-EOF                  PIC X(01) VALUE SPACES.             
               88 F945-END-OF-FILE                    VALUE 'Y'.                
           05  WS-F940-EOF                  PIC X(01) VALUE SPACES.             
               88 F940-END-OF-FILE                    VALUE 'Y'.                
           05  WS-F945-READ-CNT             PIC 9(08) VALUE ZEROES.             
           05  WS-F940-READ-CNT             PIC 9(08) VALUE ZEROES.             
           05  WS-WRT-MATCH                 PIC 9(08) VALUE ZEROES.             
           05  WS-WRT-N-FND-F945            PIC 9(08) VALUE ZEROES.             
           05  WS-WRT-N-FND-F940            PIC 9(08) VALUE ZEROES.             
       01  WS-DATE-TIME                     PIC X(15) VALUE SPACES.             
       01  WS-CURR-DATE                     PIC X(10) VALUE SPACES.             
       01  WS-CURR-TIME                     PIC X(08) VALUE SPACES.             
       01  WS-EDT-CNT                       PIC Z(7)9.                          
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-AB02F945       THRU 2000-EXIT.                     
           PERFORM 2100-READ-AB02F940       THRU 2100-EXIT.                     
           PERFORM 3000-PROCESS-F945        THRU 3000-EXIT                      
             UNTIL F945-END-OF-FILE OR F940-END-OF-FILE.                        
           PERFORM 4000-CHECK-EOF           THRU 4000-EXIT.                     
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  AB02F945                                                 
                       AB02F940                                                 
              OUTPUT   P13F945A                                                 
                       P13F945B                                                 
                       P13F940A                                                 
                       P13F940B.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-AB02F945.                                                      
      *---------------------------------------------------------------*         
           READ AB02F945                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F945-EOF                      
             NOT AT END                                                         
                ADD  1                      TO WS-F945-READ-CNT                 
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2100-READ-AB02F940.                                                      
      *---------------------------------------------------------------*         
           READ AB02F940                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F940-EOF                      
             NOT AT END                                                         
                ADD  1                      TO WS-F940-READ-CNT                 
           END-READ.                                                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-F945.                                                       
      *---------------------------------------------------------------*         
           IF AB02F940-DETAIL-REC(1:47) = AB02F945-DETAIL-REC(1:47)             
              WRITE P13F945A-REC            FROM AB02F945-DETAIL-REC            
              WRITE P13F940A-REC            FROM AB02F940-DETAIL-REC            
              ADD 1                         TO WS-WRT-MATCH                     
              PERFORM 2000-READ-AB02F945    THRU 2000-EXIT                      
              PERFORM 2100-READ-AB02F940    THRU 2100-EXIT                      
           ELSE                                                                 
               IF AB02F940-DETAIL-REC(1:47) > AB02F945-DETAIL-REC(1:47)         
                  WRITE P13F945B-REC        FROM AB02F945-DETAIL-REC            
                  ADD 1                     TO WS-WRT-N-FND-F940                
                  PERFORM 2000-READ-AB02F945 THRU 2000-EXIT                     
               ELSE                                                             
                  WRITE P13F940B-REC        FROM AB02F940-DETAIL-REC            
                  ADD 1                     TO WS-WRT-N-FND-F945                
                  PERFORM 2100-READ-AB02F940 THRU 2100-EXIT                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-CHECK-EOF.                                                          
      *---------------------------------------------------------------*         
           IF F945-END-OF-FILE AND F940-END-OF-FILE                             
              CONTINUE                                                          
           ELSE                                                                 
             IF F945-END-OF-FILE                                                
                PERFORM UNTIL F940-END-OF-FILE                                  
                  WRITE P13F940B-REC        FROM AB02F940-DETAIL-REC            
                  ADD 1                     TO WS-WRT-N-FND-F945                
                  PERFORM 2100-READ-AB02F940 THRU 2100-EXIT                     
                END-PERFORM                                                     
             END-IF                                                             
             IF F940-END-OF-FILE                                                
                PERFORM UNTIL F945-END-OF-FILE                                  
                  WRITE P13F945B-REC        FROM AB02F945-DETAIL-REC            
                  ADD 1                     TO WS-WRT-N-FND-F940                
                  PERFORM 2000-READ-AB02F945 THRU 2000-EXIT                     
                END-PERFORM                                                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY ' '.                                                         
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY '   PROGRAM : BP13CB3F ' .                                   
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY ' '.                                                         
           DISPLAY ' REC READ FROM AB02F945   - ' WS-F945-READ-CNT.             
           DISPLAY ' REC READ FROM AB02F940   - ' WS-F940-READ-CNT.             
           DISPLAY ' MATCHED CASES            - ' WS-WRT-MATCH.                 
           DISPLAY ' UNMATCHED WITH F940      - ' WS-WRT-N-FND-F940.            
           DISPLAY ' UNMATCHED WITH F945      - ' WS-WRT-N-FND-F945.            
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
                                                                                
           CLOSE AB02F945                                                       
                 AB02F940                                                       
                 P13F945A                                                       
                 P13F945B                                                       
                 P13F940A                                                       
                 P13F940B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
