       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C453.                                                 
       AUTHOR.        MTDULLONA.                                                
      *DATE-WRITTEN.  JAN2004.                                                  
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE    :  TO EXTRACT FLATS WITH KEYS RETURNED TO SOU  *           
      *                                                             *           
      * 1. FLATS SHOULD HAVE A RETURNED DATE = CURRENT DATE         *           
      * 2. FLATS SHOULD BE ACCEPTED                                 *           
      *-------------------------------------------------------------*           
      * INPUT FILES:  BP13K432 (DAILY LOGFILE OF CASES WITH KEYS    *           
      *                         RETURNED TO SOU)                    *           
      *               BP13K800                                      *           
      *               BP13K820                                      *           
      *               BP27K800                                      *           
      *               BM06K110                                      *           
      * OUTPUT FILE:  BP13F432                                      *           
      *-------------------------------------------------------------*           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      *--------- ---  ------  --------------------------------------*           
      * BP132449 MTD  300104  EXTRACT RECORDS WITH KEYS RETURNED    *           
      * BP132647 GAN1 201204  ADD COLUMN FOR FLAT STATUS            *           
      * BP132658 CG7  070205  ADD READING OF BP13K435               *           
      * BP133084 RB12 110607  ADD READING OF BP27K800 FOR HLE EXPIRY*           
      * BP136799 KVL1 170517  REMOVED CHECKING OF BM06K110 REGN NUM *           
      * BP136799 KVL1 170522  TO TAG VACANT FLATS                   *           
      * BP138047 KAC1 251119  RECOMPILE FOR BP27 COPYBOOK UPDATE    *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K432  ASSIGN       TO BP13K432                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K432-KEY-FLD                        
                            FILE STATUS  IS WS-K432-STATUS.                     
                                                                                
           SELECT BP13K435  ASSIGN       TO BP13K435                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K435-KEY-FLD                        
                            FILE STATUS  IS WS-K435-STATUS.                     
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT BP27K800  ASSIGN       TO BP27K800                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K800-NUM-HLA                        
                                         OF BP27K800-MASTER                     
                            FILE STATUS  IS WS-BP27K800-STATUS.                 
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT SY02F001  ASSIGN TO SY02F001.                                 
                                                                                
           SELECT BP13F432  ASSIGN TO BP13F432.                                 
                                                                                
       DATA DIVISION.                                                           
      *--------------------------------------------------------------           
       FILE SECTION.                                                            
      *--------------------------------------------------------------           
                                                                                
       FD  BP13K432                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K432.                                                           
                                                                                
       FD  BP13K435                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13K435.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP27K800                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP27K800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13F432                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  BP13F432-RECORD.                                                     
           05  F432-NUM-REGN                 PIC X(08).                         
           05  F432-NUM-NRIC                 PIC X(09) OCCURS 4.                
           05  F432-NME-OCCP                 PIC X(66) OCCURS 4.                
           05  F432-NUM-BLK                  PIC X(05).                         
           05  F432-NUM-LEVEL                PIC X(02).                         
           05  F432-NUM-UNIT                 PIC X(04).                         
           05  F432-NME-STREET               PIC X(32).                         
           05  F432-NUM-CA-TAG               PIC X(01).                         
           05  F432-NUM-LOAN-TAG             PIC X(01).                         
           05  F432-DTE-ACCEPTANCE           PIC X(08).                         
           05  F432-DTE-HLE-EXPIRY           PIC X(08).                         
           05  F432-NUM-FLAT-STATUS          PIC X(02).                         
           05  FILLER                        PIC X(129).                        
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-WORK-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE         PIC X(08) VALUE SPACES.                  
               10  FILLER              PIC X(13) VALUE SPACES.                  
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-MM          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-CCYY        PIC X(04) VALUE SPACES.                  
           05  WS-SUB                  PIC 9(02) VALUE ZEROES.                  
           05  WS-RETCODE              PIC -ZZZZ.                               
       01 WS-PREV-NUM-HDB-REF          PIC X(11)  VALUE SPACES.                 
       01 WS-VACANT-FLAT               PIC X(01)  VALUE 'N'.                    
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K432-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K435-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-BP27K800-STATUS      PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-K432-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-K432       VALUE 'Y'.                               
           05  WS-K435-EOF             PIC X(01) VALUE 'N'.                     
           05  WS-K435-SW              PIC X(01) VALUE 'N'.                     
           05  WS-K435-FOUND           PIC X(01) VALUE 'Y'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K432-READ-CTR       PIC 9(06) VALUE ZEROES.                   
           05  WS-WRT-CTR             PIC 9(06) VALUE ZEROES.                   
                                                                                
      *-------------------------------------------------------------            
      * BP13C913 PARAMETERS                                                     
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *--------------------------------------------------------------           
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------           
                                                                                
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 1100-READ-SY02F001     THRU 1100-EXIT.                       
           PERFORM 2000-START-BP13K432    THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
             UNTIL C-END-OF-K432.                                               
           PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BP13K432                                               
                         BP13K435                                               
                         BP13K800                                               
                         BP13K820                                               
                         BP27K800                                               
                         BM06K110                                               
                         SY02F001                                               
                OUTPUT   BP13F432.                                              
                                                                                
           IF WS-K432-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K432-STATUS ' WS-K432-STATUS              
              MOVE WS-K432-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K435-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K435-STATUS ' WS-K435-STATUS              
              MOVE WS-K435-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-BP27K800-STATUS NOT = 00 AND 97                                
              DISPLAY 'OPEN ERROR-BP27K800-STATUS ' WS-BP27K800-STATUS          
              MOVE WS-BP27K800-STATUS    TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BM06K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1100-READ-SY02F001.                                                      
      *--------------------------------*                                        
                                                                                
           READ SY02F001 AT END                                                 
                DISPLAY 'EMPTY FILE - SY02F001'                                 
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                      
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-START-BP13K432.                                                     
      *--------------------------------*                                        
                                                                                
           MOVE LOW-VALUES        TO K432-KEY-FLD.                              
           START BP13K432 KEY NOT LESS THAN K432-KEY-FLD.                       
           IF WS-K432-STATUS = ZEROES                                           
              NEXT SENTENCE                                                     
           ELSE                                                                 
              DISPLAY 'START READ ERROR BP13K432,STATUS ' WS-K432-STATUS        
              MOVE WS-K432-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BP13K432 NEXT AT END                                            
                MOVE 'Y'     TO WS-END-OF-K432-SW                               
                GO TO 3000-EXIT.                                                
                                                                                
           IF WS-K432-STATUS = 00 OR 22                                         
              ADD 1       TO WS-K432-READ-CTR                                   
              PERFORM 3100-CHECK-RECORD        THRU 3100-EXIT                   
           ELSE                                                                 
              DISPLAY 'READNEXT ERROR BP13K432, STATUS:' WS-K432-STATUS         
              MOVE WS-K432-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3100-CHECK-RECORD.                                                       
      *--------------------------------*                                        
                                                                                
      **-- CHECK IF RETURN DATE EQUAL CURRUNT DATE IN SY02F001                  
           IF K432-DTE-KEY-RETURN NOT = F001-DTE-CURRENT                        
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
      **-- READ BM06K110 TO CHECK IF FLAT ACCEPTED                              
           MOVE 'N'                      TO WS-VACANT-FLAT.                     
           MOVE SPACES                   TO K110-KEY-FLD.                       
           MOVE K432-NUM-HDB-REF         TO K110-SCH-ACC-NO.                    
           PERFORM 3300-READ-BM06K110       THRU 3300-EXIT.                     
           IF (K110-REGN-NO = SPACES OR LOW-VALUES) OR                          
              (K110-REGN-NO (1:1) = '#' OR SPACES OR LOW-VALUES)                
      *       GO TO 3100-EXIT                                                   
              MOVE SPACES                TO K110-REGN-NO                        
              MOVE 'Y'                   TO WS-VACANT-FLAT                      
           END-IF.                                                              
                                                                                
           PERFORM 3110-STARTBR-BP13K435 THRU 3110-EXIT.                        
                                                                                
           IF WS-K435-FOUND = 'Y'                                               
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 3200-GET-DETAILS         THRU 3200-EXIT.                     
           WRITE BP13F432-RECORD.                                               
           ADD   1 TO WS-WRT-CTR.                                               
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       3110-STARTBR-BP13K435.                                                   
      *--------------------------------*                                        
           MOVE 'N'             TO  WS-K435-EOF                                 
                                    WS-K435-SW                                  
                                    WS-K435-FOUND.                              
           MOVE SPACES          TO  K435-KEY-FLD.                               
           MOVE K110-KEY-FLD    TO  K435-NUM-HDB-REF                            
                                    WS-PREV-NUM-HDB-REF.                        
                                                                                
           START BP13K435 KEY >= K435-KEY-FLD.                                  
           EVALUATE WS-K435-STATUS                                              
           WHEN 00                                                              
                PERFORM 3120-READNEXT-BP13K435 THRU 3120-EXIT                   
                   UNTIL K435-NUM-HDB-REF NOT = WS-PREV-NUM-HDB-REF             
                      OR WS-K435-EOF = 'Y'                                      
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'N'        TO  WS-K435-FOUND                               
           WHEN OTHER                                                           
                DISPLAY 'ERROR START BP13K435, STATUS  '                        
                        WS-K435-STATUS                                          
                        ',  HDB REF ' K435-NUM-HDB-REF                          
                MOVE WS-K435-STATUS TO RETURN-CODE                              
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT .                                                               
                                                                                
                                                                                
      *--------------------------------*                                        
       3120-READNEXT-BP13K435.                                                  
      *--------------------------------*                                        
                                                                                
           READ BP13K435 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-K435-EOF.                                 
                                                                                
           EVALUATE WS-K435-STATUS                                              
           WHEN 00                                                              
                IF K435-NUM-HDB-REF = K110-KEY-FLD                              
                   MOVE 'Y' TO WS-K435-FOUND                                    
                   MOVE 'Y' TO WS-K435-SW                                       
                ELSE                                                            
                   IF WS-K435-SW = 'N'                                          
                      MOVE 'N' TO WS-K435-FOUND                                 
                   END-IF                                                       
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y' TO WS-K435-EOF                                         
                MOVE 'N' TO WS-K435-FOUND                                       
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K435, STATUS  '                      
                        WS-K435-STATUS                                          
                        ',  HDB REF ' K435-NUM-HDB-REF                          
                MOVE WS-K435-STATUS TO RETURN-CODE                              
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3120-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------*                                        
       3200-GET-DETAILS.                                                        
      *--------------------------------*                                        
                                                                                
           MOVE SPACES                   TO BP13F432-RECORD.                    
           INITIALIZE                       BP13F432-RECORD.                    
                                                                                
           MOVE K110-REGN-NO             TO F432-NUM-REGN.                      
           MOVE K110-DTE-ACCEPTANCE      TO F432-DTE-ACCEPTANCE.                
           MOVE K110-FLAT-STATUS         TO F432-NUM-FLAT-STATUS.               
                                                                                
           MOVE K432-NUM-BLK             TO F432-NUM-BLK.                       
           MOVE K432-NUM-UNIT-MAIN       TO F432-NUM-UNIT.                      
           MOVE K432-NUM-LEVEL           TO F432-NUM-LEVEL.                     
                                                                                
      **-- GET STREET NAME FROM PIDB TABLE                                      
           MOVE SPACES                   TO BP13COMM8-REC.                      
           INITIALIZE                       BP13COMM8-REC.                      
           MOVE K110-SCH-ACC-NO          TO WS-LINK-REC.                        
           PERFORM 3600-READ-PIDB      THRU 3600-EXIT.                          
           MOVE COMM8-NME-STREET         TO F432-NME-STREET.                    
                                                                                
           IF WS-VACANT-FLAT = 'Y'                                              
              GO TO 3200-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES                   TO K800-NUM-REGN                       
           MOVE K110-REGN-NO             TO K800-NUM-REGN                       
           PERFORM 3400-READ-BP13K800  THRU 3400-EXIT                           
                                                                                
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
                                         TO K800-NUM-HLA OF                     
                                            BP27K800-MASTER                     
           PERFORM 3700-READ-BP27K800  THRU 3700-EXIT                           
                                                                                
           MOVE K800-DTE-HLA-EXPIRY OF BP27K800-MASTER                          
                                         TO F432-DTE-HLE-EXPIRY                 
                                                                                
           MOVE K800-NUM-CA-TAG          TO F432-NUM-CA-TAG.                    
           MOVE K800-NUM-LOAN-TAG OF BP13K800-MASTER                            
                                         TO F432-NUM-LOAN-TAG.                  
                                                                                
           IF K800-NUM-NRIC1 OF BP13K800-MASTER                                 
                             NOT = SPACES AND LOW-VALUES                        
              MOVE SPACES                TO K820-KEY-FLD                        
              MOVE K800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE K800-NUM-NRIC1 OF BP13K800-MASTER                            
                                         TO K820-NUM-NRIC                       
              PERFORM 3500-READ-BP13K820    THRU 3500-EXIT                      
              MOVE K800-NUM-NRIC1 OF BP13K800-MASTER                            
                                         TO F432-NUM-NRIC (1)                   
              MOVE K820-NME-OCCP         TO F432-NME-OCCP (1)                   
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 OF BP13K800-MASTER                                 
                             NOT = SPACES AND LOW-VALUES                        
              MOVE SPACES                TO K820-KEY-FLD                        
              MOVE K800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE K800-NUM-NRIC2 OF BP13K800-MASTER                            
                                         TO K820-NUM-NRIC                       
              PERFORM 3500-READ-BP13K820    THRU 3500-EXIT                      
              MOVE K800-NUM-NRIC2 OF BP13K800-MASTER                            
                                         TO F432-NUM-NRIC (2)                   
              MOVE K820-NME-OCCP         TO F432-NME-OCCP (2)                   
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 OF BP13K800-MASTER                                 
                             NOT = SPACES AND LOW-VALUES                        
              MOVE SPACES                TO K820-KEY-FLD                        
              MOVE K800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE K800-NUM-NRIC3 OF BP13K800-MASTER                            
                                         TO K820-NUM-NRIC                       
              PERFORM 3500-READ-BP13K820    THRU 3500-EXIT                      
              MOVE K800-NUM-NRIC3 OF BP13K800-MASTER                            
                                         TO F432-NUM-NRIC (3)                   
              MOVE K820-NME-OCCP         TO F432-NME-OCCP (3)                   
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 OF BP13K800-MASTER                                 
                             NOT = SPACES AND LOW-VALUES                        
              MOVE SPACES                TO K820-KEY-FLD                        
              MOVE K800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE K800-NUM-NRIC4 OF BP13K800-MASTER                            
                                         TO K820-NUM-NRIC                       
              PERFORM 3500-READ-BP13K820    THRU 3500-EXIT                      
              MOVE K800-NUM-NRIC4 OF BP13K800-MASTER                            
                                         TO F432-NUM-NRIC (4)                   
              MOVE K820-NME-OCCP         TO F432-NME-OCCP (4)                   
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3300-READ-BM06K110.                                                      
      *--------------------------------*                                        
                                                                                
           READ BM06K110.                                                       
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BM06K110, KEY: '                
                            K110-KEY-FLD                                        
                   MOVE SPACES     TO K110-REC                                  
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BM06K110,STATUS ' WS-K110-STATUS         
                   MOVE WS-K110-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3400-READ-BP13K800.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K800, KEY: '                
                            K800-NUM-REGN                                       
                   MOVE SPACES     TO BP13K800-MASTER                           
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K800,STATUS ' WS-K800-STATUS         
                   MOVE WS-K800-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3500-READ-BP13K820.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP13K820.                                                       
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY: '                
                            K820-KEY-FLD                                        
                   MOVE SPACES     TO BP13K820-REC                              
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K820,STATUS ' WS-K820-STATUS         
                   MOVE WS-K820-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3600-READ-PIDB.                                                          
      *--------------------------------*                                        
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
              WHEN +000                                                         
                 CONTINUE                                                       
              WHEN +100                                                         
                 MOVE SPACES   TO BP13COMM8-REC                                 
                 INITIALIZE       BP13COMM8-REC                                 
                 DISPLAY 'REC NOT FOUND IN PIDB: ' K110-SCH-ACC-NO              
              WHEN OTHER                                                        
                 MOVE COMM8-CDE-SYSERR      TO WS-RETCODE                       
                 DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                 
                         WS-RETCODE                                             
                 MOVE 99                    TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3700-READ-BP27K800.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP27K800.                                                       
           EVALUATE WS-BP27K800-STATUS                                          
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP27K800, KEY: '                
                            K800-NUM-HLA OF BP27K800-MASTER                     
                   MOVE SPACES     TO BP27K800-MASTER                           
              WHEN OTHER                                                        
                DISPLAY 'READ ERR BP27K800,STATUS ' WS-BP27K800-STATUS          
                MOVE WS-BP27K800-STATUS    TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C453'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BP13K432 RECORDS READ       : ' WS-K432-READ-CTR.           
           DISPLAY 'BP13F432 RECORDS WRITTEN    : ' WS-WRT-CTR.                 
                                                                                
           CLOSE BP13K432                                                       
                 BP13K435                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP27K800                                                       
                 BM06K110                                                       
                 SY02F001                                                       
                 BP13F432.                                                      
                                                                                
            IF WS-K432-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K432-STATUS ' WS-K432-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K435-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K435-STATUS ' WS-K435-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K800-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K800-STATUS ' WS-K800-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-BP27K800-STATUS NOT = 00                                      
              DISPLAY 'CLOSE ERROR-BP27K800-STATUS ' WS-BP27K800-STATUS         
            END-IF.                                                             
                                                                                
            IF WS-K820-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K820-STATUS ' WS-K820-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K110-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BM06K110-STATUS ' WS-K110-STATUS            
            END-IF.                                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
