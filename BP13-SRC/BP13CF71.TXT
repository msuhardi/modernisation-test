       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF71.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  10/12/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : UPDATE K800-NUM-SALE-BROCHURE-SCPY USING    *            
      *                K757-NUM-SALE-BROCHURE-SCPY                 *            
      *                                                            *            
      *    READ BP13F740                                           *            
      *    MATCH BP13F740 WITH BP13K757                            *            
      *    IF MATCHED                                              *            
      *       READ BP13K800                                        *            
      *       IF FOUND                                             *            
      *          REWRITE K800-NUM-SALE-BROCHURE-SCPY               *            
      *            USING K757-NUM-SALE-BROCHURE-SCPY               *            
      *       ELSE                                                 *            
      *          DISPLAY RECORD NOT FOUND                          *            
      *       END-IF                                               *            
      *    ELSE                                                    *            
      *       DISPLAY RECORD NOT FOUND                             *            
      *    END-IF                                                  *            
      *                                                            *            
      * INPUT FILE   :  BP13F740  -  SEQUENTIAL DATASET            *            
      *                 BP13K757  -  BP13.K757.BTO                 *            
      * I-O FILE     :  BP13K800  -  BP13.K800.APPLN               *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134718  SMR2  10122012  NEW PROGRAM                      *            
      * BP134782  SMR2  15022013  UPDATE K800-NUM-PPS-QUOTA        *            
      * BP134782  SMR2  25022013  EVALUATE PARAMETER FROM JCL      *            
      * BP139744  AM25  21112023  CATER FOR NEW PARM CODE "HFE"    *            
      * BP139280  EAA2  22112023  EXPAND BP13K757 FROM 3000 TO 4000*            
      * BP139852  AM25  03012024  ADD NUM-HANDHOLD FOR HFE         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740  ASSIGN       TO BP13F740.                           
                                                                                
           SELECT BP13K757  ASSIGN       TO BP13K757                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K757-KEY-FLD                        
                            FILE STATUS  IS WS-K757-STATUS.                     
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K740  ASSIGN        TO BP13K740                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K740-KEY-FLD                       
                            ALTERNATE KEY IS K740-NUM-REGN                      
                            FILE STATUS   IS WS-K740-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F740                                                             
           RECORDING MODE    IS F                                               
           RECORD CONTAINS  500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K757                                                             
           RECORD CONTAINS 4000 CHARACTERS.                                     
       COPY BP13K757.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K740                                                             
           RECORD CONTAINS  500 CHARACTERS.                                     
       COPY BP13K740.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K757-STATUS           PIC 9(02)  VALUE ZEROES.                
           05  WS-K800-STATUS           PIC 9(02)  VALUE ZEROES.                
           05  WS-K740-STATUS           PIC 9(02)  VALUE ZEROES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ             PIC 9(06)  VALUE ZEROES.                
           05  WS-K757-MATCH            PIC 9(06)  VALUE ZEROES.                
           05  WS-K757-UNMATCH          PIC 9(06)  VALUE ZEROES.                
           05  WS-K740-MATCH            PIC 9(06)  VALUE ZEROES.                
           05  WS-K740-UNMATCH          PIC 9(06)  VALUE ZEROES.                
           05  WS-K800-MATCH            PIC 9(06)  VALUE ZEROES.                
           05  WS-K800-UNMATCH          PIC 9(06)  VALUE ZEROES.                
           05  WS-K800-REWRITE          PIC 9(06)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF              PIC X(01)  VALUE 'N'.                   
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                  PIC X(08)  VALUE SPACES.                
           05  WS-DATE-EDIT             PIC X(10)  VALUE SPACES.                
                                                                                
       01  WS-PARM-NUM-CDE              PIC X(3).                               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT                      
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13F740                                                  
                      BP13K757                                                  
                      BP13K740                                                  
                I-O   BP13K800.                                                 
                                                                                
           IF WS-K757-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K757 ERROR ' WS-K757-STATUS                  
              MOVE WS-K757-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K740 ERROR ' WS-K740-STATUS                  
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                         
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
                                                                                
      *** ACCEPT VALUE FROM JCL ***                                             
           ACCEPT WS-PARM-NUM-CDE.                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F740-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F740-READ.                    
                                                                                
           EVALUATE WS-PARM-NUM-CDE                                             
           WHEN 'BRO'                                                           
           WHEN 'HFE'                                                           
              PERFORM 3000-READ-BP13K757    THRU 3000-EXIT                      
           WHEN 'PPS'                                                           
              PERFORM 6000-READ-BP13K740    THRU 6000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K757.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                      TO BP13K757-REC.                    
           INITIALIZE                          BP13K757-REC.                    
                                                                                
           MOVE F740-NUM-REGN               TO K757-NUM-REGN.                   
                                                                                
           READ BP13K757.                                                       
                                                                                
           EVALUATE WS-K757-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K757-MATCH                    
              PERFORM 4000-READ-BP13K800    THRU 4000-EXIT                      
                                                                                
           WHEN 23                                                              
              ADD 1                         TO WS-K757-UNMATCH                  
              DISPLAY 'NUM-REGN NOT FOUND IN K757 : ' F740-NUM-REGN             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K757   : ' WS-K757-STATUS              
              MOVE WS-K757-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                      TO BP13K800-MASTER.                 
           INITIALIZE                          BP13K800-MASTER.                 
                                                                                
           MOVE F740-NUM-REGN               TO K800-NUM-REGN.                   
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K800-MATCH                    
              PERFORM 5000-REWRITE-BP13K800 THRU 5000-EXIT                      
                                                                                
           WHEN 23                                                              
              ADD 1                         TO WS-K800-UNMATCH                  
              DISPLAY 'NUM-REGN NOT FOUND IN K800 : ' K757-NUM-REGN             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K800   : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-REWRITE-BP13K800.                                                   
      *-------------------------------------------------------------            
           EVALUATE WS-PARM-NUM-CDE                                             
           WHEN 'BRO'                                                           
              MOVE K757-NUM-SALE-BROCHURE-SCPY                                  
                                        TO K800-NUM-SALE-BROCHURE-SCPY          
           WHEN 'HFE'                                                           
              MOVE K757-NUM-HLA         TO K800-NUM-HLA                         
              MOVE K757-NUM-HANDHOLD    TO K800-NUM-HANDHOLD                    
           WHEN 'PPS'                                                           
              MOVE K740-CDE-REQUEST-STATUS TO K800-NUM-PPS-QUOTA                
           END-EVALUATE.                                                        
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K800-REWRITE                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING TO BP13K800. STATUS IS - '                 
                      WS-K800-STATUS                                            
              MOVE WS-K800-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6000-READ-BP13K740.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                      TO BP13K740-REC.                    
           INITIALIZE                          BP13K740-REC.                    
                                                                                
           MOVE F740-NUM-REGN               TO K740-NUM-REGN.                   
                                                                                
           READ BP13K740 KEY IS K740-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              ADD 1                         TO WS-K740-MATCH                    
              PERFORM 4000-READ-BP13K800    THRU 4000-EXIT                      
                                                                                
           WHEN 23                                                              
              ADD 1                         TO WS-K740-UNMATCH                  
              DISPLAY 'NUM-REGN NOT FOUND IN K740 : ' K800-NUM-REGN             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K740   : ' WS-K740-STATUS              
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '    BP13CF71 CONTROL TOTAL   : ' WS-DATE-EDIT.              
           DISPLAY '  '.                                                        
           DISPLAY ' 1. BP13F740 READ            : ' WS-F740-READ.              
                                                                                
           EVALUATE WS-PARM-NUM-CDE                                             
           WHEN 'BRO'                                                           
           WHEN 'HFE'                                                           
              DISPLAY ' 2. BP13K757 MATCHED RECS    : ' WS-K757-MATCH           
              DISPLAY ' 3. BP13K800 MATCHED RECS    : ' WS-K800-MATCH           
              DISPLAY ' 4. BP13K800 UNMATCHED RECS  : ' WS-K800-UNMATCH         
              DISPLAY ' 5. BP13K757 UNMATCHED RECS  : ' WS-K757-UNMATCH         
           WHEN 'PPS'                                                           
              DISPLAY ' 2. BP13K740 MATCHED RECS    : ' WS-K740-MATCH           
              DISPLAY ' 3. BP13K800 MATCHED RECS    : ' WS-K800-MATCH           
              DISPLAY ' 4. BP13K800 UNMATCHED RECS  : ' WS-K800-UNMATCH         
              DISPLAY ' 5. BP13K740 UNMATCHED RECS  : ' WS-K740-UNMATCH         
           END-EVALUATE.                                                        
                                                                                
           DISPLAY ' 6. BP13K800 REWRITTEN RECS  : ' WS-K800-REWRITE.           
           DISPLAY '  '.                                                        
           DISPLAY ' CHECK '.                                                   
           DISPLAY '       1.  =   2.  +  5. '.                                 
           DISPLAY '       2.  =   3.  +  4. '.                                 
           DISPLAY '       3.  =   6.        '.                                 
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F740                                                       
                 BP13K757                                                       
                 BP13K740                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-K757-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K757 ERROR ' WS-K757-STATUS                  
              MOVE WS-K757-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K740 ERROR ' WS-K740-STATUS                  
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
