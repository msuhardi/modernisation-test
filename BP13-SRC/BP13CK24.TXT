       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CK24.                                                 
      *AUTHOR.        YCH2.                                                     
      *DATE-WRITTEN.  06/01/2016.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : REPORT FOR SERS AFTER DETERMINED HOUSEHOLD  *            
      *                AND FT2T IN BP13K740                        *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP136090  YCH2  06012016  SIMILAR TO BP13CF92 W/O UPDATE   *            
      * BP137724  SMP3  01102019  CATER FOR NO RECORD/S FOUND      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13F205 ASSIGN       TO BP13F205.                            
                                                                                
           SELECT BP13K558 ASSIGN       TO BP13K558                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K558-KEY-FLD                         
                           FILE STATUS  IS WS-K558-STATUS.                      
                                                                                
           SELECT BP13LK24 ASSIGN       TO BP13LK24.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F205                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS   80 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K558                                                            
            RECORD CONTAINS  600 CHARACTERS.                                    
       COPY BP13K558.                                                           
                                                                                
       FD   BP13LK24                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   BP13LK24-REC                   PIC X(132).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K558-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(05)   VALUE ZEROES.            
           05  WS-LK24-SNO                 PIC 9(05)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K558-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K558-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-OCCP-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-LK24-HDR                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CUR-DATE                 PIC X(08)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NRIC                     PIC X(09)   VALUE SPACES.            
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 LK24-HEADER1.                                                      
             10 LK24-RPTNAME      PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(05)  VALUE SPACES.                      
             10 FILLER            PIC X(08)  VALUE 'HDBCAT 3'.                  
             10 FILLER            PIC X(39)  VALUE SPACES.                      
             10 FILLER            PIC X(20)  VALUE                              
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER            PIC X(33)  VALUE SPACES.                      
             10 FILLER            PIC X(07)  VALUE 'DATE : '.                   
             10 LK24-DATE         PIC X(10)  VALUE SPACES.                      
          05 LK24-HEADER2.                                                      
             10 FILLER            PIC X(57)  VALUE SPACES.                      
             10 FILLER            PIC X(25)  VALUE                              
                'REPORT ON FT2T SERS CASES'.                                    
          05 LK24-HEADER3.                                                      
             10 FILLER            PIC X(30)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'SMS'.                       
             10 FILLER            PIC X(21)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'RSL'.                       
             10 FILLER            PIC X(22)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'TRF'.                       
             10 FILLER            PIC X(21)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'MAM'.                       
             10 FILLER            PIC X(12)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'SLS'.                       
          05 LK24-HEADER4.                                                      
             10 FILLER            PIC X(52)  VALUE                              
                ' S/NO REG NO   NRIC       REL HDB REF     ST FT SOLD'.         
             10 FILLER            PIC X(50)  VALUE                              
                '  HDB REF     FS  APPROVAL HDB REF     ST EFF SALE'.           
             10 FILLER            PIC X(30)  VALUE                              
                ' HDB REF     ST HDB REF     ST'.                               
          05 LK24-BLANK.                                                        
             10 FILLER            PIC X(132) VALUE SPACES.                      
          05 LK24-RECORD.                                                       
             10 LK24-SNO          PIC X(05)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-REGN         PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-DETAIL       PIC X(117) VALUE SPACES.                      
          05 LK24-DET-REC.                                                      
             10 LK24-DET-NRIC     PIC X(09)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-DET-APPLN    PIC X(01)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-DET-REL      PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-SMS-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-SMS-ST       PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-SMS-DATE     PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-RSL-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-RSL-FS       PIC X(03)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-RSL-DATE     PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-TRF-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-TRF-ST       PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-TRF-DATE     PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-MAM-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-MAM-ST       PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-SLS-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LK24-SLS-ST       PIC X(02)  VALUE SPACES.                      
          05 LK24-NIL.                                                          
             10 FILLER            PIC X(11)  VALUE 'NO RECORDS.'.               
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 1100-READ-BP13F205      THRU 1100-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT                       
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F205                                                 
                       BP13K558                                                 
                OUTPUT BP13LK24.                                                
                                                                                
           IF WS-K558-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K558 FILE ERROR ' WS-K558-STATUS             
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4) DELIMITED BY SIZE                            
             INTO LK24-DATE.                                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       1100-READ-BP13F205.                                                      
      *********************                                                     
                                                                                
           READ BP13F205 AT END                                                 
                GO TO 9000-CLOSE-FILES.                                         
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F730.                                                      
      *********************                                                     
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                   TO WS-F730-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F730-READ.                     
           PERFORM 3000-PROCESS-RECS       THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       3000-PROCESS-RECS.                                                       
      ********************                                                      
                                                                                
           MOVE 'N'                        TO WS-K558-FND.                      
                                                                                
           MOVE '1'                        TO LK24-DET-APPLN.                   
           MOVE F730-NUM-NRIC1             TO WS-NRIC.                          
           PERFORM 4000-READ-BP13K558      THRU 4000-EXIT.                      
           IF WS-K558-FND = 'Y'                                                 
              ADD 1                        TO WS-LK24-SNO                       
              PERFORM 8000-WRITE-BP13LK24  THRU 8000-EXIT                       
           END-IF.                                                              
           PERFORM 4200-CHECK-OTHER-HA     THRU 4200-EXIT.                      
           PERFORM 5000-STARTBR-K558       THRU 5000-EXIT.                      
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13K558.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BP13K558-REC.                     
           INITIALIZE                         BP13K558-REC.                     
                                                                                
           MOVE F730-NUM-REGN              TO K558-NUM-REGN.                    
           MOVE WS-NRIC                    TO K558-NUM-NRIC.                    
                                                                                
           READ BP13K558.                                                       
                                                                                
           EVALUATE WS-K558-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K558-FND                       
              PERFORM 4100-MOVE-DETAILS    THRU 4100-EXIT                       
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K558-FND                       
              DISPLAY 'RECORD NOT FOUND IN BP13K558 : ' K558-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K558'                               
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       4100-MOVE-DETAILS.                                                       
      ********************                                                      
                                                                                
           MOVE K558-NUM-NRIC              TO LK24-DET-NRIC.                    
           MOVE K558-CDE-RELATIONSHIP      TO LK24-DET-REL.                     
           MOVE K558-NUM-HDB-REF-SMS       TO LK24-SMS-HDBREF.                  
           MOVE K558-CDE-SALES-TYPE-SMS    TO LK24-SMS-ST.                      
           MOVE K558-DTE-FLAT-SOLD-SMS     TO LK24-SMS-DATE.                    
           MOVE K558-NUM-HDB-REF-RSL       TO LK24-RSL-HDBREF.                  
           MOVE K558-CDE-FILE-STATUS-RSL   TO LK24-RSL-FS.                      
           MOVE K558-DTE-APPROVAL-RSL      TO LK24-RSL-DATE.                    
           MOVE K558-NUM-HDB-REF-TR        TO LK24-TRF-HDBREF.                  
           MOVE K558-NUM-SALES-TYPE-TR     TO LK24-TRF-ST.                      
           MOVE K558-DTE-EFF-SALE-TR       TO LK24-TRF-DATE.                    
           MOVE K558-NUM-HDB-REF-MAM       TO LK24-MAM-HDBREF.                  
           MOVE K558-NUM-SALES-TYPE-MAM    TO LK24-MAM-ST.                      
           MOVE K558-NUM-HDB-REF-SLS       TO LK24-SLS-HDBREF.                  
           MOVE K558-NUM-SALES-TYPE-SLS    TO LK24-SLS-ST.                      
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       4200-CHECK-OTHER-HA.                                                     
      **********************                                                    
                                                                                
           IF F730-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC2(1:1) NOT = '#'                                  
                 MOVE '2'                  TO LK24-DET-APPLN                    
                 MOVE F730-NUM-NRIC2       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K558   THRU 4000-EXIT                    
                 IF WS-K558-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LK24  THRU 8000-EXIT                 
                 END-IF                                                         
              ELSE                                                              
                 GO TO 4200-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC3(1:1) NOT = '#'                                  
                 MOVE '3'                  TO LK24-DET-APPLN                    
                 MOVE F730-NUM-NRIC3       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K558   THRU 4000-EXIT                    
                 IF WS-K558-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LK24  THRU 8000-EXIT                 
                 END-IF                                                         
              ELSE                                                              
                 GO TO 4200-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC4(1:1) NOT = '#'                                  
                 MOVE '4'                  TO LK24-DET-APPLN                    
                 MOVE F730-NUM-NRIC4       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K558   THRU 4000-EXIT                    
                 IF WS-K558-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LK24  THRU 8000-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       5000-STARTBR-K558.                                                       
      ********************                                                      
                                                                                
           MOVE SPACES                     TO BP13K558-REC.                     
           INITIALIZE                         BP13K558-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K558-EOF                       
                                              WS-OCCP-FND.                      
                                                                                
           MOVE F730-NUM-REGN              TO K558-NUM-REGN.                    
                                                                                
           START BP13K558 KEY >= K558-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K558-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 5100-READNEXT-K558   THRU 5100-EXIT                       
              PERFORM 5200-PROCESS-K558    THRU 5200-EXIT                       
                UNTIL WS-K558-EOF = 'Y'                                         
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START ERROR BP13K558 : ' WS-K558-STATUS                  
                      ' REGN : ' K558-NUM-REGN                                  
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5100-READNEXT-K558.                                                      
      *********************                                                     
                                                                                
           READ BP13K558 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K558-STATUS                                              
           WHEN ZEROES                                                          
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K558 : ' K558-KEY-FLD            
              MOVE 'Y'                     TO WS-K558-EOF                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K558'                               
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       5200-PROCESS-K558.                                                       
      ********************                                                      
                                                                                
           IF F730-NUM-REGN = K558-NUM-REGN                                     
              IF K558-NUM-NRIC NOT = F730-NUM-NRIC1 AND                         
                                     F730-NUM-NRIC2 AND                         
                                     F730-NUM-NRIC3 AND                         
                                     F730-NUM-NRIC4                             
                 IF WS-OCCP-FND = 'N'                                           
                    MOVE 'O'               TO LK24-DET-APPLN                    
                    MOVE 'Y'               TO WS-OCCP-FND                       
                 ELSE                                                           
                    MOVE SPACES            TO LK24-DET-APPLN                    
                 END-IF                                                         
                 PERFORM 4100-MOVE-DETAILS    THRU 4100-EXIT                    
                 PERFORM 8000-WRITE-BP13LK24  THRU 8000-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                     TO WS-K558-EOF                       
           END-IF.                                                              
                                                                                
           PERFORM 5100-READNEXT-K558      THRU 5100-EXIT.                      
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8000-WRITE-BP13LK24.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO BP13LK24-REC.                     
                                                                                
           IF WS-LK24-HDR = 'N'                                                 
              MOVE 'Y'                     TO WS-LK24-HDR                       
              MOVE 'BP13LK24'              TO LK24-RPTNAME                      
              WRITE BP13LK24-REC           FROM LK24-HEADER1                    
              WRITE BP13LK24-REC           FROM LK24-HEADER2                    
              WRITE BP13LK24-REC           FROM LK24-BLANK                      
              WRITE BP13LK24-REC           FROM LK24-BLANK                      
              WRITE BP13LK24-REC           FROM LK24-HEADER3                    
              WRITE BP13LK24-REC           FROM LK24-HEADER4                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LK24-RECORD.                      
           IF LK24-DET-APPLN = '1'                                              
              MOVE WS-LK24-SNO             TO LK24-SNO                          
              MOVE F730-NUM-REGN           TO LK24-REGN                         
              WRITE BP13LK24-REC           FROM LK24-BLANK                      
           ELSE                                                                 
              MOVE SPACES                  TO LK24-SNO                          
                                              LK24-REGN                         
           END-IF.                                                              
           MOVE LK24-DET-REC               TO LK24-DETAIL.                      
           WRITE BP13LK24-REC              FROM LK24-RECORD.                    
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************                                                         
       8100-WRITE-NIL.                                                          
      *****************                                                         
                                                                                
           MOVE SPACES                     TO BP13LK24-REC.                     
           MOVE 'BP13LK24'                 TO LK24-RPTNAME.                     
           WRITE BP13LK24-REC              FROM LK24-HEADER1.                   
           WRITE BP13LK24-REC              FROM LK24-HEADER2.                   
           WRITE BP13LK24-REC              FROM LK24-BLANK.                     
           WRITE BP13LK24-REC              FROM LK24-NIL.                       
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           IF WS-LK24-HDR = 'N'                                                 
              PERFORM 8100-WRITE-NIL       THRU 8100-EXIT                       
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CK24 ================*'.              
           DISPLAY '  NO OF F730 RECORDS READ     = ' WS-F730-READ.             
           DISPLAY '  NO OF LK24 RECORDS WRITTEN  = ' WS-LK24-SNO.              
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F730                                                       
                 BP13F205                                                       
                 BP13K558                                                       
                 BP13LK24.                                                      
                                                                                
           IF WS-K558-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K558 FILE ERROR ' WS-K558-STATUS             
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        9000-EXIT.                                                              
            EXIT.                                                               
                                                                                
