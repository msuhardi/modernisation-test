       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C93S.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  22 JUN 2017.                                              
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *   OBJECTIVE   : UPDATE/WRITE BP13KW3D BASED ON COMPLETE-TAG *           
      *                 TAG = 'M' UPDATE NON-MGPS FIELDS            *           
      *                 TAG = 'B' NO UPDATE                         *           
      *                 TAG = ' ' REPLACE RECORD                    *           
      *   INPUT FILES : BP13FW3D - BOOKING DATA                     *           
      *   I-O    FILE : BP13KW3D - VSAM BOOKING DATA FILE           *           
      *                                                             *           
      * =========================================================== *           
      * CHQ REQ  DATE       BY   DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136856 03/07/2017 FNP1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FW3D ASSIGN       TO BP13FW3D.                            
                                                                                
           SELECT BP13KW3D ASSIGN       TO BP13KW3D                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KW3D-KEY-FLD                         
                           FILE STATUS  IS BP13KW3D-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FW3D                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       COPY BP13FW3D.                                                           
                                                                                
       FD   BP13KW3D                                                            
            RECORD CONTAINS  300 CHARACTERS.                                    
       COPY BP13KW3D.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FW3D-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-KW3D-FOUND                PIC 9(06)   VALUE ZEROES.           
           05  WS-KW3D-UPDATED              PIC 9(06)   VALUE ZEROES.           
           05  WS-KW3D-WRITTEN              PIC 9(06)   VALUE ZEROES.           
           05  WS-KW3D-BK-COMP              PIC 9(06)   VALUE ZEROES.           
                                                                                
       01  WS-FW3D-EOF                      PIC X       VALUE SPACES.           
       01  BP13KW3D-STATUS                  PIC 9(02)   VALUE ZEROES.           
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FW3D       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
              UNTIL WS-FW3D-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FW3D                                                 
                I-O    BP13KW3D.                                                
                                                                                
           IF BP13KW3D-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR BP13KW3D. STATUS : ' BP13KW3D-STATUS          
              MOVE BP13KW3D-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13FW3D.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13FW3D AT END                                                 
                MOVE HIGH-VALUES TO BP13FW3D-REC                                
                MOVE 'Y' TO WS-FW3D-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-FW3D-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-RECS.                                                       
      *-----------------*                                                       
                                                                                
           PERFORM 3500-READ-BP13KW3D    THRU 3500-EXIT.                        
                                                                                
           PERFORM 2000-READ-BP13FW3D    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3500-READ-BP13KW3D.                                                      
      *-----------------*                                                       
           MOVE SPACES                 TO BP13KW3D-REC.                         
           INITIALIZE                     BP13KW3D-REC                          
                                                                                
           MOVE FW3D-KEY-FLD           TO KW3D-KEY-FLD.                         
                                                                                
           READ BP13KW3D.                                                       
                                                                                
           EVALUATE BP13KW3D-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1 TO WS-KW3D-FOUND                                            
              IF KW3D-CDE-COMPLETE-TAG = 'B'                                    
                 ADD 1 TO WS-KW3D-BK-COMP                                       
              ELSE                                                              
      *       IF (KW3D-DTE-BKAPPMT-DATE-MGPS = SPACES AND                       
      *           FW3D-DTE-BKAPPMT-DATE-MGPS NOT = SPACES) OR                   
      *          (KW3D-DTE-BKAPPMT-DATE = SPACES AND                            
      *           FW3D-DTE-BKAPPMT-DATE NOT = SPACES)                           
                 PERFORM 4000-UPDATE-BP13KW3D THRU 4000-EXIT                    
              END-IF                                                            
           WHEN 23                                                              
              PERFORM 5000-WRITE-BP13KW3D   THRU 5000-EXIT                      
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR BP13KW3D, STATUS  '                         
                        BP13KW3D-STATUS                                         
                        ',  KEY    ' KW3D-KEY-FLD                               
                MOVE BP13KW3D-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-UPDATE-BP13KW3D.                                                    
      *------------------*                                                      
           IF KW3D-CDE-COMPLETE-TAG = ' '                                       
              MOVE FW3D-DTE-BKAPPMT-DATE-MGPS TO                                
                                  KW3D-DTE-BKAPPMT-DATE-MGPS                    
              MOVE FW3D-TME-BKAPPMT-MGPS  TO KW3D-TME-BKAPPMT-MGPS              
              MOVE FW3D-NUM-MGPS-TAG      TO KW3D-NUM-MGPS-TAG                  
              MOVE FW3D-NUM-MGPS-QUEUE    TO KW3D-NUM-MGPS-QUEUE                
              MOVE FW3D-NUM-MGPS-FT-PAIR  TO KW3D-NUM-MGPS-FT-PAIR              
           END-IF.                                                              
                                                                                
           IF ((KW3D-DTE-BKAPPMT-DATE = SPACES OR LOW-VALUES) AND               
               (KW3D-TME-BKAPPMT = SPACES OR LOW-VALUES)) AND                   
              ((FW3D-DTE-BKAPPMT-DATE NOT = SPACES AND LOW-VALUES) AND          
              (FW3D-TME-BKAPPMT      NOT = SPACES AND LOW-VALUES))              
              MOVE FW3D-DTE-BKAPPMT-DATE  TO KW3D-DTE-BKAPPMT-DATE              
              MOVE FW3D-TME-BKAPPMT       TO KW3D-TME-BKAPPMT                   
           END-IF.                                                              
                                                                                
           REWRITE BP13KW3D-REC.                                                
                                                                                
           EVALUATE BP13KW3D-STATUS                                             
           WHEN 00                                                              
                ADD 1 TO WS-KW3D-UPDATED                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR REWRITING BP13KW3D, STATUS  '                    
                        BP13KW3D-STATUS                                         
                        ',  KEY    ' KW3D-KEY-FLD                               
                MOVE BP13KW3D-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       5000-WRITE-BP13KW3D.                                                     
      *------------------*                                                      
           MOVE BP13FW3D-REC       TO BP13KW3D-REC.                             
                                                                                
           WRITE BP13KW3D-REC.                                                  
                                                                                
           EVALUATE BP13KW3D-STATUS                                             
           WHEN 00                                                              
                ADD 1 TO WS-KW3D-WRITTEN                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR WRITING BP13KW3D, STATUS  '                      
                        BP13KW3D-STATUS                                         
                        ',  KEY    ' KW3D-KEY-FLD                               
                MOVE BP13KW3D-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '*==========BP13C93S=========================*'.             
           DISPLAY '*-------------------------------------------*'.             
           DISPLAY '  RECS READ FROM BP13FW3D    : ' WS-FW3D-READ.              
           DISPLAY '  RECS FOUND IN  BP13KW3D    : ' WS-KW3D-FOUND.             
           DISPLAY '  RECS BOOK COMP BP13KW3D    : ' WS-KW3D-BK-COMP.           
           DISPLAY '  RECS UPDATED IN BP13KW3D   : ' WS-KW3D-UPDATED.           
           DISPLAY '  RECS WRITTEN IN BP13KW3D   : ' WS-KW3D-WRITTEN.           
           DISPLAY '*===========================================*'.             
                                                                                
           CLOSE BP13FW3D                                                       
                 BP13KW3D.                                                      
                                                                                
           IF BP13KW3D-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR BP13KW3D. STATUS : ' BP13KW3D-STATUS         
              MOVE BP13KW3D-STATUS       TO   RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
