       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C31E.                                                 
       AUTHOR.        JGO1.                                                     
       DATE-WRITTEN.  09 APR 2013.                                              
      ************************************************************              
      *   SYSTEM NAME : SOC - SYSTEM OF COMMITMENT (BP13)        *              
      *                                                          *              
      *   OBJECTIVE:                                             *              
      *      SOC - DE-EARMARK OF REJECTED E-STAMPING             *              
      *      VOUCHERS AND CLEAR STAMP DATE OF K800               *              
      *                                                          *              
      * CHG-NO  BY   ON       DESCRIPTION                        *              
      *-------  ---  ------   ----------                         *              
      *BP134810 JGO1 09/04/13 INITIAL CODING                     *              
      *BP135193 JGO1 19/13/14 MOVE ZEROES TO DTE-SUATOL-STAMP    *              
      *BP135193 JGO1 18/03/14 UPDATE K410 FOR TP STAMPING        *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800     ASSIGN   TO      BP13K800                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  RANDOM                                   
                  RECORD KEY       IS  K800-NUM-REGN                            
                  FILE STATUS      IS  WS-K800-STATUS.                          
                                                                                
           SELECT BP13K410     ASSIGN   TO      BP13K410                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  RANDOM                                   
                  RECORD KEY       IS  K410-NUM-REGN                            
                  FILE STATUS      IS  WS-K410-STATUS.                          
                                                                                
           SELECT AG07F225  ASSIGN       TO AG07F225.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   AG07F225                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY AG07F225.                                                           
                                                                                
      *-------------------------                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------                                                
       01  WS-F225-EOF                  PIC X      VALUE 'N'.                   
       01  WS-CNT-F225-RD               PIC 9(4)   VALUE ZEROES.                
       01  WS-CNT-K800-UPD              PIC 9(4)   VALUE ZEROES.                
       01  WS-CNT-K410-UPD              PIC 9(4)   VALUE ZEROES.                
       01  WS-CNT-DEEARMARK             PIC 9(4)   VALUE ZEROES.                
       01  WS-AMT-EARMARK               PIC 9(10)V99 VALUE ZEROES.              
       01  WS-NUM-ACTION-TYP            PIC X(02)    VALUE SPACES.              
       01  WS-APPLICATION               PIC X(8)  VALUE SPACE.                  
           88  UPD-TP                   VALUE 'BP13C416'.                       
       01  WS-STATUS.                                                           
           05  WS-K800-STATUS           PIC X(02).                              
           05  WS-K410-STATUS           PIC X(02).                              
      *----------------------------------------------------------*              
      *    WS LINKAGE VARIABLES PASSED TO "AB05C902"             *              
      *----------------------------------------------------------*              
       COPY AB05902C.                                                           
                                                                                
      *-------------------------                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------                                                
       0000-MAIN.                                                               
                                                                                
            PERFORM 0500-INITIALIZE     THRU 0500-EXIT.                         
            PERFORM 1000-PROCESS        THRU 1000-EXIT                          
                    UNTIL WS-F225-EOF = 'Y'.                                    
            PERFORM 9999-CLOSING        THRU 9999-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       0500-INITIALIZE.                                                         
      ******************************************************************        
                                                                                
            OPEN INPUT AG07F225                                                 
                 I-O   BP13K410                                                 
                       BP13K800.                                                
                                                                                
           IF    WS-K800-STATUS = '00' OR '97'                                  
                 CONTINUE                                                       
           ELSE                                                                 
                 DISPLAY 'ERROR WHILE OPENING BP13K800' WS-K800-STATUS          
                 PERFORM  9999-CLOSING     THRU     9999-EXIT                   
           END-IF.                                                              
                                                                                
           IF    WS-K410-STATUS = '00' OR '97'                                  
                 CONTINUE                                                       
           ELSE                                                                 
                 DISPLAY 'ERROR WHILE OPENING BP13K410' WS-K410-STATUS          
                 PERFORM  9999-CLOSING     THRU     9999-EXIT                   
           END-IF.                                                              
                                                                                
      **CALL AB05C902 TO OPEN B05K006 AND AB05K009 FILES(CODE='O')              
           MOVE SPACES              TO   WS-B902-AREA.                          
           INITIALIZE                    WS-B902-AREA.                          
           MOVE 'O'                 TO   B902-CDE-TYPE.                         
           CALL 'AB05C902'  USING WS-B902-AREA.                                 
                                                                                
           IF B902-CDE-SYSERR NOT = ZEROES                                      
                 MOVE B902-CDE-SYSERR  TO   RETURN-CODE                         
                 DISPLAY 'ERROR CALLING AB05C902 SUB-ROUTINE!'                  
                 PERFORM  9999-CLOSING     THRU     9999-EXIT                   
           END-IF.                                                              
                                                                                
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       1000-PROCESS.                                                            
      ******************************************************************        
                                                                                
           PERFORM 1500-READ-F225       THRU 1500-EXIT.                         
                                                                                
           IF F225-NUM-REC-TYP = '00'                                           
              MOVE F225-NUM-ACTION-TYP TO WS-NUM-ACTION-TYP                     
              MOVE F225-NUM-VR-CREATE-USERID TO WS-APPLICATION                  
              GO TO 1000-EXIT                                                   
           ELSE                                                                 
              IF  F225-NUM-REC-TYP        = '99'                                
                MOVE SPACES              TO WS-NUM-ACTION-TYP                   
                GO TO 1000-EXIT                                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-NUM-ACTION-TYP = 'RI'                                          
              PERFORM 2000-DEEARMARK-ACCT  THRU 2000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-NUM-ACTION-TYP NOT = SPACES                                    
              IF UPD-TP                                                         
                 PERFORM 3500-UPD-BP13K410   THRU 3500-EXIT                     
              ELSE                                                              
                 PERFORM 2500-UPD-BP13K800   THRU 2500-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       1500-READ-F225.                                                          
      ******************************************************************        
                                                                                
            READ AG07F225                                                       
                 AT END MOVE 'Y' TO WS-F225-EOF                                 
                 GO TO 1000-EXIT.                                               
            ADD 1 TO WS-CNT-F225-RD.                                            
                                                                                
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2000-DEEARMARK-ACCT.                                                     
      ******************************************************************        
                                                                                
      **CALL AB05C902 TO DEEARMARK ACCOUNT(CODE='D')                            
           MOVE SPACES              TO   WS-B902-AREA.                          
           INITIALIZE                    WS-B902-AREA.                          
                                                                                
           STRING F225-NUM-DDD  F225-NUM-FIN-YEAR                               
                  F225-NUM-VR-SERIAL                                            
                  DELIMITED BY SIZE INTO B902-VR-NO                             
           END-STRING.                                                          
                                                                                
           MOVE 'D'                     TO   B902-CDE-TYPE.                     
           MOVE 'BP13'                  TO   B902-CDE-SYSTEMID.                 
           MOVE FUNCTION CURRENT-DATE   TO   B902-DTE-TRANS.                    
           MOVE 'P13C31E'               TO   B902-NUM-USERID-UPDATE.            
           MOVE 'CV'                    TO   B902-CDE-EARMARK-REASON.           
           MOVE 01                      TO   B902-NUM-HDB-REF-CNT.              
           MOVE F225-NUM-HDB-REF        TO   B902-NUM-HDB-REF(1).               
                                                                                
           MOVE F225-AMT-TRANS          TO   B902-AMT-EARMARK(1).               
                                                                                
           DISPLAY '----------------'.                                          
           DISPLAY 'B902-VR-NO       = ' B902-VR-NO.                            
           DISPLAY 'B902-CDE-TYPE    = ' B902-CDE-TYPE.                         
           DISPLAY 'CDE-SYSTEMID     = ' B902-CDE-SYSTEMID.                     
           DISPLAY 'B902-DTE-TRANS   = ' B902-DTE-TRANS.                        
           DISPLAY 'NUM-USERID       = ' B902-NUM-USERID-UPDATE.                
           DISPLAY 'HDB-REF-CNT      = ' B902-NUM-HDB-REF-CNT.                  
           DISPLAY 'NUM-HDB-REF      = ' B902-NUM-HDB-REF(1).                   
           DISPLAY 'B902-AMT-EARMARK = ' B902-AMT-EARMARK(1).                   
                                                                                
           CALL 'AB05C902'  USING WS-B902-AREA.                                 
                                                                                
           IF B902-CDE-SYSERR NOT = ZEROES                                      
                 MOVE B902-CDE-SYSERR  TO   RETURN-CODE                         
                 DISPLAY 'ERROR CALLING AB05C902 SUB-ROUTINE!'                  
                 PERFORM  9999-CLOSING     THRU     9999-EXIT                   
           END-IF.                                                              
                                                                                
           IF B902-CDE-ERROR-ACCT(1) = SPACES                                   
              ADD 1 TO  WS-CNT-DEEARMARK                                        
              GO TO 2000-EXIT                                                   
           END-IF.                                                              
                                                                                
           EVALUATE B902-CDE-ERROR-ACCT(1)                                      
              WHEN 'S0045'                                                      
                 DISPLAY  'B05K006 FILE ERROR ' F225-NUM-HDB-REF                
              WHEN 'S0052'                                                      
                 DISPLAY  'INVALID INPUT '      F225-NUM-HDB-REF                
              WHEN 'S0053'                                                      
                 DISPLAY  'AB05K009 FILE ERROR' F225-NUM-HDB-REF                
              WHEN 'S0054'                                                      
                 DISPLAY  'B05K006 REC NOT FND' F225-NUM-HDB-REF                
              WHEN 'S0055'                                                      
                 DISPLAY 'DOESN''T HAVE ENOUGH BALANCE '                        
                                                F225-NUM-HDB-REF                
              WHEN OTHER                                                        
                 DISPLAY 'AB05C902 SUBROUTINE ERROR'                            
                                                F225-NUM-HDB-REF                
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       2500-UPD-BP13K800.                                                       
      ******************************************************************        
                                                                                
           INITIALIZE                  BP13K800-MASTER.                         
           MOVE  F225-TXT-INFO(1:8) TO  K800-NUM-REGN.                          
           READ  BP13K800.                                                      
                                                                                
           IF WS-K800-STATUS = 00 OR 02                                         
                 MOVE '00000000'   TO K800-DTE-STAMP-AGMT                       
                 MOVE ZEROES       TO K800-DTE-SUATOL-STAMP                     
                 MOVE SPACES       TO K800-NUM-RELEASE-PAYMENT                  
                 PERFORM 3000-REWRITE-K800 THRU 3000-EXIT                       
           ELSE                                                                 
                 DISPLAY 'NOT FOUND IN BP13K800 - ' K800-NUM-REGN               
                 MOVE WS-K800-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSING                                             
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3000-REWRITE-K800.                                                       
      ******************************************************************        
                                                                                
            REWRITE BP13K800-MASTER.                                            
            IF WS-K800-STATUS  = '00'                                           
              ADD 1 TO WS-CNT-K800-UPD                                          
            ELSE                                                                
              DISPLAY 'ERROR WRITING, BP13K800 FILE:' WS-K800-STATUS            
              MOVE     WS-K800-STATUS  TO RETURN-CODE                           
              PERFORM 9999-CLOSING THRU 9999-EXIT                               
            END-IF.                                                             
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3500-UPD-BP13K410.                                                       
      ******************************************************************        
                                                                                
           INITIALIZE                  BP13K410-REC.                            
           MOVE  F225-TXT-INFO(1:8) TO  K410-NUM-REGN.                          
           READ  BP13K410.                                                      
                                                                                
           IF WS-K410-STATUS = 00 OR 02                                         
                 MOVE '00000000'   TO K410-DTE-STAMP-DOA                        
                 MOVE SPACES       TO K410-CDE-RELEASE-PAYMENT                  
                 PERFORM 4000-REWRITE-K410 THRU 4000-EXIT                       
           ELSE                                                                 
                 DISPLAY 'NOT FOUND IN BP13K410 - ' K410-NUM-REGN               
                 MOVE WS-K410-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSING                                             
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4000-REWRITE-K410.                                                       
      ******************************************************************        
                                                                                
            REWRITE BP13K410-REC.                                               
            IF WS-K410-STATUS  = '00'                                           
              ADD 1 TO WS-CNT-K410-UPD                                          
            ELSE                                                                
              DISPLAY 'ERROR WRITING, BP13K410 FILE:' WS-K410-STATUS            
              MOVE     WS-K410-STATUS  TO RETURN-CODE                           
              PERFORM 9999-CLOSING THRU 9999-EXIT                               
            END-IF.                                                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9999-CLOSING.                                                            
                                                                                
            DISPLAY '*********************'.                                    
            DISPLAY '*  BP13C31E TOTALS  *'.                                    
            DISPLAY '*********************'.                                    
            DISPLAY 'F225  READ:  ' WS-CNT-F225-RD.                             
            DISPLAY 'K800  UPD :  ' WS-CNT-K800-UPD.                            
            DISPLAY 'K410  UPD :  ' WS-CNT-K410-UPD.                            
            DISPLAY 'K006  UPD :  ' WS-CNT-DEEARMARK.                           
                                                                                
            CLOSE AG07F225                                                      
                  BP13K410                                                      
                  BP13K800.                                                     
                                                                                
      **CALL AB05C902 TO OPEN B05K006 AND AB05K009 FILES(CODE='C')              
           MOVE SPACES              TO   WS-B902-AREA.                          
           INITIALIZE                    WS-B902-AREA.                          
           MOVE 'C'                 TO   B902-CDE-TYPE.                         
           CALL 'AB05C902'  USING WS-B902-AREA.                                 
                                                                                
           IF B902-CDE-SYSERR NOT = ZEROES                                      
                 MOVE B902-CDE-SYSERR  TO   RETURN-CODE                         
                 DISPLAY 'ERROR CALLING AB05C902 SUB-ROUTINE!'                  
                 PERFORM  9999-CLOSING     THRU     9999-EXIT                   
           END-IF.                                                              
                                                                                
            STOP RUN.                                                           
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
