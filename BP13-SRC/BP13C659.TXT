      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C659.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
      *DATE-WRITTEN.  14/04/92.                                                 
                                                                                
      ******************************************************************        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  TO PRINT LETTER FOR REFUND OF OPTION FEE  *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F650  -  PREPVRCH FILE                *        
      *                  2.  BP13K800  -  SOC APPLICATION MASTER       *        
      *                  3.  BM06K110  -  FLATINV FILE                 *        
      *                  4.  AG07K920  -  BANK PAY TO FILE             *        
      *                  5.  BP13K022  -  OIC/REGN FILE                *        
      *                  6.  BP13K024  -  OIC FILE                     *        
      *                                                                *        
      *   FORMS ID    :  BP1307 / BP1344                               *        
      *                                                                *        
      *   LISTINGS    :  1.  LETTER FOR REFUND OF OPTION FEE           *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ---------------------------------   *        
      *   SOC-PH8   RML   100893   CALL 'BP13C904' TO GET ADDRESS      *        
      *                                  FIELDS                        *        
      *                            REFER TO BP13K800 INSTEAD OF        *        
      *                                  P10K020                       *        
      *   C9309A1   JEP   011193   TO CHECK FOR FOREIGN ADDRESS        *        
      *                                                                *        
      *   SOC-8B    TON   221193   TO CHANGE NAME FIELD FROM 22 BYTES  *        
      *                            TO 66 BYTES (BP13F650)              *        
      *   BP130418  SIA   200398   CHGE BM06K211 TO BM06K110           *        
      *   BP130594  RAS   23111998 TO CATER FOR IBG TRANSACTIONS       *        
      *   BP130612  LSB   29011999 TO CHANGE THE MORTGAGE LOAN APPLICA-*        
      *                            TION FORM                           *        
      *   BP130790  SCK   02022000 TO ADD IN IN-SOURCE DOCUMENTATION   *        
      *   BP130883  RASM  08082000 TO CHANGE BP13C904 TO BP13C913      *        
      *   BP132625  JF4   11012004 PRINT REFUND AMOUNT WITH DECIMAL    *        
      *                            PRINT OIC NAME                      *        
      *   BP135616 JGO1   090315   CHANGE LENGTH OF F650/K650 TO 500   *        
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F650  ASSIGN        TO BP13F650.                          
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE RECORD KEY IS K022-NUM-REGN               
                            FILE STATUS   IS WS-K022-STATUS.                    
                                                                                
           SELECT BP13K024  ASSIGN        TO BP13K024                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K024-KEY-FLD                       
                            FILE STATUS   IS WS-K024-STATUS.                    
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE RECORD KEY IS K110-REGN-NO                
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT AG07K920  ASSIGN        TO AG07K920                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K920-KEY-FLD                       
                            FILE STATUS   IS WS-K920-STATUS.                    
                                                                                
           SELECT P13L659A  ASSIGN        TO P13L659A.                          
                                                                                
           SELECT P13L659B  ASSIGN        TO P13L659B.                          
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F650                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F650.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS  100 CHARACTERS.                                    
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS  100 CHARACTERS.                                    
       COPY BP13K024.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   AG07K920                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY AG07K920.                                                           
                                                                                
       FD  P13L659A                                                             
           RECORDING MODE   IS F                                                
           RECORD CONTAINS  90 CHARACTERS.                                      
       01  P13L659A-REC                  PIC X(90).                             
                                                                                
       FD  P13L659B                                                             
           RECORDING MODE   IS F                                                
           RECORD CONTAINS  90 CHARACTERS.                                      
       01  P13L659B-REC                  PIC X(90).                             
                                                                                
                                                                                
      **************************                                                
       WORKING-STORAGE SECTION.                                                 
      **************************                                                
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K800-STATUS            PIC 99    VALUE 00.                    
           05  WS-K022-STATUS            PIC 99    VALUE 00.                    
           05  WS-K024-STATUS            PIC 99    VALUE 00.                    
           05  WS-K110-STATUS            PIC 99    VALUE 00.                    
           05  WS-K920-STATUS            PIC 99    VALUE 00.                    
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05  WS-CNT-F650               PIC 9(4)  VALUE ZEROES.                
           05  WS-CNT-PRTED-A            PIC 9(4)  VALUE ZEROES.                
           05  WS-CNT-PRTED-B            PIC 9(4)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-F650               PIC X     VALUE 'N'.                   
               88 EOF-F650                         VALUE 'Y'.                   
           05  BP13K022-FND-TAG          PIC X     VALUE 'N'.                   
               88 BP13K022-FND                     VALUE 'Y'.                   
           05  BP13K024-FND-TAG          PIC X     VALUE 'N'.                   
               88 BP13K024-FND                     VALUE 'Y'.                   
                                                                                
       01  WS-EDIT-DATE.                                                        
           05  WS-EDIT-CCYY              PIC 9(4)  VALUE 00.                    
           05  WS-EDIT-MM                PIC 99    VALUE 00.                    
           05  WS-EDIT-DD                PIC 99    VALUE 00 .                   
                                                                                
       01  WS-CURR-CCYYMMDD.                                                    
           03  WS-CURR-CC                PIC 99    VALUE 00.                    
           03  WS-CURR-YY                PIC 99    VALUE 00.                    
           03  WS-CURR-MM                PIC 99    VALUE 00.                    
           03  WS-CURR-DD                PIC 99    VALUE 00.                    
                                                                                
      *----------------------------------------------------------*              
      *    LINKAGE VARIABLES FOR SUB-PROGRAM 'BP13C913'          *              
      *----------------------------------------------------------*              
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10 WS-LINK-NUM-SCH        PIC X(4)  VALUE SPACES.                
               10 WS-LINK-NUM-ACC        PIC X(5)  VALUE SPACES.                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *----------------------------------------------------------*              
      *    PRINT OUT LAY-OUT FOR LETTER                          *              
      *----------------------------------------------------------*              
                                                                                
       01  L659-LINE1A.                                                         
           05  FILLER                    PIC X(30) VALUE SPACES.                
           05  FILLER                    PIC X(43) VALUE SPACES.                
           05  L659-REGN                 PIC X(8)  VALUE SPACES.                
           05  FILLER                    PIC X(8)  VALUE SPACES.                
                                                                                
       01  L659-LINE1.                                                          
           05  FILLER                    PIC X(30) VALUE SPACES.                
           05  FILLER                    PIC X(43) VALUE SPACES.                
           05  L659-DATE                 PIC X(8)  VALUE SPACES.                
           05  FILLER                    PIC X(8)  VALUE SPACES.                
                                                                                
       01  L659-LINE1B.                                                         
           05  FILLER                    PIC X(30) VALUE SPACES.                
           05  FILLER                    PIC X(43) VALUE SPACES.                
           05  L659-NUM-DID              PIC X(8)  VALUE SPACES.                
           05  FILLER                    PIC X(8)  VALUE SPACES.                
                                                                                
       01  L659-LINE2.                                                          
           05  FILLER                    PIC X(12) VALUE SPACES.                
           05  L659-NAME                 PIC X(66) VALUE SPACES.                
           05  FILLER                    PIC X(12) VALUE SPACES.                
                                                                                
       01  L659-LINE3.                                                          
           05  FILLER                    PIC X(12) VALUE SPACES.                
           05  L659-HDB-ADDR.                                                   
               10  L659-BLK-HEADER       PIC X(4)  VALUE 'BLK '.                
               10  L659-BLK-NO           PIC X(5)  VALUE SPACES.                
               10  FILLER                PIC X(2)  VALUE SPACES.                
               10  L659-STREET-NAME      PIC X(29) VALUE SPACES.                
           05  L659A-NONHDB-ADDR REDEFINES L659-HDB-ADDR.                       
               10  L659A-BLK-NO          PIC X(5).                              
               10  FILLER                PIC X(2).                              
               10  L659A-STREET-NAME     PIC X(29).                             
               10  FILLER                PIC X(4).                              
           05  FILLER                    PIC X(38) VALUE SPACES.                
                                                                                
       01  L659-LINE4.                                                          
           05  FILLER                    PIC X(12) VALUE SPACES.                
           05  L659-CHAR                 PIC X     VALUE '#'.                   
           05  L659-LEVEL                PIC X(2)  VALUE SPACES.                
           05  L659-DASH                 PIC X     VALUE '-'.                   
           05  L659-HSE-MAIN             PIC X(4)  VALUE SPACES.                
           05  L659-DASH2                PIC X     VALUE '-'.                   
           05  L659-HSE-SUB              PIC X(3)  VALUE SPACES.                
           05  FILLER                    PIC X(66) VALUE SPACES.                
                                                                                
       01  L659-LINE5.                                                          
           05  FILLER                    PIC X(12) VALUE SPACES.                
           05  L659-POST-COUNTRY-NAME    PIC X(10) VALUE 'SINGAPORE '.          
           05  L659-POST-OPEN-PAR        PIC X(1)  VALUE '('.                   
           05  L659-POST-CODE            PIC X(6)  VALUE SPACES.                
           05  L659-POST-CLSE-PAR        PIC X(1)  VALUE ')'.                   
           05  FILLER                    PIC X(60) VALUE SPACES.                
                                                                                
       01  L659-LINE6.                                                          
           05  FILLER                    PIC X(78) VALUE SPACES.                
           05  L659-BLK                  PIC X(5)  VALUE SPACES.                
           05  FILLER                    PIC X(7)  VALUE SPACES.                
                                                                                
       01  L659-LINE7.                                                          
           05  FILLER                    PIC X(12) VALUE SPACES.                
           05  L659-STREET               PIC X(20) VALUE SPACES.                
           05  FILLER                    PIC X(2)  VALUE ', '.                  
           05  FILLER                    PIC X(1)  VALUE '#'.                   
           05  L659-LEVEL-NO             PIC X(2)  VALUE SPACES.                
           05  L659-DASH3                PIC X     VALUE '-'.                   
           05  L659-MAIN-UNIT            PIC X(4)  VALUE SPACES.                
           05  L659-DASH4                PIC X     VALUE '-'.                   
           05  L659-SUB-UNIT             PIC X(4)  VALUE SPACES.                
           05  L659-COMMA                PIC X(2)  VALUE ', '.                  
           05  L659-COUNTRY-NAME         PIC X(10) VALUE 'SINGAPORE '.          
           05  L659-OPEN-PAR             PIC X(1)  VALUE '('.                   
           05  L659-POSTAL               PIC X(6)  VALUE SPACES.                
           05  L659-CLSE-PAR             PIC X(1)  VALUE ')'.                   
           05  FILLER                    PIC X(23) VALUE SPACES.                
                                                                                
       01  L659-LINE8.                                                          
           05  FILLER                    PIC X(76) VALUE SPACES.                
           05  L659-APP-DATE             PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(4)  VALUE SPACES.                
                                                                                
       01  L659-LINE9.                                                          
           05  FILLER                    PIC X(74) VALUE SPACES.                
           05  L659-OPTION-AMT           PIC ZZZ9.99.                           
           05  FILLER                    PIC X(09) VALUE SPACES.                
                                                                                
       01  L659-LINE10A.                                                        
           05  FILLER                    PIC X(20) VALUE SPACES.                
           05  L659-SOLE-NAME            PIC X(66) VALUE SPACES.                
                                                                                
       01  L659-LINE10B.                                                        
           05  FILLER                    PIC X(26) VALUE SPACES.                
           05  L659-NUM-ACCOUNT          PIC X(11) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE SPACES.                
           05  L659-NME-BANK             PIC X(40) VALUE SPACES.                
                                                                                
       01  L659-LINE11.                                                         
           05  FILLER                    PIC X(12) VALUE SPACES.                
           05  L659-NME-OIC              PIC X(30) VALUE SPACES.                
           05  FILLER                    PIC X(38) VALUE SPACES.                
                                                                                
        01  DJDE-BP1344                  PIC X(80) VALUE                        
            '$DJDE$ FORMS=BP1344,END;'.                                         
                                                                                
                                                                                
      ************************                                                  
       PROCEDURE DIVISION.                                                      
      ************************                                                  
                                                                                
      ******************************************************************        
       100-MAIN-ROUTINE.                                                        
      ******************************************************************        
           PERFORM  200-OPEN-FILES       THRU   200-EXIT.                       
           PERFORM  300-READ-F650        THRU   300-EXIT.                       
           PERFORM  400-PROCESS-DATA     THRU   400-EXIT                        
                        UNTIL EOF-F650.                                         
           PERFORM  1000-CLOSE-ROUTINE   THRU   1000-EXIT.                      
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-OPEN-FILES.                                                          
      ******************************************************************        
           OPEN INPUT BP13F650                                                  
                      BP13K800                                                  
                      BP13K022                                                  
                      BP13K024                                                  
                      BM06K110                                                  
                      AG07K920                                                  
               OUTPUT P13L659A                                                  
                      P13L659B.                                                 
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR-BP13K800, STATUS = ' WS-K800-STATUS        
              MOVE     WS-K800-STATUS  TO  RETURN-CODE                          
              PERFORM  1000-CLOSE-ROUTINE.                                      
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR-BP13K022, STATUS = ' WS-K022-STATUS        
              MOVE     WS-K022-STATUS  TO  RETURN-CODE                          
              PERFORM  1000-CLOSE-ROUTINE.                                      
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR-BP13K024, STATUS = ' WS-K024-STATUS        
              MOVE     WS-K024-STATUS  TO  RETURN-CODE                          
              PERFORM  1000-CLOSE-ROUTINE.                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR-BM06K110, STATUS = ' WS-K110-STATUS        
              MOVE     WS-K110-STATUS  TO  RETURN-CODE                          
              PERFORM  1000-CLOSE-ROUTINE.                                      
                                                                                
           IF WS-K920-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR-AG07K920, STATUS = ' WS-K920-STATUS        
              MOVE     WS-K920-STATUS  TO  RETURN-CODE                          
              PERFORM  1000-CLOSE-ROUTINE.                                      
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-READ-F650.                                                           
      ******************************************************************        
           READ BP13F650                                                        
                AT END MOVE  'Y'  TO  WS-EOF-F650                               
                   GO  TO 300-EXIT.                                             
                                                                                
           ADD  1  TO  WS-CNT-F650.                                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       400-PROCESS-DATA.                                                        
      ******************************************************************        
      *-----------------------------------------------------------*             
      *    READ BP13K800 USING F650-NUM-REGN                      *             
      *         IF FOUND                                          *             
      *            PRINT LETTERS                                  *             
      *         ELSE                                              *             
      *            DISPLAY ERROR MESSAGES                         *             
      *-----------------------------------------------------------*             
           PERFORM 500-READ-BM06K110   THRU   500-EXIT.                         
                                                                                
           MOVE    F650-NUM-REGN       TO     K800-NUM-REGN.                    
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              PERFORM 600-CALL-BP13C913   THRU   600-EXIT                       
              PERFORM 800-PRINT-LETTERS   THRU   800-EXIT                       
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 DISPLAY 'BP13K800 REC NOT FOUND, STAT ' WS-K800-STATUS         
                 DISPLAY '    K800-NUM-REGN    =  ' K800-NUM-REGN               
              ELSE                                                              
                 DISPLAY 'READ ERROR BP13K800, STATUS = ' WS-K800-STATUS        
                 MOVE     WS-K800-STATUS  TO  RETURN-CODE                       
                 PERFORM  1000-CLOSE-ROUTINE.                                   
                                                                                
           PERFORM  300-READ-F650    THRU   300-EXIT.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-READ-BM06K110.                                                       
      ******************************************************************        
      *-----------------------------------------------------------*             
      *    ACCESS BM06K110 TO GET SO-EXECUTING-DATE               *             
      *-----------------------------------------------------------*             
           MOVE F650-NUM-REGN  TO K110-REGN-NO.                                 
                                                                                
           READ BM06K110 KEY   IS K110-REGN-NO.                                 
                                                                                
           IF WS-K110-STATUS = 00 OR 02                                         
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF WS-K110-STATUS = 23                                            
                 DISPLAY 'BM06K110 RECORD NOT FOUND  = ' WS-K110-STATUS         
                 DISPLAY '    K110-REGN-NO  = ' K110-REGN-NO                    
              ELSE                                                              
                 DISPLAY 'READ ERROR BM06K110, STATUS ' WS-K110-STATUS          
                 DISPLAY '     K110-REGN-NO =  ' K110-REGN-NO                   
                 PERFORM 1000-CLOSE-ROUTINE.                                    
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       600-CALL-BP13C913.                                                       
      ******************************************************************        
      *-----------------------------------------------------------*             
      *    CALL 'BP13C913' USING K800-NUM-SCH-ACC                 *             
      *          TO GET ADDRESS FIELDS                            *             
      *-----------------------------------------------------------*             
           MOVE  SPACES                TO BP13COMM8-REC.                        
           INITIALIZE                     BP13COMM8-REC.                        
           MOVE  K800-NUM-SCH-ACC      TO WS-LINK-REC.                          
                                                                                
           CALL 'BP13C913'  USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
                                                                                
           IF COMM8-NUM-BLK = SPACES OR LOW-VALUES OR ZEROES                    
              DISPLAY 'SCH ACC NOT FOUND IN PIDB '     F650-NUM-REGN            
              MOVE     SPACES  TO  L659-BLK            L659-STREET              
                                   L659-LEVEL-NO       L659-MAIN-UNIT           
                                   L659-DASH4          L659-SUB-UNIT            
                                   L659-COMMA          L659-COUNTRY-NAME        
                                   L659-OPEN-PAR       L659-CLSE-PAR            
                                   L659-POSTAL                                  
              GO TO 600-EXIT.                                                   
                                                                                
           MOVE  COMM8-NUM-BLK          TO L659-BLK.                            
           MOVE  COMM8-NME-STREET       TO L659-STREET.                         
           MOVE  COMM8-NUM-LEVEL        TO L659-LEVEL-NO.                       
           MOVE  COMM8-NUM-UNIT-MAIN    TO L659-MAIN-UNIT.                      
                                                                                
           IF COMM8-NUM-UNIT-SUB        = SPACES OR LOW-VALUES                  
              MOVE SPACES               TO L659-DASH4                           
           ELSE                                                                 
              MOVE '-'                  TO L659-DASH4                           
              MOVE COMM8-NUM-UNIT-SUB   TO L659-SUB-UNIT.                       
                                                                                
           IF COMM8-NUM-POSTAL-CODE = SPACES OR LOW-VALUES OR ZEROES            
              MOVE  SPACES              TO  L659-COMMA                          
                                            L659-COUNTRY-NAME                   
                                            L659-OPEN-PAR                       
                                            L659-POSTAL                         
                                            L659-CLSE-PAR                       
           ELSE                                                                 
              MOVE ', '                  TO  L659-COMMA                         
              MOVE 'SINGAPORE'           TO  L659-COUNTRY-NAME                  
              MOVE '('                   TO  L659-OPEN-PAR                      
              MOVE ')'                   TO  L659-CLSE-PAR                      
              MOVE COMM8-NUM-POSTAL-CODE TO  L659-POSTAL.                       
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-PRINT-LETTERS.                                                       
      ******************************************************************        
           MOVE    SPACES             TO    P13L659A-REC.                       
           MOVE    SPACES             TO    P13L659B-REC.                       
                                                                                
           MOVE FUNCTION CURRENT-DATE TO    WS-CURR-CCYYMMDD.                   
           MOVE    F650-NUM-REGN      TO    L659-REGN.                          
           MOVE    SPACES             TO    L659-DATE.                          
                                                                                
           IF F650-PAYABLE-NAME1      =   SPACES OR LOW-VALUES                  
              MOVE F650-PAYABLE-NAME2 TO  L659-SOLE-NAME                        
                                          L659-NAME                             
           ELSE                                                                 
              MOVE F650-PAYABLE-NAME1 TO  L659-SOLE-NAME                        
                                          L659-NAME.                            
                                                                                
           IF F650-CDE-ADDR-TYPE   =  'H'                                       
              MOVE SPACES             TO  L659A-BLK-NO                          
              MOVE SPACES             TO  L659A-STREET-NAME                     
              MOVE 'BLK '             TO  L659-BLK-HEADER                       
              MOVE F650-BLK           TO  L659-BLK-NO                           
              MOVE F650-STREET-NAME   TO  L659-STREET-NAME                      
           ELSE                                                                 
              MOVE SPACES             TO  L659-BLK-HEADER                       
              MOVE SPACES             TO  L659-BLK-NO                           
              MOVE SPACES             TO  L659-STREET-NAME                      
              MOVE F650-BLK           TO  L659A-BLK-NO                          
              MOVE F650-STREET-NAME   TO  L659A-STREET-NAME                     
           END-IF.                                                              
                                                                                
           IF F650-HSE-LVL  =  SPACES OR  LOW-VALUES                            
              MOVE SPACES             TO  L659-CHAR                             
              MOVE SPACES             TO  L659-DASH                             
              MOVE F650-HSE-LVL       TO  L659-LEVEL                            
           ELSE                                                                 
              MOVE '#'                TO  L659-CHAR                             
              MOVE '-'                TO  L659-DASH                             
              MOVE F650-HSE-LVL       TO  L659-LEVEL.                           
                                                                                
           MOVE    F650-HSE-MAIN      TO  L659-HSE-MAIN.                        
                                                                                
           IF F650-HSE-SUB  =  SPACES OR  LOW-VALUES                            
              MOVE SPACES             TO  L659-DASH2                            
                                          L659-HSE-SUB                          
           ELSE                                                                 
              MOVE '-'                TO  L659-DASH2                            
              MOVE F650-HSE-SUB       TO  L659-HSE-SUB.                         
                                                                                
           IF F650-POSTAL-CDE = SPACES OR LOW-VALUES OR ZEROES                  
              MOVE SPACES             TO  L659-POST-COUNTRY-NAME                
                                          L659-POST-OPEN-PAR                    
                                          L659-POST-CODE                        
                                          L659-POST-CLSE-PAR                    
           ELSE                                                                 
              MOVE 'SINGAPORE'        TO  L659-POST-COUNTRY-NAME                
              MOVE '('                TO  L659-POST-OPEN-PAR                    
              MOVE ')'                TO  L659-POST-CLSE-PAR                    
              MOVE  F650-POSTAL-CDE   TO  L659-POST-CODE.                       
                                                                                
           MOVE    K800-AMT-BK-PAID   TO  L659-OPTION-AMT.                      
                                                                                
           MOVE   K110-DTE-SO-EXECUTING TO WS-EDIT-DATE.                        
           STRING WS-EDIT-DD '/' WS-EDIT-MM '/' WS-EDIT-CCYY                    
                  DELIMITED BY SIZE INTO L659-APP-DATE.                         
                                                                                
           PERFORM 900-GET-OIC-NAME   THRU 900-EXIT.                            
                                                                                
           IF (F650-NUM-IBG-ACCT = SPACES) AND                                  
              (F650-NUM-BANK = SPACES)                                          
              ADD    1            TO    WS-CNT-PRTED-A                          
              WRITE  P13L659A-REC AFTER PAGE                                    
              WRITE  P13L659A-REC FROM L659-LINE1A AFTER 1                      
              WRITE  P13L659A-REC FROM L659-LINE1 AFTER 2                       
              WRITE  P13L659A-REC FROM L659-LINE1B AFTER 1                      
              WRITE  P13L659A-REC FROM L659-LINE2 AFTER 3                       
              WRITE  P13L659A-REC FROM L659-LINE3 AFTER 2                       
              WRITE  P13L659A-REC FROM L659-LINE4 AFTER 2                       
              WRITE  P13L659A-REC FROM L659-LINE5 AFTER 2                       
              WRITE  P13L659A-REC FROM L659-LINE6 AFTER 10                      
              WRITE  P13L659A-REC FROM L659-LINE7 AFTER 2                       
              WRITE  P13L659A-REC FROM L659-LINE8 AFTER 8                       
              WRITE  P13L659A-REC FROM L659-LINE9 AFTER 4                       
              WRITE  P13L659A-REC FROM L659-LINE10A AFTER 2                     
              WRITE  P13L659A-REC FROM L659-LINE11  AFTER 11                    
              MOVE   SPACES         TO P13L659A-REC                             
              WRITE  P13L659A-REC                                               
           ELSE                                                                 
              MOVE   SPACES         TO K920-KEY-FLD                             
              MOVE   F650-NUM-BANK  TO K920-NUM-BANK                            
              MOVE   F650-NUM-BRNCH TO K920-NUM-BRNCH                           
                                                                                
              READ AG07K920                                                     
                                                                                
              EVALUATE WS-K920-STATUS                                           
                 WHEN 00                                                        
                      ADD    1                 TO WS-CNT-PRTED-B                
                      MOVE   K920-NME-BANK     TO L659-NME-BANK                 
                      MOVE   F650-NUM-IBG-ACCT TO L659-NUM-ACCOUNT              
                      WRITE  P13L659B-REC FROM DJDE-BP1344 AFTER PAGE           
                      MOVE   SPACES            TO P13L659B-REC                  
                      WRITE  P13L659B-REC AFTER PAGE                            
                      WRITE  P13L659B-REC FROM L659-LINE1A AFTER 1              
                      WRITE  P13L659B-REC FROM L659-LINE1 AFTER 2               
                      WRITE  P13L659B-REC FROM L659-LINE1B AFTER 1              
                      WRITE  P13L659B-REC FROM L659-LINE2 AFTER 3               
                      WRITE  P13L659B-REC FROM L659-LINE3 AFTER 2               
                      WRITE  P13L659B-REC FROM L659-LINE4 AFTER 2               
                      WRITE  P13L659B-REC FROM L659-LINE5 AFTER 2               
                      WRITE  P13L659B-REC FROM L659-LINE6 AFTER 10              
                      WRITE  P13L659B-REC FROM L659-LINE7 AFTER 2               
                      WRITE  P13L659B-REC FROM L659-LINE8 AFTER 8               
                      WRITE  P13L659B-REC FROM L659-LINE9 AFTER 4               
                      WRITE  P13L659B-REC FROM L659-LINE10B AFTER 2             
                      WRITE  P13L659B-REC FROM L659-LINE11  AFTER 11            
                      MOVE   SPACES         TO P13L659B-REC                     
                      WRITE  P13L659B-REC                                       
                 WHEN 23                                                        
                      DISPLAY 'AG07K920 REC NOT FOUND, STAT '                   
                               WS-K920-STATUS                                   
                      DISPLAY '    K920-KEY-FLD     =  ' K920-KEY-FLD           
                 WHEN OTHER                                                     
                      DISPLAY 'READ ERROR AG07K920, STATUS = '                  
                               WS-K920-STATUS                                   
                      MOVE     WS-K920-STATUS  TO  RETURN-CODE                  
                      PERFORM  1000-CLOSE-ROUTINE THRU 1000-EXIT                
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       900-GET-OIC-NAME.                                                        
      ******************************************************************        
           MOVE SPACES                  TO   L659-NME-OIC                       
                                             L659-NUM-DID.                      
           MOVE F650-NUM-REGN           TO   K022-NUM-REGN.                     
           PERFORM 910-READ-BP13K022    THRU 910-EXIT.                          
           IF BP13K022-FND                                                      
              MOVE K022-NUM-OIC         TO   K024-KEY-FLD                       
              PERFORM 930-READ-BP13K024 THRU 930-EXIT                           
              IF BP13K024-FND                                                   
                 MOVE K024-NME-OIC      TO   L659-NME-OIC                       
                 MOVE K024-NUM-DID      TO   L659-NUM-DID                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       910-READ-BP13K022.                                                       
      ******************************************************************        
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 MOVE   'Y'             TO   BP13K022-FND-TAG                   
              WHEN 23                                                           
                 DISPLAY 'BP13K022 REC NOT FOUND, REGN : '                      
                                             K022-NUM-REGN                      
                 MOVE   SPACE           TO   BP13K022-FND-TAG                   
              WHEN OTHER                                                        
                 DISPLAY 'BP13K022 READ ERRROR, RET CODE : '                    
                                             WS-K022-STATUS                     
                 MOVE  WS-K022-STATUS   TO   RETURN-CODE                        
                 PERFORM 1000-CLOSE-ROUTINE                                     
           END-EVALUATE.                                                        
                                                                                
       910-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       930-READ-BP13K024.                                                       
      ******************************************************************        
           READ BP13K024.                                                       
           EVALUATE WS-K024-STATUS                                              
              WHEN 00                                                           
                 MOVE   'Y'             TO   BP13K024-FND-TAG                   
              WHEN 23                                                           
                 DISPLAY 'BP13K024 REC NOT FOUND, OIC : '                       
                                             K024-KEY-FLD                       
                 MOVE   SPACE           TO   BP13K024-FND-TAG                   
              WHEN OTHER                                                        
                 DISPLAY 'BP13K024 READ ERRROR, RET CODE : '                    
                                             WS-K024-STATUS                     
                 MOVE  WS-K024-STATUS   TO   RETURN-CODE                        
                 PERFORM 1000-CLOSE-ROUTINE                                     
           END-EVALUATE.                                                        
                                                                                
       930-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
            DISPLAY '                                        '.                 
            DISPLAY ' ******  BP13C659 CONTROL TOTALS  ******'.                 
            DISPLAY '                                        '.                 
            DISPLAY ' NO. OF RECS READ FROM F650    : ' WS-CNT-F650.            
            DISPLAY ' NO. OF RECS PRINTED (P13L659A): ' WS-CNT-PRTED-A.         
            DISPLAY ' NO. OF RECS PRINTED (P13L659B): ' WS-CNT-PRTED-B.         
                                                                                
            CLOSE BP13F650                                                      
                  BP13K800                                                      
                  BP13K022                                                      
                  BP13K024                                                      
                  BM06K110                                                      
                  AG07K920                                                      
                  P13L659A                                                      
                  P13L659B.                                                     
                                                                                
            IF WS-K800-STATUS NOT = 00 AND 97                                   
               DISPLAY 'CLOSING ERROR-BP13K800, STATUS ' WS-K800-STATUS.        
                                                                                
            IF WS-K022-STATUS NOT = 00 AND 97                                   
               DISPLAY 'CLOSING ERROR-BP13K022, STATUS ' WS-K022-STATUS.        
                                                                                
            IF WS-K024-STATUS NOT = 00 AND 97                                   
               DISPLAY 'CLOSING ERROR-BP13K024, STATUS ' WS-K024-STATUS.        
                                                                                
            IF WS-K110-STATUS NOT = 00 AND 97                                   
               DISPLAY 'CLOSING ERROR-BM06K110, STATUS ' WS-K110-STATUS.        
                                                                                
            IF WS-K920-STATUS NOT = 00 AND 97                                   
               DISPLAY 'CLOSING ERROR-AG07K920, STATUS ' WS-K920-STATUS.        
                                                                                
            STOP RUN.                                                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
