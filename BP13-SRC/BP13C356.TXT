      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.      BP13C356.                                               
      *AUTHOR.          MARIA THERESA R. SANTOS                                 
      *DATE-WRITTEN.    27/07/95.                                               
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      **********************************************************                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1.  THIS IS A PROGRAM TO WRITE THE DAILY          *                
      *          SUB-TRANS CASES TO ACCUMULATED SUB-TRANSFER   *                
      *          FILE                                          *                
      *                                                        *                
      *      2.  WRITE SUB-TRANSFER REC TO ACCUMULATED         *                
      *          AGREEMENT ORDER FILE                          *                
      *                                                        *                
      *      3.  WRITE EX-APPLN TO SEQUENTIAL FILE             *                
      *          FOR BACKUP PURPOSE                            *                
      *                                                        *                
      * MODIFICATION HISTORY:                                  *                
      * ---------------------                                  *                
      * CHGE NO   BY    DATE          REMARKS                  *                
      * --------  ---  --------  ---------------               *                
      * BP130094  EG1  09/03/96  TO CATER FOR SUB-TRANSFER     *                
      *                          ENHANCEMENTS.                 *                
      * BP130418  RHB  04/06/98  Y2K                           *                
      * BP130696  RAS  02/08/99  TO MOVE F880-NUM-INCREASE-SCCC*                
      *                          , F880-AMT-SC-CC1-NET         *                
      *                          , F880-AMT-CUR-SELL-PRICE     *                
      *                          INTO BP13K330                 *                
      * BP132625  JF4  23/11/04  COMPUTE TOTAL SF, CF,MORT-CF  *                
      *                          AND CAVEAT FOR EACH CPF A/C   *                
      *                          AFTER SUB-TRF.                *                
      * BP132660  JF4  31/01/05  CHG K330-AMT-FLEXI-SELLPR TO  *                
      *                          K330-AMT-CURR-SELLPR          *                
      * BP132660  JF4  17/03/05  UPDATE POD, POF AND OPT-IN    *                
      * BP132526  SK   16/06/04  MOVE CAVEAT,MORT FEE TO K330  *                
      **********************************************************                
                                                                                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
      **********************************************************                
                                                                                
      *--------------------------------------------------------*                
       CONFIGURATION SECTION.                                                   
      *--------------------------------------------------------*                
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *--------------------------------------------------------*                
       INPUT-OUTPUT SECTION.                                                    
      *--------------------------------------------------------*                
                                                                                
       FILE-CONTROL.                                                            
           SELECT BP13F880   ASSIGN TO BP13F880.                                
           SELECT BP13F885   ASSIGN TO BP13F885.                                
           SELECT BP13K882   ASSIGN TO BP13K882                                 
                  ORGANIZATION INDEXED                                          
                  ACCESS       RANDOM                                           
                  RECORD KEY   K882-KEY-FLD                                     
                  FILE STATUS  K882-FSTAT.                                      
           SELECT BP13K820   ASSIGN TO BP13K820                                 
                  ORGANIZATION INDEXED                                          
                  ACCESS       RANDOM                                           
                  RECORD KEY   K820-KEY-FLD                                     
                  FILE STATUS  K820-FSTAT.                                      
           SELECT BP13K330   ASSIGN TO BP13K330                                 
                  ORGANIZATION INDEXED                                          
                  ACCESS       DYNAMIC                                          
                  RECORD KEY   K330-KEY-FLD                                     
                  FILE STATUS  K330-FSTAT.                                      
           SELECT BP13K885   ASSIGN TO BP13K885                                 
                  ORGANIZATION INDEXED                                          
                  ACCESS       DYNAMIC                                          
                  RECORD KEY   K885-KEY-FLD                                     
                  FILE STATUS  K885-FSTAT.                                      
                                                                                
      **********************************************************                
       DATA DIVISION.                                                           
      **********************************************************                
                                                                                
      *--------------------------------------------------------*                
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   BP13F880                                                            
            BLOCK CONTAINS 0                                                    
            RECORD CONTAINS 2000                                                
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F880.                                                           
                                                                                
       FD   BP13F885                                                            
            BLOCK CONTAINS 0                                                    
            RECORD CONTAINS 400                                                 
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F885.                                                           
                                                                                
       FD   BP13K882                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K882.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400.                                                
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K885                                                            
            RECORD CONTAINS 400.                                                
       COPY BP13K885.                                                           
                                                                                
       FD   BP13K330                                                            
            RECORD CONTAINS 2000.                                               
       COPY BP13K330.                                                           
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
                                                                                
       01  WORK-AREAS.                                                          
                                                                                
           05  K882-FSTAT           PIC XX.                                     
           05  K820-FSTAT           PIC XX.                                     
           05  K885-FSTAT           PIC XX.                                     
           05  K330-FSTAT           PIC XX.                                     
           05  WS-EOF-F880          PIC X         VALUE '0'.                    
               88  EOF-F880                       VALUE '1'.                    
           05  WS-EOF-K885          PIC X         VALUE '0'.                    
               88  EOF-K885                       VALUE '1'.                    
           05  WS-EOF-K330          PIC X         VALUE '0'.                    
               88  EOF-K330                       VALUE '1'.                    
           05  WS-DTE-UPDATE.                                                   
               10 WS-DTE-UPDATE-CC  PIC X(2).                                   
               10 WS-DTE-UPDATE-YY  PIC X(2).                                   
               10 WS-DTE-UPDATE-MM  PIC X(2).                                   
               10 WS-DTE-UPDATE-DD  PIC X(2).                                   
           05  WS-TIME.                                                 00323990
               10  WS-TME-HH        PIC 99    VALUE ZEROES.             00323990
               10  WS-TME-MM        PIC 99    VALUE ZEROES.             00324090
               10  WS-TME-SS        PIC 99    VALUE ZEROES.             00324190
           05  WS-SYS-TIME.                                             00323990
               10  WS-TME-HHMM      PIC 9(6)  VALUE ZEROES.             00323990
               10  WS-TME-MICRO     PIC 9(1)  VALUE ZEROES.             00323990
           05  WS-CNT-F880-READ     PIC 9(5)      VALUE 0.                      
           05  WS-CNT-K882-WRITE    PIC 9(5)      VALUE 0.                      
           05  WS-CNT-K882-SKIP     PIC 9(5)      VALUE 0.                      
           05  WS-CNT-K885-READ     PIC 9(5)      VALUE 0.                      
           05  WS-CNT-K820-READ     PIC 9(5)      VALUE 0.                      
           05  WS-CNT-K330-READ     PIC 9(5)      VALUE 0.                      
           05  WS-CNT-F885-WRITE    PIC 9(5)      VALUE 0.                      
           05  WS-CNT-K330-WRITE    PIC 9(5)      VALUE 0.                      
           05  WS-CNT-K330-SKIP     PIC 9(5)      VALUE 0.                      
           05  WS-SEQ-NO            PIC 9         VALUE 0.                      
                                                                                
           05 WS-TROR               PIC 9(02) VALUE ZEROES.                     
           05 WS-TREE               PIC 9(02) VALUE ZEROES.                     
                                                                                
       01  WS-MATCH-TABLE.                                                      
           05 WS-TROR-TREE-MATCH OCCURS 4 TIMES.                                
              10 WS-TROR-MATCH-NO   PIC 9(2).                                   
              10 WS-TREE-MATCH-NO   PIC 9(2).                                   
                                                                                
       01 WS-BP13F880-REC.                                                      
          05 FILLER                          PIC X(35).                         
          05 WS-F880-TROR OCCURS 4 TIMES.                                       
             10 WS-F880-NRIC-TROR            PIC X(09).                         
             10 WS-F880-SHR-TROR             PIC X(03).                         
             10 WS-F880-DEMISE-TROR          PIC X(01).                         
          05 FILLER                          PIC X(11).                         
          05 WS-F880-TREE OCCURS 4 TIMES.                                       
             10 WS-F880-NRIC-TREE            PIC X(09).                         
             10 WS-F880-SHR-TREE             PIC X(03).                         
             10 WS-F880-DEMISE-TREE          PIC X(01).                         
          05 FILLER                          PIC X(211).                        
          05 WS-F880-AMT-SF-CASH             PIC S9(5)V99.                      
          05 WS-F880-SF-CPF OCCURS 4 TIMES.                                     
             10 WS-F880-AMT-SF-CPF           PIC S9(5)V99.                      
          05 FILLER                          PIC X(15).                         
          05 WS-F880-AMT-CF-CASH             PIC S9(5)V99.                      
          05 WS-F880-CF-CPF OCCURS 4 TIMES.                                     
             10 WS-F880-AMT-CF-CPF           PIC S9(5)V99.                      
          05 FILLER                          PIC X(386).                        
          05 WS-F880-AMT-TREE-SF-CASH        PIC S9(5)V99.                      
          05 WS-F880-TREE-SF-CPF OCCURS 4 TIMES.                                
             10 WS-F880-AMT-TREE-SF-CPF      PIC S9(5)V99.                      
          05 WS-F880-AMT-TREE-CF-CASH        PIC S9(5)V99.                      
          05 WS-F880-TREE-CF-CPF OCCURS 4 TIMES.                                
             10 WS-F880-AMT-TREE-CF-CPF      PIC S9(5)V99.                      
          05 FILLER                          PIC X(418).                        
          05 WS-F880-AMT-CAVEAT-CASH         PIC S9(3)V99.                      
          05 WS-F880-CAVEAT-CPF OCCURS 4 TIMES.                                 
             10 WS-F880-AMT-CAVEAT-CPF       PIC S9(3)V99.                      
          05 FILLER                          PIC X(45).                         
          05 WS-F880-AMT-MORT-CF-CASH        PIC S9(5)V99.                      
          05 WS-F880-MORT-CF-CPF OCCURS 4 TIMES.                                
             10 WS-F880-AMT-MORT-CF-CPF      PIC S9(5)V99.                      
          05 FILLER                          PIC X(110).                        
          05 WS-F880-AMT-TREE-CAVEAT-CASH    PIC S9(3)V99.                      
          05 WS-F880-TREE-CAVEAT-CPF OCCURS 4 TIMES.                            
             10 WS-F880-AMT-TREE-CAVEAT-CPF  PIC S9(3)V99.                      
          05 FILLER                          PIC X(45).                         
          05 WS-F880-AMT-TREE-MORT-CF-CASH   PIC S9(5)V99.                      
          05 WS-F880-TREE-MORT-CF-CPF OCCURS 4 TIMES.                           
             10 WS-F880-AMT-TREE-MORT-CF-CPF PIC S9(5)V99.                      
          05 FILLER                          PIC X(360).                        
                                                                                
                                                                                
      **********************************************************                
       PROCEDURE DIVISION.                                                      
      **********************************************************                
                                                                                
      *--------------------------------------------------------*                
       000-MAIN.                                                                
      *--------------------------------------------------------*                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
                                                                                
           PERFORM 200-READ-F880     THRU 200-EXIT.                             
                                                                                
           PERFORM 300-PROCESS-REC   THRU 300-EXIT UNTIL EOF-F880.              
                                                                                
           PERFORM 900-CLOSE-FILES   THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       100-OPEN-FILES.                                                          
      *--------------------------------------------------------*                
           OPEN INPUT  BP13F880                                                 
                       BP13K820                                                 
                       BP13K885                                                 
                I-O    BP13K330                                                 
                       BP13K882                                                 
                OUTPUT BP13F885.                                                
                                                                                
           IF K882-FSTAT NOT =  '00'  AND '97'                                  
              DISPLAY 'BP13K882 OPEN ERROR : ' K882-FSTAT                       
              PERFORM 900-CLOSE-FILES   THRU 900-EXIT.                          
                                                                                
           IF K820-FSTAT NOT =  '00'  AND '97'                                  
              DISPLAY 'BP13K820 OPEN ERROR : ' K820-FSTAT                       
              PERFORM 900-CLOSE-FILES   THRU 900-EXIT.                          
                                                                                
           IF K885-FSTAT NOT =  '00'  AND '97'                                  
              DISPLAY 'BP13K885 OPEN ERROR : ' K885-FSTAT                       
              PERFORM 900-CLOSE-FILES   THRU 900-EXIT.                          
                                                                                
           IF K330-FSTAT NOT =  '00'  AND '97'                                  
              DISPLAY 'BP13K330 OPEN ERROR : ' K330-FSTAT                       
              PERFORM 900-CLOSE-FILES   THRU 900-EXIT.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DTE-UPDATE.                         
           ACCEPT   WS-TIME      FROM  TIME.                                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       200-READ-F880.                                                           
      *--------------------------------------------------------*                
           READ BP13F880                                                        
                AT END                                                          
                   MOVE '1' TO WS-EOF-F880                                      
                   GO TO 200-EXIT.                                              
                                                                                
           ADD 1 TO WS-CNT-F880-READ.                                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       300-PROCESS-REC.                                                         
      *--------------------------------------------------------*                
           PERFORM 400-WRITE-K882    THRU 400-EXIT.                             
                                                                                
           PERFORM 500-WRITE-K330    THRU 500-EXIT.                             
                                                                                
           PERFORM 600-WRITE-F885    THRU 600-EXIT.                             
                                                                                
           PERFORM 200-READ-F880     THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       400-WRITE-K882.                                                          
      *--------------------------------------------------------*                
           MOVE SPACES             TO   BP13K882-REC.                           
           INITIALIZE BP13K882-REC.                                             
           MOVE  BP13F880-REC      TO   BP13K882-REC.                           
           MOVE  F880-NUM-REGN     TO   K882-NUM-REGN-SUBTRF.                   
           MOVE  WS-DTE-UPDATE     TO   K882-DTE-UPDATE.                        
                                                                                
           WRITE BP13K882-REC.                                                  
                                                                                
           IF K882-FSTAT NOT = '00'                                             
              DISPLAY 'BP13K882 WRITE ERROR : ' K882-FSTAT                      
                      ' RECORD ' K882-KEY-FLD                                   
              ADD 1 TO WS-CNT-K882-SKIP                                         
           ELSE                                                                 
              ADD 1 TO WS-CNT-K882-WRITE.                                       
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       500-WRITE-K330.                                                          
      *--------------------------------------------------------*                
           PERFORM 505-STARTBR-BP13K330   THRU 505-EXIT.                        
                                                                                
           MOVE SPACES                    TO BP13K330-REC.                      
           INITIALIZE                        BP13K330-REC.                      
           MOVE F880-NUM-REGN             TO K330-CUR-REG.                      
           MOVE F880-NUM-SCH-ACC          TO K330-SCH-ACC.                      
           MOVE F880-NUM-IDP-SCH-TREE     TO K330-IDP-SCH.                      
           MOVE F880-NUM-ELIG-SCH-TREE    TO K330-ELIG-SCH.                     
           MOVE F880-NUM-ALLOC-SCH        TO K330-ALLOC-SCH.                    
           MOVE F880-NUM-NRIC1-TREE       TO K330-NRIC1.                        
           MOVE F880-CDE-PR-TAG1-TREE     TO K330-PR-TAG1.                      
           MOVE F880-NUM-NRIC2-TREE       TO K330-NRIC2.                        
           MOVE F880-CDE-PR-TAG2-TREE     TO K330-PR-TAG2.                      
           MOVE F880-NUM-NRIC3-TREE       TO K330-NRIC3.                        
           MOVE F880-CDE-PR-TAG3-TREE     TO K330-PR-TAG3.                      
           MOVE F880-NUM-NRIC4-TREE       TO K330-NRIC4.                        
           MOVE F880-CDE-PR-TAG4-TREE     TO K330-PR-TAG4.                      
                                                                                
           IF F880-NUM-NRIC1-TREE NOT = SPACES AND LOW-VALUES                   
              MOVE F880-NUM-REGN        TO K820-NUM-REGN                        
              MOVE F880-NUM-NRIC1-TREE  TO K820-NUM-NRIC                        
              PERFORM 520-READ-BP13K820 THRU 520-EXIT                           
              MOVE K820-NME-OCCP        TO K330-NAME1                           
              MOVE K820-NUM-CPF         TO K330-CPF1                            
           END-IF.                                                              
                                                                                
           IF F880-NUM-NRIC2-TREE NOT = SPACES AND LOW-VALUES                   
              MOVE F880-NUM-REGN        TO K820-NUM-REGN                        
              MOVE F880-NUM-NRIC2-TREE  TO K820-NUM-NRIC                        
              PERFORM 520-READ-BP13K820 THRU 520-EXIT                           
              MOVE K820-NME-OCCP        TO K330-NAME2                           
              MOVE K820-NUM-CPF         TO K330-CPF2                            
           END-IF.                                                              
                                                                                
           IF F880-NUM-NRIC3-TREE NOT = SPACES AND LOW-VALUES                   
              MOVE F880-NUM-REGN        TO K820-NUM-REGN                        
              MOVE F880-NUM-NRIC3-TREE  TO K820-NUM-NRIC                        
              PERFORM 520-READ-BP13K820 THRU 520-EXIT                           
              MOVE K820-NME-OCCP        TO K330-NAME3                           
              MOVE K820-NUM-CPF         TO K330-CPF3                            
           END-IF.                                                              
                                                                                
           IF F880-NUM-NRIC4-TREE NOT = SPACES AND LOW-VALUES                   
              MOVE F880-NUM-REGN        TO K820-NUM-REGN                        
              MOVE F880-NUM-NRIC4-TREE  TO K820-NUM-NRIC                        
              PERFORM 520-READ-BP13K820 THRU 520-EXIT                           
              MOVE K820-NME-OCCP        TO K330-NAME4                           
              MOVE K820-NUM-CPF         TO K330-CPF4                            
           END-IF.                                                              
                                                                                
           MOVE F880-AMT-PREV-SELL-PRICE  TO K330-AMT-PREV-SELLPR.              
           MOVE F880-AMT-CONV-GST         TO K330-AMT-CONVEY-GST.               
           MOVE F880-AMT-SOC-DEBTOR       TO K330-AMT-SOC-DEBTOR.               
           MOVE F880-AMT-SOC-PAID-CASH    TO K330-AMT-SOC-CASH.                 
           MOVE F880-AMT-SOC-PAID-CPF1    TO K330-AMT-SOC-CPF1.                 
           MOVE F880-AMT-SOC-PAID-CPF2    TO K330-AMT-SOC-CPF2.                 
           MOVE F880-AMT-SOC-PAID-CPF3    TO K330-AMT-SOC-CPF3.                 
           MOVE F880-AMT-SOC-PAID-CPF4    TO K330-AMT-SOC-CPF4.                 
           MOVE F880-AMT-DISCOUNT-OCS     TO K330-AMT-DISCOUNT-SELLPR.          
           MOVE F880-AMT-CUR-SELL-PRICE   TO K330-AMT-OFFER-SELLPR.             
           MOVE F880-AMT-NEW-CD-REQ       TO K330-AMT-CD-REQ.                   
           MOVE F880-AMT-BKNG-FEE-REQ     TO K330-AMT-BOOKING-FEE.              
           MOVE F880-AMT-LS               TO K330-AMT-LEASE-STAMP.              
           MOVE F880-AMT-CONV             TO K330-AMT-CONVEY.                   
           MOVE F880-CDE-OCS-TAG          TO K330-CDE-OCS-TAG.                  
           MOVE F880-CDE-OCS-TYPE         TO K330-CDE-OCS-TYPE.                 
           MOVE F880-AMT-TOPUP-CD-CASH    TO K330-AMT-TOPUP-CASH.               
           MOVE F880-AMT-TOPUP-CD-CPF1    TO K330-AMT-TOPUP-CPF1.               
           MOVE F880-AMT-TOPUP-CD-CPF2    TO K330-AMT-TOPUP-CPF2.               
           MOVE F880-AMT-TOPUP-CD-CPF3    TO K330-AMT-TOPUP-CPF3.               
           MOVE F880-AMT-TOPUP-CD-CPF4    TO K330-AMT-TOPUP-CPF4.               
           MOVE F880-AMT-TOPUP-SOCA-SF-CASH                                     
                                          TO K330-AMT-LEASE-STAMP-CASH.         
           MOVE F880-AMT-TOPUP-SOCA-SF-CPF1                                     
                                          TO K330-AMT-LEASE-STAMP-CPF1.         
           MOVE F880-AMT-TOPUP-SOCA-SF-CPF2                                     
                                          TO K330-AMT-LEASE-STAMP-CPF2.         
           MOVE F880-AMT-TOPUP-SOCA-SF-CPF3                                     
                                          TO K330-AMT-LEASE-STAMP-CPF3.         
           MOVE F880-AMT-TOPUP-SOCA-SF-CPF4                                     
                                          TO K330-AMT-LEASE-STAMP-CPF4.         
           MOVE F880-AMT-TOPUP-SOCA-CF-CASH TO K330-AMT-CONV-CASH.              
           MOVE F880-AMT-TOPUP-SOCA-CF-CPF1 TO K330-AMT-CONV-CPF1.              
           MOVE F880-AMT-TOPUP-SOCA-CF-CPF2 TO K330-AMT-CONV-CPF2.              
           MOVE F880-AMT-TOPUP-SOCA-CF-CPF3 TO K330-AMT-CONV-CPF3.              
           MOVE F880-AMT-TOPUP-SOCA-CF-CPF4 TO K330-AMT-CONV-CPF4.              
           MOVE F880-NUM-HOLDING-TYPE-TREE  TO K330-CDE-HOLDING-PREV.           
           MOVE 'AA'                      TO K330-CDE-TRANS.                    
           MOVE F880-DTE-FSA-SIGNED       TO K330-DTE-SALE-AGRM.                
           MOVE F880-DTE-SUB-TRANSFER     TO K330-DTE-AGRM-ORDER.               
           MOVE F880-NUM-SUB-TRANSFER     TO K330-CDE-SUB-TRANSFER.             
           MOVE F880-NUM-NRIC1-TROR       TO K330-PREV-NRIC1.                   
           MOVE F880-NUM-NRIC2-TROR       TO K330-PREV-NRIC2.                   
           MOVE F880-NUM-NRIC3-TROR       TO K330-PREV-NRIC3.                   
           MOVE F880-NUM-NRIC4-TROR       TO K330-PREV-NRIC4.                   
           MOVE F880-NUM-ASPF             TO K330-CDE-ASPF.                     
           MOVE F880-NUM-PRIORITY-PURCHASER TO K330-CDE-PRIORITY-PUR.           
           MOVE F880-DTE-OCS-DUE          TO K330-DTE-OCS.                      
           MOVE F880-NUM-SERIAL-CPF1      TO K330-NUM-SERIAL-CPF1.              
           MOVE F880-NUM-SERIAL-CPF2      TO K330-NUM-SERIAL-CPF2.              
           MOVE F880-NUM-SERIAL-CPF3      TO K330-NUM-SERIAL-CPF3.              
           MOVE F880-NUM-SERIAL-CPF4      TO K330-NUM-SERIAL-CPF4.              
           MOVE F880-DTE-SUB-TRANSFER     TO K330-DTE-SUB-TRANSFER.             
                                                                                
           IF F880-AMT-MORT-CF    IS NOT NUMERIC                                
              MOVE ZEROES                 TO K330-AMT-MORT-CONVY                
           ELSE                                                                 
              MOVE F880-AMT-MORT-CF       TO K330-AMT-MORT-CONVY                
           END-IF.                                                              
                                                                                
           IF F880-AMT-MORT-CF-GST   IS NOT NUMERIC                             
              MOVE ZEROES                 TO K330-AMT-MORT-CONVY-GST            
           ELSE                                                                 
              MOVE F880-AMT-MORT-CF-GST   TO K330-AMT-MORT-CONVY-GST            
           END-IF.                                                              
                                                                                
           IF F880-AMT-CAVEAT     IS NOT NUMERIC                                
              MOVE ZEROES                 TO K330-AMT-CAVEAT-REGN               
           ELSE                                                                 
              MOVE F880-AMT-CAVEAT        TO K330-AMT-CAVEAT-REGN               
           END-IF.                                                              
                                                                                
           IF F880-AMT-CAVEAT-CASH-TREE NOT NUMERIC                             
              MOVE ZEROES              TO K330-AMT-CAVEAT-REGN-CASH             
           ELSE                                                                 
              MOVE F880-AMT-CAVEAT-CASH-TREE                                    
                                       TO K330-AMT-CAVEAT-REGN-CASH             
           END-IF.                                                              
                                                                                
           IF F880-AMT-CAVEAT-CPF1-TREE NOT NUMERIC                             
              MOVE ZEROES              TO K330-AMT-CAVEAT-REGN-CPF1             
           ELSE                                                                 
              MOVE F880-AMT-CAVEAT-CPF1-TREE                                    
                                       TO K330-AMT-CAVEAT-REGN-CPF1             
           END-IF.                                                              
                                                                                
           IF F880-AMT-CAVEAT-CPF2-TREE NOT NUMERIC                             
              MOVE ZEROES              TO K330-AMT-CAVEAT-REGN-CPF2             
           ELSE                                                                 
              MOVE F880-AMT-CAVEAT-CPF2-TREE                                    
                                       TO K330-AMT-CAVEAT-REGN-CPF2             
           END-IF.                                                              
                                                                                
           IF F880-AMT-CAVEAT-CPF3-TREE NOT NUMERIC                             
              MOVE ZEROES              TO K330-AMT-CAVEAT-REGN-CPF3             
           ELSE                                                                 
              MOVE F880-AMT-CAVEAT-CPF3-TREE                                    
                                       TO K330-AMT-CAVEAT-REGN-CPF3             
           END-IF.                                                              
                                                                                
           IF F880-AMT-CAVEAT-CPF4-TREE NOT NUMERIC                             
              MOVE ZEROES              TO K330-AMT-CAVEAT-REGN-CPF4             
           ELSE                                                                 
              MOVE F880-AMT-CAVEAT-CPF4-TREE                                    
                                       TO K330-AMT-CAVEAT-REGN-CPF4             
           END-IF.                                                              
                                                                                
           IF F880-AMT-SC-CC1-NET IS NOT NUMERIC                                
              MOVE ZEROES                 TO K330-AMT-SC-CC1-NET                
           ELSE                                                                 
              MOVE F880-AMT-SC-CC1-NET    TO K330-AMT-SC-CC1-NET                
           END-IF.                                                              
                                                                                
           MOVE F880-NUM-SDS-2ND-PYMT-PREV TO                                   
                K330-NUM-SDS-2ND-PYMT-PREV.                                     
                                                                                
           IF F880-AMT-POD  IS NOT NUMERIC                                      
              MOVE ZEROES           TO K330-AMT-POD                             
           ELSE                                                                 
              MOVE F880-AMT-POD     TO K330-AMT-POD                             
           END-IF.                                                              
                                                                                
           IF F880-AMT-POF  IS NOT NUMERIC                                      
              MOVE ZEROES           TO K330-AMT-POF                             
           ELSE                                                                 
              MOVE F880-AMT-POF     TO K330-AMT-POF                             
           END-IF.                                                              
                                                                                
           MOVE F880-NUM-OPT-IN           TO K330-NUM-OPT-IN.                   
                                                                                
           MOVE F880-NUM-INCREASE-SCCC    TO K330-NUM-INCREASE-SCCC.            
           MOVE F880-AMT-CUR-SELL-PRICE   TO K330-AMT-CURR-SELLPR.              
           MOVE F880-NUM-ASPF             TO K330-CDE-ASPF.                     
           MOVE WS-DTE-UPDATE             TO K330-DTE-LAST-UPDATE.              
           MOVE F880-NUM-REGN             TO K330-CURR-REG.                     
           MOVE WS-SEQ-NO                 TO K330-SEQ-NO.                       
           MOVE WS-TIME                   TO WS-TME-HHMM.                       
           MOVE WS-SYS-TIME               TO K330-SYSTIME.                      
           MOVE F880-NUM-USERID           TO K330-USERID.                       
                                                                                
      *----------------------------------------------------------*              
      * COMPUTE TOTAL SF, CF, MORT-CF AND CAVEAT IN CASH AND CPF *              
      *----------------------------------------------------------*              
           INITIALIZE WS-BP13F880-REC.                                          
           MOVE SPACES                    TO   WS-BP13F880-REC.                 
           MOVE BP13F880-REC              TO   WS-BP13F880-REC.                 
           PERFORM 550-MATCH-TROR-TREE    THRU 550-EXIT.                        
           PERFORM 570-COMPUTE-FEES       THRU 570-EXIT.                        
                                                                                
           WRITE BP13K330-REC.                                                  
                                                                                
           IF K330-FSTAT NOT = '00'                                             
              DISPLAY 'BP13K330 WRITE ERROR : ' K330-FSTAT                      
                      ' RECORD ' K330-KEY-FLD                                   
              ADD 1 TO WS-CNT-K330-SKIP                                         
              MOVE 99 TO RETURN-CODE                                            
           ELSE                                                                 
              ADD 1 TO WS-CNT-K330-WRITE.                                       
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       505-STARTBR-BP13K330.                                                    
      *--------------------------------------------------------*                
           MOVE SPACES          TO K330-KEY-FLD.                                
           MOVE F880-NUM-REGN   TO K330-CURR-REG.                               
           MOVE 1               TO WS-SEQ-NO.                                   
                                                                                
           START BP13K330                                                       
              KEY >= K330-KEY-FLD.                                              
                                                                                
           EVALUATE K330-FSTAT                                                  
              WHEN 00                                                           
                 PERFORM 510-READ-BP13K330  THRU 510-EXIT                       
                 PERFORM 515-GET-SEQ-NO     THRU 515-EXIT                       
                    UNTIL F880-NUM-REGN NOT = K330-CURR-REG                     
                    OR EOF-K330                                                 
              WHEN 23                                                           
                 MOVE 1                      TO WS-SEQ-NO                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13K330 READ ERROR : ' K330-FSTAT                    
                        ' RECORD ' K330-KEY-FLD                                 
           END-EVALUATE.                                                        
                                                                                
       505-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       510-READ-BP13K330.                                                       
      *--------------------------------------------------------*                
           READ BP13K330 NEXT RECORD                                            
                AT END MOVE '1' TO WS-EOF-K330.                                 
                                                                                
           ADD 1                 TO WS-CNT-K330-READ.                           
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       515-GET-SEQ-NO.                                                          
      *--------------------------------------------------------*                
           IF K330-SEQ-NO IS NOT NUMERIC                                        
              MOVE ZEROS TO K330-SEQ-NO                                         
           END-IF.                                                              
                                                                                
           IF F880-NUM-REGN = K330-CURR-REG                                     
              IF WS-SEQ-NO = 9                                                  
                 MOVE 99 TO RETURN-CODE                                         
                 DISPLAY 'K330-KEY-FLD ==>  ' K330-KEY-FLD                      
                 DISPLAY 'RECORD EXCEEDED ALLOWABLE SEQUENCE'                   
              ELSE                                                              
                 MOVE K330-SEQ-NO        TO WS-SEQ-NO                           
                 ADD 1                   TO WS-SEQ-NO                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 510-READ-BP13K330     THRU 510-EXIT.                         
                                                                                
       515-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       520-READ-BP13K820.                                                       
      *--------------------------------------------------------*                
           READ BP13K820.                                                       
                                                                                
           EVALUATE K820-FSTAT                                                  
              WHEN 00                                                           
                 ADD 1                   TO WS-CNT-K820-READ                    
              WHEN 23                                                           
                 DISPLAY 'BP13K820 RECORD NOT FOUND ' K820-FSTAT                
                        ' RECORD ' K820-KEY-FLD                                 
                 MOVE SPACES TO K820-NME-OCCP                                   
              WHEN OTHER                                                        
                 DISPLAY 'BP13K820 READ ERROR : ' K820-FSTAT                    
                        ' RECORD ' K820-KEY-FLD                                 
                 MOVE SPACES TO K820-NME-OCCP                                   
           END-EVALUATE.                                                        
                                                                                
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       550-MATCH-TROR-TREE.                                                     
      *--------------------------------------------------------*                
           INITIALIZE  WS-MATCH-TABLE.                                          
                                                                                
           PERFORM VARYING WS-TREE FROM 1 BY 1 UNTIL WS-TREE > 4                
              IF WS-F880-NRIC-TREE (WS-TREE) > SPACES                           
                 PERFORM VARYING WS-TROR FROM 1 BY 1 UNTIL WS-TROR > 4          
                    IF (WS-F880-NRIC-TREE (WS-TREE) =                           
                                   WS-F880-NRIC-TROR (WS-TROR))                 
                       MOVE WS-TROR  TO WS-TREE-MATCH-NO (WS-TREE)              
                       MOVE WS-TREE  TO WS-TROR-MATCH-NO (WS-TROR)              
                    END-IF                                                      
                 END-PERFORM                                                    
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       570-COMPUTE-FEES.                                                        
      *--------------------------------------------------------*                
           PERFORM 580-CHECK-NUMERIC       THRU 580-EXIT.                       
                                                                                
           COMPUTE K330-AMT-LEASE-STAMP-CASH =                                  
                   WS-F880-AMT-SF-CASH + WS-F880-AMT-TREE-SF-CASH.              
                                                                                
           COMPUTE K330-AMT-CONV-CASH        =                                  
                   WS-F880-AMT-CF-CASH + WS-F880-AMT-TREE-CF-CASH.              
                                                                                
           COMPUTE K330-AMT-MORT-CONVY-CASH  =                                  
              WS-F880-AMT-MORT-CF-CASH + WS-F880-AMT-TREE-MORT-CF-CASH.         
                                                                                
           COMPUTE K330-AMT-CAVEAT-REGN-CASH =                                  
              WS-F880-AMT-CAVEAT-CASH  + WS-F880-AMT-TREE-CAVEAT-CASH.          
                                                                                
           PERFORM VARYING WS-TREE FROM 1 BY 1 UNTIL WS-TREE > 4                
              EVALUATE WS-TREE                                                  
              WHEN 1                                                            
                 IF WS-TREE-MATCH-NO(WS-TREE) = ZEROES                          
                    MOVE WS-F880-AMT-TREE-SF-CPF(WS-TREE)      TO               
                         K330-AMT-LEASE-STAMP-CPF1                              
                    MOVE WS-F880-AMT-TREE-CF-CPF(WS-TREE)      TO               
                         K330-AMT-CONV-CPF1                                     
                    MOVE WS-F880-AMT-TREE-MORT-CF-CPF(WS-TREE) TO               
                         K330-AMT-MORT-CONVY-CPF1                               
                    MOVE WS-F880-AMT-TREE-CAVEAT-CPF(WS-TREE)  TO               
                         K330-AMT-CAVEAT-REGN-CPF1                              
                 ELSE                                                           
                    MOVE WS-TREE-MATCH-NO(WS-TREE)      TO WS-TROR              
                    COMPUTE K330-AMT-LEASE-STAMP-CPF1 =                         
                            WS-F880-AMT-TREE-SF-CPF(WS-TREE)      +             
                            WS-F880-AMT-SF-CPF(WS-TROR)                         
                    COMPUTE K330-AMT-CONV-CPF1        =                         
                            WS-F880-AMT-TREE-CF-CPF(WS-TREE)      +             
                            WS-F880-AMT-CF-CPF(WS-TROR)                         
                    COMPUTE K330-AMT-MORT-CONVY-CPF1  =                         
                            WS-F880-AMT-TREE-MORT-CF-CPF(WS-TREE) +             
                            WS-F880-AMT-MORT-CF-CPF(WS-TROR)                    
                    COMPUTE K330-AMT-CAVEAT-REGN-CPF1 =                         
                            WS-F880-AMT-TREE-CAVEAT-CPF(WS-TREE)  +             
                            WS-F880-AMT-CAVEAT-CPF(WS-TROR)                     
                 END-IF                                                         
              WHEN 2                                                            
                 IF WS-TREE-MATCH-NO(WS-TREE) = ZEROES                          
                    MOVE WS-F880-AMT-TREE-SF-CPF(WS-TREE)      TO               
                         K330-AMT-LEASE-STAMP-CPF2                              
                    MOVE WS-F880-AMT-TREE-CF-CPF(WS-TREE)      TO               
                         K330-AMT-CONV-CPF2                                     
                    MOVE WS-F880-AMT-TREE-MORT-CF-CPF(WS-TREE) TO               
                         K330-AMT-MORT-CONVY-CPF2                               
                    MOVE WS-F880-AMT-TREE-CAVEAT-CPF(WS-TREE)  TO               
                         K330-AMT-CAVEAT-REGN-CPF2                              
                 ELSE                                                           
                    MOVE WS-TREE-MATCH-NO(WS-TREE)  TO WS-TROR                  
                    COMPUTE K330-AMT-LEASE-STAMP-CPF2 =                         
                            WS-F880-AMT-TREE-SF-CPF(WS-TREE)      +             
                            WS-F880-AMT-SF-CPF(WS-TROR)                         
                    COMPUTE K330-AMT-CONV-CPF2        =                         
                            WS-F880-AMT-TREE-CF-CPF(WS-TREE)      +             
                            WS-F880-AMT-CF-CPF(WS-TROR)                         
                    COMPUTE K330-AMT-MORT-CONVY-CPF2  =                         
                            WS-F880-AMT-TREE-MORT-CF-CPF(WS-TREE) +             
                            WS-F880-AMT-MORT-CF-CPF(WS-TROR)                    
                    COMPUTE K330-AMT-CAVEAT-REGN-CPF2 =                         
                            WS-F880-AMT-TREE-CAVEAT-CPF(WS-TREE)  +             
                            WS-F880-AMT-CAVEAT-CPF(WS-TROR)                     
                 END-IF                                                         
              WHEN 3                                                            
                 IF WS-TREE-MATCH-NO(WS-TREE) = ZEROES                          
                    MOVE WS-F880-AMT-TREE-SF-CPF(WS-TREE)      TO               
                         K330-AMT-LEASE-STAMP-CPF3                              
                    MOVE WS-F880-AMT-TREE-CF-CPF(WS-TREE)      TO               
                         K330-AMT-CONV-CPF3                                     
                    MOVE WS-F880-AMT-TREE-MORT-CF-CPF(WS-TREE) TO               
                         K330-AMT-MORT-CONVY-CPF3                               
                    MOVE WS-F880-AMT-TREE-CAVEAT-CPF(WS-TREE)  TO               
                         K330-AMT-CAVEAT-REGN-CPF3                              
                 ELSE                                                           
                    MOVE WS-TREE-MATCH-NO(WS-TREE)  TO WS-TROR                  
                    COMPUTE K330-AMT-LEASE-STAMP-CPF3 =                         
                            WS-F880-AMT-TREE-SF-CPF(WS-TREE)      +             
                            WS-F880-AMT-SF-CPF(WS-TROR)                         
                    COMPUTE K330-AMT-CONV-CPF3        =                         
                            WS-F880-AMT-TREE-CF-CPF(WS-TREE)      +             
                            WS-F880-AMT-CF-CPF(WS-TROR)                         
                    COMPUTE K330-AMT-MORT-CONVY-CPF3  =                         
                            WS-F880-AMT-TREE-MORT-CF-CPF(WS-TREE) +             
                            WS-F880-AMT-MORT-CF-CPF(WS-TROR)                    
                    COMPUTE K330-AMT-CAVEAT-REGN-CPF3 =                         
                            WS-F880-AMT-TREE-CAVEAT-CPF(WS-TREE)  +             
                            WS-F880-AMT-CAVEAT-CPF(WS-TROR)                     
                 END-IF                                                         
              WHEN 4                                                            
                 IF WS-TREE-MATCH-NO(WS-TREE) = ZEROES                          
                    MOVE WS-F880-AMT-TREE-SF-CPF(WS-TREE)      TO               
                         K330-AMT-LEASE-STAMP-CPF4                              
                    MOVE WS-F880-AMT-TREE-CF-CPF(WS-TREE)      TO               
                         K330-AMT-CONV-CPF4                                     
                    MOVE WS-F880-AMT-TREE-MORT-CF-CPF(WS-TREE) TO               
                         K330-AMT-MORT-CONVY-CPF4                               
                    MOVE WS-F880-AMT-TREE-CAVEAT-CPF(WS-TREE)  TO               
                         K330-AMT-CAVEAT-REGN-CPF4                              
                 ELSE                                                           
                    MOVE WS-TREE-MATCH-NO(WS-TREE)  TO WS-TROR                  
                    COMPUTE K330-AMT-LEASE-STAMP-CPF4 =                         
                            WS-F880-AMT-TREE-SF-CPF(WS-TREE)      +             
                            WS-F880-AMT-SF-CPF(WS-TROR)                         
                    COMPUTE K330-AMT-CONV-CPF4        =                         
                            WS-F880-AMT-TREE-CF-CPF(WS-TREE)      +             
                            WS-F880-AMT-CF-CPF(WS-TROR)                         
                    COMPUTE K330-AMT-MORT-CONVY-CPF4  =                         
                            WS-F880-AMT-TREE-MORT-CF-CPF(WS-TREE) +             
                            WS-F880-AMT-MORT-CF-CPF(WS-TROR)                    
                    COMPUTE K330-AMT-CAVEAT-REGN-CPF4 =                         
                            WS-F880-AMT-TREE-CAVEAT-CPF(WS-TREE)  +             
                            WS-F880-AMT-CAVEAT-CPF(WS-TROR)                     
                 END-IF                                                         
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
       570-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       580-CHECK-NUMERIC.                                                       
      *--------------------------------------------------------*                
           IF WS-F880-AMT-SF-CASH            NOT NUMERIC                        
              MOVE ZEROES      TO WS-F880-AMT-SF-CASH                           
           END-IF.                                                              
           IF WS-F880-AMT-CF-CASH            NOT NUMERIC                        
              MOVE ZEROES      TO WS-F880-AMT-CF-CASH                           
           END-IF.                                                              
           IF WS-F880-AMT-MORT-CF-CASH       NOT NUMERIC                        
              MOVE ZEROES      TO WS-F880-AMT-MORT-CF-CASH                      
           END-IF.                                                              
           IF WS-F880-AMT-CAVEAT-CASH        NOT NUMERIC                        
              MOVE ZEROES      TO WS-F880-AMT-CAVEAT-CASH                       
           END-IF.                                                              
                                                                                
           IF WS-F880-AMT-TREE-SF-CASH       NOT NUMERIC                        
              MOVE ZEROES      TO WS-F880-AMT-TREE-SF-CASH                      
           END-IF.                                                              
           IF WS-F880-AMT-TREE-CF-CASH       NOT NUMERIC                        
              MOVE ZEROES      TO WS-F880-AMT-TREE-CF-CASH                      
           END-IF.                                                              
           IF WS-F880-AMT-TREE-MORT-CF-CASH  NOT NUMERIC                        
              MOVE ZEROES      TO WS-F880-AMT-TREE-MORT-CF-CASH                 
           END-IF.                                                              
           IF WS-F880-AMT-TREE-CAVEAT-CASH   NOT NUMERIC                        
              MOVE ZEROES      TO WS-F880-AMT-TREE-CAVEAT-CASH                  
           END-IF.                                                              
                                                                                
           PERFORM VARYING WS-TREE FROM 1 BY 1 UNTIL WS-TREE > 4                
              IF WS-F880-AMT-SF-CPF(WS-TREE)           NOT NUMERIC              
                 MOVE ZEROES   TO WS-F880-AMT-SF-CPF(WS-TREE)                   
              END-IF                                                            
              IF WS-F880-AMT-TREE-SF-CPF(WS-TREE)      NOT NUMERIC              
                 MOVE ZEROES   TO WS-F880-AMT-TREE-SF-CPF(WS-TREE)              
              END-IF                                                            
              IF WS-F880-AMT-CF-CPF(WS-TREE)           NOT NUMERIC              
                 MOVE ZEROES   TO WS-F880-AMT-CF-CPF(WS-TREE)                   
              END-IF                                                            
              IF WS-F880-AMT-TREE-CF-CPF(WS-TREE)      NOT NUMERIC              
                 MOVE ZEROES   TO WS-F880-AMT-TREE-CF-CPF(WS-TREE)              
              END-IF                                                            
              IF WS-F880-AMT-MORT-CF-CPF(WS-TREE)      NOT NUMERIC              
                 MOVE ZEROES   TO WS-F880-AMT-MORT-CF-CPF(WS-TREE)              
              END-IF                                                            
              IF WS-F880-AMT-TREE-MORT-CF-CPF(WS-TREE) NOT NUMERIC              
                 MOVE ZEROES   TO WS-F880-AMT-TREE-MORT-CF-CPF(WS-TREE)         
              END-IF                                                            
              IF WS-F880-AMT-CAVEAT-CPF(WS-TREE)       NOT NUMERIC              
                 MOVE ZEROES   TO WS-F880-AMT-CAVEAT-CPF(WS-TREE)               
              END-IF                                                            
              IF WS-F880-AMT-TREE-CAVEAT-CPF(WS-TREE)  NOT NUMERIC              
                 MOVE ZEROES   TO WS-F880-AMT-TREE-CAVEAT-CPF(WS-TREE)          
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       580-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       600-WRITE-F885.                                                          
      *--------------------------------------------------------*                
           MOVE SPACES          TO K885-KEY-FLD.                                
           MOVE F880-NUM-REGN   TO K885-NUM-REGN.                               
                                                                                
           START BP13K885                                                       
              KEY >= K885-KEY-FLD.                                              
                                                                                
           EVALUATE K885-FSTAT                                                  
              WHEN 00                                                           
                 PERFORM 610-READ-K885     THRU 610-EXIT                        
                 PERFORM 620-EXTR-K885     THRU 620-EXIT                        
                   UNTIL EOF-K885 OR F880-NUM-REGN NOT = K885-NUM-REGN          
              WHEN 23                                                           
                 DISPLAY 'BP13K885 RECORD NOT FOUND ' K885-FSTAT                
                        ' RECORD ' K885-KEY-FLD                                 
              WHEN OTHER                                                        
                 DISPLAY 'BP13K885 READ ERROR : ' K885-FSTAT                    
                        ' RECORD ' K885-KEY-FLD                                 
           END-EVALUATE.                                                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       610-READ-K885.                                                           
      *--------------------------------------------------------*                
           READ BP13K885 NEXT RECORD                                            
                 AT END MOVE '1' TO WS-EOF-K885                                 
                 GO TO 610-EXIT.                                                
                                                                                
           IF F880-NUM-REGN = K885-NUM-REGN                                     
              ADD 1        TO WS-CNT-K885-READ                                  
           END-IF.                                                              
                                                                                
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       620-EXTR-K885.                                                           
      *--------------------------------------------------------*                
           MOVE SPACES           TO   BP13F885-REC.                             
           INITIALIZE                 BP13F885-REC.                             
           MOVE  BP13K885-REC    TO   BP13F885-REC.                             
           WRITE                      BP13F885-REC.                             
           ADD 1                 TO WS-CNT-F885-WRITE.                          
           PERFORM 610-READ-K885 THRU 610-EXIT.                                 
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       900-CLOSE-FILES.                                                         
      *--------------------------------------------------------*                
           CLOSE BP13K882                                                       
                 BP13F880                                                       
                 BP13K330                                                       
                 BP13K820                                                       
                 BP13K885                                                       
                 BP13F885.                                                      
                                                                                
           IF K820-FSTAT NOT =  '00'  AND '97'                                  
              DISPLAY 'BP13K820 CLOSE ERROR : ' K820-FSTAT.                     
                                                                                
           IF K882-FSTAT NOT =  '00'  AND '97'                                  
              DISPLAY 'BP13K882 CLOSE ERROR : ' K882-FSTAT.                     
                                                                                
           IF K330-FSTAT NOT =  '00'  AND '97'                                  
              DISPLAY 'BP13K330 CLOSE ERROR : ' K330-FSTAT.                     
                                                                                
           IF K885-FSTAT NOT =  '00'  AND '97'                                  
              DISPLAY 'BP13K885 CLOSE ERROR : ' K885-FSTAT.                     
                                                                                
           DISPLAY '**********  BP13C356 **********  '.                         
           DISPLAY '                      '.                                    
           DISPLAY 'RUN-DATE : ' WS-DTE-UPDATE.                                 
           DISPLAY SPACES.                                                      
           DISPLAY '         CONTROL TOTALS'.                                   
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF BP13F880 RECORDS READ    : ' WS-CNT-F880-READ         
           DISPLAY 'NO OF BP13K885 RECORDS READ    : ' WS-CNT-K885-READ         
           DISPLAY 'NO OF BP13K820 RECORDS READ    : ' WS-CNT-K820-READ         
           DISPLAY 'NO OF BP13K330 RECORDS READ    : ' WS-CNT-K330-READ         
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF BP13K882 RECORDS WRITTEN : '                          
                                                    WS-CNT-K882-WRITE           
           DISPLAY 'NO OF BP13K330 RECORDS WRITTEN : '                          
                                                    WS-CNT-K330-WRITE           
           DISPLAY 'NO OF BP13F885 RECORDS WRITTEN : ' WS-CNT-F885-WRITE        
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF BP13K882 NOT WRITTEN     : '                          
                                                    WS-CNT-K882-SKIP            
           DISPLAY 'NO OF BP13K330 NOT WRITTEN     : '                          
                                                    WS-CNT-K330-SKIP            
           DISPLAY SPACES.                                                      
           DISPLAY '*******************************  '.                         
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
