       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C24L.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  07 MAY 2012.                                              
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : ALLOCATION OF FLATS.                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *                                                        *                
      *        THIS IS THE PROGRAM TO PRINT ATTENDANCE LIST    *                
      *                 FOR MGPS                               *                
      *                                                        *                
      *    INPUT FILES : BM06CTL                               *                
      *                  BM06K010                              *                
      *                  BP13K800                              *                
      *                  BP13K820                              *                
      *                  BP13K060                              *                
      *                  BP13K76D                              *                
      *                                                        *                
      *    OUTPUT      : BP13L24L                              *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DESCIPTION                      *                
      * -------- ---- -------- --------------------------------*                
      * BP134587 ESA1 20120507 NEW PROGRAM                     *                
      * BP134681 ESA1 20120919 REPLACE BP13K816 WITH BP13K813  *                
      * BP134716 ESA1 20130123 TO FIX LOOP ISSUE               *                
      * BP135190 SMR2 20131204 EXPAND BP13K813 TO 1000         *                
      * BP135561 ESA1 20150128 TO INCLUDE DJDE IN THE PROGRAM  *                
      * BP136066 ESA1 20160121 TO CATER FOR 2-ROOM FLEXI PRINTING               
      * BP136122 ESA1 20160408 CATER FOR '2F' IN READING BP13K246               
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K060-KEY-FLD                                  
                  FILE STATUS  IS WS-K060-STATUS.                               
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K800-NUM-REGN                                 
                  FILE STATUS  IS WS-K800-STATUS.                               
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K820-KEY-FLD                                  
                  FILE STATUS  IS WS-K820-STATUS.                               
           SELECT BP13K76D ASSIGN TO BP13K76D                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K76D-KEY-FLD                                  
                  FILE STATUS  IS WS-K76D-STATUS.                               
           SELECT BP13K246 ASSIGN TO BP13K246                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K246-KEY-FLD                                  
                  FILE STATUS  IS WS-K246-STATUS.                               
           SELECT BP13L24L ASSIGN TO BP13L24L.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F205                                                             
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F205.                                                           
                                                                                
       FD  BP13F200                                                             
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F200.                                                           
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS 25 CHARACTERS.                                       
       COPY BP13K060.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K76D                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K76D.                                                           
                                                                                
       FD  BP13K246                                                             
           RECORD CONTAINS 80 CHARACTERS.                                       
       COPY BP13K246.                                                           
                                                                                
       FD  BP13L24L                                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 80 CHARACTERS                                        
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC                        PIC X(80).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-K060-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K800-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K820-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K76D-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K246-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-F205-READ-CNT               PIC 9(4)   VALUE ZEROS.               
       01  WS-F200-READ-CNT               PIC 9(4)   VALUE ZEROS.               
       01  WS-K060-CNT                    PIC 9(4)   VALUE ZEROS.               
       01  WS-LINE-CNT                    PIC 99     VALUE ZEROS.               
       01  EOF-F205                       PIC X      VALUE 'N'.                 
       01  EOF-F200                       PIC X      VALUE 'N'.                 
       01  WS-K76D-FND                    PIC X      VALUE 'N'.                 
       01  WS-PREV-RM-TYP                 PIC X(2)   VALUE SPACES.              
       01  WS-PREV-NT-ZONE                PIC X(3)   VALUE SPACES.              
       01  WS-PREV-APPT-DATE              PIC 9(8)   VALUE ZEROS.               
       01  WS-PREV-APPT-TIME              PIC 9(4)   VALUE ZEROS.               
       01  WS-NAME                        PIC X(22)  VALUE SPACES.              
       01  WS-PAGE-CNT                    PIC 9999   VALUE ZEROS.               
       01  WS-PRINT-TIME                  PIC X      VALUE 'N'.                 
       01  WS-NT-ZONE-DESC                PIC X(20)  VALUE SPACES.              
       01  WS-JB                          PIC X(02)  VALUE SPACES.              
       01  WS-LF                          PIC X(02)  VALUE SPACES.              
       01  WS-PROJECT-NAME                PIC X(32)  VALUE SPACES.              
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(50).                          
           05 WS-PARSED-STRING              PIC X(50).                          
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
       01  WS-DTE-ALLOC.                                                        
           05 WS-DTE-DD                   PIC 99.                               
           05 FILLER                      PIC X   VALUE '/'.                    
           05 WS-DTE-MM                   PIC 99.                               
           05 FILLER                      PIC X   VALUE '/'.                    
           05 WS-DTE-CC                   PIC 99.                               
           05 WS-DTE-YY                   PIC 99.                               
                                                                                
       01  WS-CURRENT-DATE                PIC X(8).                             
                                                                                
       01  WS-TIME.                                                             
           05 WS-HH                       PIC 99.                               
           05 WS-MM                       PIC 99.                               
                                                                                
       01  WS-DTE-APPT.                                                         
           05 WS-DTE-CC                   PIC 99.                               
           05 WS-DTE-YY                   PIC 99.                               
           05 WS-DTE-MM                   PIC 99.                               
           05 WS-DTE-DD                   PIC 99.                               
                                                                                
       01  WS-ALLOC-NT-ZONE.                                                    
           05 WS-ALLOC-DATE               PIC 9(8).                             
           05 WS-NT-ZONE                  PIC X(3).                             
           05 WS-FT                       PIC X(2).                             
                                                                                
      *-----------------------------------------------------------*             
      *       LINKAGE FOR SUB-PROGRAM BP13C910                    *             
      *-----------------------------------------------------------*             
       01  WS-LINK-REC-2.                                                       
           05  WS-LINK-ZONE               PIC X(03).                            
       01  WS-BP13K813-REC                PIC X(1000).                          
       01  WS-ZONE-FND-FLAG               PIC X(01).                            
       01  WS-RETURN-CODE                 PIC 9(02).                            
                                                                                
       COPY BP13K813.                                                           
                                                                                
       01  DJDE-LINE1                      PIC X(80) VALUE                      
           '$DJDE$ JDE=13C230,JDL=BP13,NUMBER=(0000,63,80),END;'.               
                                                                                
      *============================================================*            
      *            P R I N T    L A Y O U T                        *            
      *============================================================*            
                                                                                
       01  L24LA-HEADER-1.                                                      
           05  FILLER                     PIC X(05) VALUE                       
               'PAGE '.                                                         
           05  L24LA-PAGE                 PIC ZZZ9.                             
           05  FILLER                     PIC X(40) VALUE SPACES.               
           05  FILLER                     PIC X(20) VALUE                       
               '             DATE : '.                                          
           05  FILLER.                                                          
               10  L24LA-RUN-DD           PIC X(2).                             
               10  FILLER                 PIC X(1)  VALUE '/'.                  
               10  L24LA-RUN-MM           PIC X(2).                             
               10  FILLER                 PIC X(1)  VALUE '/'.                  
               10  L24LA-RUN-CY           PIC X(4).                             
                                                                                
       01  L24LA-HEADER-2.                                                      
           05  FILLER                     PIC X(13) VALUE SPACES.               
           05  FILLER                     PIC X(55) VALUE                       
              'ATTENDANCE LIST FOR PRIORITY AND PUBLIC SELECTION(MGPS)'.        
           05  FILLER                     PIC X(05) VALUE SPACES.               
           05  FILLER                     PIC X(04) VALUE 'CAT3'.               
                                                                                
       01  L24LA-HEADER-3.                                                      
           05  FILLER                     PIC X(13)  VALUE SPACES.              
           05  FILLER                     PIC X(49)  VALUE ALL '-'.             
                                                                                
       01  L24LA-HEADER-3A.                                                     
           05  FILLER                     PIC X(19)  VALUE                      
               'BALLOT QUARTER   : '.                                           
           05  L24LA-BALLOT-QTR           PIC X(07).                            
                                                                                
       01  L24LA-HEADER-4.                                                      
           05  L24LA-NT-ZONE-TXT          PIC X(8)  VALUE SPACES.               
           05  FILLER                     PIC X(11) VALUE                       
               '         : '.                                                   
           05  L24LA-NEWTOWN-ZONE         PIC X(25).                            
           05  L24LA-PROJECT-NAME         PIC X(32).                            
                                                                                
       01  L24LA-HEADER-5.                                                      
           05  FILLER                     PIC X(19)  VALUE                      
               'ROOM-TYPE        : '.                                           
           05  L24LA-ROOMTYPE             PIC X(21).                            
                                                                                
       01  L24LA-HEADER-6.                                                      
           05  FILLER                     PIC X(19)  VALUE                      
               'APPOINTMENT DATE : '.                                           
           05  L24LA-APPTDATE             PIC X(10).                            
                                                                                
       01  L24LA-HEADER-7.                                                      
           05  FILLER                     PIC X(19)  VALUE                      
               'APPOINTMENT TIME : '.                                           
           05  L24LA-APPT-HH              PIC Z9.                               
           05  FILLER                     PIC X      VALUE '.'.                 
           05  L24LA-APPT-MM              PIC 99.                               
           05  FILLER                     PIC X      VALUE SPACE.               
           05  L24LA-AM-PM                PIC X(02).                            
                                                                                
       01  L24LA-HEADER-8.                                                      
           05  FILLER                     PIC X(48)  VALUE                      
               'REGN NO                 ALSCH NAME'.                            
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  FILLER                     PIC X(27)  VALUE                      
               'CAT  TIME             TIME'.                                    
                                                                                
       01  L24LA-HEADER-8-ZONE.                                                 
           05  FILLER                     PIC X(48)  VALUE                      
               'QUEUE NO/HT/REG NO/     ALSCH NAME'.                            
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  FILLER                     PIC X(27)  VALUE                      
               'CAT  TIME             TIME'.                                    
                                                                                
       01  L24LA-HEADER-9.                                                      
           05  FILLER                     PIC X(08)  VALUE                      
               'QUEUE NO'.                                                      
           05  FILLER                     PIC X(50)  VALUE SPACES.              
           05  FILLER                     PIC X(4)   VALUE                      
               ' IN '.                                                          
           05  FILLER                     PIC X(2)   VALUE SPACES.              
           05  FILLER                     PIC X(9)   VALUE                      
               ' REMARKS '.                                                     
           05  FILLER                     PIC X(3)   VALUE SPACES.              
           05  FILLER                     PIC X(04)  VALUE                      
               'OUT '.                                                          
                                                                                
       01  L24LA-HEADER-10.                                                     
           05  L24LA-ULINE1               PIC X(23)  VALUE                      
               '--------               '.                                       
           05  FILLER                     PIC X(30)  VALUE                      
               ' ----- ----'.                                                   
           05  FILLER                     PIC X(27)  VALUE                      
               '---  ----  ---------  ----'.                                    
                                                                                
       01  L24LA-SUB-HEADER-1.                                                  
           05  FILLER                     PIC X(19)  VALUE                      
               'APPOINTMENT TIME : '.                                           
           05  L24LA-APPT-HH2             PIC Z9.                               
           05  FILLER                     PIC X      VALUE '.'.                 
           05  L24LA-APPT-MM2             PIC 99.                               
           05  FILLER                     PIC X      VALUE SPACE.               
           05  L24LA-AM-PM2               PIC X(02).                            
                                                                                
       01  L24LA-DETAIL-LINE-1.                                                 
           05  L24LA-NT-FORMAT.                                                 
               10  L24LA-REGN-NO-NT       PIC X(8).                             
               10  FILLER                 PIC X(1).                             
               10  L24LA-FT               PIC X(6).                             
               10  L24LA-JB-LF            PIC X(6).                             
               10  FILLER                 PIC X(2).                             
           05  L24LA-ZONE-FORMAT REDEFINES L24LA-NT-FORMAT.                     
               10  L24LA-QUEUE-NO         PIC X(5).                             
               10  L24LA-SLASH-1          PIC X(1).                             
               10  L24LA-HT               PIC X(1).                             
               10  L24LA-SLASH-2          PIC X(1).                             
               10  L24LA-SEQ-NO           PIC X(7).                             
               10  L24LA-SLASH-3          PIC X(1).                             
               10  FILLER-1               PIC X(7).                             
           05  L24LA-ZONE-FORMAT2 REDEFINES L24LA-ZONE-FORMAT.                  
               10  L24LA-QUEUE-NO-A       PIC X(5).                             
               10  L24LA-SLASH-1A         PIC X(1).                             
               10  L24LA-HT-A             PIC X(1).                             
               10  L24LA-SLASH-2A         PIC X(1).                             
               10  L24LA-JB-QUEUE-SEQ     PIC X(7).                             
               10  L24LA-SLASH-3A         PIC X(1).                             
               10  L24LA-SEQ-NO-A         PIC X(7).                             
           05  FILLER                     PIC X      VALUE SPACES.              
           05  L24LA-LEFT-PAR             PIC X      VALUE SPACES.              
           05  L24LA-ALLOC-SCH            PIC X(3)   VALUE SPACES.              
           05  L24LA-RIGHT-PAR            PIC X      VALUE SPACES.              
           05  FILLER                     PIC X(1)   VALUE SPACES.              
           05  L24LA-APPL-NAME            PIC X(22)  VALUE SPACES.              
           05  FILLER                     PIC X(2)   VALUE SPACES.              
           05  L24LA-CAT                  PIC X      VALUE SPACES.              
           05  FILLER                     PIC X(13)  VALUE SPACES.              
           05  L24LA-REMARKS              PIC X      VALUE SPACES.              
           05  FILLER                     PIC X(05)  VALUE SPACES.              
                                                                                
       01  L24LA-DETAIL-LINE-2.                                                 
           05  L24LA-QUEUE-NO-2           PIC X(05)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L24LA-REGN-NO-ZONE         PIC X(08)  VALUE SPACES.              
           05  FILLER                     PIC X(14)  VALUE SPACES.              
           05  L24LA-COAPPL-NAME          PIC X(22)  VALUE SPACES.              
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  L24LA-MGPS-REMARKS         PIC X(09)  VALUE SPACES.              
                                                                                
       01  L24LA-UNDERLINE.                                                     
           05  FILLER                     PIC X(80)  VALUE ALL '-'.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F205 THRU 200-EXIT                                  
             UNTIL EOF-F205 = 'Y'.                                              
           PERFORM 999-CLOSE-FILES.                                             
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
           OPEN INPUT  BP13F205                                                 
                       BP13K060                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K76D                                                 
                       BP13K246                                                 
                OUTPUT BP13L24L.                                                
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K060 ' WS-K060-STATUS                
              MOVE WS-K060-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K800 ' WS-K800-STATUS                
              MOVE WS-K800-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K820 ' WS-K820-STATUS                
              MOVE WS-K820-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           IF WS-K76D-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K76D ' WS-K76D-STATUS                
              MOVE WS-K76D-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           IF WS-K246-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K246 ' WS-K246-STATUS                
              MOVE WS-K246-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           CALL 'OP13K813'         USING WS-RETURN-CODE.                        
           IF WS-RETURN-CODE NOT = ZEROS AND 97                                 
              DISPLAY 'OPEN ERROR, BP13K813 STATUS ' WS-RETURN-CODE             
              MOVE WS-RETURN-CODE TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           MOVE   FUNCTION CURRENT-DATE   TO WS-CURRENT-DATE.                   
           MOVE   WS-CURRENT-DATE (7:2)   TO L24LA-RUN-DD.                      
           MOVE   WS-CURRENT-DATE (5:2)   TO L24LA-RUN-MM.                      
           MOVE   WS-CURRENT-DATE (1:4)   TO L24LA-RUN-CY.                      
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F205.                                                           
           READ BP13F205 AT END MOVE 'Y' TO EOF-F205                            
                            GO  TO 200-EXIT.                                    
                                                                                
           IF F205-NUM-GRO NOT = 'P'                                            
              GO TO 200-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              MOVE 'NEW TOWN'          TO L24LA-NT-ZONE-TXT                     
           ELSE                                                                 
              MOVE 'ZONE'              TO L24LA-NT-ZONE-TXT                     
              MOVE ALL '-'             TO L24LA-ULINE1                          
              MOVE '/'                 TO L24LA-SLASH-1                         
                                          L24LA-SLASH-1A                        
                                          L24LA-SLASH-2                         
                                          L24LA-SLASH-2A                        
                                          L24LA-SLASH-3                         
                                          L24LA-SLASH-3A                        
           END-IF.                                                              
                                                                                
           MOVE F205-NUM-NT-ZONE   TO WS-NT-ZONE.                               
           MOVE F205-DTE-ALLOCN    TO WS-ALLOC-DATE.                            
           MOVE F205-NUM-FLAT-TYPE TO WS-FT.                                    
           MOVE 'N'                TO EOF-F205.                                 
           ADD  1                  TO WS-F205-READ-CNT.                         
           PERFORM 300-PROCESS-F200 THRU 300-EXIT.                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       210-READ-F200.                                                           
           READ BP13F200 AT END MOVE 'Y' TO EOF-F200                            
                             GO TO 210-EXIT.                                    
                                                                                
           ADD  1               TO WS-F200-READ-CNT.                            
           MOVE 'N'             TO EOF-F200.                                    
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-F200.                                                        
           MOVE 'N' TO EOF-F200.                                                
           OPEN INPUT BP13F200.                                                 
           PERFORM 210-READ-F200 THRU 210-EXIT.                                 
           IF EOF-F200 = 'Y'                                                    
              GO TO 300-EXIT                                                    
           END-IF.                                                              
           PERFORM 400-CHECK-F200 THRU 400-EXIT                                 
             UNTIL EOF-F200 = 'Y'.                                              
                                                                                
           CLOSE BP13F200.                                                      
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-CHECK-F200.                                                          
           IF (F200-DTE-BKAPPMT-DATE-MGPS > WS-CURRENT-DATE)                    
              PERFORM 500-READ-K800     THRU 500-EXIT                           
              PERFORM 600-READ-K060     THRU 600-EXIT                           
              PERFORM 800-PROCESS-PRINT THRU 800-EXIT                           
              PERFORM 900-PRINT-DETAIL  THRU 900-EXIT                           
              MOVE F200-DTE-BKAPPMT-DATE-MGPS TO WS-PREV-APPT-DATE              
              MOVE F200-TME-BKAPPMT-MGPS  TO WS-PREV-APPT-TIME                  
           END-IF.                                                              
                                                                                
           PERFORM 210-READ-F200 THRU 210-EXIT.                                 
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-READ-K800.                                                           
                                                                                
           MOVE SPACES                TO BP13K800-MASTER.                       
                                                                                
           MOVE F200-NUM-REGN         TO K800-NUM-REGN.                         
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              PERFORM 550-READ-K820 THRU 550-EXIT                               
           ELSE                                                                 
           IF WS-K800-STATUS = 23                                               
              MOVE SPACES TO L24LA-APPL-NAME                                    
                             L24LA-COAPPL-NAME                                  
              DISPLAY 'RECORD NOT FOUND IN BP13K800 ' WS-K800-STATUS            
              DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                          
              GO TO 500-EXIT                                                    
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K800 ' WS-K800-STATUS                  
              GO TO 999-CLOSE-FILES.                                            
                                                                                
        500-EXIT.                                                               
            EXIT.                                                               
                                                                                
        550-READ-K820.                                                          
                                                                                
            MOVE K800-NUM-REGN     TO K820-NUM-REGN.                            
            MOVE K800-NUM-NRIC1    TO K820-NUM-NRIC.                            
            PERFORM 560-ACCESS-K820 THRU 560-EXIT.                              
            MOVE WS-NAME           TO L24LA-APPL-NAME                           
                                                                                
            IF (K800-NUM-NRIC2(2:1) NOT = '#') AND                              
               (K800-NUM-NRIC2      NOT = SPACES AND LOW-VALUES)                
               MOVE K800-NUM-REGN  TO K820-NUM-REGN                             
               MOVE K800-NUM-NRIC2 TO K820-NUM-NRIC                             
               PERFORM 560-ACCESS-K820 THRU 560-EXIT                            
               MOVE WS-NAME        TO L24LA-COAPPL-NAME                         
            ELSE                                                                
               MOVE SPACES         TO L24LA-COAPPL-NAME.                        
                                                                                
        550-EXIT.                                                               
            EXIT.                                                               
                                                                                
        560-ACCESS-K820.                                                        
                                                                                
            READ BP13K820 KEY IS K820-KEY-FLD.                                  
                                                                                
            IF WS-K820-STATUS = 00                                              
               MOVE K820-NME-OCCP   TO WS-NAME                                  
            ELSE                                                                
            IF WS-K820-STATUS = 23                                              
               MOVE SPACES          TO WS-NAME                                  
               DISPLAY 'RECORD NOT FOUND IN K820 ' WS-K820-STATUS               
               DISPLAY 'K820-KEY-FLD = ' K820-KEY-FLD                           
           ELSE                                                                 
               DISPLAY 'ERROR READING BP13K820 ' WS-K820-STATUS                 
               DISPLAY 'K820-KEY-FLD = ' K820-KEY-FLD                           
               GO TO 999-CLOSE-FILES.                                           
                                                                                
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-READ-K060.                                                           
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              MOVE 02                  TO K060-SERIAL-NO                        
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW = 'Z'                                         
              MOVE 45                  TO K060-SERIAL-NO                        
           END-IF.                                                              
                                                                                
           IF F200-NUM-SELTYPE = 'BTO'                                          
              PERFORM 700-CALL-BP13C910 THRU 700-EXIT                           
              MOVE K813-NME-SITE        TO   WS-NT-ZONE-DESC                    
              STRING '(' K813-NME-PROJECT-LTR ')'                               
                     DELIMITED BY SIZE INTO WS-PROJECT-NAME                     
           ELSE                                                                 
              MOVE SPACES               TO   WS-PROJECT-NAME                    
              MOVE F200-NUM-NT-ZONE    TO K060-CODE                             
              READ BP13K060                                                     
              EVALUATE WS-K060-STATUS                                           
                 WHEN 00                                                        
                   MOVE K060-DESC        TO WS-NT-ZONE-DESC                     
                   ADD 1                 TO WS-K060-CNT                         
                 WHEN 23                                                        
                   MOVE SPACES           TO WS-NT-ZONE-DESC                     
                   DISPLAY 'REC NOT FOUND IN BP13K060 ' WS-K060-STATUS          
                   DISPLAY 'K060-KEY-FLD = ' K060-KEY-FLD                       
                 WHEN OTHER                                                     
                   DISPLAY 'ERROR READING BP13K060 ' WS-K060-STATUS             
                   DISPLAY 'K060-KEY-FLD = ' K060-KEY-FLD                       
                   GO TO 999-CLOSE-FILES                                        
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-CALL-BP13C910.                                                       
                                                                                
           MOVE F200-NUM-NT-ZONE       TO WS-LINK-ZONE.                         
           CALL 'BP13C910' USING WS-LINK-REC-2,                                 
                                 WS-BP13K813-REC,                               
                                 WS-ZONE-FND-FLAG.                              
           IF WS-ZONE-FND-FLAG = 'Y'                                            
              MOVE WS-BP13K813-REC TO BP13K813-REC                              
           ELSE                                                                 
              MOVE SPACES          TO BP13K813-REC                              
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-PROCESS-PRINT.                                                       
           IF (WS-LINE-CNT               = 0)              OR                   
              (F200-DTE-BKAPPMT-DATE-MGPS NOT = WS-PREV-APPT-DATE)              
              PERFORM 850-PRINT-HEADING  THRU 850-EXIT                          
              MOVE 10                      TO WS-LINE-CNT.                      
                                                                                
           IF F200-TME-BKAPPMT-MGPS NOT = WS-PREV-APPT-TIME                     
              MOVE 'Y' TO WS-PRINT-TIME.                                        
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
       850-PRINT-HEADING.                                                       
                                                                                
           ADD  1            TO WS-PAGE-CNT.                                    
           MOVE WS-PAGE-CNT  TO L24LA-PAGE.                                     
           MOVE 'Y'          TO WS-PRINT-TIME.                                  
           MOVE L24LA-PAGE   TO DJDE-LINE1(36:4).                               
           WRITE PRINT-REC FROM DJDE-LINE1 AFTER PAGE.                          
           WRITE PRINT-REC FROM L24LA-HEADER-1.                                 
           WRITE PRINT-REC FROM L24LA-HEADER-2  AFTER 1.                        
           WRITE PRINT-REC FROM L24LA-HEADER-3 AFTER 1.                         
           MOVE SPACES       TO PRINT-REC.                                      
           WRITE PRINT-REC AFTER 1.                                             
           MOVE F200-DTE-BKAPPMT-DATE-MGPS TO WS-DTE-APPT.                      
           STRING F200-DTE-BALLOT(5:2) '/' F200-DTE-BALLOT(1:4)                 
                  DELIMITED BY SIZE INTO L24LA-BALLOT-QTR.                      
           WRITE PRINT-REC  FROM L24LA-HEADER-3A AFTER 2.                       
           MOVE CORR WS-DTE-APPT           TO WS-DTE-ALLOC.                     
           MOVE WS-DTE-ALLOC               TO L24LA-APPTDATE.                   
           MOVE WS-NT-ZONE-DESC            TO L24LA-NEWTOWN-ZONE.               
           MOVE WS-PROJECT-NAME            TO L24LA-PROJECT-NAME.               
           IF F200-NUM-SELTYPE = 'BTO'                                          
              IF K813-NUM-FLAT-TYPE = '4P' OR '5P'                              
                 STRING K813-NUM-FLAT-TYPE (1:1) '-ROOM'                        
                        ' (PAIRED UNITS)'                                       
                        DELIMITED BY SIZE                                       
                        INTO L24LA-ROOMTYPE                                     
              ELSE                                                              
                  IF K813-NUM-FLAT-TYPE = '2F'                                  
                     MOVE '2-ROOM FLEXI FLAT'  TO L24LA-ROOMTYPE                
                  ELSE                                                          
                     STRING K813-NUM-FLAT-TYPE (1:1) '-ROOM'                    
                            DELIMITED BY SIZE                                   
                            INTO L24LA-ROOMTYPE                                 
                  END-IF                                                        
              END-IF                                                            
           ELSE                                                                 
           IF F200-NUM-FLAT-TYPE = 'SE'                                         
      **      MOVE F200-NUM-ACTUAL-FT TO L24LA-ROOMTYPE                         
              MOVE 'SE'     TO L24LA-ROOMTYPE                                   
           ELSE                                                                 
           IF F200-NUM-FLAT-TYPE = '4D'                                         
              MOVE 'TYPE-A' TO L24LA-ROOMTYPE                                   
           ELSE                                                                 
           IF F200-NUM-FLAT-TYPE = '5D'                                         
              MOVE 'TYPE-B' TO L24LA-ROOMTYPE                                   
           ELSE                                                                 
           IF F200-NUM-FLAT-TYPE = 'ED'                                         
              MOVE 'TYPE-C' TO L24LA-ROOMTYPE                                   
           ELSE                                                                 
           IF F200-NUM-FLAT-TYPE = 'E '                                         
              MOVE ' EXEC ' TO L24LA-ROOMTYPE                                   
           ELSE                                                                 
              MOVE F200-NUM-FLAT-TYPE  TO L24LA-ROOMTYPE.                       
                                                                                
           IF F200-NUM-SELTYPE = 'SBF'                                          
              PERFORM 920-READ-K246    THRU 920-EXIT                            
           END-IF.                                                              
           WRITE PRINT-REC  FROM L24LA-HEADER-4 AFTER 1.                        
           WRITE PRINT-REC  FROM L24LA-HEADER-5 AFTER 1.                        
           WRITE PRINT-REC  FROM L24LA-HEADER-6 AFTER 1.                        
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              WRITE PRINT-REC  FROM L24LA-HEADER-8  AFTER 2                     
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW = 'Z'                                         
              WRITE PRINT-REC  FROM L24LA-HEADER-8-ZONE AFTER 2                 
           END-IF.                                                              
           WRITE PRINT-REC  FROM L24LA-HEADER-9 AFTER 1.                        
           WRITE PRINT-REC  FROM L24LA-HEADER-10 AFTER 1.                       
           MOVE SPACES      TO   PRINT-REC.                                     
           WRITE PRINT-REC                       AFTER 1.                       
           MOVE SPACES      TO   PRINT-REC.                                     
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-PRINT-DETAIL.                                                        
                                                                                
           IF WS-PRINT-TIME = 'Y'                                               
              MOVE F200-TME-BKAPPMT-MGPS TO WS-TIME                             
              PERFORM 905-TIME-CHECK THRU 905-EXIT                              
              MOVE WS-HH             TO L24LA-APPT-HH2                          
              MOVE WS-MM             TO L24LA-APPT-MM2                          
              ADD  1                 TO WS-LINE-CNT                             
              WRITE PRINT-REC FROM L24LA-SUB-HEADER-1 AFTER 1                   
              MOVE SPACES            TO PRINT-REC                               
              WRITE PRINT-REC AFTER 1                                           
              MOVE 'N'               TO WS-PRINT-TIME.                          
                                                                                
           MOVE K800-NUM-CAT         TO L24LA-CAT.                              
                                                                                
           PERFORM 910-READ-BP13K76D THRU 910-EXIT.                             
           IF WS-K76D-FND = 'Y'                                                 
              MOVE 'B'               TO L24LA-REMARKS                           
           ELSE                                                                 
              MOVE SPACE             TO L24LA-REMARKS                           
           END-IF.                                                              
                                                                                
           IF K800-NUM-ALLO-SCHEME = 'PA'                                       
              MOVE SPACES               TO L24LA-LEFT-PAR                       
              MOVE SPACES               TO L24LA-ALLOC-SCH                      
              MOVE SPACES               TO L24LA-RIGHT-PAR                      
           ELSE                                                                 
              MOVE '('                  TO L24LA-LEFT-PAR                       
              MOVE K800-NUM-ALLO-SCHEME TO L24LA-ALLOC-SCH                      
              MOVE ')'                  TO L24LA-RIGHT-PAR.             .       
                                                                                
           STRING '('  F200-NUM-MGPS-TAG ')'                                    
              DELIMITED BY SIZE INTO L24LA-MGPS-REMARKS.                        
                                                                                
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              MOVE F200-NUM-REGN           TO L24LA-REGN-NO-NT                  
              MOVE F200-NUM-MGPS-QUEUE-SERIAL TO L24LA-QUEUE-NO-2               
              IF F200-NUM-FLAT-TYPE = 'SE'                                      
                 EVALUATE K800-NUM-FLAT-TYPE                                    
                   WHEN '2 '                                                    
                     MOVE '(2-RM)' TO L24LA-FT                                  
                   WHEN '3 '                                                    
                     MOVE '(3-RM)' TO L24LA-FT                                  
                   WHEN '4 '                                                    
                     MOVE '(4-RM)' TO L24LA-FT                                  
                   WHEN '5 '                                                    
                     MOVE '(5-RM)' TO L24LA-FT                                  
                 END-EVALUATE                                                   
                 PERFORM 901-CHECK-JB-LF          THRU 901-EXIT                 
              ELSE                                                              
                 MOVE SPACES TO L24LA-FT                                        
              END-IF                                                            
              WRITE PRINT-REC FROM L24LA-DETAIL-LINE-1 AFTER 1                  
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW = 'Z'                                         
              IF F200-NUM-JB-QUEUE-SEQUENCE = SPACES OR LOW-VALUES              
                 MOVE F200-NUM-MGPS-QUEUE-SERIAL TO L24LA-QUEUE-NO              
                 EVALUATE F200-NUM-MGPS-QUEUE-ALPHA                             
                   WHEN 'H'                                                     
                        MOVE 'F' TO L24LA-HT                                    
                   WHEN 'G'                                                     
                        MOVE 'U' TO L24LA-HT                                    
                   WHEN 'C'                                                     
                        MOVE 'G' TO L24LA-HT                                    
                   WHEN OTHER                                                   
                        MOVE F200-NUM-MGPS-QUEUE-ALPHA TO L24LA-HT              
                 END-EVALUATE                                                   
                 MOVE F200-NUM-SEQUENCE          TO L24LA-SEQ-NO                
                 MOVE '/'                        TO L24LA-SLASH-3               
                 MOVE SPACES                     TO FILLER-1                    
              ELSE                                                              
                 MOVE F200-NUM-MGPS-QUEUE-SERIAL TO L24LA-QUEUE-NO-A            
                 EVALUATE F200-NUM-MGPS-QUEUE-ALPHA                             
                   WHEN 'H'                                                     
                        MOVE 'F' TO L24LA-HT-A                                  
                   WHEN 'G'                                                     
                        MOVE 'U' TO L24LA-HT-A                                  
                   WHEN 'C'                                                     
                        MOVE 'G' TO L24LA-HT-A                                  
                   WHEN OTHER                                                   
                        MOVE F200-NUM-MGPS-QUEUE-ALPHA TO L24LA-HT-A            
                 END-EVALUATE                                                   
                 MOVE F200-NUM-SEQUENCE          TO L24LA-SEQ-NO-A              
                 MOVE '/'                        TO L24LA-SLASH-3A              
                 MOVE F200-NUM-JB-QUEUE-SEQUENCE TO L24LA-JB-QUEUE-SEQ          
              END-IF                                                            
              MOVE F200-NUM-REGN              TO L24LA-REGN-NO-ZONE             
              WRITE PRINT-REC FROM L24LA-DETAIL-LINE-1 AFTER 1                  
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC FROM L24LA-DETAIL-LINE-2 AFTER 1.                    
           WRITE PRINT-REC FROM L24LA-UNDERLINE     AFTER 1.                    
                                                                                
           ADD 3                        TO WS-LINE-CNT.                         
           IF WS-LINE-CNT > 42                                                  
              MOVE 0                    TO WS-LINE-CNT.                         
                                                                                
        900-EXIT.                                                               
            EXIT.                                                               
                                                                                
        901-CHECK-JB-LF.                                                        
                                                                                
            MOVE SPACES       TO WS-JB                                          
                                 WS-LF                                          
                                 L24LA-JB-LF.                                   
                                                                                
            IF K800-NUM-JT-BALLOT-MTF = 8                                       
               MOVE 'JB'      TO WS-JB                                          
            END-IF.                                                             
                                                                                
            IF K800-NUM-LOW-FLOOR = 'G' OR 'Y'                                  
               MOVE K800-NUM-LOW-FLOOR   TO WS-LF                               
            END-IF.                                                             
                                                                                
            IF WS-JB NOT = SPACES AND                                           
               WS-LF NOT = SPACES                                               
               STRING '/'  WS-JB                                                
                      '/'  WS-LF                                                
                      DELIMITED BY SIZE INTO L24LA-JB-LF                        
               END-STRING                                                       
            END-IF.                                                             
                                                                                
            IF WS-JB NOT = SPACES AND                                           
               WS-LF = SPACES                                                   
               STRING '/'  WS-JB                                                
                      DELIMITED BY SIZE INTO L24LA-JB-LF                        
               END-STRING                                                       
            END-IF.                                                             
                                                                                
            IF WS-JB = SPACES AND                                               
               WS-LF NOT = SPACES                                               
               STRING '/'  WS-LF                                                
                      DELIMITED BY SIZE INTO L24LA-JB-LF                        
               END-STRING                                                       
            END-IF.                                                             
                                                                                
        901-EXIT.                                                               
            EXIT.                                                               
                                                                                
        905-TIME-CHECK.                                                         
                                                                                
           IF WS-HH = 12                                                        
              MOVE 'PM'           TO L24LA-AM-PM2                               
           ELSE                                                                 
           IF WS-HH < 12                                                        
              MOVE 'AM'           TO L24LA-AM-PM2                               
           ELSE                                                                 
              MOVE 'PM'           TO L24LA-AM-PM2                               
              SUBTRACT 12 FROM WS-HH.                                           
                                                                                
        905-EXIT.                                                               
            EXIT.                                                               
                                                                                
        910-READ-BP13K76D.                                                      
                                                                                
           MOVE F200-NUM-SELTYPE       TO K76D-NUM-SALES-MODE.                  
           MOVE F200-DTE-BALLOT        TO K76D-NUM-DTE-BALLOT.                  
           MOVE F200-NUM-REGN          TO K76D-NUM-REGN.                        
                                                                                
           READ BP13K76D.                                                       
           EVALUATE WS-K76D-STATUS                                              
             WHEN   00                                                          
                    MOVE 'Y'      TO WS-K76D-FND                                
             WHEN   23                                                          
                    MOVE 'N'      TO WS-K76D-FND                                
             WHEN   OTHER                                                       
                    DISPLAY 'READ BP13K76D ERROR ' K76D-NUM-REGN                
                    DISPLAY 'STATUS ' WS-K800-STATUS                            
                    MOVE WS-K76D-STATUS TO RETURN-CODE                          
                    PERFORM 999-CLOSE-FILES                                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
        910-EXIT.                                                               
           EXIT.                                                                
                                                                                
        920-READ-K246.                                                          
                                                                                
           EVALUATE F200-NUM-FLAT-TYPE                                          
             WHEN '1A'                                                          
             WHEN '2A'                                                          
              MOVE 'SA       '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '2 '                                                          
             WHEN '2F'                                                          
              MOVE '2-ROOM   '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '3 '                                                          
              MOVE '3-ROOM   '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '4 '                                                          
              MOVE '4-ROOM   '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '5 '                                                          
              MOVE '5-ROOM   '        TO  K246-NUM-FLAT-TYPE                    
           END-EVALUATE.                                                        
           MOVE K060-DESC             TO  K246-NUM-NEW-TOWN.                    
           MOVE F200-DTE-BALLOT       TO  K246-DTE-BALLOT.                      
                                                                                
           READ BP13K246.                                                       
                                                                                
           IF WS-K246-STATUS = 00                                               
              IF K246-COMBINED-NT NOT = SPACES AND LOW-VALUES                   
                 IF K246-NUM-NT-TAG = 'Y'                                       
                    STRING K246-NUM-NEW-TOWN '/ '                               
                           K246-COMBINED-NT                                     
                           DELIMITED BY SIZE INTO WS-STRING-TO-PARSE            
                    END-STRING                                                  
                    MOVE LENGTH OF WS-STRING-TO-PARSE                           
                                      TO WS-STRING-LENGTH                       
                    MOVE 'Y'          TO WS-INSERT-SPACE-FLAG                   
                    PERFORM 930-PARSE-STRING    THRU 930-EXIT                   
                    MOVE WS-PARSED-STRING   TO  L24LA-NEWTOWN-ZONE              
                 ELSE                                                           
                    STRING K246-COMBINED-NT '/ '                                
                           K246-NUM-NEW-TOWN                                    
                           DELIMITED BY SIZE INTO WS-STRING-TO-PARSE            
                    END-STRING                                                  
                    MOVE LENGTH OF WS-STRING-TO-PARSE                           
                                      TO WS-STRING-LENGTH                       
                    MOVE 'Y'          TO WS-INSERT-SPACE-FLAG                   
                    PERFORM 930-PARSE-STRING    THRU 930-EXIT                   
                    MOVE WS-PARSED-STRING   TO  L24LA-NEWTOWN-ZONE              
                 END-IF                                                         
              END-IF                                                            
              IF K246-COMBINED-FT NOT = SPACES AND LOW-VALUES                   
                 IF K246-NUM-FT-TAG = 'N'                                       
                    STRING K246-COMBINED-FT(1:1) '/'                            
                           K246-NUM-FLAT-TYPE(1:1)                              
                           DELIMITED BY SIZE                                    
                           INTO  L24LA-ROOMTYPE                                 
                    END-STRING                                                  
                 ELSE                                                           
                    STRING K246-NUM-FLAT-TYPE(1:1) '/'                          
                           K246-COMBINED-FT(1:1)                                
                           DELIMITED BY SIZE INTO L24LA-ROOMTYPE                
                    END-STRING                                                  
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF WS-K246-STATUS = 23                                            
                 DISPLAY 'REC NOT FOUND IN BP13K246 ' K246-KEY-FLD              
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K246 ' WS-K246-STATUS               
                 DISPLAY 'K246-KEY-FLD = ' K246-KEY-FLD                         
                 PERFORM 999-CLOSE-FILES                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
        920-EXIT.                                                               
            EXIT.                                                               
                                                                                
        930-PARSE-STRING.                                                       
                                                                                
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 940-GET-CHAR           THRU 940-EXIT                         
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
                                                                                
        930-EXIT.                                                               
            EXIT.                                                               
                                                                                
        940-GET-CHAR.                                                           
                                                                                
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
        940-EXIT.                                                               
            EXIT.                                                               
        999-CLOSE-FILES.                                                        
           CLOSE BP13F205                                                       
                 BP13K060                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K76D                                                       
                 BP13K246                                                       
                 BP13L24L.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========END OF BP13C24L============*'.                  
           DISPLAY SPACES.                                                      
           DISPLAY '   NO. OF F205 RECORDS READ = ' WS-F205-READ-CNT.           
           DISPLAY '   NO. OF F200 RECORDS READ = ' WS-F200-READ-CNT.           
           DISPLAY '   NO. OF K060 RECORDS READ = ' WS-K060-CNT.                
           DISPLAY SPACES.                                                      
           DISPLAY '*======================================*'.                  
                                                                                
           IF WS-K060-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K060  CLOSE ERROR ' WS-K060-STATUS.             
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K800  CLOSE ERROR ' WS-K800-STATUS.             
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K820  CLOSE ERROR ' WS-K820-STATUS.             
           IF WS-K76D-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K76D  CLOSE ERROR ' WS-K76D-STATUS.             
           IF WS-K246-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K246  CLOSE ERROR ' WS-K246-STATUS.             
                                                                                
           CALL 'CP13K813'         USING WS-RETURN-CODE.                        
           IF WS-RETURN-CODE NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING BP13K813 - ' WS-RETURN-CODE                
              MOVE WS-RETURN-CODE TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
