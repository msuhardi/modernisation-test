       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C25Y.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  11 MAY 2016.                                              
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                             
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO CATER FOR COMBINED FLAT-TYPE AND COMBINED   *            
      *             NEW-TOWN IN BM06F120.                          *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP136205  ESA1  20160511  NEW PROGRAM                      *            
      * BP137034  ESA1  20171106  CATER FOR 3P IN NON-MATURE ONLY  *            
      * BP136991  ESA1  20170919  TO CATER FOR MODEL-DSGN = '98'   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BM06F120  ASSIGN        TO BM06F120.                          
                                                                                
           SELECT BP13K813  ASSIGN        TO BP13K813                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K813-KEY-FLD                       
                            FILE STATUS   IS BP13K813-STATUS.                   
                                                                                
           SELECT BP13K246  ASSIGN        TO BP13K246                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K246-KEY-FLD                       
                            FILE STATUS   IS BP13K246-STATUS.                   
                                                                                
           SELECT BP13K060  ASSIGN        TO BP13K060                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K060-KEY-FLD                       
                            FILE STATUS   IS BP13K060-STATUS.                   
                                                                                
           SELECT M06F120A  ASSIGN        TO M06F120A.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BM06F120                                                             
           RECORD CONTAINS 700 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BM06F120.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD  BP13K246                                                             
           RECORD CONTAINS 80 CHARACTERS.                                       
       COPY BP13K246.                                                           
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS 25 CHARACTERS.                                       
       COPY BP13K060.                                                           
                                                                                
       FD  M06F120A                                                             
           RECORD CONTAINS 700 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  M06F120A-REC.                                                        
           05  FILLER                   PIC X(604).                             
           05  WS-F120-MATURE-TAG       PIC X(01).                              
           05  WS-F120-CDE-TYP-ACTUSE   PIC X(02).                              
           05  FILLER                   PIC X(07).                              
           05  WS-F120-NUM-NT           PIC X(03).                              
           05  FILLER                   PIC X(83).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-COUNTERS.                                                          
          05 WS-F120-READ              PIC 9(08) VALUE ZEROES.                  
          05 WS-F120-WRITE             PIC 9(08) VALUE ZEROES.                  
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 BP13K813-STATUS           PIC 9(02) VALUE ZEROES.                  
          05 BP13K246-STATUS           PIC 9(02) VALUE ZEROES.                  
          05 BP13K060-STATUS           PIC 9(02) VALUE ZEROES.                  
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-COMBINED-IND           PIC X     VALUE 'N'.                     
          05 WS-F120-END               PIC X     VALUE 'N'.                     
          05 WS-K813-EOF               PIC X     VALUE 'N'.                     
          05 WS-K813-FND               PIC X     VALUE 'N'.                     
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL WS-F120-END = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BM06F120                                                 
                       BP13K813                                                 
                       BP13K246                                                 
                       BP13K060                                                 
               OUTPUT  M06F120A.                                                
                                                                                
           IF BP13K813-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K813 : ' BP13K813-STATUS             
              MOVE     BP13K813-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K246-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K246 : ' BP13K246-STATUS             
              MOVE     BP13K246-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K060-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K060 : ' BP13K060-STATUS             
              MOVE     BP13K060-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BM06F120  THRU 2000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BM06F120.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06F120 AT END                                                 
                MOVE 'Y'         TO WS-F120-END                                 
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F120-READ.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE F120-REC         TO M06F120A-REC.                               
                                                                                
           PERFORM 3200-READ-BP13K246    THRU 3200-EXIT.                        
           IF WS-COMBINED-IND = 'Y'                                             
              IF F120-NUM-NT = 'JW'                                             
                 MOVE 'JE'       TO WS-F120-NUM-NT                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3100-START-BP13K813    THRU 3100-EXIT.                       
                                                                                
           IF WS-K813-FND = 'Y'                                                 
              MOVE K813-NUM-MATURE-EST-TAG  TO WS-F120-MATURE-TAG               
           ELSE                                                                 
              MOVE SPACES                   TO WS-F120-MATURE-TAG               
           END-IF.                                                              
                                                                                
           EVALUATE F120-CDE-TYP-ACTUSE                                         
              WHEN '01'                                                         
              WHEN '02'                                                         
                 MOVE '2F'       TO WS-F120-CDE-TYP-ACTUSE                      
              WHEN '03'                                                         
                 IF F120-CDE-MODL-DESG = '81' OR '82' OR '86' OR                
                                         '87' OR '88' OR '85' OR                
                                         '83' OR '84'                           
                    IF WS-F120-MATURE-TAG = 'N'                                 
                       MOVE '3P'    TO WS-F120-CDE-TYP-ACTUSE                   
                    ELSE                                                        
                       MOVE '3 '    TO WS-F120-CDE-TYP-ACTUSE                   
                    END-IF                                                      
                 ELSE                                                           
                    IF F120-NUM-DESIGN-OFFER = 'P' AND                          
                       WS-F120-MATURE-TAG = 'N'                                 
                       MOVE '3P'    TO WS-F120-CDE-TYP-ACTUSE                   
                    ELSE                                                        
                       MOVE '3 '    TO WS-F120-CDE-TYP-ACTUSE                   
                    END-IF                                                      
                 END-IF                                                         
              WHEN '04'                                                         
                 MOVE '4 '       TO WS-F120-CDE-TYP-ACTUSE                      
              WHEN '05'                                                         
              WHEN '06'                                                         
              WHEN '08'                                                         
                 IF K813-NUM-GEN3-TAG = 'Y'                                     
                    MOVE '5G'    TO WS-F120-CDE-TYP-ACTUSE                      
                 ELSE                                                           
                    MOVE '5 '    TO WS-F120-CDE-TYP-ACTUSE                      
                 END-IF                                                         
           END-EVALUATE.                                                        
                                                                                
           WRITE M06F120A-REC.                                                  
           ADD 1   TO WS-F120-WRITE.                                            
                                                                                
           PERFORM 2000-READ-BM06F120 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------              
       3100-START-BP13K813.                                                     
      *-----------------------------------------------------------              
           MOVE 'N'               TO WS-K813-EOF                                
                                     WS-K813-FND.                               
           MOVE SPACES            TO K813-KEY-FLD.                              
           MOVE WS-F120-NUM-NT    TO K813-NUM-ZONE.                             
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K813-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3110-READ-BP13K813  THRU 3110-EXIT                  
                         UNTIL WS-K813-EOF = 'Y' OR                             
                               WS-K813-FND = 'Y'                                
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K813: ' BP13K813-STATUS         
                            ' KEY = ' K813-KEY-FLD                              
                    PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------              
       3110-READ-BP13K813.                                                      
      *-----------------------------------------------------------              
                                                                                
            READ  BP13K813 NEXT AT END                                          
                  MOVE 'Y'    TO  WS-K813-EOF.                                  
                                                                                
            EVALUATE BP13K813-STATUS                                            
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K813-NUM-ZONE = WS-F120-NUM-NT                          
                        IF K813-DTE-BALLOT = F120-NUM-ALLOCN-QTR                
                           MOVE 'Y'      TO  WS-K813-FND                        
                        END-IF                                                  
                     ELSE                                                       
                        MOVE 'Y'      TO  WS-K813-EOF                           
                     END-IF                                                     
                WHEN 10                                                         
                WHEN 23                                                         
                     MOVE 'Y'         TO  WS-K813-EOF                           
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K813 : ' BP13K813-STATUS        
                             ' KEY = ' K813-KEY-FLD                             
                     PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                
            END-EVALUATE.                                                       
                                                                                
       3110-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3200-READ-BP13K246.                                                      
      *-------------------------------------------------------------            
                                                                                
           EVALUATE F120-CDE-TYP-ACTUSE                                         
             WHEN '01'                                                          
              MOVE 'SA       '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '02'                                                          
              IF F120-CDE-MODL-DESG = '80' OR '89' OR '92' OR '97' OR           
                                      '98'                                      
                 MOVE 'SA       '     TO  K246-NUM-FLAT-TYPE                    
              ELSE                                                              
                 MOVE '2-ROOM   '     TO  K246-NUM-FLAT-TYPE                    
              END-IF                                                            
             WHEN '03'                                                          
              MOVE '3-ROOM   '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '04'                                                          
              MOVE '4-ROOM   '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '05'                                                          
              MOVE '5-ROOM   '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '06'                                                          
              MOVE 'EXECUTIVE'        TO  K246-NUM-FLAT-TYPE                    
           END-EVALUATE.                                                        
                                                                                
           PERFORM 3300-READ-BP13K060 THRU 3300-EXIT.                           
           MOVE K060-DESC             TO  K246-NUM-NEW-TOWN.                    
                                                                                
           MOVE F120-NUM-ALLOCN-QTR   TO  K246-DTE-BALLOT.                      
                                                                                
           READ BP13K246.                                                       
                                                                                
           EVALUATE BP13K246-STATUS                                             
              WHEN 00                                                           
                 MOVE 'Y'             TO  WS-COMBINED-IND                       
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'N'             TO  WS-COMBINED-IND                       
                 DISPLAY 'REC NOT FOUND IN BP13K246 ' K246-KEY-FLD              
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K246 ' BP13K246-STATUS              
                 DISPLAY 'K246-KEY-FLD = ' K246-KEY-FLD                         
                 PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3300-READ-BP13K060.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                         TO BP13K060-REC.                 
           INITIALIZE                             BP13K060-REC.                 
                                                                                
           MOVE SPACES                         TO K060-KEY-FLD                  
           MOVE 02                             TO K060-SERIAL-NO.               
           MOVE F120-NUM-NT                    TO K060-CODE.                    
                                                                                
           READ BP13K060.                                                       
                                                                                
           EVALUATE BP13K060-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    MOVE SPACES                     TO BP13K060-REC             
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K060: ' BP13K060-STATUS          
                            ' KEY = ' K060-KEY-FLD                              
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13C25Y CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BM06F120 : ' WS-F120-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO M06F120A : ' WS-F120-WRITE.           
                                                                                
           CLOSE BM06F120                                                       
                 BP13K813                                                       
                 BP13K246                                                       
                 BP13K060                                                       
                 M06F120A.                                                      
                                                                                
           IF BP13K813-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K813 : ' BP13K813-STATUS             
              MOVE BP13K813-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF BP13K246-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K246 : ' BP13K246-STATUS             
              MOVE BP13K246-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K060 : ' BP13K060-STATUS             
              MOVE BP13K060-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
