       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C801                                                  
       AUTHOR.        LSB.                                                      
       DATE-WRITTEN.  03/09/97.                                                 
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE   : CHECK PARENT/CHILD RELATIONSHIP IN    *                
      *                  REQUEST FILE                          *                
      *                                                        *                
      *    INPUT FILE  : BP13F558                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      * ------  ---  ----     -------                          *                
      * N940014 YCH  111094   CHANGE OF KEY FIELDS             *                
      *BP130377 LSB  110997   ALLO H/W AND SINGLE SON FOR P/C  *                
      *BP130455 SSS  050298   EXPAND F558 RECL FROM 150 TO 400 *                
      *BP130418 LSB  101098   Y2K CHANGES                      *                
      *BP130897 AKT  050800   EXPAND F558 RECL FROM 400 TO 600 *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F558A ASSIGN TO P13F558A.                                  
           SELECT P13F558B ASSIGN TO P13F558B.                                  
                                                                                
           SELECT BP13K800   ASSIGN       TO BP13K800                           
                             ACCESS       IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K800-NUM-REGN                      
                             FILE STATUS  IS WS-K800-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F558.                                                           
                                                                                
       FD  P13F558A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F558A-REC                   PIC X(600).                           
                                                                                
       FD  P13F558B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F558B-REC                   PIC X(600).                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F558-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-K800-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-K800-STATUS              PIC 9(2)    VALUE ZEROS.              
           05 WS-OUTPUT                   PIC 9(8)    VALUE ZEROS.              
           05 WS-OUTPUT-PARENT            PIC 9(8)    VALUE ZEROS.              
           05 WS-INPUT-REGN               PIC 9(8)    VALUE ZEROS.              
           05 WS-OUTPUT-REGN              PIC 9(8)    VALUE ZEROS.              
           05 WS-BYPASS-REGN              PIC 9(8)    VALUE 0.                  
           05 WS-RECORD-BYPASS            PIC 9(8)    VALUE 0.                  
                                                                                
       01  WS-PREV-REGN.                                                        
           05  WS-PREV-REF                PIC X(6)    VALUE SPACES.             
           05  FILLER                     PIC X(2)    VALUE SPACES.             
                                                                                
       01  WS-HA-PARENT-CHILD             PIC X(1)    VALUE 'N'.                
       01  WS-FATHER-MOTHER               PIC X(1)    VALUE 'N'.                
       01  WS-SON-ONLY                    PIC X(1)    VALUE 'N'.                
       01  WS-HA-SON                      PIC X(1)    VALUE 'N'.                
                                                                                
       01  WS-FILE-POINTER.                                                     
           05 WS-F558-SUB                 PIC 9(2)    VALUE 0.                  
           05 WS-HA-STORED                PIC 9(2)    VALUE 0.                  
                                                                                
       01  WS-EOF-F558                    PIC X.                                
           88 EOF-F558                    VALUE 'Y'.                            
                                                                                
       COPY WSF558.                                                             
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F558      THRU 200-EXIT.                            
           PERFORM 400-PROCESS-RTN    THRU 400-EXIT                             
             UNTIL EOF-F558.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
                                                                                
           OPEN INPUT  BP13F558                                                 
                       BP13K800                                                 
                OUTPUT P13F558A                                                 
                       P13F558B.                                                
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'K800 OPEN ERROR '                                        
              DISPLAY 'FILE STATUS IS :' WS-K800-STATUS                         
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F558.                                                           
                                                                                
           READ BP13F558 AT END                                                 
                MOVE HIGH-VALUE TO F558-NUM-REGN                                
                MOVE 'Y'        TO WS-EOF-F558                                  
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1 TO WS-F558-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       400-PROCESS-RTN.                                                         
                                                                                
           MOVE  SPACES       TO  WS-F558-RECORD.                               
           MOVE  'N'          TO  WS-SON-ONLY.                                  
           MOVE  'N'          TO  WS-FATHER-MOTHER.                             
           MOVE  'N'          TO  WS-HA-PARENT-CHILD.                           
           MOVE  'N'          TO  WS-HA-SON.                                    
           MOVE  1            TO  WS-F558-SUB.                                  
           MOVE  ZEROES       TO  WS-HA-STORED.                                 
           MOVE  F558-NUM-REGN TO WS-PREV-REGN.                                 
           ADD   1            TO  WS-INPUT-REGN.                                
                                                                                
           PERFORM 500-STORE-F558 THRU 500-EXIT                                 
             UNTIL F558-NUM-REGN NOT = WS-PREV-REGN                             
             OR    EOF-F558.                                                    
      *                                                                         
      * OUTPUT REC FOR PARENT-SON IN NEW APPLICATION PROVIDED                   
      * SON IS SINGLE                                                           
      *                                                                         
           IF WS-HA-PARENT-CHILD = 'Y'                                          
              IF  WS-SON-ONLY = 'Y' AND                                         
                  WS-FATHER-MOTHER = 'N' AND                                    
                  WS-HA-SON = 'N'                                               
                  ADD 1            TO WS-BYPASS-REGN                            
                  ADD WS-HA-STORED TO WS-RECORD-BYPASS                          
                  PERFORM 900-WRITE-PARENT-CHILD THRU 900-EXIT                  
                   VARYING WS-F558-SUB FROM 1 BY 1                              
                     UNTIL WS-F558-SUB > WS-HA-STORED                           
              END-IF                                                            
           ELSE                                                                 
              ADD 1            TO WS-OUTPUT-REGN                                
              PERFORM 800-WRITE-OUTPUT THRU 800-EXIT                            
              VARYING WS-F558-SUB FROM 1 BY 1                                   
              UNTIL WS-F558-SUB > WS-HA-STORED                                  
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      * CDE-RELATIONSHIP ='12' OR '19' OR '32' - MOTHER, INLAW                  
      * CDE-RELATIONSHIP ='02' OR '22' - FATHER, INLAW                          
      * CDE-RELATIONSHIP ='41'         - CHILD                                  
      * CDE-RELATIONSHIP ='42'         - CHILD'S SPOUSE                         
      * F558-NUM-MARITAL-STATUS = '1'  - SINGLE                                 
      * F558-NUM-MARITAL-STATUS = '2'  - MARRIED                                
                                                                                
       500-STORE-F558.                                                          
           IF F558-CDE-RELATIONSHIP = '02' OR '12' OR '19' OR '22' OR           
                                      '32' OR '41' OR '42'                      
              MOVE 'Y' TO WS-HA-PARENT-CHILD                                    
           ELSE                                                                 
              NEXT SENTENCE.                                                    
                                                                                
           IF F558-CDE-RELATIONSHIP = '02' OR '12' OR '19' OR '22' OR           
                                      '32' OR '42'                              
              MOVE 'Y' TO WS-FATHER-MOTHER                                      
           ELSE                                                                 
              NEXT SENTENCE.                                                    
                                                                                
           IF F558-CDE-RELATIONSHIP = '41'                                      
                 AND F558-NUM-MARITAL-STATUS = '1'                              
                 MOVE 'Y' TO WS-SON-ONLY                                        
                 PERFORM 600-READ-BP13K800 THRU 600-EXIT                        
           ELSE                                                                 
              NEXT SENTENCE.                                                    
                                                                                
           MOVE BP13F558-REC TO WS-F558-REC (WS-F558-SUB).                      
           ADD 1 TO WS-F558-SUB.                                                
           ADD 1 TO WS-HA-STORED.                                               
           PERFORM 200-READ-F558 THRU 200-EXIT.                                 
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       600-READ-BP13K800.                                                       
      *-----------------*                                                       
      * IF CHILD IS THE APPLICANT/CO-APPLICANT, DO NOT PASS RECORDS FOR         
      * PARENT/CHILD CHECK                                                      
                                                                                
           MOVE  F558-NUM-REGN TO K800-NUM-REGN.                                
                                                                                
           READ  BP13K800.                                                      
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD  1         TO WS-K800-READ                                  
                IF F558-NUM-NRIC = K800-NUM-NRIC1 OR                            
                   F558-NUM-NRIC = K800-NUM-NRIC2 OR                            
                   F558-NUM-NRIC = K800-NUM-NRIC3 OR                            
                   F558-NUM-NRIC = K800-NUM-NRIC4                               
                   MOVE 'Y' TO WS-HA-SON                                        
                END-IF                                                          
           WHEN OTHER                                                           
                DISPLAY 'K800-FILE STATUS IS  : ' WS-K800-STATUS                
                        ' K800-REGN  : ' K800-NUM-REGN                          
           END-EVALUATE.                                                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       800-WRITE-OUTPUT.                                                        
           MOVE WS-F558-REC (WS-F558-SUB) TO P13F558A-REC.                      
           WRITE P13F558A-REC.                                                  
           ADD 1 TO WS-OUTPUT.                                                  
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-WRITE-PARENT-CHILD.                                                  
                                                                                
           IF WS-F558-CDE-RELATIONSHIP(WS-F558-SUB) = '41'                      
                 AND WS-F558-NUM-MARITAL-STATUS(WS-F558-SUB) = '1'              
              GO TO 900-EXIT.                                                   
                                                                                
           MOVE WS-F558-REC (WS-F558-SUB) TO P13F558B-REC.                      
           WRITE P13F558B-REC.                                                  
           ADD 1 TO WS-OUTPUT-PARENT.                                           
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
           CLOSE BP13F558                                                       
                 P13F558A                                                       
                 P13F558B                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'K800 CLOSE ERROR '                                       
              DISPLAY 'FILE STATUS IS : ' WS-K800-STATUS                        
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C801 ================*'.              
           DISPLAY '1.  NO. OF F558 RECORDS READ    = ' WS-F558-READ.           
           DISPLAY '2.  NO. OF F558 REGNO READ      = ' WS-INPUT-REGN           
           DISPLAY '3.  NO. OF F558 RECORDS OUTPUT  = ' WS-OUTPUT.              
           DISPLAY ' '.                                                         
           DISPLAY '4.  NO. OF F558 REGNO OUTPUT    = ' WS-OUTPUT-REGN          
           DISPLAY '5.  NO. OF F558 REGNO BYPASS    = ' WS-BYPASS-REGN.         
           DISPLAY '6.  NO. OF F558 RECORD BYPASS   = ' WS-RECORD-BYPASS        
           DISPLAY '7.  NO. OF F558 REC PARENT/CHILD= '                         
                                              WS-OUTPUT-PARENT.                 
           DISPLAY '8.  NO. OF F800 RECORD READ     = ' WS-K800-READ.           
           DISPLAY ' '.                                                         
           DISPLAY 'NOTE : 3 + 6 = 1'.                                          
           DISPLAY '     : 4 + 5 = 2'.                                          
           DISPLAY '     : 6     = 7'.                                          
           DISPLAY ' '.                                                         
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
