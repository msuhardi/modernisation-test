       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C564.                                                 
      *AUTHOR.        LSB.                                                      
      *DATE-WRITTEN.  12/03/92.                                                 
      *DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  PROGRAM TO COMPUTE THE NUMBER OF SUCCESSFUL   *                
      *          REQUEST TO BE DEBARRED BASED ON 95% OF NEW    *                
      *          UNITS ALLOCATED FOR ECH NT/FT OR AT LEAST 10  *                
      *          UNITS LEFT FOR SELECTION.                     *                
      *          THE PROGRAM WILL COMPUTE BASED ON TOT-UINT.   *                
      *                                                        *                
      *     INPUT-OUTPUT FILES :                               *                
      *      1. BP13K010                                       *                
      *                                                        *                
      *  CHG-NO  BY  ON      DESCRIPTION                       *                
      *-----------------------------------                     *                
      * C930335 LSB 930317 - EXPAND BP13K010 FOR RDC           *                
      * I920718 LJL 930419 - CONVERT TO COBOLII.               *                
      * C950199 LSB 950207 - CHG KEY-FLD FOR BP13K015          *                
      *BP134861 EAA1 130422  EXPAND BP13K010 TO 600            *                
      **********************************************************                
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                ACCESS MODE  IS DYNAMIC                                         
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K010-KEY-FLD                                    
                FILE STATUS  IS BP13K010-STATUS.                                
                                                                                
           SELECT BP13K015 ASSIGN TO BP13K015                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K015-KEY-FLD                                    
                FILE STATUS IS BP13K015-STATUS.                                 
           SELECT BP13L564 ASSIGN TO BP13L564.                                  
                                                                                
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD   BP13K010                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K010.                                                           
      *                                                                         
       FD   BP13K015                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K015.                                                           
      *                                                                         
       FD   BP13L564                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED.                                            
                                                                                
       01   L564-PRINT-REC                PIC X(132).                           
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-YY1                     PIC X(2).                             
           05  WS-MM1                     PIC X(2).                             
           05  WS-DD1                     PIC X(2).                             
                                                                                
       01  WS-DTE-TODATE.                                                       
           05  WS-DD                      PIC X(2).                             
           05  FILLER                     PIC X(1)    VALUE '/'.                
           05  WS-MM                      PIC X(2).                             
           05  FILLER                     PIC X(1)    VALUE '/'.                
           05  WS-YY                      PIC X(2).                             
                                                                                
       01  WS-FILE-ACCESS-STATUS.                                               
           05  BP13K010-STATUS            PIC 9(2)    VALUE ZEROS.              
           05  BP13K015-STATUS            PIC 9(2)    VALUE ZEROS.              
                                                                                
       01  WS-VARIABLE.                                                         
           05  WS-K010-EOF                PIC X(1)    VALUE 'N'.                
           05  WS-K015-EOF                PIC X(1)    VALUE 'N'.                
           05  WS-K010-READ               PIC 9(5)    VALUE 0.                  
           05  WS-K015-READ               PIC 9(5)    VALUE 0.                  
           05  WS-K010-UPD                PIC 9(5)    VALUE 0.                  
           05  WS-K015-FD                 PIC X(1)    VALUE SPACES.             
           05  WS-CNT-PAGE                PIC 9(4)    VALUE 0.                  
           05  WS-CNT-LINE                PIC 9(2)    VALUE 99.                 
           05  WS-DEBAR-UNIT              PIC 9(5)    VALUE 0.                  
           05  WS-UNIT-LEFT               PIC 9(5)    VALUE 0.                  
           05  WS-REMARK                  PIC X(1)    VALUE SPACES.             
                                                                                
                                                                                
       01  L564-HEAD1.                                                          
           05  FILLER                     PIC X(09) VALUE 'BP13L564'.           
           05  FILLER                     PIC X(04) VALUE SPACES.               
           05  FILLER                     PIC X(04) VALUE 'HDB3'.               
           05  FILLER                     PIC X(22) VALUE SPACES.               
           05  FILLER                     PIC X(42) VALUE                       
               'S Y S T E M     O F    C O M M I T M E N T'.                    
           05  FILLER                     PIC X(20) VALUE SPACES.               
           05  FILLER                     PIC X(07) VALUE 'DATE : '.            
           05  L564-DTE-CURRENT           PIC X(08).                            
           05  FILLER                     PIC X(04) VALUE SPACES.               
           05  FILLER                     PIC X(07) VALUE 'PAGE : '.            
           05  L564-CNT-PAGE              PIC ZZZ9.                             
                                                                                
       01  L564-HEAD2.                                                          
           05  FILLER                     PIC X(100) VALUE SPACES.              
           05  FILLER                     PIC X(32)  VALUE                      
               'SALES SECTION (APPLICATION TEAM)'.                              
                                                                                
       01  L564-HEAD3.                                                          
           05  FILLER                     PIC X(30)  VALUE SPACES.              
           05  FILLER                     PIC X(60)  VALUE                      
               'REPORT ON TOTAL REQUEST TO BE DEBARRED FOR EACH NT/FT'.         
                                                                                
       01  L564-HEAD4.                                                          
           05  FILLER                     PIC X(05).                            
           05  FILLER                     PIC X(60) VALUE                       
           'S/N   NT   FT   TOT-NEW-FLAT   TOT-DEBARRED  REMARK'.               
                                                                                
       01  L564-HEAD5.                                                          
           05  FILLER                     PIC X(2).                             
           05  L564-SERIAL                PIC ZZZZ9.                            
           05  FILLER                     PIC X(3).                             
           05  L564-NT                    PIC X(3).                             
           05  FILLER                     PIC X(3).                             
           05  L564-FT                    PIC X(2).                             
           05  FILLER                     PIC X(3).                             
           05  L564-TOT-NEW               PIC ZZZZ9.                            
           05  FILLER                     PIC X(8).                             
           05  L564-TOT-DEBAR             PIC ZZZZ9.                            
           05  FILLER                     PIC X(8).                             
           05  L564-REMARK                PIC X(22).                            
                                                                                
       01  L564-HEAD6.                                                          
           05  FILLER                     PIC X(30)  VALUE SPACES.              
           05  FILLER                     PIC X(54)  VALUE                      
               'TOTAL NUMBER OF TENDER RECORD READ         :'.                  
           05  L564-K010-READ             PIC ZZZZ9.                            
                                                                                
       01  L564-HEAD7.                                                          
           05  FILLER                     PIC X(30)  VALUE SPACES.              
           05  FILLER                     PIC X(54)  VALUE                      
               'TOTAL NUMBER OF TENDER RECORD UPDATED      :'.                  
           05  L564-K010-UPD              PIC ZZZZ9.                            
                                                                                
       01  L564-HEAD8.                                                          
           05  FILLER                     PIC X(30)  VALUE SPACES.              
           05  FILLER                     PIC X(54)  VALUE                      
               'TOTAL NEW TOWN BALLOT RECORD READ          :'.                  
           05  L564-K015-READ             PIC ZZZZ9.                            
                                                                                
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
      ***************************************************************           
       0000-MAIN-ROUTINE.                                                       
      ***************************************************************           
                                                                                
           PERFORM 1000-OPEN-FILES                                              
              THRU 1000-EXIT.                                                   
                                                                                
           PERFORM 1500-START-BP13K010                                          
              THRU 1500-EXIT.                                                   
                                                                                
           PERFORM 2000-COMPUTE-UNIT-DEBAR                                      
              THRU 2000-EXIT                                                    
             UNTIL WS-K010-EOF = 'Y'.                                           
                                                                                
           PERFORM 9000-CLOSE-ROUTINE                                           
              THRU 9000-EXIT.                                                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       1000-OPEN-FILES.                                                         
      ***************************************************************           
                                                                                
           OPEN INPUT  BP13K015                                                 
                I-O    BP13K010                                                 
                OUTPUT BP13L564.                                                
                                                                                
           IF BP13K010-STATUS NOT = 0 AND 97                                    
              DISPLAY '*** OPENING ERROR, BP13K010-STATUS : '                   
                        BP13K010-STATUS                                         
              MOVE BP13K010-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT.                                                
                                                                                
           IF BP13K015-STATUS NOT = 0 AND 97                                    
              DISPLAY '*** OPENING ERROR, BP13K015-STATUS : '                   
                        BP13K015-STATUS                                         
              MOVE BP13K015-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1500-START-BP13K010.                                                     
           MOVE SPACES TO K010-KEY-FLD.                                         
           START BP13K010 KEY > K010-KEY-FLD                                    
           IF BP13K010-STATUS  = 0                                              
              PERFORM 1800-READ-BP13K010 THRU 1800-EXIT                         
           ELSE                                                                 
              DISPLAY '*** START ERROR, BP13K010-STATUS : '                     
                        BP13K010-STATUS                                         
              DISPLAY '*** K010-KEY-FLD : ' K010-KEY-FLD                        
              MOVE BP13K010-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT.                                                
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1800-READ-BP13K010.                                                      
           READ BP13K010 NEXT AT END                                            
                         MOVE 'Y' TO WS-K010-EOF.                               
           ADD 1 TO WS-K010-READ.                                               
                                                                                
                                                                                
       1800-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-COMPUTE-UNIT-DEBAR.                                                 
            PERFORM 2500-READ-NT-BALLOT-FILE THRU 2500-EXIT.                    
            IF WS-K015-FD = SPACE                                               
                PERFORM 1800-READ-BP13K010 THRU 1800-EXIT                       
                GO TO 2000-EXIT.                                                
                                                                                
            MOVE SPACE TO WS-REMARK.                                            
            IF K010-NUM-TOT-UNIT IS NOT NUMERIC                                 
                MOVE 0 TO K010-NUM-TOT-UNIT.                                    
            IF K010-NUM-TOT-UNIT > 0                                            
                NEXT SENTENCE                                                   
            ELSE                                                                
                PERFORM 1800-READ-BP13K010 THRU 1800-EXIT                       
                GO TO 2000-EXIT.                                                
                                                                                
            MOVE ZEROS TO WS-DEBAR-UNIT.                                        
            COMPUTE WS-DEBAR-UNIT = 0.95 * K010-NUM-TOT-UNIT.                   
            COMPUTE WS-UNIT-LEFT  = K010-NUM-TOT-UNIT - WS-DEBAR-UNIT.          
            IF WS-UNIT-LEFT  < 10                                               
               MOVE 'Y' TO WS-REMARK                                            
               COMPUTE WS-DEBAR-UNIT = K010-NUM-TOT-UNIT - 10.                  
            MOVE WS-DEBAR-UNIT TO K010-NUM-DEBAR.                               
                                                                                
            REWRITE BP13K010-TENDER.                                            
            IF BP13K010-STATUS  = 0                                             
                ADD 1 TO WS-K010-UPD                                            
                PERFORM 3000-PRINT-REPORT THRU 3000-EXIT                        
            ELSE                                                                
            IF BP13K010-STATUS  = 02 OR 22                                      
                DISPLAY 'REWRITE ERROR, BP13K010-STATUS : '                     
                        BP13K010-STATUS                                         
                DISPLAY 'K010-KEY-FLD : ' K010-KEY-FLD                          
            ELSE                                                                
                DISPLAY 'REWRITE ERROR, BP13K010-STATUS : '                     
                        BP13K010-STATUS                                         
                DISPLAY ' K010-KEY-FLD : ' K010-KEY-FLD                         
                MOVE BP13K010-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                     
                                                                                
            PERFORM 1800-READ-BP13K010 THRU 1800-EXIT.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2500-READ-NT-BALLOT-FILE.                                                
                                                                                
           MOVE SPACES TO WS-K015-FD.                                           
           MOVE K010-CDE-NT-ZONE TO K015-CDE-NT.                                
           MOVE K010-CDE-FLAT-TY TO K015-CDE-FLAT-TY.                           
           READ BP13K015                                                        
           IF BP13K015-STATUS  = 0                                              
                ADD 1 TO WS-K015-READ                                           
                MOVE 'Y' TO WS-K015-FD                                          
           ELSE                                                                 
                DISPLAY 'READ ERROR, BP13K015-STATUS : '                        
                         BP13K015-STATUS                                        
                         ' K015-KEY-FLD : ' K015-KEY-FLD.                       
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-PRINT-REPORT.                                                       
            IF WS-CNT-LINE > 60                                                 
                 PERFORM 3500-PRINT-HEADING THRU 3500-EXIT                      
                 PERFORM 3800-PRINT-DETAIL  THRU 3800-EXIT                      
            ELSE                                                                
                 PERFORM 3800-PRINT-DETAIL  THRU 3800-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3500-PRINT-HEADING.                                                      
                                                                                
           ADD 1 TO WS-CNT-PAGE.                                                
           MOVE WS-CNT-PAGE         TO L564-CNT-PAGE.                           
           ACCEPT WS-DTE-CURRENT    FROM DATE.                                  
           MOVE WS-YY1         TO WS-YY.                                        
           MOVE WS-MM1         TO WS-MM.                                        
           MOVE WS-DD1         TO WS-DD.                                        
           MOVE WS-DTE-TODATE       TO L564-DTE-CURRENT.                        
           WRITE L564-PRINT-REC FROM L564-HEAD1  AFTER PAGE.                    
           WRITE L564-PRINT-REC FROM L564-HEAD2  AFTER 1.                       
           WRITE L564-PRINT-REC FROM L564-HEAD3  AFTER 1.                       
           WRITE L564-PRINT-REC FROM L564-HEAD4  AFTER 2.                       
           MOVE 0 TO WS-CNT-LINE.                                               
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3800-PRINT-DETAIL.                                                       
           ADD 1  TO WS-CNT-LINE.                                               
           MOVE WS-K010-UPD  TO L564-SERIAL.                                    
           MOVE K010-CDE-NT-ZONE TO L564-NT.                                    
           MOVE K010-CDE-FLAT-TY   TO L564-FT.                                  
           MOVE K010-NUM-TOT-UNIT TO L564-TOT-NEW.                              
           MOVE WS-DEBAR-UNIT TO L564-TOT-DEBAR.                                
           IF WS-REMARK = 'Y'                                                   
              MOVE '5% LESS THAN 10 UNITS' TO L564-REMARK                       
           ELSE                                                                 
              MOVE SPACES                  TO L564-REMARK.                      
           WRITE L564-PRINT-REC FROM L564-HEAD5  AFTER 1.                       
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PRINT-CONTROL-LIST.                                                 
           MOVE WS-K010-READ TO L564-K010-READ.                                 
           MOVE WS-K015-READ TO L564-K015-READ.                                 
           MOVE WS-K010-UPD  TO L564-K010-UPD.                                  
           WRITE L564-PRINT-REC FROM L564-HEAD6  AFTER 3.                       
           WRITE L564-PRINT-REC FROM L564-HEAD7  AFTER 1.                       
           WRITE L564-PRINT-REC FROM L564-HEAD8  AFTER 1.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13K010                                                       
                 BP13K015                                                       
                 BP13L564.                                                      
                                                                                
           IF BP13K010-STATUS NOT = 0 AND 97                                    
              DISPLAY '***  CLOSING ERROR, BP13K010-STATUS '                    
              BP13K010-STATUS.                                                  
                                                                                
           IF BP13K015-STATUS NOT = 0 AND 97                                    
              DISPLAY '***  CLOSING ERROR, BP13K015-STATUS '                    
              BP13K015-STATUS.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
