       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C757.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 05/08/2019.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES : TO ADJUST PA ETHNIC PROPORTION BASED ON TOTAL   *         
      *               MINUS THE REGISTERED.                           *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F010 I     SEQUENTIAL 600                                 *         
      * BP13K010 I/O   RANDOM     600  K010-KEY-FLD                   *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHG-REQ#  DD/MM/YY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP137750  05/08/19  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F010 ASSIGN      TO BP13F010.                             
                                                                                
           SELECT BP13K010 ASSIGN      TO BP13K010                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K010-KEY-FLD                                     
              FILE STATUS   IS WS-K010-STATUS.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F010                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F010.                                                           
                                                                                
       FD  BP13K010                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BP13K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K010-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-TOT-REG              PIC 9(8)        VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F010-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K010-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K010-UPDT            PIC 9(5)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F010-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F010    THRU 2000-EXIT.                        
           PERFORM 3000-MAIN-PROCESSING  THRU 3000-EXIT                         
              UNTIL WS-F010-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F010                                                 
                I-O    BP13K010.                                                
                                                                                
           IF (WS-K010-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K010 FAIL STATUS=' WS-K010-STATUS               
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F010.                                                      
      *****************************************************************         
                                                                                
           READ BP13F010                                                        
              AT END                                                            
                 MOVE 'Y'              TO WS-F010-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F010-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           IF F010-KEY-FLD = SPACES OR LOW-VALUES                               
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 3100-READ-BP13K010 THRU 3100-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F010 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-READ-BP13K010.                                                      
      *****************************************************************         
                                                                                
           MOVE F010-KEY-FLD           TO K010-KEY-FLD.                         
                                                                                
           READ BP13K010.                                                       
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K010-READ                          
                                                                                
                 PERFORM 3200-COMPUTE-PA      THRU 3200-EXIT                    
                 PERFORM 3300-UPDATE-BP13K010 THRU 3300-EXIT                    
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K010: ' F010-KEY-FLD             
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K010 FAIL STATUS=' WS-K010-STATUS            
                 MOVE WS-K010-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3200-COMPUTE-PA.                                                         
      *****************************************************************         
                                                                                
      *--->COMPUTE K010-NUM-TOT-PA                                              
           COMPUTE WS-TOT-REG =                                                 
                K010-REG-MTF1 + K010-REG-MTF2 + K010-REG-MTF3 +                 
                K010-REG-CCC1 + K010-REG-CCC2 + K010-REG-CCC3 +                 
                K010-REG-RH1  + K010-REG-RH2  + K010-REG-RH3  +                 
                K010-REG-REN1 + K010-REG-REN2 + K010-REG-REN3 +                 
                K010-REG-JB1  + K010-REG-JB2  + K010-REG-JB3  +                 
                K010-REG-RDC1 + K010-REG-RDC2 + K010-REG-RDC3 +                 
                K010-REG-TCP1 + K010-REG-TCP2 + K010-REG-TCP3.                  
                                                                                
           IF WS-TOT-REG < K010-NUM-TOT-UNIT                                    
              COMPUTE K010-NUM-TOT-PA = K010-NUM-TOT-UNIT -                     
                                        WS-TOT-REG                              
           ELSE                                                                 
              MOVE ZEROES              TO K010-NUM-TOT-PA                       
           END-IF.                                                              
                                                                                
      *--->COMPUTE K010-NUM-MAX-PA1                                             
           IF F010-NUM-MAX-PA1 > 0                                              
              COMPUTE WS-TOT-REG =                                              
                 K010-REG-MTF1 + K010-REG-CCC1 + K010-REG-RH1  +                
                 K010-REG-REN1 + K010-REG-JB1  + K010-REG-RDC1 +                
                 K010-REG-TCP1                                                  
                                                                                
              IF WS-TOT-REG < F010-NUM-MAX-PA1                                  
                 COMPUTE K010-NUM-MAX-PA1 = F010-NUM-MAX-PA1 -                  
                                            WS-TOT-REG                          
              ELSE                                                              
                 MOVE ZEROES              TO K010-NUM-MAX-PA1                   
              END-IF                                                            
           ELSE                                                                 
              MOVE ZEROES              TO K010-NUM-MAX-PA1                      
           END-IF.                                                              
                                                                                
      *--->COMPUTE K010-NUM-MAX-PA2                                             
           IF F010-NUM-MAX-PA2 > 0                                              
              COMPUTE WS-TOT-REG =                                              
                 K010-REG-MTF2 + K010-REG-CCC2 + K010-REG-RH2  +                
                 K010-REG-REN2 + K010-REG-JB2  + K010-REG-RDC2 +                
                 K010-REG-TCP2                                                  
                                                                                
              IF WS-TOT-REG < F010-NUM-MAX-PA2                                  
                 COMPUTE K010-NUM-MAX-PA2 = F010-NUM-MAX-PA2 -                  
                                            WS-TOT-REG                          
              ELSE                                                              
                 MOVE ZEROES              TO K010-NUM-MAX-PA2                   
              END-IF                                                            
           ELSE                                                                 
              MOVE ZEROES              TO K010-NUM-MAX-PA2                      
           END-IF.                                                              
                                                                                
      *--->COMPUTE K010-NUM-MAX-PA3                                             
           IF F010-NUM-MAX-PA3 > 0                                              
              COMPUTE WS-TOT-REG =                                              
                 K010-REG-MTF3 + K010-REG-CCC3 + K010-REG-RH3  +                
                 K010-REG-REN3 + K010-REG-JB3  + K010-REG-RDC3 +                
                 K010-REG-TCP3                                                  
                                                                                
              IF WS-TOT-REG < F010-NUM-MAX-PA3                                  
                 COMPUTE K010-NUM-MAX-PA3 = F010-NUM-MAX-PA3 -                  
                                            WS-TOT-REG                          
              ELSE                                                              
                 MOVE ZEROES              TO K010-NUM-MAX-PA3                   
              END-IF                                                            
           ELSE                                                                 
              MOVE ZEROES              TO K010-NUM-MAX-PA3                      
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3300-UPDATE-BP13K010.                                                    
      *****************************************************************         
                                                                                
           REWRITE BP13K010-TENDER.                                             
                                                                                
           IF WS-K010-STATUS = ZEROES                                           
              ADD 1                    TO WS-K010-UPDT                          
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K010 FAIL STATUS=' WS-K010-STATUS            
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F010                                                       
                 BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K010 FAIL STATUS=' WS-K010-STATUS              
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*****************************'.                             
           DISPLAY '* PROGRAM BP13C757          *'.                             
           DISPLAY '* CONTROL COUNTS            *'.                             
           DISPLAY '*---------------------------*'.                             
           DISPLAY '* REC READ F010     : ' WS-F010-READ ' *'.                  
           DISPLAY '* REC READ K010     : ' WS-K010-READ ' *'.                  
           DISPLAY '* REC UPDT K010     : ' WS-K010-UPDT ' *'.                  
           DISPLAY '*****************************'.                             
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
