       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C94A.                                                  
      *DATE-WRITTEN. 17 APR 2017.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO UPDATE BP13K48K FILE WITH BB24 INTERFA-  *            
      *                CING FILE.                                  *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136743 20170417 KVL1  NEW PROGRAM                        *            
      * BP136986 20171012 KVL1  ADDED READING OF HISTORY FILE      *            
      * BP137040 20180627 FNP1  CREATE K48K IF F007-DTE-REGISTER > 0            
      * BP137040 20180703 PCL4  TO UPDATE K48K-DTE-RESALE-APPROVAL *            
      * BP137040 20180710 PCL4  TO UPDATE K48K-DTE-RESALE-APPROVAL *            
      * BP137385 20180816 PCL4  TO ADD CHECK FOR K110-REGN-NO      *            
      * BP137465 20181119 DN8   TO WRITE RECORD NOT PRESENT IN K110*            
      *                         INTO A SEPARATE WORK FILE.         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BB24F007  ASSIGN        TO BB24F007.                          
                                                                                
           SELECT BP13F007  ASSIGN        TO BP13F007.                          
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS BP13K800-STATUS.                   
                                                                                
           SELECT BP13K893  ASSIGN        TO BP13K893                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K893-KEY-FLD                       
                            FILE STATUS   IS BP13K893-STATUS.                   
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS BM06K110-STATUS.                   
                                                                                
           SELECT BP13K48K  ASSIGN        TO BP13K48K                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K48K-KEY-FLD                       
                            FILE STATUS   IS BP13K48K-STATUS.                   
                                                                                
           SELECT BP13F94A  ASSIGN        TO BP13F94A.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BB24F007                                                             
           RECORD CONTAINS 300  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BB24F007.                                                           
                                                                                
       FD  BP13F007                                                             
           RECORD CONTAINS 300  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  BP13F007-RECORD         PIC X(300).                                  
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K893                                                             
           RECORD CONTAINS 2050 CHARACTERS.                                     
       COPY BP13K893.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500  CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K48K                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP13K48K.                                                           
                                                                                
       FD  BP13F94A                                                             
           RECORD CONTAINS 130  CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01 BP13F94A-REC.                                                         
          05 F94A-REC          PIC X(114).                                      
          05 F94A-NUM-REGN     PIC X(08).                                       
          05 F94A-STAT1        PIC X(01).                                       
          05 F94A-STAT2        PIC X(01).                                       
          05 F94A-STAT3        PIC X(01).                                       
          05 F94A-STAT4        PIC X(01).                                       
          05 F94A-CASE-TYPE    PIC X(01).                                       
          05 FILLER            PIC X(03).                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-DATE             PIC 9(08)  VALUE ZEROES.                         
       01  BP13K800-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BP13K893-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BP13K48K-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BM06K110-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  WS-S                PIC 9(06)  VALUE ZEROES.                         
       01  WS-READ-CNT         PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STATA-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STATB-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STATC-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STATD-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STATE-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STATF-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STATG-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STATH-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STATI-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STAT1-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STAT2-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STAT3-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-STAT4-CNT  PIC 9(06)  VALUE ZEROES.                         
       01  WS-CNCL-WRITE       PIC 9(06)  VALUE ZEROES.                         
       01  WS-BB24F007-EOF     PIC X(01)  VALUE 'N'.                            
       01  WS-NUM-CANCEL       PIC X(01)  VALUE 'N'.                            
       01  WS-K893-DONE        PIC X(01)  VALUE 'N'.                            
       01  WS-K800-FND         PIC X(01)  VALUE 'N'.                            
       01  WS-K893-FND         PIC X(01)  VALUE 'N'.                            
       01  WS-K48K-FND         PIC X(01)  VALUE 'N'.                            
       01  WS-STOP             PIC X(01)  VALUE 'N'.                            
       01  WS-MATCH            PIC X(01)  VALUE 'N'.                            
       01  WS-STAT-TAG         PIC X(01)  VALUE 'N'.                            
       01  WS-COMPLETE-TAG     PIC X(01)  VALUE 'N'.                            
       01  WS-STAT1            PIC X(01)  VALUE ' '.                            
       01  WS-STAT2            PIC X(01)  VALUE ' '.                            
       01  WS-STAT3            PIC X(01)  VALUE ' '.                            
       01  WS-STAT4            PIC X(01)  VALUE ' '.                            
       01  WS-ARRAY.                                                            
           05 WS-HA OCCURS 4 TIMES                                              
                               PIC X(09)  VALUE SPACES.                         
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BB24F007       THRU 2000-EXIT.                     
           IF WS-BB24F007-EOF = 'Y'                                             
              DISPLAY 'FILE BB24F007 IS  EMPTY'                                 
           ELSE                                                                 
              PERFORM 3000-PROCESS-DATA     THRU 3000-EXIT                      
                UNTIL WS-BB24F007-EOF = 'Y'                                     
           END-IF                                                               
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BB24F007                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       BM06K110                                                 
                I-O    BP13K48K                                                 
                OUTPUT BP13F94A                                                 
                       BP13F007.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K800 OPEN ERROR, STATUS : ' BP13K800-STATUS          
              MOVE     BP13K800-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K893-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K893 OPEN ERROR, STATUS : ' BP13K893-STATUS          
              MOVE     BP13K893-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K48K-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K48K OPEN ERROR, STATUS : ' BP13K48K-STATUS          
              MOVE     BP13K48K-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BM06K110 OPEN ERROR, STATUS : ' BM06K110-STATUS          
              MOVE     BM06K110-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-DATE.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BB24F007.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BB24F007 AT END                                                 
                         MOVE 'Y'              TO WS-BB24F007-EOF               
                         NOT AT END                                             
                         ADD  1                TO WS-READ-CNT                   
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
              WHEN 00                                                           
                 MOVE 'Y'             TO WS-K800-FND                            
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'N'             TO WS-K800-FND                            
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K800. STATUS IS '                   
                            BP13K800-STATUS                                     
                 MOVE BP13K800-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-READ-BP13K48K.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K48K.                                                       
                                                                                
           EVALUATE BP13K48K-STATUS                                             
              WHEN 00                                                           
                 MOVE 'Y'                TO WS-K48K-FND                         
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'N'                TO WS-K48K-FND                         
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K48K. STATUS IS '                   
                            BP13K48K-STATUS                                     
                 MOVE BP13K48K-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06K110 KEY IS K110-KEY-FLD                                    
                                                                                
           EVALUATE BM06K110-STATUS                                             
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
      **      DISPLAY 'HDB REF NOT FOUND IN BM06K110, KEY IS '                  
      **               K110-KEY-FLD                                             
      **      MOVE     BM06K110-STATUS       TO   RETURN-CODE                   
      **      PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
              MOVE BB24F007-REC TO BP13F007-RECORD                              
              WRITE                BP13F007-RECORD                              
              ADD             1 TO WS-CNCL-WRITE                                
           WHEN OTHER                                                           
              DISPLAY 'BM06K110 READ ERROR, STATUS IS '                         
                       BM06K110-STATUS                                          
              MOVE     BM06K110-STATUS       TO   RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2400-DO-VALIDATION.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF K800-NUM-STATUS = 'C'                                             
              MOVE 'F'                         TO   WS-STAT1                    
           ELSE                                                                 
              MOVE F007-NUM-UIN-SLR1           TO   WS-HA(1)                    
              MOVE F007-NUM-UIN-SLR2           TO   WS-HA(2)                    
              MOVE F007-NUM-UIN-SLR3           TO   WS-HA(3)                    
              MOVE F007-NUM-UIN-SLR4           TO   WS-HA(4)                    
              MOVE 'N'                         TO   WS-STOP                     
                                                    WS-MATCH                    
              PERFORM VARYING WS-S FROM 1 BY 1 UNTIL WS-S > 4 OR                
                 WS-STOP = 'Y' OR WS-MATCH = 'Y'                                
                 EVALUATE WS-HA(WS-S)                                           
                    WHEN SPACES                                                 
                       MOVE 'Y' TO WS-STOP                                      
                    WHEN K800-NUM-NRIC1                                         
                    WHEN K800-NUM-NRIC2                                         
                    WHEN K800-NUM-NRIC3                                         
                    WHEN K800-NUM-NRIC4                                         
                       MOVE 'Y' TO WS-MATCH                                     
                 END-EVALUATE                                                   
              END-PERFORM                                                       
              IF WS-MATCH NOT = 'Y'                                             
                 MOVE 'G'                      TO   WS-STAT1                    
              END-IF                                                            
           END-IF.                                                              
           IF WS-STAT1 = SPACE                                                  
              IF WS-K48K-FND  = 'N'                                             
                 PERFORM 2650-CHECK-NO-MISSING THRU 2650-EXIT                   
                 IF WS-COMPLETE-TAG = 'Y'                                       
                    MOVE 'A'                   TO   WS-STAT1                    
                 END-IF                                                         
              ELSE                                                              
                 IF F007-DTE-CPF-REFUND NOT = SPACES AND LOW-VALUES             
                    IF K48K-DTE-CPF-REFUND  = SPACES                            
                       MOVE  'B'               TO   WS-STAT1                    
                       MOVE  'Y'               TO   WS-STAT-TAG                 
                    ELSE                                                        
                       MOVE  'H'               TO   WS-STAT1                    
                       MOVE  '1'               TO   WS-STAT2                    
                    END-IF                                                      
                 END-IF                                                         
                 IF (F007-DTE-COMPLETION  NOT = SPACES AND LOW-VALUES)          
                     IF K48K-DTE-RES-COMPLTN  = SPACES                          
                        IF WS-STAT1 = SPACE                                     
                           MOVE 'C'               TO   WS-STAT1                 
                        ELSE                                                    
                           MOVE 'C'               TO   WS-STAT2                 
                        END-IF                                                  
                        MOVE    'Y'               TO   WS-STAT-TAG              
                     ELSE                                                       
                        MOVE    'H'               TO   WS-STAT1                 
                        MOVE    '2'               TO   WS-STAT2                 
                     END-IF                                                     
                 END-IF                                                         
                 IF (F007-DTE-FP-CONFIRM   NOT = SPACES AND LOW-VALUES)         
                     IF  F007-DTE-FP-CONFIRM   NOT =                            
                         K48K-DTE-RES-FIN-PLAN                                  
                        IF WS-STAT1 = SPACE                                     
                           MOVE    'D'            TO   WS-STAT1                 
                        ELSE                                                    
                           IF WS-STAT2 = SPACE                                  
                              MOVE 'D'            TO   WS-STAT2                 
                           ELSE                                                 
                              MOVE 'D'            TO   WS-STAT3                 
                           END-IF                                               
                        END-IF                                                  
                        MOVE       'Y'            TO   WS-STAT-TAG              
                     ELSE                                                       
                        IF WS-STAT-TAG NOT = 'Y'                                
                           MOVE    'H'            TO   WS-STAT1                 
                           MOVE    '3'            TO   WS-STAT2                 
                        END-IF                                                  
                     END-IF                                                     
                 END-IF                                                         
                 IF (F007-DTE-APPTM     NOT = SPACES AND LOW-VALUES)            
                     IF  F007-DTE-APPTM NOT = K48K-DTE-APPTM                    
                        IF WS-STAT1 = SPACE                                     
                           MOVE 'E'               TO   WS-STAT1                 
                        ELSE                                                    
                           IF WS-STAT2 = SPACE                                  
                              MOVE    'E'         TO   WS-STAT2                 
                           ELSE                                                 
                              IF WS-STAT3 = SPACE                               
                                 MOVE 'E'         TO   WS-STAT3                 
                              ELSE                                              
                                 MOVE 'E'         TO   WS-STAT4                 
                              END-IF                                            
                           END-IF                                               
                        END-IF                                                  
                     ELSE                                                       
                        IF WS-STAT-TAG NOT = 'Y'                                
                           MOVE       'H'         TO   WS-STAT1                 
                           MOVE       '4'         TO   WS-STAT2                 
                        END-IF                                                  
                     END-IF                                                     
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2500-WRITE-TO-OUTPUT.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO   F94A-REC.                      
           MOVE BB24F007-REC                TO   F94A-REC.                      
           MOVE K48K-NUM-REGN               TO   F94A-NUM-REGN.                 
           MOVE K48K-CASE-ELIGBLTY          TO   F94A-CASE-TYPE.                
           MOVE WS-STAT1                    TO   F94A-STAT1.                    
           MOVE WS-STAT2                    TO   F94A-STAT2.                    
           MOVE WS-STAT3                    TO   F94A-STAT3.                    
           MOVE WS-STAT4                    TO   F94A-STAT4.                    
           WRITE BP13F94A-REC                                                   
                                                                                
           EVALUATE WS-STAT1                                                    
              WHEN 'A'                                                          
                 PERFORM 2600-MOVE-TO-BP13K48K                                  
                                            THRU 2600-EXIT                      
                 ADD 1                      TO   WS-WRITE-STATA-CNT             
              WHEN 'B'                                                          
                 MOVE F007-DTE-CPF-REFUND   TO   K48K-DTE-CPF-REFUND            
                 ADD 1                      TO   WS-WRITE-STATB-CNT             
              WHEN 'C'                                                          
                 MOVE F007-DTE-COMPLETION   TO   K48K-DTE-RES-COMPLTN           
                 ADD 1                      TO   WS-WRITE-STATC-CNT             
              WHEN 'D'                                                          
                 MOVE F007-DTE-FP-CONFIRM   TO   K48K-DTE-RES-FIN-PLAN          
                 ADD 1                      TO   WS-WRITE-STATD-CNT             
              WHEN 'E'                                                          
                 MOVE F007-DTE-APPTM        TO   K48K-DTE-APPTM                 
                 ADD 1                      TO   WS-WRITE-STATE-CNT             
              WHEN 'F'                                                          
                 ADD 1                      TO   WS-WRITE-STATF-CNT             
              WHEN 'G'                                                          
                 ADD 1                      TO   WS-WRITE-STATG-CNT             
              WHEN 'H'                                                          
                 ADD 1                      TO   WS-WRITE-STATH-CNT             
              WHEN 'I'                                                          
                 ADD 1                      TO   WS-WRITE-STATI-CNT             
           END-EVALUATE.                                                        
           ADD       1                      TO   WS-WRITE-STAT1-CNT.            
                                                                                
           IF WS-STAT2 NOT = SPACE AND                                          
              WS-STAT1 NOT = 'H' AND 'I'                                        
              ADD    1                      TO   WS-WRITE-STAT2-CNT             
           EVALUATE WS-STAT2                                                    
              WHEN 'B'                                                          
                 MOVE F007-DTE-CPF-REFUND   TO   K48K-DTE-CPF-REFUND            
                 ADD 1                      TO   WS-WRITE-STATB-CNT             
              WHEN 'C'                                                          
                 MOVE F007-DTE-COMPLETION   TO   K48K-DTE-RES-COMPLTN           
                 ADD 1                      TO   WS-WRITE-STATC-CNT             
              WHEN 'D'                                                          
                 MOVE F007-DTE-FP-CONFIRM   TO   K48K-DTE-RES-FIN-PLAN          
                 ADD 1                      TO   WS-WRITE-STATD-CNT             
              WHEN 'E'                                                          
                 MOVE F007-DTE-APPTM        TO   K48K-DTE-APPTM                 
                 ADD 1                      TO   WS-WRITE-STATE-CNT             
           END-EVALUATE                                                         
           END-IF.                                                              
                                                                                
           IF WS-STAT3 NOT = SPACE                                              
              ADD    1                      TO   WS-WRITE-STAT3-CNT             
           EVALUATE WS-STAT3                                                    
              WHEN 'B'                                                          
                 MOVE F007-DTE-CPF-REFUND   TO   K48K-DTE-CPF-REFUND            
                 ADD 1                      TO   WS-WRITE-STATB-CNT             
              WHEN 'C'                                                          
                 MOVE F007-DTE-COMPLETION   TO   K48K-DTE-RES-COMPLTN           
                 ADD 1                      TO   WS-WRITE-STATC-CNT             
              WHEN 'D'                                                          
                 MOVE F007-DTE-FP-CONFIRM   TO   K48K-DTE-RES-FIN-PLAN          
                 ADD 1                      TO   WS-WRITE-STATD-CNT             
              WHEN 'E'                                                          
                 MOVE F007-DTE-APPTM        TO   K48K-DTE-APPTM                 
                 ADD 1                      TO   WS-WRITE-STATE-CNT             
           END-EVALUATE                                                         
           END-IF.                                                              
                                                                                
           IF WS-STAT4 NOT = SPACE                                              
              ADD    1                      TO   WS-WRITE-STAT4-CNT             
           EVALUATE WS-STAT4                                                    
              WHEN 'B'                                                          
                 MOVE F007-DTE-CPF-REFUND   TO   K48K-DTE-CPF-REFUND            
                 ADD 1                      TO   WS-WRITE-STATB-CNT             
              WHEN 'C'                                                          
                 MOVE F007-DTE-COMPLETION   TO   K48K-DTE-RES-COMPLTN           
                 ADD 1                      TO   WS-WRITE-STATC-CNT             
              WHEN 'D'                                                          
                 MOVE F007-DTE-FP-CONFIRM   TO   K48K-DTE-RES-FIN-PLAN          
                 ADD 1                      TO   WS-WRITE-STATD-CNT             
              WHEN 'E'                                                          
                 MOVE F007-DTE-APPTM        TO   K48K-DTE-APPTM                 
                 ADD 1                      TO   WS-WRITE-STATE-CNT             
           END-EVALUATE                                                         
           END-IF.                                                              
                                                                                
           MOVE F007-DTE-RESL-APPROVAL    TO K48K-DTE-RESALE-APPROVAL.          
                                                                                
           IF WS-STAT1 = 'A'                                                    
              PERFORM 2700-WRITE-BP13K48K   THRU 2700-EXIT                      
           ELSE                                                                 
              PERFORM 2800-REWRITE-BP13K48K THRU 2800-EXIT                      
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2600-MOVE-TO-BP13K48K.                                                   
      *-------------------------------------------------------------            
           MOVE SPACES                  TO BP13K48K-REC.                        
           INITIALIZE                      BP13K48K-REC.                        
           MOVE K800-NUM-REGN           TO K48K-NUM-REGN.                       
           MOVE F007-NUM-CASE           TO K48K-NUM-CASE.                       
           MOVE F007-NUM-HDB-REF        TO K48K-RES-NUM-HDB-REF.                
           MOVE F007-NUM-UIN-SLR1       TO K48K-NUM-CPF-LSE1.                   
           MOVE F007-NUM-UIN-SLR2       TO K48K-NUM-CPF-LSE2.                   
           MOVE F007-NUM-UIN-SLR3       TO K48K-NUM-CPF-LSE3.                   
           MOVE F007-NUM-UIN-SLR4       TO K48K-NUM-CPF-LSE4.                   
           MOVE F007-DTE-RESL-APPL      TO K48K-DTE-SUBMIT-APPLN.               
           MOVE F007-DTE-REGISTER       TO K48K-DTE-RES-REG.                    
           MOVE F007-DTE-APPTM          TO K48K-DTE-APPTM.                      
           MOVE F007-NUM-HDB-REF-NEW    TO K48K-NUM-HDB-REF.                    
           MOVE 'RESL'                  TO K48K-SOURCE.                         
           MOVE WS-DATE                 TO K48K-DTE-CREATED                     
                                           K48K-DTE-UPDATED.                    
           MOVE 'BP13C94A'              TO K48K-USR-CREATED                     
                                           K48K-USR-UPDATED.                    
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2650-CHECK-NO-MISSING.                                                   
      *-------------------------------------------------------------            
           IF F007-NUM-CASE     = SPACES                                        
              MOVE '1'                  TO WS-STAT2                             
           END-IF.                                                              
           IF F007-NUM-HDB-REF  = SPACES                                        
              MOVE '2'                  TO WS-STAT2                             
           END-IF.                                                              
           IF F007-NUM-UIN-SLR1 = SPACES AND                                    
              F007-NUM-UIN-SLR2 = SPACES AND                                    
              F007-NUM-UIN-SLR3 = SPACES AND                                    
              F007-NUM-UIN-SLR4 = SPACES                                        
              MOVE '3'                  TO WS-STAT2                             
           END-IF.                                                              
           IF F007-DTE-RESL-APPL = SPACES                                       
              MOVE '4'                  TO WS-STAT2                             
           END-IF.                                                              
           IF F007-DTE-REGISTER  = SPACES                                       
              MOVE '5'                  TO WS-STAT2                             
           END-IF.                                                              
      *    IF F007-DTE-APPTM     = SPACES                                       
      *       MOVE '6'                  TO WS-STAT2                             
      *    END-IF.                                                              
           IF WS-STAT2 NOT = SPACES                                             
              MOVE 'I'                  TO WS-STAT1                             
           ELSE                                                                 
              IF F007-DTE-REGISTER  > ZEROES                                    
                 MOVE 'Y'                  TO WS-COMPLETE-TAG                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2650-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2700-WRITE-BP13K48K.                                                     
      *-------------------------------------------------------------            
           WRITE    BP13K48K-REC.                                               
                                                                                
           EVALUATE BP13K48K-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WRITING BP13K48K FOR KEY '                      
                         'RES CASE NO: ' F007-NUM-CASE                          
                         ' REGN NO:    ' K800-NUM-REGN                          
                 MOVE BP13K48K-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2800-REWRITE-BP13K48K.                                                   
      *-------------------------------------------------------------            
           REWRITE  BP13K48K-REC.                                               
                                                                                
           EVALUATE BP13K48K-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR REWRITING BP13K48K FOR KEY '                    
                         'RES CASE NO ' F007-NUM-CASE                           
                         'REGN NO     ' K800-NUM-REGN                           
                 MOVE BP13K48K-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES                      TO   K110-KEY-FLD.                  
           MOVE F007-NUM-HDB-REF-NEW        TO   K110-SCH-ACC-NO.               
           PERFORM 2300-READ-BM06K110       THRU 2300-EXIT.                     
           MOVE SPACES                      TO   BP13K800-MASTER.               
           MOVE K110-REGN-NO                TO   K800-NUM-REGN.                 
                                                                                
           MOVE SPACES                      TO  BP13K48K-REC.                   
           MOVE SPACE                       TO  WS-K48K-FND                     
                                                WS-K893-DONE                    
                                                WS-STAT-TAG                     
                                                WS-COMPLETE-TAG                 
                                                WS-STAT1                        
                                                WS-STAT2                        
                                                WS-STAT3                        
                                                WS-STAT4                        
                                                WS-NUM-CANCEL.                  
                                                                                
           IF K110-REGN-NO(1:2) = '##' OR SPACES OR LOW-VALUES                  
              MOVE 'Y'                         TO WS-NUM-CANCEL                 
           ELSE                                                                 
              PERFORM 2100-READ-BP13K800     THRU 2100-EXIT                     
                                                                                
              IF WS-K800-FND = 'Y'                                              
                 MOVE K800-NUM-REGN            TO   K48K-NUM-REGN               
              ELSE                                                              
                 MOVE SPACES                   TO   K893-KEY-FLD                
                 MOVE K110-REGN-NO             TO   K893-NUM-REGN-HIST          
                 PERFORM 3100-STARTBR-BP13K893 THRU 3100-EXIT                   
                                                                                
                 IF WS-K893-FND = 'Y'                                           
                    MOVE K893-NUM-REGN-HIST    TO K48K-NUM-REGN                 
                    MOVE SPACES                TO BP13K800-MASTER               
                    MOVE BP13K893-MASTER       TO BP13K800-MASTER               
                 ELSE                                                           
                    MOVE 23                    TO BP13K893-STATUS               
                    DISPLAY 'ERROR REC NOT FND IN BP13K893. STATUS IS '         
                            BP13K893-STATUS ' KEY IS ' K48K-NUM-REGN            
                    MOVE BP13K893-STATUS TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-ROUTINE                                  
                       THRU 9000-EXIT                                           
                 END-IF                                                         
                                                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-NUM-CANCEL NOT = 'Y'                                           
              PERFORM 2200-READ-BP13K48K    THRU 2200-EXIT                      
              PERFORM 2400-DO-VALIDATION    THRU 2400-EXIT                      
              PERFORM 2500-WRITE-TO-OUTPUT  THRU 2500-EXIT                      
           ELSE                                                                 
              ADD 1                         TO WS-WRITE-STATF-CNT               
              DISPLAY F007-NUM-HDB-REF-NEW ' - CANCELED CASE'                   
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BB24F007       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-STARTBR-BP13K893.                                                   
      *-------------------------------------------------------------            
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K893-STATUS                                             
              WHEN 00                                                           
                 PERFORM 3110-READNXT-BP13K893 THRU 3110-EXIT                   
                   UNTIL WS-K893-DONE = 'Y'                                     
              WHEN 23                                                           
                 DISPLAY 'ERROR REC NOT FND IN BP13K893. STATUS IS '            
                          BP13K893-STATUS                                       
                 MOVE BP13K893-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
              WHEN OTHER                                                        
                 DISPLAY 'ERROR STR BRW BP13K893. STATUS IS '                   
                          BP13K893-STATUS                                       
                 MOVE BP13K893-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3110-READNXT-BP13K893.                                                   
      *-------------------------------------------------------------            
                                                                                
           READ BP13K893 NEXT RECORD.                                           
                                                                                
           EVALUATE BP13K893-STATUS                                             
            WHEN 00                                                             
              IF K893-NUM-REGN-HIST = K110-REGN-NO                              
                 MOVE 'Y'                   TO   WS-K893-FND                    
              ELSE                                                              
                 MOVE 'N'                   TO   WS-K893-FND                    
              END-IF                                                            
              MOVE 'Y'                      TO   WS-K893-DONE                   
            WHEN 10                                                             
            WHEN 23                                                             
                 MOVE 'N'                   TO   WS-K893-FND                    
                 MOVE 'Y'                   TO   WS-K893-DONE                   
            WHEN OTHER                                                          
                 DISPLAY 'ERROR READING BP13K893. STATUS IS '                   
                            BP13K893-STATUS                                     
                 MOVE BP13K893-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '------ BP13C94A CONTROL TOTAL -------'.                     
           DISPLAY ' NO OF REC READ FROM BB24F007      = '                      
                     WS-READ-CNT.                                               
           DISPLAY ' NO OF NEW BB24 REQUESTS           = '                      
                     WS-WRITE-STATA-CNT                                         
           DISPLAY ' NO OF REC FOR CPF REFUNDED        = '                      
                     WS-WRITE-STATB-CNT                                         
           DISPLAY ' NO OF REC FOR RESALE COMPLETION   = '                      
                     WS-WRITE-STATC-CNT                                         
           DISPLAY ' NO OF REC FOR FINANCL PLAN CONF   = '                      
                     WS-WRITE-STATD-CNT                                         
           DISPLAY ' NO OF REC FOR 1ST APPT CHANGE     = '                      
                     WS-WRITE-STATE-CNT                                         
           DISPLAY ' NO OF REC WITH CANCELLED APPLN    = '                      
                     WS-WRITE-STATF-CNT                                         
           DISPLAY ' NO OF REC MISMATCH NRIC           = '                      
                     WS-WRITE-STATG-CNT                                         
           DISPLAY ' NO OF REC WITH STAT 1             = '                      
                     WS-WRITE-STAT1-CNT                                         
           DISPLAY ' NO OF REC WITH STAT 2             = '                      
                     WS-WRITE-STAT2-CNT                                         
           DISPLAY ' NO OF REC WITH STAT 3             = '                      
                     WS-WRITE-STAT3-CNT                                         
           DISPLAY ' NO OF REC WITH STAT 4             = '                      
                     WS-WRITE-STAT4-CNT                                         
           DISPLAY ' NO OF REC WRITTEN TO CANCEL FILE  = '                      
                     WS-CNCL-WRITE                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BB24F007                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       BP13K48K                                                 
                       BM06K110                                                 
                       BP13F94A                                                 
                       BP13F007.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
