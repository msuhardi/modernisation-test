      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C411.                                                 
      *AUTHOR.        TMK.                                                      
      *DATE-WRITTEN.  02/11/89.                                                 
                                                                                
      *--------------------------------------------------------*                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  REFORMATS A DAILY SALES ORDER FILE FOR      *                
      *            INTERFACE TO SALES MORTGAGE SYSTEM FOR      *                
      *            CREATING OF NEW LESSEE INFORMATION.         *                
      *                                                        *                
      *     NOTE .                                             *                
      *           1.BP13F410 CONTAINS SALES ORDER WHICH        *                
      *             HAVE KEYS ISSUED. ACCESS INTO THIS FILE    *                
      *             IS SEQUENTIAL.                             *                
      *           2.BP13F430 CONTAINS THE REFORMATTED FINANCE  *                
      *             SALES ORDER FILE. IT IS AN OUTPUT FILE ONLY*                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *           1. BP13F410 (DAILY SALES ORDER FILE)         *                
      *                                                        *                
      *     OUTPUT FILES:                                      *                
      *           2. BP13F430 (FINANCE SALES ORDER FILE)       *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  ON        DESCRIPTION                      *                
      *======= === ====== ==================================== *                
      *I900637 LSB 900618 GET BOARD IMP COST FR FIS, F400 IS   *                
      *                   KEEPING THE LESSEE'S IMPROVEMENT COST*                
      *                                                        *                
      *C90     LKH 900925 TO INCLUDE THE PRICING CODE :-       *                
      *                   C - CBP                              *                
      *                   D - DPP                              *                
      *C911143 LSB 911206 TO MOVE ZERO TO AGR                  *                
      *C930317 LMS 930424 TO CHANGE BP13B430 TO NEW FORMAT     *                
      *                   DUE TO SMS PHASE 3B.                 *                
      *I920718 LMS 930424 TO CONVERT TO COBOL II.              *                
      *C930751 LJL 930716 TO MINUS OFF 1 MTH FOR COMPUTATION   *                
      *                   OF LOAN EXPIRY DATE.                 *                
      *C930993 LJL 931117 TO CATER FOR GST.                    *                
      *C940256 LJL 940309 TO CATER FOR MORTGAGE FIRE INSURANCE *                
      *                   SCHEME.                              *                
      *C9404E6 LJL 940610 TO CATER FOR SALE OF SITTING TENANT  *                
      *                   FLATS (SALES TYPE 2B)                *                
      *C950330 LJL 950309 TO CATER FOR BOOKING FEES TRANSFER   *                
      *                   FOR AA CASES, SALES TYPE 1,2,3       *                
      *                   EXCEPT 2B.                           *                
      *C950420 LJL 950509 TO CATER FOR THE YEAR 2000.          *                
      *BP130037LJL 951013 TO CATER FOR THE AMALGAMATION OF     *                
      *                   SALES TYPE 1,2,2B,3,3A AND 10.       *                
      *BP130209SIA 961112 TO CATER REV-INST TO PASS TO SMS.    *                
      *BP130186LJL 970820 TO PASS AFL DATE TO SMS.             *                
      *BP130302LJL 970820 TO CATER FOR REGN DEP TR AS CD.      *                
      *BP130415SGK 971229 TO CATER FOR SER-CONTRA FOR ICP.     *                
      *BP130311SGK 980108 TO CATER FOR EXPANSION OF AMT-PREMIUM*                
      *BP130480LJL 980406 TO CATER FOR NEW FIELD INTEREST RATE.*                
      *BP130480SGK 980428 TO CATER FOR NEW FIELD DTE-REV-INSTAL*                
      *BP130522LJL 980711 TO CATER FOR EXPANSION OF CPF NUMBER *                
      *                   FROM 8 BYTES TO 9 BYTES.             *                
      *BP130418SIA 980803 TO CHGE BM06K211 TO BM06K110.        *                
      *BP130602LLW 981204 TO CATER FOR SCHEME D & NEW FIELDS IN*                
      *                   BP13F430.                            *                
      *BP130651RAS 990326 TO CATER FOR SCCC ELGBTY, A NEW FIELD*                
      *                   IN BP13F430.                         *                
      *BP130906CLT 010401 TO CATER FOR SA FLAT, SALES TYPE 13  *                
      *BP131028RAS 010627 TO MOVE 'P' TO SCCC ELGBTY IF ALLOC  *                
      *                   SCHEME IS 'SPO'.                     *                
      *BP131066MAL 180801 IF LOAN-AMT NOT> 0 PASS THE FF TO SMS*                
      *                   LOAN-EXP-DTE = 0, LOAN-SCH = 'N' AND *                
      *                   LOAN-SCH-POLICY = 'NR', INT-CDE = 0  *                
      *BP132031MALC031001 CATER FOR SALE OF SERS FLAT TO       *                
      *                   SINGLES                              *                
      *BP132062CKK 140202 TO ADD NEW SCCC-ELGBTY CODE, 'E'     *                
      *BP132118CLT 140302 CATER FOR SERS/REN STDAPT CASE       *                
      *BP132158MCC 120602 TO ADD ERL CODE '8' AND FRS PERCENT  *                
      *BP132301CLT 130203 EXPAND BP13F430 TO 1000 BYTES        *                
      *BP132341CLT 130503 CATER FOR BANK LOAN CASE             *                
      *BP132479CLT 040204 EXPAND INSTALMENT FIELDS             *                
      *BP132625CLT 040205 INITIALISE NUM-SHR                   *                
      *BP132839CLT 170406 CATER FOR SA FLAT USING CPF          *                
      *BP132852CLT 240406 CATER FOR AHG                        *                
      *BP132858YCH 180506 CATER FOR SPR CONV EXPIRY            *                
      *                   COPY BP13C41R FROM COPYCOB           *                
      *BP13312 CLT 221107 CATER FOR NON CON INTEREST RATE      *                
      *BP13298 LSB 230608 CATER FOR DBSS TOP                   *                
      *BP133298 JB8 060808 CHG TO OUTPUT BP13F430               *               
      *BP133298 JB8 290109 DEL F430-DTE-NVP/PRO-TOP             *               
      *                    ADD DTE-PROP-TAX                     *               
      *BP133519 CCC 020409 UPDATE DUE TO SALES EFFECTIVE DATE   *               
      *BP13     JB8 200409 INIT DATE FIELDS TO ZEROES.          *               
      *BP133713 ZDD 201009 MOVE AMR IF ALLO CAT IS NPL          *               
      *BP133729 EA1 031109 FOR NON-DBS, CORRECT THE COMPUTATION *               
      *                    OF THE MONTH IN THE NEW DATE EXPIRY  *               
      *BP133761 EA1 231209 FOR NON-DBS, CORRECT THE COMPUTATION *               
      *                    OF F430-DTE-LOAN-EXPIRY-CYMD(8 BYTES)*               
      *                    REMOVE F430-DTE-LOAN-EXPIRY(6 BYTES) *               
      *BP133929 JB8 010710 TO GET RIGHT LOAN-SCH-POLICY FOR BUY *               
      *                    1ST CASES                            *               
      *BP133929 JB8 050111 TO CATER FOR GST-RATE                *               
      *BP133991 ESA1010211 TO CATER FOR SC/SPR PREMIUM          *               
      *BP134156 JB8 100311 TO PASS THE SELLING PRICE (EXCLUDE   *               
      *                    SC/SPR PREMIUM) AND SC/SPR PREMIUM   *               
      *                    AMOUNT SEPARATELY TO SMS UPON KEY ISSUE              
      *                    FOR DP FLATS CASES, FOR DBSS FLATS WITH              
      *                    SC/SPR HOUSEHOLD, TO PASS THE CODE "SCP"             
      *                    TO SMS UNDER THE FIELD                               
      *                    CDE_SPCL_SCHME UPON KEY ISSUED.                      
      *BP134540 IMC1040512 TO PASS HANDOVER-KIV IF H/O DATE NOTFD*              
      *BP134216 AB9 20120613 ADD CASH-PROCEED TO ICP             *              
      *BP135027 ESA120131008 ADD READING OF BP13KD15 ALT-KEY1    *              
      *BP135146 ESD120140523 CATER TO DERIVE LOAN POLICY SCHEME  *              
      *                      ORIGINAL CR NUMBER IS 4695.         *              
      *BP135146 ESD120140603 CATER TO NEW SELLING PRICE WITH CENTS              
      *                      AND SGL (SINGLES PREMIUM - FTS)     *              
      *BP135339 ESA120140617 CATER TO BANK LOAN INFORMATION FOR DBS             
      *BP135041 ESD120140512 CATER FOR MGL CASES.                *              
      *BP135150 ESD120140722 CATER FOR BUY SELL TAG E.           *              
      *BP135339 JGO120150107 UPDATE DEFAULT VALUES FOR DBSS BANK *              
      *                      AND NO LOAN CASES                   *              
      *BP135631 JGO120150115 UPDATE COMPUTATION OF DTE-NVP-NUM   *              
      *                      DEPENDING ON PROJECT CODE           *              
      *BP135339 ZDD120150216 CHECK FOR KD00-NUM-GIRO             *              
      *         ESA120150216 ADD MOVING OF SPACES TO             *              
      *                      F430-CDE-LOAN-REPYMT                *              
      *BP135677 ZDD120150305 ALLO-SCH=MGL, MGP, PAIR UNIT        *              
      *BP135641 RJB 20150306 TO CATER FOR TEMP LOAN              *              
      *BP135909 FNP 20150910 PASS 'Z' TO F430 IF SCCC-ELIG = Z   *              
      *BP135193 ZDD120151113 ALLO-SCH=MGD FOR DAWSON PAIR UNITS  *              
      *BP136098 LJL120160121 TO CATER FOR SALES TYPE 18.         *              
      *BP136176 KSJ320160427 TO CATER FOR ELDERLY PREMIUM        *              
      *BP136530 FNP120161202 MOVE KD00 GIRO TO F430 GIRO FIELD   *              
      *BP136717 KSJ320170327 TO CATER FOR TEMP LOAN HYBRID TL+LOAN              
      *BP136869 KSJ320170705 TO CATER FOR TEMP LOAN FOR SA AND 2F*              
      *                      PASS '1' TO F430-CDE-INT            *              
      *BP136771 PCL420170717 TO CATER FOR REQUISITE-MOP/DTE-MOP  *              
      *BP136771 PCL420171218 TO CATER FOR REQUISITE-MOP/DTE-MOP  *              
      *                      FOR SERS PORT AND SERS.             *              
      *BP137367 PCL420180625 TO ADD CHECK FOR FSH SCHEME         *              
      *BP137429 PCL420180725 TO CATER FOR BOOKING FEE TRANSFER   *              
      *                      AT TP FOR DDS CASES, CD = 0 AT SOCA *              
      *BP137709 KV5 20190404 ADD TO CHECK MOP SERS ANNOUNCEMENT  *              
      *                      (K100-DTE-ANNOUNCE) GREATER THAN    *              
      *                      EQUAL TO 20190101 PASS '5' TO       *              
      *                      F430-NUM-REQUISITE-MOP              *              
      *BP138849 ZDD 20210915 ADD TO CHECK K800-HOUSEHOLD-CITIZEN *              
      *----------------------------------------------------------*              
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13F430 ASSIGN TO BP13F430.                                  
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                ACCESS MODE IS RANDOM                                           
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS K110-KEY-FLD OF BM06K110                          
                FILE STATUS IS BM06K110-STATUS.                                 
                                                                                
           SELECT BP13K800  ASSIGN TO BP13K800                                  
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K800-NUM-REGN                                   
                FILE STATUS  IS BP13K800-STATUS.                                
                                                                                
           SELECT BP13K480  ASSIGN TO BP13K480                                  
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K480-NUM-REGN                                   
                FILE STATUS  IS BP13K480-STATUS.                                
                                                                                
           SELECT BP13KD00  ASSIGN TO BP13KD00                                  
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS KD00-KEY-FLD                                    
                FILE STATUS  IS BP13KD00-STATUS.                                
                                                                                
           SELECT BP13KD15  ASSIGN TO BP13KD15                                  
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS KD15-KEY-FLD                                    
                ALTERNATE KEY IS KD15-ALT-KEY1                                  
                ALTERNATE KEY IS KD15-ALT-KEY2                                  
                FILE STATUS  IS BP13KD15-STATUS.                                
                                                                                
           SELECT BP13K670  ASSIGN TO BP13K670                                  
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K670-NUM-REGN                                   
                FILE STATUS  IS BP13K670-STATUS.                                
                                                                                
           SELECT BP17K000  ASSIGN TO BP17K000                                  
                ACCESS MODE   IS RANDOM                                         
                ORGANIZATION  IS INDEXED                                        
                RECORD KEY    IS K000-KEY-FLD                                   
                ALTERNATE KEY IS K000-NUM-REGNO                                 
                FILE STATUS   IS WS-K000-STATUS.                                
                                                                                
           SELECT BP17K100  ASSIGN TO BP17K100                                  
                ACCESS MODE   IS RANDOM                                         
                ORGANIZATION  IS INDEXED                                        
                RECORD KEY    IS K100-KEY-FLD                                   
                FILE STATUS   IS WS-K100-STATUS.                                
                                                                                
           SELECT BP17K110 ASSIGN  TO BP17K110                                  
                ACCESS MODE   IS RANDOM                                         
                ORGANIZATION  IS INDEXED                                        
                RECORD KEY    IS K110-KEY-FLD OF BP17K110                       
                ALTERNATE KEY IS K110-ALT-KEY1                                  
                FILE STATUS   IS WS-K110-STATUS.                                
                                                                                
           SELECT BP17K120 ASSIGN  TO BP17K120                                  
                ACCESS MODE   IS RANDOM                                         
                ORGANIZATION  IS INDEXED                                        
                RECORD KEY    IS K120-KEY-FLD                                   
                FILE STATUS   IS WS-K120-STATUS.                                
                                                                                
           SELECT BP13KA20 ASSIGN  TO BP13KA20                                  
                ACCESS MODE   IS RANDOM                                         
                ORGANIZATION  IS INDEXED                                        
                RECORD KEY    IS KA20-KEY-FLD                                   
                FILE STATUS   IS BP13KA20-STATUS.                               
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD  BP13F410                                                             
           BLOCK  CONTAINS   0 CHARACTERS                                       
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL  RECORDS  ARE STANDARD                                         
           RECORDING MODE IS F.                                                 
       COPY BP13F410.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13F430                                                             
           BLOCK CONTAINS   0  CHARACTERS                                       
           RECORD CONTAINS 1000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F430.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K480                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K480.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD15                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13KD15.                                                           
                                                                                
       FD   BP13K670                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K670.                                                           
                                                                                
       FD   BP17K000 IS GLOBAL                                                  
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP17K000.                                                           
                                                                                
       FD   BP17K100 IS GLOBAL                                                  
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP17K100.                                                           
                                                                                
       FD   BP17K110 IS GLOBAL                                                  
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP17K110.                                                           
                                                                                
       FD   BP17K120 IS GLOBAL                                                  
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP17K120.                                                           
                                                                                
       FD   BP13KA20                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13KA20.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
      *---------------------------------------------------------------*         
      *       HEADINGS FOR CONTROL DISPLAY                            *         
      *---------------------------------------------------------------*         
       01  L040S1-DL1.                                                          
           03  FILLER               PIC X(01)   VALUE SPACES.                   
           03  L040S1-DL-NUM        PIC 9       VALUE ZERO.                     
           03  FILLER               PIC X(3)    VALUE '.  '.                    
           03  L040S1-DL-DESCRPN-1  PIC X(50)   VALUE SPACES.                   
           03  L040S1-DL-DESCRPN-2  PIC X(16)   VALUE SPACES.                   
           03  L040S1-DL-COUNTER    PIC ZZZZZZ9.                                
           03  FILLER               PIC X(39)   VALUE SPACES.                   
                                                                                
       77  WS-YEAR                   PIC 9(4)    VALUE ZEROS.                   
       77  WS-NO-OF-REC              PIC 9(3)    VALUE 4.                       
       77  WS-READ-CNT               PIC 9(7)    VALUE ZEROS.                   
       77  WS-READ-K110              PIC 9(7)    VALUE ZEROS.                   
       77  WS-READ-BP13K800          PIC 9(7)    VALUE ZEROS.                   
       77  WS-READ-BP13K480          PIC 9(7)    VALUE ZEROS.                   
       77  WS-READ-BP13K670          PIC 9(7)    VALUE ZEROS.                   
       77  WS-REC-PASSED-TO-F430     PIC 9(7)    VALUE ZEROS.                   
       77  BM06K110-STATUS           PIC 9(2)    VALUE 0.                       
       77  BP13K800-STATUS           PIC 9(2)    VALUE 0.                       
       77  BP13K480-STATUS           PIC 9(2)    VALUE 0.                       
       77  BP13KD00-STATUS           PIC 9(2)    VALUE 0.                       
       77  BP13KD15-STATUS           PIC 9(2)    VALUE 0.                       
       77  BP13K670-STATUS           PIC 9(2)    VALUE 0.                       
       77  BP13KA20-STATUS           PIC 9(2)    VALUE 0.                       
                                                                                
       01  WS-VARIABLES GLOBAL.                                                 
           05  WS-RET-CODE                  PIC 9(2)  VALUE ZEROES.             
           05  WS-K000-STATUS               PIC 9(2)  VALUE ZEROES.             
           05  WS-K100-STATUS               PIC 9(2)  VALUE ZEROES.             
           05  WS-K110-STATUS               PIC 9(2)  VALUE ZEROES.             
           05  WS-K120-STATUS               PIC 9(2)  VALUE ZEROES.             
           05  WS-K100-FND                  PIC X     VALUE 'N'.                
           05  WS-K110-FND                  PIC X     VALUE 'N'.                
           05  WS-K120-FND                  PIC X     VALUE 'N'.                
           05  WS-K000-FND                  PIC X     VALUE 'N'.                
                                                                                
       01  WS-DTE-EFFECTIVE.                                                    
           05  WS-EFF-CCYY                  PIC 9(4).                           
           05  WS-EFF-MM                    PIC 9(2).                           
           05  WS-EFF-DD                    PIC 9(2).                           
                                                                                
       01  WS-DTE-SELECTION.                                                    
           05  WS-SEL-CCYY                  PIC 9(4).                           
           05  WS-SEL-MM                    PIC 9(2).                           
           05  WS-SEL-DD                    PIC 9(2).                           
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CENT             PIC X(2).                               
           05  WS-CURR-DTE.                                                     
               10  WS-CURR-YY           PIC 9(2).                               
               10  WS-CURR-MM           PIC 9(2).                               
               10  WS-CURR-DD           PIC 9(2).                               
                                                                                
       01  WS-DTE-INSTAL-COMMENCE.                                              
           05  WS-DTE-INST-COMM-CCYY        PIC 9(4).                           
           05  WS-DTE-INST-COMM-MM          PIC 9(2).                           
           05  WS-DTE-INST-COMM-DD          PIC 9(2).                           
                                                                                
       01  WS-DTE-CCYYMMDD.                                                     
           05  WS-DTE-CCYY                  PIC 9(4).                           
           05  WS-DTE-MM                    PIC 9(2).                           
           05  WS-DTE-DD                    PIC 9(2).                           
                                                                                
       01  WS-DTE-NVP                       PIC 9(8).                           
       01  WS-DTE-NVP-NUM                   PIC 9(7).                           
       01  WS-DTE-TP                        PIC 9(8).                           
       01  WS-DTE-TP-NUM                    PIC 9(7).                           
       01  WS-DTE-SC-REBATE                 PIC 9(8).                           
       01  WS-DTE-PROP-TAX                  PIC 9(8).                           
       01  WS-DTE-PROP-TAX-NUM              PIC 9(7).                           
                                                                                
       01  WS-F430-DTE-LOAN-EXPIRY.                                             
           03  WS-F430-LOAN-EXPIRY-CCYY     PIC 9(4).                           
           03  WS-F430-LOAN-EXPIRY-MM       PIC 9(2).                           
                                                                                
       01  WS-DTE-INSTAL-COMM-NEW           PIC 9(8).                           
       01  WS-DTE-LOAN-EXP-NEW              PIC 9(8).                           
       01  WS-DTE-LOAN-EXP-INT              PIC 9(8).                           
       01  WS-MONTHS-LOAN-COMM              PIC 9(5)   VALUE ZERO.              
       01  WS-DTE-LOAN-COMM                 PIC 9(8).                           
       01  WS-REMAINDER                     PIC 9(3)   VALUE ZEROES.            
       01  WS-QUOTIENT                      PIC 9(2)   VALUE ZEROES.            
                                                                                
       01  WS-PROGRAM-SWITCHES.                                                 
           03  WS-EOF1         PIC X(2)    VALUE 'N'.                           
                                                                                
      *----------------------*                                                  
       PROCEDURE DIVISION.                                                      
      *----------------------*                                                  
                                                                                
      *----------------------*                                                  
       1000-MAIN-PROCEDURE.                                                     
      *----------------------*                                                  
           PERFORM 2000-OPENING-ROUTINE      THRU 2000-EXIT.                    
           PERFORM 3000-READ-F410-REC        THRU 3000-EXIT.                    
           PERFORM 4000-PROCESS-BP13F410     THRU 4000-EXIT                     
                   UNTIL WS-EOF1 = 'Y'.                                         
           PERFORM 8000-DISPLAY-CONTROL-LIST THRU 8000-EXIT.                    
           PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       2000-OPENING-ROUTINE.                                                    
      *----------------------*                                                  
           OPEN INPUT BP13F410                                                  
                      BM06K110                                                  
                      BP13K800                                                  
                      BP13KD00                                                  
                      BP13KD15                                                  
                      BP13KA20                                                  
                      BP13K670                                                  
                      BP17K000                                                  
                      BP17K100                                                  
                      BP17K110                                                  
                      BP17K120                                                  
                I-O   BP13K480                                                  
               OUTPUT BP13F430.                                                 
                                                                                
             IF BM06K110-STATUS NOT = 0 AND 97                                  
                DISPLAY 'OPENING ERROR K110-STATUS ' BM06K110-STATUS            
                PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT.               
                                                                                
             IF BP13K800-STATUS NOT = 0 AND 97                                  
                DISPLAY 'OPENING ERROR K800-STATUS ' BP13K800-STATUS            
                PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT.               
                                                                                
             IF BP13K480-STATUS NOT = 0 AND 97                                  
                DISPLAY 'OPENING ERROR K480-STATUS ' BP13K480-STATUS            
                PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT.               
                                                                                
             IF BP13KD00-STATUS NOT = 0 AND 97                                  
                DISPLAY 'OPENING ERROR KD00-STATUS ' BP13KD00-STATUS            
                PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT.               
                                                                                
             IF BP13KD15-STATUS NOT = 0 AND 97                                  
                DISPLAY 'OPENING ERROR KD15-STATUS ' BP13KD15-STATUS            
                PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT.               
                                                                                
             IF BP13K670-STATUS NOT = 0 AND 97                                  
                DISPLAY 'OPENING ERROR K670-STATUS ' BP13K670-STATUS            
                PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT.               
                                                                                
            IF WS-K000-STATUS NOT = ZEROES AND 97                               
                DISPLAY 'ERROR IN OPEN BP17K000 : ' WS-K000-STATUS              
                MOVE WS-K000-STATUS           TO WS-RET-CODE                    
                PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT                
            END-IF.                                                             
                                                                                
            IF WS-K100-STATUS NOT = ZEROES AND 97                               
                DISPLAY 'ERROR IN OPEN BP17K100 : ' WS-K100-STATUS              
                MOVE WS-K100-STATUS           TO WS-RET-CODE                    
                PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT                
            END-IF.                                                             
                                                                                
            IF WS-K110-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'ERROR IN OPEN BP17K110 : ' WS-K110-STATUS               
               MOVE WS-K110-STATUS           TO WS-RET-CODE                     
               PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT                 
            END-IF.                                                             
                                                                                
            IF WS-K120-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'ERROR IN OPEN BP17K120 : ' WS-K120-STATUS               
               MOVE WS-K120-STATUS           TO WS-RET-CODE                     
               PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT                 
            END-IF.                                                             
                                                                                
            IF BP13KA20-STATUS NOT = ZEROES AND 97                              
               DISPLAY 'ERROR IN OPEN BP13KA20 : ' BP13KA20-STATUS              
               MOVE BP13KA20-STATUS           TO WS-RET-CODE                    
               PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT                 
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       3000-READ-F410-REC.                                                      
      *----------------------*                                                  
           READ BP13F410 AT END MOVE 'Y' TO WS-EOF1                             
                GO TO 3000-EXIT.                                                
           ADD 1 TO WS-READ-CNT.                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       4000-PROCESS-BP13F410.                                                   
      *----------------------*                                                  
           PERFORM 4100-FORMAT-FIN-SALES-ORDER THRU 4100-EXIT.                  
           PERFORM 3000-READ-F410-REC          THRU 3000-EXIT.                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------*                                           
       4001-PROCESS-SALES-TYPE-MOP.                                             
      *-----------------------------*                                           
                                                                                
           EVALUATE F410-CDE-SALES-TYPE                                         
               WHEN '13'                                                        
               WHEN '18'                                                        
                                                                                
                    MOVE 999      TO F430-NUM-REQUISITE-MOP                     
                    MOVE F410-DTE-SO-EFFECTIVE                                  
                                  TO F430-DTE-MOP-COMMENCEMENT                  
               WHEN '19'                                                        
                                                                                
                    MOVE 20       TO F430-NUM-REQUISITE-MOP                     
                    MOVE F410-DTE-SO-EFFECTIVE                                  
                                  TO F430-DTE-MOP-COMMENCEMENT                  
               WHEN OTHER                                                       
                                                                                
                    IF (K800-NUM-ALLO-CAT      = 'SER') OR                      
                       (K800-NUM-PORT-ELIG-TAG = 'Y')                           
                       PERFORM 4002-PROCESS-SERS-MOP  THRU 4002-EXIT            
                    ELSE                                                        
                       MOVE 5     TO F430-NUM-REQUISITE-MOP                     
                       MOVE F410-DTE-SO-EFFECTIVE                               
                                  TO F430-DTE-MOP-COMMENCEMENT                  
                    END-IF                                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       4002-PROCESS-SERS-MOP.                                                   
      *-----------------------*                                                 
                                                                                
           IF F410-DTE-SO-EFFECTIVE > 0                                         
              MOVE F410-DTE-SO-EFFECTIVE   TO WS-DTE-EFFECTIVE                  
                                                                                
              COMPUTE WS-EFF-CCYY = WS-EFF-CCYY + 5                             
           END-IF.                                                              
                                                                                
           IF K110-DTE-ACCEPTANCE > 0                                           
              MOVE K110-DTE-ACCEPTANCE     TO WS-DTE-SELECTION                  
                                                                                
              COMPUTE WS-SEL-CCYY = WS-SEL-CCYY + 7                             
           END-IF.                                                              
                                                                                
           IF K800-NUM-PORT-ELIG-TAG = 'Y'                                      
              MOVE 5                  TO F430-NUM-REQUISITE-MOP                 
              MOVE F410-DTE-SO-EFFECTIVE TO F430-DTE-MOP-COMMENCEMENT           
           ELSE                                                                 
             IF (F410-DTE-SO-EFFECTIVE > 0) AND                                 
                (K110-DTE-ACCEPTANCE   > 0)                                     
                                                                                
                IF WS-DTE-EFFECTIVE < WS-DTE-SELECTION                          
                   MOVE 5                  TO F430-NUM-REQUISITE-MOP            
                   MOVE F410-DTE-SO-EFFECTIVE                                   
                                         TO F430-DTE-MOP-COMMENCEMENT           
                                                                                
                ELSE                                                            
                   MOVE 7                TO F430-NUM-REQUISITE-MOP              
                   MOVE K110-DTE-ACCEPTANCE                                     
                                         TO F430-DTE-MOP-COMMENCEMENT           
                END-IF                                                          
             END-IF                                                             
           END-IF                                                               
                                                                                
           MOVE K800-NUM-REGN            TO K000-NUM-REGNO                      
           CALL 'BP13C41R'                                                      
           IF WS-K100-FND = 'Y' AND                                             
              K100-DTE-ANNOUNCE >= '20190101'                                   
              DISPLAY 'K100-DTE-ANNOUNCE' K100-DTE-ANNOUNCE                     
              MOVE 5                     TO F430-NUM-REQUISITE-MOP              
              MOVE F410-DTE-SO-EFFECTIVE                                        
                                         TO F430-DTE-MOP-COMMENCEMENT           
           END-IF.                                                              
                                                                                
       4002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------*                                            
       4100-FORMAT-FIN-SALES-ORDER.                                             
      *----------------------------*                                            
           MOVE SPACES                     TO BP13F430-REC.                     
           INITIALIZE BP13F430-REC.                                             
                                                                                
           MOVE F410-NUM-SCH-ACC           TO F430-NUM-HDB-REF.                 
           MOVE F410-NUM-REGN              TO F430-NUM-REG.                     
           MOVE F410-CDE-IDP-SCH           TO F430-CDE-IDP-SCH.                 
                                                                                
                                                                                
           MOVE F410-NRIC1                 TO F430-NUM-UIN1.                    
           INSPECT F430-NUM-UIN1 REPLACING ALL LOW-VALUES BY ' '.               
           MOVE F410-NRIC2                 TO F430-NUM-UIN2.                    
           INSPECT F430-NUM-UIN2 REPLACING ALL LOW-VALUES BY ' '.               
           MOVE F410-NRIC3                 TO F430-NUM-UIN3.                    
           INSPECT F430-NUM-UIN3 REPLACING ALL LOW-VALUES BY ' '.               
           MOVE F410-NRIC4                 TO F430-NUM-UIN4.                    
           INSPECT F430-NUM-UIN4 REPLACING ALL LOW-VALUES BY ' '.               
                                                                                
           MOVE F410-CPF1                  TO F430-NUM-CPF1.                    
           INSPECT F430-NUM-CPF1      REPLACING ALL LOW-VALUES BY ' '.          
           MOVE F410-CPF2                  TO F430-NUM-CPF2.                    
           INSPECT F430-NUM-CPF2      REPLACING ALL LOW-VALUES BY ' '.          
           MOVE F410-CPF3                  TO F430-NUM-CPF3.                    
           INSPECT F430-NUM-CPF3      REPLACING ALL LOW-VALUES BY ' '.          
           MOVE F410-CPF4                  TO F430-NUM-CPF4.                    
           INSPECT F430-NUM-CPF4      REPLACING ALL LOW-VALUES BY ' '.          
                                                                                
           IF   F410-AMT-MTHLY-CPF1 IS NOT NUMERIC                              
           MOVE ZEROES                     TO F430-AMT-CPF-RATE1                
           ELSE                                                                 
           MOVE F410-AMT-MTHLY-CPF1        TO F430-AMT-CPF-RATE1.               
                                                                                
           IF   F410-AMT-MTHLY-CPF2 IS NOT NUMERIC                              
           MOVE ZEROES                     TO F430-AMT-CPF-RATE2                
           ELSE                                                                 
           MOVE F410-AMT-MTHLY-CPF2        TO F430-AMT-CPF-RATE2.               
                                                                                
           IF   F410-AMT-MTHLY-CPF3 IS NOT NUMERIC                              
           MOVE ZEROES                     TO F430-AMT-CPF-RATE3                
           ELSE                                                                 
           MOVE F410-AMT-MTHLY-CPF3        TO F430-AMT-CPF-RATE3.               
                                                                                
           IF   F410-AMT-MTHLY-CPF4 IS NOT NUMERIC                              
           MOVE ZEROES                     TO F430-AMT-CPF-RATE4                
           ELSE                                                                 
           MOVE F410-AMT-MTHLY-CPF4        TO F430-AMT-CPF-RATE4.               
                                                                                
           MOVE F410-NAME1                 TO F430-NME-LESSEE1.                 
           INSPECT F430-NME-LESSEE1 REPLACING ALL LOW-VALUES BY ' '.            
                                                                                
           MOVE F410-NAME2                 TO F430-NME-LESSEE2.                 
           INSPECT F430-NME-LESSEE2 REPLACING ALL LOW-VALUES BY ' '.            
                                                                                
           MOVE F410-NAME3                 TO F430-NME-LESSEE3.                 
           INSPECT F430-NME-LESSEE3 REPLACING ALL LOW-VALUES BY ' '.            
                                                                                
           MOVE F410-NAME4                 TO F430-NME-LESSEE4.                 
           INSPECT F430-NME-LESSEE4 REPLACING ALL LOW-VALUES BY ' '.            
                                                                                
           MOVE F410-SEX1                  TO F430-CDE-SEX1.                    
           IF F430-CDE-SEX1 = LOW-VALUES                                        
              MOVE SPACE TO F430-CDE-SEX1.                                      
                                                                                
           MOVE F410-SEX2                  TO F430-CDE-SEX2.                    
           IF F430-CDE-SEX2 = LOW-VALUES                                        
              MOVE SPACE TO F430-CDE-SEX2.                                      
                                                                                
           MOVE F410-SEX3                  TO F430-CDE-SEX3.                    
           IF F430-CDE-SEX3 = LOW-VALUES                                        
              MOVE SPACE TO F430-CDE-SEX3.                                      
                                                                                
           MOVE F410-SEX4                  TO F430-CDE-SEX4.                    
           IF F430-CDE-SEX4 = LOW-VALUES                                        
              MOVE SPACE TO F430-CDE-SEX4.                                      
                                                                                
           IF F410-RACE1 = '1'                                                  
              MOVE 'M' TO F430-CDE-RACE1                                        
           ELSE                                                                 
           IF F410-RACE1 = '2'                                                  
              MOVE 'C' TO F430-CDE-RACE1                                        
           ELSE                                                                 
           IF F410-RACE1 = '3'                                                  
              MOVE 'I' TO F430-CDE-RACE1                                        
           ELSE                                                                 
           IF F410-RACE1 = '4'                                                  
              MOVE 'O' TO F430-CDE-RACE1                                        
           ELSE                                                                 
              MOVE SPACE TO F430-CDE-RACE1.                                     
                                                                                
           IF F410-RACE2 = '1'                                                  
              MOVE 'M' TO F430-CDE-RACE2                                        
           ELSE                                                                 
           IF F410-RACE2 = '2'                                                  
              MOVE 'C' TO F430-CDE-RACE2                                        
           ELSE                                                                 
           IF F410-RACE2 = '3'                                                  
              MOVE 'I' TO F430-CDE-RACE2                                        
           ELSE                                                                 
           IF F410-RACE2 = '4'                                                  
              MOVE 'O' TO F430-CDE-RACE2                                        
           ELSE                                                                 
              MOVE SPACE TO F430-CDE-RACE2.                                     
                                                                                
           IF F410-RACE3 = '1'                                                  
              MOVE 'M' TO F430-CDE-RACE3                                        
           ELSE                                                                 
           IF F410-RACE3 = '2'                                                  
              MOVE 'C' TO F430-CDE-RACE3                                        
           ELSE                                                                 
           IF F410-RACE3 = '3'                                                  
              MOVE 'I' TO F430-CDE-RACE3                                        
           ELSE                                                                 
           IF F410-RACE3 = '4'                                                  
              MOVE 'O' TO F430-CDE-RACE3                                        
           ELSE                                                                 
              MOVE SPACE TO F430-CDE-RACE3.                                     
                                                                                
           IF F410-RACE4 = '1'                                                  
              MOVE 'M' TO F430-CDE-RACE4                                        
           ELSE                                                                 
           IF F410-RACE4 = '2'                                                  
              MOVE 'C' TO F430-CDE-RACE4                                        
           ELSE                                                                 
           IF F410-RACE4 = '3'                                                  
              MOVE 'I' TO F430-CDE-RACE4                                        
           ELSE                                                                 
           IF F410-RACE4 = '4'                                                  
              MOVE 'O' TO F430-CDE-RACE4                                        
           ELSE                                                                 
              MOVE SPACE TO F430-CDE-RACE4.                                     
                                                                                
           MOVE F410-CDE-ELIG-SCH       TO F430-CDE-ELIG-SCH.                   
           MOVE F410-CDE-HPI-SCH        TO F430-CDE-HPI.                        
           MOVE ' '                     TO F430-CDE-CPF-STATUS.                 
                                                                                
           MOVE F410-NATIONALITY1       TO F430-CDE-NAT1.                       
           IF F430-CDE-NAT1 = LOW-VALUES                                        
              MOVE SPACE TO F430-CDE-NAT1.                                      
                                                                                
           MOVE F410-NATIONALITY2       TO F430-CDE-NAT2.                       
           IF F430-CDE-NAT2 = LOW-VALUES                                        
              MOVE SPACE TO F430-CDE-NAT2.                                      
                                                                                
           MOVE F410-NATIONALITY3       TO F430-CDE-NAT3.                       
           IF F430-CDE-NAT3 = LOW-VALUES                                        
              MOVE SPACE TO F430-CDE-NAT3.                                      
                                                                                
           MOVE F410-NATIONALITY4       TO F430-CDE-NAT4.                       
           IF F430-CDE-NAT4 = LOW-VALUES                                        
              MOVE SPACE TO F430-CDE-NAT4.                                      
                                                                                
           IF F410-CDE-CATEGORY = '1'                                           
              MOVE 'M' TO F430-CDE-HSEHOLD-CAT                                  
           ELSE                                                                 
           IF F410-CDE-CATEGORY = '2'                                           
              MOVE 'C' TO F430-CDE-HSEHOLD-CAT                                  
           ELSE                                                                 
           IF F410-CDE-CATEGORY = '3'                                           
              MOVE 'I' TO F430-CDE-HSEHOLD-CAT                                  
           ELSE                                                                 
           IF F410-CDE-CATEGORY = '4'                                           
              MOVE 'O' TO F430-CDE-HSEHOLD-CAT                                  
           ELSE                                                                 
              MOVE SPACE TO F430-CDE-HSEHOLD-CAT.                               
                                                                                
           IF F410-CDE-ASPF = 'Y'                                               
              MOVE 'Y'                  TO F430-CDE-ASPF                        
           ELSE                                                                 
              MOVE 'N'                  TO F430-CDE-ASPF.                       
                                                                                
           IF F410-CDE-PAYMENT-CD = 1 OR 5                                      
              MOVE 1                    TO F430-CDE-LOAN-REPYMT.                
           IF F410-CDE-PAYMENT-CD = 2                                           
              MOVE 2                    TO F430-CDE-LOAN-REPYMT.                
           IF F410-CDE-PAYMENT-CD = 3 OR 6                                      
              MOVE 3                    TO F430-CDE-LOAN-REPYMT.                
           IF F410-CDE-PAYMENT-CD = 4                                           
              MOVE 4                    TO F430-CDE-LOAN-REPYMT.                
                                                                                
           IF F410-NUM-REPAY-PERIOD NOT NUMERIC                                 
              MOVE ZEROES               TO F410-NUM-REPAY-PERIOD.               
           MOVE F410-NUM-REPAY-PERIOD   TO F430-NUM-LOAN-REPYMT-PERIOD.         
                                                                                
           IF F410-DTE-SO-EFFECTIVE NOT NUMERIC                                 
              MOVE ZEROES               TO F410-DTE-SO-EFFECTIVE.               
           MOVE F410-DTE-SO-EFFECTIVE   TO F430-DTE-SALE-EFF.                   
                                                                                
                                                                                
           MOVE SPACES                  TO F430-DTE-BOND-EXPIRY.                
                                                                                
           IF F410-CDE-ERL-SCH = 'A'                                            
              MOVE '1'                     TO F430-CDE-ERL                      
           ELSE                                                                 
           IF F410-CDE-ERL-SCH = 'B'                                            
              MOVE '2'                     TO F430-CDE-ERL                      
           ELSE                                                                 
           IF F410-CDE-ERL-SCH = 'C'                                            
              MOVE '3'                     TO F430-CDE-ERL                      
           ELSE                                                                 
           IF F410-CDE-ERL-SCH = '8'                                            
              MOVE '8'                     TO F430-CDE-ERL                      
              MOVE F410-NUM-FRS-PERCENT    TO F430-NUM-FRS-PERCENT              
           ELSE                                                                 
              MOVE SPACES                  TO F430-CDE-ERL.                     
                                                                                
           IF F410-CDE-ERL-SCH = 'A' OR 'B' OR 'C' OR 'D' OR '8'                
              MOVE F410-DTE-SO-EFFECTIVE   TO F430-DTE-ERL.                     
                                                                                
           IF (F410-CDE-SALES-TYPE = '10')                                      
              IF F410-CDE-ERL-SCH = 'A' OR 'B' OR 'C' OR 'D' OR '8'             
                 MOVE '1B'                 TO F430-CDE-SALES-TYP                
              ELSE                                                              
                 MOVE '10'                 TO F430-CDE-SALES-TYP.               
                                                                                
           IF (F410-CDE-SALES-TYPE = '01')                                      
              IF F410-CDE-ERL-SCH = 'A' OR 'B' OR 'C' OR 'D' OR '8'             
                 MOVE '1A'                 TO F430-CDE-SALES-TYP                
              ELSE                                                              
                 MOVE '1 '                 TO F430-CDE-SALES-TYP.               
                                                                                
           IF F410-CDE-SALES-TYPE = '02'                                        
              MOVE '2 '                    TO F430-CDE-SALES-TYP.               
                                                                                
           IF F410-CDE-SALES-TYPE = '2B'                                        
              MOVE '2B'                    TO F430-CDE-SALES-TYP.               
                                                                                
           IF F410-CDE-SALES-TYPE = '03'                                        
              MOVE '3 '                    TO F430-CDE-SALES-TYP.               
                                                                                
           IF F410-CDE-SALES-TYPE = '3A'                                        
              MOVE '3A'                    TO F430-CDE-SALES-TYP.               
                                                                                
           IF F410-CDE-SALES-TYPE = '5B' OR '5C'                                
              MOVE F410-CDE-SALES-TYPE     TO F430-CDE-SALES-TYP.               
                                                                                
           IF F410-CDE-SALES-TYPE = '13'                                        
              MOVE '13'                    TO F430-CDE-SALES-TYP.               
           IF F410-CDE-SALES-TYPE = '14'                                        
              MOVE '14'                    TO F430-CDE-SALES-TYP.               
           IF F410-CDE-SALES-TYPE = '15'                                        
              MOVE '15'                    TO F430-CDE-SALES-TYP.               
           IF F410-CDE-SALES-TYPE = '18'                                        
              MOVE '18'                    TO F430-CDE-SALES-TYP.               
                                                                                
           IF F410-CDE-SALES-TYPE = '19'                                        
              MOVE '19'                    TO F430-CDE-SALES-TYP.               
                                                                                
                                                                                
                                                                                
           COMPUTE F430-AMT-STAMP-FEE = F410-AMT-PREMIUM-STAMP-FEE +            
                                        F410-AMT-LEASE-ESCROW-FEE  +            
                                        F410-AMT-LEASE-REGN-FEE    +            
                                        F410-AMT-MORT-ESCROW-FEE   +            
                                        F410-AMT-MORT-STAMP-FEE    +            
                                        F410-AMT-MORT-REGN-FEE.                 
                                                                                
           MOVE F410-AMT-SURVEY-FEE  TO F430-AMT-SURVEY-FEE.                    
           IF   F410-AMT-SURVEY-FEE-GST  IS NOT NUMERIC                         
             MOVE ZEROES                   TO F430-AMT-SURVEY-FEE-GST           
           ELSE                                                                 
             MOVE F410-AMT-SURVEY-FEE-GST  TO F430-AMT-SURVEY-FEE-GST.          
                                                                                
           MOVE F410-AMT-CONVEY-FEE  TO F430-AMT-CC-FEE.                        
           IF   F410-AMT-CONVEY-FEE-GST  IS NOT NUMERIC                         
             MOVE ZEROES                   TO F430-AMT-CC-FEE-GST               
           ELSE                                                                 
             MOVE F410-AMT-CONVEY-FEE-GST  TO F430-AMT-CC-FEE-GST.              
                                                                                
           COMPUTE F430-AMT-STAMP-SURVEY-CC-FEE = F430-AMT-STAMP-FEE  +         
                                                  F430-AMT-SURVEY-FEE +         
                                                  F430-AMT-CC-FEE.              
                                                                                
           MOVE F410-AMT-REV-LOAN-INSTAL                                        
                                      TO F430-AMT-REV-LOAN-INSTAL-NEW.          
                                                                                
           MOVE F410-AMT-REV-SUB-INSTAL                                         
                                      TO F430-AMT-REV-SUB-INSTAL-NEW.           
                                                                                
           MOVE F410-AMT-LOAN-INSTAL     TO F430-AMT-LOAN-INSTAL-NEW.           
                                                                                
           MOVE F410-AMT-SUB-INSTAL      TO F430-AMT-SUB-INSTAL-NEW.            
                                                                                
           MOVE F410-AMT-SELLING-PRICE   TO F430-AMT-SELL-PRICE.                
           MOVE F410-AMT-SELLING-PRICE   TO F430-AMT-SELL-PRICE-NEW.            
           MOVE F410-AMT-LOAN            TO F430-AMT-LOAN-GRANTD.               
                                                                                
           IF F410-AMT-MTHLY-CPF1 IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-MTHLY-CPF1.                              
           IF F410-AMT-MTHLY-CPF2 IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-MTHLY-CPF2.                              
           IF F410-AMT-MTHLY-CPF3 IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-MTHLY-CPF3.                              
           IF F410-AMT-MTHLY-CPF4 IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-MTHLY-CPF4.                              
                                                                                
           COMPUTE F430-AMT-CPF-RATE = F410-AMT-MTHLY-CPF1 +                    
                                       F410-AMT-MTHLY-CPF2 +                    
                                       F410-AMT-MTHLY-CPF3 +                    
                                       F410-AMT-MTHLY-CPF4.                     
                                                                                
           IF  F410-AMT-BK-TRANSFER IS NOT NUMERIC                              
           MOVE ZEROS                TO    F410-AMT-BK-TRANSFER.                
                                                                                
           IF  F410-AMT-RDE-TRANSFER IS NOT NUMERIC                             
           MOVE ZEROS                TO    F410-AMT-RDE-TRANSFER.               
                                                                                
           IF F410-AMT-CD-CASH    IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-CD-CASH.                                 
           IF F410-AMT-CD-CPF1    IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-CD-CPF1.                                 
           IF F410-AMT-CD-CPF2    IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-CD-CPF2.                                 
           IF F410-AMT-CD-CPF3    IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-CD-CPF3.                                 
           IF F410-AMT-CD-CPF4    IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-CD-CPF4.                                 
           IF F410-AMT-CD-SERCNTR IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-CD-SERCNTR.                              
                                                                                
           IF F410-AMT-ICP-CASH   IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-ICP-CASH.                                
           IF F410-AMT-ICP-CPF1   IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-ICP-CPF1.                                
           IF F410-AMT-ICP-CPF2   IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-ICP-CPF2.                                
           IF F410-AMT-ICP-CPF3   IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-ICP-CPF3.                                
           IF F410-AMT-ICP-CPF4   IS NOT NUMERIC                                
              MOVE ZEROES TO  F410-AMT-ICP-CPF4.                                
           IF F410-AMT-SER-CONTRA-ICP  IS NOT NUMERIC                           
              MOVE ZEROES TO  F410-AMT-SER-CONTRA-ICP.                          
           IF F410-AMT-LOAN-QUANTUM    IS NOT NUMERIC                           
              MOVE ZEROES TO  F410-AMT-LOAN-QUANTUM.                            
           IF F410-AMT-CASH-PROCEED-USE IS NOT NUMERIC                          
              MOVE ZEROES TO  F410-AMT-CASH-PROCEED-USE.                        
                                                                                
           IF F410-AMT-SER-CONTRA-STAMP-FEES IS NOT NUMERIC                     
              MOVE ZEROES TO  F410-AMT-SER-CONTRA-STAMP-FEES.                   
           IF F410-AMT-SER-CONTRA-CONVY-FEES IS NOT NUMERIC                     
              MOVE ZEROES TO  F410-AMT-SER-CONTRA-CONVY-FEES.                   
           IF F410-AMT-SER-CONTRA-MISC-FEE   IS NOT NUMERIC                     
              MOVE ZEROES TO  F410-AMT-SER-CONTRA-MISC-FEE.                     
                                                                                
           IF F410-NUM-BUYBACK           = 'Y' OR 'N'                           
              COMPUTE F430-AMT-TOT-CD-IDP   = F410-AMT-BK-TRANSFER              
                                            + F410-AMT-RDE-TRANSFER             
           ELSE                                                                 
           IF F410-CDE-SA-TP             = 'Y'                                  
              COMPUTE F430-AMT-TOT-CD-IDP   = F410-AMT-BK-TRANSFER              
                                            + F410-AMT-RDE-TRANSFER             
           ELSE                                                                 
           IF F410-AMT-BK-TRANSFER > ZEROES                                     
              COMPUTE F430-AMT-TOT-CD-IDP   = F410-AMT-BK-TRANSFER              
                                            + F410-AMT-RDE-TRANSFER             
           ELSE                                                                 
              COMPUTE F430-AMT-TOT-CD-IDP   = F410-AMT-CD-CASH +                
                                              F410-AMT-CD-SERCNTR +             
                                              F410-AMT-CD-CPF1 +                
                                              F410-AMT-CD-CPF2 +                
                                              F410-AMT-CD-CPF3 +                
                                              F410-AMT-CD-CPF4.                 
                                                                                
      * FOR SERS CASES TO INCLUDE SER-CONTRA.                                   
      * FOR SERS CASES TO INCLUDE SER-CONTRA FOR 2 RM FLEXI                     
           IF F410-CDE-SALES-TYPE = '13' OR '18'                                
              COMPUTE F430-AMT-ICP  =  F410-AMT-ICP-CASH +                      
                                       F410-AMT-ICP-CPF1 +                      
                                       F410-AMT-ICP-CPF2 +                      
                                       F410-AMT-ICP-CPF3 +                      
                                       F410-AMT-ICP-CPF4 +                      
                                       F410-AMT-CASH-PROCEED-USE +              
                                       F410-AMT-SER-CONTRA-ICP +                
                                       F410-AMT-SER-CONTRA-STAMP-FEES +         
                                       F410-AMT-SER-CONTRA-MISC-FEE +           
                                       F410-AMT-SER-CONTRA-CONVY-FEES           
           ELSE                                                                 
              COMPUTE F430-AMT-ICP  =  F410-AMT-ICP-CASH +                      
                                       F410-AMT-ICP-CPF1 +                      
                                       F410-AMT-ICP-CPF2 +                      
                                       F410-AMT-ICP-CPF3 +                      
                                       F410-AMT-ICP-CPF4 +                      
                                       F410-AMT-CASH-PROCEED-USE +              
                                       F410-AMT-LOAN-QUANTUM +                  
                                       F410-AMT-SER-CONTRA-ICP                  
           END-IF.                                                              
                                                                                
      * FOR CREDIT ASSESSMENT .                                                 
           MOVE F410-NUM-CA-TAG          TO F430-CDE-CREDIT-ASSESSMENT.         
           IF  F410-DTE-REV-INSTAL-EFFECTIVE IS NOT NUMERIC                     
               MOVE  SPACES      TO     F430-DTE-REV-INSTAL-EFFECTIVE           
           ELSE                                                                 
               MOVE  F410-DTE-REV-INSTAL-EFFECTIVE  TO                          
                                        F430-DTE-REV-INSTAL-EFFECTIVE           
           END-IF.                                                              
                                                                                
           IF  F410-NUM-SHR1 IS NOT NUMERIC                                     
               MOVE ZEROES             TO F430-NUM-SHARE-FLAT1                  
           ELSE                                                                 
               MOVE F410-NUM-SHR1      TO F430-NUM-SHARE-FLAT1                  
           END-IF.                                                              
                                                                                
           IF  F410-NUM-SHR2 IS NOT NUMERIC                                     
               MOVE ZEROES             TO F430-NUM-SHARE-FLAT2                  
           ELSE                                                                 
               MOVE F410-NUM-SHR2      TO F430-NUM-SHARE-FLAT2                  
           END-IF.                                                              
                                                                                
           IF  F410-NUM-SHR3 IS NOT NUMERIC                                     
               MOVE ZEROES             TO F430-NUM-SHARE-FLAT3                  
           ELSE                                                                 
               MOVE F410-NUM-SHR3      TO F430-NUM-SHARE-FLAT3                  
           END-IF.                                                              
                                                                                
           IF  F410-NUM-SHR4 IS NOT NUMERIC                                     
               MOVE ZEROES             TO F430-NUM-SHARE-FLAT4                  
           ELSE                                                                 
               MOVE F410-NUM-SHR4      TO F430-NUM-SHARE-FLAT4                  
           END-IF.                                                              
                                                                                
           IF  F410-NUM-SHR-BASE IS NOT NUMERIC                                 
               MOVE ZEROES             TO F430-NUM-SHARE-FLAT-BASE              
           ELSE                                                                 
               MOVE F410-NUM-SHR-BASE  TO F430-NUM-SHARE-FLAT-BASE              
           END-IF.                                                              
                                                                                
           MOVE F410-CDE-HOLDING-TYPE  TO F430-CDE-HOLDING-TYPE.                
                                                                                
           MOVE F410-DTE-KEY-ISSUE     TO F430-DTE-EXE-SALE-AGRMT.              
           MOVE F410-AMT-PREMIUM-NEW   TO F430-AMT-PREMIUM-NEW.                 
                                                                                
           IF F410-NUM-LOAN-REPYMT-MAX IS NOT NUMERIC                           
              MOVE  ZERO     TO   F410-NUM-LOAN-REPYMT-MAX                      
           END-IF.                                                              
                                                                                
           IF F410-NUM-CA-TAG = 'E'                                             
              MOVE  30       TO   F410-NUM-LOAN-REPYMT-MAX                      
           END-IF.                                                              
                                                                                
           IF F410-NUM-CA-TAG = 'N' OR SPACES OR LOW-VALUES                     
              MOVE  25       TO   F410-NUM-LOAN-REPYMT-MAX                      
           END-IF.                                                              
                                                                                
           MOVE F410-NUM-LOAN-REPYMT-MAX  TO F430-NUM-LOAN-REPYMT-MAX           
           MOVE F410-NUM-FI-PERIOD        TO F430-NUM-FI-PERIOD.                
                                                                                
           IF F410-DTE-FI-EFFECTIVE = '00000000' OR LOW-VALUES OR SPACES        
              MOVE SPACES                 TO F430-DTE-FI-EFF                    
           ELSE                                                                 
              MOVE F410-DTE-FI-EFFECTIVE  TO F430-DTE-FI-EFF.                   
                                                                                
           MOVE F410-NUM-FI-COMPULSORY TO F430-NUM-FI-COMPULSORY.               
                                                                                
           IF F410-DTE-AGRMT-FOR-LEASE IS NOT NUMERIC                           
             MOVE SPACES                   TO F430-DTE-AGRMT-FOR-LEASE          
           ELSE                                                                 
             MOVE F410-DTE-AGRMT-FOR-LEASE TO F430-DTE-AGRMT-FOR-LEASE.         
                                                                                
           IF F410-DTE-SUB-TR-EFF IS NOT NUMERIC                                
              MOVE SPACES TO F430-DTE-EFF-SUB-TRANSFER                          
           ELSE                                                                 
             IF F410-DTE-SUB-TR-EFF  = ZEROS                                    
                MOVE SPACES TO F430-DTE-EFF-SUB-TRANSFER                        
             ELSE                                                               
                MOVE F410-DTE-SUB-TR-EFF TO F430-DTE-EFF-SUB-TRANSFER.          
                                                                                
           MOVE F410-NUM-INTEREST-RATE TO F430-PCT-INT-RATE.                    
                                                                                
           PERFORM 4130-CHECK-SCCC-ELGBTY THRU 4130-EXIT.                       
           PERFORM 4150-FIS-IMP-COST      THRU 4150-EXIT.                       
           PERFORM 4160-READ-BP13K800     THRU 4160-EXIT.                       
                                                                                
           PERFORM 4001-PROCESS-SALES-TYPE-MOP  THRU 4001-EXIT.                 
                                                                                
           MOVE F410-CDE-ALLOC-SCH      TO F430-CDE-ALLOC-SCH.                  
                                                                                
           IF K800-NUM-MGPS = 'P' OR 'C'                                        
              MOVE 'MGP'                TO F430-CDE-ALLOC-SCH                   
                                                                                
           END-IF.                                                              
           IF (K800-NUM-JT-BALLOT-REGN NOT = SPACES AND LOW-VALUES) AND         
              (K800-NUM-NEW-TOWN = 'QT ') AND                                   
              (K800-DTE-BALLOT = '200912')                                      
              MOVE 'MGD'                TO F430-CDE-ALLOC-SCH                   
           END-IF.                                                              
                                                                                
           IF K800-NUM-ALLO-SCHEME = 'MGL'                                      
              PERFORM 4180-READ-BP13K480     THRU 4180-EXIT                     
           END-IF.                                                              
                                                                                
                                                                                
           IF K800-NUM-BUY-SELL-TAG = 'B'                                       
              MOVE 'PB'                     TO F430-CDE-LOAN-SCH-POLICY         
           ELSE                                                                 
              MOVE F410-CDE-LOAN-SCH-POLICY TO F430-CDE-LOAN-SCH-POLICY         
           END-IF.                                                              
                                                                                
           IF F410-NUM-LOAN-TAG = 'B'                                           
             MOVE F410-NUM-ELIG-LOAN-POLICY TO F430-NUM-ELIG-LOAN-POLICY        
             MOVE F410-DTE-KEY-ISSUE        TO F430-DTE-BANK-LOAN-COMM          
             MOVE F410-AMT-LOAN-QUANTUM     TO F430-AMT-BANK-LOAN               
                                                                                
              PERFORM 4170-READ-BP13K670  THRU 4170-EXIT                        
                                                                                
              IF F410-NUM-LOAN-TAG-ORIG = 'H'                                   
                 IF F410-CDE-IDP-SCH = 'CGO'                                    
                    MOVE   'C' TO F430-NUM-ELIG-HDB-LOAN-SCH                    
                 ELSE                                                           
                   IF F410-CDE-IDP-SCH = 'SFB' OR 'SFD' OR 'SFF' OR             
                                         'SBN' OR 'SDN' OR 'SFN'                
                      MOVE 'S' TO F430-NUM-ELIG-HDB-LOAN-SCH                    
                   ELSE                                                         
                      MOVE 'P' TO F430-NUM-ELIG-HDB-LOAN-SCH                    
                   END-IF                                                       
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'M'      TO F430-NUM-ELIG-HDB-LOAN-SCH                    
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES      TO F430-NUM-BANK                                 
              MOVE SPACES      TO F430-DTE-BANK-LOAN-COMM                       
              MOVE ZEROES      TO F430-AMT-BANK-LOAN                            
              MOVE SPACES      TO F430-NUM-BANK-TRAN                            
              MOVE SPACES      TO F430-NUM-ELIG-HDB-LOAN-SCH                    
              MOVE SPACES      TO F430-NUM-ELIG-LOAN-POLICY                     
           END-IF.                                                              
                                                                                
           IF F410-CDE-LOAN-ELIG = 'A'                                          
             MOVE F410-NUM-ELIG-LOAN-POLICY TO F430-NUM-ELIG-LOAN-POLICY        
             MOVE 'A'  TO   F430-NUM-ELIG-HDB-LOAN-SCH                          
           END-IF.                                                              
                                                                                
           IF (F410-CDE-SALES-TYPE = '01' OR '02' OR '2B' OR '03' OR            
                                     '3A' OR '10' OR '13' OR '14' OR            
                                     '15')                                      
               MOVE 'Y' TO F430-CDE-SOC-FLAT                                    
           END-IF.                                                              
                                                                                
                                                                                
           IF F410-HH-CITZ = 'SCP' OR                                           
              K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCP'                            
              MOVE 'SCP'                   TO  F430-CDE-SPECIAL-SCH             
           END-IF.                                                              
                                                                                
           IF F410-NUM-ALLO-CAT = 'DBS'                                         
              PERFORM 6000-PROCEED-DBSS   THRU 6000-EXIT                        
           ELSE                                                                 
              PERFORM 6500-PROCEED-NONDBS THRU 6500-EXIT                        
           END-IF.                                                              
                                                                                
           IF F410-AMT-SC-SPR-PREMIUM NOT NUMERIC                               
              MOVE ZEROES               TO F410-AMT-SC-SPR-PREMIUM.             
                                                                                
           MOVE F410-AMT-SC-SPR-PREMIUM TO F430-AMT-SC-SPR-PREMIUM              
                                                                                
           IF F410-NUM-ALLO-CAT = 'DBS'                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF F410-AMT-SC-SPR-PREMIUM = 10000                                
                 COMPUTE F430-AMT-SELL-PRICE = F410-AMT-SELLING-PRICE           
                                             - F410-AMT-SC-SPR-PREMIUM          
             COMPUTE F430-AMT-SELL-PRICE-NEW = F410-AMT-SELLING-PRICE           
                                             - F410-AMT-SC-SPR-PREMIUM          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F410-NUM-AHG             TO F430-NUM-AHG-GRANT.                 
           IF F410-AMT-GST-RATE NOT NUMERIC                                     
              MOVE 7                     TO F430-AMT-GST-RATE                   
           ELSE                                                                 
              MOVE F410-AMT-GST-RATE     TO F430-AMT-GST-RATE                   
           END-IF.                                                              
           PERFORM 6600-DERIVE-LOAN-POLICY THRU 6600-EXIT.                      
                                                                                
           IF F410-AMT-LOAN-INSTAL > 0                                          
              NEXT SENTENCE                                                     
           ELSE                                                                 
           IF F410-NUM-CONTRA-TYPE  = 'TM'                                      
              PERFORM 4198-READ-BP13K480 THRU 4198-EXIT                         
              IF K480-NUM-CONTRA-TYPE = 'TM'                                    
                 PERFORM 7000-TEMP-LOAN  THRU 7000-EXIT                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE BP13F430-REC.                                                  
           ADD 1 TO WS-REC-PASSED-TO-F430.                                      
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------*                                                   
       4120-CHECK-LOAN-SCH.                                                     
      *---------------------*                                                   
           IF F410-CDE-LOAN-SCH = '1'                                           
              IF F410-CDE-LOAN-ELIG = 'A'                                       
                 MOVE 'A' TO F430-CDE-LOAN-SCH                                  
              ELSE                                                              
                 MOVE 'P' TO F430-CDE-LOAN-SCH                                  
              END-IF                                                            
           ELSE                                                                 
           IF F410-CDE-LOAN-SCH = '2'                                           
              MOVE 'C' TO F430-CDE-LOAN-SCH                                     
           ELSE                                                                 
           IF F410-CDE-LOAN-SCH = '3'                                           
              MOVE 'S' TO F430-CDE-LOAN-SCH                                     
           ELSE                                                                 
           IF F410-CDE-LOAN-SCH = '4'                                           
              MOVE 'M' TO F430-CDE-LOAN-SCH.                                    
                                                                                
       4120-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       4130-CHECK-SCCC-ELGBTY.                                                  
      *-----------------------*                                                 
                                                                                
           IF F410-NUM-SCCC-ELGBTY = 'Z'                                        
              MOVE 'Z'           TO F430-NUM-SCCC-ELGBTY                        
           ELSE                                                                 
           IF (F410-NUM-SCCC-ELGBTY = 'N') OR                                   
              (F410-CDE-ALLOC-SCH = 'SPO' OR 'SPS')                             
              MOVE 'P'           TO F430-NUM-SCCC-ELGBTY                        
           ELSE                                                                 
           IF F410-CDE-ALLOC-SCH = 'SPP'                                        
              MOVE 'N'           TO F430-NUM-SCCC-ELGBTY                        
           ELSE                                                                 
           IF (F410-CDE-ALLOC-SCH = 'SSP' OR 'SRP') AND                         
              (F410-NUM-SCCC-ELGBTY NOT = 'R')                                  
               MOVE 'N'           TO F430-NUM-SCCC-ELGBTY                       
           ELSE                                                                 
           IF F410-NUM-SCCC-ELGBTY = 'R' OR 'E'                                 
              MOVE 'Y'           TO F430-NUM-SCCC-ELGBTY                        
           ELSE                                                                 
              MOVE 'Y'           TO F430-NUM-SCCC-ELGBTY                        
           END-IF.                                                              
                                                                                
       4130-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       4150-FIS-IMP-COST.                                                       
      *-------------------*                                                     
           MOVE F410-NUM-SCH-ACC  TO K110-KEY-FLD OF BM06K110.                  
                                                                                
           READ BM06K110                                                        
             IF BM06K110-STATUS = 0                                             
                 IF  K110-AMT-SINGLES-PREMIUM IS NOT NUMERIC                    
                    MOVE  ZEROES TO K110-AMT-SINGLES-PREMIUM                    
                 END-IF                                                         
                 IF  K110-AMT-SINGLES-PREMIUM > ZEROES                          
                    MOVE  'SGL' TO F430-CDE-SPECIAL-SCH                         
                 END-IF                                                         
                                                                                
                 IF  K110-AMT-ELDERLY-PREMIUM IS NOT NUMERIC                    
                    MOVE  ZEROES TO K110-AMT-ELDERLY-PREMIUM                    
                 END-IF                                                         
                 IF  K110-AMT-ELDERLY-PREMIUM > ZEROES                          
                    MOVE  'ELD' TO F430-CDE-SPECIAL-SCH                         
                 END-IF                                                         
                                                                                
                 MOVE K110-IMPROVEMT-COST TO F430-AMT-IMPRVE-COST               
                 MOVE K110-PRICING-CODE   TO F430-CDE-PRICE-POLICY              
                 MOVE ZEROS               TO F430-AMT-ORIG-SELL-PRICE           
                 MOVE ZEROS             TO F430-AMT-ORIG-SELL-PRICE-NEW         
                 ADD 1                    TO WS-READ-K110                       
             ELSE                                                               
                 MOVE ZEROS               TO F430-AMT-IMPRVE-COST               
                 DISPLAY 'BM06K110-STATUS ' BM06K110-STATUS                     
                 DISPLAY 'F410-SCH-ACC    ' F410-NUM-SCH-ACC.                   
                                                                                
       4150-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       4160-READ-BP13K800.                                                      
      *-------------------*                                                     
           MOVE F410-NUM-REGN  TO K800-NUM-REGN.                                
                                                                                
           READ BP13K800                                                        
             IF BP13K800-STATUS = 0                                             
                 MOVE K800-DTE-REGN       TO F430-DTE-REGN                      
                 IF K800-NUM-ALLO-CAT = 'NPL'                                   
                   MOVE 'AMR'             TO  F430-CDE-SPECIAL-SCH              
                 END-IF                                                         
                 ADD 1 TO WS-READ-BP13K800                                      
                 PERFORM 5000-CHK-SPR-CONV-EXPIRY THRU 5000-EXIT                
             ELSE                                                               
                 MOVE SPACES              TO F430-DTE-REGN                      
                 DISPLAY 'BP13K800-STATUS ' BP13K800-STATUS                     
                 DISPLAY 'F410-NUM-REGN   ' F410-NUM-REGN                       
             END-IF.                                                            
                                                                                
       4160-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       4170-READ-BP13K670.                                                      
      *-------------------*                                                     
           MOVE F410-NUM-REGN  TO K670-NUM-REGN.                                
                                                                                
           READ BP13K670                                                        
             IF BP13K670-STATUS = 0                                             
                 MOVE K670-NUM-FIN            TO F430-NUM-BANK                  
                 MOVE 'N'                     TO F430-NUM-BANK-TRAN             
                 ADD 1                        TO WS-READ-BP13K670               
             ELSE                                                               
                 DISPLAY 'BP13K670-STATUS ' BP13K670-STATUS                     
                 DISPLAY 'F410-NUM-REGN   ' F410-NUM-REGN                       
             END-IF.                                                            
                                                                                
       4170-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4180-READ-BP13K480.                                                      
      *-------------------*                                                     
                                                                                
           MOVE F410-NUM-REGN  TO K480-NUM-REGN.                                
                                                                                
           READ BP13K480                                                        
             IF BP13K480-STATUS = 0                                             
                ADD 1                     TO WS-READ-BP13K480                   
                COMPUTE K480-AMT-LRA-CTRA-SF-REQ-TP =                           
                                          F410-AMT-PREMIUM-STAMP-FEE +          
                                          F410-AMT-MORT-STAMP-FEE               
                                                                                
                IF F410-AMT-CONVEY-FEE > 0                                      
                   MOVE F410-AMT-CONVEY-FEE TO                                  
                                     K480-AMT-INT-BILL-CF-REQ-TP                
                                                                                
                END-IF                                                          
                                                                                
                COMPUTE K480-AMT-LRA-CONTRA-ICP-REQ =                           
                                 K480-AMT-LRA-CTRA-SF-REQ-TP +                  
                                 K480-AMT-INT-BILL-CF-REQ-TP                    
                                                                                
                PERFORM 4190-REWRITE-K480 THRU 4190-EXIT                        
             ELSE                                                               
                IF BP13K480-STATUS = 23                                         
                   PERFORM 4195-CREATE-BP13K480 THRU 4195-EXIT                  
                ELSE                                                            
                   DISPLAY 'BP13K480-STATUS ' BP13K480-STATUS                   
                   DISPLAY 'F410-NUM-REGN   ' F410-NUM-REGN                     
                END-IF                                                          
             END-IF.                                                            
                                                                                
       4180-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4190-REWRITE-K480.                                                       
      *-------------------*                                                     
                                                                                
           REWRITE BP13K480-REC.                                                
                                                                                
           IF BP13K480-STATUS = ZEROES                                          
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K480, STATUS : '                     
                       BP13K480-STATUS                                          
              MOVE BP13K480-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT.                      
                                                                                
       4190-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4195-CREATE-BP13K480.                                                    
      *-------------------*                                                     
                                                                                
           MOVE SPACES TO BP13K480-REC                                          
           INITIALIZE BP13K480-REC                                              
           MOVE  F410-NUM-REGN     TO  K480-NUM-REGN                            
           MOVE  F410-NUM-SCH-ACC  TO  K480-SCH-ACC                             
           MOVE  F410-NRIC1        TO  K480-NUM-NRIC1                           
           MOVE  F410-NRIC2        TO  K480-NUM-NRIC2                           
           MOVE  F410-NRIC3        TO  K480-NUM-NRIC3                           
           MOVE  F410-NRIC4        TO  K480-NUM-NRIC4                           
           MOVE  F410-CPF1         TO  K480-NUM-CPF1                            
           MOVE  F410-CPF2         TO  K480-NUM-CPF2                            
           MOVE  F410-CPF3         TO  K480-NUM-CPF3                            
           MOVE  F410-CPF4         TO  K480-NUM-CPF4                            
           MOVE  FUNCTION CURRENT-DATE TO  WS-CURRENT-DATE                      
           MOVE  WS-CURRENT-DATE   TO  K480-DTE-UPDATE                          
           MOVE  'BP13C411'        TO  K480-USERID                              
           COMPUTE K480-AMT-LRA-CTRA-SF-REQ-TP =                                
                                     F410-AMT-PREMIUM-STAMP-FEE +               
                                     F410-AMT-MORT-STAMP-FEE                    
                                                                                
           IF F410-AMT-CONVEY-FEE > 0                                           
              MOVE F410-AMT-CONVEY-FEE TO                                       
                                K480-AMT-INT-BILL-CF-REQ-TP                     
                                                                                
           END-IF                                                               
                                                                                
           COMPUTE K480-AMT-LRA-CONTRA-ICP-REQ =                                
                            K480-AMT-LRA-CTRA-SF-REQ-TP +                       
                            K480-AMT-INT-BILL-CF-REQ-TP                         
                                                                                
           WRITE BP13K480-REC.                                                  
                                                                                
           IF BP13K480-STATUS = ZEROES                                          
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K480, STATUS : '                     
                       BP13K480-STATUS                                          
              MOVE BP13K480-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT.                      
                                                                                
                                                                                
       4195-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4198-READ-BP13K480.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES         TO BP13K480-REC.                                 
           MOVE F410-NUM-REGN  TO K480-NUM-REGN.                                
                                                                                
           READ BP13K480                                                        
             IF BP13K480-STATUS = 0                                             
                CONTINUE                                                        
             ELSE                                                               
                IF BP13K480-STATUS = 23                                         
                   DISPLAY 'BP13K480 FILE NOT FOUND -' F410-NUM-REGN            
                ELSE                                                            
                   DISPLAY 'BP13K480-STATUS ' BP13K480-STATUS                   
                   DISPLAY 'F410-NUM-REGN   ' F410-NUM-REGN                     
                END-IF                                                          
             END-IF.                                                            
                                                                                
       4198-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4200-READ-BP13KD00.                                                      
      *-------------------*                                                     
           MOVE F410-NUM-REGN  TO KD00-NUM-REGN.                                
                                                                                
           READ BP13KD00                                                        
           IF BP13KD00-STATUS = 0                                               
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'BP13KD00-STATUS ' BP13KD00-STATUS                        
              DISPLAY 'F410-NUM-REGN   ' F410-NUM-REGN                          
              INITIALIZE BP13KD00-REC                                           
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4300-READ-BP13KD15.                                                      
      *-------------------*                                                     
           MOVE F410-NUM-SCH-ACC                TO KD15-NUM-SCH-ACC.            
           MOVE ZEROES                          TO WS-DTE-NVP                   
                                                   WS-DTE-NVP-NUM               
                                                   WS-DTE-TP                    
                                                   WS-DTE-TP-NUM                
                                                   WS-DTE-PROP-TAX              
                                                   WS-DTE-PROP-TAX-NUM          
                                                   WS-DTE-SC-REBATE.            
           READ BP13KD15 KEY IS KD15-ALT-KEY2.                                  
           IF BP13KD15-STATUS = 0                                               
               MOVE KD15-DTE-NVP                TO WS-DTE-NVP                   
               MOVE KD15-DTE-IND-TP             TO WS-DTE-TP                    
               PERFORM 4400-GET-SC-REBATE     THRU 4400-EXIT                    
               IF KA20-DTE-HANDOVER >= WS-DTE-SC-REBATE                         
                  MOVE KA20-DTE-HANDOVER        TO F430-DTE-SC-REBATE           
               ELSE                                                             
                  MOVE WS-DTE-SC-REBATE         TO F430-DTE-SC-REBATE           
               END-IF                                                           
           ELSE                                                                 
               PERFORM 4310-READ-BP13KD15  THRU 4310-EXIT                       
           END-IF.                                                              
                                                                                
           IF KA20-DTE-HANDOVER = SPACES OR ZEROS                               
               MOVE 'Y' TO F430-NUM-HANDOVER-KIV                                
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4310-READ-BP13KD15.                                                      
      *-------------------*                                                     
           MOVE SPACES                          TO KD15-ALT-KEY1.               
           MOVE K800-NUM-DBSS-PRJCT             TO KD15-NUM-PRJCT-KEY1.         
           MOVE K800-NUM-REGN                   TO KD15-NUM-REGN.               
           MOVE ZEROES                          TO WS-DTE-NVP                   
                                                   WS-DTE-NVP-NUM               
                                                   WS-DTE-TP                    
                                                   WS-DTE-TP-NUM                
                                                   WS-DTE-PROP-TAX              
                                                   WS-DTE-PROP-TAX-NUM          
                                                   WS-DTE-SC-REBATE.            
           READ BP13KD15 KEY IS KD15-ALT-KEY1.                                  
           IF BP13KD15-STATUS = 0                                               
               MOVE KD15-DTE-NVP                TO WS-DTE-NVP                   
               MOVE KD15-DTE-IND-TP             TO WS-DTE-TP                    
               PERFORM 4400-GET-SC-REBATE     THRU 4400-EXIT                    
               IF KA20-DTE-HANDOVER >= WS-DTE-SC-REBATE                         
                  MOVE KA20-DTE-HANDOVER        TO F430-DTE-SC-REBATE           
               ELSE                                                             
                  MOVE WS-DTE-SC-REBATE         TO F430-DTE-SC-REBATE           
               END-IF                                                           
           ELSE                                                                 
               MOVE F410-DTE-SO-EFFECTIVE       TO F430-DTE-SC-REBATE           
           END-IF.                                                              
                                                                                
       4310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------*                                              
       4400-GET-SC-REBATE.                                                      
      *--------------------------*                                              
                                                                                
           IF WS-DTE-NVP = ZEROES OR SPACES OR LOW-VALUES                       
              MOVE ZEROES                   TO WS-DTE-SC-REBATE                 
              GO TO 4400-EXIT                                                   
           END-IF.                                                              
           IF WS-DTE-TP = ZEROES OR SPACES OR LOW-VALUES                        
              MOVE ZEROES                   TO WS-DTE-SC-REBATE                 
              GO TO 4400-EXIT                                                   
           END-IF.                                                              
           COMPUTE WS-DTE-NVP-NUM                                               
                            = FUNCTION INTEGER-OF-DATE (WS-DTE-NVP)             
           COMPUTE WS-DTE-TP-NUM                                                
                            = FUNCTION INTEGER-OF-DATE (WS-DTE-TP)              
                                                                                
           IF KD15-NUM-PRJCT = 'D12' OR 'D13'                                   
              COMPUTE WS-DTE-NVP-NUM = WS-DTE-NVP-NUM + 15                      
           ELSE                                                                 
              COMPUTE WS-DTE-NVP-NUM = WS-DTE-NVP-NUM + 14                      
           END-IF.                                                              
                                                                                
           IF WS-DTE-NVP-NUM > WS-DTE-TP-NUM                                    
              COMPUTE WS-DTE-SC-REBATE                                          
                            = FUNCTION DATE-OF-INTEGER(WS-DTE-TP-NUM)           
           ELSE                                                                 
              COMPUTE WS-DTE-SC-REBATE                                          
                            = FUNCTION DATE-OF-INTEGER(WS-DTE-NVP-NUM)          
           END-IF.                                                              
                                                                                
           COMPUTE WS-DTE-PROP-TAX-NUM                                          
                            = FUNCTION INTEGER-OF-DATE (WS-DTE-NVP).            
                                                                                
           COMPUTE WS-DTE-PROP-TAX-NUM = WS-DTE-PROP-TAX-NUM + 1.               
           COMPUTE WS-DTE-PROP-TAX                                              
                   = FUNCTION DATE-OF-INTEGER (WS-DTE-PROP-TAX-NUM).            
                                                                                
           MOVE WS-DTE-PROP-TAX TO F430-DTE-PROPERTY-TAX.                       
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------*                                              
       5000-CHK-SPR-CONV-EXPIRY.                                                
      *--------------------------*                                              
           IF  (K800-NUM-ALLO-SCHEME = 'SSP' OR 'SRP' OR 'SPP') OR              
               (K800-NUM-ALLO-CAT = 'SER')                                      
               IF (K800-NUM-ELIG-LOAN-POLICY = 'IP' OR 'MO' OR 'MP' OR          
                                               'MV' OR 'M1')                    
                  IF F410-DTE-KEY-ISSUE >= '20050412'                           
                     MOVE K800-NUM-REGN TO K000-NUM-REGNO                       
                     CALL 'BP13C41R'                                            
                     IF WS-K100-FND = 'Y' AND                                   
                        K100-DTE-ANNOUNCE < '20030101'                          
                        PERFORM 5100-COMPUTE-DATE THRU 5100-EXIT                
                     END-IF                                                     
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------*                                              
       5100-COMPUTE-DATE.                                                       
      *--------------------------*                                              
           MOVE ZEROES TO WS-QUOTIENT                                           
                          WS-REMAINDER.                                         
                                                                                
           MOVE F410-DTE-SO-EFFECTIVE TO WS-DTE-EFFECTIVE.                      
                                                                                
           COMPUTE WS-EFF-CCYY = WS-EFF-CCYY + 5.                               
                                                                                
           IF WS-EFF-MM = 01                                                    
              MOVE 12     TO  WS-EFF-MM                                         
              SUBTRACT 1 FROM WS-EFF-CCYY                                       
           ELSE                                                                 
              COMPUTE WS-EFF-MM = WS-EFF-MM - 1                                 
           END-IF.                                                              
                                                                                
           IF WS-EFF-MM = 02                                                    
              DIVIDE 4 INTO WS-EFF-CCYY GIVING WS-QUOTIENT                      
                            REMAINDER WS-REMAINDER                              
              IF WS-REMAINDER = 0                                               
                 MOVE 29 TO WS-EFF-DD                                           
              ELSE                                                              
                 MOVE 28 TO WS-EFF-DD                                           
              END-IF                                                            
           ELSE                                                                 
              IF WS-EFF-MM = 04 OR 06 OR 09 OR 11                               
                 MOVE 30 TO WS-EFF-DD                                           
              ELSE                                                              
                 MOVE 31 TO WS-EFF-DD                                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-DTE-EFFECTIVE TO F430-DTE-SPR-CONV-EXPIRY.                   
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------*                                            
       6000-PROCEED-DBSS.                                                       
      *----------------------------*                                            
                                                                                
                                                                                
           IF F410-AMT-LOAN > 0                                                 
              MOVE '1'                   TO F430-CDE-INT                        
              MOVE F410-CDE-LOAN-ELIG    TO F430-CDE-LOAN-SCH                   
           ELSE                                                                 
              MOVE '0'                   TO F430-CDE-INT                        
              MOVE 'N'                   TO F430-CDE-LOAN-SCH                   
           END-IF.                                                              
                                                                                
           PERFORM 6100-READ-BP13KA20  THRU 6100-EXIT                           
                                                                                
           PERFORM 4300-READ-BP13KD15  THRU 4300-EXIT                           
                                                                                
           PERFORM 4200-READ-BP13KD00  THRU 4200-EXIT                           
                                                                                
           IF KD00-AMT-ARR-INTR NOT NUMERIC                                     
              MOVE ZEROES                TO KD00-AMT-ARR-INTR.                  
           IF KD00-AMT-TOT-INTR-TODTE NOT NUMERIC                               
              MOVE ZEROES                TO KD00-AMT-TOT-INTR-TODTE.            
           IF KD00-AMT-TOTL-PYMT-TODTE NOT NUMERIC                              
              MOVE ZEROES                TO KD00-AMT-TOTL-PYMT-TODTE.           
           IF KD00-NUM-MTH-INSTAL NOT NUMERIC                                   
              MOVE ZEROES                TO KD00-NUM-MTH-INSTAL.                
           IF KD00-AMT-ARR-INSTAL NOT NUMERIC                                   
              MOVE ZEROES                TO KD00-AMT-ARR-INSTAL.                
           IF KD00-AMT-ARR-INSTAL-BF NOT NUMERIC                                
              MOVE ZEROES                TO KD00-AMT-ARR-INSTAL-BF.             
           IF KD00-NUM-MTH-INSTAL NOT NUMERIC                                   
              MOVE ZEROES                TO KD00-NUM-MTH-INSTAL.                
           IF KD00-AMT-LOAN-DISBURSED NOT NUMERIC                               
              MOVE ZEROES                TO KD00-AMT-LOAN-DISBURSED.            
           IF KD00-DTE-LOAN-EXPIRY NOT NUMERIC                                  
              MOVE ZEROES                TO KD00-DTE-LOAN-EXPIRY.               
           IF KD00-DTE-LOAN-COMM NOT NUMERIC                                    
              MOVE ZEROES                TO KD00-DTE-LOAN-COMM.                 
           IF KD00-DTE-INSTAL-COMM NOT NUMERIC                                  
              MOVE ZEROES                TO KD00-DTE-INSTAL-COMM.               
                                                                                
           MOVE KD00-AMT-ARR-INTR        TO F430-AMT-ARR-INTR                   
           MOVE KD00-AMT-TOT-INTR-TODTE  TO F430-AMT-INTR-PYMT-TODTE            
           MOVE KD00-AMT-TOTL-PYMT-TODTE TO F430-AMT-TOTL-PYMT-TODTE            
           MOVE KD00-NUM-MTH-INSTAL      TO F430-NUM-MTH-INSTAL                 
           MOVE KD00-AMT-ARR-INSTAL      TO F430-AMT-ARR-INSTAL                 
           MOVE KD00-AMT-ARR-INSTAL-BF   TO F430-AMT-ARR-INSTAL-BF              
           MOVE KD00-NUM-MTH-INSTAL      TO F430-NUM-MTH-INSTAL-BF              
                                                                                
                                                                                
           MOVE KD00-NUM-GIRO-INFO       TO F430-NUM-GIRO-INFO                  
           MOVE KD00-DTE-GIRO-EFF        TO F430-DTE-GIRO-EFF                   
           MOVE KD00-NUM-GIRO            TO F430-NUM-GIRO                       
                                                                                
           MOVE KD00-AMT-PP-LUMPSUM-REQ  TO F430-AMT-ICP-LUMSUM                 
           MOVE KD00-AMT-LOAN-DISBURSED  TO F430-AMT-LOAN-DISBURSED             
                                                                                
      *    MOVE KD00-DTE-LOAN-EXPIRY     TO F430-DTE-LOAN-EXPIRY                
           MOVE KD00-DTE-LOAN-EXPIRY     TO F430-DTE-LOAN-EXPIRY-CYMD           
           MOVE KD00-DTE-INSTAL-COMM     TO F430-DTE-LOAN-INSTAL-COMM.          
           MOVE KD00-DTE-LOAN-COMM       TO F430-DTE-LOAN-EFF.                  
           MOVE ZEROES    TO F430-AMT-STAMP-FEE                                 
                             F430-AMT-SURVEY-FEE                                
                             F430-AMT-STAMP-SURVEY-CC-FEE                       
                             F430-AMT-STAMP-FEE-CASH                            
                             F430-AMT-STAMP-FEE-CPF1                            
                             F430-AMT-STAMP-FEE-CPF2                            
                             F430-AMT-STAMP-FEE-CPF3                            
                             F430-AMT-STAMP-FEE-CPF4                            
                             F430-AMT-SURVEY-FEE-GST.                           
                                                                                
           IF F410-NUM-LOAN-TAG = 'B'                                           
              MOVE KD00-NUM-FIN   TO F430-NUM-BANK                              
              MOVE 'N'            TO F430-NUM-BANK-TRAN                         
              IF ((F430-NUM-BANK NOT = SPACES AND LOW-VALUES) AND               
                  (F430-AMT-BANK-LOAN > ZEROES))                                
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE SPACES      TO F430-NUM-BANK                              
                 MOVE SPACES      TO F430-DTE-BANK-LOAN-COMM                    
                 MOVE ZEROES      TO F430-AMT-BANK-LOAN                         
                 MOVE SPACES      TO F430-NUM-BANK-TRAN                         
                 MOVE SPACES      TO F430-NUM-ELIG-HDB-LOAN-SCH                 
                 MOVE SPACES      TO F430-NUM-ELIG-LOAN-POLICY                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F410-NUM-LOAN-TAG = 'B' OR 'N'                                    
              MOVE SPACES         TO F430-CDE-LOAN-REPYMT                       
              MOVE ZEROES         TO F430-AMT-ICP                               
              MOVE ZEROES         TO F430-NUM-LOAN-REPYMT-PERIOD                
              MOVE ZEROES         TO F430-NUM-LOAN-REPYMT-MAX                   
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------*                                            
       6100-READ-BP13KA20.                                                      
      *----------------------------*                                            
                                                                                
           MOVE SPACES                    TO BP13KA20-REC.                      
           INITIALIZE                        BP13KA20-REC.                      
                                                                                
           IF (K800-NUM-DBSS-PRJCT = SPACES OR LOW-VALUES) OR                   
              (K110-BLK-NO = SPACES OR LOW-VALUES)                              
              MOVE ZEROS                  TO KA20-DTE-HANDOVER                  
              GO TO 6100-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-DBSS-PRJCT       TO KA20-NUM-PRJCT                     
           MOVE K110-BLK-NO               TO KA20-NUM-BLK                       
                                                                                
           READ BP13KA20.                                                       
                                                                                
           IF BP13KA20-STATUS = 00 OR 02                                        
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'BP13KA20 - ERROR READING : ' BP13KA20-STATUS             
              MOVE ZEROS                  TO KA20-DTE-HANDOVER                  
           END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------*                                            
       6500-PROCEED-NONDBS.                                                     
      *----------------------------*                                            
                                                                                
           MOVE F410-DTE-KEY-ISSUE        TO F430-DTE-PROPERTY-TAX.             
           MOVE F410-DTE-SO-EFFECTIVE     TO F430-DTE-SC-REBATE                 
                                                                                
      * FOR 8 CHAR EXPIRY DATE                                                  
      * TO COMPUTE LOAN EXPIRY DATE                                             
      *          EXPIRY-DATE = DTE-SO-EFFECTIVE + 2 MONTHS + REPAY PRD          
      *                      = CHANGE TO THE LAST DAY OF PREVIOUS MONTH         
      *          E.G. DTE-SO-EFFECTIVE = 20090125 (NON-LEAP YEAR)               
      *                      REPAY PRD = 30 YEARS                               
      *                    EXPIRY-DATE = 20090125 + 2 MON = 20090325            
      *                                = 20090325 + 30 YEARS = 20390325         
      *                                = 20390228 (LAST DAY OF PREVIOUS)        
      *          E.G. DTE-SO-EFFECTIVE = 20090125 (LEAP YEAR)                   
      *                      REPAY PRD = 3 YEARS                                
      *                    EXPIRY-DATE = 20090125 + 2 MON = 20090325            
      *                                = 20090325 + 3 YEARS = 20120325          
      *                                = 20120229 (LAST DAY OF PREVIOUS)        
                                                                                
           IF F410-AMT-LOAN > 0                                                 
              MOVE F410-DTE-SO-EFFECTIVE  TO WS-DTE-CCYYMMDD                    
              MOVE  1                     TO WS-DTE-DD                          
                                                                                
              COMPUTE WS-DTE-MM   = WS-DTE-MM + 2                               
                                                                                
              IF WS-DTE-MM < 13                                                 
                 COMPUTE WS-DTE-CCYY = WS-DTE-CCYY +                            
                                       F410-NUM-REPAY-PERIOD                    
              END-IF                                                            
                                                                                
              IF WS-DTE-MM = 13                                                 
                 MOVE 1 TO WS-DTE-MM                                            
                 COMPUTE WS-DTE-CCYY = WS-DTE-CCYY + 1 +                        
                                       F410-NUM-REPAY-PERIOD                    
              END-IF                                                            
                                                                                
              IF WS-DTE-MM = 14                                                 
                 MOVE 2 TO WS-DTE-MM                                            
                 COMPUTE WS-DTE-CCYY = WS-DTE-CCYY + 1 +                        
                                       F410-NUM-REPAY-PERIOD                    
              END-IF                                                            
                                                                                
              MOVE WS-DTE-CCYYMMDD        TO WS-DTE-LOAN-COMM                   
      *                                                                         
      * SUBTRACT 1 DAY FROM THE COMPUTED EXPIRY DATE                            
      *                                                                         
              COMPUTE WS-DTE-LOAN-EXP-INT =                                     
                      FUNCTION INTEGER-OF-DATE(WS-DTE-LOAN-COMM)                
              SUBTRACT 1 FROM WS-DTE-LOAN-EXP-INT                               
                                                                                
              COMPUTE WS-DTE-LOAN-EXP-NEW =                                     
                      FUNCTION DATE-OF-INTEGER(WS-DTE-LOAN-EXP-INT)             
                                                                                
              MOVE WS-DTE-LOAN-EXP-NEW    TO WS-DTE-CCYYMMDD                    
              MOVE WS-DTE-CCYYMMDD        TO F430-DTE-LOAN-EXPIRY-CYMD          
           ELSE                                                                 
      *       MOVE '000000'               TO F430-DTE-LOAN-EXPIRY               
              MOVE '00000000'             TO F430-DTE-LOAN-EXPIRY-CYMD          
           END-IF.                                                              
      *                                                                         
      * TO COMPUTE INSTALLMENT COMMENCEMENT DATE                                
      *    INSTALL-COMM-DATE = DTE-SO-EFFECTIVE + 2 MONTHS                      
      *                      = CHANGE TO 1ST OF THE MONTH                       
      *                                                                         
           IF F410-AMT-LOAN > 0                                                 
              MOVE F410-DTE-SO-EFFECTIVE  TO F430-DTE-LOAN-EFF                  
                                             WS-DTE-INSTAL-COMMENCE             
                                                                                
              MOVE 1                      TO WS-DTE-INST-COMM-DD                
              ADD 2                       TO WS-DTE-INST-COMM-MM                
              IF WS-DTE-INST-COMM-MM = 14                                       
                 MOVE 2                   TO WS-DTE-INST-COMM-MM                
                 ADD 1                    TO WS-DTE-INST-COMM-CCYY              
              END-IF                                                            
                                                                                
              IF WS-DTE-INST-COMM-MM = 13                                       
                 MOVE 1                   TO WS-DTE-INST-COMM-MM                
                 ADD 1                    TO WS-DTE-INST-COMM-CCYY              
              END-IF                                                            
                                                                                
              MOVE WS-DTE-INSTAL-COMMENCE TO WS-DTE-CCYYMMDD                    
              MOVE WS-DTE-CCYYMMDD        TO F430-DTE-LOAN-INSTAL-COMM          
                                                                                
           ELSE                                                                 
              MOVE '00000000'             TO F430-DTE-LOAN-EFF                  
                                             F430-DTE-LOAN-INSTAL-COMM          
           END-IF.                                                              
                                                                                
      *  FOR CASES OF OUTRIGHT PURCHASE THE INTEREST RATE IS 0                  
      *  BUT FOR SALE MORTGAGE SYSTEM, THE INTEREST RATE IS 6.25 %              
      *  AND THE LOAN SCHEME MUST BE PUBLIC.                                    
      *  INTEREST CODE CAN ONLY BE 1 OR ZERO FOR NEW FLATS                      
      *  WITH CA  INTEREST CODE CAN BE 0 , 1, 2, 3 FOR NEW FLATS                
      *  TO STANDARDISE INT CODE = 0 AND LOAN SCH = N FOR ALL FLATS             
                                                                                
           EVALUATE F410-CDE-INTEREST-RATE                                      
              WHEN '0'                                                          
                 MOVE '0' TO F430-CDE-INT                                       
                 MOVE 'N' TO F430-CDE-LOAN-SCH                                  
              WHEN '1'                                                          
                 MOVE '1' TO F430-CDE-INT                                       
                 PERFORM 4120-CHECK-LOAN-SCH    THRU 4120-EXIT                  
              WHEN '2'                                                          
                 MOVE '2' TO F430-CDE-INT                                       
                 PERFORM 4120-CHECK-LOAN-SCH    THRU 4120-EXIT                  
              WHEN '3'                                                          
                 MOVE '3' TO F430-CDE-INT                                       
                 PERFORM 4120-CHECK-LOAN-SCH    THRU 4120-EXIT                  
           END-EVALUATE.                                                        
                                                                                
           IF F410-AMT-LOAN NOT > 0                                             
              MOVE 'N'         TO F430-CDE-LOAN-SCH                             
              MOVE '0'         TO F430-CDE-INT                                  
           END-IF.                                                              
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------*                                            
       6600-DERIVE-LOAN-POLICY.                                                 
      *----------------------------*                                            
                                                                                
            IF F410-AMT-LOAN > 0                                                
               CONTINUE                                                         
            ELSE                                                                
               MOVE 'NR' TO F430-CDE-LOAN-SCH-POLICY                            
               GO TO 6600-EXIT                                                  
            END-IF.                                                             
                                                                                
            IF K800-NUM-BUY-SELL-TAG = SPACES OR LOW-VALUES                     
               MOVE SPACES TO K800-NUM-BUY-SELL-TAG                             
            END-IF.                                                             
                                                                                
            EVALUATE F410-NUM-LOAN-TAG                                          
              WHEN 'H'                                                          
                 IF K800-NUM-BUY-SELL-TAG = 'B' OR 'S' OR 'E'                   
                    MOVE 'PB' TO F430-CDE-LOAN-SCH-POLICY                       
                 ELSE                                                           
                    IF K800-NUM-BUY-SELL-TAG = 'N' OR ' ' OR LOW-VALUES         
                       IF F430-CDE-LOAN-SCH = 'A'                               
                          MOVE 'AI' TO F430-CDE-LOAN-SCH-POLICY                 
                       ELSE                                                     
                          IF F430-CDE-LOAN-SCH = 'P'                            
                             MOVE 'PO' TO F430-CDE-LOAN-SCH-POLICY              
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              WHEN 'B'                                                          
              WHEN 'N'                                                          
                 IF F430-CDE-LOAN-SCH = 'N'                                     
                    MOVE 'NR' TO F430-CDE-LOAN-SCH-POLICY                       
                 END-IF                                                         
            END-EVALUATE.                                                       
                                                                                
       6600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------*                                            
       7000-TEMP-LOAN.                                                          
      *----------------------------*                                            
           MOVE F430-DTE-SALE-EFF     TO F430-DTE-LOAN-EFF.                     
                                                                                
           MOVE SPACES                TO F430-DTE-LOAN-EXPIRY-CYMD              
                                         F430-DTE-LOAN-INSTAL-COMM              
                                         F430-NUM-LOAN-REPYMT-PERIOD            
                                         F430-NUM-ELIG-LOAN-POLICY.             
                                                                                
           MOVE ZEROS                 TO F430-NUM-LOAN-REPYMT-MAX               
                                         F430-AMT-LOAN-INSTAL-NEW               
                                         F430-AMT-SUB-INSTAL-NEW                
                                         F430-AMT-REV-LOAN-INSTAL-NEW           
                                         F430-AMT-REV-SUB-INSTAL-NEW.           
                                                                                
           IF F410-CDE-LOAN-ELIG = 'A' OR F430-CDE-LOAN-SCH = 'A'               
              MOVE 'A'                TO F430-CDE-LOAN-SCH                      
              MOVE 'AT'               TO F430-CDE-LOAN-SCH-POLICY               
              MOVE '1'                TO F430-CDE-INT                           
           ELSE                                                                 
           IF F410-CDE-LOAN-ELIG = 'M' OR F430-CDE-LOAN-SCH = 'M'               
              MOVE 'A'                TO F430-CDE-LOAN-SCH                      
              MOVE 'AT'               TO F430-CDE-LOAN-SCH-POLICY               
              MOVE '1'                TO F430-CDE-INT                           
           ELSE                                                                 
              IF F410-CDE-LOAN-ELIG = 'P' OR F430-CDE-LOAN-SCH = 'P'            
                 MOVE 'P'             TO F430-CDE-LOAN-SCH                      
                 MOVE 'PT'            TO F430-CDE-LOAN-SCH-POLICY               
                 MOVE '1'             TO F430-CDE-INT                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------*                                            
       8000-DISPLAY-CONTROL-LIST.                                               
      *----------------------------*                                            
           DISPLAY ' **** BP13C411 ****'.                                       
           MOVE  1                    TO L040S1-DL-NUM.                         
           MOVE 'NO  OF  RECORDS  READ  FROM  TEMPORARY-SALES-ORDER'            
                                      TO L040S1-DL-DESCRPN-1.                   
           MOVE ' (BP13F410)     '    TO L040S1-DL-DESCRPN-2.                   
           MOVE  WS-READ-CNT          TO L040S1-DL-COUNTER.                     
           DISPLAY L040S1-DL1.                                                  
                                                                                
           MOVE  2                    TO L040S1-DL-NUM.                         
           MOVE 'NO  OF  RECORDS PASSED TO FINANCE-SALES-ORDER   ('             
                                      TO L040S1-DL-DESCRPN-1.                   
           MOVE 'BP13F430 )      '    TO L040S1-DL-DESCRPN-2.                   
           MOVE WS-REC-PASSED-TO-F430 TO L040S1-DL-COUNTER.                     
           DISPLAY L040S1-DL1.                                                  
                                                                                
           MOVE  3                    TO L040S1-DL-NUM.                         
           MOVE 'NO  OF  RECORDS FOUND IN FIS FOR IMPROVE COST   ('             
                                      TO L040S1-DL-DESCRPN-1.                   
           MOVE 'BM06K110 )      '    TO L040S1-DL-DESCRPN-2.                   
           MOVE WS-READ-K110          TO L040S1-DL-COUNTER.                     
           DISPLAY L040S1-DL1.                                                  
                                                                                
           MOVE  4                    TO L040S1-DL-NUM.                         
           MOVE 'NO  OF  RECORDS FOUND IN SOC MASTER (BP13K800)'                
                                      TO L040S1-DL-DESCRPN-1.                   
           MOVE SPACES                TO L040S1-DL-DESCRPN-2.                   
           MOVE WS-READ-BP13K800      TO L040S1-DL-COUNTER.                     
           DISPLAY L040S1-DL1.                                                  
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       9000-CLOSING-ROUTINE.                                                    
      *----------------------*                                                  
           CLOSE BP13F410                                                       
                 BP13F430                                                       
                 BP13K800                                                       
                 BP13K480                                                       
                 BP13KD00                                                       
                 BP13KD15                                                       
                 BP13K670                                                       
                 BP13KA20                                                       
                 BM06K110                                                       
                 BP17K000                                                       
                 BP17K100                                                       
                 BP17K110                                                       
                 BP17K120.                                                      
                                                                                
             IF BM06K110-STATUS NOT = 0 AND 97                                  
                MOVE BM06K110-STATUS           TO WS-RET-CODE                   
                DISPLAY 'CLOSING ERROR STATUS BM06K110' BM06K110-STATUS.        
                                                                                
             IF BP13K800-STATUS NOT = 0 AND 97                                  
                MOVE BP13K800-STATUS           TO WS-RET-CODE                   
                DISPLAY 'CLOSING ERROR STATUS BP13K800' BP13K800-STATUS.        
                                                                                
             IF BP13K480-STATUS NOT = 0 AND 97                                  
                MOVE BP13K480-STATUS           TO WS-RET-CODE                   
                DISPLAY 'CLOSING ERROR STATUS BP13K480' BP13K480-STATUS.        
                                                                                
             IF BP13KD00-STATUS NOT = 0 AND 97                                  
                MOVE BP13KD00-STATUS           TO WS-RET-CODE                   
                DISPLAY 'CLOSING ERROR STATUS BP13KD00' BP13KD00-STATUS.        
                                                                                
             IF BP13KD15-STATUS NOT = 0 AND 97                                  
                MOVE BP13KD15-STATUS           TO WS-RET-CODE                   
                DISPLAY 'CLOSING ERROR STATUS BP13KD15' BP13KD15-STATUS.        
                                                                                
             IF BP13K670-STATUS NOT = 0 AND 97                                  
                MOVE BP13K670-STATUS           TO WS-RET-CODE                   
                DISPLAY 'CLOSING ERROR STATUS BP13K670' BP13K670-STATUS.        
                                                                                
             IF WS-K100-STATUS NOT = ZEROES                                     
                DISPLAY 'ERROR IN CLOSE BP17K100 : ' WS-K100-STATUS             
                MOVE WS-K100-STATUS            TO WS-RET-CODE                   
             END-IF.                                                            
                                                                                
             IF WS-K000-STATUS NOT = ZEROES                                     
                DISPLAY 'ERROR IN CLOSE BP17K000 : ' WS-K000-STATUS             
                MOVE WS-K000-STATUS            TO WS-RET-CODE                   
             END-IF.                                                            
                                                                                
             IF WS-K110-STATUS NOT = ZEROES                                     
                DISPLAY 'ERROR IN CLOSE BP17K110 : ' WS-K110-STATUS             
                MOVE WS-K110-STATUS            TO WS-RET-CODE                   
             END-IF.                                                            
                                                                                
             IF WS-K120-STATUS NOT = ZEROES                                     
                DISPLAY 'ERROR IN CLOSE BP17K120 : ' WS-K120-STATUS             
                MOVE WS-K120-STATUS            TO WS-RET-CODE                   
             END-IF.                                                            
                                                                                
             IF BP13KA20-STATUS NOT = ZEROES                                    
                DISPLAY 'ERROR IN CLOSE BP13KA20 : ' BP13KA20-STATUS            
                MOVE BP13KA20-STATUS            TO WS-RET-CODE                  
             END-IF.                                                            
                                                                                
             IF WS-RET-CODE NOT = ZEROES                                        
                MOVE WS-RET-CODE TO RETURN-CODE                                 
             END-IF.                                                            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       COPY BP13C41R.                                                           
       END PROGRAM BP13C411.                                                    
