       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C27E.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  25/07/2017.                                               
      * ======================================================== *              
      * SYSTEM OF COMMITMENT (BP13)                              *              
      * ======================================================== *              
      *    OBJECTIVE  :                                          *              
      *      1.  PROGRAM TO DTE-EST-CMPLTN AND DTE-LSE-COMM      *              
      *          (UPDATE BP13K240) FOR ROF SALES LAUNCH.         *              
      *     INPUT FILES:                                         *              
      *           1. BP13F240                                    *              
      *           2. BM06K100                                    *              
      *           3. BM06K110                                    *              
      *           4. BB14K500                                    *              
      *           5. BP13K260                                    *              
      *                                                          *              
      *     I-O  FILES:                                          *              
      *           1. BP13K240                                    *              
      *                                                          *              
      * -------------------------------------------------------- *              
      *  CHG NO     DATE     BY   DESCRIPTION                    *              
      * -------- ----------  ---  ------------------------------ *              
      * BP136835 25/07/2017  FNP1 NEW PROGRAM                    *              
      * BP137163 05/02/2018  ESA1 ADD READING OF BM06K110 TO GET *              
      *                           K110-KEY-AVAIL                 *              
      * BP138405 31/08/2020  MRR5 ADD 18 MONTHS TO COMPLETION DTE*              
      *                           TO GET DLVRY-POSSN-DATE        *              
      * BP139110 29/03/2022  EL27 CATER FOR 15 MONTHS DPD FOR 6 PROJ            
      * ======================================================== *              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F240 ASSIGN TO BP13F240.                                  
                                                                                
           SELECT BP13K260 ASSIGN TO BP13K260                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K260-KEY-FLD                                    
                FILE STATUS  IS BP13K260-STATUS.                                
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K100-KEY-FLD                                    
                FILE STATUS  IS BM06K100-STATUS.                                
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS DYNAMIC                                         
                RECORD KEY   IS K110-KEY-FLD                                    
                ALTERNATE RECORD KEY IS K110-AIX1                               
                FILE STATUS  IS BM06K110-STATUS.                                
                                                                                
           SELECT BB14K500 ASSIGN TO BB14K500                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K500-KEY-FLD                                    
                FILE STATUS  IS BB14K500-STATUS.                                
                                                                                
           SELECT BB14K580 ASSIGN TO BB14K580                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS DYNAMIC                                         
                RECORD KEY   IS K580-KEY-FLD                                    
                ALTERNATE KEY IS K580-ALTX-KEY                                  
                FILE STATUS  IS BB14K580-STATUS.                                
                                                                                
           SELECT BB14K990 ASSIGN TO BB14K990                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS DYNAMIC                                         
                RECORD KEY   IS K990-KEY-FLD                                    
                ALTERNATE KEY IS K990-ALT-KEY                                   
                FILE STATUS  IS BB14K990-STATUS.                                
                                                                                
           SELECT BP13K240 ASSIGN TO BP13K240                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K240-KEY-FLD                                    
                FILE STATUS  IS BP13K240-STATUS.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13K260                 RECORD CONTAINS 100 CHARACTERS.            
       COPY BP13K260.                                                           
                                                                                
       FD   BB14K500                 RECORD CONTAINS 400 CHARACTERS.            
       COPY BB14K500.                                                           
                                                                                
       FD   BB14K580                 RECORD CONTAINS 300 CHARACTERS.            
       COPY BB14K580.                                                           
                                                                                
       FD   BB14K990                 RECORD CONTAINS 200 CHARACTERS.            
       COPY BB14K990.                                                           
                                                                                
       FD   BM06K100                 RECORD CONTAINS 500 CHARACTERS.            
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110                 RECORD CONTAINS 500 CHARACTERS.            
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K240                 RECORD CONTAINS 450  CHARACTERS.           
       COPY BP13K240.                                                           
                                                                                
       FD   BP13F240                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 450 CHARACTERS.                                     
       01  F240-HEADER-REC.                                                     
           05  F240-CDE-INTV            PIC X(04).                              
           05  F240-KEY-FLD.                                                    
               10  F240-CDE-HEADER      PIC X(02).                              
               10  F240-DTE-ALLOC       PIC X(08).                              
               10  F240-NUM-NEIGH       PIC X(04).                              
               10  F240-NUM-CNTRCT      PIC X(04).                              
               10  FILLER               PIC X(07).                              
               10  F240-NUM-NEW-TOWN    PIC X(20).                              
               10  F240-NUM-FLAT-TYPE   PIC X(02).                              
               10  F240-NUM-BLK         PIC X(05).                              
           05  F240-NUM-MAX-CAT1        PIC 9(03).                              
           05  F240-NUM-MAX-CAT2        PIC 9(03).                              
           05  F240-NUM-MAX-CAT3        PIC 9(03).                              
           05  F240-NUM-CAT1-FULL       PIC X(01).                              
           05  F240-NUM-CAT2-FULL       PIC X(01).                              
           05  F240-NUM-CAT3-FULL       PIC X(01).                              
           05  F240-NUM-TOTAL           PIC 9(04).                              
           05  F240-NUM-TOT-CAT1        PIC 9(03).                              
           05  F240-NUM-TOT-CAT2        PIC 9(03).                              
           05  F240-NUM-TOT-CAT3        PIC 9(03).                              
           05  F240-DTE-UPDATE          PIC X(08).                              
           05  F240-TME-UPDATE          PIC X(07).                              
           05  F240-NUM-ZONE            PIC X(10).                              
           05  F240-NUM-BALANCE         PIC X(01).                              
           05  F240-NUM-SELECTION       PIC X(3).                               
           05  F240-NUM-ESTATE          PIC X(03).                              
           05  F240-CDE-NT              PIC X(03).                              
           05  F240-DTE-BALLOT          PIC X(06).                              
           05  F240-NUM-USERID          PIC X(08).                              
           05  F240-ALT-KEY.                                                    
               10  F240-NUM-SELECTION2  PIC X(3).                               
               10  F240-NUM-ZONE2       PIC X(10).                              
               10  F240-NUM-NEW-TOWN2   PIC X(20).                              
               10  F240-NUM-FLAT-TYPE2  PIC X(09).                              
               10  F240-NUM-DESIGN2     PIC X(04).                              
               10  F240-NUM-BLK2        PIC X(05).                              
           05  F240-ALT-KEY3.                                                   
               10  F240-NUM-SELECTION3  PIC X(3).                               
               10  F240-NUM-ZONE3       PIC X(10).                              
               10  F240-NUM-NEW-TOWN3   PIC X(20).                              
               10  F240-NUM-FLAT-TYPE3  PIC X(09).                              
               10  F240-NUM-BLK3        PIC X(05).                              
           05  F240-NUM-BONUS           PIC X(01).                              
           05  F240-NUM-HOLD-CAT1       PIC 9(03).                              
           05  F240-NUM-HOLD-CAT2       PIC 9(03).                              
           05  F240-NUM-HOLD-CAT3       PIC 9(03).                              
           05  F240-NUM-DESIGN          PIC X(04).                              
           05  F240-NME-STREET          PIC X(32).                              
           05  FILLER                   PIC X(08).                              
           05  F240-DTE-EST-CMPLTN      PIC X(08).                              
           05  F240-DTE-LSE-COMM        PIC X(08).                              
           05  F240-NUM-UPG-STATUS      PIC X(03).                              
           05  F240-DTE-KEY-AVAIL       PIC X(08).                              
           05  F240-ALT-KEY4.                                                   
               10  F240-NUM-SELECTION4   PIC X(3).                              
               10  F240-NUM-NEW-TOWN4    PIC X(20).                             
               10  F240-NUM-FLAT-TYPE4   PIC X(09).                             
               10  F240-NUM-NEIGH4       PIC X(04).                             
               10  F240-NUM-CNTRCT4      PIC X(04).                             
               10  F240-NUM-BLK4         PIC X(05).                             
           05  F240-NUM-BTO-NEW-CONTRACT PIC X(01).                             
           05  F240-NUM-COMBINE-SA       PIC X(01).                             
           05  F240-ALT-KEY5.                                                   
               10  F240-NUM-NEW-TOWN5    PIC X(20).                             
               10  F240-NUM-FLAT-TYPE5   PIC X(09).                             
               10  F240-NUM-SELECTION5   PIC X(03).                             
               10  F240-DTE-BALLOT5      PIC X(06).                             
               10  F240-NUM-BONUS5       PIC X(01).                             
           05  FILLER                    PIC X(01).                             
           05  F240-NUM-MAX-SPR          PIC 9(03).                             
           05  F240-NUM-TOTAL-SPR        PIC 9(03).                             
           05  F240-NUM-HOLD-SPR         PIC 9(03).                             
           05  F240-NUM-SPR-FULL         PIC X(01).                             
           05  F240-NUM-BTO-ZONE         PIC X(03).                             
           05  F240-NUM-TOTAL-GEN3       PIC 9(03).                             
           05  F240-NUM-TOT-CAT1-GEN3    PIC 9(03).                             
           05  F240-NUM-TOT-CAT2-GEN3    PIC 9(03).                             
           05  F240-NUM-TOT-CAT3-GEN3    PIC 9(03).                             
           05  FILLER                    PIC X(29).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  BP13K260-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BM06K100-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BM06K110-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BB14K500-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BB14K580-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BB14K990-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BP13K240-STATUS              PIC 9(02)  VALUE ZEROS.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F240-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K500-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K580-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K990-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K100-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K240-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K240-UPDATED              PIC 9(08)  VALUE ZEROS.             
           05  WS-UPDATE-ECD                PIC 9(08)  VALUE ZEROS.             
           05  WS-UPDATE-LSE                PIC 9(08)  VALUE ZEROS.             
           05  WS-K100-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K500-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K260-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K240-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K580-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K990-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-UPDATE-TAG                PIC X(01)  VALUE SPACES.            
                                                                                
       01  WS-FILE-VARIABLES.                                                   
           05 WS-K500-DTE-EST-CMPL          PIC X(08)  VALUE SPACES.            
           05 WS-K500-DTE-LSE-COMM          PIC X(08)  VALUE SPACES.            
           05 WS-K100-CDE-BLK               PIC X(05)  VALUE SPACES.            
           05 WS-K110-DTE-KEY-AVBL          PIC X(08)  VALUE SPACES.            
           05 WS-DTE-DLVRY-POSSESSION       PIC X(08)  VALUE SPACES.            
           05 WS-DEL-POSSN-DATE.                                                
              10  WS-DEL-POSSN-YYYY         PIC X(4).                           
              10  WS-DEL-POSSN-MM           PIC X(2).                           
              10  WS-DEL-POSSN-DD           PIC X(2).                           
           05  WS-8-DATE-CHAR               PIC X(8).                           
           05  WS-8-DATE  REDEFINES WS-8-DATE-CHAR.                             
               10  WS-8-DATE-YYYY           PIC 9(4).                           
               10  WS-8-DATE-MM             PIC 9(2).                           
               10  WS-8-DATE-DD             PIC 9(2).                           
           05  WS-DAY                       PIC X(2).                           
           05  WS-MONTH                     PIC X(2).                           
               88  MTH-END-WITH-30    VALUE '04' '06' '09' '11'.                
               88  MTH-END-WITH-28    VALUE '02'.                               
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-F240-FLAG                 PIC X(01)  VALUE 'N'.               
               88 WS-F240-EOF                          VALUE 'Y'.               
           05  WS-K260-FND                  PIC X(01)  VALUE 'N'.               
           05  WS-K110-EOF                  PIC X(01)  VALUE 'N'.               
                                                                                
       01  WS-EDT-DATE.                                                         
           05  WS-EDT-DD                    PIC 9(02).                          
           05  FILLER                       PIC X      VALUE '/'.               
           05  WS-EDT-MM                    PIC 9(02).                          
           05  FILLER                       PIC X      VALUE '/'.               
           05  WS-EDT-CCYY                  PIC 9(04).                          
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-LCOMM-DTE.                                                        
           05  WS-LCOMM-YYYY                 PIC 9(4).                          
           05  WS-LCOMM-MM                   PIC 9(2).                          
           05  WS-LCOMM-DD                   PIC 9(2).                          
                                                                                
       01  WS-YEAR-COMM                      PIC 99.                            
       01  WS-MONTH-COMM                     PIC 99.                            
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-----------------------------------------------------------              
       000-MAIN-ROUTINE.                                                        
      *-----------------------------------------------------------              
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-READ-BP13F240 THRU 200-EXIT.                             
           PERFORM 300-PROCESS-REC   THRU 300-EXIT                              
                   UNTIL WS-F240-EOF.                                           
                                                                                
           PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-----------------------------------------------------------              
           OPEN INPUT  BP13F240                                                 
                       BP13K260                                                 
                       BM06K100                                                 
                       BM06K110                                                 
                       BB14K500                                                 
                       BB14K580                                                 
                       BB14K990                                                 
                I-O    BP13K240.                                                
                                                                                
           IF BM06K100-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BM06K100 STATUS ' BM06K100-STATUS            
              MOVE BM06K100-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BM06K110 STATUS ' BM06K110-STATUS            
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BB14K500-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BB14K500 STATUS ' BB14K500-STATUS            
              MOVE BB14K500-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BB14K580-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BB14K580 STATUS ' BB14K580-STATUS            
              MOVE BB14K580-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BB14K990-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BB14K990 STATUS ' BB14K990-STATUS            
              MOVE BB14K990-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K260-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BP13K260 STATUS ' BP13K260-STATUS            
              MOVE BP13K260-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K240-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BP13K240 STATUS ' BP13K240-STATUS            
              MOVE BP13K240-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           MOVE WS-CUR-CCYY           TO WS-EDT-CCYY.                           
           MOVE WS-CUR-MM             TO WS-EDT-MM.                             
           MOVE WS-CUR-DD             TO WS-EDT-DD.                             
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F240.                                                       
      *-----------------------------------------------------------              
           READ BP13F240 AT END                                                 
                MOVE 'Y' TO WS-F240-FLAG                                        
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F240-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-REC.                                                         
      *-----------------------------------------------------------              
           MOVE SPACES          TO WS-K500-DTE-EST-CMPL                         
                                   WS-K500-DTE-LSE-COMM.                        
           MOVE SPACES          TO WS-UPDATE-TAG.                               
                                                                                
           PERFORM 350-BP13K240-READ      THRU 350-EXIT.                        
                                                                                
           PERFORM 200-READ-BP13F240      THRU 200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-BP13K240-READ.                                                       
      *-----------------------------------------------------------              
                                                                                
           MOVE SPACES                    TO K240-KEY-FLD.                      
           MOVE F240-KEY-FLD              TO K240-KEY-FLD.                      
                                                                                
           READ BP13K240.                                                       
           EVALUATE BP13K240-STATUS                                             
           WHEN 00                                                              
              ADD  1      TO WS-K240-READ                                       
              PERFORM 600-COMPUTE-DATE     THRU 600-EXIT                        
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND BP13K240 : ' K240-KEY-FLD               
              ADD   1          TO WS-K240-NFND                                  
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K240 : ' BP13K240-STATUS               
              MOVE BP13K240-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-BM06K100-READ.                                                       
      *-----------------------------------------------------------              
           MOVE LOW-VALUES           TO K100-KEY-FLD.                           
           MOVE F240-NUM-ESTATE      TO K100-ESTATE.                            
           MOVE F240-NUM-NEIGH       TO K100-NEIGHBOURHOOD.                     
           MOVE F240-NUM-CNTRCT      TO K100-CONTRACT-NO.                       
           MOVE F240-NUM-BLK         TO K100-BLK-NO.                            
                                                                                
           READ BM06K100.                                                       
           EVALUATE BM06K100-STATUS                                             
           WHEN 00                                                              
              ADD  1      TO WS-K100-READ                                       
              MOVE K100-CDE-BLK          TO WS-K100-CDE-BLK                     
              PERFORM 800-BM06K110-START THRU 800-EXIT                          
           WHEN 23                                                              
              MOVE SPACES                TO WS-K100-CDE-BLK                     
              MOVE SPACES                TO WS-K110-DTE-KEY-AVBL                
              DISPLAY 'RECORD NOT FOUND BM06K100 : ' K100-KEY-FLD               
              ADD   1          TO WS-K100-NFND                                  
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K100 : ' BM06K100-STATUS               
              MOVE BM06K100-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       460-BB14K500-READ.                                                       
      *-----------------------------------------------------------              
           MOVE LOW-VALUES           TO K500-KEY-FLD.                           
           MOVE WS-K100-CDE-BLK      TO K500-CDE-BLK.                           
                                                                                
           READ BB14K500.                                                       
           EVALUATE BB14K500-STATUS                                             
           WHEN 00                                                              
              ADD   1      TO WS-K500-READ                                      
              MOVE K500-DTE-BLDG-COMPLETE   TO WS-K500-DTE-EST-CMPL             
              MOVE K500-DTE-LEASE-COMM-FLAT TO WS-K500-DTE-LSE-COMM             
           WHEN 23                                                              
              ADD   1          TO WS-K500-NFND                                  
              DISPLAY 'RECORD NOT FOUND BB14K500 : ' K500-KEY-FLD               
              MOVE SPACES       TO WS-K500-DTE-EST-CMPL                         
              MOVE ZEROES       TO WS-K500-DTE-LSE-COMM                         
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BB14K500 : ' BB14K500-STATUS               
              MOVE BB14K500-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-BB14K990-READ.                                                       
      *-----------------------------------------------------------              
           MOVE LOW-VALUES           TO K990-ALT-KEY.                           
           MOVE WS-K100-CDE-BLK      TO K990-NUM-BLDNG-GL.                      
                                                                                
           READ BB14K990 KEY IS K990-ALT-KEY.                                   
           EVALUATE BB14K990-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
              ADD   1      TO WS-K990-READ                                      
              MOVE K990-DTE-REV-LCD         TO WS-K500-DTE-LSE-COMM             
           WHEN 23                                                              
              ADD   1          TO WS-K990-NFND                                  
              MOVE SPACES                   TO WS-K500-DTE-LSE-COMM             
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BB14K990 : ' BB14K990-STATUS               
              MOVE BB14K990-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-COMPUTE-DATE.                                                        
      *-----------------------------------------------------------              
                                                                                
           PERFORM 450-BM06K100-READ      THRU 450-EXIT.                        
           PERFORM 500-BB14K990-READ      THRU 500-EXIT.                        
           IF WS-K500-DTE-LSE-COMM = SPACES OR ZEROES OR LOW-VALUES             
              PERFORM 460-BB14K500-READ     THRU 460-EXIT                       
           END-IF.                                                              
           IF WS-K110-DTE-KEY-AVBL = ZEROES OR SPACES OR LOW-VALUES             
              PERFORM 702-GET-DLVRY-POSSN-DATE THRU 702-EXIT                    
              MOVE WS-DEL-POSSN-DATE      TO WS-DTE-DLVRY-POSSESSION            
           ELSE                                                                 
              IF WS-K500-DTE-LSE-COMM = ZEROES OR SPACES OR LOW-VALUES          
                 PERFORM 701-GET-COMM-DATE THRU 701-EXIT                        
              END-IF                                                            
              PERFORM 705-READ-BB14K580 THRU 705-EXIT                           
              IF K580-DTE-UNIT-DELIVERY = SPACES OR LOW-VALUES OR ZEROES        
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE SPACES TO WS-DTE-DLVRY-POSSESSION                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-DTE-DLVRY-POSSESSION NOT = K240-DTE-EST-CMPLTN                 
              MOVE WS-DTE-DLVRY-POSSESSION TO K240-DTE-EST-CMPLTN               
              ADD 1 TO WS-UPDATE-ECD                                            
              MOVE 'Y'          TO WS-UPDATE-TAG                                
           END-IF.                                                              
                                                                                
           IF WS-K500-DTE-LSE-COMM NOT = K240-DTE-LSE-COMM                      
              MOVE WS-K500-DTE-LSE-COMM    TO K240-DTE-LSE-COMM                 
              ADD 1 TO WS-UPDATE-LSE                                            
              MOVE 'Y'          TO WS-UPDATE-TAG                                
           END-IF.                                                              
                                                                                
           MOVE WS-K110-DTE-KEY-AVBL       TO K240-DTE-KEY-AVAIL.               
                                                                                
           IF WS-UPDATE-TAG = 'Y'                                               
              PERFORM 700-REWRITE-BP13K240 THRU 700-EXIT                        
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       700-REWRITE-BP13K240.                                                    
      *-----------------------------------------------------------              
                                                                                
           REWRITE K240-HEADER-REC.                                             
                                                                                
           EVALUATE BP13K240-STATUS                                             
           WHEN 00                                                              
                ADD 1     TO WS-K240-UPDATED                                    
           WHEN OTHER                                                           
                DISPLAY 'ERROR REWRITING BP13K240, STATUS  '                    
                        BP13K240-STATUS                                         
                        ',  KEY    ' K240-KEY-FLD                               
                PERFORM 999-CLOSE-ROUTINE    THRU 999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       701-GET-COMM-DATE.                                                       
      *-------------------------------------------------------------            
      **                                                                        
      * CALCULATE K500-DTE-LEASE-COMM-FLAT IF K500-DTE-BLDG-COMPLETE            
      * NOT EQUAL TO ZEROES                                                     
      **                                                                        
           IF K500-DTE-BLDG-COMPLETE = LOW-VALUES OR SPACES OR                  
                                       '00000000'                               
              GO TO 701-EXIT                                                    
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           MOVE K500-DTE-BLDG-COMPLETE TO WS-LCOMM-DTE.                         
                                                                                
           MOVE K100-KEY-FLD           TO K260-KEY-FLD.                         
                                                                                
           MOVE SPACES                 TO WS-K260-FND.                          
           READ BP13K260.                                                       
                                                                                
           EVALUATE BP13K260-STATUS                                             
           WHEN 00                                                              
                MOVE 'Y'            TO WS-K260-FND                              
           WHEN 23                                                              
                ADD   1             TO WS-K260-NFND                             
                MOVE 'N'            TO WS-K260-FND                              
           WHEN OTHER                                                           
                DISPLAY 'BP13K260 FILE READ ERROR.:' BP13K260-STATUS            
                PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
           IF WS-K260-FND = 'Y'                                                 
              ADD    13                TO WS-LCOMM-MM                           
           ELSE                                                                 
           IF K100-DTE-1ST-SALE  > '19960610'                                   
              ADD    13                TO WS-LCOMM-MM                           
           ELSE                                                                 
              ADD    10                TO WS-LCOMM-MM.                          
           DIVIDE WS-LCOMM-MM BY 12  GIVING    WS-YEAR-COMM                     
                                 REMAINDER WS-MONTH-COMM.                       
                                                                                
           IF WS-MONTH-COMM = 0                                                 
              MOVE 12                TO WS-LCOMM-MM                             
              ADD  1                 TO WS-LCOMM-YYYY                           
           ELSE                                                                 
              ADD  WS-YEAR-COMM      TO WS-LCOMM-YYYY                           
              MOVE WS-MONTH-COMM     TO WS-LCOMM-MM.                            
                                                                                
           MOVE 01               TO WS-LCOMM-DD.                                
           MOVE WS-LCOMM-DTE     TO WS-K500-DTE-LSE-COMM.                       
                                                                                
           IF F240-NUM-FLAT-TYPE2 = 'SA'                                        
              MOVE SPACES           TO WS-K500-DTE-LSE-COMM                     
           END-IF.                                                              
                                                                                
       701-EXIT.                                                                
           EXIT.                                                                
                                                                                
       702-GET-DLVRY-POSSN-DATE.                                                
      *-------------------------------------------------------------            
      *-----------------------------------------------------                    
      * **** COMPUTE DELIVERY POSSESSION DATE ***                               
      *     IF COMPLETION DATE NOT = SPACES                                     
      *        ADD 12 MONTHS TO COMPLETION DATE.                                
      *-----------------------------------------------------                    
           MOVE SPACES      TO WS-DEL-POSSN-DATE.                               
           IF K500-DTE-BLDG-COMPLETE = SPACES OR LOW-VALUES OR                  
                                       '00000000'                               
              GO TO 702-EXIT                                                    
           END-IF.                                                              
                                                                                
           MOVE K500-DTE-BLDG-COMPLETE TO WS-8-DATE-CHAR                        
           IF K100-DTE-1ST-SALE  >= '20200801'                                  
              IF (K100-ESTATE       = '645' AND                                 
                  K100-NEIGHBOURHOOD = 'N1  ' AND                               
                  K100-CONTRACT-NO   = 'C59 ') OR                               
                 (K100-ESTATE       = '646' AND                                 
                  K100-NEIGHBOURHOOD = 'N5  ' AND                               
                  K100-CONTRACT-NO   = 'C7  ') OR                               
                 (K100-ESTATE       = '646' AND                                 
                  K100-NEIGHBOURHOOD = 'N5  ' AND                               
                  K100-CONTRACT-NO   = 'C8  ') OR                               
                 (K100-ESTATE       = '645' AND                                 
                  K100-NEIGHBOURHOOD = 'N2  ' AND                               
                  K100-CONTRACT-NO   = 'C36 ') OR                               
                 (K100-ESTATE       = '644' AND                                 
                  K100-NEIGHBOURHOOD = 'N4  ' AND                               
                  K100-CONTRACT-NO   = 'C50 ') OR                               
                 (K100-ESTATE       = '720' AND                                 
                  K100-NEIGHBOURHOOD = 'N2  ' AND                               
                  K100-CONTRACT-NO   = 'C11 ')                                  
                 ADD 15         TO WS-8-DATE-MM                                 
              ELSE                                                              
                 ADD 18         TO WS-8-DATE-MM                                 
              END-IF                                                            
           ELSE                                                                 
              ADD 12         TO WS-8-DATE-MM                                    
           END-IF                                                               
           IF WS-8-DATE-MM > 12                                                 
              ADD 1                    TO WS-8-DATE-YYYY                        
              SUBTRACT 12            FROM WS-8-DATE-MM                          
              IF WS-8-DATE-MM > 12                                              
                 ADD 1                    TO WS-8-DATE-YYYY                     
                 SUBTRACT 12            FROM WS-8-DATE-MM                       
              END-IF                                                            
              MOVE WS-8-DATE-MM        TO WS-MONTH                              
              MOVE WS-8-DATE-DD        TO WS-DAY                                
              PERFORM 703-CHECK-DAY       THRU 703-EXIT                         
              MOVE WS-DAY              TO WS-DEL-POSSN-DD                       
              MOVE WS-8-DATE-MM        TO WS-DEL-POSSN-MM                       
              MOVE WS-8-DATE-YYYY      TO WS-DEL-POSSN-YYYY                     
           ELSE                                                                 
              MOVE WS-8-DATE-MM        TO WS-MONTH                              
              MOVE WS-8-DATE-DD        TO WS-DAY                                
              PERFORM 703-CHECK-DAY       THRU 703-EXIT                         
              MOVE WS-DAY               TO WS-DEL-POSSN-DD                      
              MOVE WS-8-DATE-MM         TO WS-DEL-POSSN-MM                      
              MOVE WS-8-DATE-YYYY       TO WS-DEL-POSSN-YYYY                    
           END-IF.                                                              
                                                                                
       702-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       703-CHECK-DAY.                                                           
      *---------------------------------------------------------------*         
           IF MTH-END-WITH-30                                                   
              IF WS-DAY = '31'                                                  
                 MOVE '30'   TO WS-DAY                                          
              END-IF                                                            
           ELSE                                                                 
              IF MTH-END-WITH-28                                                
                 IF WS-DAY = '29' OR '30' OR '31'                               
                    MOVE '28'   TO WS-DAY                                       
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       703-EXIT.                                                                
           EXIT.                                                                
                                                                                
       705-READ-BB14K580.                                                       
      *-------------------------------------------------------------            
           MOVE LOW-VALUES           TO K580-ALTX-KEY.                          
           MOVE WS-K100-CDE-BLK      TO K580-CDE-BLK.                           
                                                                                
           READ BB14K580 KEY IS K580-ALTX-KEY                                   
                                                                                
           EVALUATE BB14K500-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 CONTINUE                                                       
              WHEN 23                                                           
                  ADD  1 TO WS-K580-NFND                                        
              WHEN OTHER                                                        
                  DISPLAY 'BB13K580 READ ERROR : ' BB14K580-STATUS              
                  MOVE BB14K580-STATUS         TO   RETURN-CODE                 
                  PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       705-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-BM06K110-START.                                                      
      *-------------------------------------------------------------            
           MOVE 'N'                  TO  WS-K110-EOF.                           
           MOVE SPACES               TO  K110-AIX1.                             
           MOVE K100-KEY-FLD         TO  K110-AIX1.                             
                                                                                
           START BM06K110 KEY >= K110-AIX1                                      
                                                                                
           EVALUATE BM06K110-STATUS                                             
              WHEN 00                                                           
                 PERFORM 810-READNXT-BM06K110 THRU 810-EXIT                     
                         UNTIL WS-K110-EOF = 'Y'                                
              WHEN OTHER                                                        
                 DISPLAY 'BM06K110 READ ERROR : ' BM06K110-STATUS               
                 DISPLAY 'K110-AIX1           : ' K110-AIX1                     
                 MOVE BM06K110-STATUS         TO   RETURN-CODE                  
                 PERFORM 999-CLOSE-ROUTINE    THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
       810-READNXT-BM06K110.                                                    
      *-------------------------------------------------------------            
           READ BM06K110 NEXT RECORD AT END                                     
              MOVE 'Y'      TO WS-K110-EOF                                      
              GO TO 810-EXIT.                                                   
                                                                                
           EVALUATE BM06K110-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K100-KEY-FLD = K110-AIX1                                    
                    IF (K110-DTE-KEY-AVAIL NOT = ZEROS AND SPACES               
                                                   AND LOW-VALUES)              
                       MOVE K110-DTE-KEY-AVAIL TO WS-K110-DTE-KEY-AVBL          
                       MOVE 'Y'                TO WS-K110-EOF                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE '00000000'         TO WS-K110-DTE-KEY-AVBL             
                    MOVE 'Y'                TO WS-K110-EOF                      
                 END-IF                                                         
              WHEN 23                                                           
                 MOVE '00000000'            TO WS-K110-DTE-KEY-AVBL             
              WHEN OTHER                                                        
                 DISPLAY 'BM06K110 READ ERROR : ' BM06K110-STATUS               
                 DISPLAY 'BM06K110 KEY-FLD    : ' K110-SCH-ACC-NO               
                 MOVE BM06K110-STATUS       TO   RETURN-CODE                    
                 PERFORM 999-CLOSE-ROUTINE  THRU 999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       810-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-ROUTINE.                                                       
      *-----------------------------------------------------------              
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C27E       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECS READ FROM BP13F240 : ' WS-F240-READ.             
           DISPLAY 'NO OF RECS READ FROM BP13K240 : ' WS-K240-READ.             
           DISPLAY 'NO OF RECS READ FROM BM06K100 : ' WS-K100-READ.             
           DISPLAY 'NO OF RECS READ FROM BB14K500 : ' WS-K500-READ.             
           DISPLAY 'NO OF RECS READ FROM BB14K990 : ' WS-K990-READ.             
           DISPLAY 'NO OF RECS UPDATED - BP13K240 : ' WS-K240-UPDATED.          
           DISPLAY 'NO OF RECS UPDATED - ECD      : ' WS-UPDATE-ECD.            
           DISPLAY 'NO OF RECS UPDATED - LSE      : ' WS-UPDATE-LSE.            
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F240                                                       
                 BP13K260                                                       
                 BM06K100                                                       
                 BM06K110                                                       
                 BB14K500                                                       
                 BB14K580                                                       
                 BB14K990                                                       
                 BP13K240.                                                      
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BM06K100-STATUS ' BM06K100-STATUS           
              MOVE BM06K100-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BM06K110-STATUS ' BM06K110-STATUS           
              MOVE BM06K110-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BP13K260-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BP13K260-STATUS ' BP13K260-STATUS           
              MOVE BP13K260-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BB14K500-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BB14K500-STATUS ' BB14K500-STATUS           
              MOVE BB14K500-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BB14K580-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BB14K580-STATUS ' BB14K580-STATUS           
              MOVE BB14K580-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BB14K990-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BB14K990-STATUS ' BB14K990-STATUS           
              MOVE BB14K990-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BP13K240-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BP13K240-STATUS ' BP13K240-STATUS           
              MOVE BP13K240-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
