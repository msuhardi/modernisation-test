      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID.    BP13C379.                                                 
      *AUTHOR. SK.                                                              
      *DATE-WRITTEN.  18/07/2003.                                               
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  : RECONCILIATION STATMENMT 40-4153 ON    *                
      *                 SOLICITOR FEE :                        *                
      *                 TT 11 - RECEIPT FROM COLLECTION SYSTEM *                
      *                 TT 12 - CPF TRANS FROM IBG THRU BB24   *                
      *     TT 13, 14,14,15,16- PAYMENT THRU PAY SYSTEM        *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *           1. BP13F121                                  *                
      *           2. BP13K671                                  *                
      *           3. BP13K673                                  *                
      *           4. SY02F001                                  *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *           1. P13L379A                                  *                
      *           2. P13L379B                                  *                
      *           3. BP13F673                                  *                
      *                                                        *                
      * CHG-NO   BY    DATE     DETAILS                        *                
      * -------- ----  -------- -------------------------------*                
      * BP132347 SK    18072003 NEW PGM                        *                
      * BP132347 SK    29072003 MOVE SYSTEM DATE TO K673-DTE-  *                
      *                         CREATE, ADD SY02F001           *                
      * BP132400 SK    18122003 CATER FOR NEGATIVE AMOUNT      *                
      * BP132426 SK    18062004 INCREASE BP13F671 TO 3000      *                
      **********************************************************                
                                                                                
      *--------------------------------------------------------*                
       ENVIRONMENT DIVISION.                                                    
      *--------------------------------------------------------*                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F121 ASSIGN TO BP13F121.                                  
           SELECT BP13F671 ASSIGN TO BP13F671.                                  
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13K673 ASSIGN TO BP13K673                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K673-KEY-FLD                               
                  FILE STATUS     IS WS-K673-STATUS.                            
                                                                                
           SELECT BP13F673 ASSIGN TO BP13F673.                                  
           SELECT P13L379A ASSIGN TO P13L379A.                                  
           SELECT P13L379B ASSIGN TO P13L379B.                                  
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD  BP13F121                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F121.                                                           
                                                                                
       FD   BP13K673                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K673.                                                           
                                                                                
       FD  BP13F673                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F673.                                                           
                                                                                
       FD  BP13F671                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 3000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F671.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  P13L379A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS  132 CHARACTERS.                                     
       01  P13L379A-REC         PIC X(132).                                     
                                                                                
       FD  P13L379B                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS  132 CHARACTERS.                                     
       01  P13L379B-REC         PIC X(132).                                     
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F121-EOF             PIC X       VALUE 'N'.                   
           05  WS-F671-EOF             PIC X       VALUE 'N'.                   
           05  WS-K673-FOUND           PIC X       VALUE 'N'.                   
           05  WS-K671-FOUND           PIC X       VALUE 'N'.                   
           05  WS-SY02-EOF             PIC X       VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F121-READ         PIC 9(8)    VALUE ZEROES.                
           05 WS-CNT-F671-READ         PIC 9(8)    VALUE ZEROES.                
           05 WS-CNT-F673-WRITE        PIC 9(8)    VALUE ZEROES.                
           05 WS-CNT-UPD-K673          PIC 9(8)    VALUE ZEROES.                
           05 WS-CNT-WRITE-K673        PIC 9(8)    VALUE ZEROES.                
                                                                                
           05 WS-MATCH                 PIC 9(8)    VALUE ZEROES.                
           05 WS-NOT-MATCH-F121        PIC 9(8)    VALUE ZEROES.                
           05 WS-NOT-MATCH-F671        PIC 9(8)    VALUE ZEROES.                
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-TTYPE-LAST            PIC X(2)    VALUE SPACES.                
           05 WS-PCDE-LAST             PIC X(3)    VALUE SPACES.                
           05 WS-K673-STATUS           PIC 9(2)    VALUE ZEROES.                
           05 WS-REGN-F121             PIC X(8)    VALUE SPACES.                
           05 WS-PAY-TYPE              PIC X(3)    VALUE SPACES.                
                                                                                
           05 WS-AMOUNT                PIC 9(7)V99 VALUE 0.                     
                                                                                
           05 WS-OPEN-TOTAL            PIC S9(7)V99 VALUE 0.                    
           05 WS-SYST-TOTAL            PIC S9(7)V99 VALUE 0.                    
           05 WS-MANL-TOTAL            PIC S9(7)V99 VALUE 0.                    
           05 WS-PYMT-TOTAL-SYST       PIC S9(7)V99 VALUE 0.                    
           05 WS-PYMT-TOTAL-MANL       PIC S9(7)V99 VALUE 0.                    
           05 WS-CLOS-TOTAL            PIC S9(7)V99 VALUE 0.                    
                                                                                
           05 WS-SYST-TOTAL-A          PIC S9(7)V99 VALUE 0.                    
           05 WS-MANL-TOTAL-A          PIC S9(7)V99 VALUE 0.                    
           05 WS-PYMT-TOTAL-A          PIC S9(7)V99 VALUE 0.                    
           05 WS-CLOS-TOTAL-A          PIC S9(7)V99 VALUE 0.                    
                                                                                
           05 WS-SYST-TOTAL-B          PIC S9(7)V99 VALUE 0.                    
           05 WS-MANL-TOTAL-B          PIC S9(7)V99 VALUE 0.                    
           05 WS-PYMT-TOTAL-B          PIC S9(7)V99 VALUE 0.                    
           05 WS-CLOS-TOTAL-B          PIC S9(7)V99 VALUE 0.                    
                                                                                
           05 WS-GRAND-TOTAL-A         PIC S9(9)V99 VALUE 0.                    
           05 WS-GRAND-TOTAL-A-OP      PIC S9(9)V99 VALUE 0.                    
           05 WS-GRAND-TOTAL-A-SYS     PIC S9(9)V99 VALUE 0.                    
           05 WS-GRAND-TOTAL-A-MAN     PIC S9(9)V99 VALUE 0.                    
           05 WS-GRAND-TOTAL-A-PAY-SYS PIC S9(9)V99 VALUE 0.                    
           05 WS-GRAND-TOTAL-A-PAY-MAN PIC S9(9)V99 VALUE 0.                    
                                                                                
           05 WS-GRAND-TOTAL-B         PIC S9(9)V99 VALUE 0.                    
           05 WS-GRAND-TOTAL-B-OP      PIC S9(9)V99 VALUE 0.                    
           05 WS-GRAND-TOTAL-B-SYS     PIC S9(9)V99 VALUE 0.                    
           05 WS-GRAND-TOTAL-B-MAN     PIC S9(9)V99 VALUE 0.                    
           05 WS-GRAND-TOTAL-B-PAY-SYS PIC S9(9)V99 VALUE 0.                    
           05 WS-GRAND-TOTAL-B-PAY-MAN PIC S9(9)V99 VALUE 0.                    
                                                                                
           05 WS-CNT-LINE-ON-PAGE1     PIC 9(8)    VALUE 66.                    
           05 WS-CNT-PRINT-L379-A      PIC 9(8)    VALUE 1.                     
           05 WS-CNT-WRITE-L379-A      PIC 9(8)    VALUE 0.                     
           05 WS-NO-OF-PAGE1           PIC 9(8)    VALUE 0.                     
                                                                                
           05 WS-CNT-LINE-ON-PAGE2     PIC 9(8)    VALUE 66.                    
           05 WS-CNT-PRINT-L379-B      PIC 9(8)    VALUE 1.                     
           05 WS-CNT-WRITE-L379-B      PIC 9(8)    VALUE 0.                     
           05 WS-NO-OF-PAGE2           PIC 9(8)    VALUE 0.                     
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY             PIC 9(4).                                
           05  WS-SYS-MM               PIC 9(2).                                
           05  WS-SYS-DD               PIC 9(2).                                
                                                                                
       01  WS-EDITED-DATE              PIC X(10).                               
       01  WS-NUM-REGN                 PIC X(08) VALUE SPACES.                  
       01  WS-NUM-SCH                  PIC X(11) VALUE SPACES.                  
       01  WS-TTYPE                    PIC X(02) VALUE SPACES.                  
                                                                                
      *-----------------------------------------------------------*             
      *    WS FOR CONTROL LISTING                                 *             
      *-----------------------------------------------------------*             
                                                                                
       01  L379-PR-HEAD-01-A.                                                   
           05  FILLER                   PIC X(14) VALUE 'P13L379A'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L379-PRINT-DATE-1        PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L379-PRINT-PAGE-1        PIC ZZZ9.                               
                                                                                
       01  L379-PR-HEAD-02-A.                                                   
           05  FILLER                   PIC X(45) VALUE SPACES.                 
           05  FILLER                   PIC X(80) VALUE                         
           'RECONCILIATION STATEMNT FOR ACCOUNT 40-4153'.                       
                                                                                
       01  L379-PR-HEAD-01-B.                                                   
           05  FILLER                   PIC X(14) VALUE 'P13L379B'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L379-PRINT-DATE-2        PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L379-PRINT-PAGE-2        PIC ZZZ9.                               
                                                                                
       01  L379-PR-HEAD-02-B.                                                   
           05  FILLER                   PIC X(45) VALUE SPACES.                 
           05  FILLER                   PIC X(80) VALUE                         
           'IBG 40-4153 NOT FOUND IN SOC BO TRANS FILE'.                        
                                                                                
       01  L379-PR-HEAD-03.                                                     
           05  FILLER          PIC X(5)  VALUE ' S-NO'.                         
           05  FILLER          PIC X(2)  VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE 'REGN NO'.                       
           05  FILLER          PIC X(15) VALUE 'SCHEME A/C'.                    
           05  FILLER          PIC X(4)  VALUE 'HBL'.                           
           05  FILLER          PIC X(5)  VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE 'OPENING'.                       
           05  FILLER          PIC X(8)  VALUE SPACES.                          
           05  FILLER          PIC X(25) VALUE '      RECEIVE'.                 
           05  FILLER          PIC X(7)  VALUE SPACES.                          
           05  FILLER          PIC X(25) VALUE '      PAYMENT'.                 
           05  FILLER          PIC X(7)  VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE 'CLOSING'.                       
                                                                                
       01  L379-PR-HEAD-03A.                                                    
           05  FILLER          PIC X(59) VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE 'SYSTEM'.                        
           05  FILLER          PIC X(6)  VALUE SPACES.                          
           05  FILLER          PIC X(16) VALUE 'MANUAL'.                        
           05  FILLER          PIC X(10) VALUE 'SYSTEM'.                        
           05  FILLER          PIC X(5)  VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE 'MANUAL'.                        
                                                                                
       01  L379-PR-DETAILS.                                                     
           05  L379-NUMBER       PIC ZZZZ9.                                     
           05  FILLER            PIC X(2)  VALUE SPACES.                        
           05  L379-REGN         PIC X(10).                                     
           05  L379-SCH-ACC      PIC X(15).                                     
           05  L379-HBL          PIC X(4).                                      
           05  FILLER            PIC X(1)  VALUE SPACES.                        
           05  L379-OPENING      PIC $$$$,$$9.99-.                              
           05  FILLER            PIC X(4)  VALUE SPACES.                        
           05  L379-SYSTEM       PIC $$$$,$$9.99.                               
           05  FILLER            PIC X(4)  VALUE SPACES.                        
           05  L379-MANUAL       PIC $$$$,$$9.99-.                              
           05  FILLER            PIC X(4)  VALUE SPACES.                        
           05  L379-PAYMENT-SYS  PIC $$$$,$$9.99.                               
           05  FILLER            PIC X(4)  VALUE SPACES.                        
           05  L379-PAYMENT-MAN  PIC $$$$,$$9.99-.                              
           05  FILLER            PIC X(6)  VALUE SPACES.                        
           05  L379-CLOSING      PIC $$$$,$$9.99-.                              
                                                                                
       01  L379-PR-FOOTER-A.                                                    
           05  FILLER                  PIC X(34) VALUE 'GRAND TOTAL :'.         
           05  L379-GRAND-TOTAL-OP-A       PIC $$$,$$$,$$9.99-.                 
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  L379-GRAND-TOTAL-SYS-A      PIC $$$,$$$,$$9.99.                  
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  L379-GRAND-TOTAL-MAN-A      PIC $$$,$$$,$$9.99-.                 
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  L379-GRAND-TOTAL-PAY-A-SYS  PIC $$$,$$$,$$9.99.                  
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  L379-GRAND-TOTAL-PAY-A-MAN  PIC $$$,$$$,$$9.99-.                 
           05  FILLER                      PIC X(3)  VALUE SPACES.              
           05  L379-GRAND-TOTAL-A          PIC $$$,$$$,$$9.99.                  
                                                                                
       01  L379-PR-FOOTER-B.                                                    
           05  FILLER                  PIC X(34) VALUE 'GRAND TOTAL :'.         
           05  L379-GRAND-TOTAL-OP-B       PIC $$$,$$$,$$9.99-.                 
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  L379-GRAND-TOTAL-SYS-B      PIC $$$,$$$,$$9.99.                  
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  L379-GRAND-TOTAL-MAN-B      PIC $$$,$$$,$$9.99-.                 
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  L379-GRAND-TOTAL-PAY-B-SYS  PIC $$$,$$$,$$9.99.                  
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  L379-GRAND-TOTAL-PAY-B-MAN  PIC $$$,$$$,$$9.99-.                 
           05  FILLER                      PIC X(3)  VALUE SPACES.              
           05  L379-GRAND-TOTAL-B          PIC $$$,$$$,$$9.99.                  
                                                                                
       01  L379-PR-LINE.                                                        
           05  FILLER                PIC X(132) VALUE ALL '-'.                  
                                                                                
       01  L379-PR-BLANK             PIC X(132) VALUE SPACES.                   
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
       0000-MAIN.                                                               
                                                                                
           PERFORM 1000-OPEN-FILE       THRU 1000-EXIT.                         
           PERFORM 1100-READ-F671       THRU 1100-EXIT.                         
           PERFORM 1200-READ-F121       THRU 1200-EXIT.                         
           PERFORM 1300-READ-SY02F001   THRU 1300-EXIT.                         
           PERFORM 2000-PROCESS         THRU 2000-EXIT                          
                   UNTIL ( F671-NUM-REGN = HIGH-VALUES AND                      
                           F121-NUM-REGN = HIGH-VALUES)                         
                                                                                
           PERFORM 5200-PRINT-FOOTER    THRU 5200-EXIT.                         
           PERFORM 5300-PRINT-FOOTER    THRU 5300-EXIT.                         
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILE.                                                          
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F121                                                 
                       BP13F671                                                 
                       SY02F001                                                 
                I-O    BP13K673                                                 
                OUTPUT P13L379A                                                 
                       P13L379B                                                 
                       BP13F673.                                                
                                                                                
           EVALUATE WS-K673-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   MOVE WS-K673-STATUS      TO RETURN-CODE                      
                   DISPLAY 'OPEN BP13K673 FAIL. STATUS=' WS-K673-STATUS         
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1100-READ-F671.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F671                                                        
                AT END  MOVE 'Y' TO WS-F671-EOF                                 
                MOVE HIGH-VALUES TO F671-NUM-REGN                               
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F671-READ.                                           
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1200-READ-F121.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F121                                                        
                AT END  MOVE 'Y' TO WS-F121-EOF                                 
                MOVE HIGH-VALUES TO F121-NUM-REGN                               
                GO TO 1200-EXIT                                                 
           END-READ.                                                            
                                                                                
           IF WS-REGN-F121 = SPACES OR LOW-VALUES                               
              MOVE F121-NUM-REGN       TO WS-REGN-F121                          
              MOVE F121-NUM-SCH-ACCT   TO WS-NUM-SCH                            
              MOVE F121-CDE-TRANS-TYPE TO WS-TTYPE                              
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-CNT-F121-READ.                                           
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1300-READ-SY02F001.                                                      
      *--------------------------------------------------------*                
                                                                                
           READ SY02F001 AT END MOVE 'Y' TO WS-SY02-EOF                         
                GO TO 1300-EXIT                                                 
           END-READ.                                                            
                                                                                
           MOVE F001-DTE-CURRENT  TO WS-SYSTEM-DATE.                            
                                                                                
           STRING  WS-SYSTEM-DATE(7:2) '/' WS-SYSTEM-DATE(5:2) '/'              
                   WS-SYSTEM-DATE(1:4) DELIMITED BY SIZE                        
                   INTO WS-EDITED-DATE                                          
           END-STRING.                                                          
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-PROCESS.                                                            
      *--------------------------------------------------------*                
                                                                                
           MOVE 0 TO WS-AMOUNT.                                                 
           MOVE 'N' TO WS-K671-FOUND.                                           
           IF F671-NUM-REGN = F121-NUM-REGN                                     
              ADD 1 TO WS-MATCH                                                 
              MOVE 'Y' TO WS-K671-FOUND                                         
              IF  F671-AMT-RECEIPT-MANUAL IS NOT NUMERIC                        
                  MOVE 0 TO F671-AMT-RECEIPT-MANUAL                             
              END-IF                                                            
                                                                                
              MOVE 'N'                    TO WS-K673-FOUND                      
                                                                                
              MOVE 'SOL'          TO WS-PAY-TYPE                                
              MOVE F671-NUM-REGN  TO WS-NUM-REGN                                
              PERFORM 3600-READ-K673      THRU 3600-EXIT                        
                                                                                
              IF WS-K673-FOUND = 'Y'                                            
                 COMPUTE WS-CLOS-TOTAL = WS-CLOS-TOTAL +                        
                                         WS-OPEN-TOTAL                          
              END-IF                                                            
                                                                                
              PERFORM 3000-CHECK-1         THRU 3000-EXIT                       
                UNTIL F121-NUM-REGN NOT = WS-REGN-F121                          
                OR WS-F121-EOF = 'Y'                                            
                                                                                
              PERFORM 2500-WRITE-UPD      THRU 2500-EXIT                        
              PERFORM 1100-READ-F671      THRU 1100-EXIT                        
              MOVE F121-NUM-REGN          TO WS-REGN-F121                       
              MOVE F121-NUM-SCH-ACCT      TO WS-NUM-SCH                         
              MOVE F121-CDE-TRANS-TYPE    TO WS-TTYPE                           
           ELSE                                                                 
              IF F671-NUM-REGN < F121-NUM-REGN                                  
                 PERFORM 2100-PRINT-FND-K671 THRU 2100-EXIT                     
                 ADD 1 TO WS-NOT-MATCH-F121                                     
                 PERFORM 1100-READ-F671   THRU 1100-EXIT                        
              ELSE                                                              
                 PERFORM 3050-CHECK-2         THRU 3050-EXIT                    
                    UNTIL F121-NUM-REGN NOT = WS-REGN-F121                      
                    OR WS-F121-EOF = 'Y'                                        
                 PERFORM 2200-PRINT-FND-F121 THRU 2200-EXIT                     
                 ADD 1 TO WS-NOT-MATCH-F671                                     
                 MOVE F121-NUM-REGN       TO WS-REGN-F121                       
                 MOVE F121-NUM-SCH-ACCT   TO WS-NUM-SCH                         
                 MOVE F121-CDE-TRANS-TYPE TO WS-TTYPE                           
      *          PERFORM 1200-READ-F121   THRU 1200-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2100-PRINT-FND-K671.                                                     
      *--------------------------------------------------------*                
                                                                                
            MOVE 'N'  TO WS-K673-FOUND.                                         
            MOVE F671-NUM-REGN  TO WS-NUM-REGN.                                 
                                                                                
            MOVE 'SOL'          TO WS-PAY-TYPE.                                 
            PERFORM 3600-READ-K673          THRU 3600-EXIT.                     
                                                                                
            IF WS-OPEN-TOTAL NOT = 0                                            
               PERFORM 3100-WRITE-F673     THRU 3100-EXIT                       
               PERFORM 4170-MOVE-DATA-C    THRU 4170-EXIT                       
               PERFORM 4200-WRITE-P13L379A THRU 4200-EXIT                       
               PERFORM 4250-WRITE-BLANK    THRU 4250-EXIT                       
            END-IF                                                              
                                                                                
            IF WS-K673-FOUND = 'Y'                                              
               MOVE WS-OPEN-TOTAL TO WS-CLOS-TOTAL                              
               PERFORM 3700-UPDATE-K673 THRU 3700-EXIT                          
            ELSE                                                                
               PERFORM 3800-WRITE-K673 THRU 3800-EXIT                           
            END-IF.                                                             
                                                                                
                                                                                
           MOVE 0                     TO WS-OPEN-TOTAL                          
                                         WS-PYMT-TOTAL-SYST                     
                                         WS-PYMT-TOTAL-MANL                     
                                         WS-SYST-TOTAL                          
                                         WS-MANL-TOTAL                          
                                         WS-CLOS-TOTAL                          
                                         WS-PYMT-TOTAL-A                        
                                         WS-SYST-TOTAL-A                        
                                         WS-MANL-TOTAL-A                        
                                         WS-PYMT-TOTAL-B                        
                                         WS-SYST-TOTAL-B                        
                                         WS-MANL-TOTAL-B.                       
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2200-PRINT-FND-F121.                                                     
      *--------------------------------------------------------*                
                                                                                
           PERFORM 4160-MOVE-DATA-B    THRU 4160-EXIT.                          
           PERFORM 4300-WRITE-P13L379B THRU 4300-EXIT.                          
           PERFORM 4310-WRITE-BLANK    THRU 4310-EXIT.                          
                                                                                
                                                                                
           MOVE 0                     TO WS-OPEN-TOTAL                          
                                         WS-PYMT-TOTAL-SYST                     
                                         WS-PYMT-TOTAL-MANL                     
                                         WS-SYST-TOTAL                          
                                         WS-MANL-TOTAL                          
                                         WS-CLOS-TOTAL                          
                                         WS-PYMT-TOTAL-A                        
                                         WS-SYST-TOTAL-A                        
                                         WS-MANL-TOTAL-A                        
                                         WS-PYMT-TOTAL-B                        
                                         WS-SYST-TOTAL-B                        
                                         WS-MANL-TOTAL-B                        
                                         WS-AMOUNT.                             
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2500-WRITE-UPD.                                                          
      *--------------------------------------------------------*                
                                                                                
           PERFORM 3100-WRITE-F673     THRU 3100-EXIT.                          
                                                                                
           IF WS-K673-FOUND = 'Y'                                               
              COMPUTE WS-GRAND-TOTAL-A-OP =  WS-GRAND-TOTAL-A-OP +              
                                             K673-AMT-OPEN                      
              PERFORM 3700-UPDATE-K673   THRU 3700-EXIT                         
           ELSE                                                                 
              PERFORM 3800-WRITE-K673    THRU 3800-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 4150-MOVE-DATA-A    THRU 4150-EXIT.                          
           PERFORM 4200-WRITE-P13L379A THRU 4200-EXIT.                          
           PERFORM 4250-WRITE-BLANK    THRU 4250-EXIT.                          
                                                                                
           MOVE 0                     TO WS-OPEN-TOTAL                          
                                         WS-PYMT-TOTAL-SYST                     
                                         WS-PYMT-TOTAL-MANL                     
                                         WS-SYST-TOTAL                          
                                         WS-MANL-TOTAL                          
                                         WS-CLOS-TOTAL                          
                                         WS-PYMT-TOTAL-A                        
                                         WS-SYST-TOTAL-A                        
                                         WS-MANL-TOTAL-A                        
                                         WS-PYMT-TOTAL-B                        
                                         WS-SYST-TOTAL-B                        
                                         WS-MANL-TOTAL-B                        
                                         WS-AMOUNT.                             
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-CHECK-1.                                                            
      *--------------------------------------------------------*                
                                                                                
                                                                                
           IF F121-AMT-RECIEPT IS NOT NUMERIC                                   
              MOVE 0 TO F121-AMT-RECIEPT                                        
           END-IF.                                                              
                                                                                
           MOVE F121-AMT-RECIEPT TO WS-AMOUNT.                                  
                                                                                
           IF F121-CDE-TRANS-TYPE = '11' OR '12'                                
              COMPUTE WS-SYST-TOTAL    = WS-SYST-TOTAL   +                      
                                         WS-AMOUNT                              
              COMPUTE WS-GRAND-TOTAL-A = WS-GRAND-TOTAL-A +                     
                                         WS-AMOUNT                              
              COMPUTE WS-CLOS-TOTAL    = WS-CLOS-TOTAL   +                      
                                         WS-AMOUNT                              
              COMPUTE WS-GRAND-TOTAL-A-SYS = WS-GRAND-TOTAL-A-SYS +             
                                             F121-AMT-RECIEPT                   
           END-IF.                                                              
                                                                                
           IF F121-CDE-TRANS-TYPE = '13' OR '14' OR '15' OR '16'                
               COMPUTE WS-PYMT-TOTAL-SYST  = WS-PYMT-TOTAL-SYST +               
                                             WS-AMOUNT                          
               COMPUTE WS-CLOS-TOTAL   =  WS-CLOS-TOTAL -                       
                                          WS-AMOUNT                             
              COMPUTE WS-GRAND-TOTAL-A-PAY-SYS =                                
                                         WS-GRAND-TOTAL-A-PAY-SYS +             
                                         WS-AMOUNT                              
              COMPUTE WS-GRAND-TOTAL-A = WS-GRAND-TOTAL-A +                     
                                         WS-AMOUNT                              
           END-IF.                                                              
                                                                                
           PERFORM 1200-READ-F121      THRU 1200-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3050-CHECK-2.                                                            
      *--------------------------------------------------------*                
                                                                                
                                                                                
           IF F121-AMT-RECIEPT IS NOT NUMERIC                                   
              MOVE 0 TO F121-AMT-RECIEPT                                        
           END-IF.                                                              
                                                                                
           MOVE F121-AMT-RECIEPT TO WS-AMOUNT.                                  
                                                                                
           IF F121-CDE-TRANS-TYPE = '11' OR '12'                                
              COMPUTE WS-SYST-TOTAL    = WS-SYST-TOTAL   +                      
                                         WS-AMOUNT                              
              COMPUTE WS-GRAND-TOTAL-B-SYS = WS-GRAND-TOTAL-B-SYS +             
                                             F121-AMT-RECIEPT                   
           END-IF.                                                              
                                                                                
           IF F121-CDE-TRANS-TYPE = '13' OR '14' OR '15' OR '16'                
                                                                                
              COMPUTE WS-PYMT-TOTAL-SYST  = WS-PYMT-TOTAL-SYST +                
                                             WS-AMOUNT                          
              COMPUTE WS-GRAND-TOTAL-B-PAY-SYS =                                
                                         WS-GRAND-TOTAL-B-PAY-SYS +             
                                         WS-AMOUNT                              
           END-IF.                                                              
                                                                                
           MOVE F121-NUM-SCH-ACCT   TO WS-NUM-SCH.                              
           MOVE F121-CDE-TRANS-TYPE TO WS-TTYPE.                                
                                                                                
           PERFORM 1200-READ-F121      THRU 1200-EXIT.                          
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3100-WRITE-F673.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                   TO BP13F673-BAL.                       
           INITIALIZE                       BP13F673-BAL.                       
                                                                                
           MOVE WS-NUM-REGN              TO F673-NUM-REGN.                      
           MOVE WS-PAY-TYPE              TO F673-CDE-PAYMENT-TYPE.              
           MOVE WS-OPEN-TOTAL            TO F673-AMT-OPEN.                      
                                                                                
           MOVE WS-SYST-TOTAL            TO F673-AMT-RECEIPT-SYSTEM             
           MOVE WS-MANL-TOTAL            TO F673-AMT-RECEIPT-MANUAL.            
           MOVE WS-PYMT-TOTAL-SYST       TO F673-AMT-PAYMENT-SYSTEM.            
           MOVE WS-PYMT-TOTAL-MANL       TO F673-AMT-PAYMENT-MANUAL.            
           MOVE WS-CLOS-TOTAL            TO F673-AMT-CLOSE.                     
           MOVE WS-SYSTEM-DATE           TO F673-DTE-UPDATE                     
                                            F673-DTE-CREATE.                    
                                                                                
           WRITE  BP13F673-BAL.                                                 
           ADD 1  TO WS-CNT-F673-WRITE.                                         
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3600-READ-K673.                                                          
      *----------------------------------------------------------------*        
                                                                                
           MOVE LOW-VALUES            TO K673-KEY-FLD.                          
           MOVE WS-NUM-REGN           TO K673-NUM-REGN.                         
           MOVE WS-PAY-TYPE           TO K673-CDE-PAYMENT-TYPE.                 
                                                                                
           READ BP13K673.                                                       
                                                                                
           EVALUATE WS-K673-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                   MOVE K673-AMT-OPEN TO WS-OPEN-TOTAL                          
                   MOVE 'Y'           TO WS-K673-FOUND                          
              WHEN 23                                                           
                   MOVE 0             TO WS-OPEN-TOTAL                          
                   DISPLAY '** REGN NO NOT FOUND IN BP13K673 ** '               
                           F671-NUM-REGN                                        
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K673, STATUS = '                   
                          WS-K673-STATUS                                        
                 PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3700-UPDATE-K673.                                                        
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CLOS-TOTAL NOT = 0                                             
              MOVE 0                  TO  K673-AMT-RECEIPT-SYSTEM               
                                          K673-AMT-RECEIPT-MANUAL               
                                          K673-AMT-PAYMENT-SYSTEM               
                                          K673-AMT-PAYMENT-MANUAL               
                                          K673-AMT-OPEN                         
                                          K673-AMT-CLOSE                        
                                                                                
              MOVE WS-CLOS-TOTAL      TO  K673-AMT-OPEN                         
                                             K673-AMT-CLOSE                     
                                                                                
              MOVE WS-SYSTEM-DATE     TO  K673-DTE-UPDATE                       
                                                                                
              REWRITE BP13K673-BAL                                              
                                                                                
              EVALUATE WS-K673-STATUS                                           
                 WHEN 00                                                        
                 WHEN 97                                                        
                      ADD 1 TO WS-CNT-UPD-K673                                  
                      CONTINUE                                                  
                 WHEN OTHER                                                     
                    DISPLAY 'ERRROR UPDATING BP13K673, STATUS = '               
                             WS-K673-STATUS                                     
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
              END-EVALUATE                                                      
           ELSE                                                                 
              PERFORM 3710-DELETE-K673   THRU 3710-EXIT                         
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3710-DELETE-K673.                                                        
      *----------------------------------------------------------------*        
                                                                                
           DELETE BP13K673.                                                     
                                                                                
           EVALUATE WS-K673-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13K673, STATUS = '                  
                          WS-K673-STATUS                                        
                 PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3710-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3800-WRITE-K673.                                                         
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CLOS-TOTAL NOT = 0                                             
              MOVE SPACES             TO BP13K673-BAL                           
              INITIALIZE                 BP13K673-BAL                           
                                                                                
              MOVE WS-NUM-REGN        TO K673-NUM-REGN                          
              MOVE WS-PAY-TYPE        TO K673-CDE-PAYMENT-TYPE                  
                                                                                
              MOVE WS-CLOS-TOTAL      TO  K673-AMT-OPEN                         
                                          K673-AMT-CLOSE                        
                                                                                
              MOVE 0                  TO  K673-AMT-RECEIPT-SYSTEM               
                                          K673-AMT-RECEIPT-MANUAL               
                                          K673-AMT-PAYMENT-SYSTEM               
                                          K673-AMT-PAYMENT-MANUAL               
              MOVE WS-SYSTEM-DATE     TO  K673-DTE-UPDATE                       
                                          K673-DTE-CREATE                       
                                                                                
              WRITE BP13K673-BAL                                                
                                                                                
              EVALUATE WS-K673-STATUS                                           
                 WHEN 00                                                        
                 WHEN 97                                                        
                      ADD 1 TO WS-CNT-WRITE-K673                                
                      CONTINUE                                                  
                 WHEN OTHER                                                     
                    DISPLAY 'ERRROR IN WRITING BP13K673, STATUS = '             
                             WS-K673-STATUS                                     
                             DISPLAY 'ERROR WRITE ' F671-NUM-REGN               
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4100-PRINT-HEADING-A.                                                    
      *----------------------------------------------------------------*        
                                                                                
           ADD  1                TO     WS-NO-OF-PAGE1.                         
           MOVE WS-NO-OF-PAGE1   TO     L379-PRINT-PAGE-1.                      
           MOVE WS-EDITED-DATE   TO     L379-PRINT-DATE-1.                      
                                                                                
           MOVE SPACES           TO     P13L379A-REC.                           
           INITIALIZE                   P13L379A-REC.                           
                                                                                
           WRITE P13L379A-REC    AFTER  PAGE.                                   
           WRITE P13L379A-REC    FROM   L379-PR-HEAD-01-A  AFTER 1.             
           WRITE P13L379A-REC    FROM   L379-PR-HEAD-02-A  AFTER 1.             
           WRITE P13L379A-REC    FROM   L379-PR-HEAD-03  AFTER 1.               
           WRITE P13L379A-REC    FROM   L379-PR-HEAD-03A AFTER 1.               
           WRITE P13L379A-REC    FROM   L379-PR-LINE     AFTER 1.               
                                                                                
           MOVE 5  TO WS-CNT-LINE-ON-PAGE1.                                     
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4150-MOVE-DATA-A.                                                        
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES            TO L379-PR-DETAILS.                           
           INITIALIZE                L379-PR-DETAILS.                           
                                                                                
           MOVE WS-CNT-PRINT-L379-A TO L379-NUMBER.                             
           MOVE F671-NUM-REGN       TO L379-REGN.                               
                                                                                
           STRING F671-NUM-SCH-ACC(1:4) '-'                                     
                  F671-NUM-SCH-ACC(5:4) '-'                                     
                  F671-NUM-SCH-ACC(9:1) '-'                                     
                  F671-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE                                             
                  INTO  L379-SCH-ACC                                            
           END-STRING.                                                          
                                                                                
           MOVE F671-NUM-HBL-TYP        TO L379-HBL.                            
           MOVE WS-OPEN-TOTAL           TO L379-OPENING.                        
           MOVE WS-SYST-TOTAL           TO L379-SYSTEM.                         
           MOVE WS-MANL-TOTAL           TO L379-MANUAL.                         
           MOVE WS-PYMT-TOTAL-SYST      TO L379-PAYMENT-SYS.                    
           MOVE WS-PYMT-TOTAL-MANL      TO L379-PAYMENT-MAN.                    
           MOVE WS-CLOS-TOTAL           TO L379-CLOSING.                        
                                                                                
           COMPUTE WS-CLOS-TOTAL-A = WS-CLOS-TOTAL-A +                          
                                     WS-CLOS-TOTAL.                             
                                                                                
       4150-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       4160-MOVE-DATA-B.                                                        
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES            TO L379-PR-DETAILS.                           
           INITIALIZE                L379-PR-DETAILS.                           
                                                                                
           MOVE WS-CNT-PRINT-L379-B TO L379-NUMBER.                             
           MOVE WS-REGN-F121        TO L379-REGN.                               
           STRING WS-NUM-SCH(1:4) '-'                                           
                  WS-NUM-SCH(5:4) '-'                                           
                  WS-NUM-SCH(9:1) '-'                                           
                  WS-NUM-SCH(10:2)                                              
                  DELIMITED BY SIZE                                             
                  INTO  L379-SCH-ACC                                            
           END-STRING.                                                          
                                                                                
           MOVE SPACES                  TO L379-HBL.                            
           MOVE WS-OPEN-TOTAL           TO L379-OPENING.                        
           MOVE WS-SYST-TOTAL           TO L379-SYSTEM.                         
           MOVE 0                       TO L379-MANUAL.                         
           MOVE WS-PYMT-TOTAL-SYST      TO L379-PAYMENT-SYS.                    
           MOVE 0                       TO L379-PAYMENT-MAN.                    
                                                                                
           COMPUTE WS-CLOS-TOTAL = (WS-OPEN-TOTAL + WS-SYST-TOTAL) -            
                                   WS-PYMT-TOTAL-SYST.                          
           MOVE WS-CLOS-TOTAL           TO L379-CLOSING.                        
                                                                                
           COMPUTE WS-CLOS-TOTAL-B = WS-CLOS-TOTAL-B +                          
                                     WS-CLOS-TOTAL.                             
           COMPUTE WS-GRAND-TOTAL-B-OP = WS-GRAND-TOTAL-B-OP +                  
                                      WS-OPEN-TOTAL.                            
                                                                                
       4160-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4170-MOVE-DATA-C.                                                        
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES            TO L379-PR-DETAILS.                           
           INITIALIZE                L379-PR-DETAILS.                           
                                                                                
           MOVE WS-CNT-PRINT-L379-A TO L379-NUMBER                              
           COMPUTE WS-GRAND-TOTAL-A-OP =  WS-GRAND-TOTAL-A-OP +                 
                                          WS-OPEN-TOTAL.                        
           COMPUTE WS-CLOS-TOTAL-A = WS-CLOS-TOTAL-A +                          
                                     WS-OPEN-TOTAL.                             
                                                                                
           MOVE F671-NUM-REGN       TO L379-REGN.                               
                                                                                
           STRING F671-NUM-SCH-ACC(1:4) '-'                                     
                  F671-NUM-SCH-ACC(5:4) '-'                                     
                  F671-NUM-SCH-ACC(9:1) '-'                                     
                  F671-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE                                             
                  INTO  L379-SCH-ACC                                            
           END-STRING.                                                          
                                                                                
           MOVE F671-NUM-HBL-TYP        TO L379-HBL.                            
           MOVE WS-OPEN-TOTAL           TO L379-OPENING.                        
           MOVE 0                       TO L379-SYSTEM.                         
           MOVE 0                       TO L379-MANUAL.                         
           MOVE 0                       TO L379-PAYMENT-SYS.                    
           MOVE 0                       TO L379-PAYMENT-MAN.                    
           MOVE WS-OPEN-TOTAL           TO L379-CLOSING.                        
                                                                                
                                                                                
       4170-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4200-WRITE-P13L379A.                                                     
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-LINE-ON-PAGE1 > 54                                         
              PERFORM 4100-PRINT-HEADING-A THRU 4100-EXIT                       
              MOVE 1  TO WS-CNT-LINE-ON-PAGE1                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES         TO P13L379A-REC.                                 
           INITIALIZE             P13L379A-REC.                                 
                                                                                
                                                                                
           WRITE P13L379A-REC   FROM L379-PR-DETAILS AFTER 1.                   
           ADD   1              TO WS-CNT-LINE-ON-PAGE1.                        
           ADD   1              TO WS-CNT-PRINT-L379-A.                         
           ADD   1              TO WS-CNT-WRITE-L379-A.                         
           MOVE  SPACES         TO L379-PR-DETAILS.                             
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4250-WRITE-BLANK.                                                        
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-LINE-ON-PAGE1 > 54                                         
              PERFORM 4100-PRINT-HEADING-A THRU 4100-EXIT                       
              MOVE 1  TO WS-CNT-LINE-ON-PAGE1                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES         TO P13L379A-REC.                                 
           INITIALIZE             P13L379A-REC.                                 
                                                                                
           WRITE P13L379A-REC   FROM L379-PR-BLANK AFTER 1.                     
           ADD   1              TO WS-CNT-LINE-ON-PAGE1.                        
           MOVE  SPACES         TO L379-PR-DETAILS.                             
                                                                                
       4250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4300-WRITE-P13L379B.                                                     
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-LINE-ON-PAGE2 > 54                                         
              PERFORM 4350-PRINT-HEADING-B THRU 4350-EXIT                       
              MOVE 1  TO WS-CNT-LINE-ON-PAGE2                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES         TO P13L379B-REC.                                 
           INITIALIZE             P13L379B-REC.                                 
                                                                                
                                                                                
           WRITE P13L379B-REC   FROM L379-PR-DETAILS AFTER 1.                   
           ADD   1              TO WS-CNT-LINE-ON-PAGE2.                        
           ADD   1              TO WS-CNT-PRINT-L379-B.                         
           ADD   1              TO WS-CNT-WRITE-L379-B.                         
           MOVE  SPACES         TO L379-PR-DETAILS.                             
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4310-WRITE-BLANK.                                                        
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-LINE-ON-PAGE2 > 54                                         
              PERFORM 4350-PRINT-HEADING-B THRU 4350-EXIT                       
              MOVE 1  TO WS-CNT-LINE-ON-PAGE2                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES         TO P13L379B-REC.                                 
           INITIALIZE             P13L379B-REC.                                 
                                                                                
           WRITE P13L379B-REC   FROM L379-PR-BLANK AFTER 1.                     
           ADD   1              TO WS-CNT-LINE-ON-PAGE2.                        
           MOVE  SPACES         TO L379-PR-DETAILS.                             
                                                                                
       4310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4350-PRINT-HEADING-B.                                                    
      *----------------------------------------------------------------*        
                                                                                
           ADD  1                TO     WS-NO-OF-PAGE2.                         
           MOVE WS-NO-OF-PAGE1   TO     L379-PRINT-PAGE-2.                      
           MOVE WS-EDITED-DATE   TO     L379-PRINT-DATE-2.                      
                                                                                
           MOVE SPACES           TO     P13L379B-REC.                           
           INITIALIZE                   P13L379B-REC.                           
                                                                                
           WRITE P13L379B-REC    AFTER  PAGE.                                   
           WRITE P13L379B-REC    FROM   L379-PR-HEAD-01-B  AFTER 1.             
           WRITE P13L379B-REC    FROM   L379-PR-HEAD-02-B  AFTER 1.             
           WRITE P13L379B-REC    FROM   L379-PR-HEAD-03  AFTER 1.               
           WRITE P13L379B-REC    FROM   L379-PR-HEAD-03A AFTER 1.               
           WRITE P13L379B-REC    FROM   L379-PR-LINE     AFTER 1.               
                                                                                
           MOVE 5  TO WS-CNT-LINE-ON-PAGE2.                                     
                                                                                
       4350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5200-PRINT-FOOTER.                                                       
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-WRITE-L379-A < 1                                           
              GO TO 5200-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-CLOS-TOTAL-A           TO L379-GRAND-TOTAL-A.                
           MOVE WS-GRAND-TOTAL-A-OP       TO L379-GRAND-TOTAL-OP-A.             
           MOVE WS-GRAND-TOTAL-A-SYS      TO L379-GRAND-TOTAL-SYS-A.            
           MOVE WS-GRAND-TOTAL-A-MAN      TO L379-GRAND-TOTAL-MAN-A.            
           MOVE WS-GRAND-TOTAL-A-PAY-SYS  TO L379-GRAND-TOTAL-PAY-A-SYS.        
           MOVE WS-GRAND-TOTAL-A-PAY-MAN  TO L379-GRAND-TOTAL-PAY-A-MAN.        
                                                                                
           MOVE SPACES                TO P13L379A-REC.                          
           INITIALIZE                    P13L379A-REC.                          
                                                                                
           WRITE P13L379A-REC         FROM L379-PR-FOOTER-A AFTER 2.            
           WRITE P13L379A-REC         FROM L379-PR-LINE    AFTER 1.             
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5300-PRINT-FOOTER.                                                       
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-WRITE-L379-B < 1                                           
              GO TO 5300-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-CLOS-TOTAL-B           TO L379-GRAND-TOTAL-B.                
           MOVE WS-GRAND-TOTAL-B-OP       TO L379-GRAND-TOTAL-OP-B.             
           MOVE WS-GRAND-TOTAL-B-SYS      TO L379-GRAND-TOTAL-SYS-B.            
           MOVE WS-GRAND-TOTAL-B-MAN      TO L379-GRAND-TOTAL-MAN-B.            
           MOVE WS-GRAND-TOTAL-B-PAY-SYS  TO L379-GRAND-TOTAL-PAY-B-SYS.        
           MOVE WS-GRAND-TOTAL-B-PAY-MAN  TO L379-GRAND-TOTAL-PAY-B-MAN.        
                                                                                
           MOVE SPACES                TO P13L379B-REC.                          
           INITIALIZE                    P13L379B-REC.                          
                                                                                
           WRITE P13L379B-REC         FROM L379-PR-FOOTER-B AFTER 2.            
           WRITE P13L379B-REC         FROM L379-PR-LINE    AFTER 1.             
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           CLOSE BP13F121                                                       
                 BP13K673                                                       
                 BP13F671                                                       
                 SY02F001                                                       
                 BP13F673                                                       
                 P13L379A                                                       
                 P13L379B.                                                      
                                                                                
           EVALUATE WS-K673-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K673-STATUS TO RETURN-CODE                             
                 DISPLAY 'BP13K673 CLOSE ERROR, STATUS='                        
                                                WS-K673-STATUS                  
           END-EVALUATE.                                                        
                                                                                
           DISPLAY '******  BP13C379 *************'.                            
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACE.                                                       
           DISPLAY 'NO OF READ   (BP13F121) : ' WS-CNT-F121-READ.               
           DISPLAY 'NO OF READ   (BP13F671) : ' WS-CNT-F671-READ.               
           DISPLAY 'NO OF WRITE  (BP13F673) : ' WS-CNT-F673-WRITE.              
           DISPLAY 'NO OF UPDATE (BP13K673) : ' WS-CNT-UPD-K673.                
           DISPLAY 'NO OF WRITE  (BP13K673) : ' WS-CNT-WRITE-K673.              
           DISPLAY SPACE.                                                       
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
      *                         END OF PROGRAM                                  
      *--------------------------------------------------------------*          
