       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY52.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  11/06/2009.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVE   :                                       *                
      *                                                        *                
      *    INPUT FILE  : BP13F557                              *                
      *                  BP13F716                              *                
      *                                                        *                
      *   OUTPUT FILE  : P13F557A                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * -------- ---- -------- ------------------------------- *                
      * BP133653 JB8  11/06/09 NEW PROGRAM                     *                
      * BP135615 IMC1 23/12/14 EXPAND F557 TO X(400)           *                
      *========================================================*                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716  ASSIGN       TO BP13F716.                           
           SELECT BP13F557  ASSIGN       TO BP13F557.                           
           SELECT P13F557A  ASSIGN       TO P13F557A.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BP13F716.                                                           
                                                                                
       FD   BP13F557                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BP13F557.                                                           
                                                                                
       FD   P13F557A                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   P13F557A-REC            PIC X(400).                                 
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-YY             PIC X(04) .                                 
           05 WS-SYS-MM             PIC X(02) .                                 
           05 WS-SYS-DD             PIC X(02) .                                 
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DAY           PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-MONTH         PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-YEAR          PIC X(04) .                                 
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-F557-READ               PIC 9(8)  VALUE ZEROES.                
           05 WS-F557-MATCH              PIC 9(8)  VALUE ZEROES.                
           05 WS-F557-NOMATCH            PIC 9(8)  VALUE ZEROES.                
           05 WS-F716-NOMATCH            PIC 9(8)  VALUE ZEROES.                
           05 WS-F557A-WRITE             PIC 9(8)  VALUE ZEROES.                
           05 WS-F716-READ               PIC 9(8)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05 EOF-F716-IND               PIC X(03) VALUE SPACES.                
              88 EOF-F716                          VALUE 'YES'.                 
           05 EOF-F557-IND               PIC X(03) VALUE SPACES.                
              88 EOF-F557                          VALUE 'YES'.                 
           05 WS-NUM-DONE                PIC X(01) VALUE SPACE.                 
              88 WS-DONE                           VALUE 'Y'.                   
                                                                                
       01  WS-F716-REC.                                                         
           05  FILLER                    PIC X(50).                             
           05  WS-F716-HDB-REF           PIC X(11).                             
           05  FILLER                    PIC X(13).                             
           05  WS-F716-ROLE              PIC X(03).                             
           05  FILLER                    PIC X(29).                             
           05  WS-F716-ACCEPTED          PIC X(08).                             
           05  FILLER                    PIC X(236).                            
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *--------*                                                                
       000-MAIN.                                                                
      *--------*                                                                
                                                                                
           PERFORM 100-OPEN-FILES      THRU 100-EXIT.                           
           PERFORM 200-READ-BP13F557   THRU 200-EXIT.                           
           PERFORM 300-READ-BP13F716   THRU 300-EXIT.                           
           PERFORM 400-PROCESS-INDATA  THRU 400-EXIT                            
             UNTIL F557-NUM-REGN = HIGH-VALUES                                  
               AND F716-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 900-CLOSE-FILES     THRU 900-EXIT.                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------                                                       
       100-OPEN-FILES.                                                          
      *------------------                                                       
                                                                                
           OPEN INPUT  BP13F716                                                 
                       BP13F557                                                 
                OUTPUT P13F557A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)   TO  WS-SYS-DATE.                   
           MOVE   WS-SYS-DD       TO WS-EDIT-DAY                                
           MOVE   WS-SYS-MM       TO WS-EDIT-MONTH                              
           MOVE   WS-SYS-YY       TO WS-EDIT-YEAR.                              
                                                                                
                                                                                
           MOVE 'NO '     TO EOF-F716-IND.                                      
           MOVE 'NO '     TO EOF-F557-IND.                                      
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-READ-BP13F557.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F557  AT END                                                
                MOVE 'YES'       TO EOF-F557-IND                                
                MOVE HIGH-VALUES TO F557-NUM-REGN                               
                GO TO 200-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD  1  TO WS-F557-READ.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       300-READ-BP13F716.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F716  AT END                                                
                MOVE 'YES'       TO EOF-F716-IND                                
                MOVE HIGH-VALUES TO F716-NUM-REGN                               
                GO TO 300-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD  1  TO WS-F716-READ.                                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------                                                       
       400-PROCESS-INDATA.                                                      
      *------------------*                                                      
                                                                                
                                                                                
           IF F557-NUM-REGN = F716-NUM-REGN                                     
              ADD 1                              TO WS-F557-MATCH               
              PERFORM 490-WRITE-P13F557A       THRU 490-EXIT                    
              PERFORM 200-READ-BP13F557        THRU 200-EXIT                    
           ELSE                                                                 
              IF F557-NUM-REGN < F716-NUM-REGN                                  
                 ADD  1                          TO WS-F557-NOMATCH             
                 WRITE P13F557A-REC FROM BP13F557-REC                           
                 ADD 1                           TO WS-F557A-WRITE              
                 PERFORM 200-READ-BP13F557     THRU 200-EXIT                    
              ELSE                                                              
                 ADD  1                          TO WS-F716-NOMATCH             
                 PERFORM 300-READ-BP13F716     THRU 300-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       490-WRITE-P13F557A.                                                      
                                                                                
           MOVE 'N'                        TO WS-NUM-DONE                       
                                                                                
           MOVE BP13F716-REC               TO WS-F716-REC                       
                                                                                
           PERFORM UNTIL WS-DONE                                                
              PERFORM 300-READ-BP13F716  THRU 300-EXIT                          
              IF F716-NUM-REGN NOT = F557-NUM-REGN OR                           
                 F716-NUM-REGN = HIGH-VALUES                                    
                 MOVE 'Y'                  TO WS-NUM-DONE                       
                 EVALUATE WS-F716-ROLE                                          
                     WHEN '003'                                                 
                          MOVE 'RT'        TO F557-NUM-ROLE-SMS-HA1             
                     WHEN '023'                                                 
                          MOVE 'L '        TO F557-NUM-ROLE-SMS-HA1             
                     WHEN '024'                                                 
                          MOVE 'CO'        TO F557-NUM-ROLE-SMS-HA1             
                     WHEN '004'                                                 
                     WHEN '025'                                                 
                          MOVE 'OC'        TO F557-NUM-ROLE-SMS-HA1             
                     WHEN OTHER                                                 
                          MOVE 'OT'        TO F557-NUM-ROLE-SMS-HA1             
                 END-EVALUATE                                                   
                 MOVE WS-F716-ACCEPTED     TO F557-DTE-SALE-EFF-SMS-HA1         
                 MOVE WS-F716-HDB-REF      TO F557-NUM-HDBREF-SMS-HA1           
                                                                                
                 WRITE P13F557A-REC FROM BP13F557-REC                           
                 ADD 1                     TO WS-F557A-WRITE                    
              ELSE                                                              
                 IF F716-NUM-ROLE = '023' OR '024'                              
                    MOVE BP13F716-REC      TO WS-F716-REC                       
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       490-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------                                                         
       900-CLOSE-FILES.                                                         
      *----------------                                                         
                                                                                
           CLOSE BP13F557                                                       
                 P13F557A                                                       
                 BP13F716.                                                      
                                                                                
           DISPLAY 'PROGRAM-ID : BP13CY51                 '.                    
           DISPLAY '                                      '.                    
           DISPLAY 'DATE : ' WS-EDIT-DATE.                                      
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF RECORDS READ FROM F557   = ' WS-F557-READ.           
           DISPLAY 'NO. OF RECORDS READ FROM F716   = ' WS-F716-READ.           
           DISPLAY 'NO. OF RECORDS OF F557 MATCHD   = ' WS-F557-MATCH.          
           DISPLAY 'NO. OF RECORDS OF F557 NOT MATCHED ='                       
                   WS-F557-NOMATCH.                                             
           DISPLAY 'NO. OF RECORDS OF F716 NOT MATCHED ='                       
                   WS-F716-NOMATCH.                                             
           DISPLAY 'NO. OF RECORDS WRITTEN TO F557A = ' WS-F557A-WRITE.         
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
