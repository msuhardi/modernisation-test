       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C552.                                                 
       AUTHOR.        JAI.                                                      
       DATE-WRITTEN.  07/10/94.                                                 
       DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO PRINT REPORT ON FLAT ALLOCATED UNDER       *                
      *          FIRST TIMER/UPGRADER FOR EACH ZONE/FT/ALO-SCH *                
      *          COHORT.                                       *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *      1. BP13K712                                       *                
      *      2. BP13F713                                       *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *      1. BP13L552                                       *                
      *                                                        *                
      * CHG-NO  BY   ON        DESCRIPTION                     *                
      * ----------------------------------                     *                
      *BP130350 LSB 970908 - CHG TITLE OF THE REPORT           *                
      *BP134782 IL5 130208 - ADD COLUMNS FOR PPS               *                
      *BP134914 SMR 140613 - ADD COLUMNS FOR AST               *                
      *BP135028 SMR 050813 - ADD COLUMNS FOR FTS               *                
      *BP135028 SMR 220813 - ADD COLUMN FOR REQ-FTS            *                
      *BP135492 SMR 180814 - ADD COLUMNS FOR MCPS FT & ST      *                
      *BP136866 SMR 100817 - CHANGE REPORT HEADER FOR ROF      *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F713 ASSIGN TO BP13F713.                                  
                                                                                
           SELECT BP13K712 ASSIGN TO BP13K712                                   
                ACCESS MODE  IS SEQUENTIAL                                      
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K712-KEY-FLD                                    
                FILE STATUS  IS BP13K712-STATUS.                                
                                                                                
           SELECT BP13L552 ASSIGN TO BP13L552.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F713                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 20 CHARACTERS                                       
            RECORDING MODE IS F.                                                
       COPY BP13F713.                                                           
                                                                                
       FD   BP13K712                                                            
            BLOCK CONTAINS 0 RECORDS.                                           
       COPY BP13K712.                                                           
                                                                                
       FD   BP13L552                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED.                                            
                                                                                
       01   L552-PRINT-REC               PIC X(132).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  BP13K712-STATUS           PIC 9(02) VALUE ZEROS.                 
           05  WS-TIME.                                                         
               10  WS-TIME-HH            PIC 9(02) VALUE 0.                     
               10  WS-TIME-MM            PIC 9(02) VALUE 0.                     
               10  WS-TIME-SS            PIC 9(02) VALUE 0.                     
           05  WS-CNT-K712               PIC 9(05) VALUE 0.                     
           05  WS-CNT-PAGE               PIC 9(04) VALUE 0.                     
           05  WS-CNT-SNO                PIC 9(05) VALUE 0.                     
           05  WS-MAX-LINE               PIC 9(02) VALUE 60.                    
           05  WS-CNT-LINE               PIC 9(02) VALUE 60.                    
           05  WS-K712-EOF               PIC X(01) VALUE SPACES.                
               88  K712-EOF                        VALUE 'Y'.                   
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-YY                     PIC X(02).                             
           05  WS-MM                     PIC X(02).                             
           05  WS-DD                     PIC X(02).                             
                                                                                
       01  WS-DTE-TODATE.                                                       
           05  WS-DD                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-MM                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-YY                     PIC X(02).                             
                                                                                
      ******************* REPORT LAYOUT *****************                       
                                                                                
       01  L552-HEAD1.                                                          
           05  FILLER                    PIC X(08) VALUE 'BP13L552'.            
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'HDBCAT3'.             
           05  FILLER                    PIC X(19) VALUE SPACES.                
           05  FILLER                    PIC X(42) VALUE                        
               'S Y S T E M     O F    C O M M I T M E N T'.                    
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'DATE : '.             
           05  L552-DTE-CURRENT          PIC X(08).                             
           05  FILLER                    PIC X(02) VALUE ' ('.                  
           05  L552-TME-HH               PIC 9(02).                             
           05  FILLER                    PIC X(01) VALUE ':'.                   
           05  L552-TME-MM               PIC 9(02).                             
           05  FILLER                    PIC X(02) VALUE ') '.                  
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'PAGE : '.             
           05  L552-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  L552-HEAD2.                                                          
           05  FILLER                    PIC X(91) VALUE SPACES.                
           05  FILLER                    PIC X(23) VALUE                        
               'USER (APPLICATION TEAM)'.                                       
                                                                                
       01  L552-HEAD3.                                                          
           05  FILLER                    PIC X(30) VALUE SPACES.                
           05  FILLER                    PIC X(47) VALUE                        
               'REPORT ON FLAT ALLOTED TO FIRST TIMER/UPGRADER'.                
           05  FILLER                    PIC X(14) VALUE SPACES.                
           05  FILLER                    PIC X(10) VALUE 'PROCESS : '.          
           05  L552-PROCESS              PIC X(15) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE SPACES.                
                                                                                
       01  L552-HEAD4.                                                          
           05  FILLER                    PIC X(54) VALUE                        
               ' S/NO. NT  FT CO ALC %NHH %UPG TOTAL   REQ   REQ   REQ'.        
           05  FILLER                    PIC X(49) VALUE                        
               '         FLAT  FLAT  FLAT  FLAT  FLAT  FLAT  FLAT'.             
                                                                                
       01  L552-HEAD5.                                                          
           05  FILLER                    PIC X(54) VALUE                        
               '                 SCH            UNIT    NH    UG   FTS'.        
           05  FILLER                    PIC X(49) VALUE                        
               '           NH    UG   PPS   AST   FTS MCPNH MCPUG'.             
                                                                                
       01  L552-HEAD5-ROF.                                                      
           05  FILLER                    PIC X(54) VALUE                        
               '                 SCH            UNIT    NH    UG   OTH'.        
           05  FILLER                    PIC X(49) VALUE                        
               '           NH    UG   PPS   AST   OTH MCPNH MCPUG'.             
                                                                                
       01  L552-DETAIL1.                                                        
           05  L552-SNO                  PIC ZZZZZ9.                            
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NT                   PIC X(03).                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-FT                   PIC X(02).                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-CDE-COHORT           PIC X(02).                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-ALLOC-SCH            PIC X(03).                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-NHH              PIC Z9.9.                              
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-UPG              PIC Z9.9.                              
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-TOT-UNIT         PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-REQ-NH           PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-REQ-UG           PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-REQ-FTS          PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE ' (FLAT) '.            
           05  L552-NUM-FLAT-NH          PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-FLAT-UG          PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-FLAT-PPS         PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-FLAT-AST         PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-FLAT-FTS         PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-FLAT-MCPFT       PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-FLAT-MCPST       PIC ZZZZ9.                             
                                                                                
       01  L552-DETAIL2.                                                        
           05  FILLER                    PIC X(54) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE ' (REG ) '.            
           05  L552-NUM-REG-NH           PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-REG-UG           PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-REG-PPS          PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-REG-AST          PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-REG-FTS          PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-REG-MCPFT        PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L552-NUM-REG-MCPST        PIC ZZZZ9.                             
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************                                                        
       0000-MAIN-ROUTINE.                                                       
      ******************                                                        
                                                                                
           PERFORM 1000-OPEN-FILES                                              
              THRU 1000-EXIT.                                                   
                                                                                
           PERFORM 2000-READ-BP13K712                                           
              THRU 2000-EXIT.                                                   
                                                                                
           PERFORM 3000-MAIN-ROUTINE                                            
              THRU 3000-EXIT                                                    
             UNTIL K712-EOF.                                                    
                                                                                
           PERFORM 9000-CLOSE-ROUTINE                                           
              THRU 9000-EXIT.                                                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************                                                         
       1000-OPEN-FILES.                                                         
      *****************                                                         
                                                                                
           OPEN INPUT  BP13K712                                                 
                       BP13F713                                                 
                OUTPUT BP13L552.                                                
                                                                                
           IF BP13K712-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K712,STATUS : '                        
                        BP13K712-STATUS                                         
              MOVE BP13K712-STATUS       TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-IF.                                                              
                                                                                
           ACCEPT WS-DTE-CURRENT         FROM DATE.                             
           MOVE CORR WS-DTE-CURRENT        TO WS-DTE-TODATE.                    
           MOVE WS-DTE-TODATE              TO L552-DTE-CURRENT.                 
                                                                                
           ACCEPT  WS-TIME                 FROM TIME.                           
           MOVE WS-TIME-HH                 TO L552-TME-HH.                      
           MOVE WS-TIME-MM                 TO L552-TME-MM.                      
                                                                                
           PERFORM 1500-READ-BP13F713                                           
              THRU 1500-EXIT.                                                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       1500-READ-BP13F713.                                                      
      ********************                                                      
                                                                                
            READ BP13F713 AT END                                                
                GO TO 1500-EXIT                                                 
            END-READ                                                            
                                                                                
            EVALUATE  F713-CDE-COHORT                                           
            WHEN  '01'                                                          
               MOVE 'COHORT 1       '    TO L552-PROCESS                        
            WHEN  '02'                                                          
               MOVE 'COHORT 2       '    TO L552-PROCESS                        
            WHEN  '03'                                                          
               MOVE 'COHORT 3       '    TO L552-PROCESS                        
            WHEN  '04'                                                          
               MOVE 'COHORT 4       '    TO L552-PROCESS                        
            WHEN  '05'                                                          
               MOVE 'COHORT 5       '    TO L552-PROCESS                        
            WHEN  '06'                                                          
               MOVE 'COHORT 6       '    TO L552-PROCESS                        
            WHEN  '07'                                                          
               MOVE 'COHORT 7       '    TO L552-PROCESS                        
            WHEN  '08'                                                          
               MOVE 'COHORT 8       '    TO L552-PROCESS                        
            WHEN  'MT'                                                          
               MOVE 'MTF/JB/TCP/RH  '    TO L552-PROCESS                        
            WHEN  'CC'                                                          
               MOVE 'GRO            '    TO L552-PROCESS                        
            WHEN  'PA'                                                          
               MOVE 'MATURE NT - PA '    TO L552-PROCESS                        
            WHEN  'NH'                                                          
               MOVE 'NHH/UPG        '    TO L552-PROCESS                        
            WHEN  'TR'                                                          
               MOVE 'TRANSITIONAL   '    TO L552-PROCESS                        
            WHEN  OTHER                                                         
               MOVE '               '    TO L552-PROCESS                        
            END-EVALUATE.                                                       
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       2000-READ-BP13K712.                                                      
      ********************                                                      
                                                                                
            READ BP13K712 NEXT                                                  
                 AT END                                                         
                 MOVE 'Y'  TO WS-K712-EOF                                       
                 GO TO 2000-EXIT                                                
            END-READ.                                                           
                                                                                
            ADD   1        TO WS-CNT-K712.                                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       3000-MAIN-ROUTINE.                                                       
      *******************                                                       
                                                                                
           IF K712-CDE-NT-ZONE = SPACES OR LOW-VALUES                           
              PERFORM 2000-READ-BP13K712                                        
                 THRU 2000-EXIT                                                 
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF  WS-CNT-LINE  >=  WS-MAX-LINE                                     
                                                                                
               ADD 1                     TO WS-CNT-PAGE                         
               MOVE WS-CNT-PAGE          TO L552-CNT-PAGE                       
                                                                                
               WRITE L552-PRINT-REC      FROM L552-HEAD1 AFTER PAGE             
               WRITE L552-PRINT-REC      FROM L552-HEAD2                        
               WRITE L552-PRINT-REC      FROM L552-HEAD3 AFTER 2                
               WRITE L552-PRINT-REC      FROM L552-HEAD4 AFTER 2                
               IF K712-CDE-NT-ZONE = 'ROF'                                      
                  WRITE L552-PRINT-REC   FROM L552-HEAD5-ROF                    
               ELSE                                                             
                  WRITE L552-PRINT-REC   FROM L552-HEAD5                        
               END-IF                                                           
                                                                                
               MOVE  9                   TO WS-CNT-LINE                         
                                                                                
           END-IF.                                                              
                                                                                
           IF K712-CDE-NT-ZONE > SPACES                                         
                                                                                
              ADD 1                         TO WS-CNT-SNO                       
                                                                                
              MOVE WS-CNT-SNO               TO L552-SNO                         
              MOVE K712-CDE-NT-ZONE         TO L552-NT                          
              MOVE K712-CDE-FLAT-TY         TO L552-FT                          
              MOVE K712-CDE-COHORT          TO L552-CDE-COHORT                  
              MOVE K712-CDE-ALLOC-SCH       TO L552-ALLOC-SCH                   
              MOVE K712-NUM-NHH             TO L552-NUM-NHH                     
              MOVE K712-NUM-UPG             TO L552-NUM-UPG                     
              MOVE K712-NUM-TOT-UNIT        TO L552-NUM-TOT-UNIT                
              MOVE K712-NUM-REQ-NH          TO L552-NUM-REQ-NH                  
              MOVE K712-NUM-REQ-UG          TO L552-NUM-REQ-UG                  
              MOVE K712-NUM-REQ-FTS         TO L552-NUM-REQ-FTS                 
              MOVE K712-NUM-FLAT-NH         TO L552-NUM-FLAT-NH                 
              MOVE K712-NUM-FLAT-UG         TO L552-NUM-FLAT-UG                 
              MOVE K712-NUM-FLAT-PPS        TO L552-NUM-FLAT-PPS                
              MOVE K712-NUM-FLAT-AST        TO L552-NUM-FLAT-AST                
              MOVE K712-NUM-FLAT-FTS        TO L552-NUM-FLAT-FTS                
              MOVE K712-NUM-FLAT-MCPS-FT    TO L552-NUM-FLAT-MCPFT              
              MOVE K712-NUM-FLAT-MCPS-ST    TO L552-NUM-FLAT-MCPST              
              MOVE K712-NUM-REG-NH          TO L552-NUM-REG-NH                  
              MOVE K712-NUM-REG-UG          TO L552-NUM-REG-UG                  
              MOVE K712-NUM-REG-PPS         TO L552-NUM-REG-PPS                 
              MOVE K712-NUM-REG-AST         TO L552-NUM-REG-AST                 
              MOVE K712-NUM-REG-FTS         TO L552-NUM-REG-FTS                 
              MOVE K712-NUM-REG-MCPS-FT     TO L552-NUM-REG-MCPFT               
              MOVE K712-NUM-REG-MCPS-ST     TO L552-NUM-REG-MCPST               
                                                                                
              WRITE L552-PRINT-REC  FROM L552-DETAIL1 AFTER 2                   
              WRITE L552-PRINT-REC  FROM L552-DETAIL2                           
                                                                                
              ADD  3                        TO WS-CNT-LINE                      
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13K712                                           
              THRU 2000-EXIT.                                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-ROUTINE.                                                      
      *******************                                                       
                                                                                
           CLOSE BP13K712                                                       
                 BP13F713                                                       
                 BP13L552.                                                      
                                                                                
           DISPLAY '*************** BP13C552 ***************'.                  
           DISPLAY ' '.                                                         
           DISPLAY 'NO. OF RECORDS READ FROM BP13K712 : ' WS-CNT-K712.          
           DISPLAY 'NO. OF RECORDS WRITTEN TO REPORT  : ' WS-CNT-SNO.           
           DISPLAY ' '.                                                         
                                                                                
           IF BP13K712-STATUS NOT = 0                                           
              DISPLAY '***  CLOSING ERROR, BP13K712-STATUS '                    
              BP13K712-STATUS                                                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
