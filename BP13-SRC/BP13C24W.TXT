      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C24W.                                                 
      *AUTHOR.        ELINE S ARGA.                                             
      *DATE-WRITTEN.  08 AUGUST 2014.                                           
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   :     HOUSEKEEPING OF BP13KH20 APPLICATION    *            
      *                    HISTORY FILE.                           *            
      *                                                            *            
      *  INPUT FILE  :     BP13F205  -  CONTROL FILE               *            
      *  I-O FILE    :     BP13KH20  -  APPLN HIST FILE            *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE    DESCRIPTION                         *            
      * -------- ---  ------   -----------                         *            
      * BP135415 ESA1 20140808 NEW PROGRAM                         *            
      * BP139280 EAA2 06112023 EXPAND BP13K757 FROM 3000 TO 4000   *            
      * ========================================================== *            
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN       TO BP13F205.                           
                                                                                
           SELECT BP13K757  ASSIGN       TO BP13K757                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K757-KEY-FLD                        
                            FILE STATUS  IS BP13K757-STATUS.                    
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F205                                                            
            RECORDING MODE  IS F                                                
            RECORD CONTAINS 80 CHARACTERS.                                      
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K757                                                            
            RECORD CONTAINS 4000 CHARACTERS.                                    
       COPY BP13K757.                                                           
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-COUNTERS.                                                         
           05  WS-F205-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-K757-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-K757-DEL-CTR              PIC 9(06)  VALUE ZEROS.             
           05  WS-K757-DEL-CTR-ERR          PIC 9(06)  VALUE ZEROS.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F205-EOF                  PIC X(01)  VALUE 'N'.               
           05  WS-K757-EOF                  PIC X(01)  VALUE 'N'.               
           05  BP13K757-STATUS              PIC 99     VALUE 00.                
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE.                                                         
               10  WS-CCYY                  PIC 9(4)   VALUE 0000.              
               10  WS-MM                    PIC 99     VALUE 00.                
               10  WS-DD                    PIC 99     VALUE 00.                
           05  WS-CURR-DATE                 PIC 9(08)  VALUE ZEROES.            
           05  F757-RUN-DATE                PIC X(10)  VALUE ZEROES.            
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAIN-ROUTINE.                                                       
      ******************************************************************        
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 2000-READ-BP13F205          THRU 2000-EXIT.                  
           PERFORM 3000-PROCESS-RECORDS        THRU 3000-EXIT                   
                       UNTIL WS-F205-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT   BP13F205                                                
                I-O     BP13K757.                                               
                                                                                
           IF BP13K757-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K757-STATUS ' BP13K757-STATUS         
              MOVE     BP13K757-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE    THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE WS-CURR-DATE.             
           STRING WS-DD '/' WS-MM '/' WS-CCYY DELIMITED BY SIZE                 
                  INTO F757-RUN-DATE.                                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205 AT END                                                 
                MOVE 'Y'         TO WS-F205-EOF                                 
                                 GO TO 2000-EXIT.                               
           ADD 1                 TO WS-F205-READ-CTR.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 4000-START-BP13K757    THRU 4000-EXIT.                       
                                                                                
           PERFORM 2000-READ-BP13F205     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-START-BP13K757.                                                     
      ******************************************************************        
           MOVE SPACES                     TO BP13K757-REC.                     
           START BP13K757 KEY >= K757-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K757-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                 PERFORM 5000-READ-BP13K757     THRU 5000-EXIT                  
                        UNTIL WS-K757-EOF = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                 CONTINUE                                                       
               WHEN OTHER                                                       
                 DISPLAY 'START ERROR, BP13K757-STATUS '                        
                                       BP13K757-STATUS                          
                 MOVE     BP13K757-STATUS  TO RETURN-CODE                       
                 PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-READ-BP13K757.                                                      
      ******************************************************************        
           READ BP13K757 NEXT                                                   
                AT END MOVE 'Y' TO WS-K757-EOF                                  
                       GO TO 5000-EXIT.                                         
                                                                                
           EVALUATE BP13K757-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                 IF F205-NUM-SELECTION = 'BTO'                                  
                    IF K757-NUM-ESTATE-ZONE = F205-NUM-NT-ZONE     AND          
                       K757-DTE-BALLOT      = F205-DTE-BALLOT      AND          
                       K757-NUM-SALES-MODE  = F205-NUM-SELECTION                
                       ADD  1  TO  WS-K757-READ-CTR                             
                       PERFORM 6000-DELETE-BP13K757  THRU 6000-EXIT             
                    END-IF                                                      
                 ELSE                                                           
                    IF K757-NUM-ESTATE-ZONE = F205-NUM-NT-ZONE     AND          
                       K757-NUM-FLAT-TYPE   = F205-NUM-FLAT-TYPE   AND          
                       K757-DTE-BALLOT      = F205-DTE-BALLOT      AND          
                       K757-NUM-SALES-MODE  = F205-NUM-SELECTION                
                       ADD  1  TO  WS-K757-READ-CTR                             
                       PERFORM 6000-DELETE-BP13K757  THRU 6000-EXIT             
                    END-IF                                                      
                 END-IF                                                         
               WHEN 10                                                          
               WHEN 23                                                          
                 MOVE 'Y'           TO WS-K757-EOF                              
               WHEN OTHER                                                       
                 DISPLAY 'READNXT ERROR, BP13K757-STATUS '                      
                                         BP13K757-STATUS                        
                 MOVE     BP13K757-STATUS  TO RETURN-CODE                       
                 PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       6000-DELETE-BP13K757.                                                    
      ******************************************************************        
           DELETE BP13K757.                                                     
                                                                                
           EVALUATE BP13K757-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO WS-K757-DEL-CTR                                    
               WHEN OTHER                                                       
                    ADD 1 TO WS-K757-DEL-CTR-ERR                                
                    DISPLAY 'ERROR DELETING BP13K757-REC ...'                   
                    MOVE BP13K757-STATUS TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
            DISPLAY ' '.                                                        
            DISPLAY ' ====  BP13C24W CONTROL TOTALS  ===='.                     
            DISPLAY ' '.                                                        
            DISPLAY ' F205 RECORDS READ        : ' WS-F205-READ-CTR.            
            DISPLAY ' K757 RECORDS DELETED     : ' WS-K757-DEL-CTR.             
            DISPLAY ' K757 ERROR DELETING      : ' WS-K757-DEL-CTR-ERR.         
            DISPLAY ' '.                                                        
            DISPLAY ' ==================================='.                     
                                                                                
            CLOSE BP13F205                                                      
                  BP13K757.                                                     
                                                                                
            IF BP13K757-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR-BP13K757-STAT ' BP13K757-STATUS           
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
