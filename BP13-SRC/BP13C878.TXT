       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C878.                                                 
      *AUTHOR.        EVM.                                                      
      *DATE-WRITTEN.  03/04/96.                                                 
      *DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *    1.   THIS IS A PROGRAM TO VERIFY ZONE/HOUSEHOLD/    *                
      *         5YR-OCCUPN AND PRINT.                          *                
      *                                                        *                
      *    INPUT FILES :                                       *                
      *    1.   BP13F800 (SOC  MASTER)                         *                
      *                                                        *                
      *    OUTPUT LISTING :                                    *                
      *    1.   BP13L878 - LIST OF VERIFIED ZONE/HOUSEHOLD/    *                
      *                                     5YR-OCCUPN.        *                
      *                                                        *                
      * CHG-NO.  BY  ON       DESCRIPTION                      *                
      * -------- --- -------- -----------                      *                
      * BP130418 SIA 09071998 CATER FOR YR 2000                *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800   ASSIGN TO BP13F800.                                
                                                                                
           SELECT BP13L878   ASSIGN TO BP13L878.                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F800.                                                           
                                                                                
       FD  BP13L878                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  L878-REC                    PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F800-READ                PIC 9(8)    VALUE ZEROES.                
       01  WS-F800-BYPASS              PIC 9(8)    VALUE ZEROES.                
       01  WS-F800-PROCESS             PIC 9(8)    VALUE ZEROES.                
       01  WS-LINE-CTR                 PIC 9(2)    VALUE 60.                    
       01  WS-REC-CTR                  PIC 9(7)    VALUE 0.                     
       01  WS-PAGE-CTR                 PIC 9(4)    VALUE ZEROES.                
       01  WS-F800-EOF                 PIC X(1).                                
           88  F800-EOF                            VALUE 'Y'.                   
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CCYY            PIC 9(4).                                
           05  WS-CURR-MM              PIC 99.                                  
           05  WS-CURR-DD              PIC 99.                                  
                                                                                
      **********************************************************                
      *                   BP13L878 REPORT LAYOUT               *                
      **********************************************************                
                                                                                
       01  L878-HDG1.                                                           
           05  FILLER              PIC X(8)    VALUE 'BP13L878'.                
           05  FILLER              PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(8)    VALUE 'HDB3    '.                
           05  FILLER              PIC X(19)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(16)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE: '.                  
           05  L878-PRT-DATE.                                                   
               10  L878-PRT-DD     PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  L878-PRT-MM     PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  L878-PRT-CCYY   PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'PAGE: '.                  
           05  L878-PAGE           PIC ZZZZ9.                                   
                                                                                
       01  L878-HDG2.                                                           
           05  FILLER              PIC X(44)   VALUE SPACES.                    
           05  FILLER              PIC X(44)   VALUE                            
              'SOC - VERIFY  ZONE / HOUSEHOLD / 5YR-OCCPUPN'.                   
                                                                                
       01  L878-HDG3.                                                           
           05  FILLER              PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(50)   VALUE                            
              'S/N     NUM-REGN     CUR-REGN     ZONE     FT     '.             
           05  FILLER              PIC X(50)   VALUE                            
              'DTE-BALLOT     HHTY        REMARKS                '.             
                                                                                
       01  L878-DETAIL.                                                         
           05  FILLER              PIC X(04)       VALUE SPACES.                
           05  L878-NUM-REC        PIC Z,ZZZ,ZZ9.                               
           05  FILLER              PIC X(05)       VALUE SPACES.                
           05  L878-NUM-REGN       PIC X(08)       VALUE SPACES.                
           05  FILLER              PIC X(05)       VALUE SPACES.                
           05  L878-CUR-REGN       PIC X(08)       VALUE SPACES.                
           05  FILLER              PIC X(06)       VALUE SPACES.                
           05  L878-ZONE           PIC X(02)       VALUE SPACES.                
           05  FILLER              PIC X(06)       VALUE SPACES.                
           05  L878-FT             PIC X(02)       VALUE SPACES.                
           05  FILLER              PIC X(08)       VALUE SPACES.                
           05  L878-DTE-BALLOT     PIC X(06)       VALUE SPACES.                
           05  FILLER              PIC X(07)       VALUE SPACES.                
           05  L878-HHTY           PIC X(01)       VALUE SPACES.                
           05  FILLER              PIC X(06)       VALUE SPACES.                
           05  L878-REMARKS        PIC X(15)       VALUE SPACES.                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      ******************************************************************        
       MAIN-ROUTINE SECTION.                                                    
      ******************************************************************        
                                                                                
           PERFORM 10-OPEN-FILES    THRU 10-EXIT.                               
           PERFORM 20-READ-BP13F800 THRU 20-EXIT                                
           PERFORM 30-PROCESS-F800  THRU 30-EXIT                                
                                    UNTIL F800-EOF.                             
           PERFORM 50-CLOSE-ROUTINE THRU 50-EXIT.                               
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       10-OPEN-FILES.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F800                                                 
                OUTPUT BP13L878.                                                
                                                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
           MOVE WS-CURR-DD   TO L878-PRT-DD.                                    
           MOVE WS-CURR-MM   TO L878-PRT-MM.                                    
           MOVE WS-CURR-CCYY TO L878-PRT-CCYY.                                  
           MOVE SPACES       TO WS-F800-EOF.                                    
                                                                                
       10-EXIT.                                                                 
           EXIT.                                                                
      ******************************************************************        
       20-READ-BP13F800.                                                        
      ******************************************************************        
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y'        TO WS-F800-EOF                                  
                GO TO 20-EXIT.                                                  
                                                                                
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       20-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       30-PROCESS-F800.                                                         
      ******************************************************************        
           IF (F800-NUM-STATUS NOT = 'C') AND                                   
              (F800-NUM-NEW-TOWN = SPACES OR LOW-VALUES) AND                    
              (F800-DTE-BALLOT = SPACES OR LOW-VALUES)                          
                                                                                
              MOVE SPACES        TO L878-REMARKS                                
                                                                                
              IF F800-NUM-ZONE = SPACES OR LOW-VALUES                           
                 MOVE '    NO ZONE    '        TO L878-REMARKS                  
                 PERFORM 40-PROCESS-PRINT      THRU 40-EXIT                     
              END-IF                                                            
              IF F800-NUM-HOUSEHOLD = SPACES OR LOW-VALUES                      
                 MOVE '    NO HHTY    '        TO L878-REMARKS                  
                 PERFORM 40-PROCESS-PRINT      THRU 40-EXIT                     
              END-IF                                                            
              IF F800-NUM-5YR-OCCUPN = 'N'                                      
                 MOVE '5YR-CHECK = "N"'        TO L878-REMARKS                  
                 PERFORM 40-PROCESS-PRINT      THRU 40-EXIT                     
              END-IF                                                            
                                                                                
              IF L878-REMARKS NOT = SPACES                                      
                 ADD 1        TO WS-F800-PROCESS                                
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 20-READ-BP13F800 THRU 20-EXIT.                               
                                                                                
       30-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       40-PROCESS-PRINT.                                                        
      ******************************************************************        
           IF WS-LINE-CTR > 55                                                  
              ADD 1 TO WS-PAGE-CTR                                              
              MOVE WS-PAGE-CTR         TO L878-PAGE                             
              WRITE L878-REC           FROM L878-HDG1 AFTER PAGE                
              WRITE L878-REC           FROM L878-HDG2 AFTER 1                   
              WRITE L878-REC           FROM L878-HDG3 AFTER 2                   
              MOVE SPACES              TO L878-REC                              
              WRITE L878-REC           AFTER 1                                  
              MOVE 5                   TO WS-LINE-CTR.                          
                                                                                
           ADD 1                       TO WS-REC-CTR.                           
           MOVE WS-REC-CTR             TO L878-NUM-REC.                         
           MOVE F800-NUM-REGN          TO L878-NUM-REGN.                        
           MOVE F800-NUM-CUR-REGN      TO L878-CUR-REGN.                        
           MOVE F800-NUM-ZONE          TO L878-ZONE.                            
           MOVE F800-NUM-FLAT-TYPE     TO L878-FT.                              
           MOVE F800-DTE-BALLOT        TO L878-DTE-BALLOT.                      
           MOVE F800-NUM-HOUSEHOLD     TO L878-HHTY.                            
                                                                                
           WRITE L878-REC              FROM L878-DETAIL AFTER 1.                
           ADD 1                       TO WS-LINE-CTR.                          
                                                                                
       40-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       50-CLOSE-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           CLOSE BP13F800                                                       
                 BP13L878.                                                      
                                                                                
           DISPLAY 'PROGRAM : BP13C878    '.                                    
           DISPLAY 'DATE    : ' L878-PRT-DATE.                                  
           DISPLAY '*=========== COUNTERS ===========*'                         
           DISPLAY 'F800-REC-READ    : ' WS-F800-READ.                          
           DISPLAY 'F800-REC-PROCESS : ' WS-F800-PROCESS.                       
                                                                                
           STOP RUN.                                                            
                                                                                
       50-EXIT.                                                                 
           EXIT.                                                                
