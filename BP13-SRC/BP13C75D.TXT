       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C75D.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  15/08/2016.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE  :  GENERATE LIST OF ALL I-AMEND CASES             *         
      *                                                               *         
      *  INPUT      :  BP13F757 - FULL FILE                           *         
      *  OUTPUT     :  BP13L75D - REPORT GENERATED                    *         
      *                                                               *         
      *===============================================================*         
      * CHG-NO   DATE     BY   DESCRIPTION                            *         
      * -------- -------- ---- -------------------------------------- *         
      * BP136358 15082016 SMR2 NEW PROGRAM                            *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F757 ASSIGN       TO BP13F757.                            
                                                                                
           SELECT BP13L75D ASSIGN       TO BP13L75D.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F757                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS 3000 CHARACTERS.                                    
       COPY BP13F757.                                                           
                                                                                
       FD   BP13L75D                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   BP13L75D-REC               PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F757-EOF             PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-FILE-COUNTERS.                                                    
           05  WS-F757-READ            PIC 9(06) VALUE ZEROES.                  
           05  WS-L75D-WRITE           PIC 9(06) VALUE ZEROES.                  
           05  WS-L75D-SNO             PIC 9(05) VALUE ZEROES.                  
           05  WS-L75D-PAGE            PIC 9(05) VALUE ZEROES.                  
           05  WS-L75D-LINE            PIC 9(02) VALUE 60.                      
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-EDT-DATE             PIC X(10) VALUE SPACES.                  
           05  WS-CUR-DATE             PIC X(08) VALUE SPACES.                  
           05  WS-EDIT1                PIC Z(05)9.                              
                                                                                
      **************************************************************            
      *            BP13L75D - R E P O R T   L A Y O U T            *            
      **************************************************************            
       01  L75D-HEADING-01.                                                     
           05  FILLER              PIC X(08)     VALUE 'BP13L75D'.              
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDB3'.                  
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L75D-RUN-DATE       PIC X(10).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L75D-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  L75D-HEADING-02.                                                     
           05  FILLER              PIC X(53)     VALUE SPACES.                  
           05  FILLER              PIC X(25)     VALUE                          
               'LIST OF ALL I-AMEND CASES'.                                     
                                                                                
       01  L75D-HEADING-03.                                                     
           05  FILLER              PIC X(46)     VALUE                          
               'S/N     REGNO      SALES MODE   BALLOT DATE   '.                
           05  FILLER              PIC X(08)     VALUE 'NT    FT'.              
                                                                                
       01  L75D-HEADING-04.                                                     
           05  FILLER              PIC X(132)    VALUE ALL '-'.                 
                                                                                
       01  L75D-HEADING-05.                                                     
           05  FILLER              PIC X(132)    VALUE SPACES.                  
                                                                                
       01  L75D-DETAIL-01.                                                      
           05  L75D-S-NO           PIC ZZZZ9     VALUE ZEROES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L75D-REGN           PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L75D-SMODE          PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  L75D-DTEBAL         PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  L75D-NT             PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L75D-FT             PIC X(02)     VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F757      THRU 2000-EXIT                       
             UNTIL WS-F757-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F757                                                 
                OUTPUT BP13L75D.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2) '/' WS-CUR-DATE(5:2) '/'                     
                  WS-CUR-DATE(1:4)                                              
           DELIMITED BY SIZE INTO WS-EDT-DATE.                                  
           MOVE WS-EDT-DATE                TO L75D-RUN-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F757.                                                      
      *---------------------------------------------------------------*         
           READ BP13F757                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F757-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F757-READ.                     
                                                                                
           PERFORM 4000-WRITE-BP13L75D     THRU 4000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-WRITE-BP13L75D.                                                     
      *---------------------------------------------------------------*         
           IF WS-L75D-LINE > 55                                                 
              PERFORM 4100-WRITE-HEAD-L75D THRU 4100-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 4200-WRITE-DET-L75D     THRU 4200-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-WRITE-HEAD-L75D.                                                    
      *---------------------------------------------------------------*         
           ADD 1                       TO WS-L75D-PAGE.                         
           MOVE WS-L75D-PAGE           TO L75D-PAGE-NO.                         
                                                                                
           WRITE BP13L75D-REC          FROM L75D-HEADING-01 AFTER PAGE.         
           WRITE BP13L75D-REC          FROM L75D-HEADING-02.                    
           WRITE BP13L75D-REC          FROM L75D-HEADING-05.                    
           WRITE BP13L75D-REC          FROM L75D-HEADING-05.                    
           WRITE BP13L75D-REC          FROM L75D-HEADING-03.                    
           WRITE BP13L75D-REC          FROM L75D-HEADING-04.                    
                                                                                
           MOVE 6                      TO WS-L75D-LINE.                         
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-WRITE-DET-L75D.                                                     
      *---------------------------------------------------------------*         
           MOVE SPACES                      TO L75D-DETAIL-01.                  
                                                                                
           ADD 1                            TO WS-L75D-SNO                      
                                               WS-L75D-WRITE                    
                                               WS-L75D-LINE.                    
                                                                                
           MOVE WS-L75D-SNO                 TO L75D-S-NO.                       
           MOVE F757-NUM-REGN               TO L75D-REGN.                       
           MOVE F757-NUM-SALES-MODE         TO L75D-SMODE.                      
           MOVE F757-DTE-BALLOT             TO L75D-DTEBAL.                     
           MOVE F757-NUM-ESTATE-ZONE        TO L75D-NT.                         
           MOVE F757-NUM-FLAT-TYPE          TO L75D-FT.                         
           WRITE BP13L75D-REC               FROM L75D-DETAIL-01.                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C75D       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F757-READ               TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13F757        : ' WS-EDIT1.          
           MOVE WS-L75D-WRITE              TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WRITTEN TO BP13L75D       : ' WS-EDIT1.          
           DISPLAY ' '.                                                         
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE BP13F757                                                       
                 BP13L75D.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
