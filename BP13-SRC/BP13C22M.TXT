       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22M.                                                 
      *AUTHOR.        SHARON DUMDUM.                                            
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *                                                            *            
      *       REPORT ON UPDATE OF CURRENT SELLING PRICE FOR        *            
      *       ACCEPTED/REJECTED FLAT.                              *            
      *                                                            *            
      *  INPUT FILE :                                              *            
      *      1. BP13F310 (TRANS FILE)                              *            
      *      2. BM06K110                                           *            
      *      3. BP13K800                                           *            
      *                                                            *            
      *  LISTING:                                                  *            
      *      1. P13L22MA - LIST OF SOC ACCEPTED CASES              *            
      *      2. P13L22MB - LIST OF SOC REJECTED CASES              *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY  DATE      DESCRIPTION                         *            
      * -------- --- --------  -----------                         *            
      * BP132582 SSD 10/09/04  NEW PROGRAM                         *            
      * BP133065 LMS 11/05/07  CATER FOR BLKS WITH NO POD BUT      *            
      *                        WITH POF OPTION.                    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K800-NUM-REGN                                   
                  FILE STATUS IS BP13K800-STATUS.                               
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K110-KEY-FLD                                    
                  FILE STATUS IS BM06K110-STATUS.                               
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K100-KEY-FLD                                    
                  FILE STATUS IS BM06K100-STATUS.                               
                                                                                
           SELECT P13L22MA ASSIGN TO P13L22MA.                                  
           SELECT P13L22MB ASSIGN TO P13L22MB.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K100.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE F                                                     
           BLOCK  CONTAINS 0 CHARACTERS                                         
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS IS STANDARD.                                           
       COPY BP13F310.                                                           
                                                                                
       FD  P13L22MA                                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  P13L22MA-REPORT         PIC X(132).                                  
                                                                                
       FD  P13L22MB                                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  P13L22MB-REPORT         PIC X(132).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-EOF-F310                  PIC X      VALUE 'N'.                   
       01  WS-F310-READ                 PIC 9(4)   VALUE ZEROES.                
                                                                                
       01  REC-K800-FOUND-FLAG          PIC X      VALUE 'N'.                   
           88  REC-K800-FOUND                      VALUE 'Y'.                   
                                                                                
       01  INVALID-REC-FLAG.                                                    
           05  K110-FOUND               PIC X      VALUE 'N'.                   
           05  K100-FOUND               PIC X      VALUE 'N'.                   
                                                                                
       01  WS-DATE                      PIC X(8)   VALUE SPACES.                
                                                                                
       01  BM06K110-STATUS              PIC 99     VALUE ZEROES.                
       01  BM06K100-STATUS              PIC 99     VALUE ZEROES.                
       01  BP13K800-STATUS              PIC 99     VALUE ZEROES.                
       01  WS-ACCEPT                    PIC 9(5)   VALUE ZEROES.                
       01  WS-REJECT                    PIC 9(5)   VALUE ZEROES.                
       01  WS-CNT                       PIC 9.                                  
       01  WS-ALLOC-SCHEME              PIC X(3)   VALUE SPACES.                
       01  WS-FLAT-TYPE                 PIC X(2)   VALUE SPACES.                
       01  WS-OCS-DUE-DTE               PIC X(6)   VALUE SPACES.                
                                                                                
       01  WS-LISTING-CONTROLS.                                                 
           05  CNT-LINE-A               PIC 9(2)   VALUE 99.                    
           05  CNT-PAGE-A               PIC 9(5)   VALUE ZEROES.                
           05  CNT-LINE-B               PIC 9(2)   VALUE 99.                    
           05  CNT-PAGE-B               PIC 9(5)   VALUE ZEROES.                
           05  WS-SNO-A                 PIC 9(4)   VALUE ZEROES.                
           05  WS-SNO-B                 PIC 9(4)   VALUE ZEROES.                
                                                                                
      **************************************************************            
      *    BP13L22M REPORT COMMON HEADING                          *            
      **************************************************************            
       01  L22M-HEAD-01.                                                        
           05  L22M-RPT-ID              PIC X(8)   VALUE SPACES.                
           05  FILLER                   PIC X(4)   VALUE SPACES .               
           05  FILLER                   PIC X(8)   VALUE 'HDBCAT 3'.            
           05  FILLER                   PIC X(35)  VALUE SPACES.                
           05  FILLER                   PIC X(20)  VALUE                        
               'SYSTEM OF COMMITMENT'.                                          
           05  FILLER                   PIC X(20)  VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'DATE :'.              
           05  L22M-DATE                PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(9)   VALUE '  PAGE : '.           
           05  L22M-PAGE                PIC ZZ,ZZ9 VALUE ZEROES.                
           05  FILLER                   PIC X(6)   VALUE SPACES.                
                                                                                
       01  L22M-HEAD-02.                                                        
           05  FILLER                   PIC X(45)  VALUE SPACES.                
           05  FILLER                   PIC X(41)  VALUE                        
               'REPORT ON UPDATE OF CURRENT SELLING PRICE'.                     
           05  FILLER                   PIC X(9)   VALUE SPACES.                
           05  FILLER                   PIC X(17)  VALUE                        
               'USER : SALES UNIT'.                                             
                                                                                
       01  L22M-HEAD-03A.                                                       
           05  FILLER                   PIC X(57)  VALUE SPACES.                
           05  FILLER                   PIC X(16)  VALUE                        
           'AFTER ACCEPTANCE'.                                                  
           05  FILLER                   PIC X(35)  VALUE SPACES.                
                                                                                
       01  L22M-HEAD-03B.                                                       
           05  FILLER                   PIC X(52)  VALUE SPACES.                
           05  FILLER                   PIC X(25)  VALUE                        
           'AFTER REJECTION BEFORE SA'.                                         
                                                                                
       01  L22M-HEAD-04.                                                        
           05  FILLER                   PIC X(5)   VALUE ' S/NO'.               
           05  FILLER                   PIC X(5)   VALUE SPACE.                 
           05  FILLER                   PIC X(8)   VALUE 'REGN NO'.             
           05  FILLER                   PIC X(6)   VALUE SPACES.                
           05  FILLER                   PIC X(5)   VALUE 'OFFER'.               
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  FILLER                   PIC X(5)   VALUE 'ALLOC'.               
           05  FILLER                   PIC X      VALUE SPACES.                
           05  FILLER                   PIC X(8)   VALUE 'SERS/REN'.            
           05  FILLER                   PIC X(4)   VALUE SPACES.                
           05  FILLER                   PIC X(3)   VALUE 'OCS'.                 
           05  FILLER                   PIC X(4)   VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'POD TAG'.             
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'POF TAG'.             
           05  FILLER                   PIC X(3)   VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'CURRENT'.             
           05  FILLER                   PIC X(9)   VALUE SPACES.                
           05  FILLER                   PIC X(12)                               
                                              VALUE 'FLAT ADDRESS'.             
           05  FILLER                   PIC X(13)  VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'ACC/REJ'.             
           05  FILLER                   PIC X(4)   VALUE SPACES.                
           05  FILLER                   PIC X(4)   VALUE 'FLAT'.                
                                                                                
       01  L22M-HEAD-05.                                                        
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'HDB-REF'.             
           05  FILLER                   PIC X(9)   VALUE SPACES.                
           05  FILLER                   PIC X(2)   VALUE 'SP'.                  
           05  FILLER                   PIC X(4)   VALUE SPACES.                
           05  FILLER                   PIC X(3)   VALUE 'SCH'.                 
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  FILLER                   PIC X(8)   VALUE 'DISC AMT'.            
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'DUE DTE'.             
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'POD AMT'.             
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'POF AMT'.             
           05  FILLER                   PIC X(5)   VALUE SPACES.                
           05  FILLER                   PIC X(2)   VALUE 'SP'.                  
           05  FILLER                   PIC X(35)   VALUE SPACES.               
           05  FILLER                   PIC X(11)  VALUE 'DATE & TIME'.         
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  FILLER                   PIC X(4)   VALUE 'TYPE'.                
                                                                                
       01  L22M-DETAIL-01.                                                      
           05  FILLER                   PIC X      VALUE SPACE.                 
           05  L22M-SNO                 PIC ZZZ9.                               
           05  FILLER                   PIC X(2)   VALUE SPACE.                 
           05  L22M-REGN-NO             PIC X(8)   VALUE SPACES.                
           05  FILLER                   PIC X(6)   VALUE SPACES.                
           05  L22M-OFFER-SELL-PRICE    PIC ZZ,ZZZ,ZZ9.                         
           05  FILLER                   PIC X      VALUE SPACE.                 
           05  L22M-ALLOC-SCHEME        PIC X(3)   VALUE SPACES.                
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  L22M-AMT-FLAT-DISCOUNT   PIC ZZZ,ZZ9.                            
           05  FILLER                   PIC X(4)   VALUE SPACES.                
           05  L22M-OCS-DUE-DTE         PIC X(6)   VALUE SPACES.                
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  L22M-POD-TAG             PIC X      VALUE SPACES.                
           05  FILLER                   PIC X(8)   VALUE SPACES.                
           05  L22M-POF-TAG             PIC X      VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE SPACES.                
           05  L22M-CURRENT-SELL-PRICE  PIC ZZ,ZZZ,ZZ9.                         
           05  FILLER                   PIC X(2)   VALUE SPACE.                 
           05  FILLER                   PIC X(4)   VALUE 'BLK '.                
           05  L22M-BLK-NO              PIC X(5)   VALUE SPACES.                
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  FILLER                   PIC X(2)   VALUE '# '.                  
           05  L22M-LEVEL-NO            PIC X(2)   VALUE SPACES.                
           05  L22M-DASH-1              PIC X      VALUE SPACES.                
           05  L22M-MAIN-UNIT           PIC X(4)   VALUE SPACES.                
           05  L22M-DASH-2              PIC X      VALUE SPACE.                 
           05  L22M-SUB-UNIT            PIC X      VALUE SPACE.                 
           05  FILLER                   PIC X(7)   VALUE SPACES.                
           05  L22M-ACC-REJ-DATE        PIC X(6)   VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L22M-ACC-REJ-TIME        PIC X(5)   VALUE SPACES.                
           05  FILLER                   PIC X(2)   VALUE SPACE.                 
           05  L22M-FLAT-TYPE           PIC X(2)   VALUE SPACE.                 
                                                                                
       01  L22M-DETAIL-02.                                                      
           05  FILLER                   PIC X(7)   VALUE SPACES.                
           05  L22M-SCHEME              PIC 9(4).                               
           05  L22M-DASH-3              PIC X      VALUE SPACES.                
           05  L22M-ACC                 PIC 9(4).                               
           05  L22M-DASH-4              PIC X      VALUE SPACES.                
           05  L22M-CHK                 PIC 9.                                  
           05  L22M-DASH-5              PIC X      VALUE SPACES.                
           05  L22M-ACC-TYPE            PIC 9(2).                               
           05  FILLER                   PIC X(35)  VALUE SPACES.                
           05  L22M-POD-AMT             PIC ZZZ,ZZ9.                            
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  L22M-POF-AMT             PIC ZZZ,ZZ9.                            
           05  FILLER                   PIC X(13)  VALUE SPACES.                
           05  L22M-STREET-NAME         PIC X(20)  VALUE SPACES.                
           05  FILLER                   PIC X(3)   VALUE ' S('.                 
           05  L22M-POSTAL-CODE         PIC X(6)   VALUE SPACES.                
           05  FILLER                   PIC X      VALUE ')'.                   
                                                                                
       01  L22M-LINE                    PIC X(132) VALUE ALL '='.               
                                                                                
       01  L22M-NIL-ACCEPTANCE          PIC X(132) VALUE                        
               'THERE WERE NO ACCEPTED CASES'.                                  
                                                                                
       01  L22M-NIL-REJECTION           PIC X(132) VALUE                        
               'THERE WERE NO REJECTED CASES'.                                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 100-OPENING-ROUTINE       THRU 100-EXIT.                     
           PERFORM 150-READ-DATA             THRU 150-EXIT.                     
           PERFORM 200-READ-AND-PROCESS-F310 THRU 200-EXIT                      
                   UNTIL WS-EOF-F310 = 'Y'.                                     
           PERFORM 300-PRINT-NIL-REPORT      THRU 300-EXIT.                     
           PERFORM 1100-CLOSING-ROUTINE      THRU 1100-EXIT.                    
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       100-OPENING-ROUTINE.                                                     
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F310                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BP13K800                                                 
                OUTPUT P13L22MA                                                 
                       P13L22MB.                                                
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR, K110-STATUS ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 1100-CLOSING-ROUTINE  THRU 1100-EXIT.                     
                                                                                
           IF BM06K100-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR, K100-STATUS ' BM06K100-STATUS             
              MOVE BM06K100-STATUS TO RETURN-CODE                               
              PERFORM 1100-CLOSING-ROUTINE  THRU 1100-EXIT.                     
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR, K800-STATUS ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 1100-CLOSING-ROUTINE  THRU 1100-EXIT.                     
                                                                                
           MOVE ZEROES TO WS-ACCEPT                                             
                          WS-REJECT                                             
                          WS-SNO-A                                              
                          WS-SNO-B.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-DATE.                             
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO L22M-DATE.                             
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       150-READ-DATA.                                                           
      *-------------------------------------------------------------            
           READ BP13F310 AT END                                                 
                MOVE 'Y' TO WS-EOF-F310.                                        
                                                                                
           ADD  1        TO WS-F310-READ.                                       
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       200-READ-AND-PROCESS-F310.                                               
      *-------------------------------------------------------------            
      *                                                                         
      *   1. READ FILE BP13F310.                                                
      *   2. CHECK F310-CDE-TRANS.                                              
      *                                                                         
      *-------------------------------------------------------------            
           PERFORM 900-READ-BM06K110 THRU 900-EXIT                              
                                                                                
           IF K110-FOUND = 'Y'                                                  
              PERFORM 400-READ-BP13K800   THRU 400-EXIT                         
              PERFORM 450-READ-BM06K100   THRU 450-EXIT                         
              PERFORM 500-PRINT-LIST      THRU 500-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 150-READ-DATA     THRU 150-EXIT.                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       300-PRINT-NIL-REPORT.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF WS-ACCEPT = ZERO                                                  
              MOVE 1          TO L22M-PAGE                                      
              MOVE 'P13L22MA' TO L22M-RPT-ID                                    
              WRITE P13L22MA-REPORT FROM L22M-HEAD-01 AFTER PAGE                
              WRITE P13L22MA-REPORT FROM L22M-HEAD-02                           
              WRITE P13L22MA-REPORT FROM L22M-HEAD-03A                          
              WRITE P13L22MA-REPORT FROM L22M-NIL-ACCEPTANCE                    
           END-IF.                                                              
                                                                                
           IF WS-REJECT = ZERO                                                  
              MOVE 1          TO L22M-PAGE                                      
              MOVE 'P13L22MB' TO L22M-RPT-ID                                    
              WRITE P13L22MB-REPORT FROM L22M-HEAD-01 AFTER PAGE                
              WRITE P13L22MB-REPORT FROM L22M-HEAD-02                           
              WRITE P13L22MB-REPORT FROM L22M-HEAD-03B                          
              WRITE P13L22MB-REPORT FROM L22M-NIL-REJECTION                     
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       400-READ-BP13K800.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                  TO REC-K800-FOUND-FLAG.                    
           MOVE F310-REGN-NO         TO K800-NUM-REGN.                          
                                                                                
           READ BP13K800 INVALID KEY                                            
                MOVE SPACES          TO WS-ALLOC-SCHEME                         
                MOVE SPACES          TO WS-FLAT-TYPE                            
                DISPLAY 'RECORD NOT FOUND IN BP13K800 ' F310-REGN-NO            
                GO TO 400-EXIT.                                                 
                                                                                
           IF BP13K800-STATUS NOT = ZEROES                                      
              MOVE SPACES            TO WS-ALLOC-SCHEME                         
              MOVE SPACES            TO WS-FLAT-TYPE                            
              DISPLAY 'READ BP13K800 ERROR STATUS ' BP13K800-STATUS             
                      ',  F310-REGN IS ' F310-REGN-NO                           
              GO TO 400-EXIT.                                                   
                                                                                
           MOVE 'Y'                  TO REC-K800-FOUND-FLAG.                    
           MOVE K800-NUM-ALLO-SCHEME TO WS-ALLOC-SCHEME.                        
           MOVE K800-NUM-FLAT-TYPE   TO WS-FLAT-TYPE.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       450-READ-BM06K100.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE 'Y'                  TO K100-FOUND.                             
           MOVE K110-ESTATE          TO K100-ESTATE.                            
           MOVE K110-NEIGHBOURHOOD   TO K100-NEIGHBOURHOOD.                     
           MOVE K110-CONTRACT-NO     TO K100-CONTRACT-NO.                       
           MOVE K110-BLK-NO          TO K100-BLK-NO.                            
                                                                                
           READ BM06K100 INVALID KEY                                            
                MOVE '000000'        TO WS-OCS-DUE-DTE                          
                DISPLAY 'READ ERROR, K100-STATUS ' BM06K100-STATUS              
                        ',  K100-KEY-FLD IS ' K100-KEY-FLD                      
                MOVE 'N' TO K100-FOUND.                                         
                                                                                
           IF K100-FOUND = 'Y'                                                  
              IF K100-DTE-POD-DUE NOT = SPACES AND ZEROES AND                   
                                        LOW-VALUES                              
                 STRING K100-DTE-POD-DUE(7:2)                                   
                        K100-DTE-POD-DUE(5:2)                                   
                        K100-DTE-POD-DUE(3:2)                                   
                        DELIMITED BY SIZE INTO WS-OCS-DUE-DTE                   
              ELSE                                                              
                 IF K100-DTE-POF-DUE NOT = SPACES AND ZEROES AND                
                                           LOW-VALUES                           
                    STRING K100-DTE-POF-DUE(7:2)                                
                           K100-DTE-POF-DUE(5:2)                                
                           K100-DTE-POF-DUE(3:2)                                
                           DELIMITED BY SIZE INTO WS-OCS-DUE-DTE                
                 ELSE                                                           
                    IF K100-DTE-OCS-DUE NOT = SPACES AND ZEROES AND             
                                           LOW-VALUES                           
                       STRING K100-DTE-OCS-DUE(7:2)                             
                              K100-DTE-OCS-DUE(5:2)                             
                              K100-DTE-OCS-DUE(3:2)                             
                              DELIMITED BY SIZE INTO WS-OCS-DUE-DTE             
                    ELSE                                                        
                       MOVE '000000'        TO WS-OCS-DUE-DTE                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       500-PRINT-LIST.                                                          
      *-------------------------------------------------------------            
      *                                                                         
      *      PRINT DETAIL LIST OF PREPARATION LIST FOR                          
      *      ACCEPTED/REJECTED CASES.                                           
      *                                                                         
      *-------------------------------------------------------------            
           IF F310-CDE-TRANS = 'AI' OR 'AS'                                     
              ADD 1                  TO WS-ACCEPT                               
                                        WS-SNO-A                                
              MOVE WS-SNO-A          TO L22M-SNO                                
              PERFORM 600-MOVE-PRINTING-INFO THRU 600-EXIT                      
              PERFORM 701-HEADING-A          THRU 701-EXIT.                     
                                                                                
           IF F310-CDE-TRANS = 'RI' OR 'RS' OR 'RK' OR 'RL' OR                  
                               'RM' OR 'R1' OR 'R2' OR 'R3'                     
              ADD 1                  TO WS-REJECT                               
                                        WS-SNO-B                                
              MOVE WS-SNO-B          TO L22M-SNO                                
              PERFORM 600-MOVE-PRINTING-INFO THRU 600-EXIT                      
              PERFORM 702-HEADING-B          THRU 702-EXIT.                     
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       600-MOVE-PRINTING-INFO.                                                  
      *-------------------------------------------------------------            
                                                                                
           MOVE F310-REGN-NO         TO L22M-REGN-NO.                           
                                                                                
           MOVE K110-SCHEME          TO L22M-SCHEME.                            
           MOVE K110-ACC             TO L22M-ACC.                               
           MOVE K110-CHK             TO L22M-CHK.                               
           MOVE K110-ACC-TYPE        TO L22M-ACC-TYPE.                          
           MOVE '-'                  TO L22M-DASH-3                             
                                        L22M-DASH-4                             
                                        L22M-DASH-5.                            
           MOVE K110-1ST-OFFER-SELLING-PRICE                                    
                                     TO L22M-OFFER-SELL-PRICE.                  
           MOVE K110-CURR-SELLING-PRICE TO L22M-CURRENT-SELL-PRICE.             
           MOVE K110-CDE-POD-TAG     TO L22M-POD-TAG.                           
           MOVE K110-AMT-POD         TO L22M-POD-AMT.                           
           MOVE K110-CDE-POF-TAG     TO L22M-POF-TAG.                           
           MOVE K110-AMT-POF         TO L22M-POF-AMT.                           
           MOVE K110-AMT-FLAT-DISCOUNT  TO L22M-AMT-FLAT-DISCOUNT.              
                                                                                
           MOVE WS-ALLOC-SCHEME      TO L22M-ALLOC-SCHEME.                      
           MOVE WS-OCS-DUE-DTE       TO L22M-OCS-DUE-DTE.                       
                                                                                
           MOVE WS-FLAT-TYPE         TO L22M-FLAT-TYPE.                         
           MOVE F310-NUM-BLK         TO L22M-BLK-NO.                            
           MOVE F310-NME-STREET      TO L22M-STREET-NAME.                       
           MOVE F310-NUM-LEVEL       TO L22M-LEVEL-NO.                          
           MOVE '-'                  TO L22M-DASH-1.                            
           MOVE F310-NUM-MAIN (1:4)  TO L22M-MAIN-UNIT.                         
           IF F310-NUM-MAIN (5:1) = SPACES                                      
              MOVE SPACE             TO L22M-DASH-2                             
                                        L22M-SUB-UNIT                           
           ELSE                                                                 
              MOVE F310-NUM-MAIN (5:1)  TO L22M-SUB-UNIT                        
              MOVE '-'                  TO L22M-DASH-2.                         
           MOVE F310-NUM-POSTAL      TO L22M-POSTAL-CODE.                       
                                                                                
           STRING F310-DTE-TRANS(7:2)                                           
                  F310-DTE-TRANS(5:2)                                           
                  F310-DTE-TRANS(3:2)                                           
                  DELIMITED BY SIZE INTO L22M-ACC-REJ-DATE.                     
                                                                                
           STRING F310-TME-ACCEPT-REJECT (1:2) ':'                              
                  F310-TME-ACCEPT-REJECT (3:2)                                  
                  DELIMITED BY SIZE INTO L22M-ACC-REJ-TIME.                     
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       701-HEADING-A.                                                           
      *-------------------------------------------------------------            
                                                                                
           IF CNT-LINE-A > 54                                                   
              ADD 1                  TO CNT-PAGE-A                              
              MOVE CNT-PAGE-A        TO L22M-PAGE                               
              MOVE 'P13L22MA'        TO L22M-RPT-ID                             
              WRITE P13L22MA-REPORT  FROM L22M-HEAD-01  AFTER PAGE              
              WRITE P13L22MA-REPORT  FROM L22M-HEAD-02  AFTER 1                 
              WRITE P13L22MA-REPORT  FROM L22M-HEAD-03A AFTER 1                 
              WRITE P13L22MA-REPORT  FROM L22M-HEAD-04  AFTER 2                 
              WRITE P13L22MA-REPORT  FROM L22M-HEAD-05  AFTER 1                 
              WRITE P13L22MA-REPORT  FROM L22M-LINE     AFTER 1                 
              MOVE ZEROES            TO CNT-LINE-A                              
           END-IF.                                                              
                                                                                
           WRITE P13L22MA-REPORT     FROM L22M-DETAIL-01 AFTER 2.               
           WRITE P13L22MA-REPORT     FROM L22M-DETAIL-02 AFTER 1.               
           ADD 4                     TO CNT-LINE-A.                             
                                                                                
       701-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       702-HEADING-B.                                                           
      *-------------------------------------------------------------            
                                                                                
           IF CNT-LINE-B > 54                                                   
              ADD 1                  TO CNT-PAGE-B                              
              MOVE CNT-PAGE-B        TO L22M-PAGE                               
              MOVE 'P13L22MB'        TO L22M-RPT-ID                             
              WRITE P13L22MB-REPORT  FROM L22M-HEAD-01  AFTER PAGE              
              WRITE P13L22MB-REPORT  FROM L22M-HEAD-02  AFTER 1                 
              WRITE P13L22MB-REPORT  FROM L22M-HEAD-03B AFTER 1                 
              WRITE P13L22MB-REPORT  FROM L22M-HEAD-04  AFTER 2                 
              WRITE P13L22MB-REPORT  FROM L22M-HEAD-05  AFTER 1                 
              WRITE P13L22MB-REPORT  FROM L22M-LINE     AFTER 1                 
              MOVE ZEROES            TO CNT-LINE-B                              
           END-IF.                                                              
                                                                                
           WRITE P13L22MB-REPORT     FROM L22M-DETAIL-01 AFTER 2.               
           WRITE P13L22MB-REPORT     FROM L22M-DETAIL-02 AFTER 1.               
           ADD 4                     TO CNT-LINE-B.                             
                                                                                
       702-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       900-READ-BM06K110.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE F310-SCH-ACC         TO K110-KEY-FLD.                           
           MOVE 'Y'                  TO K110-FOUND.                             
           READ BM06K110 INVALID KEY                                            
                DISPLAY 'READ ERROR, K110-STATUS ' BM06K110-STATUS              
                        ',  K110-KEY-FLD IS ' K110-KEY-FLD                      
                MOVE 'N' TO K110-FOUND.                                         
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           CLOSE BP13F310                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13K800                                                       
                 P13L22MA                                                       
                 P13L22MB.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 0                                           
               MOVE BM06K110-STATUS  TO RETURN-CODE                             
               DISPLAY 'CLOSING ERROR, K110-STATUS ' BM06K110-STATUS.           
                                                                                
           IF BM06K100-STATUS NOT = 0                                           
               MOVE BM06K100-STATUS  TO RETURN-CODE                             
               DISPLAY 'CLOSING ERROR, K100-STATUS ' BM06K100-STATUS.           
                                                                                
           IF BP13K800-STATUS NOT = 0                                           
               MOVE BP13K800-STATUS  TO RETURN-CODE                             
               DISPLAY 'CLOSING ERROR, K800-STATUS ' BP13K800-STATUS.           
                                                                                
           STOP RUN.                                                            
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
