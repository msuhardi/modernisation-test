       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C50D.                                                 
      *AUTHOR.        KATHRINA VALENZUELA.                                      
      *DATE-WRITTEN.  15/08/2019.                                               
      **************************************************************            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      **************************************************************            
      * OBJECTIVE : TO PRODUCE REPORT ON HA(S) WITH NEW BORN BABY  *            
      *             WHERE HA IC AND SPOUSE IC MATCHED AS PARENTS   *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- -----------                         *            
      * BP137791 20190815 KV6   NEW PRGM                           *            
      **************************************************************            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BE01F502         ASSIGN BE01F502.                             
                                                                                
           SELECT BP13K820         ASSIGN       TO BP13K820                     
                                   ACCESS MODE  IS DYNAMIC                      
                                   ORGANIZATION IS INDEXED                      
                                   RECORD KEY   IS K820-KEY-FLD                 
                                   ALTERNATE RECORD KEY IS K820-NUM-NRIC        
                                   FILE STATUS  IS K820-STATUS.                 
                                                                                
           SELECT BP13K800         ASSIGN        TO BP13K800                    
                                   ACCESS MODE   IS RANDOM                      
                                   ORGANIZATION  IS INDEXED                     
                                   RECORD KEY    IS K800-NUM-REGN               
                                   FILE STATUS   IS K800-STATUS.                
                                                                                
           SELECT BP13K022         ASSIGN        TO BP13K022                    
                                   ACCESS MODE   IS RANDOM                      
                                   ORGANIZATION  IS INDEXED                     
                                   RECORD KEY    IS K022-KEY-FLD                
                                   ALTERNATE KEY IS K022-NUM-REGN               
                                   FILE STATUS   IS K022-STATUS.                
                                                                                
           SELECT BP13K060         ASSIGN        TO BP13K060                    
                                   ACCESS MODE   IS RANDOM                      
                                   ORGANIZATION  IS INDEXED                     
                                   RECORD KEY    IS K060-KEY-FLD                
                                   FILE STATUS   IS K060-STATUS.                
                                                                                
           SELECT BP13F50D         ASSIGN BP13F50D.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BE01F502                RECORD CONTAINS 511 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BE01F502.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP13F50D                RECORD CONTAINS 150 CHARACTERS              
                                    LABEL RECORDS ARE OMITTED                   
                                    RECORDING MODE IS F.                        
       01   BP13F50D-REC.                                                       
            05  F50D-REGN                     PIC X(08).                        
            05  F50D-OIC                      PIC X(05).                        
            05  F50D-SCH-ACC                  PIC X(11).                        
            05  F50D-ELIG-SCH                 PIC X(03).                        
            05  F50D-ALLO-SCH                 PIC X(03).                        
            05  F50D-HA-NRIC                  PIC X(09).                        
            05  F50D-HA-CTZNSHP               PIC X(06).                        
            05  F50D-SP-NRIC                  PIC X(09).                        
            05  F50D-SP-CTZNSHP               PIC X(06).                        
            05  F50D-SP-STATUS                PIC X(03).                        
            05  F50D-NB-NRIC                  PIC X(09).                        
            05  F50D-NB-NAME                  PIC X(20).                        
            05  F50D-NB-DOB                   PIC X(10).                        
            05  F50D-NB-NTNLTY                PIC X(02).                        
            05  FILLER                        PIC X(46).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FLAG-N-STATUS.                                                    
           05  WS-F502-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-F502-EOF              VALUE 'Y'.                          
           05  WS-K820-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-K820-EOF              VALUE 'Y'.                          
           05  WS-K800-FND-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-K800-FND              VALUE 'Y'.                          
           05  WS-K820-FND-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-K820-FND              VALUE 'Y'.                          
           05  WS-MOTHER-FND-FLAG           PIC X(01)  VALUE 'N'.               
               88  WS-MTHR-FND              VALUE 'Y'.                          
           05  WS-FATHER-FND-FLAG           PIC X(01)  VALUE 'N'.               
               88  WS-FTHR-FND              VALUE 'Y'.                          
           05  WS-CHILD-NOTFND-FLAG         PIC X(01)  VALUE 'N'.               
               88  WS-CHLD-NOTFND           VALUE 'Y'.                          
                                                                                
       01  WS-VARIABLES.                                                        
           05  K820-STATUS                  PIC 9(2)    VALUE ZEROES.           
           05  K800-STATUS                  PIC 9(2)    VALUE ZEROES.           
           05  K022-STATUS                  PIC 9(2)    VALUE ZEROES.           
           05  K060-STATUS                  PIC 9(2)    VALUE ZEROES.           
           05  WS-DTE-CURRENT               PIC X(8)    VALUE SPACES.           
           05  WS-MOTHER-REGN               PIC X(8)    VALUE SPACES.           
           05  WS-MOTHER-CTZNSHP            PIC X(6)    VALUE SPACES.           
           05  WS-FATHER-REGN               PIC X(8)    VALUE SPACES.           
           05  WS-FATHER-CTZNSHP            PIC X(6)    VALUE SPACES.           
           05  WS-CITIZENSHIP               PIC X(6)    VALUE SPACES.           
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F502-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-K820-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-K800-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-K820-BYPASS               PIC 9(05)  VALUE ZEROS.             
           05  WS-F50D-WRTN                 PIC 9(05)  VALUE ZEROS.             
           05  WS-NUM-SNO-L50DA             PIC 9(04)  VALUE ZEROES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES           THRU  1000-EXIT.                   
           PERFORM 2000-READ-BE01F502        THRU  2000-EXIT.                   
           PERFORM 3000-PROCESS THRU 3000-EXIT UNTIL WS-F502-EOF.               
           PERFORM 9000-CLOSE-FILES          THRU  9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT BE01F502                                                  
                      BP13K820                                                  
                      BP13K800                                                  
                      BP13K022                                                  
                      BP13K060                                                  
               OUTPUT BP13F50D.                                                 
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K820 OPEN ERROR, STATUS : ' K820-STATUS              
              MOVE     K820-STATUS          TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K800 OPEN ERROR, STATUS : ' K800-STATUS              
              MOVE     K800-STATUS          TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF K022-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K022 OPEN ERROR, STATUS : ' K022-STATUS              
              MOVE     K022-STATUS          TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF K060-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K060 OPEN ERROR, STATUS : ' K060-STATUS              
              MOVE     K060-STATUS          TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BE01F502.                                                      
      *-------------------------------------------------------------            
           READ BE01F502                                                        
             AT END                                                             
                MOVE 'Y'                    TO  WS-F502-EOF-FLAG                
            NOT AT END                                                          
                ADD   1                     TO  WS-F502-READ                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS.                                                            
      *-------------------------------------------------------------            
           MOVE 'N'                         TO WS-MOTHER-FND-FLAG               
                                               WS-FATHER-FND-FLAG               
                                               WS-CHILD-NOTFND-FLAG             
           MOVE SPACES                      TO BP13K820-REC.                    
           INITIALIZE BP13K820-REC.                                             
           MOVE BE01F502-MOTHER-IDNO        TO K820-NUM-NRIC.                   
                                                                                
           PERFORM 3100-READ-BP13K820       THRU 3100-EXIT                      
                                                                                
           IF (WS-K820-FND) AND                                                 
              (BE01F502-FATHER-IDNO = K820-NUM-NRIC-SPOUSE)                     
              MOVE K820-NUM-REGN            TO WS-MOTHER-REGN                   
              MOVE 'Y'                      TO WS-MOTHER-FND-FLAG               
                                                                                
              MOVE SPACES                   TO WS-CITIZENSHIP                   
              PERFORM 4050-CONVERT-CTZNSHP  THRU 4050-EXIT                      
              MOVE WS-CITIZENSHIP           TO WS-MOTHER-CTZNSHP                
           END-IF                                                               
                                                                                
           MOVE SPACES                      TO BP13K820-REC                     
           INITIALIZE BP13K820-REC                                              
           MOVE BE01F502-FATHER-IDNO        TO K820-NUM-NRIC                    
                                                                                
           PERFORM 3100-READ-BP13K820       THRU 3100-EXIT                      
                                                                                
           IF (WS-K820-FND) AND                                                 
              (BE01F502-MOTHER-IDNO = K820-NUM-NRIC-SPOUSE)                     
              MOVE K820-NUM-REGN            TO WS-FATHER-REGN                   
              MOVE 'Y'                      TO WS-FATHER-FND-FLAG               
                                                                                
              MOVE SPACES                   TO WS-CITIZENSHIP                   
              PERFORM 4050-CONVERT-CTZNSHP  THRU 4050-EXIT                      
              MOVE WS-CITIZENSHIP           TO WS-FATHER-CTZNSHP                
           END-IF                                                               
                                                                                
           MOVE SPACES                      TO BP13K820-REC                     
           INITIALIZE BP13K820-REC                                              
           MOVE BE01F502-IDNO               TO K820-NUM-NRIC                    
                                                                                
           PERFORM 3100-READ-BP13K820       THRU 3100-EXIT                      
           IF WS-K820-FND-FLAG = 'N'                                            
              MOVE 'Y'                      TO WS-CHILD-NOTFND-FLAG             
           END-IF                                                               
                                                                                
           IF (WS-MOTHER-REGN = WS-FATHER-REGN) AND                             
               WS-MTHR-FND AND WS-FTHR-FND AND WS-CHLD-NOTFND                   
              PERFORM 3200-READ-BP13K800    THRU 3200-EXIT                      
              IF WS-K800-FND-FLAG = 'Y'                                         
                 PERFORM 4000-WRITE-BP13F50D   THRU 4000-EXIT                   
              END-IF                                                            
           END-IF                                                               
                                                                                
           PERFORM 2000-READ-BE01F502       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-READ-BP13K820.                                                      
      *---------------------------------------------------------------*         
           READ BP13K820 KEY IS K820-NUM-NRIC.                                  
                                                                                
           EVALUATE K820-STATUS                                                 
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1 TO WS-K820-READ                                          
                 MOVE 'Y'             TO WS-K820-FND-FLAG                       
              WHEN 23                                                           
                 MOVE 'N'             TO WS-K820-FND-FLAG                       
                 DISPLAY 'NOT FND IN BP13K820 : ' K820-STATUS                   
                          '  K820-NUM-NRIC : ' K820-NUM-NRIC                    
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K820, STATUS '                      
                          K820-STATUS                                           
                 DISPLAY 'K820-NUM-NRIC' K820-NUM-NRIC                          
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3200-READ-BP13K800.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                 TO BP13K800-MASTER.                      
           INITIALIZE BP13K800-MASTER.                                          
           MOVE WS-MOTHER-REGN         TO K800-NUM-REGN.                        
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE K800-STATUS                                                 
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1 TO WS-K800-READ                                          
                 MOVE 'Y'             TO WS-K800-FND-FLAG                       
              WHEN 23                                                           
                 MOVE 'N'             TO WS-K800-FND-FLAG                       
                 DISPLAY 'NOT FND IN BP13K800 : ' K800-STATUS                   
                          '  K800-NUM-REGN : ' K800-NUM-REGN                    
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K800, STATUS '                      
                          K800-STATUS                                           
                 DISPLAY 'K800-NUM-REGN' K800-NUM-REGN                          
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-BP13F50D.                                                     
      *-------------------------------------------------------------            
                                                                                
           INITIALIZE BP13F50D-REC.                                             
           MOVE SPACES                      TO BP13F50D-REC.                    
           ADD 1                            TO WS-F50D-WRTN                     
                                                                                
           MOVE K800-NUM-REGN               TO F50D-REGN.                       
           MOVE K800-NUM-SCH-ACC            TO F50D-SCH-ACC.                    
           MOVE K800-NUM-ELIG-SCHEME        TO F50D-ELIG-SCH.                   
           MOVE K800-NUM-ALLO-SCHEME        TO F50D-ALLO-SCH.                   
                                                                                
           IF (BE01F502-MOTHER-IDNO = K800-NUM-NRIC1) OR                        
              (BE01F502-MOTHER-IDNO = K800-NUM-NRIC2) OR                        
              (BE01F502-MOTHER-IDNO = K800-NUM-NRIC3) OR                        
              (BE01F502-MOTHER-IDNO = K800-NUM-NRIC4)                           
              MOVE BE01F502-MOTHER-IDNO     TO F50D-HA-NRIC                     
              MOVE WS-MOTHER-CTZNSHP        TO F50D-HA-CTZNSHP                  
              MOVE BE01F502-FATHER-IDNO     TO F50D-SP-NRIC                     
              MOVE WS-FATHER-CTZNSHP        TO F50D-SP-CTZNSHP                  
           ELSE                                                                 
              MOVE BE01F502-FATHER-IDNO     TO F50D-HA-NRIC                     
              MOVE WS-FATHER-CTZNSHP        TO F50D-HA-CTZNSHP                  
              MOVE BE01F502-MOTHER-IDNO     TO F50D-SP-NRIC                     
              MOVE WS-MOTHER-CTZNSHP        TO F50D-SP-CTZNSHP                  
           END-IF                                                               
                                                                                
           IF (F50D-SP-NRIC = K800-NUM-NRIC1) OR                                
              (F50D-SP-NRIC = K800-NUM-NRIC2) OR                                
              (F50D-SP-NRIC = K800-NUM-NRIC3) OR                                
              (F50D-SP-NRIC = K800-NUM-NRIC4)                                   
              MOVE 'HA '                    TO F50D-SP-STATUS                   
           ELSE                                                                 
              MOVE 'OC '                    TO F50D-SP-STATUS                   
           END-IF                                                               
                                                                                
           MOVE BE01F502-IDNO               TO F50D-NB-NRIC.                    
           MOVE BE01F502-PRINCIPAL-NAME(1:20) TO F50D-NB-NAME.                  
           MOVE BE01F502-DATE-BIRTH         TO F50D-NB-DOB.                     
           MOVE BE01F502-NATNLTY            TO F50D-NB-NTNLTY.                  
                                                                                
           PERFORM 4200-CALL-BP13K022       THRU 4200-EXIT.                     
           MOVE K022-NUM-OIC                TO F50D-OIC.                        
                                                                                
           WRITE BP13F50D-REC.                                                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4050-CONVERT-CTZNSHP.                                                    
      *------------------------------------------------------------             
                                                                                
           EVALUATE K820-NUM-CITIZENSHIP                                        
              WHEN 10                                                           
                 MOVE 'SG    '              TO WS-CITIZENSHIP                   
              WHEN 20                                                           
                 MOVE 'SPR   '              TO WS-CITIZENSHIP                   
              WHEN 30                                                           
                 MOVE 'MY    '              TO WS-CITIZENSHIP                   
              WHEN 40                                                           
                 MOVE 'MYPR  '              TO WS-CITIZENSHIP                   
              WHEN OTHER                                                        
                 MOVE SPACES                TO K060-KEY-FLD                     
                 MOVE 25                    TO K060-SERIAL-NO                   
                 MOVE K820-NUM-CITIZENSHIP  TO K060-CODE                        
                 PERFORM 4100-CALL-BP13K060 THRU 4100-EXIT                      
                 IF K060-STATUS = 00                                            
                    MOVE  K060-DESC(1:6)    TO WS-CITIZENSHIP                   
                 END-IF                                                         
           END-EVALUATE.                                                        
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4100-CALL-BP13K060.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13K060.                                                       
                                                                                
           EVALUATE K060-STATUS                                                 
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 23                                                           
                 DISPLAY 'NOT FND IN BP13K060 : ' K060-STATUS                   
                         '  K060-KEY-FLD  : ' K060-KEY-FLD                      
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN BP13K060 : ' K060-STATUS                     
                         '  K060-KEY-FLD  :  ' K060-KEY-FLD                     
                 MOVE  K060-STATUS          TO  RETURN-CODE                     
                 PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       4200-CALL-BP13K022.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                         TO BP13K022-REC                  
           INITIALIZE                             BP13K022-REC                  
                                                                                
           MOVE K800-NUM-REGN                  TO K022-NUM-REGN                 
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF K022-STATUS = 00 OR 02                                            
              CONTINUE                                                          
           ELSE                                                                 
              IF K022-STATUS = 23                                               
                 MOVE SPACES                   TO K022-NUM-OIC                  
              ELSE                                                              
                 DISPLAY 'BP13K022 - READ ERROR (' K022-STATUS ')'              
                 PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '------- MATF410A -------'.                                  
           DISPLAY ' RECORDS READ    IN BE01F502 : ' WS-F502-READ.              
           DISPLAY ' RECORDS READ    IN BP13K820 : ' WS-K820-READ.              
           DISPLAY ' RECORDS READ    IN BP13K800 : ' WS-K800-READ.              
           DISPLAY ' RECORDS WRITTEN IN BP13F50D : ' WS-F50D-WRTN.              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE BE01F502                                                       
                 BP13K820                                                       
                 BP13K800                                                       
                 BP13K022                                                       
                 BP13K060                                                       
                 BP13F50D.                                                      
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K820 CLOSE ERROR, STATUS : ' K820-STATUS             
              MOVE     K820-STATUS          TO  RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K800 CLOSE ERROR, STATUS : ' K800-STATUS             
              MOVE     K800-STATUS          TO  RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF K022-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K022 CLOSE ERROR, STATUS : ' K022-STATUS             
              MOVE     K022-STATUS          TO  RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF K060-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K060 CLOSE ERROR, STATUS : ' K060-STATUS             
              MOVE     K060-STATUS          TO  RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
