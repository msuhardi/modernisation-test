      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CA06.                                                 
      *AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  11 APRIL 2006.                                            
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO CREATES BP13KA30 FROM BM06F410 AND       *           
      *                 BP13FA10.                                   *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BM06F410 -- SORTED INTERFACE PIDB FILE                *           
      *   2.  BP13FA10 -- SORTED PROJECT INFORMATION FILE           *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13KA30 -- UNIT INFORMATION FILE                     *           
      *   2.  BP13KD15 -- SOC - DBSS TP FILE                                    
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP132776 11/04/2006 SD10 NEW PROGRAM                        *           
      * BP133106 17/12/2007 RB12 MOVE LENGTH OF WS-RIGHT-JUST-TXT TO*           
      *                          WS-CHR-LEN                         *           
      * BP133234 13/08/2008 LMS1 TO ADD UPDATE OF BP13KA20.         *           
      * BP133403 04/12/2008 ESA1 TO ADD UPDATE OF BP13KD15.         *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KA20 ASSIGN TO BP13KA20                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KA20-KEY-FLD                               
                  FILE STATUS     IS BP13KA20-STATUS.                           
                                                                                
           SELECT BP13KA30 ASSIGN TO BP13KA30                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KA30-KEY-FLD                               
                  FILE STATUS     IS BP13KA30-STATUS.                           
                                                                                
           SELECT BP13KA63 ASSIGN TO BP13KA63                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KA63-KEY-FLD                               
                  FILE STATUS     IS BP13KA63-STATUS.                           
                                                                                
           SELECT BM06F410   ASSIGN       TO BM06F410.                          
                                                                                
           SELECT BP13FA10   ASSIGN       TO BP13FA10.                          
                                                                                
           SELECT BP13KD15 ASSIGN TO BP13KD15                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD15-KEY-FLD                               
                  FILE STATUS     IS BP13KD15-STATUS.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KA20                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13KA20.                                                           
                                                                                
       FD   BP13KA30                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13KA30.                                                           
                                                                                
       FD   BP13KA63                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13KA63.                                                           
                                                                                
       FD   BM06F410                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  400 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BM06F410.                                                           
                                                                                
       FD   BP13FA10                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  800 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FA10.                                                           
                                                                                
       FD   BP13KD15                                                            
            RECORD CONTAINS  300 CHARACTERS.                                    
       COPY BP13KD15.                                                           
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  CNT-KA20-UPDATE        PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD15-WRITE         PIC 9(08) VALUE ZEROES.                       
       01  CNT-KA30-WRITE         PIC 9(08) VALUE ZEROES.                       
       01  CNT-KA63-READ          PIC 9(08) VALUE ZEROES.                       
       01  CNT-KA20-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD15-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-KA30-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-KA20-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-KA30-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD15-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-F410-READ          PIC 9(08) VALUE ZEROES.                       
       01  CNT-FA10-READ          PIC 9(08) VALUE ZEROES.                       
       01  WS-MATCH-REC           PIC 9(08) VALUE ZEROES.                       
       01  WS-F410-EOF            PIC X     VALUE SPACE.                        
       01  WS-FA10-EOF            PIC X     VALUE SPACE.                        
       01  BP13KA20-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KA30-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KA63-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KD15-STATUS        PIC 9(02) VALUE ZEROES.                       
                                                                                
       01  WS-LEFT-JUST-TXT       PIC X(04) VALUE SPACES.                       
       01  WS-RIGHT-JUST-TXT      PIC X(04) VALUE SPACES.                       
       01  WS-CHR-CNT             PIC 9(02) VALUE ZEROES.                       
       01  WS-BLANK-CNT           PIC 9(02) VALUE ZEROES.                       
       01  WS-CHR-LEN             PIC 9(02) VALUE ZEROES.                       
       01  WS-START-POSN          PIC 9(02) VALUE ZEROES.                       
       01  WS-DATE                PIC X(08) VALUE SPACES.                       
       01  WS-CURRENT-TIME        PIC 9(08) VALUE ZEROES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F410    THRU 2000-EXIT.                            
           PERFORM 2100-READ-KA10    THRU 2100-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F410-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BM06F410                                                 
                       BP13FA10                                                 
                       BP13KA63                                                 
                I-O    BP13KA20                                                 
                       BP13KA30                                                 
                       BP13KD15.                                                
                                                                                
           IF BP13KA20-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KA20 : ' BP13KA20-STATUS             
              MOVE BP13KA20-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KA30-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KA30 : ' BP13KA30-STATUS             
              MOVE BP13KA30-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KA63-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KA63 : ' BP13KA63-STATUS             
              MOVE BP13KA63-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD15-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD15 : ' BP13KD15-STATUS             
              MOVE BP13KD15-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F410.                                                          
      *---------------*                                                         
                                                                                
           READ BM06F410 AT END                                                 
                MOVE HIGH-VALUES TO BM06F410-REC                                
                MOVE 'Y' TO WS-F410-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-F410-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2100-READ-KA10.                                                          
      *---------------*                                                         
                                                                                
           READ BP13FA10 AT END                                                 
                MOVE HIGH-VALUES TO BP13FA10-REC                                
                MOVE 'Y' TO WS-FA10-EOF                                         
                GO TO 2100-EXIT.                                                
           ADD 1 TO CNT-FA10-READ.                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF F410-NME-ST = FA10-NME-STREET                                     
              ADD  1                    TO WS-MATCH-REC                         
              PERFORM 3100-PROCESS-REC  THRU 3100-EXIT                          
              PERFORM 2000-READ-F410    THRU 2000-EXIT                          
           ELSE                                                                 
              IF F410-NME-ST > FA10-NME-STREET                                  
                 PERFORM 2100-READ-KA10    THRU 2100-EXIT                       
              ELSE                                                              
                 DISPLAY 'NAME STREET NOT FOUND: ' F410-NME-ST                  
                 PERFORM 2000-READ-F410       THRU 2000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3100-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE SPACES               TO BP13KA30-REC.                           
           INITIALIZE                   BP13KA30-REC.                           
           MOVE FA10-NUM-PRJCT       TO KA30-NUM-PRJCT.                         
           MOVE F410-NUM-BLK         TO KA30-NUM-BLK.                           
           MOVE F410-NUM-UNITMAIN    TO KA30-NUM-MAIN-UNIT.                     
           MOVE F410-NUM-UNITSUB     TO KA30-NUM-SUB-UNIT.                      
           MOVE F410-NUM-UNITLVL     TO KA30-NUM-LEVEL.                         
           MOVE F410-NUM-SCHMACCNT   TO KA30-NUM-SCH-ACC.                       
           MOVE F410-CDE-TYP-DESG    TO KA30-NUM-FLAT-TYPE.                     
           MOVE F410-QTY-AREA-STRATA TO KA30-NUM-FLOOR-AREA.                    
           STRING '##'                                                          
                  F410-NUM-SCHMACCNT(6:6)                                       
                  DELIMITED BY SIZE                                             
             INTO KA30-NUM-REGN.                                                
                                                                                
           MOVE KA30-NUM-PRJCT     TO KA63-NUM-PROJECT.                         
           MOVE KA30-NUM-BLK       TO KA63-NUM-BLK.                             
           MOVE KA30-NUM-LEVEL     TO KA63-NUM-LEVEL.                           
           MOVE KA30-NUM-MAIN-UNIT TO WS-RIGHT-JUST-TXT.                        
           PERFORM 3300-LEFT-JUSTIFY-TXT THRU 3300-EXIT.                        
           MOVE WS-LEFT-JUST-TXT   TO KA63-NUM-MAIN-UNIT.                       
           MOVE KA30-NUM-SUB-UNIT  TO KA63-NUM-SUB-UNIT.                        
           READ BP13KA63.                                                       
           EVALUATE BP13KA63-STATUS                                             
             WHEN 00                                                            
                MOVE KA63-CDE-UNIT-TYP TO KA30-CDE-UNIT-TYP                     
                ADD  1      TO CNT-KA63-READ                                    
             WHEN 23                                                            
                DISPLAY 'BP13KA63 READ KEY NOT FOUND, KEY-FLD : '               
                                                         KA63-KEY-FLD           
             WHEN OTHER                                                         
                DISPLAY 'BP13KA63 READ ERROR, STATUS : ' BP13KA63-STATUS        
                MOVE BP13KA63-STATUS  TO RETURN-CODE                            
                PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
           PERFORM 3200-WRITE-BP13KA30  THRU 3200-EXIT.                         
           PERFORM 4000-READ-BP13KA20   THRU 4000-EXIT.                         
           PERFORM 5000-READ-BP13KD15   THRU 5000-EXIT.                         
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3200-WRITE-BP13KA30.                                                     
      *--------------------*                                                    
                                                                                
           MOVE 'BP13CA06'          TO KA30-NUM-USERID.                         
           WRITE BP13KA30-REC.                                                  
                                                                                
           IF BP13KA30-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-KA30-WRITE                                        
           ELSE                                                                 
              IF BP13KA30-STATUS = 22                                           
                 ADD 1    TO CNT-KA30-FND                                       
              ELSE                                                              
                 DISPLAY 'ERROR WRITING - BP13KA30 : ' BP13KA30-STATUS          
                 MOVE BP13KA30-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3300-LEFT-JUSTIFY-TXT.                                                   
      *------------------------*                                                
                                                                                
           MOVE ZEROES   TO WS-CHR-CNT, WS-START-POSN, WS-BLANK-CNT.            
           INSPECT WS-RIGHT-JUST-TXT TALLYING WS-BLANK-CNT                      
                  FOR LEADING SPACES.                                           
           COMPUTE WS-START-POSN = WS-BLANK-CNT + 1.                            
           MOVE LENGTH OF WS-RIGHT-JUST-TXT TO WS-CHR-LEN.                      
           COMPUTE WS-CHR-CNT = WS-CHR-LEN  -  WS-BLANK-CNT.                    
           MOVE WS-RIGHT-JUST-TXT(WS-START-POSN:WS-CHR-CNT)  TO                 
                WS-LEFT-JUST-TXT.                                               
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4000-READ-BP13KA20.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES            TO KA20-KEY-FLD.                              
           MOVE FA10-NUM-PRJCT    TO KA20-NUM-PRJCT.                            
           MOVE F410-NUM-BLK      TO KA20-NUM-BLK.                              
           READ BP13KA20.                                                       
                                                                                
           IF BP13KA20-STATUS = 00                                              
              ADD 1    TO CNT-KA20-FND                                          
              PERFORM 4100-MOVE-DATA-BP13KA20 THRU 4100-EXIT                    
              PERFORM 4200-UPDATE-BP13KA20    THRU 4200-EXIT                    
           ELSE                                                                 
              IF BP13KA20-STATUS = 23                                           
                 ADD 1        TO CNT-KA20-NOTFND                                
                 MOVE SPACES  TO BP13KA20-REC                                   
                 INITIALIZE      BP13KA20-REC                                   
                 DISPLAY 'RECORD NOT FOUND IN BP13KA20 : ' KA20-KEY-FLD         
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KA20 : ' BP13KA20-STATUS          
                 MOVE BP13KA20-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       4100-MOVE-DATA-BP13KA20.                                                 
      *------------------------*                                                
                                                                                
           MOVE F410-CDE-BLK             TO KA20-NUM-BLDNG-GL.                  
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       4200-UPDATE-BP13KA20.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE BP13KA20-REC.                                                
                                                                                
           IF BP13KA20-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-KA20-UPDATE                                       
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BP13KA20 : ' BP13KA20-STATUS            
              MOVE BP13KA20-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5000-READ-BP13KD15.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES              TO KD15-KEY-FLD.                            
           MOVE KA30-NUM-PRJCT      TO KD15-NUM-PRJCT.                          
           MOVE KA30-NUM-BLK        TO KD15-NUM-BLK.                            
           MOVE KA30-NUM-MAIN-UNIT  TO KD15-NUM-MAIN-UNIT.                      
           MOVE KA30-NUM-SUB-UNIT   TO KD15-NUM-SUB-UNIT.                       
           MOVE KA30-NUM-LEVEL      TO KD15-NUM-LEVEL.                          
           READ BP13KD15.                                                       
                                                                                
           IF BP13KD15-STATUS = 00                                              
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13KD15-STATUS = 23                                           
                 PERFORM 5010-MOVE-DATA-BP13KD15 THRU 5010-EXIT                 
                 PERFORM 5020-UPDATE-BP13KD15    THRU 5020-EXIT                 
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD15 : ' BP13KD15-STATUS          
                 MOVE BP13KD15-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       5010-MOVE-DATA-BP13KD15.                                                 
      *------------------------*                                                
                                                                                
           MOVE SPACES                   TO BP13KD15-REC.                       
           INITIALIZE                       BP13KD15-REC.                       
           MOVE KA30-NUM-PRJCT           TO KD15-NUM-PRJCT.                     
           MOVE KA30-NUM-BLK             TO KD15-NUM-BLK.                       
           MOVE KA30-NUM-MAIN-UNIT       TO KD15-NUM-MAIN-UNIT.                 
           MOVE KA30-NUM-SUB-UNIT        TO KD15-NUM-SUB-UNIT.                  
           MOVE KA30-NUM-LEVEL           TO KD15-NUM-LEVEL.                     
           MOVE KA30-NUM-PRJCT           TO KD15-NUM-PRJCT-KEY1                 
                                            KD15-NUM-PRJCT-KEY3.                
           MOVE KA30-NUM-REGN            TO KD15-NUM-REGN                       
                                            KD15-NUM-REGN3                      
                                            KD15-DTE-DBSS-CONFIRM.              
           MOVE KA30-NUM-SCH-ACC         TO KD15-NUM-SCH-ACC.                   
           MOVE KA30-CDE-UNIT-TYP        TO KD15-CDE-UNIT-TYP.                  
           MOVE WS-DATE                  TO KD15-DTE-UPDATE.                    
           MOVE WS-CURRENT-TIME          TO KD15-TME-UPDATE.                    
           MOVE 'BP13CA06'               TO KD15-NUM-USERID.                    
                                                                                
       5010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       5020-UPDATE-BP13KD15.                                                    
      *---------------------*                                                   
                                                                                
           WRITE BP13KD15-REC.                                                  
                                                                                
           IF BP13KD15-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-KD15-WRITE                                        
           ELSE                                                                 
              IF BP13KD15-STATUS = 22                                           
                 ADD 1    TO CNT-KD15-FND                                       
              ELSE                                                              
                 DISPLAY 'ERROR WRITING - BP13KD15 : ' BP13KD15-STATUS          
                 MOVE BP13KD15-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CA06 *************'.                            
           DISPLAY 'NO OF RECS MATCHED F410/KA10    : ' WS-MATCH-REC.           
           DISPLAY 'NO OF RECS READ FROM  BM06F410  : ' CNT-F410-READ.          
           DISPLAY 'NO OF RECS READ FROM  BP13FA10  : ' CNT-FA10-READ.          
           DISPLAY 'NO OF RECS UPDATED TO BP13KA20  : ' CNT-KA20-UPDATE.        
           DISPLAY 'NO OF RECS WRITTEN TO BP13KD15  : ' CNT-KD15-WRITE.         
           DISPLAY 'NO OF RECS FOUND   IN BP13KD15  : ' CNT-KD15-FND.           
           DISPLAY 'NO OF RECS WRITTEN TO BP13KA30  : ' CNT-KA30-WRITE.         
           DISPLAY 'NO OF RECS READ FROM  BP13KA63  : ' CNT-KA63-READ.          
           DISPLAY 'NO OF RECS FOUND   IN BP13KA30  : ' CNT-KA30-FND.           
           DISPLAY 'NO OF RECS FOUND   IN BP13KA20  : ' CNT-KA20-FND.           
           DISPLAY 'NO OF RECS NOT FND IN BP13KA20  : ' CNT-KA20-NOTFND.        
                                                                                
           CLOSE BP13KA20                                                       
                 BP13KA30                                                       
                 BP13KA63                                                       
                 BM06F410                                                       
                 BP13FA10.                                                      
                                                                                
           IF BP13KA20-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KA20-STATUS ' BP13KA20-STATUS          
            MOVE BP13KA20-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KA30-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KA30-STATUS ' BP13KA30-STATUS          
            MOVE BP13KA30-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KA63-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KA63-STATUS ' BP13KA63-STATUS          
            MOVE BP13KA63-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KD15-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD15-STATUS ' BP13KD15-STATUS          
            MOVE BP13KD15-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
