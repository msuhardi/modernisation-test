       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CS07.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  01/06/2010.                                               
                                                                                
      *************************************************************             
      *      SYSTEM OF COMMITMENT (BP13)                          *             
      *===========================================================*             
      *                                                           *             
      *  OBJECTIVES: 1. TO NOTIFY APPLICANTS THRU EMAIL OF THE    *             
      *                 RESULTS OF THE BALLOT AND THEIR SCHEDULED *             
      *                 APPOINTMENT TO BOOK FLAT.                 *             
      *              2. PROGRAM TO PUT MQ MESSAGE INTO QUEUE      *             
      *                 WHICH CONTAINS CUSTOMER'S EMAIL ADDRESS,  *             
      *                 QUEUE NO., APPT DATE/TIME TO BE SENT BY   *             
      *                 THE JAVA INTERFACE.                       *             
      *                                                           *             
      *   PROGRAM LOGIC:                                          *             
      *         PROCESS INPUT FILES                               *             
      *         MQCONNECT TO QUEUE MANAGER                        *             
      *         MQOPEN QUEUE                                      *             
      *         MQPUT                                             *             
      *         MQCLOSE QUEUE                                     *             
      *         MQDISCONNECT FROM QUEUE MANAGER                   *             
      *                                                           *             
      *===========================================================*             
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHG REQ#    DATE    AUTHOR REMARKS                        *             
      * -------- ---------- ------ ------------------------------ *             
      * BP133920 01/06/2010  PCL3  NEW PROGRAM                    *             
      * BP133950 24/08/2010  PCL3  TO ADD QUEUE-GRO AND NUM-WARD. *             
      * BP134070 22/11/2010  CT2   TO ADD NUM-UNIT-OFFER          *             
      *************************************************************             
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       SPECIAL-NAMES.                                                           
           SYSIN IS PARM-INPUT.                                                 
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT BP13F23A ASSIGN TO BP13F23A                                   
                  ORGANIZATION    IS SEQUENTIAL                                 
                  FILE STATUS     IS BP13F23A-STAT.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F23A                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F23A.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *----------------------------------------------------------*              
      *  IMPORTANT!                                              *              
      *  FOR PRODUCTION, QUEUE MANAGER IS 'MQP1'                 *              
      *  FOR TESTING,    QUEUE MANAGER IS 'MQT1'                 *              
      *----------------------------------------------------------*              
       01  WS-QM-NAME               PIC X(48) VALUE 'MQP1'.                     
      *01  WS-QM-NAME               PIC X(48) VALUE 'MQT1'.                     
       01  WS-BP13Q-NAME            PIC X(48) VALUE SPACES.                     
                                                                                
      *----------------------------------------------------------*              
      *  OUTPUT QUEUE NAME TO CRIMSONLOGIC                       *              
      *  THE QUEUE NAME IS THE SAME FOR PRODUCTION AND TESTING   *              
      *----------------------------------------------------------*              
       01  WS-QUEUE-NAME   PIC X(48) VALUE 'BP13.WIS.MDB.REQUESTQ'.             
                                                                                
      *----------------------------------------------------------*              
      *  MESSAGE LENGTH                                          *              
      *----------------------------------------------------------*              
       01  WS-MESSAGE-LENGTH        PIC 9(06) VALUE 376012.                     
                                                                                
      *----------------------------------------------------------*              
      *  MESSAGE                                                 *              
      *----------------------------------------------------------*              
       01  WS-SEND-MESSAGE.                                                     
           05  WS-MSG-CDE                   PIC X(03).                          
           05  WS-FILLER-01                 PIC X(01).                          
           05  WS-MSG-APPT-TYP              PIC X(02).                          
           05  WS-FILLER-02                 PIC X(01).                          
           05  WS-MSG-CTR                   PIC 9(04).                          
           05  WS-FILLER-03                 PIC X(01).                          
           05  WS-MSG OCCURS 2000 TIMES.                                        
               10  WS-MSG-EMAIL             PIC X(50).                          
               10  WS-FILLER-04             PIC X(01).                          
               10  WS-DTE-BALLOT            PIC X(06).                          
               10  WS-FILLER-05             PIC X(01).                          
               10  WS-MSG-FLAT-TYPE         PIC X(02).                          
               10  WS-FILLER-06             PIC X(01).                          
               10  WS-MSG-QUEUE-NO          PIC X(05).                          
               10  WS-FILLER-07             PIC X(01).                          
               10  WS-PROJ-NAME             PIC X(30).                          
               10  WS-FILLER-08             PIC X(01).                          
               10  WS-SEL-PERIOD            PIC X(20).                          
               10  WS-FILLER-09             PIC X(01).                          
               10  WS-MSG-REGN              PIC X(08).                          
               10  WS-FILLER-10             PIC X(01).                          
               10  WS-MSG-QUEUE-GRO         PIC X(05).                          
               10  WS-FILLER-11             PIC X(01).                          
               10  WS-MSG-WARD              PIC X(25).                          
               10  WS-FILLER-12             PIC X(01).                          
               10  WS-NUM-UNIT-OFFER        PIC X(04).                          
               10  WS-FILLER-13             PIC X(01).                          
               10  WS-DTE-ACCEPTANCE        PIC X(08).                          
               10  WS-FILLER-14             PIC X(01).                          
               10  WS-NUM-BLK               PIC X(05).                          
               10  WS-FILLER-15             PIC X(01).                          
               10  WS-NUM-LEVEL             PIC X(02).                          
               10  WS-FILLER-16             PIC X(01).                          
               10  WS-NUM-UNIT              PIC X(04).                          
               10  WS-FILLER-17             PIC X(01).                          
                                                                                
      *----------------------------------------------------------*              
      *  OTHER VARIABLES                                         *              
      *----------------------------------------------------------*              
       01  WS-VARIABLES.                                                        
           05  WS-CTR               PIC 9(04).                                  
           05  WS-START             PIC 9(03).                                  
           05  WS-F23A-READ         PIC 9(05).                                  
                                                                                
      *-------------------------------------------------------------            
      *  MQI - TEMPORARY VARIABLES FOR ERROR CODES                              
      *-------------------------------------------------------------            
       01  BP13CS07-REC.                                                        
           05 CS07-REASON          PIC 9(4).                                    
           05 CS07-COMPLETION-CODE PIC 9(4).                                    
           05 CS07-ERR-MSG         PIC X(66).                                   
           05 FILLER               PIC X(27).                                   
                                                                                
                                                                                
      *----------------------------------------------------------*              
      *              MQI STRUCTURE DECLARATION                   *              
      *----------------------------------------------------------*              
       01 MY-MQ-CONSTANTS.                                                      
          COPY CMQV.                                                            
                                                                                
       01 OBJECT-DESCRIPTOR.                                                    
          COPY CMQODV.                                                          
                                                                                
       01 MESSAGE-DESCRIPTOR.                                                   
          COPY CMQMDV.                                                          
                                                                                
      * PUT MESSAGE OPTIONS                                                     
       01 PMOPTIONS.                                                            
          COPY CMQPMOV.                                                         
                                                                                
      *----------------------------------------------------------*              
      * VARIABLE DESCRIPTION:                                    *              
      * (1) HCONN                                                *              
      *       - CONNECTION HANDLE - REPRESENTS THE CONNECTION TO *              
      *         THE QUEUE MANAGER. VALUE IS RETURNED BY MQCONN   *              
      *         AND SUBSEQUENTLY REFERENCED IN MQOPEN, MQPUT,    *              
      *         MQCLOSE AND MQDISC                               *              
      *                                                          *              
      * (2) Q-HANDLE                                             *              
      *       - ACCESS THAT HAS BEEN ESTABLISHED  TO THE OBJECT  *              
      *         CONNECTION HANDLE - VALUE IS RETURNED BY MQOPEN  *              
      *         AND SUBSEQUENTLY REFERENCED IN MQPUT AND MQCLOSE *              
      *                                                          *              
      * (3) OBJECT-DESCRIPTOR                                    *              
      *       - THIS IDENTIFIES THE OBJECT TO BE OPENED          *              
      *                                                          *              
      * (4) OPTIONS                                              *              
      *       WHEN USED IN MQOPEN                                *              
      *       - OPTIONS THAT CONTROL THE ACTION OF MQOPEN        *              
      *       - OPTIONS USED IN THIS PROGRAM:                    *              
      *         MQOO-OUTPUT - THE QUEUE IS OPENED FOR USE WITH   *              
      *                       SUBSEQUENT MQPUT CALL              *              
      *         MQOO-FAIL-IF-QUIESCING - FORCES MQCALL TO FAIL   *              
      *                       IF QUEUE MANAGER IS IN QUIESCING   *              
      *                       STATE (I.E. PREPARING TO SHUTDOWN) *              
      *                                                          *              
      *       WHEN USED IN MQCLOSE                               *              
      *       - OPTIONS THAT CONTROL THE ACTION OF MQCLOSE       *              
      *       - OPTIONS USED IN THIS PROGRAM:                    *              
      *         MQCO-NONE   - NO OPTIONAL CLOSE PROCESSING       *              
      *                       REQUIRED                           *              
      *                                                          *              
      * (5) MESSAGE-DESCRIPTOR                                   *              
      *       - THIS DESCRIBES THE ATTRIBUTES OF THE MESSAGE     *              
      *         BEING SENT, AND RECEIVES INFO ABOUT THE MESSAGE  *              
      *         AFTER THE PUT REQUEST IS COMPLETE                *              
      *                                                          *              
      * (6) PMOPTIONS (PUT MESSAGE OPTIONS)                      *              
      *       - OPTIONS THAT CONTROL THE ACTION OF MQPUT         *              
      *       - OPTION USED IN THIS PROGRAM:                     *              
      *         MQMD-EXPIRY - MESSAGE LIFETIME. THE MESSAGE      *              
      *                       BECOMES ELIGIBLE TO BE DISCARDED   *              
      *                       IF IT HAS NOT BEEN REMOVED FROM    *              
      *                       FROM THE QUEUE BEFORE THIS PERIOD  *              
      *                       OF TIME ELAPSED                    *              
      *                     - MUST BE SET TO MQEI_UNLIMITED      *              
      *         MQMD-PERSISTENCE = MQPER-PERSISTENT              *              
      *                                                          *              
      * (7) BUFFER-LENGTH                                        *              
      *       - LENGTH OF THE MESSAGE IN BUFFER                  *              
      *                                                          *              
      * (7) BUFFER                                               *              
      *       - MESSAGE DATA                                     *              
      *----------------------------------------------------------*              
       01 HCONN                    PIC S9(9) BINARY.                            
       01 Q-HANDLE                 PIC S9(9) BINARY.                            
       01 OPTIONS                  PIC S9(9) BINARY.                            
       01 COMPLETION-CODE          PIC S9(9) BINARY.                            
       01 OPEN-CODE                PIC S9(9) BINARY.                            
       01 CON-REASON               PIC S9(9) BINARY.                            
       01 REASON                   PIC S9(9) BINARY.                            
       01 MQMSGD                   PIC S9(9) BINARY.                            
       01 BUFFER-LENGTH            PIC S9(9) BINARY.                            
       01 BUFFER                   PIC X(376012).                               
       01 QM-NAME                  PIC X(48) VALUE SPACES.                      
       01 TARGET-QUEUE             PIC X(48) VALUE SPACES.                      
                                                                                
      *-------------------------------------------------------------            
      *  FILE STATUS                                                            
      *-------------------------------------------------------------            
       01  WS-FILE-STAT.                                                        
           05 BP13F23A-STAT        PIC X(02)  VALUE SPACES.                     
                                                                                
      *-------------------------------------------------------------            
      *  FLAGS / SWITCHES                                                       
      *-------------------------------------------------------------            
       01  WS-FLAGS.                                                            
           05 F23A-EOF             PIC X(01) VALUE 'N'.                         
           05 F23A-EMPTY           PIC X(01) VALUE 'N'.                         
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      ********************                                                      
       0000-MAIN-PROCESS.                                                       
      ********************                                                      
                                                                                
           PERFORM 0100-OPEN-FILES         THRU 0100-EXIT.                      
           PERFORM 0200-INITIALIZATION     THRU 0200-EXIT.                      
                                                                                
           ACCEPT WS-BP13Q-NAME            FROM PARM-INPUT.                     
                                                                                
           PERFORM 1000-CHECK-MESSAGE      THRU 1000-EXIT.                      
           MOVE F23A-TYPE-APPMT            TO WS-MSG-APPT-TYP.                  
           PERFORM 3000-PROCESS-BP13F23A   THRU 3000-EXIT                       
                   UNTIL F23A-EOF = 'Y'.                                        
           DISPLAY 'WS-SEND-MESSAGE : ' WS-SEND-MESSAGE.                        
           PERFORM 5300-MQ-PUT-MESSAGE     THRU 5300-EXIT.                      
           DISPLAY ' *** APPOINTMENT NOTIFICATION SENT!'                        
           PERFORM 5400-MQ-CLOSE           THRU 5400-EXIT.                      
           PERFORM 5500-MQ-DISCONNECT      THRU 5500-EXIT.                      
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       0100-OPEN-FILES.                                                         
      ********************                                                      
                                                                                
           DISPLAY ' ***** STARTING PROGRAM BP13CS07 . . . '.                   
           DISPLAY ' '.                                                         
           OPEN    INPUT   BP13F23A.                                            
                                                                                
           IF  BP13F23A-STAT     NOT  =  00                                     
               DISPLAY ' OPEN ERROR BP13F23A'                                   
               DISPLAY 'BP13F23A-STAT = ' BP13F23A-STAT                         
               MOVE     12                   TO RETURN-CODE                     
               PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT.                    
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       0200-INITIALIZATION.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES     TO WS-SEND-MESSAGE.                                  
           INITIALIZE         WS-SEND-MESSAGE.                                  
           MOVE ';'        TO WS-FILLER-01                                      
                              WS-FILLER-02                                      
                              WS-FILLER-03.                                     
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 2000               
              MOVE ';'     TO WS-FILLER-04(WS-CTR)                              
                              WS-FILLER-05(WS-CTR)                              
                              WS-FILLER-06(WS-CTR)                              
                              WS-FILLER-07(WS-CTR)                              
                              WS-FILLER-08(WS-CTR)                              
                              WS-FILLER-09(WS-CTR)                              
                              WS-FILLER-10(WS-CTR)                              
                              WS-FILLER-11(WS-CTR)                              
                              WS-FILLER-12(WS-CTR)                              
                              WS-FILLER-13(WS-CTR)                              
                              WS-FILLER-14(WS-CTR)                              
                              WS-FILLER-15(WS-CTR)                              
                              WS-FILLER-16(WS-CTR)                              
                              WS-FILLER-17(WS-CTR)                              
           END-PERFORM.                                                         
                                                                                
           MOVE 'EML'      TO WS-MSG-CDE.                                       
                                                                                
      *-----------------------------                                            
      *  INITIAL READ OF INPUT FILES                                            
      *-----------------------------                                            
           READ BP13F23A                                                        
               AT END                                                           
                   MOVE 'Y' TO F23A-EOF                                         
                   MOVE 'Y' TO F23A-EMPTY                                       
                   DISPLAY ' *** '                                              
                   DISPLAY ' *** NO RECORDS FOUND IN SMS FILE'                  
                   DISPLAY ' *** '.                                             
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       1000-CHECK-MESSAGE.                                                      
      ************************                                                  
                                                                                
           IF F23A-EMPTY = 'Y'                                                  
               PERFORM 9000-CLOSE-FILES  THRU  9000-EXIT                        
           ELSE                                                                 
               PERFORM 5100-MQ-CONNECT   THRU  5100-EXIT                        
               PERFORM 5200-MQ-OPEN      THRU  5200-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO BUFFER.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       3000-PROCESS-BP13F23A.                                                   
      ************************                                                  
                                                                                
           IF F23A-TYPE-APPMT = 'B1' OR 'B2' OR 'B3' OR 'B4' OR                 
                                'S1' OR 'S2' OR 'S3' OR 'S4' OR                 
                                'G1' OR 'G2' OR 'G3' OR 'G4' OR                 
                                'G5' OR 'G6' OR 'G7' OR 'G8' OR                 
                                'F1' OR 'F2' OR 'C1' OR 'C2' OR                 
                                'P1'                                            
              PERFORM 3200-PASS-MSG              THRU 3200-EXIT                 
           END-IF.                                                              
                                                                                
           READ BP13F23A                                                        
               AT END                                                           
                   MOVE 'Y' TO F23A-EOF.                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3200-PASS-MSG.                                                           
      ****************************************************************          
                                                                                
           ADD 1  TO WS-MSG-CTR.                                                
                                                                                
           MOVE F23A-NUM-REGN        TO WS-MSG-REGN (WS-MSG-CTR).               
           MOVE F23A-NUM-FLAT-TYPE   TO WS-MSG-FLAT-TYPE(WS-MSG-CTR).           
           MOVE F23A-DTE-BALLOT      TO WS-DTE-BALLOT(WS-MSG-CTR).              
           MOVE F23A-NUM-PRJ-NME     TO WS-PROJ-NAME(WS-MSG-CTR).               
           MOVE F23A-NUM-SEL-PERIOD  TO WS-SEL-PERIOD(WS-MSG-CTR).              
           MOVE F23A-NUM-EMAIL       TO WS-MSG-EMAIL(WS-MSG-CTR).               
           MOVE F23A-NUM-QUEUE       TO WS-MSG-QUEUE-NO(WS-MSG-CTR).            
           MOVE F23A-NUM-QUEUE-GRO   TO WS-MSG-QUEUE-GRO(WS-MSG-CTR).           
           MOVE F23A-NUM-WARD        TO WS-MSG-WARD(WS-MSG-CTR).                
           MOVE F23A-NUM-UNIT-OFFER  TO WS-NUM-UNIT-OFFER(WS-MSG-CTR).          
           MOVE F23A-DTE-ACCEPTANCE  TO WS-DTE-ACCEPTANCE(WS-MSG-CTR).          
           MOVE F23A-NUM-BLK         TO WS-NUM-BLK (WS-MSG-CTR).                
           MOVE F23A-NUM-LEVEL       TO WS-NUM-LEVEL(WS-MSG-CTR).               
           MOVE F23A-NUM-MAIN-UNIT   TO WS-NUM-UNIT (WS-MSG-CTR).               
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5100-MQ-CONNECT.                                                         
      ****************************************************************          
      *---------------------------------------------------------------          
      *  SET THE QUEUE MANAGER NAME                                             
      *---------------------------------------------------------------          
           IF WS-BP13Q-NAME NOT = SPACES AND LOW-VALUES                         
              MOVE WS-BP13Q-NAME           TO   QM-NAME                         
           ELSE                                                                 
              MOVE WS-QM-NAME              TO   QM-NAME                         
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *   MQCONN                                                                
      *---------------------------------------------------------------          
           CALL 'MQCONN'                                                        
                USING QM-NAME, HCONN,                                           
                COMPLETION-CODE, CON-REASON.                                    
                                                                                
           IF COMPLETION-CODE  = MQCC-FAILED                                    
              MOVE CON-REASON              TO   CS07-REASON                     
              MOVE COMPLETION-CODE         TO   CS07-COMPLETION-CODE            
              MOVE 'BP13CS07 MQCONN ERROR' TO   CS07-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5200-MQ-OPEN.                                                            
      ****************************************************************          
      *---------------------------------------------------------------          
      *  USE DEFAULT WS-QUEUE-NAME                                              
      *---------------------------------------------------------------          
                                                                                
           MOVE  WS-QUEUE-NAME         TO MQOD-OBJECTNAME.                      
           ADD   MQOO-OUTPUT MQOO-FAIL-IF-QUIESCING                             
                               GIVING OPTIONS.                                  
                                                                                
      *---------------------------------------------------------------          
      *   MQOPEN                                                                
      *---------------------------------------------------------------          
           CALL 'MQOPEN'                                                        
                USING HCONN, OBJECT-DESCRIPTOR,                                 
                OPTIONS, Q-HANDLE,                                              
                OPEN-CODE, REASON.                                              
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                     TO   CS07-REASON                  
              MOVE OPEN-CODE                  TO   CS07-COMPLETION-CODE         
              MOVE 'MQOPEN ENDED WITH REASON CODE'                              
                                              TO   CS07-ERR-MSG                 
              PERFORM 9100-EXIT-MQCONN        THRU 9100-EXIT                    
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       5300-MQ-PUT-MESSAGE.                                                     
      ***********************                                                   
                                                                                
           MOVE WS-SEND-MESSAGE     TO BUFFER.                                  
                                                                                
      *---------------------------------------------------------------          
      *  SET UP MESSAGE DESCRIPTOR                                              
      *---------------------------------------------------------------          
           MOVE MQEI-UNLIMITED      TO MQMD-EXPIRY.                             
           MOVE MQPER-PERSISTENT    TO MQMD-PERSISTENCE.                        
           MOVE MQFMT-STRING        TO MQMD-FORMAT.                             
           MOVE WS-MESSAGE-LENGTH   TO BUFFER-LENGTH.                           
      *---------------------------------------------------------------          
      *   MQPUT                                                                 
      *---------------------------------------------------------------          
                                                                                
           CALL 'MQPUT'                                                         
                USING HCONN, Q-HANDLE,                                          
                      MESSAGE-DESCRIPTOR, PMOPTIONS,                            
                      BUFFER-LENGTH, BUFFER,                                    
                      COMPLETION-CODE, REASON.                                  
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                  TO   CS07-REASON                     
              MOVE COMPLETION-CODE         TO   CS07-COMPLETION-CODE            
              MOVE 'BP13CS07 MQPUT ERROR'  TO   CS07-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5400-MQ-CLOSE.                                                           
      ****************************************************************          
                                                                                
           MOVE MQCO-NONE TO OPTIONS.                                           
                                                                                
      *---------------------------------------------------------------          
      *   MQCLOSE                                                               
      *---------------------------------------------------------------          
           CALL 'MQCLOSE'                                                       
                USING HCONN, Q-HANDLE, OPTIONS,                                 
                COMPLETION-CODE, REASON.                                        
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                   TO   CS07-REASON                    
              MOVE COMPLETION-CODE          TO   CS07-COMPLETION-CODE           
              MOVE 'BP13CS07 MQCLOSE ERROR' TO   CS07-ERR-MSG                   
              PERFORM 9100-EXIT-MQCONN      THRU 9100-EXIT                      
           END-IF.                                                              
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5500-MQ-DISCONNECT.                                                      
      ****************************************************************          
      *---------------------------------------------------------------          
      *  DISCONNECT FROM QUEUE MANAGER                                          
      *---------------------------------------------------------------          
           CALL 'MQDISC'                                                        
           USING HCONN, COMPLETION-CODE, REASON                                 
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                  TO   CS07-REASON                     
              MOVE COMPLETION-CODE         TO   CS07-COMPLETION-CODE            
              MOVE 'BP13CS07 MQDISC ERROR' TO   CS07-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-FILES.                                                        
      ****************************************************************          
                                                                                
           CLOSE BP13F23A.                                                      
                                                                                
           IF    RETURN-CODE NOT = ZERO                                         
                 DISPLAY ' *** RETURN-CODE = ' RETURN-CODE ' ERROR'             
           ELSE                                                                 
                 DISPLAY ' '                                                    
                 DISPLAY ' *** SUCCESSFUL EXECUTION OF BP13CS07.'               
                 DISPLAY ' *** MESSAGE/S PUT INTO QUEUE SUCCESSFULLY!'          
                 DISPLAY ' *** Q MANAGER : ' QM-NAME                            
                 DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                      
                 DISPLAY ' '                                                    
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9100-EXIT-MQCONN.                                                        
      ****************************************************************          
                                                                                
           DISPLAY ' *** PROGRAM ID:  BP13CS07 '                                
           DISPLAY ' *** Q MANAGER : ' QM-NAME                                  
           DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                            
           DISPLAY ' '                                                          
           IF CS07-COMPLETION-CODE NOT = ZEROES AND LOW-VALUES                  
              DISPLAY ' *** MQ COMPLETION CODE: ' CS07-COMPLETION-CODE          
              DISPLAY ' *** MQ REASON         : ' CS07-REASON                   
              DISPLAY ' *** MQ ERROR MSG      : ' CS07-ERR-MSG                  
              MOVE 99                    TO  RETURN-CODE                        
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       9100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
      **********            END OF BP13CS07                 **********          
      ****************************************************************          
