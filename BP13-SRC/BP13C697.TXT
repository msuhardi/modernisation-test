       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C697.                                                 
      *AUTHOR.        MANU.                                                     
      *DATE-WRITTEN.  18/07/97.                                                 
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1.  PROGRAM TO UPDATE MTH-RESIDE-JB-INDIVIDUAL    *                
      *          TO JB LINKS IF ONE OF THE LINK IS FOUND       *                
      *          TO BE LESS THAN 5 YR OCCUPATION PERIOD        *                
      *                                                        *                
      * CHG-NO   BY   ON     DESCRIPTION                       *                
      * --------------------------------                       *                
      * BP130348 MMS  180797 NEW PROGRAM                       *                
      * BP130377 LSB  260797 MATCH BY SEQ-NO/JB-PARENT         *                
      * BP130652 NOR  190299 EXPAND F500 & F520 RECL 300 TO 500*                
      * BP132603 JF4  281004 CHANGE 10-YR TIMEBAR TO 5-YR      *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500   ASSIGN       TO BP13F500.                          
                                                                                
           SELECT BP13F520   ASSIGN       TO BP13F520.                          
                                                                                
           SELECT P13F500A   ASSIGN       TO P13F500A.                          
                                                                                
           SELECT P13F520A   ASSIGN       TO P13F520A.                          
                                                                                
           SELECT BP13L697   ASSIGN       TO BP13L697.                          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F500                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F500.                                                           
                                                                                
       FD   BP13F520                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F520.                                                           
                                                                                
       FD   BP13L697.                                                           
                                                                                
       01   BP13L697-REC               PIC X(132).                              
                                                                                
       FD   P13F500A.                                                           
                                                                                
       01   P13F500A-REC              PIC X(500).                               
                                                                                
       FD   P13F520A.                                                           
                                                                                
       01   P13F520A-REC              PIC X(500).                               
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  L697-COUNTERS.                                                       
           05 WS-SNO               PIC 9(4)     VALUE ZEROES.                   
           05 WS-LINE-CNT          PIC 9(2)     VALUE 90.                       
           05 WS-PAGENO            PIC 9(4)     VALUE ZEROES.                   
           05 WS-MATCH             PIC 9(8)     VALUE ZEROES.                   
           05 WS-MATCH-JB          PIC 9(8)     VALUE ZEROES.                   
           05 WS-MATCH-PA          PIC 9(8)     VALUE ZEROES.                   
           05 WS-F500-READ         PIC 9(08)    VALUE ZEROES.                   
           05 WS-F520-READ         PIC 9(08)    VALUE ZEROES.                   
           05 WS-P13F500A-WRITE    PIC 9(08)    VALUE ZEROES.                   
           05 WS-P13F520A-WRITE    PIC 9(08)    VALUE ZEROES.                   
                                                                                
       01  WS-F500-KEY.                                                         
           05 WS-F500-SEQUENCE     PIC X(08)    VALUE SPACES.                   
           05 WS-F500-PARENT       PIC X(08)    VALUE SPACES.                   
                                                                                
       01  WS-F520-KEY.                                                         
           05 WS-F520-SEQUENCE     PIC X(08)    VALUE SPACES.                   
           05 WS-F520-PARENT       PIC X(08)    VALUE SPACES.                   
                                                                                
       01  L697-LINE1.                                                          
           05  FILLER              PIC X(9)      VALUE ' BP13L697'.             
           05  FILLER              PIC X(4)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(21)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(23)     VALUE                          
                                    'C O M M I T M E N T '.                     
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L697-DATE           PIC X(10).                                   
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'PAGE :'.                
           05  L697-PAGENO         PIC ZZ9 .                                    
                                                                                
       01  L697-LINE2.                                                          
           05  FILLER              PIC X(26)     VALUE SPACES .                 
           05  FILLER              PIC X(56)     VALUE                          
           'TO DELETE JB LINKS WHERE ONE OF THE CASE INELIGIBLE FOR '.          
           05  FILLER              PIC X(12)     VALUE '5 YR OCCUPN '.          
                                                                                
       01  L697-LINE3.                                                          
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(03) VALUE 'S/N'.                       
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(06) VALUE 'SEQ-NO'.                    
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(07) VALUE 'REGN-NO'.                   
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(14) VALUE 'JB-PARENT-REGN'.            
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(08) VALUE 'JB-REGN'.                   
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE 'ZONE'.                      
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(02) VALUE 'FT'.                        
           05  FILLER              PIC X(03) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'JB-OWN-MTH'.                
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE '5YR '.                      
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(09) VALUE 'MTH-RS-LK'.                 
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(07) VALUE '5YR-LK '.                   
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(07) VALUE 'ALLO-SH'.                   
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(06) VALUE 'JB-CNT'.                    
                                                                                
       01  L697-DETAIL-LINE.                                                    
           05  FILLER              PIC X(01)     VALUE SPACES .                 
           05  L697-SNO            PIC ZZZZZ9 .                                 
           05  FILLER              PIC X(05)     VALUE SPACES .                 
           05  L697-SEQ-NO         PIC X(08).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L697-NUM-REGN       PIC X(08).                                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  L697-JB-PARENT-REGN PIC X(08).                                   
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  L697-JB-REGN        PIC X(08).                                   
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  L697-ZONE           PIC X(02).                                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  L697-FT             PIC X(02).                                   
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  L697-JB-INDV        PIC X(03).                                   
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  L697-5-YEAR         PIC X(01).                                   
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  L697-MTH-RESIDE     PIC X(03).                                   
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  L697-5YR-LINK       PIC X(03).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L697-ALLOC-SCH      PIC X(03).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L697-JB-CNT         PIC X(02).                                   
                                                                                
       COPY PRIOSCH.                                                            
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-YY                PIC 9(04).                                   
           05 WS-MM                PIC 9(02).                                   
           05 WS-DD                PIC 9(02).                                   
                                                                                
       01  WS-CURR-DATE            PIC X(10).                                   
                                                                                
      *================================================================*        
       PROCEDURE DIVISION.                                                      
      *================================================================*        
                                                                                
      *-----------*                                                             
       000-CONTROL.                                                             
      *-----------*                                                             
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-READ-F500     THRU 200-EXIT.                             
           PERFORM 300-READ-F520     THRU 300-EXIT.                             
           PERFORM 400-PROCESS-REC   THRU 400-EXIT                              
                   UNTIL WS-F500-KEY = HIGH-VALUES AND                          
                         WS-F520-KEY = HIGH-VALUES.                             
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       100-OPEN-FILES.                                                          
      *--------------*                                                          
                                                                                
           OPEN INPUT  BP13F520                                                 
                       BP13F500                                                 
               OUTPUT  P13F500A                                                 
                       P13F520A                                                 
                       BP13L697.                                                
                                                                                
             MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYS-DATE.                  
                                                                                
             STRING WS-DD '/'                                                   
                    WS-MM '/'                                                   
                    WS-YY DELIMITED BY SIZE INTO WS-CURR-DATE.                  
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       110-PRINT-REPORT-HEAD.                                                   
      *----------------------*                                                  
                                                                                
           ADD       1        TO   WS-PAGENO.                                   
           MOVE  WS-PAGENO    TO   L697-PAGENO.                                 
           MOVE  WS-CURR-DATE TO   L697-DATE.                                   
           WRITE BP13L697-REC FROM L697-LINE1 AFTER PAGE.                       
           WRITE BP13L697-REC FROM L697-LINE2 AFTER 2.                          
           WRITE BP13L697-REC FROM L697-LINE3 AFTER 2.                          
           MOVE  05           TO   WS-LINE-CNT.                                 
                                                                                
       110-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-READ-F500.                                                           
      *-----------------*                                                       
                                                                                
           READ BP13F500 AT END                                                 
                MOVE HIGH-VALUES TO WS-F500-KEY                                 
                GO TO 200-EXIT.                                                 
                                                                                
           MOVE F500-NUM-SEQUENCE       TO WS-F500-SEQUENCE.                    
           MOVE F500-NUM-JB-PARENT-REGN TO WS-F500-PARENT.                      
           ADD 1 TO WS-F500-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       300-READ-F520.                                                           
      *-----------------*                                                       
                                                                                
           READ BP13F520 AT END                                                 
                MOVE HIGH-VALUES TO WS-F520-KEY                                 
                GO TO 300-EXIT.                                                 
           MOVE F520-NUM-SEQUENCE       TO WS-F520-SEQUENCE.                    
           MOVE F520-NUM-JB-PARENT-REGN TO WS-F520-PARENT.                      
           ADD 1 TO WS-F520-READ.                                               
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       400-PROCESS-REC.                                                         
      *-----------------*                                                       
                                                                                
           IF WS-F520-KEY   =   WS-F500-KEY                                     
              ADD        1                TO WS-MATCH                           
              PERFORM 410-UPD-MTH-RESIDE  THRU 410-EXIT                         
                  UNTIL WS-F500-KEY NOT = WS-F520-KEY                           
                  OR    WS-F500-KEY = HIGH-VALUES                               
              PERFORM 300-READ-F520          THRU 300-EXIT                      
           ELSE                                                                 
              IF WS-F520-KEY < WS-F500-KEY                                      
                 PERFORM 300-READ-F520       THRU 300-EXIT                      
              ELSE                                                              
                 PERFORM 411-WRITE-P13F500A  THRU 411-EXIT                      
                 PERFORM 200-READ-F500       THRU 200-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       410-UPD-MTH-RESIDE.                                                      
      *------------------*                                                      
                                                                                
           IF F500-CDE-ALLOC-SCH NOT = 'JB '                                    
               ADD        1               TO WS-MATCH-PA                        
               PERFORM 411-WRITE-P13F500A THRU 411-EXIT                         
           ELSE                                                                 
               PERFORM 412-UPDATE-JB-MTH-RES THRU 412-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F500          THRU 200-EXIT.                        
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       411-WRITE-P13F500A.                                                      
      *------------------*                                                      
                                                                                
           MOVE   SPACES        TO   P13F500A-REC.                              
           WRITE  P13F500A-REC  FROM BP13F500-REC.                              
           ADD     1            TO   WS-P13F500A-WRITE.                         
                                                                                
       411-EXIT.                                                                
           EXIT.                                                                
                                                                                
       412-UPDATE-JB-MTH-RES.                                                   
                                                                                
           ADD        1             TO   WS-MATCH-JB.                           
           PERFORM 420-PRINT-REPORT THRU 420-EXIT.                              
           MOVE   SPACES            TO   P13F520A-REC.                          
           WRITE  P13F520A-REC      FROM BP13F500-REC.                          
           ADD     1                TO   WS-P13F520A-WRITE.                     
                                                                                
           IF  F500-NUM-MTH-RESIDE IS NOT NUMERIC                               
               MOVE ZEROS TO F500-NUM-MTH-RESIDE.                               
           IF  F520-NUM-MTH-RESIDE IS NOT NUMERIC                               
               MOVE ZEROS TO F520-NUM-MTH-RESIDE.                               
           MOVE F500-NUM-MTH-RESIDE  TO F500-NUM-MTH-RES-JB-IND.                
           MOVE F520-NUM-MTH-RESIDE  TO F500-NUM-MTH-RESIDE                     
           MOVE F520-NUM-5YR-OCCUPN  TO F500-NUM-5YR-OCCUPN.                    
                                                                                
           WRITE  P13F500A-REC  FROM BP13F500-REC.                              
           ADD     1            TO   WS-P13F500A-WRITE.                         
                                                                                
       412-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       420-PRINT-REPORT.                                                        
      *-----------------*                                                       
                                                                                
           ADD            1             TO  WS-SNO                              
                                            WS-LINE-CNT.                        
                                                                                
           MOVE SPACES                  TO  L697-DETAIL-LINE.                   
           MOVE WS-SNO                  TO  L697-SNO.                           
           MOVE F500-NUM-SEQUENCE       TO  L697-SEQ-NO.                        
           MOVE F500-NUM-REGN           TO  L697-NUM-REGN.                      
           MOVE F500-NUM-JB-PARENT-REGN TO  L697-JB-PARENT-REGN.                
           MOVE F500-NUM-JB-REGN        TO  L697-JB-REGN.                       
           MOVE F500-CDE-ZONE           TO  L697-ZONE.                          
           MOVE F500-CDE-FLAT-TYPE      TO  L697-FT.                            
           MOVE F520-NUM-MTH-RESIDE     TO  L697-MTH-RESIDE.                    
           MOVE F500-NUM-MTH-RESIDE     TO  L697-JB-INDV.                       
           MOVE F500-NUM-5YR-OCCUPN     TO  L697-5-YEAR.                        
           MOVE F520-NUM-5YR-OCCUPN     TO  L697-5YR-LINK.                      
           MOVE F500-CDE-ALLOC-SCH      TO  L697-ALLOC-SCH.                     
           MOVE F500-NUM-JB-CNT         TO  L697-JB-CNT.                        
           IF WS-LINE-CNT > 55                                                  
              PERFORM 110-PRINT-REPORT-HEAD THRU 110-EXIT                       
           END-IF.                                                              
                                                                                
           WRITE BP13L697-REC FROM L697-DETAIL-LINE.                            
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       900-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           DISPLAY '              PROGRAM-ID :  BP13C679 '.                     
           DISPLAY '              ~~~~~~~~~~             '.                     
           DISPLAY '                                  '                         
                                       'CURRENT-DATE : ' WS-CURR-DATE.          
           DISPLAY ' '.                                                         
           DISPLAY '1. NO OF RECS READ FROM F520      : ' WS-F520-READ.         
           DISPLAY '2. NO OF RECS READ FROM F500      : ' WS-F500-READ.         
           DISPLAY '3. NO OF RECS MATCHED             : ' WS-MATCH.             
           DISPLAY '4. NO OF RECS WRITTEN TO P13F520A : '                       
                                                     WS-P13F520A-WRITE.         
           DISPLAY '5. NO OF RECS WRITTEN TO P13F500A : '                       
                                                     WS-P13F500A-WRITE.         
           DISPLAY '  '                                                         
                                                                                
           CLOSE BP13F520                                                       
                 BP13F500                                                       
                 BP13L697                                                       
                 P13F520A                                                       
                 P13F500A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------- END OF PROGRAM  ---------------------*              
                                                                                
                                                                                
