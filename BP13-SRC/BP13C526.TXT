       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C526.                                                 
      *AUTHOR.        LJL.                                                      
      *DATE-WRITTEN.  10/04/90.                                                 
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMENCEMENT                *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  TO PRINT THE SUCCESSFULL REQUEST ASSIGNED   *                
      *            REGISTRATION NO FOR VIEWING.                *                
      *                                                        *                
      *       INPUT FILES   :                                  *                
      *       1. BP13K520  :  BALLOT FILE                      *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *       1. BP13F520  :  BALLOT FILE (SEQ)                *                
      *                                                        *                
      *     LISTINGS:                                          *                
      *       1. CONTROL LIST FOR DISPLAY     - P13L526A       *                
      *                                                        *                
      * CHGE    BY   ON     DESCRIPTION                        *                
      * ------- ---- ------ -----------                        *                
      * N930012 YPH  050893 SOC PH8 - COBOL 2                  *                
      *BP130652 NOR  200299 EXPAND F520 RECL FROM 300 TO 500   *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3900.                                               
       OBJECT-COMPUTER. IBM-3900.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F520 ASSIGN  TO BP13F520.                                 
                                                                                
           SELECT BP13K060  ASSIGN TO BP13K060                                  
                  ACCESS MODE      IS DYNAMIC                                   
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K060-KEY-FLD                              
                  FILE STATUS      IS WS-K060-STATUS.                           
                                                                                
                                                                                
           SELECT P13L526A ASSIGN TO P13L526A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F520                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F520.                                                           
                                                                                
       FD  BP13K060                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 25  CHARACTERS                                       
           LABEL  RECORDS  ARE STANDARD.                                        
       COPY BP13K060.                                                           
                                                                                
       FD  P13L526A                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  L526A-PRT-REC                PIC X(132).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-K060-STATUS              PIC X(2)    VALUE SPACES.                
       01  WS-NUM-QUEUE                PIC 9(5)    VALUE ZEROES.                
       01  WS-F520-READ                PIC 9(8)    VALUE ZEROES.                
       01  WS-REWRITE                  PIC 9(8)    VALUE ZEROES.                
       01  WS-BYPASS                   PIC 9(8)    VALUE ZEROES.                
                                                                                
       01  WS-KEY.                                                              
           03  WS-CDE-SUCC-NT.                                                  
              10  WS-CDE-SUCC-NT1      PIC X(1)    VALUE SPACES.                
              10  WS-CDE-SUCC-NT2      PIC X(2)    VALUE SPACES.                
           03  WS-CDE-FLAT-TYPE        PIC X(2)    VALUE SPACES.                
                                                                                
       01  WS-K060-KEY.                                                         
           03  WS-SEQ                  PIC 9(2)    VALUE ZEROS.                 
           03  WS-KEY1.                                                         
              10  WS-CDE-ACCESS1      PIC X(2)    VALUE SPACES.                 
              10  WS-CDE-ACCESS2      PIC X(1)    VALUE SPACES.                 
                                                                                
       01  WS-PREV-KEY.                                                         
           03  WS-PREV-CDE-SUCC-NT     PIC X(3)    VALUE SPACES.                
           03  WS-PREV-CDE-FLAT-TYPE   PIC X(2)    VALUE SPACES.                
                                                                                
       01  WS-REPORT-CNT.                                                       
           03  WS-LINE-CNT             PIC 9(2)    VALUE 65.                    
           03  WS-PAGE-CNT             PIC 9(5)    VALUE ZEROES.                
                                                                                
       01  WS-EDITED-DATE.                                                      
           03  WS-DD                   PIC 99.                                  
           03  FILLER                  PIC X      VALUE '/'.                    
           03  WS-MM                   PIC 99.                                  
           03  FILLER                  PIC X      VALUE '/'.                    
           03  WS-YY                   PIC 9999.                                
                                                                                
       01  WS-CUR-DATE                 PIC 9(8).                                
                                                                                
      ****************************************************************          
      *    CONTROL-LISTING   FOR DISPLAY.                                       
      ****************************************************************          
       01  WS-L526A-HDG1.                                                       
           03  FILLER              PIC X(05)   VALUE SPACES.                    
           03  FILLER              PIC X(09)   VALUE 'BP13L526A'.               
           03  FILLER              PIC X(03)   VALUE SPACES.                    
           03  FILLER              PIC X(4)    VALUE 'HDB3'.                    
           03  FILLER              PIC X(40)   VALUE SPACES.                    
           03  FILLER              PIC X(7)    VALUE 'DATE : '.                 
           03  L526A-DATE          PIC X(8)    VALUE SPACES.                    
                                                                                
       01  WS-L526A-HDG2.                                                       
           03  FILLER              PIC X(25)   VALUE SPACES.                    
           03  FILLER              PIC X(11)   VALUE 'SALES UNIT '.             
           03  FILLER              PIC X(13)   VALUE '(APPLICATION '.           
           03  FILLER              PIC X(05)   VALUE 'TEAM)'.                   
           03  FILLER              PIC X(7)    VALUE SPACES.                    
           03  FILLER              PIC X(6)    VALUE 'PAGE: '.                  
           03  L526A-PAGENO        PIC ZZZZ9.                                   
           03  FILLER              PIC X(3)    VALUE SPACES.                    
                                                                                
       01  WS-L526A-HDG3.                                                       
           03  FILLER              PIC X(15)   VALUE SPACES.                    
           03  FILLER              PIC X(60)   VALUE                            
               'LIST OF SUCCESSFUL NEW REQUESTS IN THE BALLOTING OF '.          
           03  FILLER              PIC X(36)   VALUE SPACES.                    
                                                                                
       01  WS-L526A-HDG4.                                                       
           03  FILLER              PIC X(15)   VALUE SPACES.                    
           03  FILLER              PIC X(11)   VALUE 'NEW TOWN : '.             
           03  L526A-NEW-TOWN      PIC X(20)   VALUE SPACES.                    
                                                                                
       01  WS-L526A-DTL.                                                        
           03  FILLER              PIC X(11)   VALUE SPACES.                    
           03  L526A-NRICNO        PIC X(09)   VALUE SPACES.                    
           03  FILLER              PIC X(17)   VALUE SPACES.                    
           03  L526A-FLAT-TYPE     PIC X(2)    VALUE SPACES.                    
           03  FILLER              PIC X(15)   VALUE SPACES.                    
           03  L526A-QUEUE-POSN    PIC ZZZZ9   VALUE ZEROS.                     
                                                                                
       01  WS-L526A-HDG5.                                                       
           03  FILLER              PIC X(12)   VALUE SPACES.                    
           03  FILLER              PIC X(07)   VALUE                            
               'NRIC NO'.                                                       
           03  FILLER              PIC X(17)   VALUE SPACES.                    
           03  FILLER              PIC X(04)   VALUE 'FLAT'.                    
           03  FILLER              PIC X(13)   VALUE SPACES.                    
           03  FILLER              PIC X(08)   VALUE 'BALLOTED'.                
                                                                                
       01  WS-L526A-HDG6.                                                       
           03  FILLER              PIC X(36)   VALUE SPACES.                    
           03  FILLER              PIC X(04)   VALUE 'TYPE'.                    
           03  FILLER              PIC X(13)   VALUE SPACES.                    
           03  FILLER              PIC X(14)   VALUE 'QUEUE POSITION'.          
                                                                                
       01  WS-L526-TOT1.                                                        
           03  FILLER              PIC X(8)    VALUE SPACES.                    
           03  FILLER              PIC X(05)   VALUE SPACES.                    
           03  FILLER              PIC X(44)   VALUE                            
               'NO OF RECORDS READ FROM BP13F520 BALLOT FILE'.                  
           03  L526-READ           PIC ZZZZZZZ9.                                
           03  FILLER              PIC X(04)   VALUE SPACES.                    
                                                                                
       01  WS-L526-TOT2.                                                        
           03  FILLER              PIC X(8)    VALUE SPACES.                    
           03  FILLER              PIC X(05)   VALUE SPACES.                    
           03  FILLER              PIC X(44)   VALUE                            
               'NO OF RECORDS WITH SUCCESSFUL NT            '.                  
           03  L526-REWRITE        PIC ZZZZZZZ9.                                
           03  FILLER              PIC X(04)   VALUE SPACES.                    
                                                                                
       01  WS-L526-TOT3.                                                        
           03  FILLER              PIC X(8)    VALUE SPACES.                    
           03  FILLER              PIC X(05)   VALUE SPACES.                    
           03  FILLER              PIC X(44)   VALUE                            
               'NO OF RECORDS WITHOUT SUCC NT               '.                  
           03  L526-BYPASS         PIC ZZZZZZZ9.                                
           03  FILLER              PIC X(04)   VALUE SPACES.                    
                                                                                
      ************************                                                  
       PROCEDURE DIVISION.                                                      
      ************************                                                  
      *----------------*                                                        
       010-OPEN-ROUTINE.                                                        
      *----------------*                                                        
           OPEN INPUT BP13F520                                                  
                      BP13K060                                                  
               OUTPUT P13L526A.                                                 
                                                                                
           IF WS-K060-STATUS NOT = ZEROES AND                                   
              WS-K060-STATUS NOT = '97'                                         
              DISPLAY ' OPENING BP13K060 ERROR ' WS-K060-STATUS                 
              MOVE WS-K060-STATUS TO RETURN-CODE                                
              PERFORM 030-CLOSE-ROUTINE.                                        
                                                                                
      *-------------------*                                                     
       020-PROCESS-ROUTINE.                                                     
      *-------------------*                                                     
           ACCEPT WS-CUR-DATE FROM DATE YYYYMMDD.                               
           MOVE WS-CUR-DATE(1:4) TO WS-YY.                                      
           MOVE WS-CUR-DATE(5:2) TO WS-MM.                                      
           MOVE WS-CUR-DATE(7:2) TO WS-DD.                                      
                                                                                
           PERFORM 100-READ-F520                                                
              THRU 100-EXIT.                                                    
           PERFORM 200-PROCESS-INDATA                                           
              THRU 200-EXIT                                                     
             UNTIL WS-KEY = HIGH-VALUE.                                         
                                                                                
      *-----------------*                                                       
       030-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
           PERFORM 600-PRINT-TOTAL THRU 600-EXIT.                               
           CLOSE BP13F520                                                       
                 BP13K060                                                       
                 P13L526A.                                                      
                                                                                
           IF WS-K060-STATUS NOT = ZEROES AND                                   
              WS-K060-STATUS NOT = '97'                                         
              DISPLAY ' CLOSING BP13K520 ERROR ' WS-K060-STATUS.                
                                                                                
           STOP RUN.                                                            
                                                                                
       030-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       100-READ-F520.                                                           
      *-----------------*                                                       
           READ BP13F520 AT END MOVE HIGH-VALUE TO WS-KEY                       
                GO TO 100-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F520-READ.                                               
                                                                                
           IF F520-CDE-SUCC-NT-ZONE = SPACES                                    
              ADD 1 TO WS-BYPASS                                                
              GO TO 100-READ-F520                                               
           ELSE                                                                 
              MOVE F520-CDE-SUCC-NT-ZONE   TO WS-CDE-SUCC-NT                    
              MOVE F520-CDE-FLAT-TYPE TO WS-CDE-FLAT-TYPE.                      
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       200-PROCESS-INDATA.                                                      
      *-------------------*                                                     
           IF WS-KEY NOT = WS-PREV-KEY                                          
              MOVE 02                   TO WS-SEQ                               
              MOVE WS-CDE-SUCC-NT       TO WS-KEY1                              
              MOVE WS-K060-KEY          TO K060-KEY-FLD                         
                                                                                
              PERFORM 9000-READ-K060                                            
              THRU    9000-READ-K060-EXIT                                       
                                                                                
              MOVE K060-DESC            TO L526A-NEW-TOWN                       
              MOVE WS-CDE-FLAT-TYPE     TO L526A-FLAT-TYPE                      
                                                                                
              PERFORM 2000-REPORT-HDG THRU 2000-EXIT                            
                                                                                
              MOVE 1 TO WS-NUM-QUEUE                                            
              MOVE WS-KEY TO WS-PREV-KEY                                        
           ELSE                                                                 
              ADD 1 TO WS-NUM-QUEUE.                                            
                                                                                
                                                                                
           PERFORM 2100-REPORT-DTL THRU 2100-EXIT.                              
           PERFORM 100-READ-F520 THRU 100-EXIT.                                 
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-PRINT-TOTAL.                                                         
           MOVE WS-F520-READ    TO L526-READ.                                   
           MOVE WS-REWRITE      TO L526-REWRITE.                                
           MOVE WS-BYPASS       TO L526-BYPASS.                                 
                                                                                
           WRITE L526A-PRT-REC FROM    WS-L526A-HDG1  AFTER PAGE.               
           WRITE L526A-PRT-REC FROM    WS-L526A-HDG2  AFTER 1.                  
           WRITE L526A-PRT-REC FROM    WS-L526A-HDG3  AFTER 1.                  
           MOVE  SPACES               TO L526A-PRT-REC.                         
           WRITE L526A-PRT-REC AFTER 1.                                         
           WRITE L526A-PRT-REC FROM    WS-L526-TOT1  AFTER 2.                   
           WRITE L526A-PRT-REC FROM    WS-L526-TOT2  AFTER 2.                   
           WRITE L526A-PRT-REC FROM    WS-L526-TOT3  AFTER 2.                   
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************************************************************          
      *            PRINT HEADING FOR DETAIL LIST                     *          
      ****************************************************************          
       2000-REPORT-HDG.                                                         
                                                                                
           MOVE SPACES                TO L526A-PRT-REC.                         
           MOVE 1                     TO WS-LINE-CNT.                           
           ADD  1                     TO WS-PAGE-CNT.                           
           MOVE WS-PAGE-CNT           TO L526A-PAGENO.                          
           MOVE WS-EDITED-DATE        TO L526A-DATE.                            
                                                                                
           WRITE L526A-PRT-REC FROM   WS-L526A-HDG1 AFTER PAGE.                 
           WRITE L526A-PRT-REC FROM   WS-L526A-HDG2 AFTER 1.                    
           WRITE L526A-PRT-REC FROM   WS-L526A-HDG3 AFTER 1.                    
           WRITE L526A-PRT-REC FROM   WS-L526A-HDG4 AFTER 2.                    
           WRITE L526A-PRT-REC FROM   WS-L526A-HDG5 AFTER 2.                    
           WRITE L526A-PRT-REC FROM   WS-L526A-HDG6 AFTER 1.                    
           MOVE  SPACES               TO L526A-PRT-REC.                         
           WRITE L526A-PRT-REC AFTER 1.                                         
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2100-REPORT-DTL.                                                         
           IF WS-LINE-CNT > 40                                                  
              PERFORM 2000-REPORT-HDG THRU 2000-EXIT.                           
           ADD 1 TO WS-REWRITE.                                                 
                                                                                
           MOVE SPACES          TO L526A-PRT-REC.                               
           MOVE F520-CDE-FLAT-TYPE TO L526A-FLAT-TYPE.                          
           MOVE F520-NUM-NRIC1     TO L526A-NRICNO.                             
           MOVE F520-NUM-NT-FT-QUEUE  TO L526A-QUEUE-POSN.                      
           WRITE L526A-PRT-REC FROM WS-L526A-DTL AFTER 1.                       
           ADD 1 TO WS-LINE-CNT.                                                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-READ-K060.                                                          
           READ BP13K060.                                                       
           IF WS-K060-STATUS = ZEROES                                           
              MOVE K060-DESC TO         L526A-NEW-TOWN                          
           ELSE                                                                 
              DISPLAY 'INVALID NEW TOWN       ' F520-CDE-SUCC-NT-ZONE.          
                                                                                
       9000-READ-K060-EXIT.                                                     
            EXIT.                                                               
                                                                                
                                                                                
