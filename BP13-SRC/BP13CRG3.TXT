      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CRG3.                                                 
       AUTHOR.        MRR5                                                      
       DATE-WRITTEN.  18/01/2023.                                               
                                                                                
      *************************************************************             
      *            SYSTEM OF COMMITMENT (BP13)                    *             
      *===========================================================*             
      *  OBJECTIVE  : TO READ BLDG_FTR TABLE                      *             
      *  INPUT      : WS-LINK-BLDG-GL                             *             
      *  OUTPUT     : WS-LINK-TYPE-FTR                            *             
      *(OCCURS 100)   WS-LINK-NUM-FTR                             *             
      *               WS-LINK-NUM-SQNC                            *             
      *               WS-LINK-QTY-FTR                             *             
      *               WS-LINK-QTY-MSRMNT                          *             
      *  COMMAREA   : 5862                                        *             
      *************************************************************             
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHGE REQ #  DATE        AUTHOR  REMARKS                   *             
      * ----------  ----------  ------  ------------------------- *             
      * BP139436    18/01/2023  MRR5    NEW SUBROUTINE            *             
      *************************************************************             
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       DATA DIVISION.                                                           
      *-----------------------*                                                 
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND                 PIC S9(4) COMP VALUE +000.          
           05  WS-SQL-NOT-FOUND             PIC S9(4) COMP VALUE +100.          
                                                                                
       01  WS-SQLCODE                       PIC S9(4) COMP.                     
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CURSOR1-EOF               PIC X VALUE 'N'.                    
           05  WS-NUM-BLDG-GL               PIC X(5).                           
           05  WS-CTR                       PIC 9(3).                           
           05  WS-CURSOR-EOF                PIC X VALUE 'N'.                    
           05  WS-SQL-CODE                  PIC 9(4).                           
           05  WS-SQL-CODE-X REDEFINES WS-SQL-CODE PIC X(4).                    
                                                                                
           EXEC SQL INCLUDE P13VBLFT END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
      *-------------------------------------------------------------*           
       LINKAGE SECTION.                                                         
      *-------------------------------------------------------------*           
       01  WS-LINK-REC.                                                         
           05  WS-LINK-BLDG-GL        PIC X(05).                                
           05  WS-LINK-BLDGFTR-DTLS.                                            
               10 WS-LINK-DTLS        OCCURS 100 TIMES.                         
                  15  WS-LINK-TYPE-FTR     PIC X(15).                           
                  15  WS-LINK-NUM-FTR      PIC X(8).                            
                  15  WS-LINK-NUM-SQNC     PIC X(2).                            
                  15  WS-LINK-QTY-FTR      PIC S9(4) USAGE COMP.                
                  15  WS-LINK-QTY-MSRMNT   PIC S9(6)V9(2) USAGE COMP-3.         
                  15  WS-LINK-DTE-LASTUPD  PIC X(26).                           
           05  WS-LINK-CDE-SYSERR     PIC S9(3).                                
           05  WS-LINK-CDE-ERROR      PIC X(04).                                
           05  WS-LINK-ERR-MSG        PIC X(50).                                
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION USING WS-LINK-REC.                                    
      *------------------*                                                      
                                                                                
           EXEC SQL WHENEVER NOT FOUND                                          
                             CONTINUE END-EXEC.                                 
           EXEC SQL WHENEVER SQLERROR                                           
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
           EXEC SQL WHENEVER SQLWARNING                                         
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
                                                                                
      *-----------------------*                                                 
       0000-START-ROUTINE.                                                      
      *-----------------------*                                                 
           MOVE SPACE            TO WS-LINK-CDE-ERROR.                          
           MOVE ZEROES           TO WS-LINK-CDE-SYSERR.                         
                                                                                
           PERFORM 1000-READ-BLDG-FTR      THRU 1000-EXIT.                      
                                                                                
           GOBACK.                                                              
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       1000-READ-BLDG-FTR.                                                      
      *-----------------------*                                                 
           MOVE 'N' TO WS-CURSOR1-EOF.                                          
           PERFORM 1100-OPEN-CURSOR     THRU 1100-EXIT.                         
           PERFORM 1200-FETCH-CURSOR    THRU 1200-EXIT                          
                   UNTIL (SQLCODE = WS-SQL-NOT-FOUND)                           
                      OR (WS-CURSOR1-EOF) = 'Y' OR                              
                      WS-CTR > 100.                                             
           PERFORM 1300-CLOSE-CURSOR    THRU 1300-EXIT.                         
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       1100-OPEN-CURSOR.                                                        
      *================================================================*        
           INITIALIZE SQLCODE.                                                  
           MOVE ZEROES                TO WS-LINK-CDE-SYSERR.                    
           IF WS-LINK-BLDG-GL = SPACES OR LOW-VALUES                            
              MOVE 'E500'             TO WS-LINK-CDE-ERROR                      
              MOVE 'BLDG-GL IS BLANK' TO WS-LINK-ERR-MSG                        
              GO TO 9100-GOBACK                                                 
           ELSE                                                                 
              MOVE WS-LINK-BLDG-GL                                              
                           TO NUM-BLDNG-GL OF DCLBLDG-FTR                       
           END-IF.                                                              
                                                                                
      *-------------------------------------*                                   
      *  DECLARE CURSOR - BLDG-FTR          *                                   
      *-------------------------------------*                                   
           EXEC SQL                                                             
             DECLARE CURSOR1 CURSOR FOR                                         
               SELECT NUM_BLDNG_GL,                                             
                      NUM_TYPE_FTR,                                             
                      NUM_FTR,                                                  
                      NUM_SQNC,                                                 
                      QTY_FTR,                                                  
                      QTY_MSRMNT,                                               
                      DTE_LASTUPD                                               
               FROM                                                             
                 BLDG_FTR                                                       
               WHERE                                                            
                 NUM_BLDNG_GL = :WS-LINK-BLDG-GL                                
           END-EXEC.                                                            
                                                                                
      *----------------------------------*                                      
      *  OPEN CURSOR - BLDG_FTR          *                                      
      *----------------------------------*                                      
           EXEC SQL                                                             
             OPEN CURSOR1                                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 0                                                               
             CONTINUE                                                           
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO WS-LINK-CDE-ERROR                       
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO WS-LINK-CDE-ERROR(1:1)                  
             END-IF                                                             
             MOVE '(CRG3) OPEN CURSOR1 ERROR' TO WS-LINK-ERR-MSG                
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       1200-FETCH-CURSOR.                                                       
      *================================================================*        
                                                                                
           EXEC SQL                                                             
             FETCH CURSOR1                                                      
               INTO                                                             
                 :DCLBLDG-FTR.NUM-BLDNG-GL,                                     
                 :DCLBLDG-FTR.NUM-TYPE-FTR,                                     
                 :DCLBLDG-FTR.NUM-FTR,                                          
                 :DCLBLDG-FTR.NUM-SQNC,                                         
                 :DCLBLDG-FTR.QTY-FTR,                                          
                 :DCLBLDG-FTR.QTY-MSRMNT,                                       
                 :DCLBLDG-FTR.DTE-LASTUPD                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-FOUND                                                    
             ADD 1 TO WS-CTR                                                    
             MOVE NUM-TYPE-FTR OF DCLBLDG-FTR                                   
                                     TO WS-LINK-TYPE-FTR(WS-CTR)                
             MOVE NUM-FTR OF DCLBLDG-FTR                                        
                                     TO WS-LINK-NUM-FTR(WS-CTR)                 
             MOVE NUM-SQNC OF DCLBLDG-FTR                                       
                                     TO WS-LINK-NUM-SQNC(WS-CTR)                
             MOVE QTY-FTR OF DCLBLDG-FTR                                        
                                     TO WS-LINK-QTY-FTR(WS-CTR)                 
             MOVE QTY-MSRMNT OF DCLBLDG-FTR                                     
                                     TO WS-LINK-QTY-MSRMNT(WS-CTR)              
           WHEN WS-SQL-NOT-FOUND                                                
             MOVE 'Y'                TO WS-CURSOR1-EOF                          
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO WS-LINK-CDE-ERROR                       
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO WS-LINK-CDE-ERROR(1:1)                  
             END-IF                                                             
             MOVE '(CRG3) FETCH CURSOR ERROR' TO WS-LINK-ERR-MSG                
             GO TO 9000-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       1300-CLOSE-CURSOR.                                                       
      *---------------------------------------------------------------*         
           INITIALIZE SQLCODE.                                                  
           MOVE ZEROES TO WS-LINK-CDE-SYSERR.                                   
                                                                                
      *-----------------------------------*                                     
      * CLOSE CURSOR - GRANT ASSESSMENT   *                                     
      *-----------------------------------*                                     
           EXEC SQL CLOSE CURSOR1                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN 0                                                            
                   CONTINUE                                                     
              WHEN OTHER                                                        
                MOVE SQLCODE TO WS-SQL-CODE WS-LINK-CDE-ERROR                   
                MOVE WS-SQL-CODE-X      TO WS-LINK-CDE-ERROR                    
                IF SQLCODE < ZEROES                                             
                   MOVE '-'             TO WS-LINK-CDE-ERROR(1:1)               
                END-IF                                                          
                MOVE '(CRG3) CLOSE CURSOR ERROR' TO WS-LINK-ERR-MSG             
                GO TO 9000-ABORT-PROCESS                                        
           END-EVALUATE.                                                        
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       9000-ABORT-PROCESS.                                                      
      *-----------------------*                                                 
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE       TO WS-LINK-CDE-SYSERR.                            
           MOVE SQLCODE       TO RETURN-CODE.                                   
           DISPLAY 'CRG3 - SYSERR = ' WS-LINK-CDE-SYSERR.                       
           GOBACK.                                                              
                                                                                
      *-----------------------*                                                 
       9100-GOBACK.                                                             
      *-----------------------*                                                 
                                                                                
           GOBACK.                                                              
                                                                                
       9100-EXIT.                                                               
            EXIT.                                                               
