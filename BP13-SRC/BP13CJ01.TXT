       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CJ01.                                                  
       AUTHOR.       ALJOHN MONCADA.                                            
       DATE-WRITTEN. 28/08/2018.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES :                                                 *         
      *     - TO CREATE RAW-DATA OF INTER-RACIAL AND RE-OFFER LETTER  *         
      *       USING IOMS; FH10, FB10 AND FTP FILES.                   *         
      *                                                               *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F730 I     SEQUENTIAL 500                                 *         
      * BP13K022 I     RANDOM     100  K022-KEY-FLD                   *         
      * BP13K024 I     RANDOM     100  K024-KEY-FLD                   *         
      * BP13K730 I     RANDOM     500  K730-KEY-NEW                   *         
      * BP13K757 I     RANDOM     3000 K757-KEY-FLD                   *         
      * BP13K813 I     RANDOM     1000 K813-KEY-FLD                   *         
      * BP13K825 I     RANDOM     200  K825-KEY-FLD                   *         
      * BP13K830 I     RANDOM     250  K830-KEY-FLD                   *         
      * BP13FB10 O     SEQUENTIAL 1200               IOMS             *         
      * BP13FH10 O     SEQUENTIAL 72                 IOMS             *         
      * BP13FTP  O     SEQUENTIAL 72                 IOMS             *         
      *                                                               *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHG-REQ#  DD/MM/YY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP137439  28/08/18  AM25  NEW PROGRAM                         *         
      * BP137662  24/01/19  AM25  UPDATE TXT-EXERCISE FIELD           *         
      * BP138104  04/02/20  AM25  ADD NEW JOB BP13A650                *         
      * BP139057  28/01/22  AM25  CHGE RACE 'OTHERS' TO 'OTHER RACES' *         
      * BP139280  31102023  EAA2  EXPAND BP13K757 FROM 3000 TO 4000   *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN      TO BP13F730.                             
                                                                                
           SELECT BP13K022 ASSIGN      TO BP13K022                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K022-KEY-FLD                                     
              ALTERNATE KEY IS K022-NUM-REGN                                    
              FILE STATUS   IS WS-K022-STATUS.                                  
                                                                                
           SELECT BP13K024 ASSIGN      TO BP13K024                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K024-KEY-FLD                                     
              FILE STATUS   IS WS-K024-STATUS.                                  
                                                                                
           SELECT BP13K730 ASSIGN      TO BP13K730                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K730-KEY-NEW                                     
              FILE STATUS   IS WS-K730-STATUS.                                  
                                                                                
           SELECT BP13K757 ASSIGN      TO BP13K757                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K757-KEY-FLD                                     
              FILE STATUS   IS WS-K757-STATUS.                                  
                                                                                
           SELECT BP13K813 ASSIGN      TO BP13K813                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K813-KEY-FLD                                     
              FILE STATUS   IS WS-K813-STATUS.                                  
                                                                                
           SELECT BP13K825 ASSIGN      TO BP13K825                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K825-KEY-FLD                                     
              FILE STATUS   IS WS-K825-STATUS.                                  
                                                                                
           SELECT BP13K830 ASSIGN      TO BP13K830                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K830-KEY-FLD                                     
              ALTERNATE KEY IS K830-NUM-REGN                                    
              FILE STATUS   IS WS-K830-STATUS.                                  
                                                                                
           SELECT BP13FH10 ASSIGN      TO BP13FH10.                             
           SELECT BP13FB10 ASSIGN      TO BP13FB10.                             
           SELECT BP13FTP  ASSIGN      TO BP13FTP.                              
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F730                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F730.                                                           
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K022.                                                           
                                                                                
       FD  BP13K024                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K024.                                                           
                                                                                
       FD  BP13K730                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K730.                                                           
                                                                                
       FD  BP13K757                                                             
           RECORD CONTAINS 4000 CHARACTERS.                                     
       COPY BP13K757.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD  BP13K825                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K825.                                                           
                                                                                
       FD  BP13K830                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13K830.                                                           
                                                                                
       FD  BP13FH10                                                             
           RECORD CONTAINS 72 CHARACTERS                                        
           RECORDING MODE IS F.                                                 
       01  BP13FH10-REC                PIC X(72).                               
                                                                                
       FD  BP13FB10                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 1200 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  BP13FB10-REC                PIC X(1200).                             
                                                                                
       FD  BP13FTP                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 72 CHARACTERS                                        
           RECORDING MODE IS F.                                                 
       01  BP13FTP-REC                 PIC X(72).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       COPY P13FH10A.                                                           
       COPY P13PJ01A.                                                           
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K022-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K024-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K730-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K757-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K813-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K825-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K830-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-SNO                  PIC 9(7)        VALUE ZEROES.            
           05  WS-NME-OCCP             PIC X(66)       VALUE SPACES.            
           05  WS-MTH-IN               PIC X(2)        VALUE SPACES.            
           05  WS-MTH-OUT              PIC X(3)        VALUE SPACES.            
           05  WS-DTE-BAL-MTH-9        PIC X(9)        VALUE SPACES.            
           05  WS-DTE-PRINT.                                                    
               10  WS-DTE-PRT-DD       PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X           VALUE '/'.               
               10  WS-DTE-PRT-MM       PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X           VALUE '/'.               
               10  WS-DTE-PRT-CCYY     PIC X(4)        VALUE SPACES.            
           05  WS-DTE-BALLOT.                                                   
               10  WS-DTE-BAL-MTH-3    PIC X(3)        VALUE SPACES.            
               10  FILLER              PIC X           VALUE SPACES.            
               10  WS-DTE-BAL-CCYY     PIC X(4)        VALUE SPACES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K022-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K024-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K730-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K757-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K813-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K825-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K830-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-FH10-WRITE           PIC 9(5)        VALUE ZEROES.            
           05  WS-FB10-WRITE           PIC 9(5)        VALUE ZEROES.            
           05  WS-FTP-WRITE            PIC 9(5)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F730-EOF             PIC X           VALUE 'N'.               
                                                                                
       01  WS-IOMS-VARIABLES.                                                   
           05  WS-UPD-TMESTAMP.                                                 
               10  WS-UPD-DATE         PIC X(8)        VALUE SPACES.            
               10  WS-UPD-TME          PIC X(8)        VALUE SPACES.            
           05  WS-FTP-DEST-REC.                                                 
               10  WS-FTP-JOBNAME      PIC X(8)        VALUE SPACES.            
               10  WS-FTP-UNDERSCORE1  PIC X           VALUE SPACES.            
               10  WS-FTP-DESC         PIC X(6)        VALUE SPACES.            
               10  WS-FTP-UNDERSCORE2  PIC X           VALUE SPACES.            
               10  WS-FTP-TMESTAMP     PIC X(12)       VALUE SPACES.            
           05  WS-P13PJ01A-WRITE       PIC 9(7)        VALUE ZEROES.            
           05  WS-PARM.                                                         
               10  WS-PARM-JOBNAME     PIC X(8)        VALUE SPACES.            
               10  WS-PARM-B10-SOURCE  PIC X(70)       VALUE SPACES.            
               10  WS-PARM-H10-SOURCE  PIC X(70)       VALUE SPACES.            
                                                                                
       01  WS-MONTHS-IN-DIGITS-AND-WORDS.                                       
           05  FILLER                  PIC X(12)       VALUE                    
               '01 JANUARY  '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '02 FEBRUARY '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '03 MARCH    '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '04 APRIL    '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '05 MAY      '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '06 JUNE     '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '07 JULY     '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '08 AUGUST   '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '09 SEPTEMBER'.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '10 OCTOBER  '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '11 NOVEMBER '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '12 DECEMBER '.                                                  
       01  FILLER REDEFINES WS-MONTHS-IN-DIGITS-AND-WORDS.                      
           05  WS-MONTH-TABLE OCCURS 12 INDEXED BY WS-MTH-IND.                  
               10  WS-MONTH-IN-DIGITS  PIC X(02).                               
               10  FILLER              PIC X(01).                               
               10  WS-MONTH-IN-WORDS   PIC X(09).                               
                                                                                
       01  WS-BP13FTP-REC.                                                      
           05  WS-FTP-LINE1.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X(12)       VALUE                    
                   'CD /DATAFILE'.                                              
               10  FILLER              PIC X(58)       VALUE SPACES.            
           05  WS-FTP-LINE2.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X(12)       VALUE                    
                   'DIR         '.                                              
               10  FILLER              PIC X(58)       VALUE SPACES.            
           05  WS-FTP-LINE3.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X(12)       VALUE                    
                   'ASCII       '.                                              
               10  FILLER              PIC X(58)       VALUE SPACES.            
           05  WS-FTP-LINE4.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  WS-FTP-SOURCE       PIC X(70)       VALUE SPACES.            
           05  WS-FTP-LINE5.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X(12)       VALUE                    
                   'DIR         '.                                              
               10  FILLER              PIC X(66)       VALUE SPACES.            
           05  WS-FTP-LINE6.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X(12)       VALUE                    
                   'QUIT        '.                                              
               10  FILLER              PIC X(66)       VALUE SPACES.            
                                                                                
       01  WS-LINK-AV02C015.                                                    
           05  AV02C015-IN-POSTCODE    PIC X(6)        VALUE SPACES.            
           05  AV02C015-OUT-POSTCODE-CHKSUM                                     
                                       PIC X(9)        VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           ACCEPT WS-PARM-JOBNAME.                                              
           ACCEPT WS-PARM-B10-SOURCE.                                           
           ACCEPT WS-PARM-H10-SOURCE.                                           
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F730    THRU 2000-EXIT.                        
           PERFORM 3000-READ-BP13K730    THRU 3000-EXIT                         
              UNTIL WS-F730-EOF = 'Y'.                                          
           PERFORM 8500-CREATE-FIOMS-REC THRU 8500-EXIT.                        
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BP13K730                                                 
                       BP13K757                                                 
                       BP13K813                                                 
                       BP13K825                                                 
                       BP13K830                                                 
                OUTPUT BP13FH10                                                 
                       BP13FB10                                                 
                       BP13FTP.                                                 
                                                                                
           IF (WS-K022-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K022 FAIL STATUS=' WS-K022-STATUS               
              MOVE WS-K022-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K024-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K024 FAIL STATUS=' WS-K024-STATUS               
              MOVE WS-K024-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K730-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K730 FAIL STATUS=' WS-K730-STATUS               
              MOVE WS-K730-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K757-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K757 FAIL STATUS=' WS-K757-STATUS               
              MOVE WS-K757-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K813-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K813 FAIL STATUS=' WS-K813-STATUS               
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K825-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K825 FAIL STATUS=' WS-K825-STATUS               
              MOVE WS-K825-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K830-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K830 FAIL STATUS=' WS-K830-STATUS               
              MOVE WS-K830-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-UPD-TMESTAMP.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F730.                                                      
      *****************************************************************         
                                                                                
           READ BP13F730                                                        
              AT END                                                            
                 MOVE 'Y'              TO WS-F730-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F730-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-BP13K730.                                                      
      *****************************************************************         
                                                                                
           MOVE F730-NUM-REGN          TO K730-NUM-REGN.                        
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K730-READ                          
                                                                                
                 PERFORM 3100-READ-BP13K757 THRU 3100-EXIT                      
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K730: ' F730-NUM-REGN            
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K730 FAIL STATUS=' WS-K730-STATUS            
                 MOVE WS-K730-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F730           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-READ-BP13K757.                                                      
      *****************************************************************         
                                                                                
           MOVE K730-NUM-REGN          TO K757-NUM-REGN.                        
                                                                                
           READ BP13K757.                                                       
                                                                                
           EVALUATE WS-K757-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K757-READ                          
                                                                                
                 IF (K757-NUM-APPL-RACE NOT = SPACES AND LOW-VALUES)            
                    PERFORM 4000-WRITE-BP13FB10 THRU 4000-EXIT                  
                 END-IF                                                         
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K757: ' K730-NUM-REGN            
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K757 FAIL STATUS=' WS-K757-STATUS            
                 MOVE WS-K757-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-WRITE-BP13FB10.                                                     
      *****************************************************************         
                                                                                
           MOVE SPACES                 TO WS-P13PJ01A-B10-REC.                  
           INITIALIZE WS-P13PJ01A-B10-REC.                                      
                                                                                
           MOVE '_'                    TO WS-PJ01A-UNDERSCORE1                  
                                          WS-PJ01A-UNDERSCORE2                  
                                          WS-PJ01A-UNDERSCORE3.                 
                                                                                
           MOVE 'B10'                  TO WS-PJ01A-RECORD-TYPE.                 
           MOVE WS-PARM-JOBNAME        TO WS-PJ01A-JOBNAME.                     
           MOVE 'CFMLTR'               TO WS-PJ01A-DESC.                        
           MOVE WS-UPD-TMESTAMP(1:12)  TO WS-PJ01A-TMESTAMP.                    
                                                                                
           ADD 1                       TO WS-SNO.                               
           MOVE WS-SNO                 TO WS-PJ01A-NUM-SNO.                     
                                                                                
           MOVE K730-NUM-REGN          TO WS-PJ01A-NUM-REGN                     
                                          WS-PJ01A-OUR-REF.                     
           MOVE SPACES                 TO WS-PJ01A-NUM-TX-TYPE.                 
                                                                                
           IF WS-PARM-JOBNAME = 'BP13A637' OR 'BP13A650'                        
              MOVE 'BP130Y'            TO WS-PJ01A-NUM-FORMID                   
           ELSE                                                                 
              MOVE 'BP130Z'            TO WS-PJ01A-NUM-FORMID                   
           END-IF.                                                              
                                                                                
           MOVE WS-UPD-DATE(7:2)       TO WS-DTE-PRT-DD.                        
           MOVE WS-UPD-DATE(5:2)       TO WS-DTE-PRT-MM.                        
           MOVE WS-UPD-DATE(1:4)       TO WS-DTE-PRT-CCYY.                      
           MOVE WS-DTE-PRINT           TO WS-PJ01A-DTE-PRINT.                   
                                                                                
           PERFORM 4100-GET-NME-APPLT THRU 4100-EXIT.                           
           PERFORM 4200-READ-BP13K022 THRU 4200-EXIT.                           
           PERFORM 4300-READ-BP13K830 THRU 4300-EXIT.                           
           PERFORM 4400-READ-BP13K813 THRU 4400-EXIT.                           
                                                                                
           EVALUATE K730-CDE-CATEGORY                                           
              WHEN '1'                                                          
                 MOVE 'MALAY'          TO WS-PJ01A-TXT-APPLIED-ETHNIC           
              WHEN '2'                                                          
                 MOVE 'CHINESE'        TO WS-PJ01A-TXT-APPLIED-ETHNIC           
              WHEN '3'                                                          
                 MOVE 'INDIAN'         TO WS-PJ01A-TXT-APPLIED-ETHNIC           
              WHEN '4'                                                          
                 MOVE 'OTHER RACES'    TO WS-PJ01A-TXT-APPLIED-ETHNIC           
              WHEN OTHER                                                        
                 MOVE SPACES           TO WS-PJ01A-TXT-APPLIED-ETHNIC           
           END-EVALUATE.                                                        
                                                                                
           WRITE BP13FB10-REC FROM WS-P13PJ01A-B10-REC.                         
           ADD 1                       TO WS-P13PJ01A-WRITE                     
                                          WS-FB10-WRITE.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4100-GET-NME-APPLT.                                                      
      *****************************************************************         
                                                                                
           MOVE K730-NUM-REGN          TO K825-NUM-REGN.                        
                                                                                
           MOVE K730-NUM-NRIC1         TO K825-NUM-NRIC.                        
           PERFORM 4110-READ-BP13K825    THRU 4110-EXIT.                        
           MOVE WS-NME-OCCP            TO WS-PJ01A-NME-APPLT1.                  
                                                                                
           IF (K730-NUM-NRIC2(2:1) = '#' OR SPACES OR LOW-VALUES)               
              MOVE SPACES              TO WS-PJ01A-NME-APPLT2                   
           ELSE                                                                 
              MOVE K730-NUM-NRIC2      TO K825-NUM-NRIC                         
              PERFORM 4110-READ-BP13K825 THRU 4110-EXIT                         
              MOVE WS-NME-OCCP         TO WS-PJ01A-NME-APPLT2                   
           END-IF.                                                              
                                                                                
           IF (K730-NUM-NRIC3(2:1) = '#' OR SPACES OR LOW-VALUES)               
              MOVE SPACES              TO WS-PJ01A-NME-APPLT3                   
           ELSE                                                                 
              MOVE K730-NUM-NRIC3      TO K825-NUM-NRIC                         
              PERFORM 4110-READ-BP13K825 THRU 4110-EXIT                         
              MOVE WS-NME-OCCP         TO WS-PJ01A-NME-APPLT3                   
           END-IF.                                                              
                                                                                
           IF (K730-NUM-NRIC4(2:1) = '#' OR SPACES OR LOW-VALUES)               
              MOVE SPACES              TO WS-PJ01A-NME-APPLT4                   
           ELSE                                                                 
              MOVE K730-NUM-NRIC4      TO K825-NUM-NRIC                         
              PERFORM 4110-READ-BP13K825 THRU 4110-EXIT                         
              MOVE WS-NME-OCCP         TO WS-PJ01A-NME-APPLT4                   
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4110-READ-BP13K825.                                                      
      *****************************************************************         
                                                                                
           MOVE SPACES                 TO WS-NME-OCCP.                          
                                                                                
           READ BP13K825.                                                       
                                                                                
           EVALUATE WS-K825-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K825-READ                          
                                                                                
                 MOVE K825-NME-OCCP    TO WS-NME-OCCP                           
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K825: ' K825-KEY-FLD             
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K825 FAIL STATUS=' WS-K825-STATUS            
                 MOVE WS-K825-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4200-READ-BP13K022.                                                      
      *****************************************************************         
                                                                                
           MOVE K730-NUM-REGN          TO K022-NUM-REGN.                        
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1                 TO WS-K022-READ                          
                                                                                
                 PERFORM 4250-READ-BP13K024 THRU 4250-EXIT                      
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K022: ' K730-NUM-REGN            
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K022 FAIL STATUS=' WS-K022-STATUS            
                 MOVE WS-K022-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4250-READ-BP13K024.                                                      
      *****************************************************************         
                                                                                
           MOVE K022-NUM-OIC           TO K024-NUM-OIC.                         
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE WS-K024-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K024-READ                          
                                                                                
                 MOVE K024-NME-OIC     TO WS-PJ01A-NME-OIC                      
                 MOVE SPACES           TO WS-PJ01A-NME-OIC-DESIGNATION          
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K024: '                          
                    'REGN=' K730-NUM-REGN ',KEY=' K022-NUM-OIC                  
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K024 FAIL STATUS=' WS-K024-STATUS            
                 MOVE WS-K024-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4300-READ-BP13K830.                                                      
      *****************************************************************         
                                                                                
           MOVE K730-NUM-REGN          TO K830-NUM-REGN.                        
                                                                                
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K830-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1                 TO WS-K830-READ                          
                                                                                
                 PERFORM 4350-GET-ADDRESS THRU 4350-EXIT                        
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K830: ' K730-NUM-REGN            
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K830 FAIL STATUS=' WS-K830-STATUS            
                 MOVE WS-K830-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4350-GET-ADDRESS.                                                        
      *****************************************************************         
                                                                                
           IF ((K830-NUM-MAIL-ADDR = SPACES OR LOW-VALUES)                      
            OR (K830-NME-MAIL-STR-BLDG(1:47) = SPACES OR LOW-VALUES))           
              MOVE K830-NUM-BLK        TO WS-PJ01A-BLK                          
                                                                                
              IF ((K830-NUM-LVL-NUM = SPACES OR LOW-VALUES)                     
              AND (K830-NUM-HSEMAIN = SPACES OR LOW-VALUES))                    
                 MOVE SPACES           TO WS-PJ01A-HSE-NO                       
              ELSE                                                              
                 MOVE K830-NUM-FLAT    TO WS-PJ01A-HSE-NO                       
              END-IF                                                            
                                                                                
              MOVE K830-NME-STREET-BLDG(1:47)                                   
                                       TO WS-PJ01A-STR-BLDG                     
                                                                                
              IF (K830-NUM-POSTAL = SPACES OR LOW-VALUES OR ZEROES)             
                 MOVE SPACES           TO WS-PJ01A-POSTAL                       
              ELSE                                                              
                 STRING 'SINGAPORE ' K830-NUM-POSTAL                            
                    DELIMITED BY SIZE INTO WS-PJ01A-POSTAL                      
              END-IF                                                            
                                                                                
              MOVE SPACES              TO WS-LINK-AV02C015                      
              MOVE K830-NUM-POSTAL     TO AV02C015-IN-POSTCODE                  
              CALL 'AV02C015' USING AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM                
              MOVE AV02C015-OUT-POSTCODE-CHKSUM                                 
                                       TO WS-PJ01A-BAR-CODE                     
           ELSE                                                                 
              MOVE K830-NUM-MAIL-BLK   TO WS-PJ01A-BLK                          
                                                                                
              IF ((K830-NUM-MAIL-LVL-NUM = SPACES OR LOW-VALUES)                
              AND (K830-NUM-MAIL-HSEMAIN = SPACES OR LOW-VALUES))               
                 MOVE SPACES           TO WS-PJ01A-HSE-NO                       
              ELSE                                                              
                 MOVE K830-NUM-MAIL-FLAT                                        
                                       TO WS-PJ01A-HSE-NO                       
              END-IF                                                            
                                                                                
              MOVE K830-NME-MAIL-STR-BLDG(1:47)                                 
                                       TO WS-PJ01A-STR-BLDG                     
                                                                                
              IF (K830-NUM-MAIL-POSTAL = SPACES                                 
               OR LOW-VALUES OR ZEROES)                                         
                 MOVE SPACES           TO WS-PJ01A-POSTAL                       
              ELSE                                                              
                 STRING 'SINGAPORE ' K830-NUM-MAIL-POSTAL                       
                    DELIMITED BY SIZE INTO WS-PJ01A-POSTAL                      
              END-IF                                                            
                                                                                
              MOVE SPACES              TO WS-LINK-AV02C015                      
              MOVE K830-NUM-MAIL-POSTAL                                         
                                       TO AV02C015-IN-POSTCODE                  
              CALL 'AV02C015' USING AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM                
              MOVE AV02C015-OUT-POSTCODE-CHKSUM                                 
                                       TO WS-PJ01A-BAR-CODE                     
           END-IF.                                                              
                                                                                
       4350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4400-READ-BP13K813.                                                      
      *****************************************************************         
                                                                                
           MOVE K730-CDE-NT1           TO K813-NUM-ZONE.                        
           MOVE K730-CDE-FLAT-TYPE     TO K813-NUM-FLAT-TYPE.                   
           MOVE K730-DTE-BALLOT        TO K813-DTE-BALLOT.                      
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K813-READ                          
                                                                                
                 PERFORM 4450-MOVE-K813   THRU 4450-EXIT                        
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K813: '                          
                    'REGN=' K730-NUM-REGN ',KEY=' K813-KEY-FLD                  
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K813 FAIL STATUS=' WS-K813-STATUS            
                 MOVE WS-K813-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4450-MOVE-K813.                                                          
      *****************************************************************         
                                                                                
           MOVE K813-DTE-BALLOT(1:4)   TO WS-DTE-BAL-CCYY.                      
           MOVE K813-DTE-BALLOT(5:2)   TO WS-MTH-IN.                            
           PERFORM 5000-EVALUATE-MONTH THRU 5000-EXIT.                          
           MOVE WS-MTH-OUT             TO WS-DTE-BAL-MTH-3.                     
           MOVE WS-DTE-BALLOT          TO WS-PJ01A-TXT-BALLOT-DATE.             
                                                                                
           PERFORM 5100-SEARCH-MONTH THRU 5100-EXIT.                            
                                                                                
           IF WS-PARM-JOBNAME = 'BP13A637' OR 'BP13A650'                        
              IF K813-NUM-SALES-MODE = 'BTO'                                    
                 STRING 'FLAT APPLICATION IN ' DELIMITED BY SIZE                
                        WS-DTE-BAL-MTH-9 DELIMITED BY SPACES                    
                       ' ' WS-DTE-BAL-CCYY ' '                                  
                       'BUILD-TO-ORDER (BTO) EXERCISE'                          
                    DELIMITED BY SIZE INTO WS-PJ01A-TXT-EXERCISE                
              ELSE                                                              
                 STRING 'FLAT APPLICATION IN ' DELIMITED BY SIZE                
                        WS-DTE-BAL-MTH-9 DELIMITED BY SPACES                    
                       ' ' WS-DTE-BAL-CCYY ' '                                  
                       'SALE OF BALANCE FLATS EXERCISE'                         
                    DELIMITED BY SIZE INTO WS-PJ01A-TXT-EXERCISE                
              END-IF                                                            
                                                                                
              MOVE K813-NME-FLAT-TYPE-LTR                                       
                                       TO WS-PJ01A-TXT-APPLIED-FT               
              MOVE K813-NME-PROJECT-LTR                                         
                                       TO WS-PJ01A-TXT-APPLIED-ESTATE           
           ELSE                                                                 
              STRING 'FLAT APPLICATION IN ' DELIMITED BY SIZE                   
                     WS-DTE-BAL-MTH-9 DELIMITED BY SPACES                       
                     ' ' WS-DTE-BAL-CCYY ' '                                    
                     'RE-OFFER OF BALANCE FLATS EXERCISE'                       
                 DELIMITED BY SIZE INTO WS-PJ01A-TXT-EXERCISE                   
                                                                                
              MOVE SPACES              TO WS-PJ01A-TXT-APPLIED-FT               
                                          WS-PJ01A-TXT-APPLIED-ESTATE           
           END-IF.                                                              
                                                                                
           MOVE K813-TXT-RESULT-PERIOD-LTR                                      
                                       TO WS-PJ01A-TXT-RESULT-PERIOD.           
                                                                                
       4450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-EVALUATE-MONTH.                                                     
      *****************************************************************         
                                                                                
           EVALUATE WS-MTH-IN                                                   
              WHEN '01'                                                         
                 MOVE 'JAN'            TO WS-MTH-OUT                            
              WHEN '02'                                                         
                 MOVE 'FEB'            TO WS-MTH-OUT                            
              WHEN '03'                                                         
                 MOVE 'MAR'            TO WS-MTH-OUT                            
              WHEN '04'                                                         
                 MOVE 'APR'            TO WS-MTH-OUT                            
              WHEN '05'                                                         
                 MOVE 'MAY'            TO WS-MTH-OUT                            
              WHEN '06'                                                         
                 MOVE 'JUN'            TO WS-MTH-OUT                            
              WHEN '07'                                                         
                 MOVE 'JUL'            TO WS-MTH-OUT                            
              WHEN '08'                                                         
                 MOVE 'AUG'            TO WS-MTH-OUT                            
              WHEN '09'                                                         
                 MOVE 'SEP'            TO WS-MTH-OUT                            
              WHEN '10'                                                         
                 MOVE 'OCT'            TO WS-MTH-OUT                            
              WHEN '11'                                                         
                 MOVE 'NOV'            TO WS-MTH-OUT                            
              WHEN '12'                                                         
                 MOVE 'DEC'            TO WS-MTH-OUT                            
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5100-SEARCH-MONTH.                                                       
      ******************************************************************        
                                                                                
           SET WS-MTH-IND              TO 1.                                    
                                                                                
           SEARCH WS-MONTH-TABLE                                                
              AT END                                                            
                 MOVE SPACES           TO WS-DTE-BAL-MTH-9                      
              WHEN WS-MONTH-IN-DIGITS(WS-MTH-IND) = WS-MTH-IN                   
                 MOVE WS-MONTH-IN-WORDS(WS-MTH-IND)                             
                                       TO WS-DTE-BAL-MTH-9                      
           END-SEARCH.                                                          
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8500-CREATE-FIOMS-REC.                                                   
      *****************************************************************         
                                                                                
           MOVE SPACES                 TO WS-P13FH10A-REC.                      
           INITIALIZE WS-P13FH10A-REC.                                          
                                                                                
           MOVE 'H10'                  TO WS-FH10-RECORD-TYPE.                  
           MOVE WS-PARM-JOBNAME        TO WS-FH10-JOBNAME.                      
           MOVE WS-UPD-TMESTAMP(1:14)  TO WS-FH10-JOBDATE.                      
           MOVE 'BP13W001'             TO WS-FH10-WFD-ID.                       
           MOVE 'Y  '                  TO WS-FH10-OUTPUT-PRINT.                 
                                                                                
           IF WS-PARM-JOBNAME = 'BP13A637' OR 'BP13A650'                        
              MOVE 'EAALST3A'          TO WS-FH10-GROUP-ID                      
           ELSE                                                                 
              MOVE 'EAALST3B'          TO WS-FH10-GROUP-ID                      
           END-IF.                                                              
                                                                                
      * FEEDBACK FROM MOOI ON 18/5/2017                                         
      * IOMS NOT CATER FOR THE PRINTING OF 2 COPIES                             
      * USING PROGRAM TO TRIGGER TWICE INSTEAD                                  
      *    MOVE 2                      TO WS-FH10-COPIES.                       
      *                                                                         
           MOVE 1                      TO WS-FH10-COPIES.                       
           MOVE 'B'                    TO WS-FH10-COLOUR.                       
           MOVE 'A4W'                  TO WS-FH10-PAPER-TYPE.                   
           MOVE 'S'                    TO WS-FH10-PRINT-SETTING.                
           MOVE 'N'                    TO WS-FH10-AMIS.                         
           MOVE 'N'                    TO WS-FH10-OMR.                          
           MOVE 'N  '                  TO WS-FH10-OUTPUT-FILING.                
           MOVE 'N  '                  TO WS-FH10-OUTPUT-ONEINBOX.              
           MOVE 'N '                   TO WS-FH10-OUTPUT-SMS.                   
           MOVE 'N '                   TO WS-FH10-OUTPUT-EMAIL.                 
           MOVE 'N '                   TO WS-FH10-OUTPUT-FAX.                   
           WRITE BP13FH10-REC FROM WS-P13FH10A-REC.                             
           ADD 1                       TO WS-FH10-WRITE.                        
                                                                                
      * T10                                                                     
           MOVE SPACES                 TO WS-P13FT10A-REC.                      
           INITIALIZE WS-P13FT10A-REC.                                          
                                                                                
           MOVE 'T10'                  TO WS-FT10-RECORD-TYPE.                  
           MOVE WS-P13PJ01A-WRITE      TO WS-FT10-TOTAL-ACCOUNTS.               
           WRITE BP13FH10-REC FROM WS-P13FT10A-REC.                             
           ADD 1                       TO WS-FH10-WRITE.                        
                                                                                
      * FTP                                                                     
           MOVE '_'                    TO WS-FTP-UNDERSCORE1                    
                                          WS-FTP-UNDERSCORE2.                   
           MOVE WS-PARM-JOBNAME        TO WS-FTP-JOBNAME.                       
           MOVE 'CFMLTR'               TO WS-FTP-DESC.                          
           MOVE WS-UPD-TMESTAMP(1:12)  TO WS-FTP-TMESTAMP.                      
                                                                                
           WRITE BP13FTP-REC FROM WS-FTP-LINE1.                                 
           WRITE BP13FTP-REC FROM WS-FTP-LINE2.                                 
           WRITE BP13FTP-REC FROM WS-FTP-LINE3.                                 
           ADD 3                       TO WS-FTP-WRITE.                         
                                                                                
      * B10                                                                     
           MOVE SPACES                 TO  WS-FTP-SOURCE.                       
           STRING "PUT '"                                                       
                  WS-PARM-B10-SOURCE                                            
                  "' "                                                          
                  WS-FTP-DEST-REC ".TXT"                                        
              DELIMITED BY '  ' INTO WS-FTP-SOURCE                              
           END-STRING.                                                          
                                                                                
           WRITE BP13FTP-REC FROM WS-FTP-LINE4.                                 
           ADD 1                       TO WS-FTP-WRITE.                         
                                                                                
      * H10                                                                     
           MOVE SPACES                 TO WS-FTP-SOURCE.                        
           STRING "PUT '"                                                       
                  WS-PARM-H10-SOURCE                                            
                  "' "                                                          
                  WS-FTP-DEST-REC ".H10"                                        
              DELIMITED BY '  ' INTO WS-FTP-SOURCE                              
           END-STRING.                                                          
                                                                                
           WRITE BP13FTP-REC FROM WS-FTP-LINE4.                                 
           ADD 1                       TO WS-FTP-WRITE.                         
                                                                                
           WRITE BP13FTP-REC FROM WS-FTP-LINE5.                                 
           WRITE BP13FTP-REC FROM WS-FTP-LINE6.                                 
           ADD 2                       TO WS-FTP-WRITE.                         
                                                                                
       8500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F730                                                       
                 BP13K022                                                       
                 BP13K024                                                       
                 BP13K730                                                       
                 BP13K757                                                       
                 BP13K813                                                       
                 BP13K825                                                       
                 BP13K830                                                       
                 BP13FH10                                                       
                 BP13FB10                                                       
                 BP13FTP.                                                       
                                                                                
           IF WS-K022-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K022 FAIL STATUS=' WS-K022-STATUS              
              MOVE WS-K022-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K024 FAIL STATUS=' WS-K024-STATUS              
              MOVE WS-K024-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K730 FAIL STATUS=' WS-K730-STATUS              
              MOVE WS-K730-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K757-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K757 FAIL STATUS=' WS-K757-STATUS              
              MOVE WS-K757-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K813 FAIL STATUS=' WS-K813-STATUS              
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K825 FAIL STATUS=' WS-K825-STATUS              
              MOVE WS-K825-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K830 FAIL STATUS=' WS-K830-STATUS              
              MOVE WS-K830-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*****************************'.                             
           DISPLAY '* PROGRAM BP13CJ01          *'.                             
           DISPLAY '* CONTROL COUNTS            *'.                             
           DISPLAY '*---------------------------*'.                             
           DISPLAY '* REC READ F730     : ' WS-F730-READ ' *'.                  
           DISPLAY '* REC READ K022     : ' WS-K022-READ ' *'.                  
           DISPLAY '* REC READ K024     : ' WS-K024-READ ' *'.                  
           DISPLAY '* REC READ K730     : ' WS-K730-READ ' *'.                  
           DISPLAY '* REC READ K757     : ' WS-K757-READ ' *'.                  
           DISPLAY '* REC READ K813     : ' WS-K813-READ ' *'.                  
           DISPLAY '* REC READ K825     : ' WS-K825-READ ' *'.                  
           DISPLAY '* REC READ K830     : ' WS-K830-READ ' *'.                  
           DISPLAY '* REC WRITTEN FH10  : ' WS-FH10-WRITE ' *'.                 
           DISPLAY '* REC WRITTEN FB10  : ' WS-FB10-WRITE ' *'.                 
           DISPLAY '* REC WRITTEN FTP   : ' WS-FTP-WRITE ' *'.                  
           DISPLAY '*****************************'.                             
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
