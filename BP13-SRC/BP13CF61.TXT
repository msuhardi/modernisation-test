       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF61.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  02/06/2014.                                               
      * ====================================================== *                
      * SYSTEM OF COMMITMENT  (BP13)                           *                
      * ====================================================== *                
      *                                                        *                
      *  OBJECTIVE  : HOUSEKEEP BP13K017 - OMIT RECORDS OVER   *                
      *               6 MONTHS STARTING FROM CURRENT DATE      *                
      *                                                        *                
      *  INPUT      : BP13F017 - RECORDS TO BE PROCESSED       *                
      *               SY02F001 - SY02.F001.DATE                *                
      *  I-O        : BP13K017 - BP13.K017.FLATBLK             *                
      *                                                        *                
      * ====================================================== *                
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                  *                
      * --------  ----  --------  ---------------------------- *                
      * BP135340  SMR2  02062014  NEW PROGRAM                  *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F017 ASSIGN TO BP13F017.                                  
                                                                                
           SELECT BP13K017 ASSIGN TO BP13K017                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K017-KEY-FLD                               
                  FILE STATUS     IS WS-K017-STATUS.                            
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F017                                                            
            RECORDING MODE   IS F                                               
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F017.                                                           
                                                                                
       FD   BP13K017                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K017.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K017-STATUS               PIC X(02)  VALUE SPACES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F017-EOF                  PIC X(01)  VALUE 'N'.               
           05  WS-K017-FLAG-FOUND           PIC X(01)  VALUE 'N'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F017-READ                 PIC 9(06)  VALUE ZEROES.            
           05  WS-K017-FND                  PIC 9(06)  VALUE ZEROES.            
           05  WS-K017-NOTFND               PIC 9(06)  VALUE ZEROES.            
           05  WS-K017-DELETE               PIC 9(06)  VALUE ZEROES.            
           05  WS-K017-BYPASS               PIC 9(06)  VALUE ZEROES.            
                                                                                
       01  WS-DATE-TIME-VARS.                                                   
           05  WS-DTE-BALLOT.                                                   
               10  WS-BALLOT-YEAR           PIC 9(04)  VALUE ZEROES.            
               10  WS-BALLOT-MON            PIC 9(02)  VALUE ZEROES.            
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F017       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-BP13F017    THRU 3000-EXIT                      
             UNTIL WS-F017-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT BP13F017                                                  
                      SY02F001                                                  
                I-O   BP13K017.                                                 
                                                                                
           IF WS-K017-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K017 OPEN ERROR, STATUS IS ' WS-K017-STATUS          
              MOVE WS-K017-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-BP13F017.                                                      
      ****************************************************************          
                                                                                
           READ BP13F017                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F017-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F017-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-BP13F017.                                                   
      ****************************************************************          
                                                                                
           PERFORM 4000-READ-BP13K017       THRU 4000-EXIT.                     
                                                                                
           IF WS-K017-FLAG-FOUND = 'Y'                                          
              MOVE K017-DTE-BALLOT          TO WS-DTE-BALLOT                    
              ADD 6                         TO WS-BALLOT-MON                    
              IF WS-BALLOT-MON > 12                                             
                 ADD 1                      TO WS-BALLOT-YEAR                   
                 SUBTRACT 12                FROM WS-BALLOT-MON                  
              END-IF                                                            
              IF WS-DTE-BALLOT < F001-DTE-CURRENT(1:6)                          
                 PERFORM 5000-DELETE-BP13K017 THRU 5000-EXIT                    
              ELSE                                                              
                 ADD 1                   TO WS-K017-BYPASS                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F017       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-READ-BP13K017.                                                      
      ****************************************************************          
                                                                                
           MOVE 'N'                         TO WS-K017-FLAG-FOUND.              
           MOVE SPACES                      TO BP13K017-REC.                    
           MOVE BP13F017-REC(1:24)          TO K017-KEY-FLD.                    
                                                                                
           READ BP13K017.                                                       
                                                                                
           EVALUATE WS-K017-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K017-FND                      
              MOVE 'Y'                      TO WS-K017-FLAG-FOUND               
                                                                                
           WHEN 23                                                              
              ADD 1                         TO WS-K017-NOTFND                   
              MOVE 'N'                      TO WS-K017-FLAG-FOUND               
              DISPLAY 'RECORD NOT FOUND - ' K017-KEY-FLD                        
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K017. STATUS IS '                      
                      WS-K017-STATUS                                            
              MOVE WS-K017-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5000-DELETE-BP13K017.                                                    
      ****************************************************************          
                                                                                
           DELETE BP13K017.                                                     
                                                                                
           IF WS-K017-STATUS NOT = ZEROES                                       
              DISPLAY 'ERROR DELETING BP13K017. STATUS IS '                     
                      WS-K017-STATUS                                            
              MOVE WS-K017-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           ADD 1                            TO WS-K017-DELETE.                  
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*===========BP13CF61 CONTROL TOTALS============*'.          
           DISPLAY ' '.                                                         
           DISPLAY 'BP13F017 RECORDS READ        = ' WS-F017-READ.              
           DISPLAY 'NO OF REC FOUND BP13K017     = ' WS-K017-FND.               
           DISPLAY 'NO OF REC NOT FOUND BP13K017 = ' WS-K017-NOTFND.            
           DISPLAY 'NO OF REC DELETED BP13K017   = ' WS-K017-DELETE.            
           DISPLAY 'NO OF REC BYPASSED BP13K017  = ' WS-K017-BYPASS.            
           DISPLAY ' '.                                                         
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13F017                                                       
                 SY02F001                                                       
                 BP13K017.                                                      
                                                                                
           IF WS-K017-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K017 CLOSE ERROR, STATUS IS ' WS-K017-STATUS         
              MOVE WS-K017-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
