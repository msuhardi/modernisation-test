      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13CA53.                                                 
      *AUTHOR.        KAVYA VAKA.                                               
      *DATE-WRITTEN.  20/05/2020.                                               
      ******************************************************************        
      *                                                                *        
      * SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                     *        
      *                                                                *        
      * SYSTEM ID   :  BP13                                            *        
      *                                                                *        
      * OBJECTIVE   :  TO UPDATE TEMP LOAN CASES FOR K482/K486         *        
      *                                                                *        
      * INPUT FILES :  1.  BP13F482                                    *        
      *                2.  BP27K482                                    *        
      *                3.  BP27K486                                    *        
      * I-O FILES      4.  BP13K482                                    *        
      *                5.  BP13K486                                    *        
      *----------------------------------------------------------------*        
      * CHGE-NO   BY    DATE      DESCRIPTION                          *        
      * --------  ---- ---------- -----------                          *        
      * BP138261  KV5  20/05/2020 NEW PROGRAM                          *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F482  ASSIGN        TO BP13F482.                          
                                                                                
                                                                                
           SELECT BP13K482  ASSIGN        TO BP13K482                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K482-KEY-FLD                       
                            FILE STATUS   IS BP13K482-STATUS.                   
                                                                                
           SELECT BP13K486  ASSIGN        TO BP13K486                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K486-KEY-FLD                       
                            FILE STATUS   IS BP13K486-STATUS.                   
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F482                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 1000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F482.                                                           
                                                                                
       FD  BP13K482                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K482.                                                           
                                                                                
       FD  BP13K486                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K486.                                                           
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
       01  WS-FILE-VAR.                                                         
           05  WS-F482-EOF                   PIC X(01) VALUE SPACES.            
               88  F482-EOF                            VALUE 'Y'.               
           05  WS-BP13K482-FND               PIC X(01) VALUE SPACES.            
               88  BP13K482-FND                        VALUE 'Y'.               
           05  WS-BP13K486-FND               PIC X(01) VALUE SPACES.            
               88  BP13K486-FND                        VALUE 'Y'.               
           05  BP13K482-STATUS               PIC 9(02) VALUE ZEROES.            
           05  BP13K486-STATUS               PIC 9(02) VALUE ZEROES.            
           05  NO-F482-READ                  PIC 9(08) VALUE ZEROES.            
           05  NO-READ-NTFND-K482            PIC 9(08) VALUE ZEROES.            
           05  NO-READ-NTFND-K486            PIC 9(08) VALUE ZEROES.            
           05  NO-K482-REWRITE               PIC 9(08) VALUE ZEROES.            
           05  NO-K486-REWRITE               PIC 9(08) VALUE ZEROES.            
                                                                                
       01  WS-I                              PIC 9(2)   VALUE ZERO.             
       01  WS-K486-INFO.                                                        
           03  WS-NRIC-INFO             PIC X(100).                             
           03  FILLER REDEFINES WS-NRIC-INFO OCCURS 4 TIMES.                    
               05  WS-NRIC              PIC X(09).                              
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-F482         THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS           THRU 3000-EXIT                        
                   UNTIL F482-EOF.                                              
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13F482                                                 
                I-O    BP13K482                                                 
                       BP13K486.                                                
                                                                                
           IF BP13K482-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP13K482-STATUS ' BP13K482-STATUS        
               MOVE     BP13K482-STATUS    TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13K486-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP13K486-STATUS ' BP13K486-STATUS        
               MOVE     BP13K486-STATUS    TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-F482.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F482                                                        
             AT END                                                             
                MOVE 'Y'   TO WS-F482-EOF                                       
                GO         TO 2000-EXIT                                         
           END-READ.                                                            
                                                                                
           ADD 1           TO NO-F482-READ.                                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                      
              MOVE SPACES                    TO WS-NRIC(WS-I)                   
           END-PERFORM                                                          
                                                                                
           MOVE F482-KEY-FLD                 TO K482-KEY-FLD                    
           PERFORM 3010-READ-BP13K482        THRU 3010-EXIT                     
           IF BP13K482-FND                                                      
              PERFORM 3011-REWRITE-BP13K482 THRU 3011-EXIT                      
           END-IF                                                               
                                                                                
           MOVE F482-NUM-NRIC1-NEW           TO WS-NRIC(1).                     
           MOVE F482-NUM-NRIC2-NEW           TO WS-NRIC(2).                     
           MOVE F482-NUM-NRIC3-NEW           TO WS-NRIC(3).                     
           MOVE F482-NUM-NRIC4-NEW           TO WS-NRIC(4).                     
                                                                                
           MOVE 1                            TO WS-I                            
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                      
              IF WS-NRIC(WS-I) NOT = SPACES AND LOW-VALUES                      
                 MOVE WS-NRIC(WS-I)            TO K486-NUM-NRIC                 
                 MOVE F482-NUM-HDBREF-NEW      TO K486-NUM-HDBREF-NEW           
                 MOVE F482-NUM-HDBREF-DISPOSAL TO K486-NUM-HDBREF-EXIST         
                 PERFORM 3020-READ-BP13K486    THRU 3020-EXIT                   
                 IF BP13K486-FND                                                
                    PERFORM 3021-REWRITE-BP13K486  THRU 3021-EXIT               
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           PERFORM 2000-READ-F482            THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3010-READ-BP13K482.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13K482.                                                       
                                                                                
           IF BP13K482-STATUS = 00 OR 02                                        
              MOVE 'Y'                        TO WS-BP13K482-FND                
           ELSE                                                                 
              IF BP13K482-STATUS = 23                                           
                 MOVE 'N'                     TO WS-BP13K482-FND                
                 ADD 1                        TO NO-READ-NTFND-K482             
              ELSE                                                              
                 DISPLAY 'BP13K482 - READ ERROR (' BP13K482-STATUS ')'          
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3011-REWRITE-BP13K482.                                                   
      *----------------------------------------------------------------*        
                                                                                
           MOVE 'Y'                       TO K482-CDE-TEMP-LOAN-BFSL            
           REWRITE BP13K482-REC.                                                
                                                                                
           IF BP13K482-STATUS = 00 OR 02                                        
              ADD 1                       TO NO-K482-REWRITE                    
           ELSE                                                                 
              DISPLAY 'BP13K482 - REWRITE ERROR (' BP13K482-STATUS ')'          
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       3011-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3020-READ-BP13K486.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13K486.                                                       
                                                                                
           IF BP13K486-STATUS = 00 OR 02                                        
              MOVE 'Y'                        TO WS-BP13K486-FND                
           ELSE                                                                 
              IF BP13K486-STATUS = 23                                           
                 DISPLAY 'BP13K486 NOT FND ' K486-NUM-NRIC                      
                 DISPLAY 'BP13K486 NOT FND ' K486-NUM-HDBREF-NEW                
                 DISPLAY 'BP13K486 NOT FND ' F482-NUM-HDBREF-DISPOSAL           
                 MOVE 'N'                     TO WS-BP13K486-FND                
                 ADD 1                        TO NO-READ-NTFND-K486             
              ELSE                                                              
                 DISPLAY 'BP13K486 - READ ERROR (' BP13K486-STATUS ')'          
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3021-REWRITE-BP13K486.                                                   
      *----------------------------------------------------------------*        
                                                                                
           MOVE 'Y'                       TO K486-CDE-TEMP-LOAN-BFSL            
           REWRITE BP13K486-REC.                                                
                                                                                
           IF BP13K486-STATUS = 00 OR 02                                        
              ADD 1                      TO NO-K486-REWRITE                     
           ELSE                                                                 
              DISPLAY 'BP13K486 - REWRITE ERROR (' BP13K486-STATUS ')'          
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       3021-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
           CLOSE BP13F482                                                       
                 BP13K482                                                       
                 BP13K486.                                                      
                                                                                
           IF BP13K482-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K482-STATUS ' BP13K482-STATUS         
              MOVE BP13K482-STATUS  TO RETURN-CODE.                             
                                                                                
           IF BP13K486-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K486-STATUS ' BP13K486-STATUS         
              MOVE BP13K486-STATUS  TO RETURN-CODE.                             
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13CA53 CONTROL LIST ********'                       
           DISPLAY ' NO-F310-READ       : ' NO-F482-READ.                       
           DISPLAY ' NO-READ-NTFND-K482 : ' NO-READ-NTFND-K482.                 
           DISPLAY ' NO-READ-NTFND-K486 : ' NO-READ-NTFND-K486.                 
           DISPLAY ' NO-K482-REWRITE    : ' NO-K482-REWRITE.                    
           DISPLAY ' NO-K486-REWRITE    : ' NO-K486-REWRITE.                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
