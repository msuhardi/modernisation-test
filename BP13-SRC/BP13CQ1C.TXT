       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CQ1C.                                                 
      *AUTHOR.        KARL MAGALONA CABUG.                                      
      *DATE-WRITTEN.  05 JULY 2017.                                             
      *============================================================*            
      * BP13 - SYSTEM OF COMMITMENT                                *            
      *============================================================*            
      * OBJECTIVE :  PRELIM AHG/SHG - MANAGEMENT REPORT            *            
      *                                                            *            
      * INPUT     :  BP13KQ10                                      *            
      * OUTPUT    :  BP13FQ10                                      *            
      *                                                            *            
      *----------------------------------------------------------- *            
      * CHG REF  DATE     BY     DESCRIPTION                       *            
      * -------- -------- ----   ------------------------------    *            
      * BP136879 05072017 KAM4   NEW PROGRAM.                      *            
      * BP137042 01112017 KAM4   CHANGE WS-DTE-CHECK FROM HARDCODED*            
      *                          TO SYSTEM DATE.                   *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13FQ1A ASSIGN       TO BP13FQ1A.                            
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-STATUS.                      
                                                                                
           SELECT BP13KQ10 ASSIGN       TO BP13KQ10                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KQ10-NUM-REGN                        
                           FILE STATUS  IS WS-KQ10-STATUS.                      
                                                                                
           SELECT BP13F001 ASSIGN       TO BP13F001.                            
           SELECT BP13F002 ASSIGN       TO BP13F002.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13FQ1A                                                             
           RECORD CONTAINS 50 CHARACTERS                                        
           RECORDING  MODE  IS F.                                               
       01  BP13FQ1A-RECORD.                                                     
           05 FQ1A-NUM-REGN             PIC X(08).                              
           05 FILLER                    PIC X(42).                              
                                                                                
       FD  BP13F001                                                             
           RECORD CONTAINS 130 CHARACTERS                                       
           RECORDING  MODE  IS F.                                               
       01  BP13F001-REC                 PIC X(130).                             
                                                                                
       FD  BP13F002                                                             
           RECORD CONTAINS 130 CHARACTERS                                       
           RECORDING  MODE  IS F.                                               
       01  BP13F002-REC                 PIC X(130).                             
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13KQ10                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13KQ10.                                                           
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
       01  WS-CURRENT-DATE               PIC 9(08).                             
       01  WS-CURR-DATE                  PIC 9(08).                             
                                                                                
       01  WS-COUNTER.                                                          
           05 WS-FQ1A-READ-CTR           PIC 9(06) VALUE ZEROES.                
           05 WS-KQ10-READ-CTR           PIC 9(06) VALUE ZEROES.                
           05 WS-1ST-TIMER-CNT           PIC 9(06) VALUE ZEROES.                
           05 WS-FT2T-CNT                PIC 9(06) VALUE ZEROES.                
           05 WS-DOC-RECVD-CNT           PIC 9(06) VALUE ZEROES.                
           05 WS-NO-DOC-RECVD-CNT        PIC 9(06) VALUE ZEROES.                
           05 WS-PRELIM-SENT-CTR         PIC 9(06) VALUE ZEROES.                
           05 WS-F001-CTR                PIC 9(06) VALUE ZEROES.                
                                                                                
       01  WS-OTHER-VAR.                                                        
           05  WS-KQ10-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K800-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-NOTFND-KQ10            PIC 9(06)  VALUE ZEROES.               
           05  WS-K800-NTFND-CTR         PIC 9(06)  VALUE ZEROES.               
           05  WS-KQ10-NTFND-CTR         PIC 9(06)  VALUE ZEROES.               
           05  WS-K800-FND               PIC X(01)  VALUE SPACES.               
           05  WS-KQ10-FND               PIC X(01)  VALUE SPACES.               
           05  WS-FQ1A-EOF               PIC X(01)  VALUE SPACES.               
           05  WS-DTE-CHECK              PIC X(08)  VALUE SPACES.               
           05  WS-DTE-BK-DUE             PIC X(08)  VALUE SPACES.               
           05  WS-DTE-BK-CHR             PIC X(08)  VALUE SPACES.               
           05  WS-DTE-BK-INT             PIC 9(08).                             
           05  WS-DATE-DUE-INT           PIC S9(09).                            
           05  WS-NUM-DTE-DUE            PIC 9(08).                             
           05  WS-NUM-BSNS-TX            PIC X(12).                             
           05  WS-NUM-BSNS-REF           PIC X(15).                             
           05  WS-NUM-DOC-STATUS-END     PIC X(03).                             
           05  WS-DTE-LAST-SEND-RCV-UNVRFY PIC X(08).                           
           05  WS-DTE-1ST-SEND-RCV-UNVRFY  PIC X(08).                           
                                                                                
       01  WS-F001-HEADER1.                                                     
           05 FILLER                     PIC X(24) VALUE                        
              'LETTERS SENT (1ST-TIMER)'.                                       
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(19) VALUE                        
              'LETTERS SENT (FT2T)'.                                            
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(25) VALUE                        
              'WITH DOCUMENT RECEIVED ON'.                                      
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(23) VALUE                        
              'NO DOCUMENT RECEIVED ON'.                                        
           05 FILLER                     PIC X(30) VALUE SPACES.                
                                                                                
       01  WS-F001-HEADER2.                                                     
           05 FILLER                     PIC X(51) VALUE SPACES.                
           05 FILLER                     PIC X(20) VALUE                        
              'OR BEFORE 30/06/2017'.                                           
           05 FILLER                     PIC X(07) VALUE SPACES.                
           05 FILLER                     PIC X(20) VALUE                        
              'OR BEFORE 30/06/2017'.                                           
           05 FILLER                     PIC X(32) VALUE SPACES.                
                                                                                
       01  WS-F001-HEADER3.                                                     
           05 FILLER                     PIC X(102) VALUE ALL '='.              
           05 FILLER                     PIC X(28) VALUE SPACES.                
                                                                                
       01  WS-F001-DTLS.                                                        
           05 FILLER                     PIC X(07) VALUE SPACES.                
           05 WS-F001-1ST-TIMER          PIC Z(06) VALUE ZEROES.                
           05 FILLER                     PIC X(18) VALUE SPACES.                
           05 WS-F001-FT2T               PIC Z(06) VALUE ZEROES.                
           05 FILLER                     PIC X(21) VALUE SPACES.                
           05 WS-F001-DOC-RECVD          PIC Z(06) VALUE ZEROES.                
           05 FILLER                     PIC X(20) VALUE SPACES.                
           05 WS-F001-NO-DOC-RECVD       PIC Z(06) VALUE ZEROES.                
           05 FILLER                     PIC X(40) VALUE SPACES.                
                                                                                
       01  WS-F002-DTLS.                                                        
           05 WS-NUM-REGN                PIC X(08) VALUE SPACES.                
           05 WS-FILLER1                 PIC X(01) VALUE SPACES.                
           05 WS-SALES-MODE              PIC X(03) VALUE SPACES.                
           05 WS-FILLER2                 PIC X(01) VALUE SPACES.                
           05 WS-DTE-BALLOT              PIC X(08) VALUE SPACES.                
           05 WS-FILLER3                 PIC X(01) VALUE SPACES.                
           05 WS-NEW-TOWN                PIC X(20) VALUE SPACES.                
           05 WS-FILLER4                 PIC X(01) VALUE SPACES.                
           05 WS-FLAT-TYPE               PIC X(02) VALUE SPACES.                
           05 WS-FILLER5                 PIC X(01) VALUE SPACES.                
           05 WS-DTE-BK-APPT             PIC X(08) VALUE SPACES.                
           05 WS-FILLER6                 PIC X(01) VALUE SPACES.                
           05 WS-1ST-DOC-RECEIVED        PIC X(08) VALUE SPACES.                
           05 WS-FILLER7                 PIC X(01) VALUE SPACES.                
           05 WS-LAST-DOC-RECEIVED       PIC X(08) VALUE SPACES.                
           05 FILLER                     PIC X(27) VALUE SPACES.                
                                                                                
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
           EXEC SQL INCLUDE P13ACDR  END-EXEC.                                  
                                                                                
       01  WS-SQLCODE                       PIC 9(9)  VALUE ZEROES.             
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND                 PIC S9(4) COMP VALUE +000.          
           05  WS-SQL-NOT-FOUND             PIC S9(4) COMP VALUE +100.          
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
           PERFORM 1000-OPEN-FILES            THRU 1000-EXIT.                   
           PERFORM 2000-READ-BP13FQ1A         THRU 2000-EXIT.                   
           PERFORM 3000-MAIN-PROCESS          THRU 3000-EXIT                    
                   UNTIL WS-FQ1A-EOF = 'Y'.                                     
                                                                                
           PERFORM 4000-WRITE-F001-DTLS       THRU 4000-EXIT.                   
           PERFORM 9000-CLOSE-FILES           THRU 9000-EXIT.                   
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT BP13FQ1A                                                  
                      BP13K800                                                  
                      BP13KQ10                                                  
               OUTPUT BP13F001                                                  
                      BP13F002.                                                 
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K800-STATUS = ' WS-K800-STATUS        
              MOVE     WS-K800-STATUS              TO RETURN-CODE               
              PERFORM  9000-CLOSE-FILES          THRU 9000-EXIT                 
           END-IF.                                                              
                                                                                
           IF WS-KQ10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13KQ10-STATUS = ' WS-KQ10-STATUS        
              MOVE     WS-KQ10-STATUS              TO RETURN-CODE               
              PERFORM  9000-CLOSE-FILES          THRU 9000-EXIT                 
           END-IF.                                                              
                                                                                
           WRITE BP13F001-REC    FROM WS-F001-HEADER1.                          
           WRITE BP13F001-REC    FROM WS-F001-HEADER2.                          
           WRITE BP13F001-REC    FROM WS-F001-HEADER3.                          
                                                                                
           MOVE ZEROES                        TO WS-1ST-TIMER-CNT               
                                                 WS-FT2T-CNT                    
                                                 WS-DOC-RECVD-CNT               
                                                 WS-NO-DOC-RECVD-CNT.           
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FQ1A.                                                      
      *-------------------------------------------------------------            
           READ BP13FQ1A                                                        
            AT END                                                              
               MOVE 'Y'                        TO WS-FQ1A-EOF                   
               GO TO 2000-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1  TO WS-FQ1A-READ-CTR.                                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-MAIN-PROCESS.                                                       
      *------------------------------------------------------------*            
           MOVE SPACES                  TO WS-DTE-LAST-SEND-RCV-UNVRFY          
                                           WS-DTE-1ST-SEND-RCV-UNVRFY           
                                           WS-DTE-BK-DUE.                       
           MOVE 'N'                              TO WS-K800-FND                 
                                                    WS-KQ10-FND.                
           MOVE FQ1A-NUM-REGN                    TO K800-NUM-REGN               
                                                    KQ10-NUM-REGN.              
           PERFORM 3100-READ-BP13K800          THRU 3100-EXIT.                  
           PERFORM 3200-READ-BP13KQ10          THRU 3200-EXIT.                  
                                                                                
           IF  WS-KQ10-FND = 'Y' AND                                            
               WS-K800-FND = 'Y'                                                
               IF K800-PRELIM-OUTCOME-SENT = 'Y'                                
                  ADD 1 TO WS-PRELIM-SENT-CTR                                   
               END-IF                                                           
                                                                                
               PERFORM 3300-CHECK-CDB          THRU 3300-EXIT                   
               PERFORM 3500-CHECK-DUE-DATE     THRU 3500-EXIT                   
                                                                                
               MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DTE-CHECK                  
               IF WS-DTE-BK-DUE <= WS-DTE-CHECK                                 
                                                                                
      ****testing***                                                            
      ****          DISPLAY 'FQ1A-NUM-REGN : ' FQ1A-NUM-REGN                    
      ****          DISPLAY 'CDB-DTE :' WS-DTE-LAST-SEND-RCV-UNVRFY             
      ****          DISPLAY 'BK-DUE  : ' WS-DTE-BK-DUE                          
      ****          DISPLAY ' '                                                 
      **************                                                            
                                                                                
                  IF K800-NUM-HOUSEHOLD = 'H'                                   
                     ADD 1         TO WS-1ST-TIMER-CNT                          
                  ELSE                                                          
                    IF (K800-NUM-FT2T = 'Y')                                    
                       ADD 1       TO WS-FT2T-CNT                               
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF (WS-DTE-LAST-SEND-RCV-UNVRFY NOT = SPACES AND               
                                            LOW-VALUES AND ZEROES)              
                    IF WS-DTE-LAST-SEND-RCV-UNVRFY <= WS-DTE-BK-DUE             
                       ADD 1         TO WS-DOC-RECVD-CNT                        
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF (WS-DTE-LAST-SEND-RCV-UNVRFY = SPACES OR                    
                                    LOW-VALUES OR ZEROES) OR                    
                    (WS-DTE-LAST-SEND-RCV-UNVRFY > WS-DTE-BK-DUE)               
                     ADD 1       TO WS-NO-DOC-RECVD-CNT                         
                                                                                
                     DISPLAY ' '                                                
                     DISPLAY 'NO DOCUMENT RECEIVED BEFORE OR ON '               
                              WS-DTE-BK-DUE                                     
                     DISPLAY 'REGN : ' FQ1A-NUM-REGN ', '                       
                             'CDB LAST DOC RECEIVED : '                         
                              WS-DTE-LAST-SEND-RCV-UNVRFY                       
                 END-IF                                                         
      **ADDED FOR 2ND REPORT**                                                  
                 MOVE SPACES                  TO WS-F002-DTLS                   
                 MOVE ';'                     TO WS-FILLER1                     
                                                 WS-FILLER2                     
                                                 WS-FILLER3                     
                                                 WS-FILLER4                     
                                                 WS-FILLER5                     
                                                 WS-FILLER6                     
                                                 WS-FILLER7                     
                 MOVE FQ1A-NUM-REGN           TO WS-NUM-REGN                    
                 MOVE K800-NUM-ALLO-CAT       TO WS-SALES-MODE                  
                 MOVE K800-DTE-BALLOT         TO WS-DTE-BALLOT                  
                 MOVE K800-NUM-NEW-TOWN       TO WS-NEW-TOWN                    
                 MOVE K800-NUM-FLAT-TYPE      TO WS-FLAT-TYPE                   
                 MOVE WS-DTE-BK-CHR           TO WS-DTE-BK-APPT                 
                 MOVE WS-DTE-1ST-SEND-RCV-UNVRFY  TO WS-1ST-DOC-RECEIVED        
                 MOVE WS-DTE-LAST-SEND-RCV-UNVRFY TO                            
                                                    WS-LAST-DOC-RECEIVED        
                 WRITE BP13F002-REC     FROM WS-F002-DTLS                       
                 ADD 1 TO WS-F001-CTR                                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FQ1A          THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           READ BP13K800                                                        
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                 TO WS-K800-FND                     
               WHEN 23                                                          
                    ADD 1                    TO WS-K800-NTFND-CTR               
                    MOVE SPACES              TO BP13K800-MASTER                 
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 - READ FILE ERROR, KEY: '                 
                            K800-NUM-REGN                                       
                    MOVE WS-K800-STATUS      TO RETURN-CODE                     
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-READ-BP13KQ10.                                                      
      *-------------------------------------------------------------            
           READ BP13KQ10                                                        
                                                                                
           EVALUATE WS-KQ10-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                 TO WS-KQ10-FND                     
               WHEN 23                                                          
                    ADD 1                    TO WS-KQ10-NTFND-CTR               
                    MOVE SPACES              TO BP13KQ10-REC                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13KQ10 - READ FILE ERROR, KEY: '                 
                            KQ10-NUM-REGN                                       
                    MOVE WS-KQ10-STATUS      TO RETURN-CODE                     
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-CHECK-CDB.                                                          
      *-------------------------------------------------------------            
           MOVE 'Sales'           TO NUM-BSNS-TX                                
                                     WS-NUM-BSNS-TX.                            
           MOVE FQ1A-NUM-REGN     TO NUM-BSNS-REF                               
                                     WS-NUM-BSNS-REF.                           
           MOVE 'REC'             TO NUM-STATUS-END                             
                                     WS-NUM-DOC-STATUS-END.                     
                                                                                
           EXEC SQL SELECT                                                      
                    NUM_BSNS_TX,                                                
                    NUM_BSNS_REF,                                               
                    NUM_STATUS_END,                                             
                    TME_LAST_SEND_RCV_UNVRFY                                    
               INTO :NUM-BSNS-TX,                                               
                    :NUM-BSNS-REF,                                              
                    :NUM-STATUS-END,                                            
                    :TME-LAST-SEND-RCV-UNVRFY                                   
               FROM APLCTN_CSTMR_DOC_RQST                                       
              WHERE NUM_BSNS_TX    = :WS-NUM-BSNS-TX                            
                AND NUM_BSNS_REF   = :WS-NUM-BSNS-REF                           
                AND NUM_STATUS_END = :WS-NUM-DOC-STATUS-END                     
                ORDER BY TME_LAST_SEND_RCV_UNVRFY DESC                          
                FETCH FIRST 1 ROWS ONLY                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN WS-SQL-FOUND                                                
                    STRING TME-LAST-SEND-RCV-UNVRFY(1:4)                AIL     
                           TME-LAST-SEND-RCV-UNVRFY(6:2)                        
                           TME-LAST-SEND-RCV-UNVRFY(9:4)                        
                           DELIMITED BY SIZE INTO                               
                           WS-DTE-LAST-SEND-RCV-UNVRFY                          
                    END-STRING                                                  
                                                                                
                    PERFORM 3400-GET-1ST-DOC-RECVD THRU 3400-EXIT               
                                                                                
               WHEN WS-SQL-NOT-FOUND                                            
                    MOVE SPACES        TO WS-DTE-LAST-SEND-RCV-UNVRFY           
                    DISPLAY 'REGN : ' FQ1A-NUM-REGN                     ERR     
                      ' - RECORD NOTFND IN APLCTN_CSTMR_DOC_RQST TABLE'         
               WHEN OTHER                                                       
                    DISPLAY 'READ SQL ERROR : ' SQLCODE                 ERR     
                            ', '  WS-SQLCODE(6:4) '.'                   ERR     
                    PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3400-GET-1ST-DOC-RECVD.                                                  
      *-------------------------------------------------------------            
           MOVE 'Sales'           TO NUM-BSNS-TX                                
                                     WS-NUM-BSNS-TX.                            
           MOVE FQ1A-NUM-REGN     TO NUM-BSNS-REF                               
                                     WS-NUM-BSNS-REF.                           
           MOVE 'REC'             TO NUM-STATUS-END                             
                                     WS-NUM-DOC-STATUS-END.                     
                                                                                
           EXEC SQL SELECT                                                      
                    NUM_BSNS_TX,                                                
                    NUM_BSNS_REF,                                               
                    NUM_STATUS_END,                                             
                    TME_LAST_SEND_RCV_UNVRFY                                    
               INTO :NUM-BSNS-TX,                                               
                    :NUM-BSNS-REF,                                              
                    :NUM-STATUS-END,                                            
                    :TME-LAST-SEND-RCV-UNVRFY                                   
               FROM APLCTN_CSTMR_DOC_RQST                                       
              WHERE NUM_BSNS_TX    = :WS-NUM-BSNS-TX                            
                AND NUM_BSNS_REF   = :WS-NUM-BSNS-REF                           
                AND NUM_STATUS_END = :WS-NUM-DOC-STATUS-END                     
                ORDER BY TME_LAST_SEND_RCV_UNVRFY ASC                           
                FETCH FIRST 1 ROWS ONLY                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN WS-SQL-FOUND                                                
                    STRING TME-LAST-SEND-RCV-UNVRFY(1:4)                AIL     
                           TME-LAST-SEND-RCV-UNVRFY(6:2)                        
                           TME-LAST-SEND-RCV-UNVRFY(9:4)                        
                           DELIMITED BY SIZE INTO                               
                           WS-DTE-1ST-SEND-RCV-UNVRFY                           
                    END-STRING                                                  
               WHEN WS-SQL-NOT-FOUND                                            
                    MOVE SPACES        TO WS-DTE-LAST-SEND-RCV-UNVRFY           
                    DISPLAY 'REGN : ' FQ1A-NUM-REGN                     ERR     
                      ' - RECORD NOTFND IN APLCTN_CSTMR_DOC_RQST TABLE'         
               WHEN OTHER                                                       
                    DISPLAY 'READ SQL ERROR (ASCENDING): ' SQLCODE      ERR     
                            ', '  WS-SQLCODE(6:4) '.'                   ERR     
                    PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                
           END-EVALUATE.                                                        
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-CHECK-DUE-DATE.                                                     
      *-------------------------------------------------------------            
           IF K800-NUM-JT-BALLOT-REGN NOT = SPACES AND LOW-VALUES AND           
              K800-DTE-BK-APPT-MGPS   NOT = SPACES AND LOW-VALUES               
              MOVE K800-DTE-BK-APPT-MGPS       TO WS-DTE-BK-CHR                 
           ELSE                                                                 
             IF K800-DTE-BK-APPT NOT = SPACES AND LOW-VALUES                    
                  MOVE K800-DTE-BK-APPT        TO WS-DTE-BK-CHR                 
             ELSE                                                               
                MOVE SPACES                    TO WS-DTE-BK-CHR                 
             END-IF                                                             
           END-IF.                                                              
           IF WS-DTE-BK-CHR = SPACES                                            
              MOVE SPACES   TO BP13K800-MASTER                                  
              DISPLAY 'REGN : ' K800-NUM-REGN ',DTE-BK : ' WS-DTE-BK-CHR        
              GO TO 3500-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-DTE-BK-CHR                  TO WS-DTE-BK-INT                 
           COMPUTE WS-DTE-BK-INT =                                              
                   FUNCTION INTEGER-OF-DATE(WS-DTE-BK-INT)                      
                                                                                
           COMPUTE WS-DTE-BK-INT = WS-DTE-BK-INT - 7                            
           COMPUTE WS-NUM-DTE-DUE =                                             
                   FUNCTION DATE-OF-INTEGER(WS-DTE-BK-INT)                      
                                                                                
           MOVE WS-NUM-DTE-DUE            TO WS-DTE-BK-DUE.                     
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-F001-DTLS.                                                    
      *-------------------------------------------------------------            
           MOVE WS-1ST-TIMER-CNT              TO WS-F001-1ST-TIMER.             
           MOVE WS-FT2T-CNT                   TO WS-F001-FT2T.                  
           MOVE WS-DOC-RECVD-CNT              TO WS-F001-DOC-RECVD.             
           MOVE WS-NO-DOC-RECVD-CNT           TO WS-F001-NO-DOC-RECVD.          
           WRITE BP13F001-REC     FROM WS-F001-DTLS.                            
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '-------------BP13CQ1C CONTROL TOTAL -----------'.           
           DISPLAY 'NO OF RECORDS READ  (BP13FQ1A) : ' WS-FQ1A-READ-CTR         
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF REC(S) NOTFND (BP13K800) : ' WS-K800-NTFND-CTR        
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF REC(S) NOTFND (BP13KQ10) : ' WS-KQ10-NTFND-CTR        
           DISPLAY ' '.                                                         
           DISPLAY 'PRELIM-OUTCOME-SENT COUNT : ' WS-PRELIM-SENT-CTR            
           DISPLAY ' '.                                                         
           DISPLAY 'TOTAL RECORD COUNT IN F002: ' WS-F001-CTR.                  
                                                                                
           CLOSE BP13K800                                                       
                 BP13KQ10.                                                      
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
