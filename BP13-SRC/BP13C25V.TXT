       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C25V.                                                  
      *AUTHOR.       ELAINE S ARGA.                                             
      *DATE-WRITTEN. 25 JANUARY 2016                                            
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  TO UPDATE BP13K200 K200-NUM-SERS-QUEUE-WITHIN-FT          *            
      *------------------------------------------------------------*            
      *  INPUT   :  BP13F200                                       *            
      *  I-O     :  BP13K200                                       *            
      *  OUTPUT  :  P13F200O                                       *            
      * ========================================================== *            
      * CHG REQ#  DATE        BY    DESCRIPTION                    *            
      * --------  ----------  ----  -------------------------------*            
      * BP136074  28/01/2016  ESA1  NEW PROGRAM.                   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
                                                                                
           SELECT BP13K200 ASSIGN TO BP13K200                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K200-KEY-FLD                               
                  FILE STATUS     IS WS-K200-STATUS.                            
                                                                                
           SELECT P13F200O ASSIGN TO P13F200O.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F200                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F200.                                                           
                                                                                
       FD  BP13K200                                                             
           RECORD CONTAINS 300 CHARACTERS.                                      
       COPY BP13K200.                                                           
                                                                                
       FD  P13F200O                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F200O-REC     PIC X(300).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F200-EOF                   PIC X    VALUE 'N'.                    
       01  WS-K200-STATUS                PIC 99   VALUE ZEROS.                  
       01  WS-QUEUE                      PIC 9(4) VALUE ZEROS.                  
                                                                                
       01  WS-F200-KEY-FLD.                                             00160000
           05  WS-F200-DTE-ALLOC             PIC X(8).                  00161000
           05  WS-F200-NUM-NT-ZONE           PIC X(3).                  00161105
           05  WS-F200-NUM-ACTUAL-FT         PIC X(2).                  00161200
                                                                                
       01  WS-PREV-KEY-FLD.                                             00160000
           05  WS-PREV-DTE-ALLOC             PIC X(8).                  00161000
           05  WS-PREV-NUM-NT-ZONE           PIC X(3).                  00161105
           05  WS-PREV-NUM-ACTUAL-FT         PIC X(2).                  00161200
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F200-READ              PIC 9(9) VALUE ZEROS.                  
           05  WS-F200O-WRITE            PIC 9(9) VALUE ZEROS.                  
           05  WS-K200-READ              PIC 9(9) VALUE ZEROS.                  
           05  WS-K200-UPD               PIC 9(9) VALUE ZEROS.                  
           05  WS-K200-BYPASS            PIC 9(9) VALUE ZEROS.                  
           05  WS-K200-NOTFND            PIC 9(9) VALUE ZEROS.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
                                                                                
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F200    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-F200 THRU 300-EXIT                              
                    UNTIL WS-F200-EOF = 'Y'.                                    
            PERFORM 900-CLOSE-FILES  THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F200                                                 
                I-O    BP13K200                                                 
                OUTPUT P13F200O.                                                
                                                                                
           IF WS-K200-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K200-STATUS ' WS-K200-STATUS              
              MOVE WS-K200-STATUS       TO RETURN-CODE                          
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F200.                                                           
      *-------------------------------------------------------------            
           READ BP13F200 AT END                                                 
                MOVE 'Y' TO WS-F200-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           MOVE F200-DTE-ALLOC      TO WS-F200-DTE-ALLOC.               00161000
           MOVE F200-NUM-NT-ZONE    TO WS-F200-NUM-NT-ZONE.             00161105
           MOVE F200-NUM-ACTUAL-FT  TO WS-F200-NUM-ACTUAL-FT.                   
                                                                                
           ADD 1         TO WS-F200-READ.                                       
                                                                                
           IF WS-F200-READ = 1                                                  
              MOVE WS-F200-KEY-FLD  TO WS-PREV-KEY-FLD                          
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F200.                                                        
      *-------------------------------------------------------------            
           IF WS-F200-KEY-FLD = WS-PREV-KEY-FLD                                 
              ADD  1          TO WS-QUEUE                                       
              PERFORM 310-UPDATE-K200  THRU 310-EXIT                            
           ELSE                                                                 
              MOVE 1          TO WS-QUEUE                                       
              PERFORM 310-UPDATE-K200  THRU 310-EXIT                            
           END-IF.                                                              
                                                                                
           MOVE WS-F200-KEY-FLD     TO WS-PREV-KEY-FLD.                         
                                                                                
           PERFORM 200-READ-F200    THRU 200-EXIT.                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       310-UPDATE-K200.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE F200-KEY-FLD          TO K200-KEY-FLD.                          
           READ BP13K200.                                                       
           EVALUATE WS-K200-STATUS                                              
           WHEN 00                                                              
                ADD 1                        TO WS-K200-READ                    
                STRING K200-NUM-HHTY-QUEUE-SERIAL(1:1)  WS-QUEUE                
                       DELIMITED BY SIZE INTO                                   
                       K200-NUM-SERS-QUEUE-WITHIN-FT                            
                END-STRING                                                      
                PERFORM 400-REWRITE-BP13K200 THRU 400-EXIT                      
           WHEN 23                                                              
                DISPLAY 'REC NOT FOUND IN BP13K200 : ' F200-NUM-REGN            
                        ', STATUS ' WS-K200-STATUS                              
                ADD 1                        TO WS-K200-NOTFND                  
           WHEN OTHER                                                           
                DISPLAY 'ERROR IN READING BP13K200 : ' F200-NUM-REGN            
                        ', STATUS ' WS-K200-STATUS                              
                MOVE WS-K200-STATUS TO RETURN-CODE                              
                PERFORM 900-CLOSE-FILES THRU 900-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-REWRITE-BP13K200.                                                    
      *-------------------------------------------------------------            
                                                                                
           REWRITE BP13K200-REC.                                                
           IF WS-K200-STATUS = 00 OR 02                                         
              ADD 1 TO WS-K200-UPD                                              
              MOVE BP13K200-REC    TO P13F200O-REC                              
              WRITE P13F200O-REC                                                
              ADD 1 TO WS-F200O-WRITE                                           
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K200 : ' F200-NUM-REGN               
                      '  STATUS ' WS-K200-STATUS                                
              MOVE WS-K200-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '----- BP13C25V RUN STATISTICS -------------'.               
           DISPLAY 'NO OF REC READ    BP13F200   = ' WS-F200-READ.              
           DISPLAY 'NO OF REC READ    BP13K200   = ' WS-K200-READ.              
           DISPLAY 'NO OF REC UPDATED BP13K200   = ' WS-K200-UPD.               
           DISPLAY 'NO OF REC NOTFND  BP13K200   = ' WS-K200-NOTFND.            
           DISPLAY 'NO OF REC WRITTEN P13F200O   = ' WS-F200O-WRITE.            
           DISPLAY '----- BP13C25V EOJ -------------------------'.              
                                                                                
           CLOSE BP13F200                                                       
                 P13F200O                                                       
                 BP13K200.                                                      
                                                                                
           IF WS-K200-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K200, STATUS ' WS-K200-STATUS          
              MOVE WS-K200-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
