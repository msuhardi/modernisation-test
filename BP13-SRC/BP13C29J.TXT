       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C29J.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  22 MAR 2010.                                              
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE: SUBROUTINE TO PRINT PRICELIST OF FLATS WITH      *           
      *            F999-NUM-POD-DUE = 'N' AND SPACES(FORM-TYPE= 'C')*           
      *-------------------------------------------------------------*           
      * INPUT FILES: BP13F999                                       *           
      *              BP13K813                                       *           
      *              BM06K110                                       *           
      *              BM06K100                                       *           
      *                                                             *           
      * OUTPUT FILE: BP13L29J (FOR F999-NUM-POD-DUE = 'N' OR SPACES)*           
      *-------------------------------------------------------------*           
      * CHG REF  OIC  DATE     DESCRIPTION                          *           
      * -------- ---- -------- ------------------------------------ *           
      * BP133851 ESA1 22032010 NEW PROGRAM TO CATER FOR PAIRED UNIT *           
      * BP134043 ESA1 28102010 TO REPLACE ECD WITH PCD FOR SBF      *           
      * BP134681 ESA1 19092012 TO REPLACE BP13K816 WITH BP13K813    *           
      * BP135190 SMR2 04122013 EXPAND BP13K813 TO 1000              *           
      * BP135555 ESA1 20150320 TO REPLACE BP13C910 W/ BP13C91J      *           
      * BP135799 ESA1 20150611 TO REMOVE DISPLAY OF MODEL DESIGN    *           
      * BP139344 MRR5 20221103 PRINT DPD AS EDPD IF SBF OR OBF      *           
      * BP139436 MRR5 20230125 CALL NEW SUBR BP13CRG3 TO READ THE   *           
      *                        BLDG_FTR TABLE FOR CAR-LITE TEXT     *           
      *                        CHECK MODL DESGN 75/76 FOR PLH TEXT  *           
      * BP139436 MRR5 20230306 FIX REMARKS ISSUE                    *           
      * BP139685 MRR5 20230929 CATER FOR SER 50YR                   *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F999  ASSIGN TO BP13F999.                                 
                                                                                
           SELECT BM06K100  ASSIGN TO BM06K100                                  
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K100-KEY-FLD                        
                            FILE STATUS  IS WS-K100-STATUS.                     
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT BP13L29J  ASSIGN TO BP13L29J.                                 
                                                                                
       DATA DIVISION.                                                           
      *--------------------------------------------------------------           
       FILE SECTION.                                                            
      *--------------------------------------------------------------           
                                                                                
       FD  BP13F999                                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F999.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  BP13L29J                                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  L29J-PRTREC                    PIC X(100).                           
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-WORK-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE            PIC X(08) VALUE SPACES.               
               10  FILLER                 PIC X(13) VALUE SPACES.               
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD             PIC X(02) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE '/'.                  
               10  WS-DISP-MM             PIC X(02) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE '/'.                  
               10  WS-DISP-CCYY           PIC X(04) VALUE SPACES.               
           05  WS-NUM-MONTH               PIC X(02) VALUE SPACES.               
           05  WS-BLOCK-INFO              PIC X(30) OCCURS 13.                  
           05  WS-REMARKS                 PIC X(30) OCCURS 10.                  
           05  WS-PREV-CDE-NT             PIC X(03) VALUE SPACES.               
           05  WS-PREV-CNTRCT-BLOCK.                                            
               10  WS-PREV-NEIGHBOURHOOD  PIC X(04) VALUE SPACES.               
               10  WS-PREV-CONTRACT-NO    PIC X(04) VALUE SPACES.               
               10  WS-PREV-BLOCK-NO       PIC X(05) VALUE SPACES.               
           05  WS-NME-ESTATE              PIC X(20) VALUE SPACES.               
           05  WS-NUM-EST                 PIC X(03) VALUE SPACES.               
           05  WS-TMP-CNTRCT-TYP          PIC X(30) VALUE SPACES.               
           05  WS-SUB                     PIC 9(02) VALUE ZEROES.               
           05  WS-MAX-REM                 PIC 9(02) VALUE ZEROES.               
           05  WS-CTR                     PIC 9(03) VALUE ZEROES.               
           05  WS-NAME-MONTH              PIC X(03) VALUE SPACES.               
           05  WS-RETURN-CODE             PIC 9(02) VALUE ZEROES.               
           05  WS-NUM-MAX-CAT.                                                  
               10  WS-NUM-MAX-CAT1        PIC ZZ9.                              
               10  WS-NUM-MAX-CAT2        PIC ZZ9.                              
               10  WS-NUM-MAX-CAT3        PIC ZZ9.                              
           05  WS-NUM-MAX-CAT-SA.                                               
               10  WS-NUM-MAX-CAT1-SA     PIC ZZ9.                              
               10  WS-NUM-MAX-CAT2-SA     PIC ZZ9.                              
               10  WS-NUM-MAX-CAT3-SA     PIC ZZ9.                              
           05  WS-FLAT-TYPE               PIC X(06) VALUE SPACES.               
           05  WS-HEADING-LINE            PIC X(78) VALUE SPACES.               
           05  WS-ALLOC-DATE.                                                   
               10  WS-ALLOC-DATE-MONTH    PIC X(03).                            
               10  FILLER                 PIC X(01).                            
               10  WS-ALLOC-DATE-YEAR     PIC X(04).                            
           05  WS-TXT-QTR                 PIC X(07) VALUE SPACES.               
           05  WS-CAR-LITE-TAG            PIC X(01) VALUE SPACES.               
                                                                                
       01  WS-TABLES.                                                           
           05  WS-MONTH-TABLE.                                                  
               10  FILLER                 PIC X(05) VALUE '01JAN'.              
               10  FILLER                 PIC X(05) VALUE '02FEB'.              
               10  FILLER                 PIC X(05) VALUE '03MAR'.              
               10  FILLER                 PIC X(05) VALUE '04APR'.              
               10  FILLER                 PIC X(05) VALUE '05MAY'.              
               10  FILLER                 PIC X(05) VALUE '06JUN'.              
               10  FILLER                 PIC X(05) VALUE '07JUL'.              
               10  FILLER                 PIC X(05) VALUE '08AUG'.              
               10  FILLER                 PIC X(05) VALUE '09SEP'.              
               10  FILLER                 PIC X(05) VALUE '10OCT'.              
               10  FILLER                 PIC X(05) VALUE '11NOV'.              
               10  FILLER                 PIC X(05) VALUE '12DEC'.              
           05  FILLER  REDEFINES WS-MONTH-TABLE.                                
               10  WS-MONTH-TBL  OCCURS 12 INDEXED BY WS-MNT-NDX.               
                   15  WS-MONTH-NUM       PIC X(02).                            
                   15  WS-MONTH-NAME      PIC X(03).                            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K853-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K820-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K010-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K110-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K100-STATUS             PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F999-SW         PIC X(01) VALUE SPACES.                
               88  C-END-OF-F999         VALUE 'Y'.                             
           05  WS-BLKINFO-PRINTED-SW     PIC X(01) VALUE SPACES.                
               88  C-BLKINFO-PRINTED     VALUE 'Y'.                             
           05  WS-REMARKS-PRINTED-SW     PIC X(01) VALUE SPACES.                
               88  C-REMARKS-PRINTED     VALUE 'Y'.                             
           05  WS-FIRST-REC-READ-SW      PIC X(01) VALUE SPACES.                
               88  C-FIRST-REC-READ      VALUE 'Y'.                             
           05  WS-CHANGE-BLK-SW          PIC X(01) VALUE SPACES.                
               88  C-CHANGE-BLK          VALUE 'Y'.                             
           05  WS-K110-FND               PIC X(01)  VALUE 'N'.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F999-READ-CTR          PIC 9(06) VALUE ZEROES.                
           05  WS-LINE-CTR               PIC 9(02) VALUE ZEROES.                
           05  WS-LINE-CTR-A             PIC 9(02) VALUE ZEROES.                
           05  WS-PAGE-CTR               PIC 9(05) VALUE ZEROES.                
           05  WS-PRT-CTR                PIC 9(06) VALUE ZEROES.                
                                                                                
      *-------------------------------                                          
      *  FOR BP13C91J                *                                          
      *-------------------------------                                          
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-ZONE        PIC X(03).                               
           05  WS-LINK-NUM-FT          PIC X(02).                               
           05  WS-LINK-DTE-BALLOT      PIC X(06).                               
       01  WS-BP13K813-REC             PIC X(1000).                             
       01  WS-ZONE-FND-FLAG            PIC X(01).                               
                                                                                
      *-------------------------------                                          
      *  FOR BP13CRG3                *                                          
      *-------------------------------                                          
       01  WS-CRG3-REC.                                                         
           05  WS-CRG3-BLDG-GL        PIC X(05).                                
           05  WS-CRG3-BLDGFTR-DTLS.                                            
               10 WS-CRG3-DTLS        OCCURS 100 TIMES.                         
                  15  WS-CRG3-TYPE-FTR     PIC X(15).                           
                  15  WS-CRG3-NUM-FTR      PIC X(8).                            
                  15  WS-CRG3-NUM-SQNC     PIC X(2).                            
                  15  WS-CRG3-QTY-FTR      PIC S9(4) USAGE COMP.                
                  15  WS-CRG3-QTY-MSRMNT   PIC S9(6)V9(2) USAGE COMP-3.         
                  15  WS-CRG3-DTE-LASTUPD  PIC X(26).                           
           05  WS-CRG3-CDE-SYSERR     PIC S9(3).                                
           05  WS-CRG3-CDE-ERROR      PIC X(04).                                
           05  WS-CRG3-ERR-MSG        PIC X(50).                                
                                                                                
       COPY BP13K813.                                                           
                                                                                
      *-------------------------------                                          
      *  REPORT LAYOUT               *                                          
      *-------------------------------                                          
      *-----------------------------------------------------------              
      *LAY-OUT FOR SALE OF FLATS FOR F999-NUM-POD-DUE = 'N'                     
      *-----------------------------------------------------------              
       01  L29J-HEADER-01.                                                      
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  FILLER             PIC X(08) VALUE 'SALE OF '.                   
           05  L29J-FLAT-TYP2     PIC X(06) VALUE SPACES.                       
           05  FILLER             PIC X(07) VALUE ' FLATS '.                    
           05  FILLER             PIC X(19) VALUE '& STUDIO APARTMENTS'.        
           05  FILLER             PIC X(15) VALUE ' (PAIRED UNITS)'.            
                                                                                
       01  L29J-HEADER-02.                                                      
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  L29J-NME-ESTATE         PIC X(30) VALUE SPACES.                  
           05  FILLER                  PIC X(46) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'PAGE: '.                
           05  L29J-PAGE               PIC ZZZZZ.                               
                                                                                
       01  L29J-DETAIL-REC.                                                     
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  L29J-BLK-DETAILS        PIC X(30) VALUE SPACES.                  
           05  L29J-NUM-UNIT           PIC X(08) VALUE SPACES.                  
           05  L29J-NUM-FLAT-CODE      PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  L29J-FLOOR-AREA         PIC ZZZ.                                 
           05  L29J-FLOOR-AREA-R REDEFINES L29J-FLOOR-AREA PIC X(03).           
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  L29J-IN-FLOOR-AREA      PIC ZZZ.                                 
           05  FILLER                  PIC X(08) VALUE SPACES.                  
           05  L29J-SELLING-PRICE      PIC ZZ,ZZZ,ZZZ.                          
                                                                                
       01  L29J-LINE.                                                           
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(86) VALUE ALL '_'.                 
                                                                                
      *-------------------------------                                          
      *  DJDE FOR FORM TYPE = 'C'    *                                          
      *-------------------------------                                          
       01  DJDE-FORM-BP13UD              PIC X(80)  VALUE                       
           '$DJDE$ JDE=13C26E,FORMS=BP13UD,COPIES=3,JDL=BP13,END;'.             
                                                                                
       01  DJDE-FORM-BP13HU              PIC X(80)  VALUE                       
           '$DJDE$ JDE=13C26E,FORMS=BP13HU,COPIES=3,JDL=BP13,END;'.             
                                                                                
      *-----------------------------------------------------------              
       LINKAGE SECTION.                                                         
      *-----------------------------------------------------------              
       01  WS-C298-PARAMETERS.                                                  
           05  WS-C298-FORM-TYPE         PIC X(01).                             
           05  WS-C298-FLAT-TYPE         PIC X(02).                             
           05  WS-C298-NUM-GRO           PIC X(01).                             
           05  WS-C298-RETURN-CODE       PIC 9(04).                             
                                                                                
      *--------------------------------------------------------------           
       PROCEDURE DIVISION USING WS-C298-PARAMETERS.                             
      *--------------------------------------------------------------           
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
                                                                                
           IF WS-C298-NUM-GRO = 'P' OR 'C'                                      
              MOVE SPACES         TO L29J-PRTREC                                
              WRITE L29J-PRTREC FROM DJDE-FORM-BP13HU AFTER PAGE                
           ELSE                                                                 
              MOVE SPACES         TO L29J-PRTREC                                
              WRITE L29J-PRTREC FROM DJDE-FORM-BP13UD AFTER PAGE                
           END-IF.                                                              
                                                                                
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
             UNTIL C-END-OF-F999.                                               
                                                                                
           IF NOT C-BLKINFO-PRINTED OR                                          
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3330-PRINT-REMAINING-INFO    THRU 3330-EXIT               
           END-IF.                                                              
                                                                                
           WRITE L29J-PRTREC     FROM L29J-LINE AFTER 1.                        
                                                                                
           MOVE ZEROES        TO WS-RETURN-CODE.                                
                                                                                
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           GOBACK.                                                              
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BP13F999                                               
                         BM06K110                                               
                         BM06K100                                               
                OUTPUT   BP13L29J.                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 OPEN ERROR, STATUS ' WS-K110-STATUS             
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 OPEN ERROR, STATUS ' WS-K100-STATUS             
              MOVE WS-K100-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
           CALL 'O13K813A'         USING WS-RETURN-CODE.                        
                                                                                
           IF WS-RETURN-CODE NOT = ZEROS AND 97                                 
              DISPLAY 'OPEN ERROR, BP13K813 STATUS ' WS-RETURN-CODE             
              PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE 'Y'       TO WS-FIRST-REC-READ-SW.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BP13F999 AT END                                                 
                MOVE 'Y'     TO WS-END-OF-F999-SW                               
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F999-READ-CTR.                                         
                                                                                
           IF F999-NUM-FLAT-TYPE = '1A' OR '2A' OR 'SA'                         
              MOVE F999-NUM-MAX-CAT1             TO WS-NUM-MAX-CAT1-SA          
              MOVE F999-NUM-MAX-CAT2             TO WS-NUM-MAX-CAT2-SA          
              MOVE F999-NUM-MAX-CAT3             TO WS-NUM-MAX-CAT3-SA          
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM 3100-PRINT-DETAILS    THRU 3100-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3100-PRINT-DETAILS.                                                      
      *--------------------------------*                                        
                                                                                
           IF (F999-NEIGHBOURHOOD = WS-PREV-NEIGHBOURHOOD) AND                  
              (F999-CONTRACT-NO = WS-PREV-CONTRACT-NO) AND                      
              (F999-BLOCK-NO = WS-PREV-BLOCK-NO)                                
              PERFORM 3300-WRITE-DETAILS    THRU 3300-EXIT                      
           ELSE                                                                 
              MOVE 'Y'     TO WS-CHANGE-BLK-SW                                  
              IF NOT C-BLKINFO-PRINTED OR                                       
                 NOT C-REMARKS-PRINTED                                          
                 PERFORM 3330-PRINT-REMAINING-INFO THRU 3330-EXIT               
              END-IF                                                            
              PERFORM 3200-GET-CNTRCT-BLOCK-INFO   THRU 3200-EXIT               
              IF NOT C-FIRST-REC-READ                                           
                 WRITE L29J-PRTREC        FROM L29J-LINE AFTER 1                
              END-IF                                                            
              IF F999-CDE-NT NOT = WS-PREV-CDE-NT                               
                 PERFORM 4100-PRINT-PAGE-BREAK        THRU 4100-EXIT            
              END-IF                                                            
              MOVE F999-CDE-NT        TO WS-PREV-CDE-NT                         
              PERFORM 3300-WRITE-DETAILS           THRU 3300-EXIT               
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3200-GET-CNTRCT-BLOCK-INFO.                                              
      *--------------------------------*                                        
                                                                                
           MOVE F999-NEIGHBOURHOOD    TO WS-PREV-NEIGHBOURHOOD.                 
           MOVE F999-CONTRACT-NO      TO WS-PREV-CONTRACT-NO.                   
           MOVE F999-BLOCK-NO         TO WS-PREV-BLOCK-NO.                      
                                                                                
           PERFORM 3210-GET-BLK-DETAILS     THRU 3210-EXIT.                     
           PERFORM 3220-GET-REMARKS         THRU 3220-EXIT.                     
           MOVE ZEROES   TO WS-SUB.                                             
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3210-GET-BLK-DETAILS.                                                    
      *--------------------------------*                                        
                                                                                
           MOVE SPACES     TO WS-BLKINFO-PRINTED-SW.                            
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 13                 
              MOVE SPACES         TO WS-BLOCK-INFO (WS-SUB)                     
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES      TO WS-SUB.                                          
                                                                                
           IF F999-NUM-ZONE = SPACES OR LOW-VALUES                              
              MOVE F999-NME-ESTATE        TO WS-NME-ESTATE                      
           ELSE                                                                 
              PERFORM 3212-GET-ESTATE-NAME   THRU 3212-EXIT                     
           END-IF.                                                              
                                                                                
           IF F999-NUM-SELTY = 'BTO'                                            
              IF WS-NME-ESTATE(1:9) = 'TOA PAYOH' AND                           
                 WS-NUM-EST = '749'                                             
                 MOVE 'TOA PAYOH(BIDADARI)'  TO WS-NME-ESTATE                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-NME-ESTATE             TO L29J-NME-ESTATE.                   
                                                                                
      *    PERFORM 3213-CHANGE-CNTRCT-TYP    THRU 3213-EXIT.                    
      *    MOVE WS-TMP-CNTRCT-TYP         TO WS-BLOCK-INFO (1).                 
           MOVE SPACES                    TO WS-BLOCK-INFO (1).                 
                                                                                
           STRING WS-NME-ESTATE      ' '                                        
                  F999-NEIGHBOURHOOD ' '                                        
                  F999-CONTRACT-NO                                              
                  DELIMITED BY '  '     INTO WS-BLOCK-INFO (2).                 
                                                                                
           MOVE F999-NME-LOCAL-PRCNCT     TO WS-BLOCK-INFO (3).                 
                                                                                
           STRING 'BLK ' F999-BLOCK-NO                                          
                   DELIMITED BY SIZE    INTO WS-BLOCK-INFO (4).                 
                                                                                
           MOVE F999-NME-STREET           TO WS-BLOCK-INFO (5).                 
                                                                                
           MOVE SPACES                    TO WS-BLOCK-INFO (6).                 
                                                                                
           MOVE 'MAXIMUM UNITS ALLOWED FOR: ' TO WS-BLOCK-INFO (7).             
           IF F999-NUM-FLAT-TYPE = '4 '                                         
              MOVE  '4-ROOM'                  TO WS-BLOCK-INFO (8)              
           ELSE                                                                 
              MOVE  '5-ROOM'                  TO WS-BLOCK-INFO (8)              
           END-IF.                                                              
                                                                                
           MOVE F999-NUM-MAX-CAT1   TO WS-NUM-MAX-CAT1                          
           MOVE F999-NUM-MAX-CAT2   TO WS-NUM-MAX-CAT2                          
           MOVE F999-NUM-MAX-CAT3   TO WS-NUM-MAX-CAT3                          
                                                                                
           STRING 'M - '   WS-NUM-MAX-CAT1 ', '                                 
                  'C - '   WS-NUM-MAX-CAT2 ', '                                 
                  'I/O - ' WS-NUM-MAX-CAT3                                      
                  DELIMITED BY SIZE     INTO WS-BLOCK-INFO (9).                 
                                                                                
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3212-GET-ESTATE-NAME.                                                    
      *--------------------------------*                                        
                                                                                
           MOVE SPACES               TO WS-LINK-REC.                            
           IF F999-NUM-SELTY = 'BTO'                                            
              MOVE F999-NUM-ZONE     TO WS-LINK-NUM-ZONE                        
           ELSE                                                                 
              MOVE F999-CDE-NT       TO WS-LINK-NUM-ZONE                        
           END-IF.                                                              
           MOVE F999-NUM-FLAT-TYPE   TO WS-LINK-NUM-FT.                         
           MOVE F999-DTE-BALLOT      TO WS-LINK-DTE-BALLOT.                     
           CALL 'BP13C91J' USING  WS-LINK-REC,                                  
                                  WS-BP13K813-REC,                              
                                  WS-ZONE-FND-FLAG.                             
                                                                                
           IF WS-ZONE-FND-FLAG = 'Y'                                            
              MOVE WS-BP13K813-REC   TO BP13K813-REC                            
              MOVE K813-NME-SITE     TO WS-NME-ESTATE                           
              MOVE K813-NUM-EST      TO WS-NUM-EST                              
           ELSE                                                                 
              MOVE SPACES            TO WS-NME-ESTATE                           
                                        WS-NUM-EST                              
           END-IF.                                                              
                                                                                
       3212-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3213-CHANGE-CNTRCT-TYP.                                                  
      *--------------------------------*                                        
                                                                                
           MOVE SPACES               TO WS-TMP-CNTRCT-TYP.                      
                                                                                
           IF F999-CDE-MODL-DESG NOT = SPACES                                   
              EVALUATE F999-CDE-MODL-DESG                                       
                 WHEN '1IMP'                                                    
                    MOVE 'IMPROVED'         TO WS-TMP-CNTRCT-TYP                
                 WHEN '1PA '                                                    
                    MOVE 'PREMIUM APARTMENTS' TO WS-TMP-CNTRCT-TYP              
                 WHEN '2DBP'                                                    
                    MOVE 'DESIGN & BUILD/DESIGN PLUS'                           
                                            TO WS-TMP-CNTRCT-TYP                
                 WHEN '2MOA'                                                    
                    MOVE 'MODEL A'          TO WS-TMP-CNTRCT-TYP                
                 WHEN '3NG'                                                     
                    MOVE 'NEW GENERATION'   TO WS-TMP-CNTRCT-TYP                
                 WHEN '1STD'                                                    
                 WHEN '3STD'                                                    
                 WHEN '4STD'                                                    
                    MOVE 'STANDARD'         TO WS-TMP-CNTRCT-TYP                
                 WHEN '4SIM'                                                    
                    MOVE 'SIMPLIFIED'       TO WS-TMP-CNTRCT-TYP                
                 WHEN '3STB'                                                    
                    MOVE 'STANDARD (BUDGET)'  TO WS-TMP-CNTRCT-TYP              
              END-EVALUATE                                                      
           ELSE                                                                 
              EVALUATE F999-NUM-CNTRCT-TYP                                      
                WHEN '1PA '                                                     
                   MOVE 'PREMIUM APARTMENTS' TO WS-TMP-CNTRCT-TYP               
                WHEN '2DBP'                                                     
                   MOVE 'DESIGN & BUILD/DESIGN PLUS'                            
                                             TO WS-TMP-CNTRCT-TYP               
                WHEN '3STD'                                                     
                   MOVE 'STANDARD'           TO WS-TMP-CNTRCT-TYP               
                WHEN '3STB'                                                     
                   MOVE 'STANDARD (BUDGET)'  TO WS-TMP-CNTRCT-TYP               
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
       3213-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3214-DECODE-ROOM-ZONE.                                                   
      *--------------------------------*                                        
                                                                                
           MOVE SPACES            TO WS-FLAT-TYPE.                              
           EVALUATE F999-NUM-FLAT-TYPE                                          
           WHEN '1 '                                                            
              MOVE '1-ROOM'       TO WS-FLAT-TYPE                               
           WHEN '1A'                                                            
              MOVE 'STUDIO'       TO WS-FLAT-TYPE                               
           WHEN '2 '                                                            
           WHEN '2F'                                                            
              MOVE '2-ROOM'       TO WS-FLAT-TYPE                               
           WHEN '2A'                                                            
              MOVE 'STUDIO'       TO WS-FLAT-TYPE                               
           WHEN '3 '                                                            
              MOVE '3-ROOM'       TO WS-FLAT-TYPE                               
           WHEN '4 '                                                            
              MOVE '4-ROOM'       TO WS-FLAT-TYPE                               
           WHEN '4D'                                                            
              MOVE 'TYPE-A'       TO WS-FLAT-TYPE                               
           WHEN '5 '                                                            
              MOVE '5-ROOM'       TO WS-FLAT-TYPE                               
           WHEN '5D'                                                            
              MOVE 'TYPE-B'       TO WS-FLAT-TYPE                               
           WHEN 'E '                                                            
              MOVE 'EXEC  '       TO WS-FLAT-TYPE                               
           WHEN 'ED'                                                            
              MOVE 'TYPE-C'       TO WS-FLAT-TYPE                               
           END-EVALUATE.                                                        
                                                                                
           SET WS-MNT-NDX TO 1.                                                 
           SEARCH WS-MONTH-TBL                                                  
              AT END                                                            
                 MOVE SPACES       TO WS-ALLOC-DATE                             
              WHEN F999-DTE-ALLOC (5:2) = WS-MONTH-NUM (WS-MNT-NDX)             
                 MOVE WS-MONTH-NAME (WS-MNT-NDX) TO WS-ALLOC-DATE-MONTH         
                 MOVE F999-DTE-ALLOC (1:4)       TO WS-ALLOC-DATE-YEAR          
           END-SEARCH.                                                          
                                                                                
           MOVE SPACES            TO WS-HEADING-LINE.                           
                                                                                
           MOVE WS-FLAT-TYPE      TO L29J-FLAT-TYP2.                            
           MOVE L29J-HEADER-01    TO WS-HEADING-LINE.                           
           WRITE L29J-PRTREC FROM L29J-HEADER-01 AFTER PAGE.                    
                                                                                
       3214-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3220-GET-REMARKS.                                                        
      *--------------------------------*                                        
                                                                                
                                                                                
           MOVE SPACES     TO WS-REMARKS-PRINTED-SW.                            
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 10                 
              MOVE SPACES       TO WS-REMARKS (WS-SUB)                          
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES          TO WS-SUB.                                      
                                                                                
           IF (F999-CDE-KEY-AVAIL = 'Y') AND                                    
              (F999-DTE-LEASE-COMM NOT = SPACES AND LOW-VALUES)                 
              ADD 1 TO WS-SUB                                                   
              MOVE 'COMPLETED FLAT,'      TO WS-REMARKS(WS-SUB)                 
           ELSE                                                                 
              IF (F999-CDE-KEY-AVAIL = 'Y') AND                                 
                 (F999-DTE-LEASE-COMM = SPACES OR LOW-VALUES)                   
                 ADD 1 TO WS-SUB                                                
                 MOVE 'COMPLETED FLAT'    TO WS-REMARKS(WS-SUB)                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F999-NUM-SELTY = 'SBF'                                            
              IF F999-DTE-PCD NOT = SPACES AND LOW-VALUES                       
                 PERFORM 3223-MOVE-PCD-QTR              THRU 3223-EXIT          
              END-IF                                                            
           ELSE                                                                 
              IF F999-DTE-ECD NOT = SPACES AND LOW-VALUES                       
                 PERFORM 3221-MOVE-ECD-QTR              THRU 3221-EXIT          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F999-DTE-DLVRY-POSSESSION NOT = SPACES AND LOW-VALUES             
              PERFORM 3222-MOVE-DLVRY-POSSESSION     THRU 3222-EXIT             
           END-IF.                                                              
                                                                                
           IF F999-DTE-LEASE-COMM NOT = SPACES AND LOW-VALUES                   
              ADD 2 TO WS-SUB                                                   
              MOVE F999-DTE-LEASE-COMM(5:2) TO WS-NUM-MONTH                     
              PERFORM 3400-GET-MONTH-NAME       THRU 3400-EXIT                  
              STRING 'LCD: '                                                    
                      F999-DTE-LEASE-COMM(7:2) ' '                              
                      WS-NAME-MONTH ' '                                         
                      F999-DTE-LEASE-COMM(1:4)                                  
                      DELIMITED BY SIZE    INTO WS-REMARKS(WS-SUB)              
           END-IF.                                                              
                                                                                
           IF (F999-CDE-OCS-TAG = 'Y') AND                                      
              (F999-NUM-FLAT-TYPE NOT = '1A' AND '2A') AND                      
              (F999-DTE-EXPIRY NOT = SPACES AND LOW-VALUES AND ZEROES)          
              ADD 2 TO WS-SUB                                                   
              MOVE 'WITH OCS OPTION'     TO WS-REMARKS(WS-SUB)                  
           END-IF.                                                              
                                                                                
      **---> GET UPGRADING STATUS                                               
           IF F999-NUM-UPG-STATUS = 'PUG' OR 'TND' OR 'CON'                     
              ADD 1    TO WS-SUB                                                
              MOVE 'MUP - COMMENCING/'   TO WS-REMARKS (WS-SUB)                 
              ADD 1    TO WS-SUB                                                
              MOVE '      IN PROGRESS'   TO WS-REMARKS (WS-SUB)                 
           ELSE                                                                 
              IF F999-NUM-UPG-STATUS = 'CPL' OR 'APP' OR                        
                                       'SEG' OR 'BIL'                           
                 ADD 1    TO WS-SUB                                             
                 MOVE 'MUP - COMPLETED'     TO WS-REMARKS (WS-SUB)              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F999-NUM-MODL-DESG = '75' OR '76'                                 
              ADD 2    TO WS-SUB                                                
              MOVE 'PRIME LOCATION PUBLIC HOUSING' TO WS-REMARKS(WS-SUB)        
           END-IF.                                                              
                                                                                
           PERFORM 322A-GET-BLDG-FTR THRU 322A-EXIT.                            
           IF WS-CAR-LITE-TAG = 'Y'                                             
              ADD 2    TO WS-SUB                                                
              MOVE 'CAR-LITE PRECINCT' TO WS-REMARKS(WS-SUB)                    
           END-IF.                                                              
                                                                                
      *** MOVE THE COUNTER OF THE LAST REMARKS LINE TO WS-MAX-REM               
                                                                                
           MOVE WS-SUB TO WS-MAX-REM.                                           
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       322A-GET-BLDG-FTR.                                                       
      ******************************************************************        
           MOVE SPACES                 TO WS-CRG3-REC.                          
           INITIALIZE                     WS-CRG3-REC.                          
           MOVE SPACES                 TO WS-CAR-LITE-TAG.                      
                                                                                
           PERFORM 322B-GET-BLDG-GL    THRU 322B-EXIT.                          
           MOVE K100-CDE-BLK           TO WS-CRG3-BLDG-GL.                      
                                                                                
           CALL 'BP13CRG3' USING WS-CRG3-REC.                                   
                                                                                
           IF (WS-CRG3-CDE-SYSERR = 0)                                          
              PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 100 OR          
                                                 WS-CAR-LITE-TAG = 'Y'          
                 IF (WS-CRG3-TYPE-FTR(WS-CTR) = 'CAR-LITE'                      
                                             OR 'CAR LITE')                     
                    AND (WS-CRG3-NUM-FTR (WS-CTR) = 'Y')                        
                    MOVE 'Y' TO WS-CAR-LITE-TAG                                 
                 END-IF                                                         
              END-PERFORM                                                       
           ELSE                                                                 
              DISPLAY 'ERROR CALLING BP13CRG3 '                                 
              DISPLAY 'SQL CODE = ' WS-CRG3-CDE-SYSERR                          
              MOVE SPACES                TO WS-CAR-LITE-TAG                     
           END-IF.                                                              
                                                                                
       322A-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       322B-GET-BLDG-GL.                                                        
      *-----------------------------------------------------------              
            MOVE SPACES           TO K110-SCH-ACC-NO.                           
            MOVE F999-NUM-HDB-REF TO K110-SCH-ACC-NO.                           
            READ BM06K110.                                                      
                                                                                
            EVALUATE WS-K110-STATUS                                             
               WHEN 00                                                          
                    PERFORM 322C-READ-BM06K100 THRU 322C-EXIT                   
               WHEN 23                                                          
                    MOVE SPACES TO K100-CDE-BLK                                 
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR : ' WS-K110-STATUS             
                    DISPLAY 'BM06K110 KEY-FLD    : ' K110-SCH-ACC-NO            
                    MOVE WS-K110-STATUS          TO   RETURN-CODE               
                    PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT               
            END-EVALUATE.                                                       
                                                                                
       322B-EXIT.                                                               
           EXIT.                                                                
                                                                                
       322C-READ-BM06K100.                                                      
      *-----------------------------------------------------------              
           MOVE LOW-VALUES           TO K100-KEY-FLD.                           
           MOVE K110-ESTATE          TO K100-ESTATE.                            
           MOVE K110-NEIGHBOURHOOD   TO K100-NEIGHBOURHOOD.                     
           MOVE K110-CONTRACT-NO     TO K100-CONTRACT-NO.                       
           MOVE K110-BLK-NO          TO K100-BLK-NO.                            
                                                                                
           READ BM06K100.                                                       
           EVALUATE WS-K100-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
              MOVE SPACES                TO K100-CDE-BLK                        
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K100 : ' WS-K100-STATUS                
              MOVE WS-K100-STATUS             TO  RETURN-CODE                   
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       322C-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3221-MOVE-ECD-QTR.                                                       
      *-------------------------------------------------------------            
                                                                                
           ADD 2 TO WS-SUB.                                                     
                                                                                
           EVALUATE F999-DTE-ECD(5:2)                                           
              WHEN '01'                                                         
              WHEN '02'                                                         
              WHEN '03'                                                         
                 MOVE '1ST QTR' TO WS-TXT-QTR                                   
              WHEN '04'                                                         
              WHEN '05'                                                         
              WHEN '06'                                                         
                 MOVE '2ND QTR' TO WS-TXT-QTR                                   
              WHEN '07'                                                         
              WHEN '08'                                                         
              WHEN '09'                                                         
                 MOVE '3RD QTR' TO WS-TXT-QTR                                   
              WHEN '10'                                                         
              WHEN '11'                                                         
              WHEN '12'                                                         
                 MOVE '4TH QTR' TO WS-TXT-QTR                                   
           END-EVALUATE.                                                        
                                                                                
           STRING 'ECD: '                                                       
                   WS-TXT-QTR ' '                                               
                   F999-DTE-ECD(1:4)                                            
                   DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB).                 
                                                                                
       3221-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3222-MOVE-DLVRY-POSSESSION.                                              
      *--------------------------------*                                        
                                                                                
           ADD 2 TO WS-SUB.                                                     
           MOVE F999-DTE-DLVRY-POSSESSION(5:2)  TO WS-NUM-MONTH.                
           PERFORM 3400-GET-MONTH-NAME       THRU 3400-EXIT.                    
                                                                                
           IF F999-NUM-BTO-NEW-CONTRACT = 'Y' OR                                
             (F999-NUM-SELTY = 'SBF' OR 'OBF' OR 'SER')                         
              STRING 'EDPD: '                                                   
                     F999-DTE-DLVRY-POSSESSION(7:2) ' '                         
                     WS-NAME-MONTH ' '                                          
                     F999-DTE-DLVRY-POSSESSION(1:4)                             
                     DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB)                
           ELSE                                                                 
              STRING 'DPD: '                                                    
                     F999-DTE-DLVRY-POSSESSION(7:2) ' '                         
                     WS-NAME-MONTH ' '                                          
                     F999-DTE-DLVRY-POSSESSION(1:4)                             
                     DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB)                
           END-IF.                                                              
                                                                                
       3222-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3223-MOVE-PCD-QTR.                                                       
      *-------------------------------------------------------------            
                                                                                
           ADD 2 TO WS-SUB.                                                     
                                                                                
           EVALUATE F999-DTE-PCD(5:2)                                           
              WHEN '01'                                                         
              WHEN '02'                                                         
              WHEN '03'                                                         
                 MOVE '1ST QTR' TO WS-TXT-QTR                                   
              WHEN '04'                                                         
              WHEN '05'                                                         
              WHEN '06'                                                         
                 MOVE '2ND QTR' TO WS-TXT-QTR                                   
              WHEN '07'                                                         
              WHEN '08'                                                         
              WHEN '09'                                                         
                 MOVE '3RD QTR' TO WS-TXT-QTR                                   
              WHEN '10'                                                         
              WHEN '11'                                                         
              WHEN '12'                                                         
                 MOVE '4TH QTR' TO WS-TXT-QTR                                   
           END-EVALUATE.                                                        
                                                                                
           STRING 'PCD: '                                                       
                   WS-TXT-QTR ' '                                               
                   F999-DTE-PCD(1:4)                                            
                   DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB).                 
                                                                                
       3223-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3300-WRITE-DETAILS.                                                      
      *--------------------------------*                                        
                                                                                
           IF C-FIRST-REC-READ                                                  
              PERFORM 3310-PRINT-FIRST-RECORD       THRU 3310-EXIT              
              GO TO 3300-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF C-CHANGE-BLK                                                      
              MOVE SPACES   TO WS-CHANGE-BLK-SW                                 
              MOVE ZEROES   TO WS-SUB                                           
              ADD  1        TO WS-LINE-CTR                                      
              IF WS-LINE-CTR < 54                                               
                 MOVE ZEROES              TO WS-LINE-CTR-A                      
              END-IF                                                            
              IF WS-LINE-CTR > 40                                               
                 PERFORM 4100-PRINT-PAGE-BREAK      THRU 4100-EXIT              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1   TO WS-LINE-CTR.                                              
           PERFORM 4000-CHECK-LINE-CTR           THRU 4000-EXIT.                
                                                                                
           IF NOT C-BLKINFO-PRINTED                                             
              PERFORM 3340-PRINT-BLOCK-INFO      THRU 3340-EXIT                 
           END-IF.                                                              
                                                                                
           IF C-BLKINFO-PRINTED AND                                             
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3350-PRINT-REMARKS         THRU 3350-EXIT                 
           END-IF.                                                              
                                                                                
           IF WS-LINE-CTR-A = 2                                                 
              PERFORM 3360-PRINT-SPACES          THRU 3360-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 3320-MOVE-OTHER-DETAIL        THRU 3320-EXIT.                
                                                                                
           WRITE L29J-PRTREC        FROM L29J-DETAIL-REC AFTER 1.               
           ADD   1  TO WS-PRT-CTR                                               
                       WS-LINE-CTR-A.                                           
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3310-PRINT-FIRST-RECORD.                                                 
      *--------------------------------*                                        
                                                                                
           MOVE 1        TO WS-SUB.                                             
           MOVE WS-BLOCK-INFO (WS-SUB)       TO L29J-BLK-DETAILS.               
           PERFORM 3320-MOVE-OTHER-DETAIL       THRU 3320-EXIT.                 
           WRITE L29J-PRTREC       FROM L29J-DETAIL-REC AFTER 1.                
           ADD  1        TO WS-PRT-CTR.                                         
                                                                                
           ADD  1        TO WS-LINE-CTR                                         
                            WS-LINE-CTR-A.                                      
                                                                                
           MOVE SPACES   TO WS-FIRST-REC-READ-SW                                
                            WS-CHANGE-BLK-SW.                                   
                                                                                
       3310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3320-MOVE-OTHER-DETAIL.                                                  
      *--------------------------------*                                        
                                                                                
           STRING ' ' F999-LEVEL-NO '-'                                         
                  F999-MAIN-UNIT                                                
                  DELIMITED BY SIZE       INTO L29J-NUM-UNIT.                   
                                                                                
           EVALUATE F999-NUM-FLAT-TYPE                                          
               WHEN '4 '                                                        
                    MOVE '(4R)'             TO L29J-NUM-FLAT-CODE               
               WHEN '5 '                                                        
                    MOVE '(5R)'             TO L29J-NUM-FLAT-CODE               
               WHEN '1A'                                                        
               WHEN '2A'                                                        
               WHEN 'SA'                                                        
                    MOVE '(SA)'             TO L29J-NUM-FLAT-CODE               
           END-EVALUATE.                                                        
                                                                                
           IF F999-QTY-AREA-TOTAL = F999-QTY-AREA-ORGNL                         
              MOVE 'NA' TO L29J-FLOOR-AREA-R                                    
              MOVE F999-QTY-AREA-ORGNL      TO L29J-IN-FLOOR-AREA               
           ELSE                                                                 
              MOVE F999-QTY-AREA-TOTAL      TO L29J-FLOOR-AREA                  
              MOVE F999-QTY-AREA-ORGNL      TO L29J-IN-FLOOR-AREA               
              IF F999-NUM-HANDOVER-BFMAY04 = 'Y'                                
                 MOVE L29J-FLOOR-AREA       TO L29J-IN-FLOOR-AREA               
                 MOVE 'NA'                  TO L29J-FLOOR-AREA-R                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F999-AMT-SELLING-PRICE      TO L29J-SELLING-PRICE.              
                                                                                
       3320-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3330-PRINT-REMAINING-INFO.                                               
      *--------------------------------*                                        
                                                                                
           IF C-FIRST-REC-READ                                                  
              GO TO 3330-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES           TO L29J-DETAIL-REC.                            
                                                                                
           IF NOT C-BLKINFO-PRINTED                                             
              PERFORM 3340-PRINT-BLOCK-INFO      THRU 3340-EXIT                 
                UNTIL C-BLKINFO-PRINTED                                         
           END-IF.                                                              
                                                                                
           IF C-BLKINFO-PRINTED AND                                             
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3350-PRINT-REMARKS         THRU 3350-EXIT                 
                UNTIL C-REMARKS-PRINTED                                         
           END-IF.                                                              
                                                                                
       3330-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3340-PRINT-BLOCK-INFO.                                                   
      *--------------------------------*                                        
                                                                                
           ADD  1   TO WS-SUB.                                                  
                                                                                
           IF WS-SUB = 11                                                       
              MOVE 'MAXIMUM UNITS ALLOWED FOR: ' TO                             
                                                 WS-BLOCK-INFO (WS-SUB)         
           END-IF.                                                              
                                                                                
           IF WS-SUB = 12                                                       
              MOVE 'STUDIO APARTMENT'       TO WS-BLOCK-INFO (WS-SUB)           
           END-IF.                                                              
                                                                                
           IF WS-SUB = 13                                                       
              STRING 'M - '   WS-NUM-MAX-CAT1-SA ', '                           
                     'C - '   WS-NUM-MAX-CAT2-SA ', '                           
                     'I/O - ' WS-NUM-MAX-CAT3-SA                                
                  DELIMITED BY SIZE     INTO WS-BLOCK-INFO (WS-SUB)             
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           IF WS-SUB > 13                                                       
              MOVE 'Y'      TO WS-BLKINFO-PRINTED-SW                            
              MOVE ZEROES   TO WS-SUB                                           
              GO TO 3340-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-BLOCK-INFO (WS-SUB)     TO L29J-BLK-DETAILS.                 
                                                                                
           IF C-CHANGE-BLK OR                                                   
              C-END-OF-F999                                                     
              ADD  1   TO WS-LINE-CTR                                           
              PERFORM 4000-CHECK-LINE-CTR      THRU 4000-EXIT                   
              WRITE L29J-PRTREC        FROM L29J-DETAIL-REC AFTER 1             
           END-IF.                                                              
                                                                                
       3340-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3350-PRINT-REMARKS.                                                      
      *--------------------------------*                                        
                                                                                
           ADD  1  TO WS-SUB.                                                   
                                                                                
           IF (WS-SUB > WS-MAX-REM)                                             
              MOVE SPACES         TO L29J-BLK-DETAILS                           
              MOVE 'Y'            TO WS-REMARKS-PRINTED-SW                      
              MOVE ZEROES         TO WS-SUB                                     
              GO TO 3350-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-REMARKS (WS-SUB)        TO L29J-BLK-DETAILS.                 
                                                                                
           IF C-CHANGE-BLK OR                                                   
              C-END-OF-F999                                                     
              ADD  1   TO WS-LINE-CTR                                           
              PERFORM 4000-CHECK-LINE-CTR           THRU 4000-EXIT              
              WRITE L29J-PRTREC        FROM L29J-DETAIL-REC AFTER 1             
           END-IF.                                                              
                                                                                
       3350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3360-PRINT-SPACES.                                                       
      *--------------------------------*                                        
                                                                                
           MOVE ZEROES        TO WS-LINE-CTR-A.                                 
                                                                                
           MOVE SPACES        TO L29J-NUM-UNIT                                  
                                 L29J-NUM-FLAT-CODE.                            
                                                                                
           MOVE ZEROES        TO L29J-FLOOR-AREA                                
                                 L29J-IN-FLOOR-AREA                             
                                 L29J-SELLING-PRICE.                            
                                                                                
           WRITE L29J-PRTREC     FROM L29J-DETAIL-REC AFTER 1                   
                                                                                
           ADD 1       TO WS-LINE-CTR                                           
           PERFORM 4000-CHECK-LINE-CTR        THRU 4000-EXIT                    
           MOVE SPACES        TO L29J-BLK-DETAILS                               
                                                                                
           IF NOT C-BLKINFO-PRINTED                                             
              PERFORM 3340-PRINT-BLOCK-INFO      THRU 3340-EXIT                 
           END-IF.                                                              
                                                                                
           IF C-BLKINFO-PRINTED AND                                             
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3350-PRINT-REMARKS         THRU 3350-EXIT                 
           END-IF.                                                              
                                                                                
       3360-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3400-GET-MONTH-NAME.                                                     
      *--------------------------------*                                        
                                                                                
           SET WS-MNT-NDX TO 1.                                                 
           SEARCH WS-MONTH-TBL                                                  
              AT END                                                            
                 MOVE SPACES     TO WS-NAME-MONTH                               
              WHEN WS-NUM-MONTH = WS-MONTH-NUM (WS-MNT-NDX)                     
                 MOVE WS-MONTH-NAME (WS-MNT-NDX)   TO WS-NAME-MONTH             
           END-SEARCH.                                                          
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4000-CHECK-LINE-CTR.                                                     
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 54  OR                                              
              WS-LINE-CTR = 0                                                   
              PERFORM 4100-PRINT-PAGE-BREAK       THRU 4100-EXIT                
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4100-PRINT-PAGE-BREAK.                                                   
      *--------------------------------*                                        
                                                                                
           PERFORM 3214-DECODE-ROOM-ZONE THRU 3214-EXIT.                        
                                                                                
           MOVE SPACES        TO L29J-PRTREC.                                   
           ADD  1             TO WS-PAGE-CTR.                                   
           MOVE WS-PAGE-CTR   TO L29J-PAGE.                                     
           IF L29J-NME-ESTATE = SPACES                                          
              MOVE F999-NME-ESTATE    TO L29J-NME-ESTATE                        
           END-IF.                                                              
           WRITE L29J-PRTREC     FROM L29J-HEADER-02.                           
           MOVE SPACES        TO L29J-PRTREC.                                   
           WRITE L29J-PRTREC     AFTER 5.                                       
           MOVE 6             TO WS-LINE-CTR.                                   
           MOVE ZEROES        TO WS-LINE-CTR-A.                                 
                                                                                
           IF (F999-NEIGHBOURHOOD = WS-PREV-NEIGHBOURHOOD) AND                  
              (F999-CONTRACT-NO = WS-PREV-CONTRACT-NO) AND                      
              (F999-BLOCK-NO = WS-PREV-BLOCK-NO)                                
              MOVE SPACES     TO WS-BLKINFO-PRINTED-SW                          
                                 WS-REMARKS-PRINTED-SW                          
              MOVE ZEROES     TO WS-SUB                                         
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C29J'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BP13F999 RECORDS READ       : ' WS-F999-READ-CTR.           
           DISPLAY 'NO. OF RECORDS PRINTED      : ' WS-PRT-CTR.                 
                                                                                
           CALL 'C13K813A'         USING WS-RETURN-CODE.                        
                                                                                
           IF WS-RETURN-CODE NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSE ERROR, BP13K813 STATUS ' WS-RETURN-CODE            
           END-IF.                                                              
                                                                                
           MOVE WS-RETURN-CODE         TO WS-C298-RETURN-CODE.                  
                                                                                
           CLOSE BP13F999                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13L29J.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR, WS-K110-STATUS ' WS-K110-STATUS             
              MOVE WS-K110-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR, WS-K100-STATUS ' WS-K100-STATUS             
              MOVE WS-K100-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
