       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C817.                                                 
      *AUTHOR.        RAYATHATHA ASHOK.                                         
      *DATE-WRITTEN.  03/07/92.                                                 
      *DATE-COMPILED.                                                           
      *REMARKS.                                                                 
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVE   : MATCH THE RELEVANT REQUEST FOR THAT   *                
      *                  ROUND OF PIGEONHOLING, OUTPUT SUCC    *                
      *                  AND UNSUCC REQUEST FOR BP13C522 AND   *                
      *                  BP13C530 PRINTING REPORTS.            *                
      *                                                        *                
      *     INPUT FILE :                                       *                
      *                  1. BP13F510 ( REQ FOR PIGEONHOLING)   *                
      *                  1. BP13F520 ( BALLOT FILE  )          *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *                  1. P13UNSUC (UNSUCC REQ)              *                
      *                  2. P13SUCC  (SUCC REQ)                *                
      *                                                        *                
      * CHG-NO  BY    ON     DESCRIPTION                       *                
      * --------------------------------                       *                
      * C950651 LSB 950705 - CHG SEQUENTIAL MATCH INSTEAD OF   *                
      *                      READ VSAM FILE BP13K520           *                
      *BP130652 NOR 990220 - EXPAND F510 & F520 RECL 300 TO 500*                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F510 ASSIGN TO  BP13F510.                                 
                                                                                
           SELECT BP13F520 ASSIGN TO  BP13F520.                                 
                                                                                
           SELECT P13UNSUC ASSIGN TO  P13UNSUC.                                 
                                                                                
           SELECT P13SUCC  ASSIGN TO  P13SUCC.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F520                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F520.                                                           
                                                                                
       FD  BP13F510                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F510.                                                           
                                                                                
       FD  P13UNSUC                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       01  P13UNSUC-REC                     PIC X(500).                         
                                                                                
       FD  P13SUCC                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       01  P13SUCC-REC                      PIC X(500).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-UNSUC-WRITE                   PIC 9(07) VALUE ZEROS.              
       01  WS-SUCC-WRITE                    PIC 9(07) VALUE ZEROS.              
       01  WS-BP13F510-READ                 PIC 9(07) VALUE ZEROS.              
       01  WS-BP13F520-READ                 PIC 9(07) VALUE ZEROS.              
       01  WS-F510-EOF                      PIC X(01) VALUE 'N'.                
           88 F510-EOF                          VALUE 'Y'.                      
       01  WS-F520-EOF                      PIC X(01) VALUE 'N'.                
           88 F520-EOF                          VALUE 'Y'.                      
                                                                                
      *-----------------*                                                       
       PROCEDURE DIVISION.                                                      
      *-----------------*                                                       
                                                                                
      **********                                                                
       000-MAIN.                                                                
      **********                                                                
                                                                                
           PERFORM 100-OPEN-FILE          THRU 100-EXIT.                        
           PERFORM 200-READ-BP13F510      THRU 200-EXIT.                        
           PERFORM 300-READ-BP13F520      THRU 300-EXIT.                        
           PERFORM 400-PROCESS-FILES      THRU 400-EXIT                         
              UNTIL F510-NUM-REGN = HIGH-VALUES AND                             
                    F520-NUM-REGN = HIGH-VALUES                                 
           PERFORM 900-CLOSE-ROUTINE      THRU 900-EXIT.                        
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***************                                                           
       100-OPEN-FILE.                                                           
      ***************                                                           
                                                                                
           OPEN INPUT  BP13F510                                                 
                       BP13F520                                                 
               OUTPUT  P13UNSUC                                                 
                       P13SUCC.                                                 
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       200-READ-BP13F510.                                                       
      *******************                                                       
                                                                                
           READ BP13F510                 AT END                                 
           MOVE HIGH-VALUES              TO F510-NUM-REGN                       
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-BP13F510-READ.                   
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       300-READ-BP13F520.                                                       
      *******************                                                       
                                                                                
           READ BP13F520                 AT END                                 
           MOVE HIGH-VALUES              TO F520-NUM-REGN                       
           GO TO 300-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-BP13F520-READ.                   
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************                                                    
       400-PROCESS-FILES.                                                       
      **********************                                                    
                                                                                
           IF F510-NUM-REGN = F520-NUM-REGN                                     
              IF F520-CDE-SUCC-NT-ZONE = SPACES OR LOW-VALUES                   
                 WRITE P13UNSUC-REC   FROM  BP13F520-REC                        
                 ADD 1                TO    WS-UNSUC-WRITE                      
              ELSE                                                              
                 WRITE P13SUCC-REC    FROM  BP13F520-REC                        
                 ADD 1                TO    WS-SUCC-WRITE                       
              END-IF                                                            
              PERFORM 200-READ-BP13F510      THRU 200-EXIT                      
              PERFORM 300-READ-BP13F520      THRU 300-EXIT                      
           ELSE                                                                 
           IF F510-NUM-REGN < F520-NUM-REGN                                     
              PERFORM 200-READ-BP13F510      THRU 200-EXIT                      
           ELSE                                                                 
              PERFORM 300-READ-BP13F520      THRU 300-EXIT.                     
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       900-CLOSE-ROUTINE.                                                       
      *******************                                                       
                                                                                
            DISPLAY ' '.                                                        
            DISPLAY ' *************** BP13C817 *************** '.               
            DISPLAY ' * DETERMINE SUCC/UNSUCC BALLOT REQUESTS* '.               
            DISPLAY ' '.                                                        
            DISPLAY ' NO OF REQUEST READ FROM BP13F510       : '                
                                              WS-BP13F510-READ.                 
            DISPLAY ' NO OF BALLOT  READ FROM BP13F520       : '                
                                              WS-BP13F520-READ.                 
            DISPLAY ' NO OF UNSUCC REQUEST IN PIGEONHOLING   : '                
                                              WS-UNSUC-WRITE.                   
            DISPLAY ' NO OF SUCC REQUEST IN PIGEONHOLING     : '                
                                              WS-SUCC-WRITE.                    
            DISPLAY ' '.                                                        
                                                                                
            CLOSE BP13F520                                                      
                  BP13F510                                                      
                  P13UNSUC                                                      
                  P13SUCC.                                                      
                                                                                
            STOP RUN.                                                           
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
