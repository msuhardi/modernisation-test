       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD17.                                                 
      *AUTHOR.        ED1.                                                      
      *DATE-WRITTEN.  05/03/2008.                                               
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE : UPDATE MASTER FOR AUTO-CEASED CPF PAYMENTS      *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE       BY   DESCRIPTION                        *           
      * -------- ---------- ---- -----------                        *           
      * BP133209 05/03/2008 ED1  NEW PRGM                           *           
      * BP133314 17/06/2008 ED1  ADD BP13KD38 UPDATE                *           
      * BP133360 28/08/2008 ED1  CHGE HEADER INTO EFF-CEASE         *           
      * BP135093 19/12/2013 IMC1 CHANGE B04F041 TO AB02F541         *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13FD00          ASSIGN BP13FD00.                            
           SELECT AB02F541          ASSIGN AB02F541.                            
           SELECT SY02F001          ASSIGN SY02F001.                            
           SELECT BP13LD17          ASSIGN BP13LD17.                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K800-NUM-REGN                                   
                FILE STATUS  IS K800-STATUS.                                    
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS DYNAMIC                                         
                RECORD KEY   IS K820-KEY-FLD                                    
                FILE STATUS  IS K820-STATUS.                                    
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD00-KEY-FLD                                    
                FILE STATUS  IS KD00-STATUS.                                    
                                                                                
           SELECT BP13KD35 ASSIGN TO BP13KD35                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD35-KEY-FLD                                    
                FILE STATUS  IS KD35-STATUS.                                    
                                                                                
           SELECT BP13KD38 ASSIGN TO BP13KD38                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS DYNAMIC                                         
                RECORD KEY   IS KD38-KEY-FLD                                    
                FILE STATUS  IS KD38-STATUS.                                    
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13FD00                RECORD CONTAINS 1600 CHARACTERS             
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13FD00.                                                           
                                                                                
       COPY AB02F541.                                                           
                                                                                
       FD   BP13K800                RECORD CONTAINS 2000 CHARACTERS.            
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                RECORD CONTAINS  400 CHARACTERS.            
       COPY BP13K820.                                                           
                                                                                
       FD   BP13KD00                RECORD CONTAINS 1600 CHARACTERS.            
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD35                RECORD CONTAINS 3500 CHARACTERS.            
       COPY BP13KD35.                                                           
                                                                                
       FD   BP13KD38                RECORD CONTAINS 400  CHARACTERS.            
       COPY BP13KD38.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13LD17    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 132 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  LD17-PRTREC  PIC X(132).                                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  K800-STATUS                  PIC 9(02)  VALUE 00.                
           05  K820-STATUS                  PIC 9(02)  VALUE 00.                
           05  KD00-STATUS                  PIC 9(02)  VALUE 00.                
           05  KD35-STATUS                  PIC 9(02)  VALUE 00.                
           05  KD38-STATUS                  PIC 9(02)  VALUE 00.                
           05  WS-FD00-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-F541-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-UNMAT                     PIC 9(05)  VALUE ZEROS.             
           05  WS-FD00-UNMATCH              PIC 9(05)  VALUE ZEROS.             
           05  WS-F541-BYPASS               PIC 9(05)  VALUE ZEROS.             
           05  WS-F541-UNMAT                PIC 9(05)  VALUE ZEROS.             
           05  WS-KD00-UPD                  PIC 9(05)  VALUE ZEROS.             
           05  WS-KD35-UPD                  PIC 9(05)  VALUE ZEROS.             
           05  WS-KD38-UPD                  PIC 9(05)  VALUE ZEROS.             
           05  WS-K820-EOF                  PIC X      VALUE 'N'.               
           05  WS-KD38-EOF                  PIC X      VALUE 'N'.               
                                                                                
       01  WS-FD00-KEY                      PIC X(11) VALUE SPACES.             
       01  WS-F541-KEY                      PIC X(11) VALUE SPACES.             
       01  WS-MATCHED-REC.                                                      
           05  WS-MATCHED-CPF               PIC X(09) VALUE SPACES.             
           05  WS-MATCHED-CPF-NRIC          PIC X(09) VALUE SPACES.             
           05  WS-MATCHED-NUM-NRIC          PIC 9(01) VALUE ZERO.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-YY                    PIC X(04)  VALUE SPACES.            
           05  WS-SYS-MM                    PIC X(02)  VALUE SPACES.            
           05  WS-SYS-DD                    PIC X(02)  VALUE SPACES.            
                                                                                
       01  WS-IDX                           PIC 9 VALUE ZEROS.                  
       01  WS-KD38-REC.                                                         
           05 WS-KD38-KEY            PIC X(13) VALUE SPACES                     
                                     OCCURS 5.                                  
       01  WS-PRINT-COUNTERS.                                                   
           05 WS-CNT-LD17-WRITE      PIC 9(7) VALUE ZEROES.                     
           05 WS-SNO                 PIC 9(5) VALUE ZEROES.                     
           05 WS-PAGE-CTR            PIC 9(4) VALUE ZEROES.                     
           05 WS-LINE-CTR            PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-DATE-HDR               PIC X(10) VALUE SPACES.                    
       01  WS-MAX-LINE               PIC 99    VALUE 55.                        
       01  WS-AMT-CPF-INST           PIC 9(6)V99 VALUE ZEROS.                   
                                                                                
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                        01010000
       01  WS-BLANK-LINE  PIC X(132)             VALUE SPACES.                  
                                                                                
       01  WS-DASH-LINE   PIC X(132)             VALUE ALL '-'.                 
                                                                                
       01  LD17-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LD17'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LD17-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LD17-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LD17-HEADER02.                                                       
           05  FILLER              PIC X(36)     VALUE SPACES.                  
           05  FILLER              PIC X(54)     VALUE                          
               'REPORT ON UPDATE CPF RATE/STATUS (AUTO-CEASED BY CPFB)'.        
                                                                                
       01  LD17-HEADER03.                                                       
           05  FILLER              PIC X(22)     VALUE SPACES.                  
           05  FILLER              PIC X(58)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
               'R  E  V  I  S  E  D '.                                          
                                                                                
       01  LD17-HEADER04.                                                       
           05  FILLER              PIC X(44)     VALUE                          
               'S/N   SCH-ACC-NO       CPF NO.     EFF-CEASE'.                  
           05  FILLER              PIC X(48)     VALUE                          
               '    REGN NO.     CPF         TOTAL CPF      CASH'.              
           05  FILLER              PIC X(14)     VALUE                          
               '      CPF STAT'.                                                
                                                                                
       01 LD17-DETAILS.                                                         
          05 LD17-DTL-SERIAL      PIC ZZZZ9     VALUE ZEROES.                   
          05 FILLER               PIC X(01)     VALUE SPACES.                   
          05 LD17-SCH-ACC         PIC X(14)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LD17-CPFNO           PIC X(09)     VALUE SPACES.                   
          05 FILLER               PIC X(05)     VALUE SPACES.                   
          05 LD17-DTE-PD          PIC X(08)     VALUE SPACES.                   
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 LD17-REGNO           PIC X(08)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LD17-CPF-INST        PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(04)     VALUE SPACES.                   
          05 LD17-CPF-TOT         PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LD17-CPF-CASH        PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(07)     VALUE SPACES.                   
          05 LD17-CPF-STAT        PIC X(01)     VALUE SPACES.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ***************************                                               
       0000-MAIN.                                                               
      ***************************                                               
           PERFORM 1000-OPEN-FILES           THRU  1000-EXIT.                   
           PERFORM 2000-READ-BP13FD00        THRU  2000-EXIT.                   
           PERFORM 2010-READ-AB02F541        THRU  2010-EXIT.                   
           PERFORM 3000-PROCESS              THRU  3000-EXIT                    
             UNTIL WS-FD00-KEY = HIGH-VALUES AND                                
                   WS-F541-KEY = HIGH-VALUES.                                   
           PERFORM 9000-CLOSE-FILES          THRU  9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       1000-OPEN-FILES.                                                         
      ***************************                                               
           OPEN INPUT  BP13FD00                                                 
                       AB02F541                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       SY02F001                                                 
                I-O    BP13KD00                                                 
                       BP13KD35                                                 
                       BP13KD38                                                 
                OUTPUT BP13LD17.                                                
                                                                                
           READ SY02F001.                                                       
                                                                                
           MOVE F001-DTE-CURRENT  TO WS-SYSTEM-DATE.                            
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13K800 FILE ERROR ' K800-STATUS                
           END-IF.                                                              
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13K820 FILE ERROR ' K820-STATUS                
           END-IF.                                                              
           IF KD00-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13KD00 FILE ERROR ' KD00-STATUS                
           END-IF.                                                              
           IF KD35-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13KD35 FILE ERROR ' KD35-STATUS                
           END-IF.                                                              
           IF KD38-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13KD38 FILE ERROR ' KD38-STATUS                
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       2000-READ-BP13FD00.                                                      
      ***************************                                               
           READ BP13FD00                                                        
             AT END                                                             
                MOVE HIGH-VALUES            TO  WS-FD00-KEY                     
            NOT AT END                                                          
                MOVE FD00-NUM-SCH-ACCNT     TO  WS-FD00-KEY                     
                ADD   1                     TO  WS-FD00-READ                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       2010-READ-AB02F541.                                                      
      ***************************                                               
           READ AB02F541                                                        
             AT END                                                             
                MOVE HIGH-VALUES            TO  WS-F541-KEY                     
            NOT AT END                                                          
                MOVE F541-HDB-REF(2:11)     TO  WS-F541-KEY                     
                ADD  1                      TO  WS-F541-READ                    
           END-READ.                                                            
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       3000-PROCESS.                                                            
      ***************************                                               
           IF WS-FD00-KEY = WS-F541-KEY                                         
              PERFORM 4000-PROCESS-MATCHED        THRU  4000-EXIT               
              PERFORM 2000-READ-BP13FD00          THRU  2000-EXIT               
              PERFORM 2010-READ-AB02F541          THRU  2010-EXIT               
           ELSE                                                                 
              IF WS-FD00-KEY > WS-F541-KEY                                      
                 ADD 1 TO WS-F541-UNMAT                                         
                 PERFORM 2010-READ-AB02F541       THRU  2010-EXIT               
              ELSE                                                              
                 PERFORM 2000-READ-BP13FD00       THRU  2000-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       4000-PROCESS-MATCHED.                                                    
      ***************************                                               
           MOVE 'N'             TO WS-K820-EOF.                                 
           MOVE SPACES          TO WS-MATCHED-REC.                              
           INITIALIZE              WS-MATCHED-REC.                              
           MOVE SPACES          TO K820-KEY-FLD.                                
           MOVE FD00-NUM-REGN   TO K820-NUM-REGN.                               
                                                                                
           START BP13K820 KEY IS >= K820-KEY-FLD.                               
                                                                                
           IF K820-STATUS = 00 OR 02                                            
              PERFORM 4100-READNEXT-BP13K820                                    
                      UNTIL WS-K820-EOF = 'Y'                                   
           ELSE                                                                 
                                                                                
              ADD 1 TO WS-F541-BYPASS                                           
                                                                                
              IF K820-STATUS = 23                                               
                 DISPLAY 'BP13K820 RECORD NOT FOUND ' K820-KEY-FLD              
              ELSE                                                              
                 DISPLAY 'BP13K820 ERROR IN STARTBR ' K820-KEY-FLD              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-MATCHED-CPF-NRIC NOT = SPACES AND LOW-VALUES AND ZEROS         
              PERFORM 4200-READ-BP13K800  THRU 4200-EXIT                        
              PERFORM 4300-READ-BP13KD00  THRU 4300-EXIT                        
              PERFORM 4400-PROCESS-UPDATE THRU 4400-EXIT                        
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       4100-READNEXT-BP13K820.                                                  
      ***************************                                               
           READ BP13K820 NEXT AT END                                            
                MOVE 'Y'                  TO WS-K820-EOF                        
                GO TO 4100-EXIT.                                                
                                                                                
           IF K820-STATUS = 00 OR 02                                            
              IF K820-NUM-REGN   = FD00-NUM-REGN                                
                 IF K820-NUM-CPF = F541-CPF-ACC                                 
                    MOVE K820-NUM-CPF  TO WS-MATCHED-CPF                        
                    MOVE K820-NUM-NRIC TO WS-MATCHED-CPF-NRIC                   
                    MOVE 'Y' TO WS-K820-EOF                                     
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y' TO WS-K820-EOF                                        
              END-IF                                                            
                                                                                
           ELSE                                                                 
                                                                                
              MOVE 'Y' TO WS-K820-EOF                                           
                                                                                
              IF K820-STATUS = 23                                               
                 DISPLAY 'BP13K820 RECORD NOT FOUND ' K820-KEY-FLD              
                 ADD 1 TO WS-F541-BYPASS                                        
              ELSE                                                              
                 DISPLAY 'BP13K820 ERROR IN READNXT ' K820-KEY-FLD              
                 ADD 1 TO WS-F541-BYPASS                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************                                               
       4200-READ-BP13K800.                                                      
      ***************************                                               
           MOVE FD00-NUM-REGN   TO  K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 00                                                  
              CONTINUE                                                          
           ELSE                                                                 
                                                                                
             ADD 1 TO WS-F541-BYPASS                                            
                                                                                
             IF K800-STATUS = 23                                                
                DISPLAY 'REC NOT FND IN K800, STATUS '                          
                K800-STATUS ', REGN IS  '    K800-NUM-REGN                      
             ELSE                                                               
                DISPLAY 'ERROR READING K800, STATUS ' K800-STATUS               
                        ', REGN IS  '    K800-NUM-REGN                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************                                               
       4300-READ-BP13KD00.                                                      
      ***************************                                               
           MOVE FD00-NUM-REGN   TO  KD00-NUM-REGN.                              
                                                                                
           READ BP13KD00.                                                       
                                                                                
           IF KD00-STATUS = 00                                                  
              IF KD00-AMT-INSTAL-CASH IS NOT NUMERIC                            
                 MOVE 0 TO KD00-AMT-INSTAL-CASH                                 
              END-IF                                                            
              IF KD00-AMT-INSTAL-CPF1 IS NOT NUMERIC                            
                 MOVE 0 TO KD00-AMT-INSTAL-CPF1                                 
              END-IF                                                            
              IF KD00-AMT-INSTAL-CPF2 IS NOT NUMERIC                            
                 MOVE 0 TO KD00-AMT-INSTAL-CPF2                                 
              END-IF                                                            
              IF KD00-AMT-INSTAL-CPF3 IS NOT NUMERIC                            
                 MOVE 0 TO KD00-AMT-INSTAL-CPF3                                 
              END-IF                                                            
              IF KD00-AMT-INSTAL-CPF4 IS NOT NUMERIC                            
                 MOVE 0 TO KD00-AMT-INSTAL-CPF4                                 
              END-IF                                                            
              IF KD00-AMT-CPF-RATE    IS NOT NUMERIC                            
                 MOVE 0 TO KD00-AMT-CPF-RATE                                    
              END-IF                                                            
                                                                                
           ELSE                                                                 
                                                                                
             ADD 1 TO WS-F541-BYPASS                                            
                                                                                
             IF KD00-STATUS = 23                                                
                DISPLAY 'REC NOT FND IN KD00, STATUS '                          
                KD00-STATUS ', REGN IS  '    KD00-NUM-REGN                      
             ELSE                                                               
                DISPLAY 'ERROR READING KD00, STATUS ' KD00-STATUS               
                        ', REGN IS  '    KD00-NUM-REGN                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************                                               
       4400-PROCESS-UPDATE.                                                     
      ***************************                                               
           MOVE ZEROS TO WS-AMT-CPF-INST.                                       
           IF K800-STATUS = 00 AND KD00-STATUS = 00                             
                                                                                
              EVALUATE WS-MATCHED-CPF-NRIC                                      
              WHEN K800-NUM-NRIC1                                               
                   MOVE KD00-AMT-INSTAL-CPF1 TO WS-AMT-CPF-INST                 
                   ADD  KD00-AMT-INSTAL-CPF1 TO KD00-AMT-INSTAL-CASH            
                   COMPUTE KD00-AMT-CPF-RATE =                                  
                           KD00-AMT-CPF-RATE - KD00-AMT-INSTAL-CPF1             
                   MOVE 0 TO KD00-AMT-INSTAL-CPF1                               
                   MOVE 1 TO WS-MATCHED-NUM-NRIC                                
                   PERFORM 4500-REWRITE-BP13KD00  THRU 4500-EXIT                
                   PERFORM 4600-UPDATE-BP13KD35   THRU 4600-EXIT                
                   PERFORM 5000-PRINT-REPORT      THRU 5000-EXIT                
                   PERFORM 8000-STARTBR-BP13KD38  THRU 8000-EXIT                
                   PERFORM 8200-UPDATE-BP13KD38   THRU 8200-EXIT                
                                                                                
              WHEN K800-NUM-NRIC2                                               
                   MOVE KD00-AMT-INSTAL-CPF2 TO WS-AMT-CPF-INST                 
                   ADD  KD00-AMT-INSTAL-CPF2 TO KD00-AMT-INSTAL-CASH            
                   COMPUTE KD00-AMT-CPF-RATE =                                  
                           KD00-AMT-CPF-RATE - KD00-AMT-INSTAL-CPF2             
                   MOVE 0 TO KD00-AMT-INSTAL-CPF2                               
                   MOVE 2 TO WS-MATCHED-NUM-NRIC                                
                   PERFORM 4500-REWRITE-BP13KD00  THRU 4500-EXIT                
                   PERFORM 4600-UPDATE-BP13KD35   THRU 4600-EXIT                
                   PERFORM 5000-PRINT-REPORT      THRU 5000-EXIT                
                   PERFORM 8000-STARTBR-BP13KD38  THRU 8000-EXIT                
                   PERFORM 8200-UPDATE-BP13KD38   THRU 8200-EXIT                
                                                                                
              WHEN K800-NUM-NRIC3                                               
                   MOVE KD00-AMT-INSTAL-CPF3 TO WS-AMT-CPF-INST                 
                   ADD  KD00-AMT-INSTAL-CPF3 TO KD00-AMT-INSTAL-CASH            
                   COMPUTE KD00-AMT-CPF-RATE =                                  
                           KD00-AMT-CPF-RATE - KD00-AMT-INSTAL-CPF3             
                   MOVE 0 TO KD00-AMT-INSTAL-CPF3                               
                   MOVE 3 TO WS-MATCHED-NUM-NRIC                                
                   PERFORM 4500-REWRITE-BP13KD00  THRU 4500-EXIT                
                   PERFORM 4600-UPDATE-BP13KD35   THRU 4600-EXIT                
                   PERFORM 5000-PRINT-REPORT      THRU 5000-EXIT                
                   PERFORM 8000-STARTBR-BP13KD38  THRU 8000-EXIT                
                   PERFORM 8200-UPDATE-BP13KD38   THRU 8200-EXIT                
                                                                                
              WHEN K800-NUM-NRIC4                                               
                   MOVE KD00-AMT-INSTAL-CPF4 TO WS-AMT-CPF-INST                 
                   ADD  KD00-AMT-INSTAL-CPF4 TO KD00-AMT-INSTAL-CASH            
                   COMPUTE KD00-AMT-CPF-RATE =                                  
                           KD00-AMT-CPF-RATE - KD00-AMT-INSTAL-CPF4             
                   MOVE 0 TO KD00-AMT-INSTAL-CPF4                               
                   MOVE 4 TO WS-MATCHED-NUM-NRIC                                
                   PERFORM 4500-REWRITE-BP13KD00  THRU 4500-EXIT                
                   PERFORM 4600-UPDATE-BP13KD35   THRU 4600-EXIT                
                   PERFORM 5000-PRINT-REPORT      THRU 5000-EXIT                
                   PERFORM 8000-STARTBR-BP13KD38  THRU 8000-EXIT                
                   PERFORM 8200-UPDATE-BP13KD38   THRU 8200-EXIT                
                                                                                
              WHEN OTHER                                                        
                   DISPLAY 'NO MATCH FOUND IN BP13K800'                         
                                     WS-MATCHED-CPF-NRIC                        
                   ADD 1 TO WS-F541-BYPASS                                      
              END-EVALUATE                                                      
                                                                                
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************                                               
       4500-REWRITE-BP13KD00.                                                   
      ***************************                                               
           IF KD00-AMT-CPF-RATE = ZEROS                                         
              MOVE 0 TO KD00-CDE-CPF-STATUS                                     
           END-IF.                                                              
                                                                                
           MOVE 'BP13CD17'               TO KD00-NUM-USERID.                    
           MOVE F001-DTE-CURRENT         TO KD00-DTE-REVSD-INSTAL-RATE.         
           MOVE F001-DTE-CURRENT         TO KD00-DTE-UPDATE.                    
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD00-TME-UPDATE.                  
                                                                                
           REWRITE BP13KD00-REC.                                                
                                                                                
           IF KD00-STATUS = 00                                                  
              ADD 1    TO WS-KD00-UPD                                           
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING KD00, STATUS ' KD00-STATUS               
                      ', REGN IS  '    KD00-NUM-REGN                            
           END-IF.                                                              
                                                                                
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************                                               
       4600-UPDATE-BP13KD35.                                                    
      ***************************                                               
           PERFORM 4610-READ-BP13KD35    THRU 4610-EXIT.                        
                                                                                
           IF KD35-STATUS NOT = 00                                              
              GO TO 4600-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE KD35-NUM-INSTAL-RATE-CPF1 TO                                    
                KD35-NUM-INSTAL-RATE-CPF1-PREV.                                 
           MOVE KD35-NUM-INSTAL-RATE-CPF2 TO                                    
                KD35-NUM-INSTAL-RATE-CPF2-PREV.                                 
           MOVE KD35-NUM-INSTAL-RATE-CPF3 TO                                    
                KD35-NUM-INSTAL-RATE-CPF3-PREV.                                 
           MOVE KD35-NUM-INSTAL-RATE-CPF4 TO                                    
                KD35-NUM-INSTAL-RATE-CPF4-PREV.                                 
                                                                                
           EVALUATE WS-MATCHED-NUM-NRIC                                         
           WHEN 1                                                               
              ADD KD35-NUM-INSTAL-RATE-CPF1 TO KD35-NUM-INSTALMENT-CASH         
              MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF1                               
           WHEN 2                                                               
              ADD KD35-NUM-INSTAL-RATE-CPF2 TO KD35-NUM-INSTALMENT-CASH         
              MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF2                               
           WHEN 3                                                               
              ADD KD35-NUM-INSTAL-RATE-CPF3 TO KD35-NUM-INSTALMENT-CASH         
              MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF3                               
           WHEN 4                                                               
              ADD KD35-NUM-INSTAL-RATE-CPF4 TO KD35-NUM-INSTALMENT-CASH         
              MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF4                               
           END-EVALUATE.                                                        
                                                                                
           PERFORM 4700-REWRITE-BP13KD35 THRU 4700-EXIT.                        
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************                                               
       4610-READ-BP13KD35.                                                      
      ***************************                                               
           MOVE FD00-NUM-REGN   TO  KD35-NUM-REGN.                              
                                                                                
           READ BP13KD35.                                                       
                                                                                
           IF KD35-STATUS = 00                                                  
              IF KD35-NUM-INSTALMENT-CASH IS NOT NUMERIC                        
                 MOVE 0 TO KD35-NUM-INSTALMENT-CASH                             
              END-IF                                                            
              IF KD35-NUM-INSTAL-RATE-CPF1 IS NOT NUMERIC                       
                 MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF1                            
              END-IF                                                            
              IF KD35-NUM-INSTAL-RATE-CPF2 IS NOT NUMERIC                       
                 MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF2                            
              END-IF                                                            
              IF KD35-NUM-INSTAL-RATE-CPF3 IS NOT NUMERIC                       
                 MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF3                            
              END-IF                                                            
              IF KD35-NUM-INSTAL-RATE-CPF4 IS NOT NUMERIC                       
                 MOVE 0 TO KD35-NUM-INSTAL-RATE-CPF4                            
              END-IF                                                            
                                                                                
           ELSE                                                                 
                                                                                
             IF KD35-STATUS = 23                                                
                DISPLAY 'REC NOT FND IN KD35, STATUS '                          
                KD35-STATUS ', REGN IS  '    KD35-NUM-REGN                      
             ELSE                                                               
                DISPLAY 'ERROR READING KD35, STATUS ' KD35-STATUS               
                        ', REGN IS  '    KD35-NUM-REGN                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4610-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************                                               
       4700-REWRITE-BP13KD35.                                                   
      ***************************                                               
           MOVE 'BP13CD17'              TO KD35-NUM-USERID.                     
           MOVE F001-DTE-CURRENT        TO KD35-DTE-UPDATE.                     
           MOVE F001-DTE-CURRENT        TO KD35-DTE-INSTAL-RATE-NEW-EFF.        
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD35-TME-UPDATE.                  
                                                                                
           REWRITE BP13KD35-REC.                                                
                                                                                
           IF KD35-STATUS = 00                                                  
              ADD 1    TO WS-KD35-UPD                                           
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING KD35, STATUS ' KD35-STATUS               
                      ', REGN IS  '    KD35-NUM-REGN                            
           END-IF.                                                              
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************                                               
       5000-PRINT-REPORT.                                                       
      ***************************                                               
           MOVE SPACES TO LD17-DETAILS.                                         
           INITIALIZE     LD17-DETAILS.                                         
                                                                                
           MOVE K800-NUM-REGN        TO  LD17-REGNO.                            
           MOVE F541-CPF-ACC         TO  LD17-CPFNO.                            
           MOVE WS-AMT-CPF-INST      TO  LD17-CPF-INST.                         
           MOVE KD00-AMT-CPF-RATE    TO  LD17-CPF-TOT.                          
           MOVE KD00-AMT-INSTAL-CASH TO  LD17-CPF-CASH.                         
           MOVE KD00-CDE-CPF-STATUS  TO  LD17-CPF-STAT.                         
                                                                                
           STRING WS-F541-KEY(1:4) '-'                                          
                  WS-F541-KEY(5:4) '-'                                          
                  WS-F541-KEY(9:1) '-'                                          
                  WS-F541-KEY(10:2)                                             
                  DELIMITED BY SIZE INTO LD17-SCH-ACC.                          
                                                                                
           STRING F541-LAST-PAID-MM '/'                                         
                  F541-LAST-PAID-CC F541-LAST-PAID-YY                           
              DELIMITED BY SIZE    INTO  LD17-DTE-PD                            
           END-STRING.                                                          
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
              DELIMITED BY SIZE    INTO  LD17-DATE-HDR                          
           END-STRING.                                                          
                                                                                
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  LD17-DTL-SERIAL.                    
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO LD17-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO LD17-PAGE-HDR                               
              PERFORM 6000-PRINT-HEADING THRU 6000-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LD17-PRTREC FROM LD17-DETAILS.                                 
           ADD   1  TO  WS-LINE-CTR.                                            
           ADD   1  TO  WS-CNT-LD17-WRITE.                                      
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       6000-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
                                                                                
            WRITE LD17-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LD17-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LD17-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LD17-PRTREC FROM LD17-HEADER01 AFTER PAGE.                    
            WRITE LD17-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LD17-PRTREC FROM LD17-HEADER02.                               
            WRITE LD17-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LD17-PRTREC FROM LD17-HEADER03.                               
            WRITE LD17-PRTREC FROM LD17-HEADER04.                               
            WRITE LD17-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE 10 TO WS-LINE-CTR.                                             
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ***************************                                               
       8000-STARTBR-BP13KD38.                                                   
      ***************************                                               
           MOVE 'N'             TO WS-KD38-EOF.                                 
           MOVE ZEROS           TO WS-IDX.                                      
           MOVE SPACES          TO WS-KD38-REC.                                 
           INITIALIZE              KD38-KEY-FLD.                                
           MOVE FD00-NUM-REGN   TO KD38-KEY-FLD.                                
                                                                                
           START BP13KD38 KEY IS >= KD38-KEY-FLD.                               
                                                                                
           IF KD38-STATUS = 00 OR 02                                            
              PERFORM 8100-READNXT-KD38                                         
                      UNTIL KD38-NUM-REGN NOT = FD00-NUM-REGN                   
                         OR WS-KD38-EOF = 'Y'                                   
           ELSE                                                                 
              IF KD38-STATUS = 23                                               
                 CONTINUE                                                       
              ELSE                                                              
                 DISPLAY 'BP13KD38 ERROR IN STARTBR ' KD38-KEY-FLD              
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ***************************                                               
       8100-READNXT-KD38.                                                       
      ***************************                                               
           READ BP13KD38 NEXT AT END                                            
                MOVE 'Y'                  TO WS-KD38-EOF                        
                GO TO 8100-EXIT.                                                
                                                                                
           IF KD38-STATUS = 00 OR 02                                            
              IF KD38-NUM-REGN   = FD00-NUM-REGN                                
                 ADD 1 TO WS-IDX                                                
                 MOVE KD38-KEY-FLD TO WS-KD38-KEY(WS-IDX)                       
              ELSE                                                              
                 MOVE 'Y' TO WS-KD38-EOF                                        
              END-IF                                                            
                                                                                
           ELSE                                                                 
                                                                                
              MOVE 'Y' TO WS-KD38-EOF                                           
                                                                                
              IF KD38-STATUS = 23                                               
                 CONTINUE                                                       
              ELSE                                                              
                 DISPLAY 'BP13KD38 ERROR IN READNXT ' KD38-KEY-FLD              
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ***************************                                               
       8200-UPDATE-BP13KD38.                                                    
      ***************************                                               
           PERFORM VARYING WS-IDX FROM 1 BY 1 UNTIL                             
                           WS-IDX > 5                                           
              IF WS-KD38-KEY(WS-IDX) NOT = SPACES AND LOW-VALUES                
                 MOVE WS-KD38-KEY(WS-IDX)      TO KD38-KEY-FLD                  
                 PERFORM 8300-READ-BP13KD38  THRU 8300-EXIT                     
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ***************************                                               
       8300-READ-BP13KD38.                                                      
      ***************************                                               
           READ BP13KD38.                                                       
                                                                                
           IF KD38-STATUS = 00                                                  
              PERFORM 8400-REWRITE-BP13KD38 THRU 8400-EXIT                      
                                                                                
           ELSE                                                                 
             IF KD38-STATUS = 23                                                
                DISPLAY 'REC NOT FND IN KD38, KEY ' KD38-KEY-FLD                
             ELSE                                                               
                DISPLAY 'ERROR READING KD38, STATUS ' KD38-STATUS               
                        ', KEY  '    KD38-KEY-FLD                               
             END-IF                                                             
           END-IF.                                                              
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************                                               
       8400-REWRITE-BP13KD38.                                                   
      ***************************                                               
           MOVE 'BP13CD17'                 TO KD38-NUM-USERID.                  
           MOVE F001-DTE-CURRENT           TO KD38-DTE-UPDATE.                  
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD38-TME-UPDATE.                  
                                                                                
           EVALUATE WS-MATCHED-NUM-NRIC                                         
           WHEN 1                                                               
              ADD KD38-AMT-INSTAL-NEW-CPF1 TO KD38-AMT-INSTAL-NEW-CASH          
              MOVE ZEROS TO KD38-AMT-INSTAL-NEW-CPF1                            
           WHEN 2                                                               
              ADD KD38-AMT-INSTAL-NEW-CPF2 TO KD38-AMT-INSTAL-NEW-CASH          
              MOVE ZEROS TO KD38-AMT-INSTAL-NEW-CPF2                            
           WHEN 3                                                               
              ADD KD38-AMT-INSTAL-NEW-CPF3 TO KD38-AMT-INSTAL-NEW-CASH          
              MOVE ZEROS TO KD38-AMT-INSTAL-NEW-CPF3                            
           WHEN 4                                                               
              ADD KD38-AMT-INSTAL-NEW-CPF4 TO KD38-AMT-INSTAL-NEW-CASH          
              MOVE ZEROS TO KD38-AMT-INSTAL-NEW-CPF4                            
           END-EVALUATE.                                                        
                                                                                
           REWRITE BP13KD38-TRANS-REC.                                          
                                                                                
           IF KD38-STATUS = 00                                                  
              ADD 1    TO WS-KD38-UPD                                           
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING KD38, STATUS ' KD38-STATUS               
                      ', REGN IS  '    KD38-NUM-REGN                            
           END-IF.                                                              
                                                                                
       8400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************                                               
       9000-CLOSE-FILES.                                                        
      ***************************                                               
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-YY.         
           DISPLAY '**************** BP13CD17 **********************'.          
           DISPLAY ' RECORDS READ    IN BP13FD00 : ' WS-FD00-READ.              
           DISPLAY ' RECORDS READ    IN AB02F541  : ' WS-F541-READ.             
           DISPLAY ' F541 NOT MATCHED            : ' WS-F541-UNMAT.             
           DISPLAY ' F541 BYPASSED               : ' WS-F541-BYPASS.            
           DISPLAY ' BP13KD00 RECS UPDATED       : ' WS-KD00-UPD.               
           DISPLAY ' BP13KD35 RECS UPDATED       : ' WS-KD35-UPD.               
           DISPLAY ' BP13KD38 RECS UPDATED       : ' WS-KD38-UPD.               
           DISPLAY ' BP13LD17 RECS WRITTEN       : ' WS-CNT-LD17-WRITE.         
           DISPLAY '**************** -------- ***********************'.         
                                                                                
           CLOSE AB02F541                                                       
                 BP13FD00                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13KD00                                                       
                 BP13KD35                                                       
                 BP13KD38                                                       
                 BP13LD17.                                                      
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING BP13K800 FILE ERROR ' K800-STATUS                
           END-IF.                                                              
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING BP13K820 FILE ERROR ' K820-STATUS                
           END-IF.                                                              
           IF KD00-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING BP13KD00 FILE ERROR ' KD00-STATUS                
           END-IF.                                                              
           IF KD35-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING BP13KD35 FILE ERROR ' KD35-STATUS                
           END-IF.                                                              
           IF KD38-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING BP13KD38 FILE ERROR ' KD38-STATUS                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
      ********************** END OF PROGRAM ***************************         
