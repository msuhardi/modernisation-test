       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB3E.                                                 
      *AUTHOR.        SANDEEP REDDY.                                            
      *DATE-WRITTEN.  24/05/2019.                                               
      * ========================================================== *            
      *             SYSTEM OF COMMITMENT  (BP13)                   *            
      * ========================================================== *            
      *                                                            *            
      *  DESCRIPTION :  TO GENERATE REPORT.                        *            
      *  INPUT FILE  :  AB02F945                                   *            
      *              :  BP13L945                                   *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY     ON          DESCRIPTION                    *            
      * -------- ---    ----------  -----------                    *            
      * BP137965 SRG1   24/05/2019  NEW PROGRAM                    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT AB02F945  ASSIGN          TO AB02F945.                        
                                                                                
           SELECT BP13L945  ASSIGN          TO BP13L945.                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *---------------------------------------------------------------*         
       FD  AB02F945                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AB02F945.                                                           
                                                                                
       FD  BP13L945                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13L945-REC.                                                        
           05  L945-NUM-NRIC                PIC X(9).                           
           05  FIL001                       PIC X(01).                          
           05  L945-TAG-EXEC-CONDO          PIC X(2).                           
           05  FIL002                       PIC X(01).                          
           05  L945-NUM-REFNO               PIC X(12).                          
           05  FIL003                       PIC X(01).                          
           05  L945-TAG-SHORT-LEASE         PIC X(2).                           
           05  FIL004                       PIC X(01).                          
           05  L945-NUM-GRNT-IDN            PIC 9(9).                           
           05  FIL005                       PIC X(01).                          
           05  L945-CDE-OFC                 PIC X(4).                           
           05  FIL006                       PIC X(01).                          
           05  L945-NUM-BSNESS-REF          PIC X(8).                           
           05  FIL007                       PIC X(01).                          
           05  L945-CDE-GRNT-TYPE-1         PIC X(6).                           
           05  FIL008                       PIC X(01).                          
           05  L945-AMT-GRNT-TYPE-1         PIC 9(11).                          
           05  FIL009                       PIC X(01).                          
           05  L945-CDE-GRNT-TYPE-2         PIC X(6).                           
           05  FIL010                       PIC X(01).                          
           05  L945-AMT-GRNT-TYPE-2         PIC 9(11).                          
           05  FIL011                       PIC X(01).                          
           05  L945-CDE-GRNT-TYPE-3         PIC X(6).                           
           05  FIL012                       PIC X(01).                          
           05  L945-AMT-GRNT-TYPE-3         PIC 9(11).                          
           05  FIL013                       PIC X(01).                          
           05  L945-CDE-REQ-STAT            PIC X(1).                           
           05  FIL014                       PIC X(01).                          
           05  L945-TAG-MANUAL-PROCESSD     PIC X(1).                           
           05  FIL015                       PIC X(01).                          
           05  L945-CDE-RECOVERY-STAT       PIC X(1).                           
           05  FIL016                       PIC X(01).                          
           05  L945-AMT-RECOVERD-PRIN       PIC 9(12).                          
           05  FIL017                       PIC X(01).                          
           05  L945-AMT-RECOVERD-INTR       PIC 9(12).                          
           05  FIL018                       PIC X(01).                          
           05  L945-DTE-ACCRUED-INTR-FRM-CYM PIC 9(6).                          
           05  FIL019                       PIC X(01).                          
           05  L945-DTE-ACCRUED-INTR-TO-CYM PIC 9(6).                           
           05  FIL020                       PIC X(01).                          
           05  L945-AMT-OS-PRIN             PIC 9(12).                          
           05  FIL021                       PIC X(01).                          
           05  L945-AMT-OS-INTR             PIC 9(12).                          
           05  FIL022                       PIC X(01).                          
           05  L945-TXT-REMARKS             PIC X(80).                          
           05  FIL023                       PIC X(01).                          
           05  L945-CDE-SYSID               PIC X(4).                           
           05  FIL024                       PIC X(01).                          
           05  L945-DTE-OF-RUN              PIC X(8).                           
           05  FIL025                       PIC X(01).                          
           05  FILLER                       PIC X(23).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-F945-EOF                  PIC X(01) VALUE SPACES.             
               88 F945-END-OF-FILE                    VALUE 'Y'.                
           05  WS-F945-READ-CNT             PIC 9(08) VALUE ZEROES.             
           05  WS-WRT-L945A                 PIC 9(08) VALUE ZEROES.             
                                                                                
       01  WS-DATE-TIME                     PIC X(15) VALUE SPACES.             
                                                                                
       01  WS-CURR-DATE                     PIC X(10) VALUE SPACES.             
       01  WS-CURR-TIME                     PIC X(08) VALUE SPACES.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-AB02F945       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-F945        THRU 3000-EXIT                      
             UNTIL F945-END-OF-FILE.                                            
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  AB02F945                                                 
                OUTPUT BP13L945.                                                
                                                                                
           STRING 'NRIC;' 'TAG-CONDO;' 'REFNO;' 'TAG-SHORT-LSE;'                
                  'GRNT-IDN;' 'CDE-OFC;' 'BUSNES-REF;' 'CDE-GRNT-TYP1;'         
                  'AMT-GRNT-TYP1;' 'CDE-GRNT-TYP2;' 'AMT-GRNT-TYP2;'            
                  'CDE-GRNT-TYP3;' 'AMT-GRNT-TYP3;' 'CDE-REQ-ST;'               
                  'TAG-MNUL-PRCSD;' 'CDE-REC-ST;' 'AMT-RECRD-PRIN;'             
                  'AMT-RECRD-INTR;' 'ACC-INTR-FRM-CYM;'                         
                  'ACC-INTR-TO-CYM;' 'AMT-OS-PRIN;' 'AMT-OS-INTR;'              
                  'REMARKS;' 'SYSID;' 'DTE-OF-RUN;'                             
                  DELIMITED BY SIZE                                             
             INTO BP13L945-REC.                                                 
                                                                                
           WRITE BP13L945-REC.                                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-AB02F945.                                                      
      *---------------------------------------------------------------*         
           READ AB02F945                                                        
           AT END                                                               
              MOVE 'Y'                      TO WS-F945-EOF                      
           NOT AT END                                                           
              ADD  1                        TO WS-F945-READ-CNT                 
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2500-MOVE-FILLER.                                                        
      *---------------------------------------------------------------*         
           MOVE ';' TO FIL001 FIL002 FIL003 FIL004 FIL005 FIL006 FIL007         
                       FIL008 FIL009 FIL010 FIL011 FIL012 FIL013 FIL014         
                       FIL015 FIL016 FIL017 FIL018 FIL019 FIL020 FIL021         
                       FIL022 FIL023 FIL024 FIL025.                             
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-F945.                                                       
      *---------------------------------------------------------------*         
           MOVE SPACES                      TO BP13L945-REC.                    
           INITIALIZE                          BP13L945-REC.                    
                                                                                
           PERFORM 2500-MOVE-FILLER         THRU 2500-EXIT.                     
                                                                                
           MOVE F945-NUM-CPF-ACCT           TO L945-NUM-NRIC                    
           MOVE F945-TAG-EXEC-CONDO         TO L945-TAG-EXEC-CONDO              
           MOVE F945-NUM-PREF-HDB           TO L945-NUM-REFNO                   
           MOVE F945-TAG-SHORT-LEASE        TO L945-TAG-SHORT-LEASE             
           MOVE F945-NUM-GRNT-IDN           TO L945-NUM-GRNT-IDN                
           MOVE F945-CDE-OFC                TO L945-CDE-OFC                     
           MOVE F945-NUM-BSNESS-REF         TO L945-NUM-BSNESS-REF              
           MOVE F945-CDE-GRNT-TYPE-1        TO L945-CDE-GRNT-TYPE-1             
           MOVE F945-AMT-GRNT-TYPE-1        TO L945-AMT-GRNT-TYPE-1             
           MOVE F945-CDE-GRNT-TYPE-2        TO L945-CDE-GRNT-TYPE-2             
           MOVE F945-AMT-GRNT-TYPE-2        TO L945-AMT-GRNT-TYPE-2             
           MOVE F945-CDE-GRNT-TYPE-3        TO L945-CDE-GRNT-TYPE-3             
           MOVE F945-AMT-GRNT-TYPE-3        TO L945-AMT-GRNT-TYPE-3             
           MOVE F945-CDE-REQ-STAT           TO L945-CDE-REQ-STAT                
           MOVE F945-TAG-MANUAL-PROCESSD    TO L945-TAG-MANUAL-PROCESSD         
           MOVE F945-CDE-RECOVERY-STAT      TO L945-CDE-RECOVERY-STAT           
           MOVE F945-TXT-REMARKS            TO L945-TXT-REMARKS                 
           MOVE F945-CDE-SYSID              TO L945-CDE-SYSID                   
           MOVE FUNCTION CURRENT-DATE(1:8)  TO L945-DTE-OF-RUN                  
           MOVE F945-DTE-ACCRUED-INTR-FRM-CYM                                   
             TO L945-DTE-ACCRUED-INTR-FRM-CYM                                   
           MOVE F945-DTE-ACCRUED-INTR-TO-CYM                                    
             TO L945-DTE-ACCRUED-INTR-TO-CYM                                    
           IF F945-AMT-RECOVERD-PRIN = ZEROS                                    
              MOVE F945-AMT-RECOVERD-PRIN   TO L945-AMT-RECOVERD-PRIN           
           ELSE                                                                 
              STRING F945-AMT-RECOVERD-PRIN(1:9) '.'                            
                     F945-AMT-RECOVERD-PRIN(10:2) DELIMITED BY SIZE             
                INTO L945-AMT-RECOVERD-PRIN                                     
           END-IF                                                               
           IF F945-AMT-RECOVERD-INTR = ZEROS                                    
              MOVE F945-AMT-RECOVERD-INTR   TO L945-AMT-RECOVERD-INTR           
           ELSE                                                                 
              STRING F945-AMT-RECOVERD-INTR(1:9) '.'                            
                     F945-AMT-RECOVERD-INTR(10:2) DELIMITED BY SIZE             
                INTO L945-AMT-RECOVERD-INTR                                     
           END-IF                                                               
           IF F945-AMT-OS-PRIN = ZEROS                                          
              MOVE F945-AMT-OS-PRIN         TO L945-AMT-OS-PRIN                 
           ELSE                                                                 
              STRING F945-AMT-OS-PRIN(1:9) '.'                                  
                     F945-AMT-OS-PRIN(10:2) DELIMITED BY SIZE                   
                INTO L945-AMT-OS-PRIN                                           
           END-IF                                                               
           IF F945-AMT-OS-INTR = ZEROS                                          
              MOVE F945-AMT-OS-INTR         TO L945-AMT-OS-INTR                 
           ELSE                                                                 
              STRING F945-AMT-OS-INTR(1:9) '.'                                  
                     F945-AMT-OS-INTR(10:2) DELIMITED BY SIZE                   
                INTO L945-AMT-OS-INTR                                           
           END-IF                                                               
                                                                                
           WRITE BP13L945-REC.                                                  
           ADD 1                            TO WS-WRT-L945A.                    
                                                                                
           PERFORM 2000-READ-AB02F945       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY ' '.                                                         
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY '   PROGRAM : BP13CB3E ' .                                   
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY ' '.                                                         
           DISPLAY ' REC READ FROM AB02F945    - ' WS-F945-READ-CNT.            
           DISPLAY ' REC WRITTEN ON THE REPORT - ' WS-WRT-L945A.                
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
                                                                                
           CLOSE AB02F945                                                       
                 BP13L945.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
