       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC91.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  14/01/2009.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (SOC)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  : TO SEPARATE LETTER ACCORDING TO NO OF PAGES   *           
      *                                                             *           
      *  INPUT FILE :  1. BP13FD63                                  *           
      *                2. SY02F001                                  *           
      *  OUTPUT FILE:  1. P13FD63A - ONE PAGE                       *           
      *                2. P13FD63B - TWO PAGES                      *           
      *                3. P13FD63C - THREE PAGES                    *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY   ON       DESCRIPTION                          *           
      * -------- ---- -------- -----------                          *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT SY02F001 ASSIGN        TO SY02F001.                           
           SELECT BP13FD63 ASSIGN        TO BP13FD63.                           
           SELECT P13FD63A ASSIGN        TO P13FD63A.                           
           SELECT P13FD63B ASSIGN        TO P13FD63B.                           
           SELECT P13FD63C ASSIGN        TO P13FD63C.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13FD63                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13FD63.                                                           
                                                                                
       FD  P13FD63A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  P13FD63A-REC PIC X(150).                                             
                                                                                
       FD  P13FD63B                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  P13FD63B-REC PIC X(150).                                             
                                                                                
       FD  P13FD63C                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  P13FD63C-REC PIC X(150).                                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-FD63-READ          PIC 9(05)  VALUE ZEROS.                    
           05  WS-FD63A-WRITE        PIC 9(05)  VALUE ZEROS.                    
           05  WS-FD63B-WRITE        PIC 9(05)  VALUE ZEROS.                    
           05  WS-FD63C-WRITE        PIC 9(05)  VALUE ZEROS.                    
           05  WS-EOF-FD63           PIC X(01)  VALUE 'N'.                      
           05  WS-CNT                PIC 9(05)  VALUE ZEROS.                    
           05  WS-CTR                PIC 9(05)  VALUE ZEROS.                    
           05  WS-PREV-YEAR          PIC 9(04)  VALUE ZEROES.                   
           05  WS-NUM-REGN           PIC X(08)  VALUE SPACES.                   
                                                                                
       01  WS-FD63-REC.                                                         
           05  FD63-REC OCCURS 100 TIMES PIC X(150).                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPENING-ROUTINE THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13FD63   THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-PROCESS    THRU 3000-EXIT                          
                   UNTIL WS-EOF-FD63 = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13FD63                                                 
                       SY02F001                                                 
                OUTPUT P13FD63A                                                 
                       P13FD63B                                                 
                       P13FD63C.                                                
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-LAST-DAY-PREV(1:4) TO WS-PREV-YEAR.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FD63.                                                      
      *-------------------------------------------------------------            
           READ BP13FD63                                                        
                AT END MOVE 'Y'         TO WS-EOF-FD63                          
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-FD63-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF FD63-KEY-FLD = SPACES OR LOW-VALUES                               
              PERFORM 2000-READ-BP13FD63   THRU 2000-EXIT                       
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE FD63-NUM-REGN                TO WS-NUM-REGN.                    
           MOVE 0                            TO WS-CNT.                         
           MOVE SPACES                       TO WS-FD63-REC.                    
                                                                                
           PERFORM 3500-COUNT-RECORDS      THRU 3500-EXIT                       
             UNTIL FD63-NUM-REGN NOT = WS-NUM-REGN                              
                OR WS-EOF-FD63 = 'Y'.                                           
                                                                                
           IF WS-CNT > 0 AND WS-CNT < 33                                        
              PERFORM VARYING WS-CTR FROM 1 BY 1                                
                UNTIL WS-CTR > WS-CNT                                           
                WRITE P13FD63A-REC FROM FD63-REC(WS-CTR)                        
                ADD 1 TO WS-FD63A-WRITE                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           IF WS-CNT > 32 AND WS-CNT < 68                                       
              PERFORM VARYING WS-CTR FROM 1 BY 1                                
                UNTIL WS-CTR > WS-CNT                                           
                WRITE P13FD63B-REC FROM FD63-REC(WS-CTR)                        
                ADD 1 TO WS-FD63B-WRITE                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           IF WS-CNT > 67                                                       
              PERFORM VARYING WS-CTR FROM 1 BY 1                                
                UNTIL WS-CTR > WS-CNT                                           
                WRITE P13FD63C-REC FROM FD63-REC(WS-CTR)                        
                ADD 1 TO WS-FD63C-WRITE                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-COUNT-RECORDS.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF FD63-DTE-PROCESS(1:4) = WS-PREV-YEAR                              
              ADD 1 TO WS-CNT                                                   
              MOVE BP13FD63-REC        TO FD63-REC(WS-CNT)                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD63  THRU 2000-EXIT.                          
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '------- BP13CC91 -----------'.                              
           DISPLAY ' NO OF FD63 READ     : ' WS-FD63-READ.                      
           DISPLAY ' NO OF FD63A WRITTEN : ' WS-FD63A-WRITE.                    
           DISPLAY ' NO OF FD63B WRITTEN : ' WS-FD63B-WRITE.                    
           DISPLAY ' NO OF FD63C WRITTEN : ' WS-FD63C-WRITE.                    
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13FD63                                                       
                 P13FD63A                                                       
                 P13FD63B                                                       
                 P13FD63C                                                       
                 SY02F001.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
