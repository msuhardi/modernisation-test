       IDENTIFICATION DIVISION.                                                 
      *========================*                                                
       PROGRAM-ID.   BP13C906.                                                  
       AUTHOR.       ROMAR ANDREW S MARALIT.                                    
       DATE-WRITTEN. 13/07/1999.                                                
       DATE-COMPILED.                                                           
      **********************************************************                
      *  OBJECTIVES   :                                        *                
      *    1. THIS SUBROUTINE IS USED TO GENERATE A BATCH      *                
      *       NUMBER.                                          *                
      *                                                        *                
      *  I-O   FILES  :                                        *                
      *                                                        *                
      *    1.  BP13K620   (VOUCHER BATCH NO. FILE)             *                
      *           ORGANIZATION : VSAM                          *                
      *           ACCESS MODE  : RANDOM                        *                
      *           REC SIZE     : 45                            *                
      *                                                        *                
      *========================================================*                
      * MODIFICATIONS:                                         *                
      *                                                        *                
      * REQ #     EFF DATE  AUTH  REMARKS                      *                
      * --------  --------  ----  ---------------------------- *                
      * BP130696 13/07/1999 RASM  NEW PROGRAM                  *                
      *                                                        *                
      **********************************************************                
                                                                                
      *=====================*                                                   
       ENVIRONMENT DIVISION.                                                    
      *=====================*                                                   
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13K620 ASSIGN  TO  BP13K620                                 
                     ORGANIZATION  IS  INDEXED                                  
                     ACCESS MODE   IS  RANDOM                                   
                     RECORD KEY    IS  K620-KEY-FLD                             
                     FILE STATUS   IS  BP13K620-STATUS.                         
                                                                                
      *==============*                                                          
       DATA DIVISION.                                                           
      *==============*                                                          
                                                                                
      *--------------*                                                          
       FILE SECTION.                                                            
      *--------------*                                                          
                                                                                
       FD   BP13K620  RECORD CONTAINS  50 CHARACTERS.                           
       COPY BP13K620.                                                           
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-DTE-SYS.                                                          
           05  WS-DTE-SYS-CCYY.                                                 
               10  WS-DTE-SYS-CC       PIC 9(2).                                
               10  WS-DTE-SYS-YY       PIC 9(2).                                
           05  WS-DTE-SYS-MM           PIC 9(2).                                
           05  WS-DTE-SYS-DD           PIC 9(2).                                
                                                                                
       01  BP13K620-STATUS             PIC X(2).                                
                                                                                
      *--------------*                                                          
       LINKAGE SECTION.                                                         
      *--------------*                                                          
       COPY P13C906C.                                                           
                                                                                
                                                                                
      *=======================================*                                 
       PROCEDURE DIVISION USING BP13C906-AREA.                                  
      *=======================================*                                 
                                                                                
      ****************************************************************          
       0000-MAIN-LOGIC.                                                         
      ****************************************************************          
           PERFORM 1000-INITIALIZATION       THRU 1000-EXIT.                    
           PERFORM 3000-GENERATE-BATCH-NUM   THRU 3000-EXIT.                    
           PERFORM 9000-END-OF-JOB           THRU 9000-EXIT.                    
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       1000-INITIALIZATION.                                                     
      ****************************************************************          
                                                                                
           IF C906-NUM-FILE-STATUS NOT = ('00' AND '97')                        
              PERFORM 9000-END-OF-JOB  THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-GENERATE-BATCH-NUM.                                                 
      ****************************************************************          
      *----------------------------------------------*                          
      *    THIS PORTION WILL COMPUTE FOR THE CORRECT *                          
      *    YEAR BASED ON THE SYSTEM DATE.            *                          
      *----------------------------------------------*                          
                                                                                
           ACCEPT WS-DTE-SYS    FROM DATE YYYYMMDD.                             
                                                                                
      * TESTING FOR NEW YEAR - ROMAR 08/02/1999.                                
      *    MOVE '20000101'      TO   WS-DTE-SYS.                                
                                                                                
           MOVE WS-DTE-SYS-CCYY TO   C906-NUM-FIN-YEAR.                         
      *-------------------------------------------------------*                 
      *    READ AND UPDATE BP13K620                           *                 
      *-------------------------------------------------------*                 
           PERFORM  4000-READ-UPDATE-BP13K620 THRU 4000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4000-READ-UPDATE-BP13K620.                                               
      ****************************************************************          
           MOVE C906-NUM-BATCH-TYPE         TO K620-NUM-BATCH-TYPE.             
           MOVE C906-NUM-FIN-YEAR           TO K620-NUM-FIN-YEAR.               
           READ BP13K620.                                                       
                                                                                
           MOVE BP13K620-STATUS             TO C906-NUM-FILE-STATUS.            
           DISPLAY C906-NUM-FILE-STATUS.                                        
           IF BP13K620-STATUS = '00'                                            
              PERFORM 4100-REWRITE-BP13K620 THRU 4100-EXIT                      
           END-IF.                                                              
                                                                                
           IF BP13K620-STATUS = '22'                                            
              DISPLAY 'ERROR : DUPLICATE BATCH NUM FOUND IN FILE : '            
                                            C906-NUM-FILENME                    
                      ', FILE STATUS : '    C906-NUM-FILE-STATUS                
              PERFORM 9000-END-OF-JOB       THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
      *----------------------------------------------*                          
      *    IF YEAR IS NOT FOUND, IE NEW YEAR,        *                          
      *    TO RESTART SERIAL NUMBER FOR NEW YEAR     *                          
      *----------------------------------------------*                          
           IF BP13K620-STATUS = '10' OR '23'                                    
              PERFORM 4200-WRITE-BP13K620 THRU 4200-EXIT                        
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4100-REWRITE-BP13K620.                                                   
      ****************************************************************          
           DISPLAY C906-NUM-BATCH-TYPE.                                         
           DISPLAY C906-NUM-FIN-YEAR.                                           
           DISPLAY 'SERIAL B/F = ' K620-NUM-BATCH-SERIAL.                       
           ADD  1          TO  K620-NUM-BATCH-SERIAL.                           
           DISPLAY 'SERIAL A/F = ' K620-NUM-BATCH-SERIAL.                       
           MOVE WS-DTE-SYS TO  K620-DTE-UPDATE.                                 
                                                                                
           REWRITE BP13K620-REC.                                                
                                                                                
           MOVE BP13K620-STATUS             TO C906-NUM-FILE-STATUS.            
                                                                                
           IF BP13K620-STATUS = '00'                                            
              MOVE K620-NUM-BATCH-SERIAL    TO C906-NUM-BATCH-SERIAL            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4200-WRITE-BP13K620.                                                     
      ****************************************************************          
                                                                                
           MOVE SPACES                      TO BP13K620-REC.                    
           MOVE C906-NUM-BATCH-TYPE         TO K620-NUM-BATCH-TYPE.             
           MOVE C906-NUM-FIN-YEAR           TO K620-NUM-FIN-YEAR.               
           MOVE 1                           TO K620-NUM-BATCH-SERIAL.           
           MOVE WS-DTE-SYS                  TO K620-DTE-UPDATE.                 
                                                                                
           WRITE BP13K620-REC.                                                  
                                                                                
           MOVE BP13K620-STATUS             TO C906-NUM-FILE-STATUS.            
                                                                                
           IF BP13K620-STATUS = '00'                                            
              MOVE K620-NUM-BATCH-SERIAL    TO C906-NUM-BATCH-SERIAL            
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9000-END-OF-JOB.                                                         
      ****************************************************************          
           GOBACK.                                                              
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9990-OPEN-BP13K620.                                                      
      ****************************************************************          
           ENTRY 'K620OPEN' USING BP13C906-AREA.                                
                                                                                
           OPEN I-O BP13K620.                                                   
                                                                                
           MOVE BP13K620-STATUS             TO C906-NUM-FILE-STATUS.            
                                                                                
           IF BP13K620-STATUS NOT = ('00' AND '97')                             
              DISPLAY 'ERROR OPENING FILE : '  C906-NUM-FILENME                 
                      ', FILE STATUS : '       C906-NUM-FILE-STATUS             
           END-IF.                                                              
                                                                                
           PERFORM 9000-END-OF-JOB          THRU 9000-EXIT.                     
                                                                                
       9990-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9991-CLOSE-BP13K620.                                                     
      ****************************************************************          
           ENTRY 'K620MCLS' USING BP13C906-AREA.                                
                                                                                
           CLOSE BP13K620.                                                      
                                                                                
           MOVE BP13K620-STATUS             TO C906-NUM-FILE-STATUS.            
                                                                                
           IF BP13K620-STATUS NOT = ('00' AND '97')                             
              DISPLAY 'ERROR CLOSING FILE : '  C906-NUM-FILENME                 
                      ', FILE STATUS : '       C906-NUM-FILE-STATUS             
           END-IF.                                                              
                                                                                
           PERFORM 9000-END-OF-JOB          THRU 9000-EXIT.                     
                                                                                
       9991-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================== END OF PROGRAM ===========================*          
