       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF46.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  05/02/2014.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  UPDATE K800-NUM-PRIO-SUCC-QUOTA USING      *            
      *                 F740-CDE-ALLOC-SCH                         *            
      *                                                            *            
      * INPUT FILE   :  BP13F740  -  SEQUENTIAL DATASET            *            
      * I-O FILE     :  BP13K800  -  BP13.K800.APPLN               *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP135211  SMR2  05022014  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740  ASSIGN       TO BP13F740.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F740                                                             
           RECORDING MODE    IS F                                               
           RECORD CONTAINS  500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS           PIC 9(02)  VALUE ZEROES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ             PIC 9(06)  VALUE ZEROES.                
           05  WS-K800-MATCH            PIC 9(06)  VALUE ZEROES.                
           05  WS-K800-UNMATCH          PIC 9(06)  VALUE ZEROES.                
           05  WS-K800-REWRITE          PIC 9(06)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF              PIC X(01)  VALUE 'N'.                   
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                  PIC X(08)  VALUE SPACES.                
           05  WS-DATE-EDIT             PIC X(10)  VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT                      
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13F740                                                  
                I-O   BP13K800.                                                 
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                         
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F740-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F740-READ.                    
                                                                                
           PERFORM 3000-READ-BP13K800       THRU 3000-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                      TO BP13K800-MASTER.                 
           INITIALIZE                          BP13K800-MASTER.                 
                                                                                
           MOVE F740-NUM-REGN               TO K800-NUM-REGN.                   
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K800-MATCH                    
              PERFORM 4000-REWRITE-BP13K800 THRU 4000-EXIT                      
                                                                                
           WHEN 23                                                              
              ADD 1                         TO WS-K800-UNMATCH                  
              DISPLAY 'NUM-REGN NOT FOUND IN K800 : ' F740-NUM-REGN             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K800   : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-REWRITE-BP13K800.                                                   
      *-------------------------------------------------------------            
           IF F740-NUM-PORT-ELIG-TAG = 'P'                                      
              MOVE 'SER'                    TO K800-NUM-PRIO-SUCC-QUOTA         
           ELSE                                                                 
              MOVE F740-CDE-ALLOC-SCH       TO K800-NUM-PRIO-SUCC-QUOTA         
           END-IF.                                                              
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K800-REWRITE                  
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING TO BP13K800. STATUS IS - '                 
                      WS-K800-STATUS                                            
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '    BP13CF46 CONTROL TOTAL   : ' WS-DATE-EDIT.              
           DISPLAY '  '.                                                        
           DISPLAY ' 1. BP13F740 READ            : ' WS-F740-READ.              
           DISPLAY ' 2. BP13K800 MATCHED RECS    : ' WS-K800-MATCH              
           DISPLAY ' 3. BP13K800 UNMATCHED RECS  : ' WS-K800-UNMATCH            
           DISPLAY ' 4. BP13K800 REWRITTEN RECS  : ' WS-K800-REWRITE.           
           DISPLAY '  '.                                                        
           DISPLAY ' CHECK '.                                                   
           DISPLAY '       1.  =   2.  +  3. '.                                 
           DISPLAY '       2.  =   4.        '.                                 
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F740                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
