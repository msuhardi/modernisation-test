       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C434.                                                 
      *AUTHOR.        AJITH KUMAR T.                                            
      *DATE WRITTEN.  13/10/1999.                                               
                                                                                
      *===============================================================*         
      * OBJECTIVE :                                                   *         
      *     TO PURGE BP13F625 RECORDS BASED ON DTE-IRAS-PROCESSED     *         
      *     KEEP RECORDS THAT ARE LESS THAN 90 DAYS OLD               *         
      *===============================================================*         
      * FILE DESCRIPTION:                                             *         
      *                  ACCESS                                       *         
      *  DDNAME    I/O   MODE    COMMENTS                             *         
      *  ========  ====  ======= =================================    *         
      *  BP13F625   I    SEQ     SORTED BY DTE-IRAS-PROCESSED         *         
      *  P13F625A   O    SEQ     TO WRITE DTE-IRAS-PROCESS > 90 CASES *         
      *  P13F625B   O    SEQ     TO WRITE DTE-IRAS-PROCESS < 90 CASES *         
      *===============================================================*         
      * REF NO   DATE    BY   DESCRIPTION                             *         
      * -------  ----    ---  -----------                             *         
      * BP130696 131099  AKT  NEW PROGRAM                             *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F625  ASSIGN TO BP13F625.                                 
           SELECT P13F625A  ASSIGN TO P13F625A.                                 
           SELECT P13F625B  ASSIGN TO P13F625B.                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F625                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS  0 RECORDS                                           
            RECORDING MODE  IS F                                                
            LABEL RECORD    IS STANDARD.                                        
       COPY BP13F625.                                                           
                                                                                
       FD   P13F625A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS  0 RECORDS                                           
            RECORDING MODE  IS F                                                
            LABEL RECORD    IS STANDARD.                                        
                                                                                
       01   P13F625A-REC              PIC X(500).                               
                                                                                
       FD   P13F625B                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS  0 RECORDS                                           
            RECORDING MODE  IS F                                                
            LABEL RECORD    IS STANDARD.                                        
                                                                                
       01   P13F625B-REC              PIC X(500).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F625-READ              PIC 9(07) VALUE ZEROES.                 
           05 WS-P13F625A-WRITE         PIC 9(07) VALUE ZEROES.                 
           05 WS-P13F625B-WRITE         PIC 9(07) VALUE ZEROES.                 
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-DATE                   PIC 9(08) VALUE ZEROES.                 
       01  WS-END-OF-FILE.                                                      
           05 WS-F625-EOF               PIC X(01) VALUE 'N'.                    
                                                                                
       01  WS-VARIABLE.                                                         
           05  WS-DTE-IRAS-PRO          PIC X(08) VALUE SPACES.                 
           05  WS-DTE-IRAS-PRO-RED REDEFINES WS-DTE-IRAS-PRO PIC 9(08).         
           05  WS-DTE-IRAS-INT          PIC S9(09).                             
           05  WS-DTE-INT               PIC S9(09).                             
           05  WS-DTE-DIFFERENCE        PIC 9(05) VALUE ZEROES.                 
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
      *    DISPLAY '0000-MAIN-ROUTINE'.                                         
                                                                                
           PERFORM 1000-START-UP          THRU  1000-EXIT.                      
           PERFORM 2000-READ-BP13F625     THRU  2000-EXIT.                      
           PERFORM 3000-PROCESS-BP13F625  THRU  3000-EXIT                       
                   UNTIL WS-F625-EOF = 'Y'.                                     
                                                                                
           PERFORM 9999-CLOSE-FILES       THRU  9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       1000-START-UP.                                                           
      *--------------------------------*                                        
      *    DISPLAY '1000-START-UP'.                                             
                                                                                
           OPEN INPUT  BP13F625                                                 
                OUTPUT P13F625A                                                 
                       P13F625B.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)         TO WS-DATE.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       2000-READ-BP13F625.                                                      
      *--------------------------------*                                        
      *    DISPLAY '2000-READ-BP13F625'.                                        
                                                                                
           READ BP13F625 AT END                                                 
                MOVE 'Y' TO WS-F625-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO  WS-F625-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       3000-PROCESS-BP13F625.                                                   
      *--------------------------------*                                        
      *    DISPLAY '3000-PROCESS-BP13F625'.                                     
                                                                                
           INITIALIZE      P13F625A-REC.                                        
           MOVE SPACES TO  P13F625A-REC.                                        
                                                                                
           INITIALIZE      P13F625B-REC.                                        
           MOVE SPACES TO  P13F625B-REC.                                        
                                                                                
           MOVE F625-DTE-IRAS-PROCESSED TO   WS-DTE-IRAS-PRO-RED.               
                                                                                
           IF WS-DTE-IRAS-PRO-RED NOT = ZEROES AND LOW-VALUES                   
              DISPLAY 'WS-DTE-IRAS-PRO-RED =' WS-DTE-IRAS-PRO-RED               
                                                                                
              COMPUTE WS-DTE-IRAS-INT   =                                       
                   FUNCTION INTEGER-OF-DATE (WS-DTE-IRAS-PRO-RED)               
                                                                                
              COMPUTE WS-DTE-INT   = FUNCTION INTEGER-OF-DATE (WS-DATE)         
                                                                                
              COMPUTE WS-DTE-DIFFERENCE = WS-DTE-INT - WS-DTE-IRAS-INT          
                                                                                
              DISPLAY 'WS-DTE-DIFFERENCE =' WS-DTE-DIFFERENCE                   
                                                                                
              IF WS-DTE-DIFFERENCE   > 90                                       
                 MOVE  BP13F625-REC   TO P13F625A-REC                           
                 WRITE P13F625A-REC                                             
                 ADD 1                TO WS-P13F625A-WRITE                      
              ELSE                                                              
                 MOVE  BP13F625-REC   TO P13F625B-REC                           
                 WRITE P13F625B-REC                                             
                 ADD 1                TO WS-P13F625B-WRITE                      
              END-IF                                                            
                                                                                
           ELSE                                                                 
                                                                                
      *-----TO WRITE THOSE RECORDS WITH DTE-IRAS-PROCESSED = SPACES             
      *-----TO P13F625B -------------------------------------------------*      
                                                                                
                 MOVE  BP13F625-REC   TO P13F625B-REC                           
                 WRITE P13F625B-REC                                             
                 ADD 1                TO WS-P13F625B-WRITE                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F625  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       9999-CLOSE-FILES.                                                        
      *--------------------------------*                                        
           DISPLAY '9000-CLOSE-FILES'.                                          
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C434'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO. OF BP13F625 READ        : ' WS-F625-READ.               
           DISPLAY 'NO. OF BP13F625 PURGED      : ' WS-P13F625A-WRITE.          
           DISPLAY 'NO. OF BP13F625 RETAINED    : ' WS-P13F625B-WRITE.          
                                                                                
           CLOSE BP13F625                                                       
                 P13F625A                                                       
                 P13F625B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      * ------------  END OF PROGRAM  ------------ *                            
