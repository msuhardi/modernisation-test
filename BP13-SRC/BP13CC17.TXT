       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC17.                                                 
       AUTHOR. BALA AMUTHAN.                                                    
       DATE-WRITTEN.  10/05/06.                                                 
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  THIS PROGRAMME WILL VALIDATE                *                
      *            CASH PAYMENT AC28F526.                      *                
      *            IF THE RECORD DOES NOT MATCH, THE           *                
      *            OUTPUT RECORD WILL MOVE TO THE KIV          *                
      *            FILE (ERROR FILE).                          *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *           1. AC28F526 (WEEKLY CPF TRANSACTIONS)        *                
      *           2. BP13F800 (RFF)                            *                
      *           3. BP13KD40                                  *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *           1. BP13K140 (KIV FILE)                       *                
      *           2. BP13F125 (CPF TRANSACTIONS FILE)          *                
      *                                                        *                
      *     LISTINGS:                                          *                
      *           1. BP13LC17 (CONTROL LIST)                   *                
      *                                                        *                
      * BP132857 BA2 100506 - SAME PGM AS BP13C101             *                
      * BP132888 BA3 030107 - INCLUDE REMARKS                  *                
      * BP133446 JB8 141108 - TO OUTPUT CORRECT SCHDL          *                
      * BP134744 IMC 141013 - WRITE TO KIV FILE IF DUP ERROR   *                
      * BP134744 LSB 100114 - CHECK END-OF-FILE BP13KD40       *                
      * BP135161 IMC 230114 - PUT BACK KD40-AMT-PP-CASH COND   *                
      **********************************************************                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AC28F526 ASSIGN TO AC28F526.                                  
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K140 ASSIGN TO BP13K140                                   
                ACCESS MODE IS RANDOM                                           
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS K140-KEY-FLD                                      
                FILE STATUS IS K140-STATUS.                                     
                                                                                
           SELECT BP13KD40 ASSIGN TO BP13KD40                                   
                  ACCESS MODE IS DYNAMIC                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY  IS KD40-KEY-FLD                                   
                  FILE STATUS IS KD40-STATUS.                                   
                                                                                
           SELECT BP13F125 ASSIGN TO BP13F125.                                  
                                                                                
           SELECT BP13LC17 ASSIGN TO BP13LC17.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AC28F526                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 130 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AC28F526.                                                           
                                                                                
       FD  BP13F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F800.                                                           
                                                                                
       FD  BP13F125                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       COPY BP13F125.                                                           
                                                                                
       FD  BP13K140                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K140.                                                           
                                                                                
       FD  BP13KD40                                                             
           RECORD CONTAINS 1500 CHARACTERS.                                     
       COPY BP13KD40.                                                           
                                                                                
       FD  BP13LC17                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-CONTROL-LIST             PIC X(132).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  F800-STATUS                    PIC 99      VALUE ZEROES.             
       01  K140-STATUS                    PIC 99      VALUE ZEROES.             
       01  KD00-STATUS                    PIC 99      VALUE ZEROES.             
       01  KD40-STATUS                    PIC 99      VALUE ZEROES.             
                                                                                
       01  WS-K140-STATUS.                                                      
           05  WS-K140-STATUS1            PIC 9.                                
           05  WS-K140-STATUS2            PIC 9.                                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F526-EOF-FLAG           PIC X       VALUE 'N'.                
           05  WS-F800-EOF-FLAG           PIC X       VALUE 'N'.                
                                                                                
       01  WS-KD00-FND                    PIC X(1)    VALUE 'N'.                
       01  WS-KD40-FND                    PIC X(1)    VALUE 'N'.                
       01  WS-CNT-AC28F526-READ           PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-BP13F800-READ           PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-K140-KIV                PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F125-VALID              PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-KD00-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-KD40-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-K140-COUNT-HISTORY          PIC 9(2)    VALUE ZEROES.             
       01  WS-F125-COUNT-HISTORY          PIC 9(3)    VALUE ZEROES.             
       01  WS-CDE-APPLT                   PIC X       VALUE SPACES.             
       01  WS-NUM-NRIC                    PIC X(9)    VALUE SPACES.             
       01  WS-LINE-CNT                    PIC 9(5)    VALUE 00054.              
       01  WS-PAGENO                      PIC 9(5)    VALUE ZEROES.             
       01  WS-SNO                         PIC 9(5)    VALUE ZEROES.             
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-CCYY                     PIC 9999.                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
      ***************************************************************           
      *    WS  OR CONTROL-LIST                                      *           
      ***************************************************************           
       01  WS-CONTROL-HEAD.                                                     
           05  FILLER                  PIC X(8)  VALUE 'BP13LC17'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'HDBCAT3'.               
           05  FILLER                  PIC X(25) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  CL-DATE-HD              PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  CL-PAGENO               PIC ZZZZ9 VALUE ZEROES.                  
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
                                                                                
       01  WS-CONTROL-LIST-1.                                                   
           05  FILLER              PIC X(42)  VALUE SPACES.                     
           05  FILLER              PIC X(43)  VALUE                             
               'ERROR REPORT ON CASH PAYMENT FOR PP-LUMPSUM'.                   
           05  FILLER              PIC X(53)  VALUE SPACES.                     
                                                                                
       01  WS-CONTROL-LIST-2.                                                   
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE ' S/NO'.                    
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE 'REG NO'.                   
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'SCH-ACC'.                  
           05  FILLER              PIC X(06)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'PAYMENT'.                  
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE 'JR-NO'.                    
           05  FILLER              PIC X(09)  VALUE SPACES.                     
           05  FILLER              PIC X(11)  VALUE  'AMT-RECEIPT'.             
           05  FILLER              PIC X(12)  VALUE  SPACES.                    
           05  FILLER              PIC X(07)  VALUE  'REMARKS'.                 
           05  FILLER              PIC X(26)  VALUE  SPACES.                    
                                                                                
       01  WS-CONTROL-LIST-3.                                                   
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  CL-SNO              PIC Z(4)9  VALUE ZEROES.                     
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  CL-RENO             PIC X(08)  VALUE SPACES.                     
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  CL-SCH-ACC          PIC X(11)  VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  CL-PAY-TY           PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  CL-JR-NO            PIC X(14)  VALUE SPACES.                     
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  CL-AMT-RECPT        PIC ZZZZZZ9.99   VALUE ZEROES.               
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  CL-REMARKS          PIC X(25)  VALUE SPACES.                     
                                                                                
       01  WS-CONTROL-LIST-4.                                                   
           05  FILLER              PIC X(132) VALUE SPACES.                     
                                                                                
       01  WS-CONTROL-LIST-5.                                                   
           05  FILLER              PIC X(4)   VALUE SPACES.                     
           05  FILLER              PIC X(98)  VALUE ALL '-'.                    
           05  FILLER              PIC X(26)  VALUE SPACES.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN LINE PROCESS                                       *        
      ******************************************************************        
       MAIN-PROCESS.                                                            
           PERFORM 000-OPEN-FILE                THRU 000-EXIT.                  
           PERFORM 100-READ-F526                THRU 100-EXIT.                  
           PERFORM 200-READ-F800                THRU 200-EXIT.                  
           PERFORM 300-PROCESS-SEQUENTIAL-FILES THRU 300-EXIT                   
               UNTIL WS-F526-EOF-FLAG = 'Y'                                     
               AND   WS-F800-EOF-FLAG = 'Y'.                                    
           PERFORM 1000-CLOSE-ROUTINE           THRU 1000-EXIT.                 
                                                                                
      ***************************************************************           
      *    OPENING FILES SECTION                                    *           
      ***************************************************************           
                                                                                
       000-OPEN-FILE.                                                           
           OPEN INPUT  AC28F526                                                 
                       BP13F800                                                 
                       BP13KD40                                                 
                I-O    BP13K140                                                 
                OUTPUT BP13F125                                                 
                       BP13LC17.                                                
                                                                                
           IF K140-STATUS NOT = 0  AND                                          
              K140-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13K140 STATUS ' K140-STATUS            
              MOVE K140-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           IF KD40-STATUS NOT = 0  AND                                          
              KD40-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13KD40 STATUS ' KD40-STATUS            
              MOVE KD40-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
           STRING WS-DD '/' WS-MM '/' WS-CCYY                                   
                  DELIMITED BY SIZE INTO CL-DATE-HD.                            
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE AC28F526                         *              
      ************************************************************              
                                                                                
       100-READ-F526.                                                           
                                                                                
           READ AC28F526                                                        
                AT END MOVE HIGH-VALUE  TO F526-NUM-HDBREF                      
                MOVE 'Y'        TO WS-F526-EOF-FLAG                             
                GO TO 100-EXIT.                                                 
           ADD  1                      TO WS-CNT-AC28F526-READ.                 
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE BP13F800                         *              
      ************************************************************              
       200-READ-F800.                                                           
           READ BP13F800                                                        
                AT END MOVE HIGH-VALUE  TO F800-NUM-SCH-ACC                     
                MOVE 'Y'         TO WS-F800-EOF-FLAG                            
                GO TO 200-EXIT.                                                 
           ADD  1                       TO WS-CNT-BP13F800-READ.                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-SEQUENTIAL-FILES.                                            
                                                                                
           MOVE SPACES TO WS-CDE-APPLT  WS-NUM-NRIC.                            
                                                                                
           IF F526-NUM-HDBREF = F800-NUM-SCH-ACC                                
               PERFORM 400-PROCESS-CASH THRU 400-EXIT                           
               PERFORM 100-READ-F526    THRU 100-EXIT                           
           ELSE                                                                 
               IF F526-NUM-HDBREF > F800-NUM-SCH-ACC                            
                  PERFORM 200-READ-F800 THRU 200-EXIT                           
               ELSE                                                             
                   MOVE SPACES                TO BP13K140-KIVTRAN               
                   INITIALIZE                    BP13K140-KIVTRAN               
                   MOVE 'A'                   TO K140-CDE-ERROR                 
                   MOVE  SPACES               TO   CL-RENO                      
                   MOVE 'NOT FOUND IN SOC MASTER' TO CL-REMARKS                 
                   PERFORM 500-WRITE-KIV-REC  THRU 500-EXIT                     
                   PERFORM 100-READ-F526      THRU 100-EXIT                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-PROCESS-CASH.                                                        
                                                                                
           IF F800-NUM-ALLO-CAT = 'DBS'                                         
              PERFORM 420-READ-KD40         THRU 420-EXIT                       
              IF WS-KD40-FND = 'Y'                                              
                 PERFORM 430-OUTPUT-FILE    THRU 430-EXIT                       
              ELSE                                                              
                 MOVE SPACES                  TO BP13K140-KIVTRAN               
                 INITIALIZE                      BP13K140-KIVTRAN               
                 MOVE  F800-NUM-REGN          TO CL-RENO                        
                 MOVE 'NOT FOUND IN PP ORDER' TO CL-REMARKS                     
                 PERFORM 500-WRITE-KIV-REC  THRU 500-EXIT                       
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES                TO BP13K140-KIVTRAN                    
              INITIALIZE                    BP13K140-KIVTRAN                    
              MOVE  F800-NUM-REGN        TO   CL-RENO                           
              MOVE 'NOT A DBSS FLAT'     TO CL-REMARKS                          
              PERFORM 500-WRITE-KIV-REC  THRU 500-EXIT                          
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    SEARCH F800-NUM-REGN  IN BP13KD40                          *         
      *****************************************************************         
                                                                                
       420-READ-KD40.                                                           
                                                                                
           MOVE   SPACES             TO     BP13KD40-REC.                       
           INITIALIZE                       BP13KD40-REC.                       
           MOVE  LOW-VALUES          TO     KD40-KEY-FLD.                       
           MOVE  F800-NUM-REGN       TO     KD40-NUM-REGN.                      
           MOVE 'N'                  TO     WS-KD40-FND                         
           START BP13KD40 KEY >= KD40-KEY-FLD.                                  
                                                                                
           IF KD40-STATUS = 00 OR 02                                            
              PERFORM 425-CHECK-TALLY THRU 425-EXIT                             
                UNTIL WS-KD40-FND = 'Y'                                         
                   OR F800-NUM-REGN NOT = KD40-NUM-REGN                         
           ELSE                                                                 
              IF KD40-STATUS = 23                                               
                 MOVE 'N' TO WS-KD40-FND                                        
              ELSE                                                              
                 DISPLAY 'ERROR WITH START READ OF BP13KD40.'                   
                         'STATUS IS ' KD40-STATUS                               
                         ' KEY= ' KD40-KEY-FLD                                  
      *          MOVE KD40-STATUS   TO   RETURN-CODE                            
      *          PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
       425-CHECK-TALLY.                                                         
                                                                                
           READ BP13KD40 NEXT RECORD                                            
                                                                                
           IF KD40-STATUS = 00 OR 02                                            
              IF F800-NUM-REGN = KD40-NUM-REGN                                  
                 IF F526-NUM-RCPTCD = '6003D'                                   
                    IF KD40-AMT-PP-CASH = F526-AMT-TXN                          
                       ADD 1 TO WS-CNT-KD40-READ                                
                       MOVE 'Y' TO WS-KD40-FND                                  
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'N' TO WS-KD40-FND                                        
              END-IF                                                            
           ELSE                                                                 
             IF KD40-STATUS = 23                                                
                MOVE 'N' TO WS-KD40-FND                                         
             ELSE                                                               
                DISPLAY 'ERROR WITH READ FILE-BP13KD40.'                        
                        'STATUS IS ' KD40-STATUS                                
                MOVE HIGH-VALUES TO KD40-NUM-REGN                               
             END-IF                                                             
           END-IF.                                                              
                                                                                
       425-EXIT.                                                                
           EXIT.                                                                
                                                                                
       430-OUTPUT-FILE.                                                         
                                                                                
           MOVE SPACES                TO BP13F125-CASHTRAN.                     
           MOVE F800-NUM-REGN         TO F125-NUM-REGN.                         
           MOVE F800-NUM-NRIC1        TO F125-NUM-NRIC.                         
           MOVE WS-CURRENT-DATE       TO F125-DTE-POST.                         
           MOVE ZEROS                 TO F125-COUNT-HISTORY.                    
           MOVE F526-AMT-TXN          TO F125-AMT-RECEIPT.                      
           MOVE F526-DTE-TXN          TO F125-DTE-TRANS.                        
           MOVE F526-NUM-HDBREF       TO F125-NUM-SCH-ACCT.                     
           MOVE F526-NUM-RCPT         TO F125-NUM-RECPT-JRNO.                   
           MOVE F526-CDE-BO           TO F125-CDE-BO.                           
           MOVE '11'                  TO F125-CDE-TRANS-TYPE.                   
                                                                                
           IF F526-NUM-RCPTCD = '6003D'                                         
              MOVE 'PP '              TO F125-CDE-PAYMENT-TYPE                  
           END-IF.                                                              
           IF F800-TME-BKIMAGE = 'DBSS-TOP'                                     
              MOVE 'Y'                TO F125-NUM-DBSS-TOP                      
           END-IF.                                                              
                                                                                
           MOVE KD40-NUM-PP-SCHDL     TO F125-NUM-PP-SCHEDULE.                  
           MOVE KD40-DTE-PP-DUE       TO F125-DTE-PP-DUE.                       
                                                                                
           WRITE BP13F125-CASHTRAN.                                             
           ADD 1 TO WS-CNT-F125-VALID.                                          
                                                                                
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      * OUTPUT ERROR RECORD TO KIV FILE                                         
                                                                                
       500-WRITE-KIV-REC.                                                       
                                                                                
           IF K140-CDE-ERROR NOT = 'A'                                          
              MOVE F800-NUM-REGN      TO K140-NUM-REGN                          
              MOVE F800-NUM-NRIC1     TO K140-NUM-NRIC                          
           END-IF.                                                              
           MOVE '11'                  TO K140-CDE-TRANS-TYPE.                   
           MOVE WS-CURRENT-DATE       TO K140-DTE-POST.                         
           ADD  1                     TO WS-K140-COUNT-HISTORY.                 
           MOVE 'N'                   TO K140-COUNT-HIST-CHAR.                  
           MOVE WS-K140-COUNT-HISTORY TO K140-COUNT-HIST-NUM.                   
           MOVE F526-AMT-TXN          TO K140-AMT-RECEIPT.                      
           MOVE F526-DTE-TXN          TO K140-DTE-TRANS.                        
           MOVE F526-NUM-RCPT         TO K140-NUM-RECPT-JRNO.                   
           MOVE F526-NUM-HDBREF       TO K140-NUM-SCH-ACCT.                     
           IF F526-NUM-RCPTCD = '6003D'                                         
              MOVE 'PP '              TO K140-CDE-PAYMENT-TYPE                  
           END-IF.                                                              
           MOVE  'A'                  TO K140-CDE-ERROR.                        
                                                                                
           WRITE BP13K140-KIVTRAN.                                              
                                                                                
           MOVE K140-STATUS TO  WS-K140-STATUS.                                 
                                                                                
           IF WS-K140-STATUS1 = 0                                               
              ADD 1 TO WS-CNT-K140-KIV                                          
           ELSE                                                                 
              IF K140-STATUS = 22                                               
                 ADD  1                   TO WS-K140-COUNT-HISTORY              
                 WRITE BP13K140-KIVTRAN                                         
                 IF WS-K140-STATUS1 = 0                                         
                    ADD 1 TO WS-CNT-K140-KIV                                    
                 ELSE                                                           
                    DISPLAY ' ERROR WRITING REC : ' K140-KEY-FLD                
                            ' FILE STAT: ' WS-K140-STATUS1                      
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY ' OPENING ERROR, BP13K140 STATUS '                     
                         K140-STATUS '  KEY=' K140-KEY-FLD                      
      *          MOVE K140-STATUS           TO   RETURN-CODE                    
      *          PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 600-MOVE-VALUE-REPORT    THRU 600-EXIT.                      
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    PRINT CONTROL-LIST                                         *         
      *****************************************************************         
                                                                                
       600-MOVE-VALUE-REPORT.                                                   
                                                                                
           IF WS-LINE-CNT > 53                                                  
              MOVE ZEROES              TO   WS-LINE-CNT                         
              PERFORM 610-PRINT-HEAD   THRU 610-EXIT                            
           END-IF.                                                              
                                                                                
           MOVE  F526-NUM-HDBREF       TO   CL-SCH-ACC.                         
           MOVE  K140-CDE-PAYMENT-TYPE TO   CL-PAY-TY.                          
           MOVE  F526-NUM-RCPT         TO   CL-JR-NO.                           
           MOVE  F526-AMT-TXN          TO   CL-AMT-RECPT.                       
                                                                                
           ADD +1                      TO   WS-SNO.                             
           MOVE  WS-SNO                TO   CL-SNO.                             
           PERFORM 620-PRINT-REC       THRU 620-EXIT.                           
                                                                                
        600-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       610-PRINT-HEAD.                                                          
           ADD +1           TO  WS-PAGENO.                                      
           MOVE WS-PAGENO   TO  CL-PAGENO.                                      
           MOVE SPACES      TO  PRINT-CONTROL-LIST.                             
           WRITE PRINT-CONTROL-LIST  AFTER PAGE.                                
           WRITE PRINT-CONTROL-LIST.                                            
           WRITE PRINT-CONTROL-LIST.                                            
           WRITE PRINT-CONTROL-LIST.                                            
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-HEAD.                       
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-1.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-4.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-2.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-5.                     
           ADD +8  TO WS-LINE-CNT.                                              
                                                                                
       610-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
       620-PRINT-REC.                                                           
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-3.                     
           ADD +1  TO WS-LINE-CNT.                                              
                                                                                
       620-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
       1000-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY '******  BP13CC17 *************'.                            
           DISPLAY 'NO OF F526 READ      (F526) : '                             
                                             WS-CNT-AC28F526-READ.              
           DISPLAY 'NO OF RFF RECS READ  (F800) : '                             
                                             WS-CNT-BP13F800-READ.              
           DISPLAY 'NO OF KD40 REC READ  (KD40) : ' WS-CNT-KD40-READ.           
           DISPLAY 'NO OF F125 REC WRITE (F125) : ' WS-CNT-F125-VALID.          
           DISPLAY 'NO OF K140 REC WRITE (K140) : ' WS-CNT-K140-KIV.            
                                                                                
           CLOSE BP13F800                                                       
                 AC28F526                                                       
                 BP13F125                                                       
                 BP13K140                                                       
                 BP13KD40                                                       
                 BP13LC17.                                                      
                                                                                
           IF K140-STATUS NOT = 0 AND K140-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13K140 STATUS ' K140-STATUS           
               MOVE K140-STATUS TO RETURN-CODE.                                 
                                                                                
           IF KD40-STATUS NOT = 0 AND KD40-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13KD40 STATUS ' KD40-STATUS           
               MOVE KD40-STATUS TO RETURN-CODE.                                 
                                                                                
       1000-EXIT.                                                               
           STOP RUN.                                                            
