      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C25N.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  04 JUNE 2018.                                             
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      * OBJECTIVE :                                                   *         
      *  1. TO UPDATE FQ10-CDE-NOTIFICATION-PRINTED                   *         
      *---------------------------------------------------------------*         
      * CHG REF  BY    DATE    DESCRIPTION                            *         
      * -------- ---  -------- -----------                            *         
      * BP137047 ESA1 04/06/18 NEW PROGRAM.                           *         
      *===============================================================*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN       TO BP13F800.                           
                                                                                
           SELECT BP13FQ10  ASSIGN       TO BP13FQ10.                           
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F800                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13F800.                                                           
                                                                                
       FD  BP13FQ10                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13FQ10.                                                           
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01 WS-F800-EOF             PIC X(01) VALUE 'N'.                          
          88 F800-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-FQ10-EOF             PIC X(01) VALUE 'N'.                          
          88 FQ10-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-F800-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-FQ10-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-FQ10-REWRITE-CNT     PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-EDT-DATE             PIC X(10) VALUE SPACES.                       
       01 WS-CUR-DATE             PIC X(08) VALUE SPACES.                       
                                                                                
       01 WS-EDIT1                PIC Z(07)9.                                   
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F800      THRU 2000-EXIT.                      
           PERFORM 3000-READ-BP13FQ10      THRU 3000-EXIT.                      
           PERFORM 4000-PROCESS-DATA       THRU 4000-EXIT                       
                   UNTIL F800-END-OF-FILE  OR                                   
                         FQ10-END-OF-FILE.                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN  INPUT  BP13F800                                                
                 I-O    BP13FQ10.                                               
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F800.                                                      
      *---------------------------------------------------------------*         
           READ BP13F800 AT END                                                 
                          MOVE 'Y' TO WS-F800-EOF                               
                          GO TO 2000-EXIT.                                      
                                                                                
           ADD    1    TO WS-F800-READ-CNT.                                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-READ-BP13FQ10.                                                      
      *---------------------------------------------------------------*         
           READ BP13FQ10 AT END                                                 
                          MOVE 'Y' TO WS-FQ10-EOF                               
                          GO TO 3000-EXIT.                                      
                                                                                
           ADD    1    TO WS-FQ10-READ-CNT.                                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           IF F800-NUM-REGN =  FQ10-NUM-REGN                                    
              MOVE 'Y' TO FQ10-CDE-NOTIFICATION-PRINTED                         
              REWRITE BP13FQ10-REC                                              
              ADD 1       TO  WS-FQ10-REWRITE-CNT                               
              PERFORM 2000-READ-BP13F800  THRU 2000-EXIT                        
              PERFORM 3000-READ-BP13FQ10  THRU 3000-EXIT                        
           ELSE                                                                 
              IF F800-NUM-REGN  >  FQ10-NUM-REGN                                
                 PERFORM 3000-READ-BP13FQ10  THRU 3000-EXIT                     
              ELSE                                                              
                 IF F800-NUM-REGN < FQ10-NUM-REGN                               
                    PERFORM 2000-READ-BP13F800  THRU 2000-EXIT                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C25N       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F800-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13F800  : ' WS-EDIT1.                
           MOVE WS-FQ10-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13FQ10  : ' WS-EDIT1.                
           MOVE WS-FQ10-REWRITE-CNT TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS UPDATED TO BP13FQ10 : ' WS-EDIT1.                
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13F800                                                     
                   BP13FQ10.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
