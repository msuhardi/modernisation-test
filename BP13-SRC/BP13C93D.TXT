      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID.     BP13C93D.                                                
      *AUTHOR.         IRVIN CHING.                                             
      *DATE-WRITTEN.   11/06/2014.                                              
      *--------------------------------------------------------*                
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1. PROGRAM TO READ FIS BP13FA30 FOR FLAT DATA     *                
      *         PROGRAM TO READ SOC BP13KB78 FOR BOOK OCCUP    *                
      *         PROGRAM TO READ SOC BP13KB78 FOR TP   OCCUP    *                
      *         PROGRAM TO READ SOC BP13KA10 FOR EST-TOP DATE  *                
      *      2. PROGRAM TO CREATE FILE BP13F213 FOR ELECTION   *                
      *         DEPARTMENT ON BOOKED/TP WITHIN HALF-YEAR PERIOD*                
      *      3. OUTPUT FILES FOR EC BOOKED, TOP                *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP135352 IMC1 200712  NEW PROGRAM                      *                
      * BP135797 SMR2 180515  ADJUST CUTOFF COMPUTATION        *                
      * BP136256 FNP  100616  PROCESS RECORDS W/O ACCEPT DATE  *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13FA30  ASSIGN        TO BP13FA30.                          
                                                                                
           SELECT BP13KB78  ASSIGN        TO BP13KB78                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS KB78-KEY-FLD                       
                            FILE STATUS   IS WS-KB78-STATUS.                    
                                                                                
           SELECT BP13KD15  ASSIGN        TO BP13KD15                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD15-KEY-FLD                       
                            FILE STATUS   IS WS-KD15-STATUS.                    
                                                                                
           SELECT BP13KA10  ASSIGN        TO BP13KA10                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS KA10-KEY-FLD                       
                            FILE STATUS   IS WS-KA10-STATUS.                    
                                                                                
           SELECT BP13KA20  ASSIGN        TO BP13KA20                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KA20-KEY-FLD                       
                            FILE STATUS   IS WS-KA20-STATUS.                    
                                                                                
           SELECT P13F213A  ASSIGN        TO P13F213A.                          
           SELECT P13F213B  ASSIGN        TO P13F213B.                          
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FA30                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13FA30.                                                           
                                                                                
       FD   BP13KB78                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB78.                                                           
                                                                                
       FD   BP13KA10                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP13KA10.                                                           
                                                                                
       FD   BP13KA20                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13KA20.                                                           
                                                                                
       FD   BP13KD15                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13KD15.                                                           
                                                                                
       FD   P13F213A                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   P13F213A-REC        PIC X(150).                                     
                                                                                
       FD   P13F213B                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   P13F213B-REC        PIC X(150).                                     
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-READ-FA30              PIC 9(8)    VALUE 0.                    
           05 WS-READ-F089              PIC 9(8)    VALUE 0.                    
           05 WS-MATCH                  PIC 9(8)    VALUE 0.                    
           05 WS-NOT-MATCH              PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-F213A            PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-F213B            PIC 9(8)    VALUE 0.                    
           05 WS-F213-STATUS            PIC 9(02)   VALUE ZEROES.               
           05 WS-FA30-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-KB78-FND               PIC X(1)    VALUE 'N'.                  
           05 WS-KB78-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-K895-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-KB78-STATUS            PIC 9(2)    VALUE 0.                    
           05 WS-KA10-STATUS            PIC 9(2)    VALUE 0.                    
           05 WS-KA20-STATUS            PIC 9(2)    VALUE 0.                    
           05 WS-KD15-STATUS            PIC 9(2)    VALUE 0.                    
                                                                                
       01  WS-BP13F213-REC              PIC X(150)  VALUE SPACES.               
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CDE-BLK               PIC X(5)    VALUE SPACES.               
           05  WS-CUTOFF-START.                                                 
               10 WS-YYYY-ST            PIC 9(4)    VALUE ZEROES.               
               10 WS-MM-ST              PIC 9(2)    VALUE ZEROES.               
               10 WS-DD-ST              PIC 9(2)    VALUE ZEROES.               
           05  WS-CUTOFF-END.                                                   
               10 WS-YYYY-END           PIC 9(4)    VALUE ZEROES.               
               10 WS-MM-END             PIC 9(2)    VALUE ZEROES.               
               10 WS-DD-END             PIC 9(2)    VALUE ZEROES.               
                                                                                
       COPY BP13F213.                                                           
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-FA30     THRU 2000-EXIT.                           
                                                                                
           PERFORM 4000-COMPARE       THRU 4000-EXIT                            
             UNTIL WS-FA30-EOF = 'Y'                                            
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13FA30                                                 
                       BP13KB78                                                 
                       BP13KD15                                                 
                       BP13KA10                                                 
                       BP13KA20                                                 
                       SY02F001                                                 
               OUTPUT  P13F213A                                                 
                       P13F213B.                                                
                                                                                
           IF WS-KB78-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR, KB78-STATUS ' WS-KB78-STATUS                 
           END-IF.                                                              
                                                                                
           IF WS-KD15-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR, KD15-STATUS ' WS-KD15-STATUS                 
           END-IF.                                                              
                                                                                
           IF WS-KA20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR, KA20-STATUS ' WS-KA20-STATUS                 
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
           MOVE F001-DTE-CURRENT       TO WS-CUTOFF-START                       
           MOVE F001-DTE-LAST-DAY-PREV TO WS-CUTOFF-END                         
                                                                                
           IF WS-MM-ST <= 6                                                     
              COMPUTE WS-YYYY-ST = WS-YYYY-ST - 1                               
              COMPUTE WS-MM-ST = WS-MM-ST + 6                                   
              MOVE  1        TO WS-DD-ST                                        
           ELSE                                                                 
              COMPUTE WS-MM-ST = WS-MM-ST - 6                                   
              MOVE  1        TO WS-DD-ST                                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-FA30.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13FA30                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-FA30-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-READ-FA30                                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-COMPARE.                                                            
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES             TO BP13F213-REC                              
                                      WS-BP13F213-REC.                          
           INITIALIZE                 BP13F213-REC                              
                                      WS-BP13F213-REC.                          
                                                                                
           PERFORM 4200-READ-BP13KD15  THRU 4200-EXIT                           
                                                                                
           IF F213-DTE-KEY-ISSUED = SPACES OR ZEROES OR LOW-VALUES              
              PERFORM 4050-PROCESS-REC     THRU 4050-EXIT                       
           ELSE                                                                 
              IF F213-DTE-KEY-ISSUED >= WS-CUTOFF-START AND                     
                 F213-DTE-KEY-ISSUED <= WS-CUTOFF-END                           
                 PERFORM 4050-PROCESS-REC     THRU 4050-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FA30       THRU 2000-EXIT.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4050-PROCESS-REC.                                                        
      *--------------------------------------------------------*                
                                                                                
           MOVE FA30-NUM-BLK           TO F213-NUM-BLOCK                        
           MOVE FA30-NUM-MAIN-UNIT     TO F213-NUM-MAIN                         
           MOVE FA30-NUM-SUB-UNIT      TO F213-NUM-SUB                          
           MOVE FA30-NUM-LEVEL         TO F213-NUM-LEVEL                        
           MOVE FA30-DTE-ACCEPT        TO F213-DTE-ACCEPTANCE                   
           MOVE FA30-NUM-FLAT-TYPE     TO F213-NUM-FLAT-TYPE                    
           PERFORM 4300-READ-BP13KA20  THRU 4300-EXIT                           
                                                                                
           IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR ZEROES              
              IF FA30-NUM-REGN(1:1) = '5' OR '6'                                
                 PERFORM 4600-READ-BP13KA10  THRU 4600-EXIT                     
              END-IF                                                            
           END-IF                                                               
                                                                                
           MOVE BP13F213-REC           TO WS-BP13F213-REC                       
           IF FA30-DTE-ACCEPT = SPACES OR LOW-VALUES OR ZEROES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE SPACES                TO F213-NUM-NRIC                      
               MOVE SPACES                TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
           ELSE                                                                 
              PERFORM 4400-READ-BP13KB78  THRU 4400-EXIT                        
           END-IF.                                                              
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4200-READ-BP13KD15.                                                      
      *-------------------*                                                     
           MOVE FA30-KEY-FLD                    TO KD15-KEY-FLD                 
                                                                                
           READ BP13KD15                                                        
           EVALUATE WS-KD15-STATUS                                              
               WHEN 00                                                          
                    MOVE KD15-DTE-IND-TP   TO F213-DTE-KEY-ISSUED               
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'BP13KD15 NOT FND ' KD15-KEY-FLD                    
                            ' ' FA30-NUM-SCH-ACC                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13KD15 READ NEXT ERROR ' WS-KD15-STATUS          
                    DISPLAY 'KD15 KEY: ' KD15-KEY-FLD                           
                    MOVE WS-KD15-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------*                                            
       4300-READ-BP13KA20.                                                      
      *----------------------------*                                            
                                                                                
           MOVE SPACES                    TO BP13KA20-REC.                      
           INITIALIZE                        BP13KA20-REC.                      
                                                                                
           MOVE FA30-NUM-PRJCT            TO KA20-NUM-PRJCT                     
           MOVE FA30-NUM-BLK              TO KA20-NUM-BLK                       
                                                                                
           READ BP13KA20.                                                       
                                                                                
           EVALUATE WS-KA20-STATUS                                              
               WHEN 00                                                          
                    MOVE KA20-NUM-POSTAL   TO F213-NUM-POSTAL                   
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'BP13KA20 NOT FND ' KA20-KEY-FLD                    
                            ' ' FA30-NUM-SCH-ACC                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13KA20 READ NEXT ERROR ' WS-KA20-STATUS          
                    DISPLAY 'KA20 KEY: ' KA20-KEY-FLD                           
                    MOVE WS-KA20-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4400-READ-BP13KB78.                                                      
      *************************************************************             
                                                                                
            MOVE SPACES                              TO BP13KB78-REC.           
            INITIALIZE                                  BP13KB78-REC.           
            MOVE 'N'                                 TO WS-KB78-FND.            
                                                                                
            MOVE FA30-NUM-REGN                       TO KB78-NUM-REGN.          
                                                                                
            READ BP13KB78.                                                      
                                                                                
            EVALUATE WS-KB78-STATUS                                             
                WHEN 00                                                         
                     PERFORM 4500-WRITE-OUTPUT    THRU 4500-EXIT                
                WHEN 23                                                         
                     DISPLAY 'BP13KB78 NOT FOUND: ' KB78-NUM-REGN               
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13KB78 : ' WS-KB78-STATUS         
                             ' REGN NO = ' KB78-NUM-REGN                        
                     PERFORM  9999-CLOSE-ROUTINE   THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       4400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       4500-WRITE-OUTPUT.                                                       
      *************************************************************             
                                                                                
            IF KB78-NUM-APPLT1-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-APPLT1-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-APPLT1       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF KB78-NUM-APPLT2-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-APPLT2-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-APPLT2       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF KB78-NUM-APPLT3-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-APPLT3-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-APPLT3       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF KB78-NUM-APPLT4-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-APPLT4-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-APPLT4       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF KB78-NUM-OCCUP1-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-OCCUP1-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-OCCUP1       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF KB78-NUM-OCCUP2-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-OCCUP2-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-OCCUP2       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF KB78-NUM-OCCUP3-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-OCCUP3-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-OCCUP3       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF KB78-NUM-OCCUP4-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-OCCUP4-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-OCCUP4       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF KB78-NUM-OCCUP5-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-OCCUP5-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-OCCUP5       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF KB78-NUM-OCCUP6-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-OCCUP6-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-OCCUP6       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF KB78-NUM-OCCUP7-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-OCCUP7-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-OCCUP7       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF KB78-NUM-OCCUP8-UIN NOT = SPACES AND LOW-VALUES                  
               MOVE WS-BP13F213-REC       TO BP13F213-REC                       
               MOVE KB78-NUM-OCCUP8-UIN   TO F213-NUM-NRIC                      
               MOVE KB78-NME-OCCUP8       TO F213-NME-OCCP                      
               IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                    
                                               OR ZEROES                        
                  WRITE P13F213A-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213A                                       
               ELSE                                                             
                  WRITE P13F213B-REC  FROM BP13F213-REC                         
                  ADD 1 TO WS-WRITE-F213B                                       
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------           
       4600-READ-BP13KA10.                                                      
      *--------------------------------------------------------------           
                                                                                
           INITIALIZE KA10-KEY-FLD.                                             
                                                                                
           MOVE FA30-NUM-PRJCT           TO KA10-KEY-FLD.                       
                                                                                
           READ BP13KA10.                                                       
                                                                                
           IF WS-KA10-STATUS = 00                                               
              MOVE KA10-DTE-ESTMD-TOP    TO F213-DTE-EST-COMPLETION             
           ELSE                                                                 
              IF WS-KA10-STATUS = 23                                            
                 DISPLAY 'BP13KA10 - REC NOT FND '                              
                         ',KEY IS '  KA10-KEY-FLD                               
              ELSE                                                              
                 DISPLAY 'BP13KA10 - ERROR ON READ ' WS-KA10-STATUS             
                         ',KEY IS '  KA10-KEY-FLD                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '*********** BP13C93D **************'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS READ (BP13FA30) :' WS-READ-FA30.             
           DISPLAY 'NO. OF RECORDS WRITE(P13F213A) :' WS-WRITE-F213A.           
           DISPLAY 'NO. OF RECORDS WRITE(P13F213B) :' WS-WRITE-F213B.           
           DISPLAY SPACES.                                                      
           DISPLAY '************************************'.                      
                                                                                
           CLOSE       BP13FA30                                                 
                       BP13KD15                                                 
                       BP13KA10                                                 
                       BP13KA20                                                 
                       BP13KB78                                                 
                       SY02F001                                                 
                       P13F213A                                                 
                       P13F213B.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
