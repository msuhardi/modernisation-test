       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC86.                                                 
      *AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN : 04 JULY 2007.                                             
      *===============================================================*         
      *  SYSTEM OF COMMITMENT (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE   :                                                *         
      *    TO READ BP13F125 AND TOTAL ALL RECORDS WITH SAME REGN NO.  *         
      *    AND NRIC :                                                 *         
      *    1.  PAID BY CPF                                            *         
      *    2.  PAID BY CASH                                           *         
      *                                                               *         
      *  INPUT  : BP13F125                                            *         
      *  OUTPUT : P13F125A                                            *         
      *                                                               *         
      *---------------------------------------------------------------*         
      *  CHG-NO   BY    DATE   DESCRIPTION                            *         
      * -------- ---- -------- ---------------------------------------*         
      * BP133051 SD10 20070704 NEW PROGRAM                            *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F125 ASSIGN TO BP13F125.                                  
           SELECT P13F125A ASSIGN TO P13F125A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F125                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F125.                                                           
                                                                                
       FD  P13F125A                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       01  P13F125A-REC            PIC X(150).                                  
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  WS-F125-READ            PIC 9(5)     VALUE ZEROS.                
           05  WS-F125-WRITE           PIC 9(5)     VALUE ZEROS.                
           05  WS-PREV-NUM-REGN        PIC X(8)     VALUE SPACES.               
           05  WS-PREV-CDE-TRANS-TYPE  PIC X(2)     VALUE SPACES.               
           05  WS-PREV-NUM-NRIC        PIC X(9)     VALUE SPACES.               
           05  WS-F125-AMT-RECEIPT     PIC 9(7)V99  VALUE ZEROS.                
           05  WS-BP13F125-CASHTRAN    PIC X(150)   VALUE SPACES.               
           05  WS-EOF-F125             PIC X(3).                                
               88  EOF-OF-F125                      VALUE 'YES'.                
           05  WS-FIRST-REC-SW         PIC X        VALUE SPACE.                
                                                                                
                                                                                
      *===================*                                                     
       PROCEDURE DIVISION.                                                      
      *===================*                                                     
       0000-MAIN-LOGIC.                                                         
      *===================*                                                     
                                                                                
           PERFORM 1000-INITIALIZATION   THRU 1000-EXIT.                        
           PERFORM 4000-GET-F125-RECS THRU 4000-EXIT                            
              UNTIL EOF-OF-F125.                                                
           PERFORM 9000-FINALIZATION     THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       1000-INITIALIZATION.                                                     
      *====================*                                                    
                                                                                
           OPEN INPUT  BP13F125                                                 
                OUTPUT P13F125A.                                                
                                                                                
           MOVE 'NO'     TO WS-EOF-F125.                                        
           MOVE 'Y'      TO WS-FIRST-REC-SW.                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================*                                                 
       4000-GET-F125-RECS.                                                      
      *=======================*                                                 
                                                                                
           PERFORM 4100-READ-F125        THRU 4100-EXIT.                        
                                                                                
           IF WS-F125-READ = 0                                                  
              GO TO 4000-EXIT.                                                  
                                                                                
           IF F125-NUM-REGN = WS-PREV-NUM-REGN                                  
              IF F125-CDE-TRANS-TYPE = WS-PREV-CDE-TRANS-TYPE AND               
                 F125-NUM-NRIC = WS-PREV-NUM-NRIC                               
                 PERFORM 4300-COMPUTE-AMT    THRU 4300-EXIT                     
              ELSE                                                              
                 PERFORM 4200-WRITE-PREV-REC THRU 4200-EXIT                     
                 PERFORM 4300-COMPUTE-AMT    THRU 4300-EXIT                     
                 MOVE F125-CDE-TRANS-TYPE    TO WS-PREV-CDE-TRANS-TYPE          
                 MOVE F125-NUM-NRIC          TO WS-PREV-NUM-NRIC                
              END-IF                                                            
           ELSE                                                                 
              IF WS-FIRST-REC-SW = 'Y'                                          
                 MOVE SPACES            TO WS-FIRST-REC-SW                      
              ELSE                                                              
                 PERFORM 4200-WRITE-PREV-REC THRU 4200-EXIT                     
              END-IF                                                            
              PERFORM 4300-COMPUTE-AMT  THRU 4300-EXIT                          
              MOVE F125-NUM-REGN        TO WS-PREV-NUM-REGN                     
              MOVE F125-CDE-TRANS-TYPE  TO WS-PREV-CDE-TRANS-TYPE               
              MOVE F125-NUM-NRIC        TO WS-PREV-NUM-NRIC                     
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       4100-READ-F125.                                                          
      *====================*                                                    
                                                                                
           READ BP13F125 AT END MOVE 'YES' TO WS-EOF-F125                       
                                MOVE HIGH-VALUE TO F125-NUM-REGN                
               GO TO 4100-EXIT.                                                 
            ADD 1 TO WS-F125-READ.                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================*                                                 
       4200-WRITE-PREV-REC.                                                     
      *=======================*                                                 
                                                                                
           MOVE WS-BP13F125-CASHTRAN   TO P13F125A-REC.                         
           WRITE P13F125A-REC.                                                  
           ADD 1  TO WS-F125-WRITE.                                             
           MOVE ZEROES                 TO WS-F125-AMT-RECEIPT.                  
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================*                                                 
       4300-COMPUTE-AMT.                                                        
      *=======================*                                                 
                                                                                
           COMPUTE WS-F125-AMT-RECEIPT =                                        
                   WS-F125-AMT-RECEIPT +                                        
                   F125-AMT-RECEIPT.                                            
                                                                                
           MOVE WS-F125-AMT-RECEIPT  TO F125-AMT-RECEIPT.                       
           MOVE BP13F125-CASHTRAN    TO WS-BP13F125-CASHTRAN.                   
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================*                                                 
       9000-FINALIZATION.                                                       
      *=======================*                                                 
             DISPLAY '*-------- BP13CC86 -----------*'.                         
             DISPLAY ' BP13F125 RECS READ    :' WS-F125-READ.                   
             DISPLAY ' P13F125A RECS WRITTEN :' WS-F125-WRITE.                  
                                                                                
           CLOSE      BP13F125                                                  
                      P13F125A.                                                 
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
