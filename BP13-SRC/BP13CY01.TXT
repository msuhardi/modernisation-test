      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY01.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  14/04/08.                                                 
      *****************************************************************         
      * OBJECTIVE -                                                             
      *   MATCH F205 WITH F813 TO GET NT OFFERED UNDER THE BTO LAUNCH           
      *                                                                         
      *     INPUT FILE :                                                        
      *                  1. BP13F205                                            
      *                  2. BP13F813                                            
      *                                                                         
      *     OUTPUT FILE:                                                        
      *                  1. P13F813A                                            
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS                        
      * -------   --------  ---  ------------------------                       
      * BP133340  14/04/08  JB8  NEW PROGRAM.                                   
      *           14/07/08  JB8  CHANGE BP13K767(150) TO BP13K816(400)          
      * BP133614  08/07/09  PCL3 TO HANDLE F205-DTE-ALLOCN FOR AY07 JOB         
      * BP135340  12/05/14  IMC1 TO REPLACE BP13K816 WITH BP13K813              
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F205  ASSIGN        TO BP13F205.                         
           SELECT  BP13F813  ASSIGN        TO BP13F813.                         
           SELECT  P13F813A  ASSIGN        TO P13F813A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F205                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 80 CHARACTERS            
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F813                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 1000 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13K813.                                                           
                                                                                
       FD   P13F813A                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 1000 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13F813A-REC                PIC X(1000).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  WORK-FILE-VARIABLE.                                                  
           05  WS-EOF-BP13F813         PIC X     VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-BP13F813-READ         PIC 9(7)  VALUE ZEROES.                  
           05 WS-P13F813A-WRITEN       PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CCYY            PIC X(4)  VALUE SPACES.                  
           05  WS-CURR-MM              PIC X(2)  VALUE SPACES.                  
           05  WS-CURR-DD              PIC X(2)  VALUE SPACES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F205      THRU 2000-EXIT.                      
           PERFORM 2100-READ-BP13F813      THRU 2100-EXIT                       
                                                                                
           PERFORM 3000-PROCESS-BP13F813   THRU 3000-EXIT                       
                   UNTIL WS-EOF-BP13F813   = 'Y'.                               
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13F813                                                 
                OUTPUT P13F813A.                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205           AT END                                       
                GO                 TO 9999-CLOSE-ROUTINE.                       
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2100-READ-BP13F813.                                                      
      ******************************************************************        
                                                                                
           READ BP13F813           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-BP13F813                         
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-BP13F813-READ.                       
                                                                                
       2100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-BP13F813.                                                   
      ******************************************************************        
                                                                                
           IF K813-DTE-OPEN-BTO(1:6) = F205-DTE-ALLOCN(1:6)                     
              IF F205-NUM-OPTION = 'Y7'                                         
                 PERFORM 4000-WRITE-P13F813A            THRU 4000-EXIT          
              ELSE                                                              
                 IF K813-DTE-OPEN-BTO = F205-DTE-ALLOCN                         
                    PERFORM 4000-WRITE-P13F813A         THRU 4000-EXIT          
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF K813-DTE-OPEN-BTO > F205-DTE-ALLOCN                            
                 MOVE 'Y'                TO WS-EOF-BP13F813                     
                 GO TO 3000-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-BP13F813  THRU 2100-EXIT.                          
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      *********************                                                     
       4000-WRITE-P13F813A.                                                     
      *********************                                                     
                                                                                
           WRITE P13F813A-REC          FROM BP13K813-REC.                       
           ADD 1                         TO WS-P13F813A-WRITEN.                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CY01'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F813 RECORDS READ..........: '                    
                    WS-BP13F813-READ.                                           
           DISPLAY 'NO OF P13F813A RECORDS WRITTEN.......: '                    
                    WS-P13F813A-WRITEN.                                         
                                                                                
           CLOSE BP13F205                                                       
                 BP13F813                                                       
                 P13F813A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
