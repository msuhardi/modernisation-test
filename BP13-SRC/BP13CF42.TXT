       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF42.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  19/12/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  COMPUTE EXTRA ETHNIC PROPORTION TAKEN BY   *            
      *                 GRO THEN ADJUST ETHNIC PROPORTION FOR PA   *            
      *                 WHICH HAVE BEEN TAKEN UP BY GRO            *            
      *                                                            *            
      * I-O FILE     :  BP13K010  BP13.K010.TENDER                 *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP135170  SMR2  19122013  SIMILAR TO BP13C751              *            
      * BP135847  SMR2  25062015  ENHANCE CALCULATION              *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.     IBM-3090.                                           
       OBJECT-COMPUTER.     IBM-3090.                                           
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K010    ASSIGN TO BP13K010                                
                              ORGANIZATION IS INDEXED                           
                              ACCESS MODE  IS SEQUENTIAL                        
                              RECORD KEY   IS K010-KEY-FLD                      
                              FILE STATUS  IS WS-K010-STATUS.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K010                                                             
                              LABEL RECORD IS STANDARD                          
                              RECORD CONTAINS 600 CHARACTERS                    
                              BLOCK CONTAINS 0 RECORDS.                         
           COPY BP13K010.                                                       
                                                                                
                                                                                
        WORKING-STORAGE SECTION.                                                
        01  WS-VARIABLES.                                                       
            05  WS-CNT-K010-READ         PIC 9(8) VALUE 0.                      
            05  WS-CNT-K010-REWRITE      PIC 9(8) VALUE 0.                      
            05  WS-CAT1                  PIC 9(5) VALUE 0.                      
            05  WS-CAT2                  PIC 9(5) VALUE 0.                      
            05  WS-CAT3                  PIC 9(5) VALUE 0.                      
            05  WS-K010-STATUS           PIC 99   VALUE 0.                      
            05  WS-K010-EOF              PIC X    VALUE 'N'.                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-CONTROL-RTN.                                                        
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-START-BP13K010-KEY THRU 2000-EXIT.              01790000
           PERFORM 4000-MAIN-ROUTINE     THRU 4000-EXIT                         
             UNTIL WS-K010-EOF = 'Y'.                                           
           PERFORM 9999-CLOSE-FILES      THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN I-O    BP13K010.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                        01780000
       2000-START-BP13K010-KEY.                                         01790000
            MOVE SPACES TO K010-KEY-FLD                                 01800000
            START BP13K010 KEY > K010-KEY-FLD                           01810000
                                                                                
           IF WS-K010-STATUS  = 00                                              
              PERFORM 3000-READ-BP13K010    THRU 3000-EXIT                      
           ELSE                                                                 
              DISPLAY 'START READ BP13K010, STATUS ' WS-K010-STATUS             
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K010.                                                      
                                                                                
           READ BP13K010 NEXT AT END                                            
                              MOVE 'Y' TO WS-K010-EOF                           
                              GO TO 3000-EXIT.                                  
           ADD 1 TO WS-CNT-K010-READ.                                           
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                        01860000
       4000-MAIN-ROUTINE.                                                       
           MOVE ZEROS TO WS-CAT1, WS-CAT2, WS-CAT3.                             
                                                                                
           IF K010-REG-CCC1 NOT NUMERIC                                         
              MOVE ZEROES      TO K010-REG-CCC1                                 
           END-IF.                                                              
           COMPUTE WS-CAT1 = K010-NUM-MAX-CCC1 - K010-REG-CCC1.                 
           IF WS-CAT1 > K010-NUM-MAX-PA1                                        
              COMPUTE WS-CAT1 = WS-CAT1 - K010-NUM-MAX-PA1                      
              ADD WS-CAT1      TO K010-NUM-MAX-PA1                              
           ELSE                                                                 
              COMPUTE WS-CAT1 = WS-CAT1 - K010-NUM-MAX-PA1                      
              COMPUTE K010-NUM-MAX-PA1 = K010-NUM-MAX-PA1 - WS-CAT1             
           END-IF.                                                              
           MOVE K010-REG-CCC1  TO K010-NUM-MAX-CCC1.                            
                                                                                
           IF K010-REG-CCC2 NOT NUMERIC                                         
              MOVE ZEROES      TO K010-REG-CCC2                                 
           END-IF.                                                              
           COMPUTE WS-CAT2 = K010-NUM-MAX-CCC2 - K010-REG-CCC2.                 
           IF WS-CAT2 > K010-NUM-MAX-PA2                                        
              COMPUTE WS-CAT2 = WS-CAT2 - K010-NUM-MAX-PA2                      
              ADD WS-CAT2      TO K010-NUM-MAX-PA2                              
           ELSE                                                                 
              COMPUTE WS-CAT2 = WS-CAT2 - K010-NUM-MAX-PA2                      
              COMPUTE K010-NUM-MAX-PA2 = K010-NUM-MAX-PA2 - WS-CAT2             
           END-IF.                                                              
           MOVE K010-REG-CCC2  TO K010-NUM-MAX-CCC2.                            
                                                                                
           IF K010-REG-CCC3 NOT NUMERIC                                         
              MOVE ZEROES      TO K010-REG-CCC3                                 
           END-IF.                                                              
           COMPUTE WS-CAT3 = K010-NUM-MAX-CCC3 - K010-REG-CCC3.                 
           IF WS-CAT3 > K010-NUM-MAX-PA3                                        
              COMPUTE WS-CAT3 = WS-CAT3 - K010-NUM-MAX-PA3                      
              ADD WS-CAT3      TO K010-NUM-MAX-PA3                              
           ELSE                                                                 
              COMPUTE WS-CAT3 = WS-CAT3 - K010-NUM-MAX-PA3                      
              COMPUTE K010-NUM-MAX-PA3 = K010-NUM-MAX-PA3 - WS-CAT3             
           END-IF.                                                              
           MOVE K010-REG-CCC3  TO K010-NUM-MAX-CCC3.                            
                                                                                
           PERFORM 6000-REWRITE-ROUTINE    THRU 6000-EXIT.                      
           PERFORM 3000-READ-BP13K010      THRU 3000-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6000-REWRITE-ROUTINE.                                                    
                                                                                
              REWRITE BP13K010-TENDER.                                          
                                                                                
              IF WS-K010-STATUS = 00 OR 02                                      
                 ADD 1                   TO WS-CNT-K010-REWRITE                 
              ELSE                                                              
                 DISPLAY 'ERROR REWRITING BP13K010, STATUS '                    
                                            WS-K010-STATUS                      
                 DISPLAY '                          KEY IS '                    
                                               K010-KEY-FLD                     
                 MOVE WS-K010-STATUS        TO RETURN-CODE                      
                 GO TO 9999-CLOSE-FILES.                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-CLOSE-FILES.                                                        
                                                                                
           DISPLAY '>>>>>>>>>>>>>>>> BP13CF42 <<<<<<<<<<<<<<<<<<'.              
           DISPLAY 'UPDATE K010-NUM-MAX-CAT AFTER PIGEONHOLE GRO '.             
           DISPLAY '  '.                                                        
           DISPLAY 'BP13K010 RECORDS READ    : '  WS-CNT-K010-READ.             
           DISPLAY 'BP13K010 RECORDS REWRITE : '  WS-CNT-K010-REWRITE.          
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
               DISPLAY 'ERROR CLOSING BP13K010, STATUS ' WS-K010-STATUS         
               MOVE WS-K010-STATUS       TO RETURN-CODE.                        
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
