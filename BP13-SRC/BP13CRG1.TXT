       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CRG1.                                                 
      *AUTHOR.        KARL MAGALONA CABUG.                                      
      *DATE-WRITTEN.  10 FEB 2017.                                              
      *============================================================*            
      * BP13 - SYSTEM OF COMMITMENT                                *            
      *============================================================*            
      * OBJECTIVE :                                                *            
      * INPUT     :  BM06F120  -                                   *            
      *              BM06K110  -                                   *            
      *              BP13K800  -                                   *            
      *              BP13K893  -                                   *            
      *              BP13K820  -                                   *            
      *              BP13K895  -                                   *            
      *              BP13K830  -                                   *            
      *              BP13K857  -                                   *            
      *              BP13K813  -                                   *            
      * OUTPUT    :  BP13FOUT  -                                   *            
      *                                                            *            
      *----------------------------------------------------------- *            
      * CHG REF  DATE     BY     DESCRIPTION                       *            
      * -------- -------- ----   ------------------------------    *            
      * BM06XXXX 10022017 KAM4   NEW PROGRAM.                      *            
      * BP137103 21122017 KAM4   ADDED COLUMN FOR UNITREF          *            
      *                          REMOVED WORDING '(end of 2016)'   *            
      *                          ALSO USE CURR DATE TO GET THE AGE *            
      * BP13XXXX 21022018 KAM4   ADDED CHECK FOR 3GEN              *            
      * BP13XXXX 27052017 CC37   ADDED HISTORY FILES               *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BM06F120 ASSIGN TO BM06F120.                                  
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN IN BP13K800-MASTER           
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K893 ASSIGN TO BP13K893                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K893-KEY-FLD                               
                  FILE STATUS     IS WS-K893-STATUS.                            
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS WS-K820-STATUS.                            
                                                                                
           SELECT BP13K895 ASSIGN TO BP13K895                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K895-KEY-FLD                               
                  FILE STATUS     IS WS-K895-STATUS.                            
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K830-KEY-FLD                               
                  ALTERNATE KEY IS K830-NUM-REGN                                
                  FILE STATUS     IS WS-K830-STATUS.                            
                                                                                
           SELECT BP13K857 ASSIGN TO BP13K857                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K857-KEY-FLD                               
                  FILE STATUS     IS WS-K857-STATUS.                            
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS WS-K813-STATUS.                            
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K060-KEY-FLD                               
                  FILE STATUS     IS WS-K060-STATUS.                            
                                                                                
           SELECT BP13FOUT ASSIGN TO BP13FOUT.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BM06F120                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS 700 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F120.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K895                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K895.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD  BP13FOUT                                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING  MODE  IS F.                                               
       01  BP13FOUT-REC                  PIC X(800).                            
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
                                                                                
       01  WS-COUNTER.                                                          
           05  WS-F120-CNT               PIC 9(07)  VALUE ZEROES.               
           05  WS-K110-NTFND-CTR         PIC 9(07)  VALUE ZEROES.               
           05  WS-FOUT-CNT               PIC 9(07)  VALUE ZEROES.               
           05  WS-K110-SKIP-REC          PIC 9(07)  VALUE ZEROES.               
           05  WS-IDX                    PIC 9(02)  VALUE ZEROES.               
           05  WS-CTR                    PIC 9(02)  VALUE ZEROES.               
           05  IDX                       PIC 9(02)  VALUE ZEROES.               
           05  IDX2                      PIC 9(02)  VALUE ZEROES.               
           05  WS-I                      PIC 9(02)  VALUE ZEROES.               
                                                                                
       01  WS-OTHER-VAR.                                                        
           05  WS-NUM-UNIT-NO            PIC X(07)  VALUE SPACES.               
           05  WS-TMP-REGN               PIC X(02)  VALUE SPACES.               
           05  WS-OK                     PIC X(01)  VALUE SPACES.               
           05  WS-F120-END               PIC X(01)  VALUE SPACES.               
           05  WS-K800-FND               PIC X(01)  VALUE SPACES.               
           05  WS-K893-FND               PIC X(01)  VALUE SPACES.               
           05  WS-K895-FND               PIC X(01)  VALUE SPACES.               
           05  WS-K820-EOF               PIC X(01)  VALUE SPACES.               
           05  WS-K895-EOF               PIC X(01)  VALUE SPACES.               
           05  WS-K893-EOF               PIC X(01)  VALUE SPACES.               
           05  WS-TMP-AGE                PIC 9(02)  VALUE ZEROES.               
           05  WS-K110-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K800-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K893-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K820-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K895-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K830-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K857-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K813-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K060-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-CURRENT-DATE           PIC X(08)  VALUE SPACES.               
           05  WS-CURR-DATE REDEFINES WS-CURRENT-DATE.                          
               15 WS-CURR-CCYY           PIC 9(04).                             
               15 WS-CURR-MM             PIC 9(02).                             
               15 WS-CURR-DD             PIC 9(02).                             
           05  WS-DATE-OF-BIRTH          PIC X(08)  VALUE SPACES.               
           05  WS-BIRTH-DATE REDEFINES WS-DATE-OF-BIRTH.                        
               15 WS-DOB-CCYY            PIC 9(04).                             
               15 WS-DOB-MM              PIC 9(02).                             
               15 WS-DOB-DD              PIC 9(02).                             
           05  WS-SAVE-NRIC     OCCURS 4 TIMES.                                 
               15 WS-NRIC                PIC X(09).                             
           05  WS-UNIT-NO.                                                      
             10 WS-MAIN-UNIT    OCCURS 4 TIMES.                                 
               15 WS-MAIN-UNIT-NO        PIC X(01).                             
           05  WS-UNITREF.                                                      
             10 WS-MAIN-UNITREF OCCURS 4 TIMES.                                 
               15 WS-MAIN-UNIT-REF       PIC X(1).                              
                                                                                
       01 WS-PARSING.                                                           
           05  WS-STRING-TO-PARSE            PIC X(77).                         
           05  WS-PARSED-STRING              PIC X(77).                         
           05  WS-STRING-LENGTH              PIC 9(03).                         
           05  WS-CUR-POS-SP                 PIC 9(03).                         
           05  WS-CUR-POS-PS                 PIC 9(03).                         
           05  WS-SPACE-FLAG                 PIC X  VALUE 'N'.                  
           05  WS-INSERT-SPACE-FLAG          PIC X  VALUE 'N'.                  
                                                                                
       01 WS-ADDR-LINES.                                                        
          05 WS-STREET-NAME                  PIC X(65)  VALUE SPACES.           
          05 WS-BLK-NO                       PIC X(10)  VALUE SPACES.           
          05 LR48-BLK-NO                     PIC X(77)  VALUE SPACES.           
          05 LR48-NUM-FLAT.                                                     
             10 LR48-CHAR                    PIC X      VALUE SPACE.            
             10 LR48-LEVEL                   PIC X(2)   VALUE SPACES.           
             10 LR48-DASH                    PIC X      VALUE SPACE.            
             10 LR48-HSE-MAIN                PIC X(4)   VALUE SPACES.           
             10 LR48-HSE-SUB                 PIC X(3)   VALUE SPACES.           
          05 LR48-CODE                       PIC X(16)  VALUE SPACES.           
                                                                                
       01 BP13FOUT-HEADER.                                                      
           05  WS-HRD-HDBREF             PIC X(11) VALUE                        
                                                  'HDB REF    '.                
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-CONTRACT-HDR           PIC X(11) VALUE                        
                                                  'CONTRACT   '.                
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-PROJECT-NAME-HDR       PIC X(60) VALUE                        
               'PROJECT NAME                                        '.          
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-REGN-HDR           PIC X(10) VALUE                        
                                                  'Sales Regn'.                 
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-SALES-MODE-HDR     PIC X(04) VALUE                        
                                                  'MODE'.                       
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-HH-ETHNIC-HDR          PIC X(06) VALUE                        
                                                  'HH Cat'.                     
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-HH-INCOME-HDR          PIC x(09) VALUE                        
                                                  'HH INCOME'.                  
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-FLAT-TYPE-HDR      PIC X(09) VALUE                        
                                                  'FLAT TYPE'.                  
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-BLOCK-HDR          pic x(05) VALUE                        
                                                  'BLOCK'.                      
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-UNIT-HDR           PIC X(08) VALUE                        
                                                  'UNIT   '.                    
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-UNIT-REF-HDR       PIC X(07) VALUE                        
                                                  'UNITREF'.                    
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-UIN-HDR            PIC X(09) VALUE                        
                                                  'NRIC1    '.                  
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NAME-APPOCC-HDR        PIC X(66) VALUE                        
                                                  'NAME1          '.            
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-SEX-HDR            PIC X(07) VALUE                        
                                                  'GENDER1'.                    
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-DOB-HDR            PIC X(08) VALUE                        
                                                  'DOB1    '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-AGE-HDR            PIC X(03) VALUE                        
                                                  'Age'.                        
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-HP1-HDR            PIC X(08) VALUE                        
                                                  'HP1     '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-TEL1-HDR           PIC X(08) VALUE                        
                                                  'TEL1    '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-NRIC2-HDR          PIC X(09) VALUE                        
                                                  'NRIC2    '.                  
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NAME2-HDR              PIC X(66) VALUE                        
                                                  'NAME2          '.            
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-SEX2-HDR           PIC X(07) VALUE                        
                                                  'GENDER2'.                    
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-DOB2-HDR           PIC X(08) VALUE                        
                                                  'DOB2    '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-AGE2-HDR           PIC X(03) VALUE                        
                                                  'Age'.                        
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-HP2-HDR            PIC X(08) VALUE                        
                                                  'HP2     '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-TEL2-HDR           PIC X(08) VALUE                        
                                                  'TEL2    '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-NRIC3-HDR          PIC X(09) VALUE                        
                                                  'NRIC3    '.                  
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NAME3-HDR              PIC X(66) VALUE                        
                                                  'NAME3          '.            
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-SEX3-HDR           PIC X(07) VALUE                        
                                                  'GENDER3'.                    
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-DOB3-HDR           PIC X(08) VALUE                        
                                                  'DOB3    '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-AGE3-HDR           PIC X(03) VALUE                        
                                                  'Age'.                        
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-HP3-HDR            PIC X(08) VALUE                        
                                                  'HP3     '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-TEL3-HDR           PIC X(08) VALUE                        
                                                  'TEL3    '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-NRIC4-HDR          PIC X(09) VALUE                        
                                                  'NRIC4    '.                  
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NAME4-HDR              PIC X(66) VALUE                        
                                                  'NAME4          '.            
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-SEX4-HDR           PIC X(07) VALUE                        
                                                  'GENDER4'.                    
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-DOB4-HDR           PIC X(08) VALUE                        
                                                  'DOB4    '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-AGE4-HDR           PIC X(03) VALUE                        
                                                  'Age'.                        
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-HP4-HDR            PIC X(08) VALUE                        
                                                  'HP4     '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-TEL4-HDR           PIC X(08) VALUE                        
                                                  'TEL4    '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-EMAIL-HDR              PIC X(50) VALUE                        
                                                  'EMAIL       '.               
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-ADDR-LINE1-HDR         PIC X(60) VALUE                        
                                                  'ADDR-LINE1  '.               
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-ADDR-LINE2-HDR         PIC X(10) VALUE                        
                                                  'ADDR-LINE2'.                 
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-ADDR-LINE3-HDR         PIC X(16) VALUE                        
                                                  'ADDR-LINE3'.                 
           05  FILLER                    PIC X(46).                             
                                                                                
       01 BP13FOUT-RECORD.                                                      
           05  WS-HDB-REF                PIC X(11).                             
           05  WS-FILLER01               PIC X(01).                             
           05  WS-CONTRACT.                                                     
               10 WS-ESTATE              PIC X(03).                             
               10 WS-NEIGHBOURHOOD       PIC X(04).                             
               10 WS-CONTRACT-NO         PIC X(04).                             
           05  WS-FILLER02               PIC X(01).                             
           05  WS-PROJECT-NAME           PIC X(60).                             
           05  WS-FILLER03               PIC X(01).                             
           05  WS-NUM-REGN               PIC X(10).                             
           05  WS-FILLER04               PIC X(01).                             
           05  WS-NUM-SALES-MODE         PIC X(04).                             
           05  WS-FILLER05               PIC X(01).                             
           05  WS-HH-ETHNIC              PIC X(06).                             
           05  WS-FILLER06               PIC X(01).                             
           05  WS-HH-INCOME              PIC x(09).                             
           05  WS-FILLER07               PIC X(01).                             
           05  WS-NUM-FLAT-TYPE          PIC X(09).                             
           05  WS-FILLER08               PIC X(01).                             
           05  WS-NUM-BLOCK              pic x(05).                             
           05  WS-FILLER09               PIC X(01).                             
           05  WS-NUM-UNIT               PIC X(08).                             
           05  WS-FILLER10               PIC X(01).                             
           05  WS-NUM-UNIT-REF           PIC X(07).                             
           05  WS-FILLER11               PIC X(01).                             
           05  WS-APPL-OTHER-INFO OCCURS 4 TIMES.                               
               10  WS-NUM-NRIC           PIC X(09).                             
               10  WS-FILLER12           PIC X(01).                             
               10  WS-NAME-APPl          PIC X(66).                             
               10  WS-FILLER13           PIC X(01).                             
               10  WS-NUM-GENDER         PIC X(07).                             
               10  WS-FILLER14           PIC X(01).                             
               10  WS-NUM-DOB            PIC X(08).                             
               10  WS-FILLER15           PIC X(01).                             
               10  WS-NUM-AGE            PIC X(03).                             
               10  WS-FILLER16           PIC X(01).                             
               10  WS-NUM-HP             PIC X(08).                             
               10  WS-FILLER17           PIC X(01).                             
               10  WS-NUM-TEL            PIC X(08).                             
               10  WS-FILLER18           PIC X(01).                             
           05  WS-EMAIL-ADDR             PIC X(50).                             
           05  WS-FILLER19               PIC X(01).                             
           05  WS-ADDR-LINE1             PIC X(60).                             
           05  WS-FILLER20               PIC X(01).                             
           05  WS-ADDR-LINE2             PIC X(10).                             
           05  WS-FILLER21               PIC X(01).                             
           05  WS-ADDR-LINE3             PIC X(16).                             
           05  FILLER                    PIC X(46).                             
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
           PERFORM 0100-INIT-ROUTINE        THRU 0100-EXIT.                     
           PERFORM 0200-READ-BM06F120       THRU 0200-EXIT.                     
           PERFORM 0300-MAIN-PROCESS        THRU 0300-EXIT                      
                   UNTIL WS-F120-END = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES.                                            
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       0100-INIT-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BM06F120                                                  
                      BM06K110                                                  
                      BP13K800                                                  
                      BP13K893                                                  
                      BP13K820                                                  
                      BP13K895                                                  
                      BP13K830                                                  
                      BP13K857                                                  
                      BP13K813                                                  
                      BP13K060                                                  
               OUTPUT BP13FOUT.                                                 
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BM06K110-STATUS = '                       
                       WS-K110-STATUS                                           
              MOVE     WS-K110-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K800 STATUS = '                       
                       WS-K800-STATUS                                           
              MOVE     WS-K800-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K893 STATUS = '                       
                       WS-K893-STATUS                                           
              MOVE     WS-K893-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K820 STATUS = '                       
                       WS-K820-STATUS                                           
              MOVE     WS-K820-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K895 STATUS = '                       
                       WS-K895-STATUS                                           
              MOVE     WS-K895-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K830 STATUS = '                       
                       WS-K830-STATUS                                           
              MOVE     WS-K830-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K857 STATUS = '                       
                       WS-K857-STATUS                                           
              MOVE     WS-K857-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K813 STATUS = '                       
                       WS-K813-STATUS                                           
              MOVE     WS-K813-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)    TO WS-CURRENT-DATE.               
      *****move '20161231'                    TO WS-CURRENT-DATE.               
           WRITE BP13FOUT-REC  FROM BP13FOUT-HEADER.                            
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0200-READ-BM06F120.                                                      
      *-------------------------------------------------------------            
           READ BM06F120                                                        
             AT END                                                             
                MOVE 'Y'             TO WS-F120-END                             
                GO TO 0200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1         TO WS-F120-CNT.                                        
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       0300-MAIN-PROCESS.                                                       
      *------------------------------------------------------------*            
           MOVE SPACES                        TO BP13FOUT-RECORD                
                                                 WS-UNITREF                     
                                                 WS-UNIT-NO.                    
           INITIALIZE BP13FOUT-RECORD.                                          
                                                                                
           MOVE F120-BLK-NO                   TO WS-NUM-BLOCK.                  
           STRING '#' F120-LEVEL-NO '-' F120-MAIN-UNIT                          
                  DELIMITED BY SIZE   INTO WS-NUM-UNIT                          
           END-STRING.                                                          
                                                                                
           MOVE ZEROES                  TO IDX2.                                
           MOVE F120-MAIN-UNIT          TO WS-UNIT-NO.                          
           PERFORM VARYING IDX FROM 1 BY 1 UNTIL IDX > 4                        
               IF WS-MAIN-UNIT-NO(IDX) NOT = SPACES AND LOW-VALUES              
                  ADD 1                     TO IDX2                             
                  MOVE WS-MAIN-UNIT-NO(IDX) TO WS-MAIN-UNIT-REF(IDX2)           
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           STRING F120-LEVEL-NO WS-UNITREF                                      
                  DELIMITED BY SPACES INTO WS-NUM-UNIT-REF                      
           END-STRING.                                                          
                                                                                
                                                                                
           MOVE 'N'                           TO WS-OK                          
                                                 WS-K800-FND                    
                                                 WS-K895-FND                    
                                                 WS-K893-FND.                   
           MOVE SPACES                        TO K110-KEY-FLD.                  
           MOVE F120-SCH-ACC-NO               TO K110-SCH-ACC-NO.               
           PERFORM 0400-READ-BM06K110       THRU 0400-EXIT                      
           IF WS-OK = 'Y'                                                       
              MOVE K110-REGN-NO               TO K800-NUM-REGN                  
              PERFORM 0605-READ-BP13K800    THRU 0605-EXIT                      
              IF WS-K800-FND = 'Y' OR WS-K893-FND = 'Y'                         
                 PERFORM 0500-READ-BP13K813 THRU 0500-EXIT                      
              END-IF                                                            
                                                                                
              PERFORM 0600-READ-OTHER-FILES THRU 0600-EXIT                      
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 0200-READ-BM06F120       THRU 0200-EXIT.                     
                                                                                
       0300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0400-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           READ BM06K110.                                                       
                                                                                
           IF WS-K110-STATUS = ZEROES                                           
              IF (K110-REGN-NO NOT = SPACES AND LOW-VALUES) AND                 
                 (K110-REGN-NO(1:2) NOT = '##')                                 
                  MOVE 'Y'                    TO WS-OK                          
                  PERFORM 0405-MOVE-K110-DATA THRU 0405-EXIT                    
              ELSE                                                              
                 ADD 1                        TO WS-K110-SKIP-REC               
              END-IF                                                            
           ELSE                                                                 
             IF WS-K110-STATUS = 23                                             
                ADD  1                        TO WS-K110-NTFND-CTR              
                MOVE SPACES                   TO K110-REC                       
             ELSE                                                               
                DISPLAY 'BM06K110 - READ FILE ERROR'                            
                DISPLAY 'KEY: ' K110-KEY-FLD                                    
                DISPLAY 'ERROR : ' WS-K110-STATUS                               
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
             END-IF                                                             
           END-IF.                                                              
       0400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0405-MOVE-K110-DATA.                                                     
      *-------------------------------------------------------------            
           MOVE K110-SCH-ACC-NO               TO WS-HDB-REF.                    
           MOVE K110-ESTATE                   TO WS-ESTATE.                     
           MOVE K110-NEIGHBOURHOOD            TO WS-NEIGHBOURHOOD.              
           MOVE K110-CONTRACT-NO              TO WS-CONTRACT-NO.                
           MOVE K110-REGN-NO                  TO WS-NUM-REGN.                   
       0405-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0500-READ-BP13K813.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                         TO BP13K813-REC.                 
           IF WS-K800-FND = 'Y'                                                 
              IF K800-NUM-ALLO-CAT = 'SBF' OR 'ROF'                             
                 MOVE K800-NUM-NEW-TOWN        TO K813-NUM-ZONE                 
                 MOVE K800-NUM-BTO-FLAT-TYPE   TO K813-NUM-FLAT-TYPE            
                 IF K800-NUM-ALLO-CAT = 'ROF'                                   
                    MOVE 'ROF'                 TO K813-NUM-ZONE                 
                    MOVE 'RF'                  TO K813-NUM-FLAT-TYPE            
                 END-IF                                                         
              ELSE                                                              
                 IF K800-NUM-ALLO-CAT = 'BTO'                                   
                    IF K800-NUM-BTO-ZONE NOT = SPACES AND LOW-VALUES            
                       MOVE K800-NUM-BTO-ZONE  TO K813-NUM-ZONE                 
                    ELSE                                                        
                       MOVE K800-NUM-ZONE      TO K813-NUM-ZONE                 
                    END-IF                                                      
                 END-IF                                                         
                 MOVE  K800-NUM-FLAT-TYPE      TO K813-NUM-FLAT-TYPE            
              END-IF                                                            
           MOVE K800-DTE-BALLOT                TO K813-DTE-BALLOT               
           END-IF.                                                              
                                                                                
           IF WS-K893-FND = 'Y'                                                 
              IF K893-NUM-ALLO-CAT = 'SBF' OR 'ROF'                             
                 MOVE K893-NUM-NEW-TOWN        TO K813-NUM-ZONE                 
                 MOVE K893-NUM-BTO-FLAT-TYPE   TO K813-NUM-FLAT-TYPE            
                 IF K893-NUM-ALLO-CAT = 'ROF'                                   
                    MOVE 'ROF'                 TO K813-NUM-ZONE                 
                    MOVE 'RF'                  TO K813-NUM-FLAT-TYPE            
                 END-IF                                                         
              ELSE                                                              
                 IF K893-NUM-ALLO-CAT = 'BTO'                                   
                    IF K893-NUM-BTO-ZONE NOT = SPACES AND LOW-VALUES            
                       MOVE K893-NUM-BTO-ZONE  TO K813-NUM-ZONE                 
                    ELSE                                                        
                       MOVE K893-NUM-ZONE      TO K813-NUM-ZONE                 
                    END-IF                                                      
                 END-IF                                                         
                 MOVE  K893-NUM-FLAT-TYPE      TO K813-NUM-FLAT-TYPE            
              END-IF                                                            
           MOVE K893-DTE-BALLOT                TO K813-DTE-BALLOT               
           END-IF.                                                              
                                                                                
           READ BP13K813.                                                       
                                                                                
           IF WS-K813-STATUS = ZEROES                                           
              MOVE K813-NME-PROJECT-LTR        TO WS-PROJECT-NAME               
           ELSE                                                                 
             IF WS-K813-STATUS = 23                                             
                MOVE SPACES                    TO WS-PROJECT-NAME               
             ELSE                                                               
                DISPLAY 'BP13K813 - READ FILE ERROR'                            
                DISPLAY 'KEY: ' K813-KEY-FLD                                    
                DISPLAY 'ERROR : ' WS-K813-STATUS                               
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
             END-IF                                                             
           END-IF.                                                              
       0500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0600-READ-OTHER-FILES.                                                   
      *-------------------------------------------------------------            
           IF WS-K800-FND = 'Y'                                                 
              MOVE K800-NUM-NRIC1             TO WS-NRIC(1)                     
              MOVE K800-NUM-NRIC2             TO WS-NRIC(2)                     
              MOVE K800-NUM-NRIC3             TO WS-NRIC(3)                     
              MOVE K800-NUM-NRIC4             TO WS-NRIC(4)                     
           ELSE                                                                 
              MOVE K893-NUM-NRIC1             TO WS-NRIC(1)                     
              MOVE K893-NUM-NRIC2             TO WS-NRIC(2)                     
              MOVE K893-NUM-NRIC3             TO WS-NRIC(3)                     
              MOVE K893-NUM-NRIC4             TO WS-NRIC(4)                     
           END-IF                                                               
           PERFORM VARYING WS-IDX FROM 1 BY 1                                   
              UNTIL WS-IDX > 4                                                  
                 OR (WS-NRIC(WS-IDX) = SPACES OR LOW-VALUES)                    
                 MOVE SPACES                  TO K820-KEY-FLD                   
                 MOVE SPACES                  TO K895-KEY-FLD                   
                 IF WS-K800-FND = 'Y'                                           
                    MOVE K800-NUM-REGN        TO K820-NUM-REGN                  
                    MOVE K800-NUM-REGN        TO K895-NUM-REGN                  
                                                 K895-NUM-REGN-HIST             
                 ELSE                                                           
                    MOVE K893-NUM-REGN        TO K820-NUM-REGN                  
                    MOVE K893-NUM-REGN        TO K895-NUM-REGN                  
                                                 K895-NUM-REGN-HIST             
                 END-IF                                                         
                 MOVE WS-NRIC(WS-IDX)         TO K820-NUM-NRIC                  
                 MOVE WS-NRIC(WS-IDX)         TO K895-NUM-NRIC                  
                 PERFORM 0610-READ-BP13K820 THRU 0610-EXIT                      
           END-PERFORM.                                                         
                                                                                
           MOVE SPACES                        TO K060-KEY-FLD                   
           MOVE 09                            TO K060-SERIAL-NO                 
           IF WS-K800-FND = 'Y'                                                 
              MOVE K800-NUM-CAT               TO K060-CODE                      
              MOVE K800-AMT-HSE-INCOME        TO WS-HH-INCOME                   
           ELSE                                                                 
              MOVE K893-NUM-CAT               TO K060-CODE                      
              MOVE K893-AMT-HSE-INCOME        TO WS-HH-INCOME                   
           END-IF                                                               
           PERFORM 0625-READ-BP13K060       THRU 0625-EXIT                      
                                                                                
           MOVE K060-DESC1(1:1)               TO WS-HH-ETHNIC                   
      *    MOVE K800-AMT-HSE-INCOME           TO WS-HH-INCOME                   
                                                                                
           MOVE K110-REGN-NO                  TO K857-NUM-REGN.                 
           PERFORM 0630-GET-EMAIL           THRU 0630-EXIT.                     
                                                                                
           MOVE SPACES                        TO K830-KEY-FLD.                  
           MOVE K110-REGN-NO                  TO K830-NUM-REGN.                 
           PERFORM 0640-GET-MAIL-ADDRESS    THRU 0640-EXIT.                     
                                                                                
           MOVE ';'               TO WS-FILLER01 WS-FILLER02                    
                                     WS-FILLER03 WS-FILLER04                    
                                     WS-FILLER05 WS-FILLER06                    
                                     WS-FILLER07 WS-FILLER08                    
                                     WS-FILLER09 WS-FILLER10                    
                                     WS-FILLER11.                               
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1                                   
             UNTIL WS-CTR > 4                                                   
             MOVE ';'             TO WS-FILLER12(WS-CTR)                        
                                     WS-FILLER13(WS-CTR)                        
                                     WS-FILLER14(WS-CTR)                        
                                     WS-FILLER15(WS-CTR)                        
                                     WS-FILLER16(WS-CTR)                        
                                     WS-FILLER17(WS-CTR)                        
                                     WS-FILLER18(WS-CTR)                        
           END-PERFORM.                                                         
                                                                                
           MOVE ';'               TO WS-FILLER19 WS-FILLER20                    
                                     WS-FILLER21.                               
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1                                     
              UNTIL WS-I > 4 OR WS-NUM-NRIC(WS-I) = SPACES                      
               MOVE 'XXXX'        TO WS-NUM-NRIC(WS-I)(2:4)                     
           END-PERFORM                                                          
                                                                                
           WRITE BP13FOUT-REC  FROM BP13FOUT-RECORD.                            
           ADD 1   TO WS-FOUT-CNT.                                              
                                                                                
                                                                                
       0600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0605-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           READ BP13K800.                                                       
           IF WS-K800-STATUS = ZEROES                                           
              MOVE K800-NUM-ALLO-CAT          TO WS-NUM-SALES-MODE              
              MOVE K800-NUM-FLAT-TYPE         TO WS-NUM-FLAT-TYPE               
                                                                                
              IF (K893-NUM-ROOM = '5') AND                                      
                 (F120-CDE-MODL-DESG = '90' OR '91' OR '95')                    
                  MOVE '3Gen'                 TO WS-NUM-FLAT-TYPE               
              END-IF                                                            
                                                                                
              MOVE K800-NUM-NRIC1             TO WS-NUM-NRIC(1)                 
              MOVE K800-NUM-NRIC2             TO WS-NUM-NRIC(2)                 
              MOVE K800-NUM-NRIC3             TO WS-NUM-NRIC(3)                 
              MOVE K800-NUM-NRIC4             TO WS-NUM-NRIC(4)                 
              MOVE 'Y'                        TO WS-K800-FND                    
           ELSE                                                                 
             IF WS-K800-STATUS = 23                                             
                MOVE 'N'                      TO WS-K800-FND                    
                MOVE SPACES                   TO BP13K800-MASTER                
                MOVE K110-REGN-NO             TO K893-NUM-REGN                  
                                                 K893-NUM-REGN-HIST             
                PERFORM 0607-READ-BP13K893 THRU 0607-EXIT                       
             ELSE                                                               
                DISPLAY 'BP13K800 - READ FILE ERROR'                            
                DISPLAY 'KEY: ' K800-NUM-REGN                                   
                DISPLAY 'ERROR : ' WS-K800-STATUS                               
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       0605-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0607-READ-BP13K893.                                                      
      *-------------------------------------------------------------            
           START BP13K893 KEY >= K893-KEY-FLD                                   
                                                                                
           PERFORM UNTIL K893-NUM-REGN-HIST = K110-REGN-NO                      
                      OR WS-K893-EOF = 'Y'                                      
                                                                                
             READ BP13K893 NEXT                                                 
             AT END MOVE 'Y' TO WS-K893-EOF                                     
             END-READ                                                           
             IF WS-K893-STATUS = ZEROES                                         
                IF K893-NUM-REGN-HIST = K110-REGN-NO                            
                   MOVE K893-NUM-ALLO-CAT     TO WS-NUM-SALES-MODE              
                   MOVE K893-NUM-FLAT-TYPE    TO WS-NUM-FLAT-TYPE               
                                                                                
                   IF (K893-NUM-ROOM = '5') AND                                 
                      (F120-CDE-MODL-DESG = '90' OR '91' OR '95')               
                       MOVE '3Gen'            TO WS-NUM-FLAT-TYPE               
                   END-IF                                                       
                                                                                
                   MOVE K893-NUM-NRIC1        TO WS-NUM-NRIC(1)                 
                   MOVE K893-NUM-NRIC2        TO WS-NUM-NRIC(2)                 
                   MOVE K893-NUM-NRIC3        TO WS-NUM-NRIC(3)                 
                   MOVE K893-NUM-NRIC4        TO WS-NUM-NRIC(4)                 
                   MOVE 'Y'                   TO WS-K893-FND                    
                END-IF                                                          
             ELSE                                                               
                IF WS-K893-STATUS = 23                                          
                   MOVE 'N'                   TO WS-K893-FND                    
                   MOVE SPACES                TO BP13K893-MASTER                
                ELSE                                                            
                   DISPLAY 'BP13K893 - READ FILE ERROR'                         
                   DISPLAY 'KEY: ' K893-NUM-REGN                                
                   DISPLAY 'ERROR : ' WS-K893-STATUS                            
                   PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                   
                END-IF                                                          
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
       0607-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0610-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
           READ BP13K820                                                        
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
                    PERFORM 0620-MOVE-K820-DATA THRU 0620-EXIT                  
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES               TO BP13K820-REC                   
                    START BP13K895 KEY >= K895-KEY-FLD                          
                    MOVE 'N'                  TO WS-K895-FND                    
                    PERFORM 0615-READ-BP13K895 THRU 0615-EXIT UNTIL             
                            WS-K895-FND = 'Y' OR WS-K895-EOF = 'Y'              
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 - START READ ERROR'                       
                    DISPLAY 'KEY: ' K820-NUM-REGN ' / ' K820-NUM-NRIC           
                    DISPLAY 'ERROR : ' WS-K820-STATUS                           
                    PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       0610-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0615-READ-BP13K895.                                                      
      *-------------------------------------------------------------            
           READ BP13K895 NEXT                                                   
           EVALUATE WS-K895-STATUS                                              
               WHEN 00                                                          
                    IF WS-NRIC(WS-IDX) = K895-NUM-NRIC AND                      
                       ((K895-NUM-REGN = K893-NUM-REGN) OR                      
                        (K895-NUM-REGN = K800-NUM-REGN))                        
                       PERFORM 0623-MOVE-K895-DATA THRU 0623-EXIT               
                       MOVE 'Y'  TO  WS-K895-FND                                
                    END-IF                                                      
               WHEN 10                                                          
                    MOVE 'Y'  TO WS-K895-EOF                                    
               WHEN 23                                                          
                    MOVE SPACES               TO BP13K895-REC                   
               WHEN OTHER                                                       
                    DISPLAY 'BP13K895 - START READ ERROR'                       
                    DISPLAY 'KEY: ' K895-NUM-REGN ' / ' K895-NUM-NRIC           
                    DISPLAY 'ERROR : ' WS-K895-STATUS                           
                    PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       0615-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0620-MOVE-K820-DATA.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE K820-NME-OCCP                 TO WS-NAME-APPl(WS-IDX).          
                                                                                
           MOVE SPACES                        TO K060-KEY-FLD.                  
           MOVE 43                            TO K060-SERIAL-NO.                
           MOVE K820-NUM-SEX                  TO K060-CODE.                     
           PERFORM 0625-READ-BP13K060       THRU 0625-EXIT.                     
                                                                                
           MOVE K060-DESC1(1:1)               TO WS-NUM-GENDER(WS-IDX)          
           MOVE K820-DTE-BIRTH                TO WS-NUM-DOB(WS-IDX)             
                                                 WS-DATE-OF-BIRTH.              
           COMPUTE WS-TMP-AGE = WS-CURR-CCYY - WS-DOB-CCYY.                     
           IF WS-DOB-MM = WS-CURR-MM                                            
              IF WS-DOB-DD > WS-CURR-DD                                         
                 COMPUTE WS-TMP-AGE = WS-TMP-AGE - 1                            
              END-IF                                                            
           ELSE                                                                 
             IF WS-DOB-MM > WS-CURR-MM                                          
                COMPUTE WS-TMP-AGE = WS-TMP-AGE - 1                             
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE WS-TMP-AGE                    TO WS-NUM-AGE(WS-IDX).            
           MOVE K820-NUM-HP-PGR               TO WS-NUM-HP(WS-IDX).             
           MOVE K820-NUM-PHONE                TO WS-NUM-TEL(WS-IDX).            
                                                                                
       0620-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0623-MOVE-K895-DATA.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE K895-NME-OCCP                 TO WS-NAME-APPl(WS-IDX).          
                                                                                
           MOVE SPACES                        TO K060-KEY-FLD.                  
           MOVE 43                            TO K060-SERIAL-NO.                
           MOVE K895-NUM-SEX                  TO K060-CODE.                     
           PERFORM 0625-READ-BP13K060       THRU 0625-EXIT.                     
                                                                                
           MOVE K060-DESC1(1:1)               TO WS-NUM-GENDER(WS-IDX)          
           MOVE K895-DTE-BIRTH                TO WS-NUM-DOB(WS-IDX)             
                                                 WS-DATE-OF-BIRTH.              
           COMPUTE WS-TMP-AGE = WS-CURR-CCYY - WS-DOB-CCYY.                     
           IF WS-DOB-MM = WS-CURR-MM                                            
              IF WS-DOB-DD > WS-CURR-DD                                         
                 COMPUTE WS-TMP-AGE = WS-TMP-AGE - 1                            
              END-IF                                                            
           ELSE                                                                 
             IF WS-DOB-MM > WS-CURR-MM                                          
                COMPUTE WS-TMP-AGE = WS-TMP-AGE - 1                             
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE WS-TMP-AGE                    TO WS-NUM-AGE(WS-IDX).            
           MOVE K895-NUM-HP-PGR               TO WS-NUM-HP(WS-IDX).             
           MOVE K895-NUM-PHONE                TO WS-NUM-TEL(WS-IDX).            
                                                                                
       0623-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0625-READ-BP13K060.                                                      
      *-------------------------------------------------------------            
           READ BP13K060.                                                       
                                                                                
           IF WS-K060-STATUS = ZEROES                                           
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K060-STATUS = 23                                            
                 MOVE SPACES                  TO K060-DESC1                     
      *          DISPLAY 'RECORD NOT FOUND IN BP13K060, KEY: '                  
      *                  K060-KEY-FLD ' ' K820-KEY-FLD                          
              ELSE                                                              
                 DISPLAY 'BP13K060 - READ FILE ERROR'                           
                 DISPLAY 'KEY: ' K820-NUM-REGN ' / ' K820-NUM-NRIC              
                 DISPLAY 'ERROR : ' WS-K060-STATUS                              
                 PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0625-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0630-GET-EMAIL.                                                          
      *-------------------------------------------------------------            
           READ BP13K857.                                                       
                                                                                
           IF WS-K857-STATUS = ZEROES                                           
              MOVE K857-NUM-EMAIL             TO WS-EMAIL-ADDR                  
           ELSE                                                                 
              IF WS-K857-STATUS = 23                                            
                 MOVE SPACES                  TO WS-EMAIL-ADDR                  
              ELSE                                                              
                 DISPLAY 'BP13K857 - READ FILE ERROR'                           
                 DISPLAY 'KEY: ' K857-NUM-REGN                                  
                 DISPLAY 'ERROR : ' WS-K857-STATUS                              
                 PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
       0630-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0640-GET-MAIL-ADDRESS.                                                   
      *-------------------------------------------------------------            
           READ BP13K830 KEY IS  K830-NUM-REGN                                  
                                                                                
           IF WS-K830-STATUS = ZEROES                                           
              MOVE SPACES TO WS-ADDR-LINES                                      
              PERFORM 0650-MOVE-DATA     THRU 0650-EXIT                         
                                                                                
           ELSE                                                                 
              IF WS-K830-STATUS = 23                                            
                 MOVE SPACES      TO WS-ADDR-LINE1                              
                                     WS-ADDR-LINE2                              
                                     WS-ADDR-LINE3                              
              ELSE                                                              
                 DISPLAY 'BP13K830 - READ FILE ERROR'                           
                 DISPLAY 'KEY: ' K830-NUM-REF                                   
                 DISPLAY 'ERROR : ' WS-K830-STATUS                              
                 PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
       0640-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0650-MOVE-DATA.                                                          
      *-------------------------------------------------------------            
           IF (K830-NUM-MAIL-ADDR  = SPACES OR LOW-VALUES) OR                   
              ((K830-NUM-MAIL-LVL-CHAR = '#') AND                               
              (K830-NUM-MAIL-LVL-NUM = SPACES OR LOW-VALUES))                   
              IF K830-NUM-ADDR-TYPE  = 'H'                                      
                 MOVE K830-NUM-BLK       TO WS-BLK-NO                           
                 MOVE K830-NME-STREET    TO WS-STREET-NAME                      
                 MOVE K830-NUM-LVL-CHAR  TO LR48-CHAR                           
                 MOVE K830-NUM-LVL-NUM   TO LR48-LEVEL                          
                 MOVE K830-NUM-LVL-DASH  TO LR48-DASH                           
                 MOVE K830-NUM-HSEMAIN   TO LR48-HSE-MAIN                       
                 MOVE K830-NUM-HSESUB    TO LR48-HSE-SUB                        
                 PERFORM 4110-GET-STREET-NAME  THRU 4110-EXIT                   
                 STRING 'SINGAPORE ' K830-NUM-POSTAL                            
                         DELIMITED BY SIZE INTO LR48-CODE                       
              ELSE                                                              
                 MOVE K830-NUM-BLK         TO WS-BLK-NO                         
                 MOVE K830-NUM-FLAT        TO LR48-NUM-FLAT                     
                 MOVE K830-NME-STREET-BLDG TO WS-STREET-NAME                    
                 PERFORM 4110-GET-STREET-NAME  THRU 4110-EXIT                   
                 IF K830-NUM-POSTAL = SPACES OR LOW-VALUES OR                   
                                      ZEROES                                    
                    MOVE SPACES TO LR48-CODE                                    
                 ELSE                                                           
                    STRING 'SINGAPORE ' K830-NUM-POSTAL                         
                            DELIMITED BY SIZE INTO LR48-CODE                    
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF K830-NUM-MAIL-ADDR-TYPE =  'H'                                 
                 MOVE K830-NUM-MAIL-BLK      TO WS-BLK-NO                       
                 MOVE K830-NME-MAIL-STREET   TO WS-STREET-NAME                  
                 MOVE K830-NUM-MAIL-LVL-CHAR TO LR48-CHAR                       
                 MOVE K830-NUM-MAIL-LVL-NUM  TO LR48-LEVEL                      
                 MOVE K830-NUM-MAIL-LVL-DASH TO LR48-DASH                       
                 MOVE K830-NUM-MAIL-HSEMAIN  TO LR48-HSE-MAIN                   
                 MOVE K830-NUM-MAIL-HSESUB   TO LR48-HSE-SUB                    
                 PERFORM 4110-GET-STREET-NAME   THRU 4110-EXIT                  
                 STRING 'SINGAPORE '   K830-NUM-MAIL-POSTAL                     
                         DELIMITED BY SIZE INTO LR48-CODE                       
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK      TO WS-BLK-NO                       
                 MOVE K830-NME-MAIL-STR-BLDG TO WS-STREET-NAME                  
                 MOVE K830-NUM-MAIL-FLAT TO LR48-NUM-FLAT                       
                 PERFORM 4110-GET-STREET-NAME  THRU 4110-EXIT                   
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES                 
                                   OR ZEROES                                    
                    MOVE SPACES TO LR48-CODE                                    
                 ELSE                                                           
                    STRING 'SINGAPORE '   K830-NUM-MAIL-POSTAL                  
                          DELIMITED BY SIZE INTO LR48-CODE                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE LR48-BLK-NO    TO WS-ADDR-LINE1.                                
           MOVE LR48-NUM-FLAT  TO WS-ADDR-LINE2.                                
           MOVE LR48-CODE      TO WS-ADDR-LINE3.                                
                                                                                
       0650-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4110-GET-STREET-NAME.                                                    
      *-------------------------------------------------------------            
           MOVE SPACES       TO WS-STRING-TO-PARSE.                             
           STRING WS-BLK-NO ' ' WS-STREET-NAME                                  
                  DELIMITED BY SIZE INTO WS-STRING-TO-PARSE                     
           END-STRING.                                                          
           MOVE LENGTH OF WS-STRING-TO-PARSE                                    
                             TO WS-STRING-LENGTH                                
           MOVE 'Y'          TO WS-INSERT-SPACE-FLAG                            
           PERFORM 8000-PARSE-STRING    THRU 8000-EXIT                          
           MOVE WS-PARSED-STRING   TO  LR48-BLK-NO.                             
                                                                                
       4110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       8000-PARSE-STRING.                                                       
      *--------------------------------------------------------------           
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 8500-GET-CHAR          THRU 8500-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------          
       8500-GET-CHAR.                                                           
      *---------------------------------------------------------------          
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       8500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '-------------BP13CRG1 CONTROL TOTAL -----------'.           
           DISPLAY 'NO OF RECORDS READ  (BM06F120) : ' WS-F120-CNT.             
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF SKIP RECORDS  (BM06K110) : ' WS-K110-SKIP-REC         
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS NOTFND(BM06K110) : ' WS-K110-NTFND-CTR        
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS WRITTEN (BP13FOUT) : ' WS-FOUT-CNT.           
           DISPLAY ' '.                                                         
           CLOSE BM06F120                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 BP13K893                                                       
                 BP13K820                                                       
                 BP13K895                                                       
                 BP13K830                                                       
                 BP13K857                                                       
                 BP13K813                                                       
                 BP13K060                                                       
                 BP13FOUT.                                                      
           STOP RUN.                                                            
       9000-EXIT.                                                               
                                                                                
