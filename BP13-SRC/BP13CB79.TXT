      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CB79.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  19/10/09.                                                 
      *****************************************************************         
      *OBJECTIVE - MATCH F595-NRIC1 WITH F520-NUM-NRIC1, WRITE RECORD *         
      *            FROM BP13F520-REC.                                 *         
      *                                                               *         
      *     INPUT FILE : 1. BP13F520                                  *         
      *                  2. BP13F595                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. P13F595A                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP133792 19/10/2009 PCL3 NEW PROGRAM.                         *         
      * BP133921 31/05/2010 PCL3 ADD 'NS' TO COULD NOT BOOK CODE      *         
      * BP135039 03/12/2013 IMC1 CATER FOR FTS BOOK-STATUS CODES      *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F595  ASSIGN        TO BP13F595.                         
           SELECT  BP13F520  ASSIGN        TO BP13F520.                         
           SELECT  P13F595A ASSIGN         TO P13F595A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F520                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F520.                                                           
                                                                                
       FD   BP13F595                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F595.                                                           
                                                                                
                                                                                
       FD   P13F595A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   P13F595A-REC.                                                       
            05  FILLER                    PIC X(464).                           
            05  F595A-NUM-BE-CNT          PIC 9(02).                            
            05  F595A-NUM-BTO-CNT         PIC 9(02).                            
            05  F595A-NUM-BK-TAG          PIC X(01).                            
            05  F595A-NUM-BK-CNT          PIC 9(02).                            
            05  F595A-NUM-REST-CNT        PIC 9(02).                            
            05  F595A-NUM-NV-CNT          PIC 9(02).                            
            05  F595A-NUM-NOTCAT          PIC 9(02).                            
            05  FILLER                    PIC X(23).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F595             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-F520             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F595-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-F520-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-F595A-WRITE          PIC 9(07) VALUE ZEROES.                  
           05  WS-F595A-INVITE         PIC 9(07) VALUE ZEROES.                  
           05  WS-F595A-NV             PIC 9(07) VALUE ZEROES.                  
           05  WS-CNT-BTO              PIC 9(02) VALUE ZEROES.                  
           05  WS-CNT-NV               PIC 9(02) VALUE ZEROES.                  
           05  WS-F520-NOMATCH         PIC 9(07) VALUE ZEROES.                  
           05  WS-F595-NOMATCH         PIC 9(07) VALUE ZEROES.                  
           05  WS-MATCH                PIC 9(07) VALUE ZEROES.                  
           05  WS-UPG                  PIC 9(07) VALUE ZEROES.                  
           05  WS-NEAPPT               PIC 9(07) VALUE ZEROES.                  
           05  WS-NEAPPT-NO            PIC 9(07) VALUE ZEROES.                  
           05  WS-NE-NOAPPT            PIC X(01) VALUE SPACES.                  
           05  WS-NE-APPMT             PIC X(01) VALUE SPACES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SYSTEM-DATE          PIC 9(08) VALUE ZEROES.                  
           05  WS-NUM-NRIC             PIC X(09) VALUE SPACES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F520      THRU 2000-EXIT.                      
           PERFORM 2100-READ-BP13F595      THRU 2100-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F520 = 'Y' AND                                        
                   WS-EOF-F595 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F595                                                 
                       BP13F520                                                 
              OUTPUT   P13F595A.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F520.                                                      
      ******************************************************************        
                                                                                
           READ BP13F520           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F520                             
                MOVE HIGH-VALUES   TO   F520-NUM-NRIC1                          
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F520-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2100-READ-BP13F595.                                                      
      ******************************************************************        
                                                                                
           READ BP13F595           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F595                             
                MOVE HIGH-VALUES   TO   F595-NUM-NRIC1                          
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F595-READ.                           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                             TO WS-NE-NOAPPT              
                                                      WS-NE-APPMT.              
                                                                                
           IF F520-NUM-NRIC1 = F595-NUM-NRIC1                                   
               ADD 1                               TO WS-MATCH                  
               MOVE 00                             TO WS-CNT-BTO                
               MOVE 00                             TO WS-CNT-NV                 
                                                                                
               PERFORM 3500-PROCEED-SAME-NRIC    THRU 3500-EXIT                 
                 UNTIL F595-NUM-NRIC1 NOT = F520-NUM-NRIC1                      
                    OR WS-EOF-F595 = 'Y'                                        
                                                                                
               PERFORM 4000-WRITE-BP13F520       THRU 4000-EXIT                 
               PERFORM 2000-READ-BP13F520        THRU 2000-EXIT                 
           ELSE                                                                 
              IF F520-NUM-NRIC1 < F595-NUM-NRIC1                                
                  ADD 1                          TO WS-F520-NOMATCH             
                  MOVE 00                          TO WS-CNT-BTO                
                  PERFORM 5000-WRITE-BP13F520    THRU 5000-EXIT                 
                  PERFORM 2000-READ-BP13F520     THRU 2000-EXIT                 
              ELSE                                                              
                  ADD 1                          TO WS-F595-NOMATCH             
                  PERFORM 2100-READ-BP13F595      THRU 2100-EXIT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3500-PROCEED-SAME-NRIC.                                                  
      ****************************************************************          
                                                                                
           IF F595-NUM-ALLO-CAT = 'BTO'                                         
              IF (BP13F595-REC(493:2)= 'AI' OR 'AG'  OR                         
                                       'NI' OR 'SI'  OR                         
                                       'RS' OR 'FN'  OR                         
                                       'RI' OR 'RG'  OR                         
                                       'NV' OR 'NS'  OR                         
                                       'NC' OR 'NF') OR                         
                 (F595-NUM-BOOK-STATUS = 'LF' OR 'WF' OR 'BF' OR                
                                         'LA' OR 'WA' OR 'LG' OR                
                                         'WG' OR 'LS' OR 'WS' OR                
                                     'NV' OR 'NC' OR 'NF' OR                    
                                     'SF' OR 'SC' OR 'NS') OR                   
                 (BP13F595-REC(493:2) = 'NE' AND                                
                  F595-DTE-BK-APPT NOT = SPACES)                                
                  ADD 1                           TO WS-CNT-BTO                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F595-NUM-ALLO-CAT = 'BTO'                                         
             IF (BP13F595-REC(493:2)                                            
                           = 'NV' OR 'NC' OR 'NF' OR 'SF' OR 'SC' OR            
                             'NS') OR                                           
                (F595-NUM-BOOK-STATUS                                           
                           = 'NV' OR 'NC' OR 'NF' OR 'SF' OR 'SC' OR            
                             'NS')                                              
                  ADD 1                           TO WS-CNT-NV                  
             END-IF                                                             
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-BP13F595           THRU 2100-EXIT.                 
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-WRITE-BP13F520.                                                     
      ******************************************************************        
                                                                                
           IF F520-CDE-BALLOT-HOUSEHOLD = 'G'                                   
              ADD 1                         TO WS-UPG                           
              GO TO 4000-EXIT.                                                  
                                                                                
           IF BP13F520-REC(493:2) = 'NE' AND                                    
              F520-DTE-BK-APPT = SPACES                                         
              ADD 1 TO WS-NEAPPT-NO                                             
              GO TO 4000-EXIT.                                                  
                                                                                
           IF BP13F520-REC(493:2) = 'NE' AND                                    
              F520-DTE-BK-APPT NOT = SPACES                                     
              ADD 1 TO WS-NEAPPT-NO                                             
           END-IF.                                                              
                                                                                
           MOVE BP13F520-REC             TO P13F595A-REC.                       
           MOVE WS-CNT-BTO               TO F595A-NUM-BTO-CNT.                  
           MOVE WS-CNT-NV                TO F595A-NUM-NV-CNT.                   
                                                                                
           IF (BP13F520-REC(493:2) = 'NV' OR 'NC' OR 'NF' OR                    
                                     'SF' OR 'SC' OR 'NS') OR                   
              (F520-NUM-BOOK-STATUS =                                           
                     'NS' OR 'NV' OR 'NC' OR 'NF' OR 'SF' OR 'SC')              
             MOVE 'N' TO F595A-NUM-BK-TAG                                       
             ADD 1 TO WS-F595A-NV                                               
           ELSE                                                                 
             MOVE 'Y' TO F595A-NUM-BK-TAG                                       
             ADD 1 TO WS-F595A-INVITE                                           
           END-IF.                                                              
                                                                                
           WRITE P13F595A-REC.                                                  
                                                                                
           ADD 1                         TO WS-F595A-WRITE.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-WRITE-BP13F520.                                                     
      ******************************************************************        
                                                                                
           IF F520-CDE-BALLOT-HOUSEHOLD = 'G'                                   
              ADD 1                         TO WS-UPG                           
              GO TO 5000-EXIT.                                                  
                                                                                
           IF BP13F520-REC(493:2) = 'NE' AND                                    
              F520-DTE-BK-APPT = SPACES                                         
              ADD 1 TO WS-NEAPPT-NO                                             
              GO TO 5000-EXIT.                                                  
                                                                                
           IF BP13F520-REC(493:2) = 'NE' AND                                    
              F520-DTE-BK-APPT NOT = SPACES                                     
              ADD 1 TO WS-NEAPPT-NO                                             
           END-IF.                                                              
                                                                                
           IF BP13F520-REC(493:2) = 'NE' AND                                    
              F520-DTE-BK-APPT = SPACES                                         
              ADD 1 TO WS-NEAPPT-NO                                             
           END-IF.                                                              
                                                                                
           MOVE BP13F520-REC             TO P13F595A-REC.                       
           MOVE ZEROS                    TO F595A-NUM-BTO-CNT.                  
           MOVE ZEROS                    TO F595A-NUM-NV-CNT.                   
           IF (BP13F520-REC(493:2)=                                             
                   'NS' OR 'NV' OR 'NC' OR 'NF' OR 'SF' OR 'SC') OR             
              (F520-NUM-BOOK-STATUS =                                           
                   'NS' OR 'NV' OR 'NC' OR 'NF' OR 'SF' OR 'SC')                
             MOVE 'N' TO F595A-NUM-BK-TAG                                       
             ADD 1 TO WS-F595A-NV                                               
           ELSE                                                                 
             MOVE 'Y' TO F595A-NUM-BK-TAG                                       
             ADD 1 TO WS-F595A-INVITE                                           
           END-IF.                                                              
                                                                                
           WRITE P13F595A-REC.                                                  
                                                                                
           ADD 1                         TO WS-F595A-WRITE.                     
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CB79'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F595 RECORDS READ ........ : '                
                    WS-F595-READ.                                               
           DISPLAY '(2) NO OF BP13F520 RECORDS READ ........ : '                
                    WS-F520-READ                                                
           DISPLAY '(3) NO OF NRIC MATCHED ................. : '                
                    WS-MATCH.                                                   
           DISPLAY '(4) NO OF F520 NOT FD IN F595........... : '                
                    WS-F520-NOMATCH.                                            
           DISPLAY '    NO OF F595 NOT FD IN F520........... : '                
                    WS-F595-NOMATCH.                                            
           DISPLAY '                                           '                
           DISPLAY '(5) NO OF P13F595 WRITE ................ : '                
                    WS-F595A-WRITE.                                             
           DISPLAY '(6) NO OF UPGRADER       ............... : '                
                    WS-UPG.                                                     
           DISPLAY '(7) NO OF NE NO APPOINTMENT ............ : '                
                    WS-NEAPPT-NO.                                               
           DISPLAY '(8) NO OF NE WITH APPOINTMENT .......... : '                
                    WS-NEAPPT.                                                  
           DISPLAY '(9) NO OF INVITED             .......... : '                
                    WS-F595A-INVITE.                                            
           DISPLAY '(10) NO OF NO FLAT /NO CAT    .......... : '                
                    WS-F595A-NV.                                                
                                                                                
           CLOSE BP13F595                                                       
                 BP13F520                                                       
                 P13F595A.                                                      
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
