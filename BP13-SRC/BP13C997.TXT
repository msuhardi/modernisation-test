       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C997.                                                  
      *DATE-WRITTEN. 07 FEB 2017.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO MOVE ALL RECORDS IN BP13K62A AND BP13K62E*            
      *                TO HISTORY FILES WHEN KEY HAS BEEN ISSUED   *            
      *                OR ALREADY PROCESSED BY IRAS 6 MONTHS OR    *            
      *                MORE HAVE PASSED                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136651 20170207 KVL1  NEW PROGRAM                        *            
      * BP138132 20200204 KAC1  ADDED REWRITE FOR K62D WHEN DUP ERR*            
      *                         AND ADD +1 TO K62D-NUM-DOC-CNT.    *            
      * BP138916 20230519 ZAR7  CORRECT REWRITE ON BP13K62D        *            
      * BP139557 20230522 ZAR7  CATER REWRITE FOR BP13K62F         *            
      * BP138916 20230523 ZAR7  CATER FOR K62D-REF-CASE-REF-NO-NEW *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FINP  ASSIGN        TO BP13FINP.                          
                                                                                
           SELECT BP13K62A  ASSIGN        TO BP13K62A                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K62A-KEY-FLD                       
                            ALTERNATE KEY IS K62A-ALT-KEY2                      
                            FILE STATUS   IS BP13K62A-STATUS.                   
                                                                                
           SELECT BP13K62E  ASSIGN        TO BP13K62E                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K62E-KEY-FLD                       
                            FILE STATUS   IS BP13K62E-STATUS.                   
                                                                                
           SELECT BP13K62D  ASSIGN        TO BP13K62D                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K62D-KEY-FLD                       
                            FILE STATUS   IS BP13K62D-STATUS.                   
                                                                                
           SELECT BP13K62F  ASSIGN        TO BP13K62F                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K62F-KEY-FLD                       
                            FILE STATUS   IS BP13K62F-STATUS.                   
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13FINP                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13K410.                                                           
                                                                                
       FD  BP13K62A                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP13K62A.                                                           
                                                                                
       FD  BP13K62E                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP13K62E.                                                           
                                                                                
       FD  BP13K62D                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP13K62D.                                                           
                                                                                
       FD  BP13K62F                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP13K62F.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K62E-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BP13K62A-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BP13K62D-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BP13K62F-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  WS-S                PIC 9(01)  VALUE ZERO.                           
       01  WS-READ-FINP-CNT    PIC 9(06)  VALUE ZEROES.                         
       01  WS-READ-CNT         PIC 9(06)  VALUE ZEROES.                         
       01  WS-READ-K62E-CNT    PIC 9(06)  VALUE ZEROES.                         
       01  WS-SKIP-CNT         PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-CNT        PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-CNT2       PIC 9(06)  VALUE ZEROES.                         
       01  WS-BP13FINP-EOF     PIC X      VALUE 'N'.                            
       01  WS-BP13K62E-EOF     PIC X      VALUE 'N'.                            
       01  WS-BP13K62E-FND     PIC X      VALUE 'N'.                            
       01  WS-STOP             PIC X      VALUE 'N'.                            
       01  WS-DELETE-FLD.                                                       
           05 WS-K62E-KEY-FLD  OCCURS 4 TIMES                                   
                               PIC X(22)  VALUE SPACES.                         
       01  WS-CURR-DATE.                                                        
           05 WS-CURRENT-DATE  PIC 9(08)  VALUE ZEROES.                         
           05 WS-CURRENT-TIME  PIC 9(08)  VALUE ZEROES.                         
           05 WS-DIFF-FR-GMT   PIC S9(04) VALUE ZEROES.                         
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO   WS-CURR-DATE.                  
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FINP       THRU 2000-EXIT.                     
           IF WS-BP13FINP-EOF = 'Y'                                             
              DISPLAY 'FILE BP13FINP IS  EMPTY'                                 
           ELSE                                                                 
              PERFORM 3000-PROCESS-DATA     THRU 3000-EXIT                      
                UNTIL WS-BP13FINP-EOF = 'Y'                                     
           END-IF                                                               
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13FINP                                                 
                I-O    BP13K62A                                                 
                       BP13K62E                                                 
                       BP13K62D                                                 
                       BP13K62F.                                                
                                                                                
           IF BP13K62A-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K62A OPEN ERROR, STATUS : ' BP13K62A-STATUS          
              MOVE     BP13K62A-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K62E-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K62E OPEN ERROR, STATUS : ' BP13K62E-STATUS          
              MOVE     BP13K62E-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K62D-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K62D OPEN ERROR, STATUS : ' BP13K62D-STATUS          
              MOVE     BP13K62D-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K62F-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K62F OPEN ERROR, STATUS : ' BP13K62F-STATUS          
              MOVE     BP13K62F-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FINP.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FINP AT END                                                 
                         MOVE 'Y'  TO WS-BP13FINP-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-FINP-CNT                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13K62A.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K62A KEY IS K62A-ALT-KEY2.                                  
                                                                                
           EVALUATE BP13K62A-STATUS                                             
              WHEN 00                                                           
                 ADD 1                TO WS-READ-CNT                            
                 MOVE SPACES          TO BP13K62D-REC                           
                 MOVE BP13K62A-REC    TO BP13K62D-REC                           
                 MOVE WS-CURRENT-DATE TO K62D-DTE-MOVED-TO-HISTORY              
                 PERFORM 2300-WRITE-BP13K62D  THRU 2300-EXIT                    
                 ADD 1                TO WS-WRITE-CNT                           
                 PERFORM 2400-DELETE-BP13K62A THRU 2400-EXIT                    
                 MOVE SPACES                  TO   BP13K62E-REC                 
                 MOVE K410-KEY-FLD            TO   K62E-NUM-REGN                
                 PERFORM 2200-STARTBR-BP13K62E THRU 2200-EXIT                   
              WHEN 10                                                           
              WHEN 23                                                           
                 ADD 1                TO WS-SKIP-CNT                            
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K62A. STATUS IS '                   
                          BP13K62A-STATUS                                       
                 MOVE BP13K62A-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-STARTBR-BP13K62E.                                                   
      *-------------------------------------------------------------            
                                                                                
           START BP13K62E KEY >= K62E-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K62E-STATUS                                             
              WHEN 00                                                           
                 PERFORM 2210-READNXT-BP13K62E THRU 2210-EXIT                   
                   UNTIL WS-BP13K62E-EOF = 'Y'                                  
              WHEN 10                                                           
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR STR BRW BP13K62E. STATUS IS '                   
                          BP13K62E-STATUS                                       
                 MOVE BP13K62E-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2210-READNXT-BP13K62E.                                                   
      *-------------------------------------------------------------            
                                                                                
           READ BP13K62E NEXT RECORD.                                           
                                                                                
           EVALUATE BP13K62E-STATUS                                             
            WHEN 00                                                             
                 IF K62E-NUM-REGN          = K410-NUM-REGN                      
                    IF K62E-NUM-DOC-DTLS   = 'ALEO1'                            
                       MOVE 'Y'             TO WS-BP13K62E-FND                  
                       ADD 1                TO WS-S                             
                       MOVE K62E-KEY-FLD    TO WS-K62E-KEY-FLD(WS-S)            
                       ADD 1                TO WS-READ-K62E-CNT                 
                       MOVE SPACES          TO BP13K62F-REC                     
                       MOVE BP13K62E-REC    TO BP13K62F-REC                     
                       MOVE WS-CURRENT-DATE TO K62F-DTE-MOVED-TO-HISTORY        
                       PERFORM 2500-WRITE-BP13K62F  THRU 2500-EXIT              
                       ADD 1                TO WS-WRITE-CNT2                    
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'Y'                TO WS-BP13K62E-EOF                  
                 END-IF                                                         
            WHEN 10                                                             
            WHEN 23                                                             
                 MOVE 'Y'                   TO WS-BP13K62E-EOF                  
            WHEN OTHER                                                          
                 DISPLAY 'ERROR READNXT BP13K62E. STATUS IS '                   
                            BP13K62E-STATUS                                     
                 MOVE BP13K62E-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-WRITE-BP13K62D.                                                     
      *-------------------------------------------------------------            
                                                                                
           WRITE    BP13K62D-REC.                                               
                                                                                
           EVALUATE BP13K62D-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 22                                                           
                 PERFORM 2305-REWRITE-BP13K62D   THRU 2305-EXIT                 
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WRITING BP13K62D FOR KEY '                      
                          K410-KEY-FLD                                          
                 MOVE BP13K62D-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2305-REWRITE-BP13K62D.                                                   
      *-------------------------------------------------------------            
           MOVE ZEROES                   TO BP13K62D-STATUS.                    
      ***  ADD 1                         TO K62D-NUM-DOC-CNT.                   
           REWRITE  BP13K62D-REC.                                               
                                                                                
           EVALUATE BP13K62D-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WRITING BP13K62D FOR KEY '                      
                          K410-KEY-FLD                                          
                 MOVE BP13K62D-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
       2305-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2400-DELETE-BP13K62A.                                                    
      *-------------------------------------------------------------            
                                                                                
           DELETE   BP13K62A.                                                   
                                                                                
           EVALUATE BP13K62A-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR DELETING BP13K62A FOR KEY '                     
                          K410-KEY-FLD                                          
                 MOVE BP13K62A-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2500-WRITE-BP13K62F.                                                     
      *-------------------------------------------------------------            
                                                                                
           WRITE    BP13K62F-REC.                                               
                                                                                
           EVALUATE BP13K62F-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 22                                                           
                 PERFORM 2505-REWRITE-BP13K62F   THRU 2505-EXIT                 
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WRITING BP13K62F FOR KEY '                      
                          K410-KEY-FLD                                          
                 MOVE BP13K62F-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2505-REWRITE-BP13K62F.                                                   
      *-------------------------------------------------------------            
           REWRITE  BP13K62F-REC.                                               
                                                                                
           EVALUATE BP13K62F-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR REWRITING BP13K62F FOR KEY '                    
                          K410-KEY-FLD                                          
                 MOVE BP13K62F-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
       2505-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2600-DELETE-BP13K62E.                                                    
      *-------------------------------------------------------------            
                                                                                
           DELETE   BP13K62E.                                                   
                                                                                
           EVALUATE BP13K62E-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR DELETING BP13K62E FOR KEY '                     
                          K410-KEY-FLD                                          
                 MOVE BP13K62E-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM VARYING WS-S FROM 1 BY 1 UNTIL WS-S > 4                      
              MOVE SPACES                TO   WS-K62E-KEY-FLD(WS-S)             
           END-PERFORM.                                                         
           MOVE ZERO                     TO   WS-S.                             
           MOVE 'N'                      TO   WS-BP13K62E-FND                   
                                              WS-BP13K62E-EOF                   
                                              WS-STOP.                          
           MOVE SPACES                   TO   BP13K62A-REC.                     
           MOVE K410-KEY-FLD             TO   K62A-ALT-KEY2.                    
           PERFORM 2100-READ-BP13K62A    THRU 2100-EXIT.                        
           IF WS-BP13K62E-FND = 'Y'                                             
              PERFORM VARYING WS-S FROM 1 BY 1 UNTIL WS-S > 4                   
                 OR WS-STOP = 'Y'                                               
                 IF WS-K62E-KEY-FLD(WS-S) = SPACES                              
                    MOVE 'Y'             TO WS-STOP                             
                 ELSE                                                           
                    MOVE SPACES          TO K62E-KEY-FLD                        
                    MOVE WS-K62E-KEY-FLD(WS-S)                                  
                                         TO K62E-KEY-FLD                        
                    PERFORM 2600-DELETE-BP13K62E                                
                                         THRU 2600-EXIT                         
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
           PERFORM 2000-READ-BP13FINP    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '---- BP13CMAN CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF RECS READ FROM BP13FINP   = '                        
                     WS-READ-FINP-CNT.                                          
           DISPLAY ' NO OF RECS READ FROM BP13K62A   = '                        
                     WS-READ-CNT.                                               
           DISPLAY ' NO OF RECS READ FROM BP13K62E   = '                        
                     WS-READ-K62E-CNT.                                          
           DISPLAY ' NO OF RECS SKIPPED              = '                        
                     WS-SKIP-CNT.                                               
           DISPLAY ' NO OF ROWS WRITTEN TO BP13K62D = '                         
                     WS-WRITE-CNT.                                              
           DISPLAY ' NO OF ROWS WRITTEN TO BP13K62F = '                         
                     WS-WRITE-CNT2.                                             
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13FINP                                                 
                       BP13K62A                                                 
                       BP13K62D                                                 
                       BP13K62E                                                 
                       BP13K62F.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
