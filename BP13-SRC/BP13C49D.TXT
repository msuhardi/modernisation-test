      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C49D.                                                 
       AUTHOR.        KARL ANDRUS MAGALONA CABUG.                               
       DATE-WRITTEN.  JANUARY 28, 2020.                                         
      *===============================================================*         
      * OBJECTIVE  :  TO MATCHING OF P13F49C1 AND BP13K800            *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :  1. P13F49C1                                     *         
      *               2. BP13K800                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. P13F49D1  - DA = 'Y' & LOAN-TAG = 'N'        *         
      *               2  P13F49D2  - DA = 'Y' & LOAN-TAG = 'B' OR 'H' *         
      *               2  P13F49D3  - DA = 'Y' & LT NOT = B/H/N        *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP138170 28/01/2020 KAC1 NEW PROGRAM                          *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  P13F49C1  ASSIGN        TO P13F49C1.                         
           SELECT  P13F49D1  ASSIGN        TO P13F49D1.                         
           SELECT  P13F49D2  ASSIGN        TO P13F49D2.                         
           SELECT  P13F49D3  ASSIGN        TO P13F49D3.                         
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   P13F49C1                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13F49C1-RECORD.                                                    
            05 P13F49C1-NUM-REGN              PIC X(08).                        
            05 P13F49C1-LOAN-TAG              PIC X(01).                        
            05 P13F49C1-DTE-REGN              PIC X(08).                        
            05 FILLER                         PIC X(33).                        
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   P13F49D1                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F49D1-RECORD.                                                    
            05 P13F49D1-NUM-REGN           PIC X(08).                           
            05 P13F49D1-LOAN-TAG           PIC X(01).                           
            05 P13F49D1-DTE-REGN           PIC X(08).                           
            05 FILLER                      PIC X(33).                           
                                                                                
       FD   P13F49D2                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F49D2-RECORD.                                                    
            05 P13F49D2-NUM-REGN           PIC X(08).                           
            05 P13F49D2-LOAN-TAG           PIC X(01).                           
            05 P13F49D2-DTE-REGN           PIC X(08).                           
            05 FILLER                      PIC X(33).                           
                                                                                
       FD   P13F49D3                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F49D3-RECORD.                                                    
            05 P13F49D3-NUM-REGN           PIC X(08).                           
            05 P13F49D3-LOAN-TAG           PIC X(01).                           
            05 P13F49D3-DTE-REGN           PIC X(08).                           
            05 FILLER                      PIC X(33).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-K800-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-EOF-F49C1            PIC X(1)  VALUE 'N'.                     
           05  WS-K800-FND             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F49C1-READ           PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-FND-CTR         PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-NTFND-CTR       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-F49D1            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-F49D2            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-F49D3            PIC 9(7)  VALUE ZEROES.                  
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-P13F49C1      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F49C1 = 'Y'.                                          
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  P13F49C1                                                 
                       BP13K800                                                 
               OUTPUT  P13F49D1                                                 
                       P13F49D2                                                 
                       P13F49D3.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K800 , STAT = ' WS-K800-STATUS         
              MOVE WS-K800-STATUS             TO RETURN-CODE                    
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-P13F49C1.                                                      
      ******************************************************************        
           READ P13F49C1           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F49C1                            
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F49C1-READ.                            
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
           MOVE 'N'                                TO WS-K800-FND.              
           MOVE P13F49C1-NUM-REGN                  TO K800-NUM-REGN.            
           PERFORM 3100-READ-BP13K800            THRU 3100-EXIT.                
                                                                                
           IF WS-K800-FND = 'Y'                                                 
              IF K800-NUM-LOAN-TAG = 'N'                                        
                 PERFORM 4000-WRITE-P13F49D1     THRU 4000-EXIT                 
              ELSE                                                              
                IF K800-NUM-LOAN-TAG = 'B' OR 'H'                               
                   PERFORM 4200-WRITE-P13F49D2   THRU 4200-EXIT                 
                ELSE                                                            
                  IF K800-NUM-LOAN-TAG NOT = 'N' AND 'B' AND 'H'                
                     PERFORM 4400-WRITE-P13F49D3 THRU 4400-EXIT                 
                  END-IF                                                        
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-P13F49C1            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-BP13K800.                                                      
      ******************************************************************        
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                    TO WS-K800-FND                  
               WHEN 23                                                          
                    ADD 1                       TO WS-K800-NTFND-CTR            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800 : ' WS-K800-STATUS          
                            ' REGN = ' K800-NUM-REGN                            
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-WRITE-P13F49D1.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO P13F49D1-RECORD.                
           MOVE K800-NUM-REGN                TO P13F49D1-NUM-REGN.              
           MOVE K800-NUM-LOAN-TAG            TO P13F49D1-LOAN-TAG.              
           MOVE K800-DTE-REGN                TO P13F49D1-DTE-REGN.              
                                                                                
           WRITE P13F49D1-RECORD.                                               
           ADD 1                             TO WS-CNT-F49D1.                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-WRITE-P13F49D2.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO P13F49D2-RECORD.                
           MOVE K800-NUM-REGN                TO P13F49D2-NUM-REGN.              
           MOVE K800-NUM-LOAN-TAG            TO P13F49D2-LOAN-TAG.              
           MOVE K800-DTE-REGN                TO P13F49D2-DTE-REGN.              
                                                                                
           WRITE P13F49D2-RECORD.                                               
           ADD 1                             TO WS-CNT-F49D2.                   
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4400-WRITE-P13F49D3.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO P13F49D3-RECORD.                
           MOVE K800-NUM-REGN                TO P13F49D3-NUM-REGN.              
           MOVE K800-NUM-LOAN-TAG            TO P13F49D3-LOAN-TAG.              
           MOVE K800-DTE-REGN                TO P13F49D3-DTE-REGN.              
                                                                                
           WRITE P13F49D3-RECORD.                                               
           ADD 1                             TO WS-CNT-F49D3.                   
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C49D'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF INPUT(F49C1) RECORDS READ.... : '                 
                    WS-F49C1-READ.                                              
           DISPLAY '(2) NO OF BP13K800 NOT FOUND REC  ..... : '                 
                    WS-K800-NTFND-CTR.                                          
           DISPLAY '(3) NO OF P13F49D1 RECORDS WRITTEN..... : '                 
                    WS-CNT-F49D1.                                               
           DISPLAY '(4) NO OF P13F49D2 RECORDS WRITTEN..... : '                 
                    WS-CNT-F49D2.                                               
           DISPLAY '(5) NO OF P13F49D3 RECORDS WRITTEN..... : '                 
                    WS-CNT-F49D3.                                               
                                                                                
           CLOSE    P13F49C1                                                    
                    BP13K800                                                    
                    P13F49D1                                                    
                    P13F49D2                                                    
                    P13F49D3.                                                   
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800 , STAT = ' WS-K800-STATUS         
              MOVE WS-K800-STATUS             TO RETURN-CODE.                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
