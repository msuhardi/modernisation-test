       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CA04.                                                 
      *AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  17/02/2006.                                               
      * ========================================================== *            
      *             SYSTEM OF COMMITMENT  (BP13)                   *            
      * ========================================================== *            
      *                                                            *            
      *  DESCRIPTION :  TO UPDATE BP13KA30.                        *            
      *  INPUT FILE  :  BP13F100                                   *            
      *  I-O FILE    :  BP13KA30                                   *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY  ON          DESCRIPTION                       *            
      * -------- --- ----------  -----------                       *            
      * BP132789 LMS 17/02/2006  NEW PROGRAM.                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F100  ASSIGN       TO BP13F100.                           
                                                                                
           SELECT BP13KA30  ASSIGN       TO BP13KA30                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS KA30-KEY-FLD                        
                            FILE STATUS  IS WS-KA30-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *---------------------------------------------------------------*         
       FD  BP13F100                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       01  BP13F100-REC.                                                        
           05  F100-NUM-PRJCT          PIC X(03).                               
           05  F100-NUM-BLOCK          PIC X(05).                               
           05  F100-NUM-LEVEL          PIC X(02).                               
           05  F100-NUM-UNIT           PIC X(07).                               
           05  FILLER                  PIC X(01).                               
           05  F100-NUM-RANDOM         PIC X(28).                               
           05  F100-NUM-PRIO           PIC X(01).                               
           05  FILLER                  PIC X(02).                               
           05  F100-NUM-PRIO-UPDATE    PIC X(01).                               
           05  FILLER                  PIC X(750).                              
                                                                                
       FD  BP13KA30                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13KA30.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-KA30-STATUS      PIC 9(02)  VALUE ZEROES.                     
           05  WS-F100-EOF         PIC X(01)  VALUE SPACES.                     
               88 F100-END-OF-FILE   VALUE 'Y'.                                 
           05  WS-F100-READ-CNT    PIC 9(08)  VALUE ZEROES.                     
           05  WS-KA30-READ-CNT    PIC 9(08)  VALUE ZEROES.                     
           05  WS-KA30-UPD-CNT     PIC 9(08)  VALUE ZEROES.                     
                                                                                
       01  WS-BP13KA30-FOUND       PIC X(01)  VALUE SPACES.                     
           88 KA30-KEY-FOUND         VALUE 'Y'.                                 
                                                                                
       01  WS-EDT-CNT              PIC Z(7)9.                                   
                                                                                
       01 WS-LEFT-JUST-TXT         PIC X(04) VALUE SPACES.                      
       01 WS-RIGHT-JUST-TXT        PIC X(04) VALUE SPACES                       
                                   JUST RIGHT.                                  
       01 WS-CHR-CNT               PIC 9(02) VALUE ZEROES.                      
       01 WS-BLANK-CNT             PIC 9(02) VALUE ZEROES.                      
       01 WS-CHR-LEN               PIC 9(02) VALUE ZEROES.                      
       01 WS-START-POSN            PIC 9(02) VALUE ZEROES.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F100     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-F100      THRU 3000-EXIT                        
                                 UNTIL F100-END-OF-FILE.                        
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F100                                                 
                I-O    BP13KA30.                                                
                                                                                
           IF WS-KA30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA30 OPEN ERROR, STATUS : ' WS-KA30-STATUS           
              MOVE     WS-KA30-STATUS       TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F100.                                                      
      *---------------------------------------------------------------*         
           READ BP13F100 AT END                                                 
                          MOVE 'Y'  TO WS-F100-EOF                              
                         NOT AT END                                             
                          ADD  1    TO WS-F100-READ-CNT                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-F100.                                                       
      *---------------------------------------------------------------*         
           PERFORM 3100-READ-BP13KA30   THRU 3100-EXIT                          
           IF KA30-KEY-FOUND                                                    
              PERFORM 3200-UPD-BP13KA30 THRU 3200-EXIT                          
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F100      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-READ-BP13KA30.                                                      
      *---------------------------------------------------------------*         
           MOVE  SPACE           TO WS-BP13KA30-FOUND.                          
           MOVE F100-NUM-PRJCT   TO KA30-NUM-PRJCT.                             
           MOVE F100-NUM-BLOCK   TO KA30-NUM-BLK.                               
           MOVE F100-NUM-LEVEL   TO KA30-NUM-LEVEL.                             
           MOVE   4              TO WS-CHR-LEN.                                 
           MOVE SPACES           TO WS-RIGHT-JUST-TXT                           
                                    WS-LEFT-JUST-TXT.                           
           MOVE F100-NUM-UNIT(1:4) TO WS-LEFT-JUST-TXT.                         
           PERFORM 3300-RIGHT-JUSTIFY-TXT THRU 3300-EXIT.                       
           MOVE WS-RIGHT-JUST-TXT  TO KA30-NUM-MAIN-UNIT.                       
           MOVE F100-NUM-UNIT(5:3) TO KA30-NUM-SUB-UNIT.                        
           READ BP13KA30.                                                       
           EVALUATE WS-KA30-STATUS                                              
             WHEN 00                                                            
                MOVE 'Y'    TO WS-BP13KA30-FOUND                                
                ADD  1      TO WS-KA30-READ-CNT                                 
             WHEN 23                                                            
                MOVE 'N'    TO WS-BP13KA30-FOUND                                
                DISPLAY 'BP13KA30 READ KEY NOT FOUND, KEY-FLD : '               
                                                         KA30-KEY-FLD           
             WHEN OTHER                                                         
                DISPLAY 'BP13KA30 READ ERROR, STATUS : ' WS-KA30-STATUS         
                MOVE WS-KA30-STATUS  TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3200-UPD-BP13KA30.                                                       
      *---------------------------------------------------------------*         
           IF F100-NUM-PRIO-UPDATE NOT = SPACES                                 
              MOVE F100-NUM-PRIO-UPDATE TO KA30-NUM-PRIO                        
                                                                                
              REWRITE BP13KA30-REC                                              
              EVALUATE WS-KA30-STATUS                                           
                WHEN 00                                                         
                   ADD  1     TO WS-KA30-UPD-CNT                                
                WHEN 23                                                         
                   DISPLAY 'BP13KA30 REWRITE KEY NOT FOUND, KEY-FLD : '         
                                                      KA30-KEY-FLD              
                WHEN OTHER                                                      
                   DISPLAY 'BP13KA30 REWRITE ERROR, STATUS : '                  
                                                      WS-KA30-STATUS            
                   MOVE WS-KA30-STATUS  TO RETURN-CODE                          
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3300-RIGHT-JUSTIFY-TXT.                                                  
      *---------------------------------------------------------------*         
                                                                                
           MOVE ZEROES   TO WS-CHR-CNT, WS-START-POSN.                          
           INSPECT WS-LEFT-JUST-TXT TALLYING WS-CHR-CNT                         
                  FOR CHARACTERS BEFORE INITIAL SPACE.                          
           COMPUTE WS-START-POSN = (WS-CHR-LEN  -  WS-CHR-CNT) + 1              
           MOVE WS-LEFT-JUST-TXT(1:WS-CHR-CNT)  TO                              
                WS-RIGHT-JUST-TXT.                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY ' '.                                                         
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY '   PROGRAM : BP13CA04   ' .                                 
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY ' '.                                                         
           MOVE WS-F100-READ-CNT     TO WS-EDT-CNT.                             
           DISPLAY 'RECS READ FROM BP13F100    : ' WS-EDT-CNT.                  
           MOVE WS-KA30-READ-CNT     TO WS-EDT-CNT.                             
           DISPLAY 'RECS READ FROM BP13KA30    : ' WS-EDT-CNT.                  
           MOVE WS-KA30-UPD-CNT      TO WS-EDT-CNT.                             
           DISPLAY 'RECS UPDATED IN BP13KA30   : ' WS-EDT-CNT.                  
           DISPLAY ' '.                                                         
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
                                                                                
           CLOSE BP13F100                                                       
                 BP13KA30.                                                      
                                                                                
           IF WS-KA30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA30 CLOSE ERROR, STATUS : ' WS-KA30-STATUS          
              MOVE     WS-KA30-STATUS       TO  RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
