       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C759.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 09/01/2020.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES : TO ADJUST TOTAL QUOTA BASED ON TOTAL REGISTERED *         
      *               FOR SHORTLISTING MTF/CCC/RH/REN/JB/RDC/TCP.     *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13K010 I/O   SEQUENTIAL 600  K010-KEY-FLD                   *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHG-REQ#  DD/MM/YY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP13XXXX  09/01/20  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K010 ASSIGN      TO BP13K010                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS SEQUENTIAL                                       
              RECORD KEY    IS K010-KEY-FLD                                     
              FILE STATUS   IS WS-K010-STATUS.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K010                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BP13K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K010-STATUS          PIC 9(2)        VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K010-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K010-UPDATE          PIC 9(5)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-K010-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13K010    THRU 2000-EXIT                         
              UNTIL WS-K010-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN I-O    BP13K010.                                                
                                                                                
           IF (WS-K010-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K010 FAIL STATUS=' WS-K010-STATUS               
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13K010.                                                      
      *****************************************************************         
                                                                                
           READ BP13K010.                                                       
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K010-READ                          
                                                                                
                 IF (K010-KEY-FLD NOT = SPACES AND LOW-VALUES)                  
                    PERFORM 3000-MAIN-PROCESSING THRU 3000-EXIT                 
                    PERFORM 4000-UPDATE-BP13K010 THRU 4000-EXIT                 
                 END-IF                                                         
              WHEN 10                                                           
                 MOVE 'Y'              TO WS-K010-EOF                           
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K010 FAIL STATUS=' WS-K010-STATUS            
                 MOVE WS-K010-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           PERFORM 3100-ADJUST-MTF THRU 3100-EXIT.                              
           PERFORM 3200-ADJUST-CCC THRU 3200-EXIT.                              
           PERFORM 3300-ADJUST-RH  THRU 3300-EXIT.                              
           PERFORM 3400-ADJUST-REN THRU 3400-EXIT.                              
           PERFORM 3500-ADJUST-JB  THRU 3500-EXIT.                              
           PERFORM 3600-ADJUST-RDC THRU 3600-EXIT.                              
           PERFORM 3700-ADJUST-TCP THRU 3700-EXIT.                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-ADJUST-MTF.                                                         
      *****************************************************************         
                                                                                
           IF K010-REG-MTF1 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-MTF1                         
           END-IF.                                                              
           IF K010-REG-MTF2 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-MTF2                         
           END-IF.                                                              
           IF K010-REG-MTF3 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-MTF3                         
           END-IF.                                                              
                                                                                
           COMPUTE K010-NUM-TOT-MTF = K010-REG-MTF1 +                           
                                      K010-REG-MTF2 +                           
                                      K010-REG-MTF3.                            
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3200-ADJUST-CCC.                                                         
      *****************************************************************         
                                                                                
           IF K010-REG-CCC1 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-CCC1                         
           END-IF.                                                              
           IF K010-REG-CCC2 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-CCC2                         
           END-IF.                                                              
           IF K010-REG-CCC3 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-CCC3                         
           END-IF.                                                              
                                                                                
           COMPUTE K010-NUM-TOT-CCC = K010-REG-CCC1 +                           
                                      K010-REG-CCC2 +                           
                                      K010-REG-CCC3.                            
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3300-ADJUST-RH.                                                          
      *****************************************************************         
                                                                                
           IF K010-REG-RH1 NOT NUMERIC                                          
              MOVE ZEROES              TO K010-REG-RH1                          
           END-IF.                                                              
           IF K010-REG-RH2 NOT NUMERIC                                          
              MOVE ZEROES              TO K010-REG-RH2                          
           END-IF.                                                              
           IF K010-REG-RH3 NOT NUMERIC                                          
              MOVE ZEROES              TO K010-REG-RH3                          
           END-IF.                                                              
                                                                                
           COMPUTE K010-NUM-TOT-RH = K010-REG-RH1 +                             
                                      K010-REG-RH2 +                            
                                      K010-REG-RH3.                             
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3400-ADJUST-REN.                                                         
      *****************************************************************         
                                                                                
           IF K010-REG-REN1 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-REN1                         
           END-IF.                                                              
           IF K010-REG-REN2 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-REN2                         
           END-IF.                                                              
           IF K010-REG-REN3 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-REN3                         
           END-IF.                                                              
                                                                                
           COMPUTE K010-NUM-TOT-REN = K010-REG-REN1 +                           
                                      K010-REG-REN2 +                           
                                      K010-REG-REN3.                            
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3500-ADJUST-JB.                                                          
      *****************************************************************         
                                                                                
           IF K010-REG-JB1 NOT NUMERIC                                          
              MOVE ZEROES              TO K010-REG-JB1                          
           END-IF.                                                              
           IF K010-REG-JB2 NOT NUMERIC                                          
              MOVE ZEROES              TO K010-REG-JB2                          
           END-IF.                                                              
           IF K010-REG-JB3 NOT NUMERIC                                          
              MOVE ZEROES              TO K010-REG-JB3                          
           END-IF.                                                              
                                                                                
           COMPUTE K010-NUM-TOT-JB = K010-REG-JB1 +                             
                                      K010-REG-JB2 +                            
                                      K010-REG-JB3.                             
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3600-ADJUST-RDC.                                                         
      *****************************************************************         
                                                                                
           IF K010-REG-RDC1 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-RDC1                         
           END-IF.                                                              
           IF K010-REG-RDC2 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-RDC2                         
           END-IF.                                                              
           IF K010-REG-RDC3 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-RDC3                         
           END-IF.                                                              
                                                                                
           COMPUTE K010-NUM-TOT-RDC = K010-REG-RDC1 +                           
                                      K010-REG-RDC2 +                           
                                      K010-REG-RDC3.                            
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3700-ADJUST-TCP.                                                         
      *****************************************************************         
                                                                                
           IF K010-REG-TCP1 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-TCP1                         
           END-IF.                                                              
           IF K010-REG-TCP2 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-TCP2                         
           END-IF.                                                              
           IF K010-REG-TCP3 NOT NUMERIC                                         
              MOVE ZEROES              TO K010-REG-TCP3                         
           END-IF.                                                              
                                                                                
           COMPUTE K010-NUM-TOT-TCP = K010-REG-TCP1 +                           
                                      K010-REG-TCP2 +                           
                                      K010-REG-TCP3.                            
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-UPDATE-BP13K010.                                                    
      *****************************************************************         
                                                                                
           REWRITE BP13K010-TENDER.                                             
                                                                                
           IF WS-K010-STATUS = ZEROES                                           
              ADD 1                    TO WS-K010-UPDATE                        
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K010 FAIL STATUS=' WS-K010-STATUS            
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K010 FAIL STATUS=' WS-K010-STATUS              
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*****************************'.                             
           DISPLAY '* PROGRAM BP13C759          *'.                             
           DISPLAY '* CONTROL COUNTS            *'.                             
           DISPLAY '*---------------------------*'.                             
           DISPLAY '* REC READ K010     : ' WS-K010-READ ' *'.                  
           DISPLAY '* REC UPDATE K010   : ' WS-K010-UPDATE ' *'.                
           DISPLAY '*****************************'.                             
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
