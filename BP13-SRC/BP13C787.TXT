       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C787.                                                 
       AUTHOR.        LSB.                                                      
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :THE  UNSUCC PRIORITY CASE(REN,TCP,MTF,  *                
      *                CCC , RDC)                              *                
      *                WILL BE PIGEONHOLED AS PUBLIC, PROGRAM  *                
      *                TO ADJUST  NUMBER OF RESIDENT AND NON-  *                
      *                RESIDENT APPLICATION IN RESIDENT FILE   *                
      *                BP13K720 BY NT/FT/ALLOC-SCH FOR PUBLIC  *                
      *                RESIDENT/NONRES QUOTA ONLY.             *                
      * C9303B4 LSB 120493 TO CATER FOR RENT DE-CONTROLLED     *                
      *                    SCHEME.                             *                
      * I920718 LJL 120493 TO CONVERT TO COBOLII.              *                
      * C9309B4 LSB 011093 RESIDENT-HA3,HA4, SUB-TENANT        *                
      *BP130652 NOR 200299 EXPAND F520 RECL FROM 300 TO 500    *                
      *BP132162 ANC 110702 CHECK IF RESIDENT-HA NOT BLANK      *                
      *BP132463 SK  091203 CATER FOR HA1,HA2,HA3 & HA4         *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.     IBM-3090.                                           
       OBJECT-COMPUTER.     IBM-3090.                                           
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520    ASSIGN TO BP13F520.                               
                                                                                
           SELECT BP13K720    ASSIGN TO BP13K720                                
                              ORGANIZATION IS INDEXED                           
                              ACCESS      IS RANDOM                             
                              RECORD KEY IS K720-KEY-FLD                        
                              FILE STATUS IS WS-K720-STATUS.                    
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F520           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 500 CHARACTERS                    
                              RECORDING MODE IS F                               
                              LABEL RECORDS ARE STANDARD.                       
           COPY BP13F520.                                                       
                                                                                
       FD  BP13K720           LABEL RECORDS ARE STANDARD                        
                              RECORD CONTAINS 300 CHARACTERS                    
                              BLOCK CONTAINS 0 RECORDS.                         
           COPY BP13K720.                                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  WS-CNT-F520-READ          PIC 9(8) VALUE 0.                      
           05  WS-CNT-K720-REWRITE       PIC 9(8) VALUE 0.                      
           05  WS-K720-STATUS            PIC 9(2) VALUE 0.                      
           05  WS-PREV-NT                PIC X(3) VALUE SPACES.                 
           05  WS-PREV-FT                PIC X(2) VALUE SPACES.                 
           05  WS-CNT-RESIDENT           PIC 9(5) VALUE 0.                      
           05  WS-CNT-NON-RESIDENT       PIC 9(5) VALUE 0.                      
           05  WS-CNT-RESIDENT-PRIO      PIC 9(5) VALUE 0.                      
           05  WS-CNT-NON-RESIDENT-PRIO  PIC 9(5) VALUE 0.                      
           05  WS-F520-EOF               PIC X    VALUE 'N'.                    
               88 F520-EOF                        VALUE 'Y'.                    
                                                                                
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CC                PIC XX   VALUE SPACES.                 
           05  WS-DTE-CURRENT.                                                  
               10  WS-CURR-YY            PIC XX   VALUE SPACES.                 
               10  WS-CURR-MM            PIC XX   VALUE SPACES.                 
               10  WS-CURR-DD            PIC XX   VALUE SPACES.                 
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-CONTROL-RTN.                                                        
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
                                                                                
           PERFORM 2000-INIT-TABLE       THRU 2000-EXIT.                        
                                                                                
           PERFORM 3000-READ-BP13F520    THRU 3000-EXIT.                        
                                                                                
           MOVE F520-CDE-NT1             TO WS-PREV-NT.                         
           MOVE F520-CDE-FLAT-TYPE       TO WS-PREV-FT.                         
                                                                                
           PERFORM 4000-MAIN-ROUTINE     THRU 4000-EXIT                         
             UNTIL F520-EOF.                                                    
                                                                                
           PERFORM 5000-REWRITE-BP13K720   THRU 5000-EXIT.                      
                                                                                
           PERFORM 9999-CLOSE-FILES      THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  BP13F520                                                 
                I-O    BP13K720.                                                
                                                                                
           IF WS-K720-STATUS NOT = 0 AND 97                                     
               DISPLAY 'OPEN ERROR K720-STATUS ' WS-K720-STATUS                 
               MOVE WS-K720-STATUS TO RETURN-CODE                               
               PERFORM 9999-CLOSE-FILES.                                        
                                                                                
           ACCEPT WS-CURRENT-DATE        FROM DATE YYYYMMDD.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-INIT-TABLE.                                                         
                                                                                
           MOVE ZEROS               TO WS-CNT-RESIDENT,                         
                                       WS-CNT-NON-RESIDENT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13F520.                                                      
                                                                                
           READ BP13F520                 AT END                                 
           MOVE 'Y'                      TO WS-F520-EOF                         
           GO TO 3000-EXIT.                                                     
                                                                                
           ADD 1                         TO WS-CNT-F520-READ.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-MAIN-ROUTINE.                                                       
                                                                                
           IF F520-CDE-NT1 NOT = WS-PREV-NT OR                                  
              F520-CDE-FLAT-TYPE NOT = WS-PREV-FT                               
              PERFORM 5000-REWRITE-BP13K720     THRU 5000-EXIT.                 
                                                                                
            IF (F520-CDE-RESIDENT-HA1 NOT = SPACES AND LOW-VALUES AND           
                                            'H' AND 'M' AND 'P')   OR           
               (F520-CDE-RESIDENT-HA2 NOT = SPACES AND LOW-VALUES AND           
                                            'H' AND 'M' AND 'P')   OR           
               (F520-CDE-RESIDENT-HA3 NOT = SPACES AND LOW-VALUES AND           
                                            'H' AND 'M' AND 'P')   OR           
               (F520-CDE-RESIDENT-HA4 NOT = SPACES AND LOW-VALUES)              
              ADD 1                 TO WS-CNT-RESIDENT                          
            ELSE                                                                
              ADD 1                 TO WS-CNT-NON-RESIDENT.                     
                                                                                
       4000-READ-F520.                                                          
                                                                                
           MOVE F520-CDE-NT1             TO WS-PREV-NT.                         
           MOVE F520-CDE-FLAT-TYPE       TO WS-PREV-FT.                         
                                                                                
           PERFORM 3000-READ-BP13F520    THRU 3000-EXIT.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-REWRITE-BP13K720.                                                   
                                                                                
           MOVE WS-PREV-NT               TO K720-CDE-NT.                        
           MOVE WS-PREV-FT               TO K720-CDE-FLAT-TY.                   
           MOVE 0                        TO WS-CNT-RESIDENT-PRIO.               
           MOVE 0                        TO WS-CNT-NON-RESIDENT-PRIO.           
                                                                                
           READ BP13K720                                                        
           IF WS-K720-STATUS = 0                                                
                                                                                
               COMPUTE WS-CNT-RESIDENT-PRIO = WS-CNT-RESIDENT -                 
                                              K720-NUM-APP-RES-PA               
               COMPUTE WS-CNT-NON-RESIDENT-PRIO = WS-CNT-NON-RESIDENT -         
                                              K720-NUM-APP-NONRES-PA            
                                                                                
               DISPLAY '---------------------------'                            
               DISPLAY 'NT   FT  K720    RES   NRES'                            
               DISPLAY K720-CDE-NT  '  '   K720-CDE-FLAT-TY                     
                              '  ORIG  '   K720-NUM-APP-RES-PA                  
                                    '  '   K720-NUM-APP-NONRES-PA               
               DISPLAY '         ADD   '   WS-CNT-RESIDENT-PRIO                 
                                    '  '   WS-CNT-NON-RESIDENT-PRIO             
                                                                                
               MOVE WS-CNT-RESIDENT     TO K720-NUM-APP-RES-PA                  
               MOVE WS-CNT-NON-RESIDENT TO K720-NUM-APP-NONRES-PA               
               MOVE WS-CURRENT-DATE     TO K720-DTE-UPDATE                      
               ADD 1                    TO WS-CNT-K720-REWRITE                  
                                                                                
               DISPLAY '         NEW   '   K720-NUM-APP-RES-PA                  
                                    '  '   K720-NUM-APP-NONRES-PA               
                                                                                
               REWRITE BP13K720-RESIDENT                                        
           ELSE                                                                 
               DISPLAY 'READ ERROR,             '                               
                       '            K720-STATUS ' WS-K720-STATUS                
               DISPLAY 'K720-KEY '         K720-KEY-FLD                         
                                    ' '    WS-CNT-RESIDENT                      
                                    '  '   WS-CNT-NON-RESIDENT.                 
                                                                                
           PERFORM 2000-INIT-TABLE       THRU 2000-EXIT.                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-CLOSE-FILES.                                                        
                                                                                
           CLOSE BP13K720                                                       
                 BP13F520.                                                      
                                                                                
           IF WS-K720-STATUS NOT = 0 AND 97                                     
               DISPLAY 'OPEN ERROR K720-STATUS ' WS-K720-STATUS                 
               MOVE WS-K720-STATUS TO RETURN-CODE.                              
                                                                                
           DISPLAY '>>>>>>>>>>>>>>>>> BP13C787 <<<<<<<<<<<<<<<<<<<<<<<'.        
           DISPLAY 'COMPUTE NO. OF RESIDENT & NON RESIDENT APPLICATION'.        
           DISPLAY 'FOR UNSUCC PRIORITY FLOW DOWN TO PUBLIC PIGEONHOLE'.        
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF RECORDS READ FROM BP13F520    : '                     
                    WS-CNT-F520-READ.                                           
           DISPLAY '                        BALLOT FILE.'.                      
           DISPLAY 'NO OF RECORDS REWRITE IN BP13K720   : '                     
                    WS-CNT-K720-REWRITE.                                        
           DISPLAY '  '.                                                        
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
