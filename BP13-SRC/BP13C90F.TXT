       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C90F.                                                 
       AUTHOR.        ELGER DE GUZMAN                                           
      *DATE-WRITTEN.  17/09/2014.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : PROGRAM TO DELETE KEY-ISSUE CASE IN KC00         *        
      *                                                                         
      *                                                                         
      * OUTPUT DESCRIPTION:                                            *        
      *                                                                *        
      * BP13KC00 : FINANCIAL WORKSHEET FILE.                           *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13FC00  - WORKSHEET/CASHIER ORDER FILE  *        
      *               :  2.  BM06K110  - FIS FILE                      *        
      *   I-O    FILE :  1.  BP13KC00  - WORKSHEET/CASHIER ORDER FILE  *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *                                                                *        
      *   BP135420  ESD1           NEW PROGRAM                         *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC00  ASSIGN        TO BP13FC00.                          
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K110-KEY-FLD                        
                            ALTERNATE RECORD KEY IS K110-REGN-NO                
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT BP13KC00  ASSIGN        TO BP13KC00                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS KC00-KEY-FLD                    
                            FILE STATUS      IS WS-KC00-STATUS.                 
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13KC00                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13KC00.                                                           
                                                                                
       FD  BP13FC00                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 1000 CHARACTERS.                                     
       01  BP13FC00-REC.                                                        
           05  FC00-KEY-FLD.                                                    
               10  FC00-NUM-REGN              PIC X(8).                         
               10  FC00-DTE-UPDATE            PIC X(8).                         
           05  FILLER                         PIC X(984).                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FC00-EOF                      PIC X(1) VALUE SPACES.              
       01  WS-FOUND-K110                    PIC X(1) VALUE 'N'.                 
       01  WS-FOUND-KC00                    PIC X(1) VALUE 'N'.                 
       01  WS-K110-STATUS                   PIC 99    VALUE ZEROS.              
       01  WS-KC00-STATUS                   PIC 99    VALUE ZEROS.              
       01  WS-KC00-DELETE                   PIC 9(7)  VALUE ZEROS.              
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-FC00-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13FC00                                                 
                       BM06K110                                                 
                I-O    BP13KC00.                                                
                                                                                
           INITIALIZE  BP13FC00-REC BP13KC00-REC K110-REC.                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC00 ERROR ' WS-KC00-STATUS                  
              MOVE     WS-KC00-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BP13FC00                                                       
                 AT END MOVE 'Y'  TO WS-FC00-EOF                                
                                                                                
            END-READ.                                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           MOVE    SPACES                 TO   K110-REC                         
                                               BP13KC00-REC                     
           INITIALIZE                          K110-REC                         
                                               BP13KC00-REC                     
                                                                                
           MOVE    FC00-NUM-REGN          TO   K110-REGN-NO.                    
           MOVE    FC00-KEY-FLD           TO   KC00-KEY-FLD.                    
                                                                                
           PERFORM 3100-READ-BM06K110     THRU 3100-EXIT                        
           PERFORM 3200-READ-BP13KC00     THRU 3200-EXIT                        
                                                                                
           IF WS-FOUND-K110 = 'Y'                                               
              IF K110-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR ZEROES           
                 CONTINUE                                                       
              ELSE                                                              
                 IF WS-FOUND-KC00 = 'Y'                                         
                    PERFORM 5000-DELETE-BP13KC00 THRU 5000-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3100-READ-BM06K110.                                                      
      *================================================================*        
                                                                                
           READ BM06K110 KEY IS K110-REGN-NO.                                   
                                                                                
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                MOVE  'Y'  TO WS-FOUND-K110                                     
              WHEN 23                                                           
                MOVE  'N'  TO WS-FOUND-K110                                     
              WHEN OTHER                                                        
                MOVE  WS-K110-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BM06K110 FILE : ' WS-K110-STATUS         
                DISPLAY 'K110-KEY-FIELD = ' K110-KEY-FLD                        
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3200-READ-BP13KC00.                                                      
      *================================================================*        
                                                                                
           READ BP13KC00.                                                       
                                                                                
           EVALUATE WS-KC00-STATUS                                              
              WHEN 00                                                           
                MOVE 'Y' TO  WS-FOUND-KC00                                      
              WHEN 23                                                           
                MOVE 'N' TO  WS-FOUND-KC00                                      
              WHEN OTHER                                                        
                MOVE  WS-KC00-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13KC00 FILE : ' WS-KC00-STATUS         
                DISPLAY 'KC00-NUM-REGN = ' KC00-NUM-REGN                        
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5000-DELETE-BP13KC00.                                                    
      *================================================================*        
                                                                                
           DELETE BP13KC00                                                      
                                                                                
           IF WS-KC00-STATUS = 00                                               
              ADD 1 TO WS-KC00-DELETE                                           
           ELSE                                                                 
              MOVE  WS-KC00-STATUS TO RETURN-CODE                               
              DISPLAY 'ERROR WRITING BP13KC00 FILE : ' WS-KC00-STATUS           
              DISPLAY 'KC00-NUM-REGN = ' KC00-NUM-REGN                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           DISPLAY ' DELETED CASE IN BP13KC00 : ' WS-KC00-DELETE.               
                                                                                
           CLOSE BP13FC00                                                       
                 BM06K110                                                       
                 BP13KC00.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-KC00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC00 ERROR ' WS-KC00-STATUS                  
              MOVE     WS-KC00-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
