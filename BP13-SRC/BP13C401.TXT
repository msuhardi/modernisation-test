       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.            BP13C401.                                         
       AUTHOR.                RASM.                                             
      *DATE-WRITTEN.          09/02/1999.                                       
                                                                                
      ******************************************************************        
      *                   SYSTEM OF COMMITMENT (BP13)                  *        
      *================================================================*        
      *  OBJECTIVE:  1.  TO EXTRACT TP CASES WHERE F410-DTE-ARGORDER   *        
      *                  <= 19990101 AND NON SA-TP.                    *        
      *================================================================*        
      *  FILES DESCRIPTION:-                                           *        
      *                 ACCESS       REC                               *        
      *  DD NAME  I/O/E  MODE        LEN   KEY           REMARKS       *        
      *  -------  ----- ------       ---   ---           -------       *        
      * BP13F410    I   SEQ         2000                               *        
      * AB02F900    O   SEQ          100                               *        
      *================================================================*        
      *  MODIFICATIONS :                                               *        
      *  CHGE REQ #  DATE          AUTHOR         REMARKS              *        
      *  ----------  ----          ------         -------              *        
      *  BP130611    09/02/1999    RASM    NEW PROGRAM                 *        
      *  BP130611    25/02/1999    LSB     TRANSMISSION OF SALES ORDER *        
      *                                    DATA TO CPF BOARD THRU CTS  *        
      *  BP130611    04/03/1999    LSB     TRANSMISSION OF SALES ORDER *        
      *                                    DATA TO CPF BOARD THRU CTS  *        
      *  BP130790    02/02/2000    SCK     TO ADD IN IN-SOURCE DOCUMEN-*        
      *                                    TATION                      *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                ACCESS MODE       IS RANDOM                                     
                ORGANIZATION      IS INDEXED                                    
                RECORD KEY        IS K110-KEY-FLD                               
                FILE STATUS       IS BM06K110-STATUS.                           
                                                                                
           SELECT AB02F900 ASSIGN TO AB02F900.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F410        BLOCK  CONTAINS 0    RECORDS                         
                           RECORD CONTAINS 2000 CHARACTERS                      
                           LABEL  RECORD   IS   STANDARD                        
                           RECORDING MODE  IS   F.                              
       COPY BP13F410.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  AB02F900        BLOCK  CONTAINS 0   RECORDS                          
                           RECORD CONTAINS 100 CHARACTERS                       
                           LABEL  RECORD   IS  STANDARD                         
                           RECORDING MODE  IS  F.                               
       COPY AB02F900.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-DTE-CCYYMMDD.                                                     
           05  WS-DTE-CC                           PIC X(2).                    
           05  WS-DTE-CURRENT.                                                  
               10  WS-DTE-YY                       PIC X(2).                    
               10  WS-DTE-MM                       PIC X(2).                    
               10  WS-DTE-DD                       PIC X(2).                    
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  BP13F410-STATUS                     PIC 9(2) VALUE ZEROS.        
           05  WS-F410-EOF                         PIC X    VALUE 'N'.          
           05  AB02F900-STATUS                     PIC 9(2) VALUE ZEROS.        
           05  BM06K110-STATUS                     PIC 99  VALUE ZEROES.        
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F410-CNT-READ                    PIC 9(8) VALUE ZEROS.        
           05  WS-F900-CNT-WRITE                   PIC 9(8) VALUE ZEROS.        
           05  WS-K110-CNT-READ                    PIC 9(8) VALUE ZEROS.        
           05  WS-K110-CNT-BYPASS                  PIC 9(8) VALUE ZEROS.        
           05  WS-K110-CNT-NOTFND                  PIC 9(8) VALUE ZEROS.        
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SYSTEM-DTE.                                                   
               10  WS-SYS-DTE-DD                   PIC X(2).                    
               10  FILLER                          PIC X    VALUE '/'.          
               10  WS-SYS-DTE-MM                   PIC X(2).                    
               10  FILLER                          PIC X    VALUE '/'.          
               10  WS-SYS-DTE-CC                   PIC X(2).                    
               10  WS-SYS-DTE-YY                   PIC X(2).                    
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-CONTROL.                                                            
      ******************************************************************        
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-BP13F410-READ THRU 2000-EXIT.                           
           PERFORM 3000-MAIN-ROUTINE  THRU 3000-EXIT                            
                   UNTIL (WS-F410-EOF = 'Y').                                   
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13F410                                                 
                       BM06K110                                                 
                OUTPUT AB02F900.                                                
                                                                                
           IF BP13F410-STATUS NOT = ZEROS                                       
              MOVE BP13F410-STATUS TO RETURN-CODE                               
              DISPLAY 'OPEN BP13F410 FAIL STATUS = ' BP13F410-STATUS            
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF AB02F900-STATUS NOT = ZEROS                                       
              MOVE AB02F900-STATUS  TO RETURN-CODE                              
              DISPLAY 'OPEN AB02F900 FAIL STATUS = ' AB02F900-STATUS            
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = '00' AND NOT = '97'                         
              DISPLAY ' OPENING ERROR,BM06K110-STATUS' BM06K110-STATUS          
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-DTE-CCYYMMDD FROM DATE YYYYMMDD.                           
           DISPLAY WS-DTE-CCYYMMDD.                                             
           MOVE WS-DTE-DD TO WS-SYS-DTE-DD.                                     
           MOVE WS-DTE-MM TO WS-SYS-DTE-MM.                                     
           MOVE WS-DTE-CC TO WS-SYS-DTE-CC.                                     
           MOVE WS-DTE-YY TO WS-SYS-DTE-YY.                                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-BP13F410-READ.                                                      
      ******************************************************************        
      *----------------------------------------------------------------*        
      *  PROCEDURE TO READ BP13F410 FILE.                              *        
      *----------------------------------------------------------------*        
           READ BP13F410 AT END                                                 
                         MOVE 'Y'         TO WS-F410-EOF                        
                         GO TO 2000-EXIT                                        
           END-READ.                                                            
                                                                                
           ADD 1                          TO WS-F410-CNT-READ.                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-MAIN-ROUTINE.                                                       
      ******************************************************************        
           PERFORM 3100-READ-BM06K110 THRU 3100-EXIT.                           
                                                                                
           DISPLAY 'F410-NUM-REGN       = ' F410-NUM-REGN.                      
           DISPLAY 'K110-DTE-ACCEPTANCE = ' K110-DTE-ACCEPTANCE.                
                                                                                
           IF K110-DTE-ACCEPTANCE(1:6) < '200310'                               
              PERFORM 4000-AB02F900-WRITE THRU 4000-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F410-READ  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3100-READ-BM06K110.                                                      
      ******************************************************************        
            IF F410-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                     
               MOVE F410-NUM-SCH-ACC  TO  K110-KEY-FLD                          
            ELSE                                                                
               MOVE SPACES TO K110-DTE-ACCEPTANCE                               
               GO TO 3100-EXIT                                                  
            END-IF.                                                             
                                                                                
           READ BM06K110                                                        
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
               ADD 1 TO WS-K110-CNT-READ                                        
               IF K110-DTE-ACCEPTANCE(1:6) >= '200310'                          
                  ADD 1 TO WS-K110-CNT-BYPASS                                   
               END-IF                                                           
             WHEN 23                                                            
               ADD 1 TO WS-K110-CNT-NOTFND                                      
               MOVE SPACES TO  K110-DTE-ACCEPTANCE                              
               DISPLAY 'RECORD NOT FOUND IN FIS:' BM06K110-STATUS               
               DISPLAY 'F410-NUM-SCH-ACC: ' F410-NUM-SCH-ACC                    
             WHEN OTHER                                                         
               DISPLAY 'READ ERROR,BM06K110-STATUS' BM06K110-STATUS             
               DISPLAY 'F410-NUM-SCH-ACC: ' F410-NUM-SCH-ACC                    
               MOVE BM06K110-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-AB02F900-WRITE.                                                     
      ******************************************************************        
      *----------------------------------------------------------------*        
      *  PROCEDURE TO WRITE AB02F900 FILE.                             *        
      *----------------------------------------------------------------*        
           MOVE SPACES                      TO AB02F900-REC.                    
           INITIALIZE                          AB02F900-REC.                    
                                                                                
           MOVE F410-NUM-SCH-ACC            TO F900-NUM-HDB-REF.                
           MOVE F410-CPF1                   TO F900-NUM-CPF-ACCT1.              
           MOVE F410-CPF2                   TO F900-NUM-CPF-ACCT2.              
           MOVE F410-CPF3                   TO F900-NUM-CPF-ACCT3.              
           MOVE F410-CPF4                   TO F900-NUM-CPF-ACCT4.              
           MOVE  K110-DTE-ACCEPTANCE        TO F900-DTE-SP.                     
           MOVE 'SS'                        TO F900-CDE-TRANS.                  
      *    MOVE F410-DTE-KEY-ISSUE          TO F900-DTE-CREATE.                 
      *    MOVE F410-NUM-REGN               TO F900-DTE-CREATE.                 
                                                                                
           WRITE AB02F900-REC.                                                  
           ADD   1                          TO WS-F900-CNT-WRITE.               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
           DISPLAY 'PROGRAM ID:  BP13C401'.                                     
           DISPLAY 'DATE      :  ' WS-SYSTEM-DTE.                               
           DISPLAY '*============ COUNTERS ============*'.                      
           DISPLAY 'BP13F410 RECORDS READ    : ' WS-F410-CNT-READ.              
           DISPLAY 'BM06K110 RECORDS READ    : ' WS-K110-CNT-READ.              
           DISPLAY 'BM06K110 RECORDS BYPASSED: ' WS-K110-CNT-BYPASS.            
           DISPLAY 'BM06K110 RECORDS NOTFND  : ' WS-K110-CNT-NOTFND.            
           DISPLAY 'AB02F900 RECORDS WRITTEN : ' WS-F900-CNT-WRITE.             
                                                                                
           CLOSE BP13F410                                                       
                 BM06K110                                                       
                 AB02F900.                                                      
                                                                                
           IF BP13F410-STATUS NOT = ZEROS                                       
              MOVE BP13F410-STATUS TO RETURN-CODE                               
              DISPLAY 'CLOSE BP13F410 FAIL STATUS = ' BP13F410-STATUS           
           END-IF.                                                              
                                                                                
           IF AB02F900-STATUS NOT = ZEROS                                       
              MOVE AB02F900-STATUS TO RETURN-CODE                               
              DISPLAY 'CLOSE AB02F900 FAIL STATUS = ' AB02F900-STATUS           
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 0                                           
             MOVE BM06K110-STATUS TO RETURN-CODE                                
             DISPLAY ' CLOSING ERROR, BM06K110-STATUS:' BM06K110-STATUS         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
      *  >>>>>>>>>>>>>>>>>>>>>> END OF BP13C401 <<<<<<<<<<<<<<<<<<<<<  *        
      *----------------------------------------------------------------*        
