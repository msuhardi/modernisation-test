       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C515.                                                 
      *AUTHOR.        SAUL.                                                     
      *DATE-WRITTEN.  23/01/2006.                                               
      * ========================================================== *            
      * FLAT INVENTORY SYSTEM (BM06)                               *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *                                                            *            
      *    INFORM BO ON THE VIEWING OF SELECTED RP AND ST FLATS    *            
      *                                                            *            
      *  INPUT     :  1. BP13F310                                  *            
      *               2. BP13K800                                  *            
      *               3. BP13K820                                  *            
      *               4. BM06K110                                  *            
      *  OUTPUT    :  1. BP13MAIL                                  *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP132799 20060123 SSS2  NEW PROGRAM                        *            
      * BP132878 20060614 SSS2  BYPASS NUM-ALLO-CAT = RT           *            
      *                         READ PIDB FOR FLAT ADDR            *            
      * BP132866 20060628 SSS2  READ BO CODE FROM PIDB             *            
      * BP13     20070409 SSS2  OMTI CHECK ON FLAT TYPE            *            
      * BP133525 20090121 DW5   TO INCLUDE CLH9                    *            
      * BP133859 20100527 ESA1  TO REMOVE 'ST'                     *            
      * BP133933 20100810 RB12  REMOVE CLH9                        *            
      * BP136277 20160622 ESA1  TO CATER FOR ST FLATS W/ RP CODE=20*            
      * BP138201 20200703 MRR5  TO UPDATE SIGNATORY                *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS BP13K800-STATUS.                     
                                                                                
           SELECT BP13K820 ASSIGN TO  BP13K820                                  
                           ORGANIZATION IS  INDEXED                             
                           ACCESS MODE  IS  RANDOM                              
                           RECORD KEY   IS  K820-KEY-FLD                        
                           FILE STATUS  IS  BP13K820-STATUS.                    
                                                                                
           SELECT BM06K110 ASSIGN       TO BM06K110                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K110-KEY-FLD                         
                           FILE STATUS  IS BM06K110-STATUS.                     
                                                                                
           SELECT BP13F310 ASSIGN       TO BP13F310.                            
                                                                                
           SELECT BP13MAIL ASSIGN       TO BP13MAIL.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F310                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F310.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD  BP13MAIL                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE  IS F.                                               
       01  MAIL-PRTREC                       PIC X(80).                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K800-STATUS          PIC 99     VALUE 00.                        
       01  BP13K820-STATUS          PIC 99     VALUE 00.                        
       01  BM06K110-STATUS          PIC 99     VALUE 00.                        
       01  WS-SQL-CODE              PIC -ZZZZ.                                  
                                                                                
       01  WS-EOF-BP13F310          PIC X      VALUE SPACES.                    
                                                                                
       01  WS-SNO                   PIC 9(2)   VALUE ZEROES.                    
       01  WS-F310-READ             PIC 9(4)   VALUE ZEROES.                    
       01  WS-K800-READ             PIC 9(4)   VALUE ZEROES.                    
       01  WS-K800-NOTFND           PIC 9(4)   VALUE ZEROES.                    
       01  WS-K110-READ             PIC 9(4)   VALUE ZEROES.                    
       01  WS-MAIL-WROTE            PIC 9(4)   VALUE ZEROES.                    
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-DATE              PIC X(8)   VALUE SPACES.                    
           05  WS-CURRENT-TIME      PIC X(9)   VALUE SPACES.                    
           05  FILLER               PIC X(4)   VALUE SPACES.                    
       01  WS-CURR-TIME.                                                        
           05  WS-CURR-TIME1        PIC X(9).                                   
           05  WS-TIME REDEFINES WS-CURR-TIME1 PIC 9(9).                        
                                                                                
       01  WS-TME-ACCEPT-REJECT.                                                
           05  WS-TME-ACCEPT        PIC X(8).                                   
           05  WS-TIME-ACCEPT REDEFINES WS-TME-ACCEPT PIC 9(8).                 
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
      *  for testing purpose -------                                            
      *01  MAIL-HDR1.                                                           
      *    05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB3'.                 
      *    05  FILLER          PIC X(68)  VALUE SPACES.                         
      *                                                                         
      *01  MAIL-HDR2.                                                           
      *    05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
      *    05  MAIL-SENDID     PIC X(07)  VALUE 'CSXXXXX'.                      
      *    05  FILLER          PIC X(09)  VALUE '@SGPHDB3>'.                    
      *    05  FILLER          PIC X(53)  VALUE SPACES.                         
      *                                                                         
       01  MAIL-DTL1.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-TOMAILID   PIC X(20)  VALUE SPACES.                         
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-CCMAILID   PIC X(16)  VALUE                                 
               'oml1@hdb.gov.sg>'.                                              
           05  FILLER          PIC X(55)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-CCMAILID   PIC X(17)  VALUE                                 
               'clc11@hdb.gov.sg>'.                                             
           05  FILLER          PIC X(54)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5.                                                           
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAIL-TOMAILID1  PIC X(20)  VALUE SPACES.                         
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6.                                                           
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  MAIL-CCMAILID1  PIC X(16)  VALUE                                 
               'oml1@hdb.gov.sg>'.                                              
           05  FILLER          PIC X(60)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6B.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  MAIL-CCMAILID1  PIC X(17)  VALUE                                 
               'clc11@hdb.gov.sg>'.                                             
           05  FILLER          PIC X(59)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL7.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  FILLER          PIC X(11)  VALUE 'VIEWING OF '.                  
           05  MAIL-FT1        PIC X(4)   VALUE SPACES.                         
           05  FILLER          PIC X(13)  VALUE ' FLAT AT BLK '.                
           05  MAIL-ADDR       PIC X(42)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL8.                                                           
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY01.                                                         
           05 FILLER                   PIC X(50)  VALUE                         
              '    We wish to inform you that the following appli'.             
           05 FILLER                   PIC X(20)  VALUE                         
              'cant(s) has/have    '.                                           
           05 FILLER                   PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY02.                                                         
           05 FILLER                   PIC X(26)  VALUE                         
              'booked the abovementioned '.                                     
           05 MAIL-FT2                 PIC X(4)   VALUE SPACES.                 
           05 FILLER                   PIC X(9)   VALUE ' flat on '.            
           05 MAIL-DTE-ACCEPTANCE      PIC X(10)  VALUE SPACES.                 
           05 FILLER                   PIC X      VALUE '.'.                    
           05 FILLER                   PIC X(20)  VALUE SPACES.                 
           05 FILLER                   PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY02B.                                                        
           05 FILLER                   PIC X(10)  VALUE ' REGN NO: '.           
           05 MAIL-NUM-REGN            PIC X(8)   VALUE SPACES.                 
           05 FILLER                   PIC X(52)  VALUE SPACES.                 
           05 FILLER                   PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY03.                                                         
           05 FILLER                   PIC X(6)   VALUE SPACES.                 
           05 FILLER                   PIC X(40)  VALUE 'NAME '.                
           05 FILLER                   PIC X(2)   VALUE SPACES.                 
           05 FILLER                   PIC X(9)   VALUE 'NRIC NO'.              
           05 FILLER                   PIC X(2)   VALUE SPACES.                 
           05 FILLER                   PIC X(10)  VALUE 'CONTACT NO'.           
           05 FILLER                   PIC X(11)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY04.                                                         
           05 FILLER                   PIC X      VALUE SPACES.                 
           05 FILLER                   PIC XX     VALUE '--'.                   
           05 FILLER                   PIC X      VALUE '-'.                    
           05 FILLER                   PIC X(2)   VALUE SPACES.                 
           05 FILLER                   PIC X(40)  VALUE ALL '-'.                
           05 FILLER                   PIC X(2)   VALUE SPACES.                 
           05 FILLER                   PIC X(9)   VALUE ALL '-'.                
           05 FILLER                   PIC X(2)   VALUE SPACES.                 
           05 FILLER                   PIC X(8)   VALUE ALL '-'.                
           05 FILLER                   PIC X(13)  VALUE SPACES.                 
                                                                                
       01  MAIL-DETAIL.                                                         
           05 FILLER                   PIC X      VALUE SPACES.                 
           05 MAIL-SNO                 PIC Z9     VALUE ZEROS.                  
           05 FILLER                   PIC X      VALUE ')'.                    
           05 FILLER                   PIC X(2)   VALUE SPACES.                 
           05 MAIL-NME-OCCUP           PIC X(40)  VALUE SPACES.                 
           05 FILLER                   PIC X(2)   VALUE SPACES.                 
           05 MAIL-NUM-NRIC            PIC X(9)   VALUE SPACES.                 
           05 FILLER                   PIC X(2)   VALUE SPACES.                 
           05 MAIL-CONTACT             PIC X(8)   VALUE SPACES.                 
           05 FILLER                   PIC X(13)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY05.                                                         
           05 FILLER                   PIC X(50)  VALUE                         
              '2   Please allow the applicant(s) to view the flat'.             
           05 FILLER                   PIC X(20)  VALUE                         
              ' (during office     '.                                           
           05 FILLER                   PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY06.                                                         
           05 FILLER                   PIC X(50)  VALUE                         
              'hour), within the next 2 weeks. Thank you.        '.             
           05 FILLER                   PIC X(30)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY07.                                                         
           05 FILLER                   PIC X(50)  VALUE                         
              'OH MUN LAI JANICE (MS)                            '.             
           05 FILLER                   PIC X(30)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY08.                                                         
           05 FILLER                   PIC X(50)  VALUE                         
              'SR ESTATE MANAGER                                 '.             
           05 FILLER                   PIC X(30)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY09.                                                         
           05 FILLER                   PIC X(50)  VALUE                         
              'SALES OPERATIONS SECTION                          '.             
           05 FILLER                   PIC X(30)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY10.                                                         
           05 FILLER                   PIC X(50)  VALUE                         
              'ESTATE ADMIN & PROPERTY GROUP                     '.             
           05 FILLER                   PIC X(30)  VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
      *-------------------------------------------------------------            
      * BP13C913 PARAMETERS                                                     
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE         THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13F310        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-DETAIL       THRU  3000-EXIT                   
                    UNTIL WS-EOF-BP13F310 = 'Y'.                                
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F310                                                
                        BP13K800                                                
                        BP13K820                                                
                        BM06K110                                                
                 OUTPUT BP13MAIL.                                               
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR, BP13K800-STATUS ' BP13K800-STATUS            
              MOVE     BP13K800-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE     THRU 9000-EXIT                    
           END-IF.                                                              
                                                                                
           IF BP13K820-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR, BP13K820-STATUS ' BP13K820-STATUS            
              MOVE     BP13K820-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE     THRU 9000-EXIT                    
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR, BM06K110-STATUS ' BM06K110-STATUS            
              MOVE     BM06K110-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE     THRU 9000-EXIT                    
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
           MOVE WS-CURRENT-TIME     TO  WS-CURR-TIME1.                          
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                   DELIMITED BY SIZE INTO MAIL-DATE.                            
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-READ-BP13F310.                                                      
      *-------------------------------------------------------------            
           READ BP13F310                                                        
                AT END MOVE 'Y' TO WS-EOF-BP13F310                              
                GO TO  2000-EXIT.                                               
                                                                                
           ADD 1 TO WS-F310-READ.                                               
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES TO MAIL-DETAIL                                           
                          MAIL-TOMAILID                                         
                          MAIL-TOMAILID1                                        
                          MAIL-FT1                                              
                          MAIL-FT2                                              
                          MAIL-ADDR                                             
                          MAIL-DTE-ACCEPTANCE                                   
                          MAIL-NUM-REGN                                         
                          MAIL-NUM-NRIC.                                        
           MOVE ZEROS  TO WS-SNO.                                               
                                                                                
           MOVE F310-TME-ACCEPT-REJECT TO WS-TME-ACCEPT-REJECT.                 
                                                                                
           IF (WS-TIME(1:4) < 1500 AND WS-TIME-ACCEPT(1:4) < 1401) OR           
              (WS-TIME(1:4) > 1800 AND WS-TIME-ACCEPT(1:4) > 1400)              
              PERFORM 3500-PROCESS-PARA THRU 3500-EXIT                          
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F310    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3500-PROCESS-PARA.                                                       
      *-------------------------------------------------------------            
           PERFORM 4000-READ-BP13K800         THRU 4000-EXIT.                   
                                                                                
           IF K800-NUM-SCH-ACC NOT = SPACES                                     
                                                                                
              IF K800-NUM-ALLO-CAT NOT = 'RT '                                  
                                                                                
                 PERFORM 4500-READ-BM06K110 THRU 4500-EXIT                      
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           MOVE F310-REGN-NO      TO K800-NUM-REGN                              
                                     MAIL-NUM-REGN.                             
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
           WHEN 00                                                              
                ADD   1    TO WS-K800-READ                                      
           WHEN 23                                                              
                ADD   1    TO WS-K800-NOTFND                                    
                DISPLAY ' K800 REC NOTFND ' F310-REGN-NO                        
           WHEN OTHER                                                           
                DISPLAY ' K800 READ ERROR, STATUS ' BP13K800-STATUS             
                        ',  KEY = ' F310-REGN-NO                                
                PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4500-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           MOVE F310-SCH-ACC TO K110-KEY-FLD.                                   
                                                                                
           READ BM06K110 KEY IS K110-KEY-FLD.                                   
                                                                                
           EVALUATE BM06K110-STATUS                                             
           WHEN  00                                                             
                                                                                
              IF ((K110-FLAT-STATUS = 'RP') OR                                  
                  (K110-FLAT-STATUS = 'ST' AND                                  
                   K110-REPOSSESSED-CODE = '20' OR '40'))                       
                 PERFORM 5000-PRINT-EMAIL  THRU 5000-EXIT                       
              END-IF                                                            
                                                                                
           WHEN  23                                                             
                 DISPLAY 'BM06K110 REC NOT FOUND = ' BM06K110-STATUS            
                         ', KEY = ' K110-KEY-FLD                                
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR READING BM06K110 = ' BM06K110-STATUS            
                         ', KEY  = ' K110-KEY-FLD                               
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       5000-PRINT-EMAIL.                                                        
      *-------------------------------------------------------------            
           MOVE F310-SCH-ACC           TO WS-LINK-REC.                          
           PERFORM 8000-READ-PIDB      THRU 8000-EXIT.                          
                                                                                
           STRING 'B' COMM8-NUM-MGMT(1:2) DELIMITED BY ' '                      
                  'Admin@hdb.gov.sg>' DELIMITED BY SIZE                         
                  INTO MAIL-TOMAILID.                                           
           MOVE MAIL-TOMAILID TO MAIL-TOMAILID1.                                
                                                                                
           EVALUATE F310-CDE-FLAT-TYPE(1:1)                                     
           WHEN '1'                                                             
                MOVE '1 RM' TO MAIL-FT1                                         
                               MAIL-FT2                                         
           WHEN '2'                                                             
                MOVE '2 RM' TO MAIL-FT1                                         
                               MAIL-FT2                                         
           WHEN '3'                                                             
                MOVE '3 RM' TO MAIL-FT1                                         
                               MAIL-FT2                                         
           WHEN '4'                                                             
                MOVE '4 RM' TO MAIL-FT1                                         
                               MAIL-FT2                                         
           WHEN '5'                                                             
                MOVE '5 RM' TO MAIL-FT1                                         
                               MAIL-FT2                                         
           WHEN 'E'                                                             
                MOVE 'EXEC' TO MAIL-FT1                                         
                               MAIL-FT2                                         
           END-EVALUATE.                                                        
                                                                                
           STRING COMM8-NUM-BLK                                                 
                  DELIMITED BY SIZE                                             
                  ' ' COMM8-NME-STREET(1:20)                                    
                  DELIMITED BY '  '                                             
                  ' #' COMM8-NUM-LEVEL                                          
                  '-'  COMM8-NUM-UNIT-MAIN                                      
                  DELIMITED BY '     ' INTO MAIL-ADDR.                          
                                                                                
           STRING K110-DTE-ACCEPTANCE (7:2) '/'                                 
                  K110-DTE-ACCEPTANCE (5:2) '/'                                 
                  K110-DTE-ACCEPTANCE (1:4)                                     
                  DELIMITED BY SIZE  INTO MAIL-DTE-ACCEPTANCE.                  
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL1.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL2B.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL3.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL4.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL5.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL6.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL6B.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL7.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL8.                                    
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY01.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY02.                                  
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY02B.                                 
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY03.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY04.                                  
                                                                                
           PERFORM 6000-PRINT-NAME       THRU 6000-EXIT.                        
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY05.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY06.                                  
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY07.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY08.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY09.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY10.                                  
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-END.                                     
           ADD  1 TO WS-MAIL-WROTE.                                             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       6000-PRINT-NAME.                                                         
      *-------------------------------------------------------------            
           IF K800-NUM-NRIC1 NOT  = SPACES AND LOW-VALUES                       
              MOVE K800-NUM-REGN  TO K820-NUM-REGN                              
              MOVE K800-NUM-NRIC1 TO K820-NUM-NRIC                              
                                     MAIL-NUM-NRIC                              
              PERFORM 7000-READ-BP13K820 THRU 7000-EXIT.                        
                                                                                
           IF K800-NUM-NRIC2 NOT  = SPACES AND LOW-VALUES                       
              MOVE K800-NUM-REGN  TO K820-NUM-REGN                              
              MOVE K800-NUM-NRIC2 TO K820-NUM-NRIC                              
                                     MAIL-NUM-NRIC                              
              PERFORM 7000-READ-BP13K820 THRU 7000-EXIT.                        
                                                                                
           IF K800-NUM-NRIC3 NOT  = SPACES AND LOW-VALUES                       
              MOVE K800-NUM-REGN  TO K820-NUM-REGN                              
              MOVE K800-NUM-NRIC3 TO K820-NUM-NRIC                              
                                     MAIL-NUM-NRIC                              
              PERFORM 7000-READ-BP13K820 THRU 7000-EXIT.                        
                                                                                
           IF K800-NUM-NRIC4 NOT  = SPACES AND LOW-VALUES                       
              MOVE K800-NUM-REGN  TO K820-NUM-REGN                              
              MOVE K800-NUM-NRIC4 TO K820-NUM-NRIC                              
                                     MAIL-NUM-NRIC                              
              PERFORM 7000-READ-BP13K820 THRU 7000-EXIT.                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       7000-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES        TO MAIL-NME-OCCUP                                 
                                 MAIL-CONTACT.                                  
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF BP13K820-STATUS = 00                                              
              MOVE K820-NME-OCCP         TO MAIL-NME-OCCUP                      
              IF K820-NUM-HP-PGR = SPACES                                       
                 MOVE K820-NUM-PHONE        TO MAIL-CONTACT                     
              ELSE                                                              
                 MOVE K820-NUM-HP-PGR       TO MAIL-CONTACT                     
              END-IF                                                            
           ELSE                                                                 
           IF BP13K820-STATUS = 23                                              
              MOVE ' -- NOT FOUND --'    TO MAIL-NME-OCCUP                      
              DISPLAY 'REC NOTFND IN BP13K820 : ' BP13K820-STATUS               
                      ', K820-KEY-FLD : ' K820-KEY-FLD                          
           ELSE                                                                 
              DISPLAY 'ERR READING BP13K820, STAT : ' BP13K820-STATUS           
                      ', K820-KEY-FLD : ' K820-KEY-FLD                          
              MOVE BP13K820-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           ADD 1 TO WS-SNO                                                      
           MOVE WS-SNO TO MAIL-SNO                                              
           WRITE MAIL-PRTREC FROM MAIL-DETAIL.                                  
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8000-READ-PIDB.                                                          
      *-------------------------------------------------------------            
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN +000                                                            
              CONTINUE                                                          
           WHEN +100                                                            
              MOVE SPACES   TO BP13COMM8-REC                                    
              INITIALIZE       BP13COMM8-REC                                    
              DISPLAY 'REC NOT FOUND IN PIDB: ' F310-SCH-ACC                    
           WHEN OTHER                                                           
              MOVE COMM8-CDE-SYSERR      TO WS-SQL-CODE                         
              DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                    
                      WS-SQL-CODE                                               
              MOVE 99                    TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
            DISPLAY ' '.                                                        
            DISPLAY ' - BP13C515 CONTROL TOTALS -----'.                         
            DISPLAY ' '.                                                        
            DISPLAY '  NO BP13F310 READ     = ' WS-F310-READ.                   
            DISPLAY '  NO BP13K800 READ     = ' WS-K800-READ.                   
            DISPLAY '  NO BP13K800 NOT FND  = ' WS-K800-NOTFND.                 
            DISPLAY '  NO BM06K110 NOT FND  = ' WS-K110-READ.                   
            DISPLAY '  NO BM06MAIL WRITTEN  = ' WS-MAIL-WROTE.                  
            DISPLAY ' '.                                                        
                                                                                
            CLOSE BP13F310                                                      
                  BP13K800                                                      
                  BP13K820                                                      
                  BM06K110                                                      
                  BP13MAIL.                                                     
                                                                                
            IF BP13K800-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSE ERROR BP13K800, STAT ' BP13K800-STATUS.           
                                                                                
            IF BP13K820-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSE ERROR BP13K820, STAT ' BP13K820-STATUS.           
                                                                                
            IF BM06K110-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSE ERROR BM06K110, STAT ' BM06K110-STATUS.           
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
