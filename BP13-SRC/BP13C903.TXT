      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C903.                                                 
       AUTHOR.        ROMAR ANDREW S MARALIT.                                   
      *DATE-WRITTEN.  21/06/2000.                                               
                                                                                
      *===============================================================*         
      *              SYSTEM OF COMMITMENT  (BP13)                     *         
      *===============================================================*         
      *                                                               *         
      *   OBJECTIVE   :  1.  A STANDARD ROUTINE TO CALCULATE SC AND   *         
      *                      CC CHARGES.                              *         
      *                  2.  THIS SUB-ROUTINE WILL GET THE SC/CC AND  *         
      *                      SC/CC REBATE FROM AC20C225.              *         
      *                  3.  THIS PROGRAM IS MODELLED AFTER BP13C902  *         
      *                                                               *         
      *   SUB-PROGRAM :  1.  AC20C225                                 *         
      *                                                               *         
      *===============================================================*         
      * CHGE-NO   BY    DATE      DESCRIPTION                         *         
      * --------  ----  ------    -----------                         *         
      * BP130885  RASM  21/06/00  SUB-ROUTINE TO CALL AC20C225 FOR    *         
      *                           SCCC DETAILS.                       *         
      *===============================================================*         
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
      *=======================*                                                 
       WORKING-STORAGE SECTION.                                                 
      *=======================*                                                 
                                                                                
       01  WS-DATE-SO-VARIABLES.                                                
           05  WS-DTE-SO-EFFECTIVE1        PIC X(8)  VALUE SPACES.              
           05  WS-DTE-SO-EFFECTIVE2        PIC X(8)  VALUE SPACES.              
           05  WS-DTE-SO-EFFECTIVE3        PIC X(8)  VALUE SPACES.              
                                                                                
       01  WS-DATE-SO-VARIABLES.                                                
           05  WS-EDITED-SQL-CDE           PIC +ZZZZ VALUE SPACES.              
                                                                                
       01  WS-DTE-CCYYMMDD.                                                     
           05  WS-DTE-YYYYMMDD.                                                 
               10  WS-YYYY                 PIC X(4)  VALUE SPACES.              
               10  WS-MM                   PIC X(2)  VALUE SPACES.              
               10  WS-DD                   PIC X(2)  VALUE SPACES.              
           05  WS-YYYYMMDD-NUM     REDEFINES WS-DTE-YYYYMMDD.                   
               10  WS-CCYY-9               PIC 9(4).                            
               10  WS-MM-9                 PIC 9(2).                            
               10  WS-DD-9                 PIC 9(2).                            
                                                                                
       01  WS-DTE-CUR-EFFSC1.                                                   
           05  WS-CC-CUR-EFFSC1            PIC X(2)  VALUE SPACES.              
           05  WS-YY-CUR-EFFSC1            PIC X(2)  VALUE SPACES.              
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  WS-MM-CUR-EFFSC1            PIC X(2)  VALUE SPACES.              
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  WS-DD-CUR-EFFSC1            PIC X(2)  VALUE SPACES.              
                                                                                
       01  WS-DTE-CUR-EFFSC2.                                                   
           05  WS-CC-CUR-EFFSC2            PIC X(2)  VALUE SPACES.              
           05  WS-YY-CUR-EFFSC2            PIC X(2)  VALUE SPACES.              
           05  WS-MM-CUR-EFFSC2            PIC X(2)  VALUE SPACES.              
           05  WS-DD-CUR-EFFSC2            PIC X(2)  VALUE SPACES.              
                                                                                
       01  WS-DTE-NXT-EFFSC1.                                                   
           05  WS-CC-NXT-EFFSC1            PIC X(2)  VALUE SPACES.              
           05  WS-YY-NXT-EFFSC1            PIC X(2)  VALUE SPACES.              
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  WS-MM-NXT-EFFSC1            PIC X(2)  VALUE SPACES.              
           05  FILLER                      PIC X(1)  VALUE SPACES.              
           05  WS-DD-NXT-EFFSC1            PIC X(2)  VALUE SPACES.              
                                                                                
       01  WS-DTE-NXT-EFFSC2.                                                   
           05  WS-CC-NXT-EFFSC2            PIC X(2)  VALUE SPACES.              
           05  WS-YY-NXT-EFFSC2            PIC X(2)  VALUE SPACES.              
           05  WS-MM-NXT-EFFSC2            PIC X(2)  VALUE SPACES.              
           05  WS-DD-NXT-EFFSC2            PIC X(2)  VALUE SPACES.              
                                                                                
      *----------------------------------------------------------------*        
      *    VARIABLES PASSED TO SUB-PROGRAM "AC20C225" TO GET REBATES   *        
      *----------------------------------------------------------------*        
                                                                                
       01  L-COMM-AREA.                                                         
           05  L-INPUTS.                                                        
               10  LK-CDE-SCHM-ACNT        PIC X(09).                           
               10  LK-DTE-EFF.                                                  
                   15  LK-DTE-EFF-CCYYMM   PIC X(06).                           
                   15  LK-DTE-EFF-DD       PIC X(02).                           
               10  FILLER                  PIC X(13).                           
           05  L-OUTPUTS.                                                       
               10  L-RETURN-CODES.                                              
                   15  LK-RETCODE          PIC X(02).                           
                   15  LK-SQL-CODE         PIC S9(3).                           
                   15  LK-REB-KEY-ERR      PIC 9(02).                           
                   15  FILLER              PIC X(05).                           
               10  L-RETURN-VALUES.                                             
                   15  LK-AMT-SCNXT-TOT    PIC 9(06)V99.                        
                   15  LK-AMT-SCCUR-TOT    PIC 9(06)V99.                        
                   15  LK-AMT-SCPRV-TOT    PIC 9(06)V99.                        
                   15  LK-DTE-EFF-SCNXT    PIC X(10).                           
                   15  LK-DTE-EFF-SCCUR    PIC X(10).                           
                   15  LK-DTE-EFF-SCPRV    PIC X(10).                           
                   15  LK-AMT-SCCC-REB     PIC 9(04)V99.                        
                   15  LK-AMT-SCCC-GRT     PIC 9(04)V99.                        
                   15  LK-AMT-SCCC-REC-TC  PIC 9(04)V99.                        
                   15  LK-AMT-SCCC-REB-SP  PIC 9(04)V99.                        
                   15  FILLER              PIC X(10).                           
                                                                                
                                                                                
      *===============*                                                         
       LINKAGE SECTION.                                                         
      *===============*                                                         
      *----------------------------------------------------------------*        
      *    VARIABLES RECEIVED FROM PROGRAMS CALLING BP13C903           *        
      *----------------------------------------------------------------*        
       01  WS-LINK-AREA.                                                        
           05  LINK-NUM-SCH-ACC            PIC X(9).                            
           05  LINK-SO-EFF-DATE            PIC 9(8).                            
           05  LINK-DESIGN-TY              PIC X(3).                            
           05  LINK-ROOM-TY                PIC X(2).                            
           05  LINK-SC-CC1                 PIC 9(6)V99.                         
           05  LINK-SC-CC2                 PIC 9(6)V99.                         
           05  LINK-SC-CC3                 PIC 9(6)V99.                         
           05  LINK-SC-TOTAL-AMT           PIC 9(7)V99.                         
           05  LINK-SC-REBATE1             PIC 9(4)V99.                         
           05  LINK-SC-REBATE2             PIC 9(4)V99.                         
           05  LINK-SC-REBATE3             PIC 9(4)V99.                         
           05  LINK-SC-GRANT1              PIC 9(4)V99.                         
           05  LINK-SC-GRANT2              PIC 9(4)V99.                         
           05  LINK-SC-GRANT3              PIC 9(4)V99.                         
           05  LINK-SC-REC-TC1             PIC 9(4)V99.                         
           05  LINK-SC-REC-TC2             PIC 9(4)V99.                         
           05  LINK-SC-REC-TC3             PIC 9(4)V99.                         
           05  LINK-SC-REB-SP1             PIC 9(4)V99.                         
           05  LINK-SC-REB-SP2             PIC 9(4)V99.                         
           05  LINK-SC-REB-SP3             PIC 9(4)V99.                         
           05  LINK-SC-CURR-EFF-DATE       PIC X(10).                           
           05  LINK-SC-NEXT-EFF-DATE       PIC X(10).                           
           05  LINK-SC-RET-CODE            PIC X(2).                            
                                                                                
                                                                                
      ****************************************                                  
       PROCEDURE DIVISION  USING WS-LINK-AREA.                                  
      ****************************************                                  
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
      ** OPEN AC20K700                                                          
           CALL  'OPENK700'  USING WS-LINK-AREA                                 
           PERFORM  100-GET-SO-EFF-DTE       THRU  100-EXIT.                    
           PERFORM  200-CALL-AC20C225        THRU  200-EXIT.                    
           PERFORM  400-GET-REBATE-2-3       THRU  400-EXIT.                    
                                                                                
      ** CLOSE AC20K700                                                         
           CALL  'CLOSK700'  USING WS-LINK-AREA.                                
           GOBACK.                                                              
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-GET-SO-EFF-DTE.                                                      
      ******************************************************************        
      *---------------------------------------------------------------*         
      * TO GET THE 3 SO EFFECTIVE DATES BASED FROM CURRENT DATE.      *         
      * ---> 1ST SO EFFECTIVE DATE IS CURRENT DATE                    *         
      *---------------------------------------------------------------*         
           MOVE LINK-SO-EFF-DATE         TO WS-DTE-SO-EFFECTIVE1.               
           MOVE LINK-SO-EFF-DATE         TO WS-DTE-CCYYMMDD.                    
           DISPLAY 'SO-EFF-DATE: '          LINK-SO-EFF-DATE.                   
                                                                                
      *---------------------------------------------------------------*         
      * ---> 2ND SO EFFECTIVE DATE IS 1ST SO EFFECTIVE DATE + 1 MONTH *         
      *---------------------------------------------------------------*         
           ADD 1                         TO WS-MM-9.                            
                                                                                
           IF WS-MM-9 > 12                                                      
              SUBTRACT 12              FROM WS-MM-9                             
              ADD 1                      TO WS-CCYY-9                           
           END-IF.                                                              
                                                                                
           MOVE WS-DTE-CCYYMMDD          TO WS-DTE-SO-EFFECTIVE2.               
           DISPLAY '+ 1 MONTH  : '          WS-DTE-CCYYMMDD.                    
                                                                                
      *---------------------------------------------------------------*         
      * ---> 3RD SO EFFECTIVE DATE IS 2ND SO EFFECTIVE DATE + 1 MONTH *         
      *---------------------------------------------------------------*         
           ADD 1                         TO WS-MM-9.                            
                                                                                
           IF WS-MM-9 > 12                                                      
              SUBTRACT 12              FROM WS-MM-9                             
              ADD 1                      TO WS-CCYY-9                           
           END-IF.                                                              
                                                                                
           MOVE WS-DTE-CCYYMMDD          TO WS-DTE-SO-EFFECTIVE3.               
           DISPLAY '+ 2 MONTH  : '          WS-DTE-CCYYMMDD.                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-CALL-AC20C225.                                                       
      ******************************************************************        
      *---------------------------------------------------------------*         
      * ---> CALL AC20C225 TO GET THE SCCC AMOUNTS FOR THE 3 MONTHS   *         
      *      IF NOT FOUND IN SCCC TABLE MOVE ZEROES                   *         
      *---------------------------------------------------------------*         
           MOVE ZEROES                   TO LINK-SC-CC1                         
                                            LINK-SC-CC2                         
                                            LINK-SC-CC3                         
                                            LINK-SC-REBATE1                     
                                            LINK-SC-REBATE2                     
                                            LINK-SC-REBATE3                     
                                            LINK-SC-GRANT1                      
                                            LINK-SC-GRANT2                      
                                            LINK-SC-GRANT3                      
                                            LINK-SC-REC-TC1                     
                                            LINK-SC-REC-TC2                     
                                            LINK-SC-REC-TC3                     
                                            LINK-SC-REB-SP1                     
                                            LINK-SC-REB-SP2                     
                                            LINK-SC-REB-SP3                     
                                            LINK-SC-CURR-EFF-DATE               
                                            LINK-SC-NEXT-EFF-DATE.              
                                                                                
           MOVE LINK-NUM-SCH-ACC         TO LK-CDE-SCHM-ACNT.                   
           MOVE WS-DTE-SO-EFFECTIVE1     TO LK-DTE-EFF.                         
           DISPLAY ' EFF-DTE1     '         LK-DTE-EFF.                         
                                                                                
           CALL 'AC20C225' USING L-COMM-AREA.                                   
                                                                                
           DISPLAY 'LK-RETCODE : '          LK-RETCODE.                         
           MOVE LK-SQL-CODE              TO WS-EDITED-SQL-CDE.                  
           DISPLAY 'LK-SQL-CDE : '          WS-EDITED-SQL-CDE.                  
                                                                                
           IF LK-RETCODE = '00'                                                 
              PERFORM 300-GET-SCCC     THRU 300-EXIT                            
              MOVE LK-DTE-EFF-SCCUR      TO LINK-SC-CURR-EFF-DATE               
              MOVE LK-DTE-EFF-SCNXT      TO LINK-SC-NEXT-EFF-DATE               
           ELSE                                                                 
              MOVE ZEROES                TO LINK-SC-CC1                         
                                            LINK-SC-CC2                         
                                            LINK-SC-CC3                         
                                            LINK-SC-REBATE1                     
                                            LINK-SC-REBATE2                     
                                            LINK-SC-REBATE3                     
                                            LINK-SC-GRANT1                      
                                            LINK-SC-GRANT2                      
                                            LINK-SC-GRANT3                      
                                            LINK-SC-REC-TC1                     
                                            LINK-SC-REC-TC2                     
                                            LINK-SC-REC-TC3                     
                                            LINK-SC-REB-SP1                     
                                            LINK-SC-REB-SP2                     
                                            LINK-SC-REB-SP3                     
                                            LINK-SC-CURR-EFF-DATE               
                                            LINK-SC-NEXT-EFF-DATE               
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-GET-SCCC.                                                            
      ******************************************************************        
      *--------------------------------------------------------------*          
      * ---> GET SC-CC FOR THE 3 MONTHS                              *          
      * ---> GET REBATE, GRANT, SCCC-RECOVER AND SPECIAL REBATE FOR  *          
      *      THE FIRST MONTH.                                        *          
      *--------------------------------------------------------------*          
           MOVE LK-DTE-EFF-SCCUR         TO WS-DTE-CUR-EFFSC1.                  
                                                                                
           MOVE WS-CC-CUR-EFFSC1         TO WS-CC-CUR-EFFSC2.                   
           MOVE WS-YY-CUR-EFFSC1         TO WS-YY-CUR-EFFSC2.                   
           MOVE WS-MM-CUR-EFFSC1         TO WS-MM-CUR-EFFSC2.                   
           MOVE WS-DD-CUR-EFFSC1         TO WS-DD-CUR-EFFSC2.                   
                                                                                
      *---------------------------------------------------------------*         
      * ---> CHECK 1ST SO EFFECTIVE DATE >= CURR SCCC EFFECTIVE DATE  *         
      *      IF YES, MOVE CURRENT SCCC VALUES.                        *         
      *---------------------------------------------------------------*         
           IF WS-DTE-SO-EFFECTIVE1 >= WS-DTE-CUR-EFFSC2                         
              MOVE LK-AMT-SCCUR-TOT      TO LINK-SC-CC1                         
              MOVE LK-AMT-SCCC-REB       TO LINK-SC-REBATE1                     
              MOVE LK-AMT-SCCC-GRT       TO LINK-SC-GRANT1                      
              MOVE LK-AMT-SCCC-REC-TC    TO LINK-SC-REC-TC1                     
              MOVE LK-AMT-SCCC-REB-SP    TO LINK-SC-REB-SP1                     
           ELSE                                                                 
              MOVE ZEROES                TO LINK-SC-CC1                         
                                            LINK-SC-REBATE1                     
                                            LINK-SC-GRANT1                      
                                            LINK-SC-REC-TC1                     
                                            LINK-SC-REB-SP1                     
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------*         
      * ---> CHECK WHETHER THERE IS A SCCC NEXT EFFECTIVE DATE        *         
      *---------------------------------------------------------------*         
           MOVE LK-DTE-EFF-SCNXT         TO WS-DTE-NXT-EFFSC1.                  
                                                                                
           MOVE WS-CC-NXT-EFFSC1         TO WS-CC-NXT-EFFSC2.                   
           MOVE WS-YY-NXT-EFFSC1         TO WS-YY-NXT-EFFSC2.                   
           MOVE WS-MM-NXT-EFFSC1         TO WS-MM-NXT-EFFSC2.                   
           MOVE WS-DD-NXT-EFFSC1         TO WS-DD-NXT-EFFSC2.                   
                                                                                
      *---------------------------------------------------------------*         
      * ---> IF SCCC NEXT EFFECTIVE DATE = '0001-01-01', MEANS NO     *         
      *      NEXT SCCC AMT, MOVE CURR SCCC AMT TO SUBSEQUENT MONTHS   *         
      *      ELSE CHECK FURTHER                                       *         
      *---------------------------------------------------------------*         
           IF WS-DTE-NXT-EFFSC2 = ZEROES OR '00010101' OR SPACES OR             
                                  LOW-VALUES                                    
              MOVE LK-AMT-SCCUR-TOT      TO LINK-SC-CC2                         
                                            LINK-SC-CC3                         
           ELSE                                                                 
      *---------------------------------------------------------------*         
      * ---> SCCC NEXT EFFECTIVE DATE EXISTS                          *         
      *      IF 2ND MONTH IS GREATER THAN NEXT EFFECTIVE DATE,        *         
      *      MOVE THE NEXT SCCC AMT TO 2ND MONTH SCCC                 *         
      *      ELSE                                                     *         
      *      MOVE THE CUR  SCCC AMT TO 2ND MONTH SCCC                 *         
      *---------------------------------------------------------------*         
              IF WS-DTE-SO-EFFECTIVE2 >= WS-DTE-NXT-EFFSC2                      
                 MOVE LK-AMT-SCNXT-TOT   TO LINK-SC-CC2                         
              ELSE                                                              
                 MOVE LK-AMT-SCCUR-TOT   TO LINK-SC-CC2                         
              END-IF                                                            
                                                                                
      *---------------------------------------------------------------*         
      * ---> SCCC NEXT EFFECTIVE DATE EXISTS                          *         
      *      IF 3RD MONTH IS GREATER THAN NEXT EFFECTIVE DATE,        *         
      *      MOVE THE NEXT SCCC AMT TO 3RD MONTH SCCC                 *         
      *      ELSE                                                     *         
      *      MOVE THE CUR  SCCC AMT TO 3RD MONTH SCCC                 *         
      *---------------------------------------------------------------*         
              IF WS-DTE-SO-EFFECTIVE3 >= WS-DTE-NXT-EFFSC2                      
                 MOVE LK-AMT-SCNXT-TOT   TO LINK-SC-CC3                         
              ELSE                                                              
                 MOVE LK-AMT-SCCUR-TOT   TO LINK-SC-CC3                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       400-GET-REBATE-2-3.                                                      
      ******************************************************************        
      *---------------------------------------------------------------*         
      * ---> CALL AC20B225 TO GET THE REBATE, GRANT, SCCC-RECOVER AND *         
      *      SPECIAL REBATE FROM AC20K700 FOR 2ND MONTH.              *         
      *---------------------------------------------------------------*         
           MOVE LINK-NUM-SCH-ACC         TO LK-CDE-SCHM-ACNT.                   
           MOVE WS-DTE-SO-EFFECTIVE2     TO LK-DTE-EFF.                         
                                                                                
           CALL 'AC20C225' USING L-COMM-AREA.                                   
                                                                                
           DISPLAY 'LK-RETCODE : '          LK-RETCODE.                         
           MOVE LK-SQL-CODE              TO WS-EDITED-SQL-CDE.                  
           DISPLAY 'LK-SQL-CDE : '          WS-EDITED-SQL-CDE.                  
                                                                                
           IF LK-RETCODE = '00'                                                 
              MOVE LK-AMT-SCCC-REB       TO LINK-SC-REBATE2                     
              MOVE LK-AMT-SCCC-GRT       TO LINK-SC-GRANT2                      
              MOVE LK-AMT-SCCC-REC-TC    TO LINK-SC-REC-TC2                     
              MOVE LK-AMT-SCCC-REB-SP    TO LINK-SC-REB-SP2                     
           ELSE                                                                 
              MOVE ZEROES                TO LINK-SC-REBATE2                     
                                            LINK-SC-GRANT2                      
                                            LINK-SC-REC-TC2                     
                                            LINK-SC-REB-SP2                     
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------*         
      * ---> CALL AC20B225 TO GET THE REBATE, GRANT, SCCC-RECOVER AND *         
      *      SPECIAL REBATE FROM AC20K700 FOR 3RD MONTH.              *         
      *---------------------------------------------------------------*         
           MOVE LINK-NUM-SCH-ACC         TO LK-CDE-SCHM-ACNT.                   
           MOVE WS-DTE-SO-EFFECTIVE3     TO LK-DTE-EFF.                         
                                                                                
           CALL 'AC20C225' USING L-COMM-AREA.                                   
                                                                                
           DISPLAY 'LK-RETCODE : '          LK-RETCODE.                         
           MOVE LK-SQL-CODE              TO WS-EDITED-SQL-CDE.                  
           DISPLAY 'LK-SQL-CDE : '          WS-EDITED-SQL-CDE.                  
                                                                                
           IF LK-RETCODE = '00'                                                 
              MOVE LK-AMT-SCCC-REB       TO LINK-SC-REBATE3                     
              MOVE LK-AMT-SCCC-GRT       TO LINK-SC-GRANT3                      
              MOVE LK-AMT-SCCC-REC-TC    TO LINK-SC-REC-TC3                     
              MOVE LK-AMT-SCCC-REB-SP    TO LINK-SC-REB-SP3                     
           ELSE                                                                 
              MOVE ZEROES                TO LINK-SC-REBATE3                     
                                            LINK-SC-GRANT3                      
                                            LINK-SC-REC-TC3                     
                                            LINK-SC-REB-SP3                     
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
