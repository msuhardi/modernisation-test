      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C65E.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  AUGUST 06, 2014.                                          
      *===============================================================*         
      *   OBJECTIVE:  TO PRINT REPORT FOR SERS W/ PREMIUM CASES       *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F410                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. P13F410A                                  *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP135325 06/08/2014 PCL4 NEW PROGRAM.                         *         
      * BP135374 24/10/2014 PCL4 TO CATER FOR CSM EMAIL ALERT         *         
      * BP135782 09/09/2015 RJB1 TO INITIALIZE WS-SCH-FND TO FIX ERROR*         
      *                          IN THE REPORT                        *         
      * BP136696 21/03/2017 MRN1 READ GRANT_ASMNT TABLE               *         
      *                          TO REPLACE KD07/K800 GRANT FIELDS    *         
      *                          READ EX TABLE TO REPLACE K893 FIELDS *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
           SELECT  P13F410A  ASSIGN        TO P13F410A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K410.                                                           
                                                                                
                                                                                
       FD  P13F410A                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F410A-REC                  PIC X(2000).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-F410-EOF             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-CURR-YY              PIC 9(4)  VALUE ZEROES.                  
           05  WS-CURR-MM              PIC 9(2)  VALUE ZEROES.                  
           05  WS-CURR-DD              PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-PREV-DATE.                                                        
           05  WS-PREV-MM              PIC X(3)  VALUE SPACES.                  
           05  FILLER                  PIC X(1)  VALUE ZEROES.                  
           05  WS-PREV-YY              PIC X(4)  VALUE SPACES.                  
                                                                                
       01  WS-GRANT-CODE               PIC X(1)    VALUE SPACES.                
       01  WS-END-CURSOR1              PIC X(01) VALUE 'N'.                     
       01  WS-END-CURSOR2              PIC X(01) VALUE 'N'.                     
       01  WS-SQLCODE                  PIC ---9.                                
       01  WS-RETURN-CODE              PIC 9(02) VALUE ZEROES.                  
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13C913.               *                 
      *-------------------------------------------------------*                 
       COPY SERSCH.                                                             
                                                                                
       01 WS-SERS-VARIABLES.                                                    
           05 WS-ALLOC-SCH          PIC X(03).                                  
           05 WS-SCH-FND            PIC X(01).                                  
                                                                                
       01  WS-SQL-STATUS.                                                       
           05 WS-SQL-OK                PIC S9(03) VALUE +000.                   
           05 WS-SQL-NFND              PIC S9(03) VALUE +100.                   
           05 WS-SQL-DUPREC            PIC S9(03) VALUE -803.                   
                                                                                
      *--------------------------------------------------------------*          
      *                          SQLCA                               *          
      *--------------------------------------------------------------*          
           EXEC SQL INCLUDE SQLCA          END-EXEC.                            
           EXEC SQL INCLUDE P13VGASM       END-EXEC.                            
           EXEC SQL INCLUDE P13VGXAS       END-EXEC.                            
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE            THRU 1000-EXIT.                 
           PERFORM 2000-READ-BP13F410           THRU 2000-EXIT.                 
                                                                                
           PERFORM 3000-PROCESS-RECORDS         THRU 3000-EXIT                  
             UNTIL WS-F410-EOF = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE           THRU 9999-EXIT.                 
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F410                                                 
               OUTPUT  P13F410A.                                                
                                                                                
           MOVE SPACES                        TO WS-PREV-DATE.                  
           MOVE FUNCTION CURRENT-DATE(1:8)    TO WS-DATE.                       
                                                                                
           IF WS-CURR-MM = 01                                                   
              MOVE 12                         TO WS-CURR-MM                     
              SUBTRACT 1                    FROM WS-CURR-YY                     
           ELSE                                                                 
              SUBTRACT 1                    FROM WS-CURR-MM                     
           END-IF.                                                              
                                                                                
           MOVE WS-CURR-YY                    TO WS-PREV-YY.                    
                                                                                
           EVALUATE WS-CURR-MM                                                  
               WHEN 01                                                          
                    MOVE 'JAN'                TO WS-PREV-MM                     
               WHEN 02                                                          
                    MOVE 'FEB'                TO WS-PREV-MM                     
               WHEN 03                                                          
                    MOVE 'MAR'                TO WS-PREV-MM                     
               WHEN 04                                                          
                    MOVE 'APR'                TO WS-PREV-MM                     
               WHEN 05                                                          
                    MOVE 'MAY'                TO WS-PREV-MM                     
               WHEN 06                                                          
                    MOVE 'JUN'                TO WS-PREV-MM                     
               WHEN 07                                                          
                    MOVE 'JUL'                TO WS-PREV-MM                     
               WHEN 08                                                          
                    MOVE 'AUG'                TO WS-PREV-MM                     
               WHEN 09                                                          
                    MOVE 'SEP'                TO WS-PREV-MM                     
               WHEN 10                                                          
                    MOVE 'OCT'                TO WS-PREV-MM                     
               WHEN 11                                                          
                    MOVE 'NOV'                TO WS-PREV-MM                     
               WHEN 12                                                          
                    MOVE 'DEC'                TO WS-PREV-MM                     
           END-EVALUATE.                                                        
                                                                                
           DISPLAY WS-PREV-DATE.                                                
           DISPLAY WS-DATE.                                                     
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT END                                       
                MOVE 'Y'           TO WS-F410-EOF                               
                GO                 TO 2000-EXIT.                                
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF K410-DTE-KEY-ISSUE(1:6) = WS-DATE(1:6)                            
              PERFORM 3100-CHECK-SERS-CASE           THRU 3100-EXIT             
                                                                                
              IF WS-SCH-FND = 'Y'                                               
                 MOVE 'N'                            TO WS-GRANT-CODE           
                 PERFORM 3200-CHECK-GRANT-ASMNT      THRU 3200-EXIT             
                                                                                
                 IF WS-GRANT-CODE = 'N'                                         
                    PERFORM 3300-CHECK-EX-GRANT      THRU 3300-EXIT             
                 END-IF                                                         
                                                                                
                 PERFORM 4000-WRITE-P13F410A         THRU 4000-EXIT             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410                THRU 2000-EXIT.            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-CHECK-SERS-CASE.                                                    
      ******************************************************************        
                                                                                
            MOVE K410-CDE-ALLOC-SCH  TO  WS-ALLOC-SCH.                          
            MOVE SPACES              TO  WS-SCH-FND.                            
                                                                                
      *-----------------------------------*                                     
      * SEARCH SCHEME IN ALLOC SCH TABLE  *                                     
      *-----------------------------------*                                     
           IF WS-ALLOC-SCH  NOT =  LOW-VALUES AND SPACES                        
               SET WS-SERS-PTR  TO 1                                            
               SEARCH WS-SERS-SCH                                               
                   AT END                                                       
                   MOVE 'N' TO WS-SCH-FND                                       
                   WHEN WS-SERS-SCHEME(WS-SERS-PTR) = WS-ALLOC-SCH              
                   MOVE 'Y' TO WS-SCH-FND                                       
               END-SEARCH                                                       
           ELSE                                                                 
               MOVE 'N' TO WS-SCH-FND                                           
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-CHECK-GRANT-ASMNT.                                                  
      ******************************************************************        
                                                                                
           MOVE K410-NUM-REGN     TO NUM-RGSTRN OF DCLGRANT-ASMNT.              
           MOVE 'N'               TO WS-GRANT-CODE.                             
                                                                                
           PERFORM 3210-OPEN-CURSOR1  THRU 3210-EXIT.                           
           MOVE 'N'                   TO WS-END-CURSOR1.                        
           PERFORM 3220-FETCH-CURSOR1 THRU 3220-EXIT                            
                   UNTIL WS-END-CURSOR1 = 'Y'                                   
           PERFORM 3230-CLOSE-CURSOR1 THRU 3230-EXIT.                           
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3210-OPEN-CURSOR1.                                                       
      *================================================================*        
           INITIALIZE SQLCODE.                                                  
           MOVE 'N'              TO WS-END-CURSOR1.                             
                                                                                
           EXEC SQL                                                             
              DECLARE CURSOR1 CURSOR FOR                                        
               SELECT NUM_RGSTRN,                                               
                      NUM_GRANT_TYPE,                                           
                      NUM_GRANT_STATUS                                          
                FROM  GRANT_ASMNT                                               
               WHERE                                                            
                 NUM_RGSTRN = :DCLGRANT-ASMNT.NUM-RGSTRN                        
           END-EXEC.                                                            
                                                                                
      *----------------------------------------*                                
      *  OPEN CURSOR1 - GRANT_ASMNT                                             
      *----------------------------------------*                                
           EXEC SQL                                                             
             OPEN CURSOR1                                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 0                                                               
             CONTINUE                                                           
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQLCODE                              
             DISPLAY 'OPEN CURSOR1 ERROR'                                       
             DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE                           
             GO TO 9998-SQL-ERROR                                               
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3220-FETCH-CURSOR1.                                                      
      *================================================================*        
                                                                                
           EXEC SQL                                                             
                FETCH CURSOR1                                                   
                INTO  :DCLGRANT-ASMNT.NUM-RGSTRN,                               
                      :DCLGRANT-ASMNT.NUM-GRANT-TYPE,                           
                      :DCLGRANT-ASMNT.NUM-GRANT-STATUS                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-OK                                                       
                IF NUM-GRANT-TYPE OF DCLGRANT-ASMNT                             
                                         = 'AHG' OR 'SHG' OR 'SUG'              
                   IF NUM-GRANT-STATUS OF DCLGRANT-ASMNT = 'A'                  
                      MOVE 'Y' TO WS-GRANT-CODE                                 
                   END-IF                                                       
                END-IF                                                          
           WHEN WS-SQL-NFND                                                     
             MOVE 'Y'                  TO WS-END-CURSOR1                        
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQLCODE                              
             DISPLAY 'FETCH CURSOR1 ERROR'                                      
             DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE                           
             GO TO 9998-SQL-ERROR                                               
           END-EVALUATE.                                                        
                                                                                
       3220-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       3230-CLOSE-CURSOR1.                                                      
      *---------------------------------------------------------------*         
           INITIALIZE SQLCODE.                                                  
                                                                                
           EXEC SQL CLOSE CURSOR1                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN 0                                                            
                   CONTINUE                                                     
              WHEN OTHER                                                        
                MOVE SQLCODE TO WS-SQLCODE                                      
                DISPLAY 'CLOSE CURSOR1 ERROR'                                   
                DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE                        
                GO TO 9998-SQL-ERROR                                            
           END-EVALUATE.                                                        
                                                                                
       3230-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-CHECK-EX-GRANT.                                                     
      ******************************************************************        
                                                                                
           MOVE K410-NUM-REGN     TO NUM-RGSTRN OF DCLEX-GRANT-ASMNT.           
           MOVE 'N'               TO WS-GRANT-CODE.                             
                                                                                
           PERFORM 3310-OPEN-CURSOR2  THRU 3310-EXIT.                           
           MOVE 'N'                   TO WS-END-CURSOR2.                        
           PERFORM 3320-FETCH-CURSOR2 THRU 3320-EXIT                            
                   UNTIL WS-END-CURSOR2 = 'Y'                                   
           PERFORM 3330-CLOSE-CURSOR2 THRU 3330-EXIT.                           
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3310-OPEN-CURSOR2.                                                       
      *================================================================*        
           INITIALIZE SQLCODE.                                                  
           MOVE 'N'              TO WS-END-CURSOR2.                             
                                                                                
           EXEC SQL                                                             
              DECLARE CURSOR2 CURSOR FOR                                        
               SELECT NUM_RGSTRN,                                               
                      NUM_GRANT_TYPE,                                           
                      NUM_GRANT_STATUS                                          
                FROM  EX_GRANT_ASMNT                                            
               WHERE                                                            
                 NUM_RGSTRN = :DCLEX-GRANT-ASMNT.NUM-RGSTRN                     
           END-EXEC.                                                            
                                                                                
      *----------------------------------------*                                
      *  OPEN CURSOR2 - EX_GRANT_ASMNT                                          
      *----------------------------------------*                                
           EXEC SQL                                                             
             OPEN CURSOR2                                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 0                                                               
             CONTINUE                                                           
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQLCODE                              
             DISPLAY 'OPEN CURSOR2 ERROR'                                       
             DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE                           
             GO TO 9998-SQL-ERROR                                               
           END-EVALUATE.                                                        
                                                                                
       3310-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3320-FETCH-CURSOR2.                                                      
      *================================================================*        
                                                                                
           EXEC SQL                                                             
                FETCH CURSOR2                                                   
                INTO  :DCLEX-GRANT-ASMNT.NUM-RGSTRN,                            
                      :DCLEX-GRANT-ASMNT.NUM-GRANT-TYPE,                        
                      :DCLEX-GRANT-ASMNT.NUM-GRANT-STATUS                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-OK                                                       
                IF NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT                          
                                         = 'AHG' OR 'SHG' OR 'SUG'              
                   IF NUM-GRANT-STATUS OF DCLEX-GRANT-ASMNT = 'A'               
                      MOVE 'Y' TO WS-GRANT-CODE                                 
                   END-IF                                                       
                END-IF                                                          
           WHEN WS-SQL-NFND                                                     
                MOVE 'Y'                  TO WS-END-CURSOR2                     
           WHEN OTHER                                                           
                MOVE SQLCODE            TO WS-SQLCODE                           
                DISPLAY 'FETCH CURSOR2 ERROR'                                   
                DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE                        
                GO TO 9998-SQL-ERROR                                            
           END-EVALUATE.                                                        
                                                                                
       3320-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       3330-CLOSE-CURSOR2.                                                      
      *---------------------------------------------------------------*         
           INITIALIZE SQLCODE.                                                  
                                                                                
           EXEC SQL CLOSE CURSOR2                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN 0                                                            
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   MOVE SQLCODE TO WS-SQLCODE                                   
                   DISPLAY 'CLOSE CURSOR2 ERROR'                                
                   DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE                     
                   GO TO 9998-SQL-ERROR                                         
           END-EVALUATE.                                                        
                                                                                
       3330-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-WRITE-P13F410A.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                        TO P13F410A-REC.                  
           INITIALIZE                            P13F410A-REC.                  
                                                                                
           MOVE WS-GRANT-CODE                 TO BP13K410-REC(1168:1)           
                                                                                
           MOVE WS-PREV-DATE                  TO BP13K410-REC(1993:8).          
           WRITE P13F410A-REC               FROM BP13K410-REC.                  
           ADD 1                              TO WS-CNT-WRITE.                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------         
       9998-SQL-ERROR.                                                          
      *----------------------------------------------------------------         
           CALL 'DBATIAR'  USING SQLCA.                                         
           EXEC SQL ROLLBACK               END-EXEC.                            
           MOVE 99                         TO    WS-RETURN-CODE.                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       9998-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           IF WS-CNT-WRITE = ZEROES                                             
              MOVE SPACES           TO P13F410A-REC                             
              INITIALIZE               P13F410A-REC                             
                                                                                
              MOVE 'N'              TO P13F410A-REC(1993:1)                     
              WRITE P13F410A-REC                                                
           END-IF.                                                              
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C65E'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1)  NO OF BP13F410 RECORDS READ............. : '           
                    WS-F410-READ.                                               
           DISPLAY '(2)  NO OF P13F410A RECORDS WRITTEN  ..... : '              
                    WS-CNT-WRITE.                                               
                                                                                
           CLOSE    BP13F410                                                    
                    P13F410A.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
