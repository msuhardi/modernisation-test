       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF94.                                                 
      *AUTHOR.        RALSTON JAMES ELIEZER B MEDRANO                           
      *DATE-WRITTEN.  22/03/2012.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO READ BP13F730 TO GET BTO APPLICATION DATA       *        
      *           : TO READ BP13K825 TO GET AGE/INCOME/HH-SIZE         *        
      *                                                                *        
      * INPUT  FILES : BP13F730                                        *        
      *                BP13K825                                        *        
      *                BP13K828                                        *        
      *                BP13K813                                        *        
      * OUTPUT FILES : P13FOUT1                                        *        
      *                P13FOUT2                                        *        
      *                                                                *        
      * CHG-NO   BY    ON    - DESCRIPTION                             *        
      * -------------------------------------------------------------- *        
      * BP134482 RJE1 22/03/12 NEW PROGRAM                             *        
      * BP134504 RJE1 03/04/12 UPDATED CHANGE REQUEST NUMBER           *        
      * BP135190 SMR2 04/12/13 EXPAND BP13K813 TO 1000                 *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   BP13F730      ASSIGN TO BP13F730.                           
                                                                                
           SELECT   BP13K825      ASSIGN TO BP13K825                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K825-KEY-FLD                        
                             FILE STATUS IS K825-STATUS.                        
                                                                                
           SELECT   BP13K828      ASSIGN TO BP13K828                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K828-KEY-FLD                        
                             FILE STATUS IS K828-STATUS.                        
                                                                                
           SELECT  BP13K813  ASSIGN        TO BP13K813                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K813-KEY-FLD                      
                             FILE STATUS   IS WS-K813-STATUS.                   
                                                                                
           SELECT   P13FOUT1      ASSIGN TO P13FOUT1.                           
           SELECT   P13FOUT2      ASSIGN TO P13FOUT2.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K828                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K828.                                                           
                                                                                
       FD   P13FOUT1                                                            
            RECORDING MODE IS F.                                                
       01   P13FOUT1-REC                    PIC X(300).                         
                                                                                
       FD   P13FOUT2                                                            
            RECORDING MODE IS F.                                                
       01   P13FOUT2-REC                    PIC X(300).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F730-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-K825-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-K828-READ                PIC 9(7)  VALUE ZEROS.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  K825-STATUS                  PIC 99    VALUE ZEROES.             
           05  K828-STATUS                  PIC 99    VALUE ZEROES.             
           05  WS-K813-STATUS               PIC 99    VALUE ZEROES.             
           05  WS-K825-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-K828-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-F730-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-K825-FOUND                PIC X(1)  VALUE 'N'.                
           05  WS-K825-FND                  PIC X(01) VALUE SPACE.              
               88  K825-FND                 VALUE 'Y'.                          
           05  WS-K828-FND                  PIC X(01) VALUE SPACE.              
               88  K828-FND                 VALUE 'Y'.                          
           05  WS-NUM-AGE                   PIC 9(08) VALUE ZEROES.             
           05  WS-AGE REDEFINES WS-NUM-AGE.                                     
               10  WS-AGE-CY                PIC 9(04).                          
               10  WS-AGE-MM                PIC 9(02).                          
               10  WS-AGE-DD                PIC 9(02).                          
           05  WS-HA1-MS                    PIC X(01) VALUE SPACE.              
           05  WS-HA1-SEX                   PIC X(01) VALUE ZEROES.             
           05  WS-HA1-BIRTH                 PIC X(08) VALUE ZEROES.             
           05  WS-HA1-CTZN                  PIC X(02) VALUE ZEROES.             
           05  WS-NUM-REGN                  PIC 9(08) VALUE ZEROES.             
           05  WS-NUM-BIRTH                 PIC 9(08) VALUE ZEROES.             
           05  WS-CTR                       PIC 9(02) VALUE ZEROES.             
           05  WS-MATURE                    PIC X(1)  VALUE SPACES.             
           05  WS-HH-SCP                    PIC X(3)  VALUE SPACES.             
           05  WS-HH-SCH                    PIC X(3)  VALUE SPACES.             
           05  WS-TOT-INCOME                PIC 9(5)  VALUE ZEROES.             
           05  WS-SNO1                      PIC 9(5)  VALUE ZEROES.             
           05  WS-SNO2                      PIC 9(5)  VALUE ZEROES.             
           05  WS-PRT-CTR1                  PIC 9(05) VALUE ZEROES.             
           05  WS-PRT-CTR2                  PIC 9(05) VALUE ZEROES.             
           05  WS-CDE-NT1                   PIC X(3)  VALUE SPACES.             
           05  WS-NUM-HA1-AGE               PIC 9(4)  VALUE ZEROES.             
                                                                                
      *---------------------------------------------------------------*         
      *        P R I N T     L A Y O U T   P 1 3 F O U T 1            *         
      *---------------------------------------------------------------*         
                                                                                
       01  OUT1-HEADING1.                                                       
           05  FILLER                       PIC X(150) VALUE                    
           '   S/N REGN     NRIC1     MATURE-EST NT  BTO PROJ NME               
      -    '                                FLAT-DESIGN FT HH HH-INCOME         
      -    'APPS MCPS MGPS HA1-AGE'.                                            
       01  OUT1-DATA.                                                           
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT1-SNO                     PIC ZZZZ9  VALUE ZEROES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT1-REGN                    PIC X(8)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT1-NRIC                    PIC X(9)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT1-MATURE                  PIC X(1)   VALUE SPACES.            
           05  FILLER                       PIC X(10)  VALUE SPACES.            
           05  OUT1-NT                      PIC X(3)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT1-BTO-PROJ                PIC X(50)  VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT1-FT-DSN                  PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACE.             
           05  OUT1-FT                      PIC X(2)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT1-HH                      PIC X(1)   VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  OUT1-INCOME                  PIC ZZZZ9  VALUE ZEROES.            
           05  FILLER                       PIC X(5)   VALUE SPACES.            
           05  OUT1-APPS                    PIC X(1)   VALUE SPACES.            
           05  FILLER                       PIC X(4)   VALUE SPACES.            
           05  OUT1-MCPS                    PIC X(1)   VALUE SPACES.            
           05  FILLER                       PIC X(4)   VALUE SPACES.            
           05  OUT1-MGPS                    PIC X(1)   VALUE SPACES.            
           05  FILLER                       PIC X(4)   VALUE SPACES.            
           05  OUT1-AGE                     PIC ZZZ9   VALUE ZEROES.            
                                                                                
      *---------------------------------------------------------------*         
      *        P R I N T     L A Y O U T   P 1 3 F O U T 2            *         
      *---------------------------------------------------------------*         
       01  OUT2-HEADING1.                                                       
           05  FILLER                       PIC X(150) VALUE                    
           '   S/N REGN     NRIC1     MATURE-EST NT  FT HH HH-INCOME APP        
      -    'S MCPS HA1-AGE'.                                                    
       01  OUT2-DATA.                                                           
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT2-SNO                     PIC ZZZZ9  VALUE ZEROES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT2-REGN                    PIC X(8)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT2-NRIC                    PIC X(9)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT2-MATURE                  PIC X(1)   VALUE SPACES.            
           05  FILLER                       PIC X(10)  VALUE SPACES.            
           05  OUT2-NT                      PIC X(3)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT2-FT                      PIC X(2)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  OUT2-HH                      PIC X(1)   VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  OUT2-INCOME                  PIC ZZZZ9  VALUE ZEROES.            
           05  FILLER                       PIC X(5)   VALUE SPACES.            
           05  OUT2-APPS                    PIC X(1)   VALUE SPACES.            
           05  FILLER                       PIC X(4)   VALUE SPACES.            
           05  OUT2-MCPS                    PIC X(1)   VALUE SPACES.            
           05  FILLER                       PIC X(4)   VALUE SPACES.            
           05  OUT2-AGE                     PIC ZZZ9   VALUE ZEROES.            
                                                                                
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      *===============================================================*         
       0000-MAIN.                                                               
      *===============================================================*         
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL WS-F730-EOF = 'Y'.                                           
                                                                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       1000-OPEN-FILES.                                                         
      *===============================================================*         
                                                                                
           OPEN INPUT BP13F730                                                  
                      BP13K825                                                  
                      BP13K828                                                  
                      BP13K813                                                  
               OUTPUT P13FOUT1                                                  
                      P13FOUT2.                                                 
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K813 , STATUS = '                      
                   WS-K813-STATUS                                               
              MOVE WS-K813-STATUS            TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF K825-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K825 , STATUS = ' K825-STATUS          
              MOVE K825-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF K828-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K828 , STATUS = ' K828-STATUS          
              MOVE K825-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           INITIALIZE                                WS-PRT-CTR1                
                                                     WS-PRT-CTR2.               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       2000-READ-BP13F730.                                                      
      *===============================================================*         
                                                                                
           READ BP13F730                                                        
             AT END                                                             
                MOVE 'Y'                TO WS-F730-EOF                          
                GO                      TO 2000-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO CNT-F730-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3000-PROCESS-RECORD.                                                     
      *===============================================================*         
                                                                                
           PERFORM 3010-INITIALIZE                   THRU 3010-EXIT.            
           PERFORM 3100-GENERATE-OUTPUT              THRU 3100-EXIT.            
           PERFORM 8000-WRITE-OUTPUT                 THRU 8000-EXIT.            
           PERFORM 2000-READ-BP13F730                THRU 2000-EXIT.            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3010-INITIALIZE.                                                         
      *===============================================================*         
                                                                                
           MOVE SPACES                    TO OUT1-REGN                          
                                             OUT1-MATURE                        
                                             OUT1-NT                            
                                             OUT1-NRIC                          
                                             OUT1-FT                            
                                             OUT1-HH                            
                                             OUT1-APPS                          
                                             OUT1-MCPS                          
                                             OUT1-MGPS                          
                                             OUT1-BTO-PROJ                      
                                             OUT1-FT-DSN                        
                                             OUT2-REGN                          
                                             OUT2-MATURE                        
                                             OUT2-NT                            
                                             OUT2-NRIC                          
                                             OUT2-FT                            
                                             OUT2-HH                            
                                             OUT2-APPS                          
                                             OUT2-MCPS                          
                                                                                
           MOVE ZEROES                    TO OUT1-INCOME                        
                                             OUT1-SNO                           
                                             OUT1-AGE                           
                                             OUT2-SNO                           
                                             OUT2-INCOME                        
                                             OUT2-AGE.                          
                                                                                
           MOVE SPACES                    TO WS-CDE-NT1                         
                                             WS-MATURE.                         
                                                                                
           MOVE ZEROES                    TO WS-TOT-INCOME                      
                                             WS-NUM-HA1-AGE.                    
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3100-GENERATE-OUTPUT.                                                    
      *===============================================================*         
                                                                                
           PERFORM 4000-READ-OCCUPIERS         THRU 4000-EXIT.                  
           PERFORM 4800-READ-APPLICANT         THRU 4800-EXIT.                  
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4000-READ-OCCUPIERS.                                                     
      *===============================================================*         
                                                                                
           MOVE 0                                   TO WS-AGE.                  
                                                                                
           MOVE 'N'                                 TO WS-K825-FOUND.           
           PERFORM 4200-START-BP13K825            THRU 4200-EXIT.               
           IF K825-FND                                                          
              PERFORM UNTIL WS-K825-EOF = 'Y'                                   
                 PERFORM 4300-READNEXT-BP13K825   THRU 4300-EXIT                
                 IF NOT WS-K825-EOF = 'Y'                                       
                    ADD 1                           TO CNT-K825-READ            
                    PERFORM 4400-COMPUTE-AGE      THRU 4400-EXIT                
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           IF WS-K825-FOUND = 'N'                                               
              PERFORM 4010-START-BP13K828            THRU 4010-EXIT             
              IF K828-FND                                                       
                 PERFORM UNTIL WS-K828-EOF = 'Y'                                
                    PERFORM 4020-READNEXT-BP13K828   THRU 4020-EXIT             
                    IF NOT WS-K828-EOF = 'Y'                                    
                       ADD 1                           TO CNT-K828-READ         
                       PERFORM 4500-COMPUTE-AGE      THRU 4500-EXIT             
                    END-IF                                                      
                 END-PERFORM                                                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4010-START-BP13K828.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                            TO BP13K828-REC               
           INITIALIZE                                BP13K828-REC.              
           MOVE 'N'                               TO WS-K828-EOF                
                                                     WS-K828-FND.               
                                                                                
           MOVE F730-NUM-REGN                     TO K828-NUM-REGN              
                                                                                
           START BP13K828 KEY >= K828-KEY-FLD.                                  
                                                                                
           IF K828-STATUS = 00 OR 02                                            
              MOVE 'Y'                            TO WS-K828-FND                
           ELSE                                                                 
              IF K828-STATUS = 23 OR 10                                         
                 MOVE 'N'                         TO WS-K828-FND                
              ELSE                                                              
                 DISPLAY 'BP13K828 - ERROR STARTING : ' K828-STATUS             
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4020-READNEXT-BP13K828.                                                  
      *===============================================================*         
                                                                                
           READ BP13K828 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K828-EOF                                      
                GO          TO 4020-EXIT                                        
           END-READ.                                                            
                                                                                
           IF K828-STATUS = 00 OR 02                                            
              IF K828-NUM-REGN = F730-NUM-REGN                                  
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE 'Y'                         TO WS-K828-EOF                
              END-IF                                                            
           ELSE                                                                 
              IF K828-STATUS = 23 OR 10                                         
                 MOVE 'Y'                         TO WS-K828-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K828 - ERROR READING NEXT : ' K828-STATUS         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4200-START-BP13K825.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                            TO BP13K825-REC               
           INITIALIZE                                BP13K825-REC.              
           MOVE 'N'                               TO WS-K825-EOF                
                                                     WS-K825-FND.               
                                                                                
           MOVE F730-NUM-REGN                     TO K825-NUM-REGN              
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           IF K825-STATUS = 00 OR 02                                            
              MOVE 'Y'                            TO WS-K825-FND                
           ELSE                                                                 
              IF K825-STATUS = 23 OR 10                                         
                 MOVE 'N'                         TO WS-K825-FND                
              ELSE                                                              
                 DISPLAY 'BP13K825 - ERROR STARTING : ' K825-STATUS             
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4300-READNEXT-BP13K825.                                                  
      *===============================================================*         
                                                                                
           READ BP13K825 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K825-EOF                                      
                GO          TO 4300-EXIT                                        
           END-READ.                                                            
                                                                                
           IF K825-STATUS = 00 OR 02                                            
              IF K825-NUM-REGN = F730-NUM-REGN                                  
                 MOVE 'Y'                         TO WS-K825-FOUND              
              ELSE                                                              
                 MOVE 'Y'                         TO WS-K825-EOF                
              END-IF                                                            
           ELSE                                                                 
              IF K825-STATUS = 23 OR 10                                         
                 MOVE 'Y'                         TO WS-K825-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K825 - ERROR READING NEXT : ' K825-STATUS         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4400-COMPUTE-AGE.                                                        
      *===============================================================*         
                                                                                
           IF K825-NUM-NRIC =  F730-NUM-NRIC1                                   
             IF F730-DTE-REQUEST NOT NUMERIC                                    
                MOVE ZEROES                         TO WS-NUM-REGN              
             ELSE                                                               
                MOVE F730-DTE-REQUEST               TO WS-NUM-REGN              
             END-IF                                                             
             IF K825-DTE-BIRTH NOT NUMERIC                                      
                MOVE ZEROES                         TO WS-NUM-BIRTH             
             ELSE                                                               
                MOVE K825-DTE-BIRTH                 TO WS-NUM-BIRTH             
             END-IF                                                             
                                                                                
             IF WS-NUM-REGN > WS-NUM-BIRTH                                      
                COMPUTE WS-NUM-AGE = WS-NUM-REGN - WS-NUM-BIRTH                 
             ELSE                                                               
                COMPUTE WS-NUM-AGE = 0                                          
             END-IF                                                             
                                                                                
             IF WS-AGE-MM > 0                                                   
                COMPUTE WS-AGE-CY = WS-AGE-CY + 1                               
             END-IF                                                             
                                                                                
             IF K825-NUM-NRIC = F730-NUM-NRIC1                                  
                MOVE WS-AGE-CY                TO WS-NUM-HA1-AGE                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF K825-AMT-INCOME NOT NUMERIC                                       
              MOVE ZEROES                         TO K825-AMT-INCOME            
           END-IF                                                               
                                                                                
           IF K825-NUM-REGN = F730-NUM-REGN                                     
             COMPUTE WS-TOT-INCOME = WS-TOT-INCOME + K825-AMT-INCOME            
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4500-COMPUTE-AGE.                                                        
      *===============================================================*         
                                                                                
           IF K828-NUM-NRIC =  F730-NUM-NRIC1                                   
             IF F730-DTE-REQUEST NOT NUMERIC                                    
                MOVE ZEROES                         TO WS-NUM-REGN              
             ELSE                                                               
                MOVE F730-DTE-REQUEST               TO WS-NUM-REGN              
             END-IF                                                             
             IF K828-DTE-BIRTH NOT NUMERIC                                      
                MOVE ZEROES                         TO WS-NUM-BIRTH             
             ELSE                                                               
                MOVE K828-DTE-BIRTH                 TO WS-NUM-BIRTH             
             END-IF                                                             
                                                                                
             IF WS-NUM-REGN > WS-NUM-BIRTH                                      
                COMPUTE WS-NUM-AGE = WS-NUM-REGN - WS-NUM-BIRTH                 
             ELSE                                                               
                COMPUTE WS-NUM-AGE = 0                                          
             END-IF                                                             
                                                                                
             IF WS-AGE-MM > 0                                                   
                COMPUTE WS-AGE-CY = WS-AGE-CY + 1                               
             END-IF                                                             
                                                                                
             MOVE WS-AGE-CY                TO WS-NUM-HA1-AGE                    
           END-IF.                                                              
                                                                                
           IF K828-AMT-INCOME NOT NUMERIC                                       
              MOVE ZEROES                         TO K828-AMT-INCOME            
           END-IF                                                               
                                                                                
           IF K828-NUM-REGN = F730-NUM-REGN                                     
             COMPUTE WS-TOT-INCOME = WS-TOT-INCOME + K828-AMT-INCOME            
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4800-READ-APPLICANT.                                                     
      *===============================================================*         
      **                                                                        
      ** DECODE NEW TOWN                                                        
      **                                                                        
           MOVE F730-CDE-NT1                   TO WS-CDE-NT1.                   
           IF F730-NUM-ALLO-CAT = 'BTO'                                         
              PERFORM 5500-DECODE-NT         THRU 5500-EXIT                     
           ELSE                                                                 
              IF F730-NUM-ALLO-CAT = 'SBF'                                      
                 PERFORM 5600-READ-BP13K813  THRU 5600-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       5500-DECODE-NT.                                                          
      *===============================================================*         
                                                                                
           MOVE SPACES                         TO BP13K813-REC.                 
           INITIALIZE                             BP13K813-REC.                 
                                                                                
           PERFORM 5600-READ-BP13K813        THRU 5600-EXIT                     
                                                                                
           IF K813-CDE-NT NOT = SPACES                                          
              MOVE K813-CDE-NT                 TO WS-CDE-NT1                    
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       5600-READ-BP13K813.                                                      
      *===============================================================*         
                                                                                
           MOVE SPACES                          TO BP13K813-REC.                
           INITIALIZE                              BP13K813-REC.                
                                                                                
           MOVE WS-CDE-NT1                      TO K813-NUM-ZONE.               
           MOVE F730-CDE-FLAT-TYPE              TO K813-NUM-FLAT-TYPE.          
           MOVE F730-DTE-BALLOT                 TO K813-DTE-BALLOT.             
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    MOVE K813-NUM-MATURE-EST-TAG TO WS-MATURE                   
                                                                                
               WHEN 23                                                          
                    MOVE SPACES                 TO BP13K813-REC                 
                                                                                
               WHEN OTHER                                                       
                    MOVE WS-K813-STATUS               TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13K813: ' WS-K813-STATUS           
                            ' KEY : ' K813-KEY-FLD                              
                    PERFORM  9000-CLOSE-FILES THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       5600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       8000-WRITE-OUTPUT.                                                       
      *===============================================================*         
                                                                                
           EVALUATE F730-NUM-ALLO-CAT                                           
           WHEN 'BTO'                                                           
              PERFORM 8010-WRITE-FOUT1   THRU 8010-EXIT                         
           WHEN 'SBF'                                                           
              PERFORM 8020-WRITE-FOUT2   THRU 8020-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       8010-WRITE-FOUT1.                                                        
      *===============================================================*         
           MOVE F730-NUM-REGN             TO OUT1-REGN.                         
           MOVE WS-MATURE                 TO OUT1-MATURE.                       
           MOVE WS-CDE-NT1                TO OUT1-NT.                           
           MOVE F730-NUM-NRIC1            TO OUT1-NRIC.                         
           MOVE F730-CDE-FLAT-TYPE        TO OUT1-FT.                           
           MOVE F730-CDE-HOUSEHOLD        TO OUT1-HH.                           
           MOVE WS-TOT-INCOME             TO OUT1-INCOME.                       
           MOVE F730-NUM-APPS             TO OUT1-APPS.                         
           MOVE F730-NUM-MCPS             TO OUT1-MCPS.                         
           MOVE WS-NUM-HA1-AGE            TO OUT1-AGE.                          
           MOVE F730-NUM-MGPS             TO OUT1-MGPS.                         
           MOVE K813-NME-PROJECT-LTR      TO OUT1-BTO-PROJ.                     
           MOVE K813-NUM-FLAT-CATEGORY    TO OUT1-FT-DSN.                       
                                                                                
           IF WS-PRT-CTR1  < 1                                                  
              MOVE 1                      TO WS-SNO1                            
              WRITE P13FOUT1-REC        FROM OUT1-HEADING1                      
           ELSE                                                                 
              ADD  1                      TO WS-SNO1                            
           END-IF.                                                              
                                                                                
           MOVE WS-SNO1                   TO OUT1-SNO                           
           WRITE P13FOUT1-REC           FROM OUT1-DATA.                         
           ADD 1                          TO WS-PRT-CTR1.                       
                                                                                
       8010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       8020-WRITE-FOUT2.                                                        
      *===============================================================*         
           MOVE F730-NUM-REGN             TO OUT2-REGN.                         
           MOVE WS-MATURE                 TO OUT2-MATURE.                       
           MOVE WS-CDE-NT1                TO OUT2-NT.                           
           MOVE F730-NUM-NRIC1            TO OUT2-NRIC.                         
           MOVE F730-CDE-FLAT-TYPE        TO OUT2-FT.                           
           MOVE F730-CDE-HOUSEHOLD        TO OUT2-HH.                           
           MOVE WS-TOT-INCOME             TO OUT2-INCOME.                       
           MOVE F730-NUM-APPS             TO OUT2-APPS.                         
           MOVE F730-NUM-MCPS             TO OUT2-MCPS.                         
           MOVE WS-NUM-HA1-AGE            TO OUT2-AGE.                          
                                                                                
           IF WS-PRT-CTR2  < 1                                                  
              MOVE 1                      TO WS-SNO2                            
              WRITE P13FOUT2-REC        FROM OUT2-HEADING1                      
           ELSE                                                                 
              ADD  1                      TO WS-SNO2                            
           END-IF.                                                              
                                                                                
           MOVE WS-SNO2                   TO OUT2-SNO                           
           WRITE P13FOUT2-REC           FROM OUT2-DATA.                         
           ADD 1                          TO WS-PRT-CTR2.                       
                                                                                
       8020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       9000-CLOSE-FILES.                                                        
      *===============================================================*         
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CF94'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13F730 READ          : ' CNT-F730-READ.            
           DISPLAY 'NO. OF BP13K825 READ          : ' CNT-K825-READ.            
           DISPLAY 'NO. OF BP13K828 READ          : ' CNT-K828-READ.            
           DISPLAY 'NO. OF RECS WRITTEN FOUT1     : ' WS-PRT-CTR1.              
           DISPLAY 'NO. OF RECS WRITTEN FOUT2     : ' WS-PRT-CTR2.              
                                                                                
           CLOSE BP13F730                                                       
                 BP13K825                                                       
                 BP13K828                                                       
                 BP13K813                                                       
                 P13FOUT1                                                       
                 P13FOUT2.                                                      
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K813 , STATUS = '                      
                 WS-K813-STATUS                                                 
           END-IF.                                                              
                                                                                
           IF K825-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K825 , STATUS = ' K825-STATUS          
           END-IF.                                                              
                                                                                
           IF K828-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K828 , STATUS = ' K828-STATUS          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
