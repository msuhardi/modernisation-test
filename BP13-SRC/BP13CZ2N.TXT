      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CZ2N.                                                 
      *AUTHOR.        GURU..                                                    
      *DATE-WRITTEN.  22/01/18.                                                 
      *****************************************************************         
      *OBJECTIVE - CHECK FIRST TIMER APPLICATION RATE < 1.0 AND YET   *         
      *            FOUND UNSUCC FIRST TIMER.                          *         
      *          - OUTPUT UN-SUCCESSFUL FIRST TIMER                   *         
      *          - OUTPUT SUCC SECOND TIMER                           *         
      *                                                               *         
      *     INPUT FILE : 1. BP13F740                                  *         
      *                  2. BP13F762                                  *         
      *     OUTPUT FILE: 1. P13F740A (UNSUCC)                         *         
      *                  2. P13F740B (SUCC)                           *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP138078 310118     KR13 NEW PGM                              *         
      * BP139411 121222     AM25 ADDT'L CONDITION FOR 1ST-TIMER-RATIO *         
      *****************************************************************         
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F762  ASSIGN      TO BP13F762.                            
                                                                                
           SELECT BP13F740  ASSIGN      TO BP13F740.                            
                                                                                
           SELECT P13F740A  ASSIGN      TO P13F740A.                            
                                                                                
           SELECT P13F740B  ASSIGN      TO P13F740B.                            
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F762.                                                           
       COPY BP13F762.                                                           
                                                                                
       FD   BP13F740.                                                           
       COPY BP13F740.                                                           
                                                                                
       FD   P13F740A                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  P13F740A-REC.                                                        
           05  FILLER                             PIC X(500).                   
                                                                                
       FD   P13F740B                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  P13F740B-REC.                                                        
           05  FILLER                             PIC X(500).                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F762-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-F740-EOF             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F762-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-F740-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-F740-BYPASS          PIC 9(07) VALUE ZEROES.                  
           05  WS-F762-BYPASS          PIC 9(07) VALUE ZEROES.                  
           05  WS-F740-LESS            PIC 9(07) VALUE ZEROES.                  
           05  WS-762-740-MTCH         PIC 9(07) VALUE ZEROES.                  
           05  WS-F762-LESS            PIC 9(07) VALUE ZEROES.                  
           05  WS-F762-GT-1            PIC 9(07) VALUE ZEROES.                  
           05  WS-SUCC-G               PIC 9(07) VALUE ZEROES.                  
           05  WS-SUCC-H               PIC 9(07) VALUE ZEROES.                  
           05  WS-UNSUCC-G             PIC 9(07) VALUE ZEROES.                  
           05  WS-UNSUCC-H             PIC 9(07) VALUE ZEROES.                  
           05  TOT-SUCC-G              PIC 9(07) VALUE ZEROES.                  
           05  TOT-SUCC-H              PIC 9(07) VALUE ZEROES.                  
           05  TOT-UNSUCC-G            PIC 9(07) VALUE ZEROES.                  
           05  TOT-UNSUCC-H            PIC 9(07) VALUE ZEROES.                  
           05  WS-F762-LESS-1          PIC 9(07) VALUE ZEROES.                  
           05  WS-1ST-TIMER-RATIO      PIC 9(02).99 VALUE ZEROES.               
           05  CUR-DTE                 PIC 9(06) VALUE ZEROES.                  
           05  WS-F740-KEY.                                                     
             10 WS-F740-CDE-NT1        PIC X(03).                               
             10 WS-F740-CDE-FLAT-TYPE  PIC X(02).                               
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
                                                                                
           PERFORM 2000-READ-BP13F762       THRU 2000-EXIT.                     
                                                                                
           PERFORM 3000-READ-BP13F740       THRU 3000-EXIT.                     
                                                                                
           PERFORM 4000-PROCESS-RECORDS     THRU 4000-EXIT                      
             UNTIL WS-F762-EOF = 'Y' OR WS-F740-EOF = 'Y'.                      
                                                                                
           PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13F762                                                 
               OUTPUT  P13F740A                                                 
                       P13F740B.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYSTEM-DATE.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F762.                                                      
      ******************************************************************        
                                                                                
           READ BP13F762 AT END                                                 
                MOVE 'Y'                    TO WS-F762-EOF                      
                MOVE HIGH-VALUE             TO F762-KEY-FLD                     
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
           ADD +1                           TO WS-F762-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-READ-BP13F740.                                                      
      ******************************************************************        
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y'                    TO WS-F740-EOF                      
                MOVE HIGH-VALUE             TO WS-F740-KEY                      
                GO TO 3000-EXIT                                                 
           END-READ.                                                            
           ADD +1                           TO WS-F740-READ                     
           MOVE F740-CDE-NT1                TO WS-F740-CDE-NT1.                 
           MOVE F740-CDE-FLAT-TYPE          TO WS-F740-CDE-FLAT-TYPE.           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
           MOVE ZEROS                       TO WS-SUCC-H                        
                                               WS-SUCC-G                        
                                               WS-UNSUCC-H                      
                                               WS-UNSUCC-G.                     
           MOVE 'N'                         TO WS-F740-EOF.                     
                                                                                
           IF F762-KEY-FLD = WS-F740-KEY                                        
             ADD +1                         TO WS-762-740-MTCH                  
             IF F762-NUM-1ST-TIMER IS NOT NUMERIC                               
               MOVE ZEROS                   TO F762-NUM-1ST-TIMER               
             END-IF                                                             
             IF F762-NUM-1ST-TIMER-CNT IS NOT NUMERIC                           
               MOVE ZEROS                   TO F762-NUM-1ST-TIMER-CNT           
             END-IF                                                             
                                                                                
             IF F762-NUM-1ST-TIMER-CNT = 0                                      
                MOVE ZEROES                 TO WS-1ST-TIMER-RATIO               
             ELSE                                                               
                COMPUTE WS-1ST-TIMER-RATIO =                                    
                   (F762-NUM-1ST-TIMER / F762-NUM-1ST-TIMER-CNT)                
             END-IF                                                             
                                                                                
             IF (WS-1ST-TIMER-RATIO < '01.00' AND > '00.00')                    
               ADD +1                         TO WS-F762-LESS-1                 
               PERFORM  4050-VALIDATE-STATUS  THRU 4050-EXIT                    
                      UNTIL WS-F740-KEY NOT = F762-KEY-FLD                      
                      OR WS-F740-EOF = 'Y'                                      
                                                                                
               IF WS-UNSUCC-H > ZEROS                                           
                  CONTINUE                                                      
               ELSE                                                             
                  ADD 1                        TO WS-F762-BYPASS                
               END-IF                                                           
             ELSE                                                               
                ADD +1                        TO WS-F762-GT-1                   
                PERFORM 4060-BYPASS-PROCESS THRU 4060-EXIT                      
                      UNTIL WS-F740-KEY NOT = F762-KEY-FLD                      
                      OR WS-F740-EOF = 'Y'                                      
             END-IF                                                             
                                                                                
             ADD WS-SUCC-H   TO TOT-SUCC-H                                      
             ADD WS-SUCC-G   TO TOT-SUCC-G                                      
             ADD WS-UNSUCC-H TO TOT-UNSUCC-H                                    
             ADD WS-UNSUCC-G TO TOT-UNSUCC-G                                    
                                                                                
             PERFORM 2000-READ-BP13F762     THRU 2000-EXIT                      
           ELSE                                                                 
             IF F762-KEY-FLD < WS-F740-KEY                                      
                ADD +1                      TO WS-F762-LESS                     
                PERFORM 2000-READ-BP13F762  THRU 2000-EXIT                      
             ELSE                                                               
                ADD +1                      TO WS-F740-LESS                     
                PERFORM 3000-READ-BP13F740  THRU 3000-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4050-VALIDATE-STATUS.                                                    
      ****************************************************************          
                                                                                
           IF F740-CDE-SUCC-NT-ZONE = SPACES                                    
              IF F740-CDE-BALLOT-HOUSEHOLD = 'G'                                
                 ADD +1                     TO WS-UNSUCC-G                      
              ELSE                                                              
                 ADD 1                      TO WS-UNSUCC-H                      
                 PERFORM 4100-WRITE-UNSUCC  THRU 4100-EXIT                      
              END-IF                                                            
           ELSE                                                                 
              IF F740-CDE-BALLOT-HOUSEHOLD = 'G'                                
                 ADD 1                      TO WS-SUCC-G                        
                 PERFORM 4200-WRITE-SUCC    THRU 4200-EXIT                      
              ELSE                                                              
                 ADD +1                     TO WS-SUCC-H                        
              END-IF                                                            
           END-IF                                                               
           PERFORM 3000-READ-BP13F740       THRU 3000-EXIT.                     
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4060-BYPASS-PROCESS.                                                     
      ****************************************************************          
                                                                                
           ADD +1                           TO WS-F740-BYPASS.                  
           PERFORM 3000-READ-BP13F740       THRU 3000-EXIT.                     
                                                                                
       4060-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4100-WRITE-UNSUCC.                                                       
      ****************************************************************          
                                                                                
           INITIALIZE P13F740A-REC.                                             
                                                                                
           MOVE BP13F740-REC                TO P13F740A-REC                     
                                                                                
           WRITE P13F740A-REC.                                                  
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4200-WRITE-SUCC.                                                         
      ****************************************************************          
                                                                                
           INITIALIZE P13F740B-REC.                                             
                                                                                
           MOVE BP13F740-REC                TO P13F740B-REC                     
                                                                                
           WRITE P13F740B-REC.                                                  
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CZ2N'                                      
           DISPLAY 'DATE    : ' WS-SYSTEM-DATE                                  
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F762 RECORDS READ ........ : '                
                    WS-F762-READ.                                               
           DISPLAY '(2) NO OF BP13F740 RECORDS READ ........ : '                
                    WS-F740-READ.                                               
           DISPLAY '(3) NO OF F762 = F740 MATCH............. : '                
                    WS-762-740-MTCH.                                            
           DISPLAY '(4) FOUND BP13F740 NOTFD BP13F762 ...... : '                
                    WS-F740-LESS.                                               
           DISPLAY '(5) FOUND BP13F762 NOTFD BP13F740 ...... : '                
                    WS-F762-LESS.                                               
           DISPLAY ' '.                                                         
           DISPLAY '(6) FIRST TIMER RATIO < 1.0  ........... : '                
                    WS-F762-LESS-1.                                             
           DISPLAY '(7) FIRST TIMER RATIO > 1.0  ........... : '                
                    WS-F762-GT-1.                                               
           DISPLAY '(9) NO OF BP13F762 NO UPDATE          .. : '                
                    WS-F762-BYPASS.                                             
           DISPLAY ' '.                                                         
           DISPLAY '(10)OUTPUT SUCC SECOND TIMER  .......... : '                
                    TOT-SUCC-G.                                                 
           DISPLAY '(11)OUTPUT UNSUCC FIRST  TIMER ......... : '                
                    TOT-UNSUCC-H.                                               
           DISPLAY ' '.                                                         
           DISPLAY '(12)COUNT SUCC  FIRST  TIMER  .......... : '                
                    TOT-SUCC-H.                                                 
           DISPLAY '(13)COUNT UNSUCC SECOND TIMER .......... : '                
                    TOT-UNSUCC-G.                                               
           DISPLAY '(14)BYPASS PROCESS BP13F740 ............ : '                
                    WS-F740-BYPASS.                                             
                                                                                
           CLOSE BP13F762                                                       
                 BP13F740                                                       
                 P13F740A                                                       
                 P13F740B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
