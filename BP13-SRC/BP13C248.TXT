       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C248.                                                  
       AUTHOR.       ANNALYN BANTA.                                             
      *DATE-WRITTEN. 18.03.94.                                                  
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *  SYSTEM ID   : BP13                                      *              
      *  OBJECTIVE   : TO CREATE ALLOCATION SUMMARY FILE BP13F222*              
      *                                                          *              
      *  BP13F205    : CONTROL REC                               *              
      *  BP13F222    : ALLOCATION SUMMARY RESULT FILE            *              
      *  BP13K270    : BOOKING PROGRAMME FILE                    *              
      *  BP13K210    : BOOKING WORKPLAN                          *              
      *                                                          *              
      * CHG-NO  BY   DATE    DESCRIPTION                         *              
      * ------  ---  ------  -----------                         *              
      * N940009 LMS  080694  TO INCLUDE K222-DTE-ALLOC.          *              
      * N940009 LMS  210694  TO CATER FOR EOF-K100.              *              
      * JAR8488 LMS  160994  TO CATER FOR NEXT BP13F205 REC.     *              
      * SOC-PH9 GD   051194 -TO RENAME NUM-NT VARIABLES TO       *              
      *                         NUM-NT-ZONE.                     *              
      *                     -TO CATER FOR GROUPING BY FLAT TYPE. *              
      *BP130067 LMS  010795  PGM RECOMPILED DUE TO CHG IN        *              
      *                      FILE BP13K222.                      *              
      *BP130336 LMS  120897  TO CATER FOR JB IN SERS.            *              
      *(BP130250 IN LCS)                                         *              
      *BP130488 LMS  080598  TO WRITE BLOCKS TO BP13K278 FOR     *              
      *                      OCS BLOCKS.                         *              
      *BP130418 LMS  020998  TO CATER FOR Y2K CHANGES.           *              
      *BP130778 GBR  141299  TO INCLUDE 3 ROOM FLAT UNDER FLAT   *              
      *                      TYPE 'SE'                           *              
      *BP130883 CLT  041100  TO READ STREET NAME FROM PIDB       *              
      *                      INTERFACE TABLE                     *              
      *BP130949 LMS  221100  TO CATER FOR 2 AND 3 ROOM OCS.      *              
      *BP131017 LMS  110501  TO WRITE TO F222 INSTEAD OF K222.   *              
      *BP131059 LMS  200801  TO CATER FOR BTO.                   *              
      *BP132075 JAM1 170102  TO USE K270-NUM-OFFER INSTEAD OF    *              
      *                      K270-NUM-ALLOC IN ACCUMULATING TOTAL*              
      *                      NUMBER OF UNITS OFFERED.            *              
      *BP132299 MTD  130203  CATER FOR F205-NUM-NT-ZONE = '00'   *              
      *BP132560 LMS  270704  CATER FOR DUXTON.                   *              
      *BP132582 LMS  141004  CATER FOR 3-ROOM.                   *              
      *BP132785 LMS  071205  CATER FOR COMBINE SA QUOTA.         *              
      *BP133610 ESA1 060509  TO REPLACE BP13K767 W/ BP13K816     *              
      *BP133809 ESA1 220110  TO CORRECT ADDING OF NUM-OFFER FOR  *              
      *                      STUDIO APARTMENT                    *              
      *BP134253 ESD1 270611  CATER TO REMOVE PROCESSING OF       *              
      *                      BP13F222 OUTPUT.                    *              
      *BP134681 ESA1 190912  TO REPLACE BP13K816 W/ BP13K813     *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT BP13F205 ASSIGN       TO BP13F205.                            
                                                                                
           SELECT BP13K270 ASSIGN       TO BP13K270                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K270-KEY-FLD                         
                           FILE STATUS  IS K270-STATUS.                         
                                                                                
           SELECT BP13K210 ASSIGN       TO BP13K210                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K210-KEY-FLD                         
                           FILE STATUS  IS K210-STATUS.                         
                                                                                
           SELECT BP13K278 ASSIGN       TO BP13K278                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K278-KEY-FLD                         
                           FILE STATUS  IS K278-STATUS.                         
                                                                                
           SELECT BM06K100 ASSIGN       TO BM06K100                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K100-KEY-FLD                         
                           FILE STATUS  IS K100-STATUS.                         
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F205                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           LABEL  RECORDS  ARE STANDARD                                         
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 80  CHARACTERS.                                      
       COPY BP13F205.                                                           
                                                                                
       FD  BP13K278                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K278.                                                           
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K100.                                                           
                                                                                
       FD  BP13K270                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13K270.                                                           
                                                                                
       FD  BP13K210                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K210.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       COPY AV02COMM.                                                           
       01  WS-FILE-STATUS.                                                      
           05  K278-STATUS         PIC 99      VALUE ZERO.                      
           05  K210-STATUS         PIC 99      VALUE ZERO.                      
           05  K270-STATUS         PIC 99      VALUE ZERO.                      
           05  K100-STATUS         PIC 99      VALUE ZERO.                      
                                                                                
       01  WS-K210-FT              PIC X(2).                                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F205-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-K278-WRITE       PIC 9(8)    VALUE ZERO.                      
           05  WS-K278-UPDATE      PIC 9(8)    VALUE ZERO.                      
           05  WS-K210-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-K100-READ        PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-SWITCHES.                                                         
           05  F205-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-F205                 VALUE 'Y'.                       
           05  K270-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-K270                 VALUE 'Y'.                       
           05  K210-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-K210                 VALUE 'Y'.                       
                                                                                
       01  WS-TEMP-VARIABLES.                                                   
           05  WS-CNT-BKAPPMT      PIC 9(4)    VALUE ZERO.                      
           05  WS-FLAT-OFFER       PIC 9(4)    VALUE ZERO.                      
           05  WS-ALLOC-TOTAL      PIC 9(4)    VALUE ZERO.                      
           05  WS-SERS-FLAT        PIC 9(4)    VALUE ZERO.                      
           05  WS-FT-NO            PIC 9(2)    VALUE ZERO.                      
           05  WS-START-DATE       PIC X(8)    VALUE SPACES.                    
           05  WS-END-DATE         PIC X(8)    VALUE SPACES.                    
           05  WS-WORK-END-DATE    PIC X(8)    VALUE SPACES.                    
           05  WS-DAY              PIC 9(2)    VALUE ZERO.                      
           05  WS-FT               PIC X(2).                                    
           05  WS-CDE-BLK          PIC X(5)    VALUE SPACES.                    
           05  WS-NME-STREET       PIC X(20)   VALUE SPACES.                    
           05  WS-VALID-NEXT-DAY   PIC X(1)    VALUE SPACES.                    
                                                                                
       01  WS-OCS-DUE-DATE         PIC 9(8).                                    
       01  WS-K278-DTE-END         PIC X(8).                                    
       01  WS-K278-DTE-END-9 REDEFINES WS-K278-DTE-END PIC 9(8).                
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND                 PIC S9(4) COMP VALUE +000.          
           05  WS-SQL-NOT-FOUND             PIC S9(4) COMP VALUE +100.          
                                                                                
       01  WS-LINK-REC.                                                         
           05  WS-LINK-ZONE           PIC X(03).                                
                                                                                
       COPY BP13K813.                                                           
                                                                                
       01  WS-ZONE-FND-FLAG           PIC X(01).                                
                                                                                
       01  WS-RETURN-CODE             PIC 9(02).                                
                                                                                
                                                                                
           EXEC SQL INCLUDE P13VINTF END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN-LOGIC.                                                         
      *************************************************************             
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-F205       THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-RTN     THRU 3000-EXIT                          
             UNTIL END-OF-F205.                                                 
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
      *************************************************************             
       1000-OPEN-FILES.                                                         
      *************************************************************             
           OPEN  INPUT BP13F205                                                 
                       BP13K210                                                 
                       BP13K270                                                 
                       BM06K100                                                 
                I-O    BP13K278.                                                
                                                                                
           CALL 'OAV02001' USING LINK-REC.                                      
                                                                                
           IF K270-STATUS NOT = 00 AND 97                                       
              MOVE K270-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BP13K270 FILE, ' K270-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                         
                                                                                
           IF K210-STATUS NOT = 00 AND 97                                       
              MOVE K210-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BP13K210 FILE, ' K210-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                         
                                                                                
           IF K278-STATUS NOT = 00 AND 97                                       
              MOVE K278-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BP13K278 FILE, ' K278-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                         
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              MOVE K100-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BM06K100 FILE, ' K100-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                         
                                                                                
           INITIALIZE      WS-SWITCHES                                          
                           WS-TEMP-VARIABLES                                    
                           WS-COUNTERS.                                         
           CALL 'OP13K813' USING WS-RETURN-CODE                                 
           IF WS-RETURN-CODE NOT = 00                                           
              DISPLAY 'ERROR OPENING BP13K813, STATUS: '                        
                       WS-RETURN-CODE                                           
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2000-READ-F205.                                                          
      *************************************************************             
           READ BP13F205                                                        
             AT END                                                             
                MOVE 'Y' TO F205-EOF                                            
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                   TO WS-F205-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3000-PROCESS-RTN.                                                        
      *************************************************************             
                                                                                
           INITIALIZE  WS-TEMP-VARIABLES                                        
                       WS-SWITCHES.                                             
                                                                                
           PERFORM 3100-START-BP13K210 THRU 3100-EXIT.                          
                                                                                
           PERFORM 2000-READ-F205      THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3100-START-BP13K210.                                                     
      *-----------------------------------------------------------*             
           MOVE 'N'                       TO K210-EOF.                          
           MOVE SPACES                    TO BP13K210-REC.                      
           MOVE F205-NUM-NT-ZONE          TO K210-NUM-NT-ZONE.                  
           MOVE ZEROS                     TO WS-CNT-BKAPPMT.                    
                                                                                
           START BP13K210 KEY NOT < K210-KEY-FLD.                               
                                                                                
           IF K210-STATUS = 00                                                  
              PERFORM 3200-READ-BP13K210 THRU 3200-EXIT                         
                UNTIL (F205-NUM-NT-ZONE   = K210-NUM-NT-ZONE    AND             
                       F205-DTE-ALLOCN    = K210-DTE-ALLOC      AND             
                       F205-NUM-FLAT-TYPE = K210-NUM-FLAT-TYPE)                 
                   OR  K210-EOF = 'Y'                                           
                                                                                
              IF (K210-EOF = 'Y') OR                                            
                 (F205-NUM-NT-ZONE   NOT = K210-NUM-NT-ZONE)    OR              
                 (F205-DTE-ALLOCN    NOT = K210-DTE-ALLOC)      OR              
                 (F205-NUM-FLAT-TYPE NOT = K210-NUM-FLAT-TYPE)                  
                 GO TO 3100-EXIT                                                
              ELSE                                                              
                 PERFORM 3250-PROCESS-BP13K210 THRU 3250-EXIT                   
              END-IF                                                            
           ELSE                                                                 
           IF K210-STATUS = 23                                                  
              DISPLAY 'START ERROR BP13K210 FILE' K210-STATUS                   
              DISPLAY 'K210-KEY-FLD : ' K210-KEY-FLD                            
           ELSE                                                                 
              MOVE K210-STATUS         TO RETURN-CODE                           
              DISPLAY 'ERROR START BP13K210 FILE, ' K210-STATUS                 
              DISPLAY 'K210-KEY-FLD = ' K210-KEY-FLD                            
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3200-READ-BP13K210.                                                      
      *-----------------------------------------------------------*             
                                                                                
           READ BP13K210 NEXT AT END  MOVE 'Y' TO K210-EOF                      
                GO TO 3200-EXIT.                                                
                                                                                
           ADD 1 TO WS-K210-READ.                                               
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3250-PROCESS-BP13K210.                                                   
      *-----------------------------------------------------------*             
                                                                                
           MOVE K210-NUM-FLAT-TYPE TO WS-K210-FT.                               
           PERFORM 3300-COUNT-SELECTION-DAY THRU 3300-EXIT                      
             UNTIL K210-NUM-NT-ZONE   > F205-NUM-NT-ZONE                        
                OR K210-NUM-FLAT-TYPE > WS-K210-FT                              
                OR K210-EOF = 'Y'.                                              
                                                                                
           PERFORM 3400-READ-K270 THRU 3400-EXIT.                               
                                                                                
       3250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3300-COUNT-SELECTION-DAY.                                                
      *-----------------------------------------------------------*             
                                                                                
           IF F205-DTE-ALLOCN = K210-DTE-ALLOC                                  
              IF K210-NUM-FLAT-TYPE = WS-K210-FT                                
                 ADD K210-NUM-BKAPPMT       TO WS-CNT-BKAPPMT                   
                 ADD 1 TO WS-DAY                                                
                 IF WS-DAY = 1                                                  
                    MOVE K210-DTE-BKAPPMT   TO WS-START-DATE                    
                 END-IF                                                         
                 MOVE K210-DTE-BKAPPMT      TO WS-END-DATE                      
                                               WS-WORK-END-DATE                 
              ELSE                                                              
                 PERFORM 3400-READ-K270   THRU 3400-EXIT                        
                 MOVE K210-NUM-FLAT-TYPE    TO WS-K210-FT                       
                 MOVE ZEROS                 TO WS-DAY                           
                                               WS-CNT-BKAPPMT                   
                 GO TO 3300-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3200-READ-BP13K210  THRU 3200-EXIT.                          
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3400-READ-K270.                                                          
      *-----------------------------------------------------------*             
                                                                                
           MOVE SPACES                    TO K270-KEY-FLD.                      
           MOVE F205-DTE-ALLOCN           TO K270-ALLOCN-DATE.                  
           MOVE ZEROS                     TO WS-FLAT-OFFER.                     
                                                                                
           START BP13K270 KEY NOT < K270-KEY-FLD.                               
                                                                                
           IF K270-STATUS = 00 OR 02                                            
              CONTINUE                                                          
           ELSE                                                                 
           IF K270-STATUS = 23                                                  
              DISPLAY 'RECORD NOT FOUND IN BP13K270'                            
              DISPLAY 'K270-KEY-FLD = ' K270-KEY-FLD                            
              GO TO 3400-EXIT                                                   
           ELSE                                                                 
              MOVE K270-STATUS           TO RETURN-CODE                         
              DISPLAY 'START ERROR READING BP13K270 FILE, ' K270-STATUS         
              DISPLAY 'K270-KEY-FLD = ' K270-KEY-FLD                            
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
      * TO GET THE SUBSCRIPT FOR K270-NUM-ALLOC BASED ON                        
      * K100-NUM-FLAT-TYPE                                                      
                                                                                
           IF WS-K210-FT = '1 '                                                 
              MOVE 1 TO WS-FT-NO                                                
           ELSE                                                                 
           IF WS-K210-FT = '2 '                                                 
              MOVE 2 TO WS-FT-NO                                                
           ELSE                                                                 
           IF WS-K210-FT = '3 '                                                 
              MOVE 3 TO WS-FT-NO                                                
           ELSE                                                                 
           IF WS-K210-FT = '4 ' OR '4D'                                         
              MOVE 4 TO WS-FT-NO                                                
           ELSE                                                                 
           IF WS-K210-FT = '4S'                                                 
              MOVE 5 TO WS-FT-NO                                                
           ELSE                                                                 
           IF WS-K210-FT = '5 ' OR '5D'                                         
              MOVE 6 TO WS-FT-NO                                                
           ELSE                                                                 
           IF WS-K210-FT = 'E ' OR 'ED'                                         
              MOVE 7 TO WS-FT-NO                                                
           ELSE                                                                 
           IF WS-K210-FT = 'H '                                                 
              MOVE 8 TO WS-FT-NO                                                
           ELSE                                                                 
           IF WS-K210-FT = 'SE'                                                 
              MOVE 9 TO WS-FT-NO.                                               
                                                                                
           IF K270-STATUS = 0                                                   
              MOVE 'N'       TO K270-EOF                                        
              PERFORM 3500-ACCUM-FLATOFFER THRU 3500-EXIT                       
                UNTIL END-OF-K270.                                              
                                                                                
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3500-ACCUM-FLATOFFER.                                                    
      *-----------------------------------------------------------*             
           READ BP13K270 NEXT                                                   
                AT END                                                          
                   MOVE 'Y' TO K270-EOF                                         
                   GO 3500-EXIT.                                                
                                                                                
           IF K270-ALLOCN-DATE NOT = F205-DTE-ALLOCN                            
              MOVE 'Y' TO K270-EOF                                              
              GO 3500-EXIT.                                                     
                                                                                
           EVALUATE F205-NUM-FLAT-TYPE                                          
             WHEN '00'                                                          
              IF F205-NUM-SELECTION = 'BTO'                                     
                 MOVE F205-NUM-NT-ZONE   TO WS-LINK-ZONE                        
                 CALL 'BP13C910' USING WS-LINK-REC, BP13K813-REC,               
                                       WS-ZONE-FND-FLAG                         
                 IF WS-ZONE-FND-FLAG = 'Y'                                      
                    MOVE 0 TO WS-ALLOC-TOTAL                                    
                   IF F205-NUM-NT-ZONE (2:2) = '17'                             
                      PERFORM 3510-COMPUTE-FOR-DUXTON THRU 3510-EXIT            
                   ELSE                                                         
                      PERFORM 3520-COMPUTE-FOR-BTO THRU 3520-EXIT               
                   END-IF                                                       
                 END-IF                                                         
              ELSE                                                              
                 IF (K270-CDE-NT = F205-NUM-NT-ZONE) OR                         
                    (K270-CDE-APPL-ZONE = F205-NUM-NT-ZONE)                     
                     MOVE 0 TO WS-ALLOC-TOTAL                                   
                     COMPUTE WS-ALLOC-TOTAL = K270-NUM-OFFER(4) +               
                                              K270-NUM-OFFER(5) +               
                                              K270-NUM-OFFER(6) +               
                                              K270-NUM-OFFER(7)                 
                     ADD WS-ALLOC-TOTAL TO WS-FLAT-OFFER                        
                     IF WS-ALLOC-TOTAL > 0                                      
                        PERFORM 3700-READ-BM06K100   THRU 3700-EXIT             
                        PERFORM 3750-COMPUTE-DATE    THRU 3750-EXIT             
                        PERFORM 4000-WRITE-BP13K278  THRU 4000-EXIT             
                     END-IF                                                     
                 ELSE                                                           
                    IF F205-NUM-NT-ZONE = '00'                                  
                        MOVE 0 TO WS-ALLOC-TOTAL                                
                        COMPUTE WS-ALLOC-TOTAL = K270-NUM-OFFER(4)              
                        ADD WS-ALLOC-TOTAL TO WS-FLAT-OFFER                     
                        IF WS-ALLOC-TOTAL > 0                                   
                           PERFORM 3700-READ-BM06K100   THRU 3700-EXIT          
                           PERFORM 3750-COMPUTE-DATE    THRU 3750-EXIT          
                           PERFORM 4000-WRITE-BP13K278  THRU 4000-EXIT          
                        END-IF                                                  
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
             WHEN OTHER                                                         
              IF (K270-CDE-NT = F205-NUM-NT-ZONE) OR                            
                 (K270-CDE-APPL-ZONE = F205-NUM-NT-ZONE)                        
                  MOVE 0 TO WS-ALLOC-TOTAL                                      
                  IF F205-NUM-FLAT-TYPE = 'SE'                                  
                     PERFORM 3530-COMPUTE-FOR-SE THRU 3530-EXIT                 
                  ELSE                                                          
                     IF F205-NUM-FLAT-TYPE = '4 '                               
                        PERFORM 3540-COMPUTE-FOR-RM4 THRU 3540-EXIT             
                     ELSE                                                       
                        IF F205-NUM-FLAT-TYPE = 'E '                            
                           COMPUTE WS-ALLOC-TOTAL = K270-NUM-OFFER(7) +         
                                                    K270-NUM-OFFER(9)           
                           MOVE WS-ALLOC-TOTAL TO WS-FLAT-OFFER                 
                        ELSE                                                    
                           IF K270-NUM-COMBINE-SA = 'Y'                         
                              PERFORM 3550-COMPUTE-FOR-COMBINE-SA               
                                 THRU 3550-EXIT                                 
                           ELSE                                                 
                              ADD K270-NUM-OFFER(WS-FT-NO)                      
                               TO WS-FLAT-OFFER                                 
                              MOVE WS-FLAT-OFFER TO WS-ALLOC-TOTAL              
                           END-IF                                               
                        END-IF                                                  
                     END-IF                                                     
                  END-IF                                                        
                  IF WS-ALLOC-TOTAL > 0                                         
                     PERFORM 3700-READ-BM06K100   THRU 3700-EXIT                
                     PERFORM 3750-COMPUTE-DATE    THRU 3750-EXIT                
                     PERFORM 4000-WRITE-BP13K278  THRU 4000-EXIT                
                  END-IF                                                        
              END-IF                                                            
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3510-COMPUTE-FOR-DUXTON.                                                 
      *-----------------------------------------------------------*             
           COMPUTE WS-ALLOC-TOTAL = K270-NUM-OFFER(4) +                         
                                    K270-NUM-OFFER(6).                          
                                                                                
           MOVE WS-ALLOC-TOTAL TO WS-FLAT-OFFER.                                
                                                                                
       3510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3520-COMPUTE-FOR-BTO.                                                    
      *-----------------------------------------------------------*             
           EVALUATE K813-NUM-FLAT-TYPE(1:1)                                     
              WHEN '1'                                                          
                  COMPUTE WS-ALLOC-TOTAL = WS-FLAT-OFFER     +                  
                                           K270-NUM-OFFER(1) +                  
                                           K270-NUM-OFFER(2)                    
                  MOVE WS-ALLOC-TOTAL TO WS-FLAT-OFFER                          
              WHEN '2'                                                          
                  COMPUTE WS-ALLOC-TOTAL = WS-FLAT-OFFER  +                     
                                           K270-NUM-OFFER(2)                    
                  MOVE WS-ALLOC-TOTAL TO WS-FLAT-OFFER                          
              WHEN '3'                                                          
                  ADD  K270-NUM-OFFER(3) TO WS-FLAT-OFFER                       
                  MOVE K270-NUM-OFFER(3) TO WS-ALLOC-TOTAL                      
              WHEN '4'                                                          
                  ADD  K270-NUM-OFFER(4) TO WS-FLAT-OFFER                       
                  MOVE K270-NUM-OFFER(4) TO WS-ALLOC-TOTAL                      
              WHEN '5'                                                          
                  ADD  K270-NUM-OFFER(6) TO WS-FLAT-OFFER                       
                  MOVE K270-NUM-OFFER(6) TO WS-ALLOC-TOTAL                      
              WHEN OTHER                                                        
                  DISPLAY 'BP13K813 - INVALID FLAT TYPE: '                      
                           K813-NUM-FLAT-TYPE                                   
           END-EVALUATE.                                                        
                                                                                
       3520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3530-COMPUTE-FOR-SE.                                                     
      *-----------------------------------------------------------*             
           COMPUTE WS-ALLOC-TOTAL = K270-NUM-OFFER(3) +                         
                                    K270-NUM-OFFER(4) +                         
                                    K270-NUM-OFFER(5) +                         
                                    K270-NUM-OFFER(6).                          
                                                                                
           MOVE WS-ALLOC-TOTAL TO WS-FLAT-OFFER.                                
                                                                                
       3530-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3540-COMPUTE-FOR-RM4.                                                    
      *-----------------------------------------------------------*             
           COMPUTE WS-ALLOC-TOTAL = K270-NUM-OFFER(4) +                         
                                    K270-NUM-OFFER(5).                          
                                                                                
           MOVE WS-ALLOC-TOTAL TO WS-FLAT-OFFER.                                
                                                                                
       3540-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3550-COMPUTE-FOR-COMBINE-SA.                                             
      *-----------------------------------------------------------*             
           COMPUTE WS-ALLOC-TOTAL = K270-NUM-OFFER(1) +                         
                                    K270-NUM-OFFER(2).                          
                                                                                
           MOVE WS-ALLOC-TOTAL TO WS-FLAT-OFFER.                                
                                                                                
       3550-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3700-READ-BM06K100.                                                      
      *************************************************************             
           INITIALIZE K100-REC.                                                 
           MOVE K270-ESTATE               TO K100-ESTATE.                       
           MOVE K270-NEIGHBOURHOOD        TO K100-NEIGHBOURHOOD.                
           MOVE K270-CONTRACT-NO          TO K100-CONTRACT-NO.                  
           MOVE K270-NUM-BLK              TO K100-BLK-NO.                       
                                                                                
           READ BM06K100                                                        
           EVALUATE K100-STATUS                                                 
                                                                                
           WHEN 00                                                              
              MOVE K100-CDE-BLK  TO WS-CDE-BLK                                  
              ADD 1 TO WS-K100-READ                                             
              IF K100-DTE-OCS-DUE = SPACES OR LOW-VALUES OR ZEROES              
                 MOVE ZEROES TO WS-OCS-DUE-DATE                                 
              ELSE                                                              
                 MOVE K100-DTE-OCS-DUE TO WS-OCS-DUE-DATE                       
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              MOVE SPACES        TO WS-CDE-BLK                                  
              DISPLAY 'REC NOTFD IN BM06K100 = '                                
                       K100-KEY-FLD OF K100-REC                                 
                                                                                
           WHEN OTHER                                                           
              MOVE K100-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR READING BM06K100 FILE, ' K100-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                         
                                                                                
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3750-COMPUTE-DATE.                                                       
      *************************************************************             
           IF WS-WORK-END-DATE = SPACES OR LOW-VALUES                           
              MOVE SPACES      TO WS-K278-DTE-END                               
           ELSE                                                                 
              PERFORM 3751-NEXT-DAY THRU 3751-EXIT                              
                UNTIL WS-VALID-NEXT-DAY = 'Y'                                   
           END-IF.                                                              
                                                                                
       3750-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3751-NEXT-DAY.                                                           
      *************************************************************             
                                                                                
           MOVE WS-WORK-END-DATE TO WS-K278-DTE-END.                            
           COMPUTE WS-K278-DTE-END-9 =                                          
             FUNCTION DATE-OF-INTEGER(                                          
             FUNCTION INTEGER-OF-DATE(WS-K278-DTE-END-9) + 1).                  
           PERFORM 3752-CHECK-HD THRU 3752-EXIT.                                
                                                                                
       3751-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3752-CHECK-HD.                                                           
      *************************************************************             
           MOVE  WS-K278-DTE-END-9     TO WS-IN-DATE.                           
           MOVE 'HD'                   TO WS-IN-OPTION.                         
           MOVE '000'                  TO WS-RETURN.                            
                                                                                
           CALL 'AV02C002' USING LINK-REC.                                      
                                                                                
           IF WS-RETURN = 'EID'                                                 
              DISPLAY 'ERROR IN DATE : ' WS-IN-DATE                             
           END-IF.                                                              
                                                                                
           IF WS-RET-DAY = 'HOL'                                                
              DISPLAY 'HOLIDAY       : ' WS-IN-DATE                             
           ELSE                                                                 
              MOVE 'Y' TO WS-VALID-NEXT-DAY                                     
           END-IF.                                                              
                                                                                
           MOVE WS-K278-DTE-END TO WS-WORK-END-DATE.                            
                                                                                
       3752-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4000-WRITE-BP13K278.                                                     
      *************************************************************             
                                                                                
           INITIALIZE BP13K278-REC.                                             
           MOVE SPACES TO BP13K278-REC.                                         
                                                                                
           PERFORM 4100-GET-STREET-NAME THRU 4100-EXIT.                         
                                                                                
           IF (WS-OCS-DUE-DATE >= WS-WORK-END-DATE)                             
               MOVE 'Y' TO K278-NUM-OCS                                         
           ELSE                                                                 
               MOVE 'N' TO K278-NUM-OCS                                         
           END-IF.                                                              
                                                                                
           MOVE F205-NUM-NT-ZONE          TO K278-NUM-NT-ZONE.                  
           MOVE F205-DTE-ALLOCN           TO K278-DTE-ALLOCN.                   
           MOVE F205-NUM-FLAT-TYPE        TO K278-NUM-FLAT-TYPE.                
           MOVE K100-CDE-BLK              TO K278-CDE-BLK.                      
           MOVE K100-ESTATE               TO K278-NUM-ESTATE.                   
           MOVE K100-NEIGHBOURHOOD        TO K278-NUM-NEIGHBOUR.                
           MOVE K100-CONTRACT-NO          TO K278-NUM-CONTR.                    
           MOVE K100-BLK-NO               TO K278-NUM-BLK.                      
           MOVE WS-K278-DTE-END           TO K278-DTE-END-BKAPPMT.              
           MOVE 'P13C248'                 TO K278-NUM-USERID.                   
           MOVE FUNCTION CURRENT-DATE     TO K278-DTE-UPDATE.                   
           PERFORM 4050-WRITE-BP13K278  THRU 4050-EXIT.                         
                                                                                
           IF F205-NUM-FLAT-TYPE = 'SE'                                         
              MOVE 0 TO WS-SERS-FLAT                                            
              COMPUTE WS-SERS-FLAT = K270-NUM-OFFER(4) +                        
                                     K270-NUM-OFFER(5)                          
              IF WS-SERS-FLAT > 0                                               
                 MOVE '4 ' TO K278-NUM-FLAT-TYPE                                
                 PERFORM 4050-WRITE-BP13K278 THRU 4050-EXIT                     
              END-IF                                                            
                                                                                
              IF K270-NUM-OFFER(6) > 0                                          
                 MOVE '5 ' TO K278-NUM-FLAT-TYPE                                
                 PERFORM 4050-WRITE-BP13K278 THRU 4050-EXIT                     
              END-IF                                                            
                                                                                
              IF K270-NUM-OFFER(3) > 0                                          
                 MOVE '3 ' TO K278-NUM-FLAT-TYPE                                
                 PERFORM 4050-WRITE-BP13K278 THRU 4050-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       4050-WRITE-BP13K278.                                                     
      *************************************************************             
                                                                                
           WRITE BP13K278-REC.                                                  
                                                                                
           IF K278-STATUS = 00                                                  
              ADD 1                       TO WS-K278-WRITE                      
           ELSE                                                                 
              IF K278-STATUS = 22                                               
                 MOVE 'UPDK278'           TO K278-NUM-USERID                    
                 REWRITE BP13K278-REC                                           
                 IF K278-STATUS NOT = 00                                        
                    MOVE K278-STATUS      TO RETURN-CODE                        
                    DISPLAY 'ERROR IN UPDATING BP13K278'                        
                    DISPLAY 'K278-KEY-FLD = ' K278-KEY-FLD                      
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
                 END-IF                                                         
                 ADD 1                    TO WS-K278-UPDATE                     
              ELSE                                                              
                 MOVE K278-STATUS         TO RETURN-CODE                        
                 DISPLAY 'ERROR WRITING BP13K278 FILE, ' K278-STATUS            
                 DISPLAY 'K278-KEY-FLD = ' K278-KEY-FLD                         
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4100-GET-STREET-NAME.                                                    
      *************************************************************             
      *--> GET STREET NAME                                                      
      *--> AS PIDB INTERFACE TABLE IS BY UNIT BASIS, CATER FOR -811             
      *    TO AVOID ABEND.                                                      
                                                                                
           EXEC SQL SELECT NME_STREET                                           
                    INTO :DCLPIDB-INTERFACE.NME-STREET                          
                    FROM PIDB_INTERFACE                                         
                    WHERE NUM_BLDNG_GL = :WS-CDE-BLK                            
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
           WHEN -811                                                            
              MOVE NME-STREET  TO K278-NME-STREET                               
           WHEN 100                                                             
              DISPLAY 'REC NOTFD IN PIDB INTERFACE TABLE = ' WS-CDE-BLK         
              MOVE SPACES      TO K278-NME-STREET                               
              GO TO 4100-EXIT                                                   
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READING PIDB INTERFACE TABLE '                  
              GO TO 9900-ABORT-PROCESS                                          
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-FILES.                                                        
      *************************************************************             
           DISPLAY '***************** BP13C248 *****************'.              
           DISPLAY 'TOTAL RECORDS READ    (BP13F205) = ' WS-F205-READ.          
           DISPLAY 'TOTAL RECORDS WRITTEN (BP13K278) = ' WS-K278-WRITE.         
           DISPLAY 'TOTAL RECORDS UPDATED (BP13K278) = ' WS-K278-UPDATE.        
                                                                                
           CLOSE BP13F205                                                       
                 BP13K270                                                       
                 BP13K210                                                       
                 BP13K278                                                       
                 BM06K100.                                                      
                                                                                
           CALL 'CAV02001' USING LINK-REC.                                      
                                                                                
           CALL 'CP13K813' USING WS-RETURN-CODE                                 
           IF WS-RETURN-CODE NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K813, STATUS: '                        
                       WS-RETURN-CODE                                           
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9900-ABORT-PROCESS.                                                      
      *************************************************************             
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE       TO RETURN-CODE.                                   
           GOBACK.                                                              
                                                                                
