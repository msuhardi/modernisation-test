      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C02G.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DTE WRITTEN.   25JUL2006.                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      **********************************************************                
      *                                                        *                
      *    OBJECTIVES  :                                       *                
      *                                                        *                
      *    1.  TO CREATE FTP FILE FOR DEVELOPER,UNIT FILE      *                
      *                                                        *                
      *    INPUT FILES : BP13FA30 - DBS FILE                   *                
      *                  BP13FB78 - DBS FILE                   *                
      *                  BP13KB20 - DBS FILE                   *                
      *                                                        *                
      *    OUTPUT FILE : BP13FB62 - FTP FILE FOR SIM LIAN      *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY    DATE   DETAILS                          *                
      * ======= ====  ======  ================================ *                
      * BP132888 BA2  190706  NEW PROGRAME.                    *                
      * BP132914 BA2  070906  MOVE '=' TO PROJ,BLK,UNIT,AND    *                
      *                       LEVEL FIELDS INSTEAD OF SPACES.  *                
      * BP133106 LMS1 171207  EXPANSION OF UNIT TYPE FROM 4    *                
      *                       TO 7.                            *                
      * BP133323 MSD1 040608  INCLUDE USERID,DATE,&TIME UPDATE *                
      *          ZDD1 250309  ADD BP13KB20 AS   INPUT FILE     *                
      *                       AND TRANSFER CHECK, SERIAL AND   *                
      *                       BANK NAME TO BP13FB62            *                
      * ====================================================== *                
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-9121.                                               
       OBJECT-COMPUTER. IBM-9121.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA30 ASSIGN    TO BP13FA30.                               
                                                                                
           SELECT BP13FB62 ASSIGN    TO BP13FB62                                
                   FILE STATUS IS WS-BP13FB62-STAT.                             
                                                                                
           SELECT BP13KB20 ASSIGN    TO BP13KB20                                
                   ORGANIZATION      IS INDEXED                                 
                   RECORD KEY        IS KB20-KEY-FLD                            
                   FILE STATUS       IS WS-BP13KB20-STAT                        
                   ACCESS MODE       IS DYNAMIC.                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD   BP13FA30                                                            
            BLOCK      CONTAINS 0    RECORDS                                    
            RECORD     CONTAINS 200  CHARACTERS                                 
            LABEL      RECORDS  ARE  STANDARD                                   
            RECORDING  MODE     IS  F.                                          
       COPY BP13FA30.                                                           
                                                                                
                                                                                
       FD   BP13FB62                                                            
            BLOCK     CONTAINS 0   RECORDS                                      
            RECORD    CONTAINS 180 CHARACTERS                                   
            LABEL     RECORDS  ARE OMITTED                                      
            RECORDING MODE     IS  F.                                           
       COPY BP13FB62.                                                           
                                                                                
       FD   BP13KB20 RECORD CONTAINS 3300 CHARACTERS.                           
       COPY BP13KB20.                                                           
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       COPY IDPTABLE.                                                           
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-FA30-EOF-SW             PIC X(1)    VALUE 'N'.                
           05  WS-BP13KB20-STAT           PIC X(02) VALUE SPACES.               
           05  WS-BP13FB62-STAT           PIC X(02) VALUE SPACES.               
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-FA30                PIC 9(5) COMP VALUE 0.                
           05  WS-CNT-FB62                PIC 9(5) COMP VALUE 0.                
           05  WS-CNT-KB20                PIC 9(5) COMP VALUE 0.                
                                                                                
       01  WS-DATES.                                                            
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD           PIC 9(2).                             
               10  FILLER                 PIC X(1)    VALUE '/'.                
               10  WS-EDITED-MM           PIC 9(2).                             
               10  FILLER                 PIC X(1)    VALUE '/'.                
               10  WS-EDITED-YY           PIC 9(4).                             
           05  WS-CURRENT-DATE.                                                 
               10  WS-CURR-CCYY.                                                
                   15  WS-CURR-YY         PIC 9(4).                             
               10  WS-CURR-MM             PIC 9(2).                             
               10  WS-CURR-DD             PIC 9(2).                             
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-TODAY-DATE.                                                   
               10  WS-SYS-YY              PIC 9(4).                             
               10  WS-SYS-MM              PIC 9(2).                             
               10  WS-SYS-DD              PIC 9(2).                             
           05  WS-TIME-NUM                PIC 9(9).                             
           05  WS-TIME REDEFINES WS-TIME-NUM.                                   
               10  WS-HH                  PIC 9(2).                             
               10  WS-MM                  PIC 9(2).                             
               10  WS-SS                  PIC 9(2).                             
               10  WS-GG                  PIC 9(2).                             
               10  WS-SG                  PIC 9(1).                             
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-FOUND-SW                PIC X(1)    VALUE 'N'.                
           05  WS-EQL-VALUE               PIC X(5)    VALUE SPACES.             
           05  WS-CTR                     PIC 9(1)    VALUE ZEROES.             
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
           PERFORM  1000-OPEN-FILES         THRU 1000-EXIT.                     
           PERFORM  2000-MAIN-ROUTINE       THRU 2000-EXIT.                     
           PERFORM  9000-CLOSE-FILES        THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13FA30                                                 
                       BP13KB20                                                 
                OUTPUT BP13FB62.                                                
                                                                                
           IF WS-BP13KB20-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN OPENING BP13KB20   STATUS - '                   
              WS-BP13KB20-STAT                                                  
              MOVE WS-BP13KB20-STAT             TO RETURN-CODE                  
              PERFORM 9000-CLOSE-FILES          THRU 9000-EXIT                  
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE                         
           MOVE   WS-SYS-YY                TO    WS-EDITED-YY                   
                                                 WS-CURR-YY.                    
           MOVE   WS-SYS-MM                TO    WS-EDITED-MM                   
                                                 WS-CURR-MM.                    
           MOVE   WS-SYS-DD                TO    WS-EDITED-DD                   
                                                 WS-CURR-DD.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
      ******************************************************************        
           PERFORM  3000-BP13FA30-READ     THRU 3000-EXIT.                      
           PERFORM  4000-PROCESS-FA30-REC  THRU 4000-EXIT                       
              UNTIL WS-FA30-EOF-SW = 'Y'.                                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-BP13FA30-READ.                                                      
      ******************************************************************        
           READ BP13FA30                                                        
              AT END MOVE 'Y'     TO WS-FA30-EOF-SW                             
              GO TO  3000-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD +1                 TO WS-CNT-FA30.                               
           MOVE FA30-NUM-REGN TO KB20-NUM-REGN.                                 
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      **********************************************************                
       4000-PROCESS-FA30-REC.                                                   
      **********************************************************                
                                                                                
           IF ((FA30-NUM-PRJCT = SPACE OR LOW-VALUES) OR                        
              (FA30-DTE-ACCEPT = SPACE OR LOW-VALUES))                          
              PERFORM 3000-BP13FA30-READ THRU 3000-EXIT                         
              GO TO 4000-EXIT                                                   
           ELSE                                                                 
              PERFORM 4100-READ-BP13KB20    THRU 4100-EXIT                      
              PERFORM 4300-FB62-WRITE       THRU 4300-EXIT                      
              PERFORM 3000-BP13FA30-READ    THRU 3000-EXIT                      
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************************************************                
       4100-READ-BP13KB20.                                                      
      **********************************************************                
                                                                                
           READ BP13KB20.                                                       
                                                                                
           ADD 1 TO WS-CNT-KB20.                                                
                                                                                
           EVALUATE WS-BP13KB20-STAT                                            
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    MOVE SPACES TO FB62-NUM-CHEQUE1                             
                                   FB62-NME-BANK1                               
                                   FB62-NUM-CHEQUE2                             
                                   FB62-NME-BANK2                               
                                   FB62-NUM-SERIAL                              
                    INITIALIZE     FB62-NUM-CHEQUE1                             
                                   FB62-NME-BANK1                               
                                   FB62-NUM-CHEQUE2                             
                                   FB62-NME-BANK2                               
                                   FB62-NUM-SERIAL                              
               WHEN OTHER                                                       
                   DISPLAY 'FILE BP13KB20 START ERR ' KB20-NUM-REGN             
                   DISPLAY 'ERROR STATUS = ' WS-BP13KB20-STAT                   
                   MOVE WS-BP13KB20-STAT     TO   RETURN-CODE                   
                   PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
      **********************************************************                
       4300-FB62-WRITE.                                                         
      **********************************************************                
                                                                                
           MOVE SPACES TO BP13FB62-REC.                                         
           INITIALIZE     BP13FB62-REC.                                         
                                                                                
           PERFORM  8000-MOVE-DELIMITER   THRU 8000-EXIT.                       
                                                                                
           MOVE FA30-NUM-PRJCT              TO                                  
                WS-EQL-VALUE.                                                   
           PERFORM  5000-MOVE-EQUALS      THRU 5000-EXIT VARYING WS-CTR         
              FROM 1 BY 1 UNTIL WS-CTR > 3.                                     
           MOVE WS-EQL-VALUE                TO                                  
                FB62-NUM-PRJCT.                                                 
                                                                                
           MOVE FA30-NUM-BLK                TO                                  
                WS-EQL-VALUE.                                                   
           PERFORM  5000-MOVE-EQUALS      THRU 5000-EXIT VARYING WS-CTR         
              FROM 1 BY 1 UNTIL WS-CTR > 5.                                     
           MOVE WS-EQL-VALUE                TO                                  
                FB62-NUM-BLK.                                                   
                                                                                
           MOVE FA30-NUM-MAIN-UNIT          TO                                  
                WS-EQL-VALUE.                                                   
           PERFORM  5000-MOVE-EQUALS      THRU 5000-EXIT VARYING WS-CTR         
              FROM 1 BY 1 UNTIL WS-CTR > 4.                                     
           MOVE WS-EQL-VALUE                TO                                  
                FB62-NUM-MAIN-UNIT.                                             
                                                                                
           MOVE FA30-NUM-SUB-UNIT           TO                                  
                WS-EQL-VALUE.                                                   
           PERFORM  5000-MOVE-EQUALS      THRU 5000-EXIT VARYING WS-CTR         
              FROM 1 BY 1 UNTIL WS-CTR > 3.                                     
           MOVE WS-EQL-VALUE                TO                                  
                FB62-NUM-SUB-UNIT.                                              
                                                                                
           MOVE FA30-NUM-LEVEL              TO                                  
                WS-EQL-VALUE.                                                   
           PERFORM  5000-MOVE-EQUALS      THRU 5000-EXIT VARYING WS-CTR         
              FROM 1 BY 1 UNTIL WS-CTR > 2.                                     
           MOVE WS-EQL-VALUE                TO                                  
                FB62-NUM-LEVEL.                                                 
                                                                                
           MOVE FA30-DTE-ACCEPT             TO                                  
                FB62-DTE-ACCEPT.                                                
           MOVE FA30-TME-ACCEPT             TO                                  
                FB62-TME-ACCEPT.                                                
           MOVE FA30-NUM-ACCEPT             TO                                  
                FB62-NUM-ACCEPT.                                                
           MOVE FA30-NUM-ETHNIC             TO                                  
                FB62-NUM-ETHNIC.                                                
           MOVE FA30-AMT-TRNSCT-SELL-PRICE  TO                                  
                FB62-AMT-TRNSCT-SELL-PRICE.                                     
           MOVE FA30-AMT-OPTION-FEE         TO                                  
                FB62-AMT-OPTION-FEE.                                            
           MOVE FA30-NUM-PRIO               TO                                  
                FB62-NUM-PRIO.                                                  
           MOVE FA30-NUM-REGN               TO                                  
                FB62-NUM-REGN.                                                  
           MOVE FA30-NUM-FLAT-TYPE          TO                                  
                FB62-NUM-DESIGN-TYPE.                                           
           MOVE FA30-CDE-UNIT-TYP           TO                                  
                FB62-NUM-UNIT-TYP.                                              
      *    MOVE FA30-NUM-SERIAL             TO                                  
                                                                                
           IF KB20-NUM-SERIAL NOT = SPACES AND LOW-VALUES                       
               MOVE KB20-NUM-SERIAL         TO                                  
                    FB62-NUM-SERIAL                                             
           END-IF.                                                              
                                                                                
      *    MOVE FA30-NUM-CHEQUE             TO                                  
                                                                                
           IF KB20-NUM-CHEQUE NOT = SPACES AND LOW-VALUES                       
               MOVE KB20-NUM-CHEQUE         TO                                  
                    FB62-NUM-CHEQUE1                                            
           END-IF.                                                              
                                                                                
           IF KB20-NUM-CHEQUE-2 NOT = SPACES AND LOW-VALUES                     
               MOVE KB20-NUM-CHEQUE-2       TO                                  
                FB62-NUM-CHEQUE2                                                
           END-IF.                                                              
                                                                                
      *    MOVE FA30-NME-BANK               TO                                  
                                                                                
           IF KB20-NME-BANK NOT  = SPACES AND LOW-VALUES                        
               MOVE KB20-NME-BANK           TO                                  
                    FB62-NME-BANK1                                              
           END-IF.                                                              
                                                                                
           IF KB20-NME-BANK-2 NOT =  SPACES AND LOW-VALUES                      
               MOVE KB20-NME-BANK-2         TO                                  
                FB62-NME-BANK2                                                  
           END-IF.                                                              
                                                                                
           MOVE FA30-DTE-UPDATE             TO                                  
                FB62-DTE-UPDATE.                                                
           MOVE FA30-TME-UPDATE             TO                                  
                FB62-TME-UPDATE.                                                
           MOVE FA30-NUM-USERID             TO                                  
                FB62-NUM-USERID.                                                
                                                                                
           WRITE BP13FB62-REC.                                                  
           ADD +1                 TO WS-CNT-FB62.                               
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       5000-MOVE-EQUALS.                                                        
      *-----------------------------------------------------------*             
                                                                                
           IF WS-EQL-VALUE(WS-CTR:1) = SPACES OR LOW-VALUES                     
              MOVE '='       TO  WS-EQL-VALUE(WS-CTR:1)                         
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       8000-MOVE-DELIMITER.                                                     
      *-----------------------------------------------------------*             
                                                                                
           MOVE ';'                      TO FB62-FIL01                          
                                            FB62-FIL02                          
                                            FB62-FIL03                          
                                            FB62-FIL04                          
                                            FB62-FIL05                          
                                            FB62-FIL06                          
                                            FB62-FIL07                          
                                            FB62-FIL08                          
                                            FB62-FIL09                          
                                            FB62-FIL10                          
                                            FB62-FIL11                          
                                            FB62-FIL12                          
                                            FB62-FIL13                          
                                            FB62-FIL14                          
                                            FB62-FIL15                          
                                            FB62-FIL16                          
                                            FB62-FIL17                          
                                            FB62-FIL18                          
                                            FB62-FIL19                          
                                            FB62-FIL20                          
                                            FB62-FIL21                          
                                            FB62-FIL22.                         
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       9000-CLOSE-FILES.                                                        
      **********************************************************                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C02G'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FA30 RECORDS READ..........: '                    
                    WS-CNT-FA30.                                                
           DISPLAY 'NO OF BP13FB62 RECORDS WRITTEN.......: '                    
                    WS-CNT-FB62.                                                
           DISPLAY 'NO OF BP13KB20 RECORDS READ..........: '                    
                    WS-CNT-KB20.                                                
                                                                                
           CLOSE BP13FA30                                                       
                 BP13KB20                                                       
                 BP13FB62.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
