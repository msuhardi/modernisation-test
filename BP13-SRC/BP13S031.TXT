/******************************************************************/            
/* SYSTEM OF COMMITMENT (SOC)                                     */            
/*================================================================*/            
/*                                                                */            
/* OBJECTIVES :                                                   */            
/*  OUTPUT FLATS REJECTED AFTER SOCA & BUILDING COMPLETED         */            
/*                                                                */            
/*----------------------------------------------------------------*/            
/* CHG REF  DATE     BY    DESCRIPTION                            */            
/* -------- -------- ----  -----------                            */            
/* BP132874 20060612 SSS2  NEW PROGRAM                            */            
/* BP133630 20090723 LMS1  ADDITIONAL FIELDS                      */            
/* BP135561 20150106 KAM4  ADDED POW TAG                          */            
/* BP135695 20150402 ESA1  TO ADD POF OPTION                      */            
/******************************************************************/            
                                                                                
  OPTIONS NODATE CAPS;                                                          
                                                                                
  DATA  BP13F310;                                                               
     INFILE BP13F310 END=EOF;                                                   
     INPUT @1    NUM_REGN        $CHAR8.                                        
           @42   CDE_BO          $CHAR2.                                        
           @9    TRANS           $CHAR2.                                        
           @61   CDE_NT          $CHAR3.                                        
           @17   HDBREF          $CHAR11.                                       
           @66   BLK             $CHAR5.                                        
           @71   NME_STR         $CHAR20.                                       
           @91   LEVEL           $CHAR2.                                        
           @94   MAIN_UNIT       $CHAR5.                                        
           @113  FT              $CHAR2.                                        
           @30   DTE_REJ         $CHAR8.                                        
           ;                                                                    
                                                                                
      LENGTH K110KEY $11.;                                                      
      K110KEY=HDBREF;                                                           
                                                                                
      INFILE BM06K110 VSAM KEY=K110KEY;                                         
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
         INPUT @437 POD    $CHAR1.                                              
               @444 POF    $CHAR1.                                              
               @485 POW    $CHAR1.                                              
               @486 POF_O  $CHAR1.                                              
               @251 HND    $CHAR8.;                                             
      ELSE                                                                      
         DO;                                                                    
            _ERROR_=0;                                                          
            FILE LOG;                                                           
            PUT @1 'HDBREF NOTFND IN K110 ' K110KEY;                            
         END;                                                                   
                                                                                
   PROC SORT DATA=BP13F310; BY HDBREF;                                          
                                                                                
   DATA FLAT1;                                                                  
      SET BP13F310;BY HDBREF;                                                   
      HSENO='#'||LEVEL||'-'||MAIN_UNIT;                                         
                                                                                
      IF FT IN ('01','02','03','04','05') THEN                                  
         DO;                                                                    
         JOB_CODE = '0650';                                                     
         GL_ACCT = '0000000203507';                                             
         END;                                                                   
      ELSE                                                                      
         IF FT = '06' THEN                                                      
            DO;                                                                 
            JOB_CODE = '0715';                                                  
            GL_ACCT = '8202300407136';                                          
            END;                                                                
                                                                                
   PROC SORT DATA=FLAT1; BY CDE_BO CDE_NT BLK HDBREF ;                          
                                                                                
   DATA _NULL_;                                                                 
   SET FLAT1  END=EOF;                                                          
   FILE BP13FOUT NOTITLES;                                                      
      CNT+1;                                                                    
      IF CNT=1 THEN                                                             
         LINK HEADER1;                                                          
                                                                                
      PUT @1     NUM_REGN                                                       
          @10    CDE_NT                                                         
          @14    BLK                                                            
          @20    HSENO                                                          
          @30    NME_STR                                                        
          @51    FT                                                             
          @54    POD                                                            
          @57    POF                                                            
          @57    POF                                                            
          @60    POF_O                                                          
          @65    POW                                                            
          @68    TRANS                                                          
          @73    HDBREF                                                         
          @86    DTE_REJ                                                        
          @96    JOB_CODE                                                       
          @103   GL_ACCT                                                        
          @116   TRANS                                                          
          @121   HND                                                            
          @130   CDE_BO                                                         
          ;                                                                     
      RETURN;                                                                   
                                                                                
   HEADER1:                                                                     
      PUT @1    'REGN'                                                          
          @10   'NT'                                                            
          @14   'BLK'                                                           
          @20   'UNIT'                                                          
          @30   'STREET'                                                        
          @51   'FT'                                                            
          @54   'POD'                                                           
          @57   'POF'                                                           
          @60   'POF_O'                                                         
          @65   'POW'                                                           
          @68   'TRANS'                                                         
          @73   'HDB_REF'                                                       
          @86   'REJ DTE'                                                       
          @96   'CODE'                                                          
          @103  'GL_ACCT'                                                       
          @116  'ATAG'                                                          
          @121  'HANDOVER'                                                      
          @130  'BO'                                                            
          ;                                                                     
      RETURN;                                                                   
                                                                                
