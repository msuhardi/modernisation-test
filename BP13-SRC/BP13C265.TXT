       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C265.                                                  
       AUTHOR.       REY ORBITA GASTADOR.                                       
       DATE-WRITTEN. 05 JAN 1996.                                               
                                                                                
      ******************************************************************        
      * APPLICATION : SYSTEM OF COMMITMENT                                      
      *                                                                         
      * ================================================================        
      * OBJECTIVES :                                                            
      * TO PRINT REPORT ON UPDATE OF JUNK MAIL TAG                              
      *                                                                         
      * ================================================================        
      * FILE DESCRIPTION :                                                      
      *                                                                         
      *  INPUT                                                                  
      *  1. BM06F900                                                            
      *  2. BM06K510                                                            
      *  3. BB14K500                                                            
      *                                                                         
      *  OUTPUT                                                                 
      *  1. BP13L265                                                            
      *                                                                         
      *=================================================================        
      * MODIFICATIONS :                                                         
      *                                                                         
      * CHGE      DATE   OIC  DESCRIPTION                                       
      * -------  ------  ---  -----------                                       
      * BP130046 161295                                                         
      * BP130418 040398  SIA  CHGED BM06F900 TO BM06F190                        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BM06F190 ASSIGN TO BM06F190.                                  
           SELECT BM06K510 ASSIGN TO BM06K510                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K510-ESTCDE                          
                           FILE STATUS  IS K510-FS.                             
           SELECT BB14K500 ASSIGN TO BB14K500                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K500-KEY-FLD                         
                           FILE STATUS  IS K500-FS.                             
           SELECT BP13L265 ASSIGN TO BP13L265.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BM06K510                                                            
            RECORD CONTAINS 60 CHARACTERS.                                      
       COPY BM06K510.                                                           
                                                                                
       FD   BM06F190                                                            
            BLOCK CONTAINS  0 RECORDS                                           
            RECORD CONTAINS 650 CHARACTERS                                      
            LABEL RECORD IS OMITTED.                                            
       COPY BM06F190.                                                           
                                                                                
       FD   BB14K500                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BB14K500.                                                           
                                                                                
       FD  BP13L265                                                             
           BLOCK CONTAINS  0 RECORDS                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORD IS OMITTED                                              
           RECORDING MODE IS F.                                                 
       01  BP13L265-REC   PIC X(132).                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-WORKING-AREA.                                                     
           05  WS-FLAGS.                                                        
               10 F190-EOF             PIC X.                                   
                  88 F190-IS-EOF       VALUE 'Y'.                               
           05  WS-FILE-STATUS.                                                  
               10 K510-FS              PIC 99.                                  
               10 K500-FS              PIC 99.                                  
           05  WS-COUNTERS.                                                     
               10 WS-SERIAL            PIC 9(05)     VALUE ZEROES.              
               10 F190-READ-CTR        PIC 9(04)     VALUE ZEROES.              
               10 K510-READ-CTR        PIC 9(04)     VALUE ZEROES.              
               10 K500-READ-CTR        PIC 9(04)     VALUE ZEROES.              
               10 WS-LINE-CTR          PIC 9(04)     VALUE 61.                  
               10 WS-PAGE-CTR          PIC 9(04)     VALUE ZEROES.              
           05  WS-MISC.                                                         
               10 WS-PAGE-DATE         PIC X(10).                               
                                                                                
       01  WS-CURR-DATE                PIC X(8).                                
                                                                                
       01  WS-PRINT-AREA.                                                       
           05 WS-PRINT-LINE            PIC X(132).                              
           05 WS-PAGE-HDG1   REDEFINES WS-PRINT-LINE.                           
              10 WS-HEADER-A           PIC X(12).                               
              10 WS-HEADER-B           PIC X(77).                               
              10 WS-HEADER-C           PIC X(10).                               
              10 WS-HEADER-D           PIC X(07).                               
              10 WS-HEADER-E           PIC X(15).                               
              10 WS-HEADER-F           PIC X(07).                               
              10 WS-HEADER-G           PIC X(04).                               
              10 WS-HDER-RED  REDEFINES WS-HEADER-G.                            
                 15 WS-HDER-G          PIC ZZZ9.                                
           05 WS-PAGE-HDG2   REDEFINES WS-PRINT-LINE.                           
              10 FILLER                PIC X(39).                               
              10 WS-SYSTEM-NAME        PIC X(60).                               
              10 WS-USER               PIC X(33).                               
           05 WS-SUBHDG     REDEFINES  WS-PRINT-LINE.                           
              10 FILLER                PIC X(42).                               
              10 WS-SUBHDG-A           PIC X(90).                               
           05 WS-COLUMN      REDEFINES  WS-PRINT-LINE.                          
              10 WS-SERIAL-NO          PIC X(13).                               
              10 WS-SER-RED  REDEFINES  WS-SERIAL-NO.                           
                 15 FILLER             PIC X(03).                               
                 15 WS-SERIAL-FLD      PIC ZZZZ9.                               
                 15 FILLER             PIC X(05).                               
              10 WS-EST-NAME           PIC X(28).                               
              10 WS-EST-CODE           PIC X(07).                               
              10 WS-NEIGHBOUR          PIC X(08).                               
              10 WS-CONTRACT           PIC X(07).                               
              10 WS-BLOCK              PIC X(08).                               
              10 WS-LOT-NO             PIC X(23).                               
              10 WS-MUKIM-NO           PIC X(11).                               
              10 WS-MUKIM-RED  REDEFINES  WS-MUKIM-NO.                          
                 15 FILLER             PIC X(02).                               
                 15 WS-MUKIM-FLD       PIC X(09).                               
              10 WS-JUNK-MAIL-TAG      PIC X(13).                               
              10 WS-TAG-RED    REDEFINES  WS-JUNK-MAIL-TAG.                     
                 15 FILLER             PIC X(06).                               
                 15 WS-JUNK-MAIL-FLD   PIC X(07).                               
              10 FILLER                PIC X(14).                               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *====================                                                     
       0000-MAIN-LOGIC.                                                         
      *====================                                                     
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BM06F190 THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS-RTN   THRU 3000-EXIT                            
                   UNTIL F190-IS-EOF.                                           
           PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                           
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================                                                  
       1000-OPEN-FILES.                                                         
      *=======================                                                  
           OPEN INPUT  BM06F190                                                 
                       BM06K510                                                 
                       BB14K500                                                 
                OUTPUT BP13L265.                                                
                                                                                
           IF K510-FS NOT = ZEROES AND 97                                       
              DISPLAY 'BM06K510 - OPEN ERROR, FS = ' K510-FS                    
              GO TO    9999-CLOSE-FILES                                         
           END-IF.                                                              
                                                                                
           IF K500-FS NOT = ZEROES AND 97                                       
              DISPLAY 'BB14K500 - OPEN ERROR, FS = ' K500-FS                    
              GO TO    9999-CLOSE-FILES                                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           STRING WS-CURR-DATE (7:2) '/'                                        
                  WS-CURR-DATE (5:2) '/'                                        
                  WS-CURR-DATE (1:4)                                            
                  DELIMITED BY SIZE                                             
             INTO WS-PAGE-DATE.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================                                                
       2000-READ-BM06F190.                                                      
      *=========================                                                
           READ BM06F190 AT END                                                 
                MOVE 'Y' TO F190-EOF.                                           
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       3000-PROCESS-RTN.                                                        
      *============================                                             
           ADD        1 TO F190-READ-CTR.                                       
           PERFORM 4100-PRINT-DETAIL  THRU 4100-EXIT.                           
           PERFORM 2000-READ-BM06F190 THRU 2000-EXIT.                           
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       4000-WRITE-HEADING.                                                      
      *============================                                             
           ADD  1                      TO WS-PAGE-CTR.                          
                                                                                
           MOVE  SPACES                TO WS-PRINT-LINE.                        
           MOVE 'REPORT ID:'           TO WS-HEADER-A.                          
           MOVE 'BP13L265'             TO WS-HEADER-B.                          
           MOVE 'HDB3'                 TO WS-HEADER-C.                          
           MOVE 'DATE:'                TO WS-HEADER-D.                          
           MOVE  WS-PAGE-DATE          TO WS-HEADER-E.                          
           MOVE 'PAGE:'                TO WS-HEADER-F.                          
           MOVE  WS-PAGE-CTR           TO WS-HDER-G.                            
           WRITE BP13L265-REC        FROM WS-PAGE-HDG1 AFTER PAGE.              
                                                                                
           MOVE SPACES                  TO WS-PRINT-LINE.                       
           MOVE 'S Y S T E M   O F   C O M M I T M E N T'                       
                                        TO WS-SYSTEM-NAME.                      
           MOVE 'USER:  ALLOCATION TEAM' TO WS-USER.                            
           WRITE BP13L265-REC         FROM WS-PAGE-HDG2.                        
                                                                                
           MOVE SPACES                 TO WS-PRINT-LINE.                        
           MOVE 'REPORT ON UPDATE OF JUNK MAIL TAG'  TO WS-SUBHDG-A.            
           WRITE BP13L265-REC        FROM WS-SUBHDG.                            
                                                                                
           MOVE SPACES                 TO WS-PRINT-LINE.                        
           MOVE ' SERIAL NO'           TO WS-SERIAL-NO.                         
           MOVE 'ESTATE NAME'          TO WS-EST-NAME.                          
           MOVE 'NEIGH'                TO WS-NEIGHBOUR.                         
           MOVE 'CONT'                 TO WS-CONTRACT.                          
           MOVE 'BLOCK'                TO WS-BLOCK.                             
           MOVE 'LOT NO'               TO WS-LOT-NO.                            
           MOVE 'MUKIM NO'             TO WS-MUKIM-NO                           
           MOVE 'JUNK MAIL TAG'        TO WS-JUNK-MAIL-TAG.                     
           WRITE BP13L265-REC        FROM WS-COLUMN AFTER 2.                    
                                                                                
           MOVE SPACES                 TO WS-PRINT-LINE.                        
           MOVE ' ---------'           TO WS-SERIAL-NO.                         
           MOVE '-----------'          TO WS-EST-NAME.                          
           MOVE '-----'                TO WS-NEIGHBOUR.                         
           MOVE '----'                 TO WS-CONTRACT.                          
           MOVE '-----'                TO WS-BLOCK.                             
           MOVE '------'               TO WS-LOT-NO.                            
           MOVE '--------'             TO WS-MUKIM-NO                           
           MOVE '-------------'        TO WS-JUNK-MAIL-TAG.                     
           WRITE BP13L265-REC        FROM WS-COLUMN.                            
                                                                                
           MOVE  SPACES                TO WS-PRINT-LINE.                        
           WRITE BP13L265-REC        FROM WS-PRINT-LINE.                        
                                                                                
           MOVE  7                     TO WS-LINE-CTR.                          
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       4100-PRINT-DETAIL.                                                       
      *============================                                             
           IF WS-LINE-CTR > 60                                                  
              PERFORM 4000-WRITE-HEADING THRU 4000-EXIT                         
           END-IF.                                                              
           MOVE SPACES                  TO WS-PRINT-LINE.                       
                                                                                
           ADD  1                       TO WS-SERIAL.                           
           MOVE WS-SERIAL               TO WS-SERIAL-FLD.                       
                                                                                
           MOVE F190-2-ESTATE           TO K510-ESTCDE.                         
           READ BM06K510.                                                       
           EVALUATE K510-FS                                                     
           WHEN 00                                                              
             MOVE K510-ESTATE-NAME   TO WS-EST-NAME                             
             ADD  1                  TO K510-READ-CTR                           
           WHEN 23                                                              
             DISPLAY 'BM06K510 - RECORD NOT FOUND, KEY = ' F190-2-ESTATE        
           WHEN OTHER                                                           
             DISPLAY 'BM06K510 - READ ERROR'                                    
           END-EVALUATE.                                                        
                                                                                
           STRING '(' F190-2-ESTATE ')'                                         
             DELIMITED BY SIZE INTO WS-EST-CODE.                                
                                                                                
           MOVE F190-2-NEIGHBOURHOOD    TO WS-NEIGHBOUR.                        
           MOVE F190-2-CONTRACT-NO      TO WS-CONTRACT.                         
           MOVE F190-2-BLK-NO           TO WS-BLOCK.                            
                                                                                
           MOVE F190-CDE-BLK            TO K500-KEY-FLD.                        
           READ BB14K500.                                                       
           EVALUATE K500-FS                                                     
           WHEN 00                                                              
             MOVE K500-NUM-LAND-LOT     TO WS-LOT-NO                            
             MOVE K500-NUM-MUKIM-TS     TO WS-MUKIM-FLD                         
             ADD  1                     TO K500-READ-CTR                        
           WHEN 23                                                              
             DISPLAY 'BB14K500 - RECORD NOT FOUND, KEY = ' F190-2-BLK-NO        
           WHEN OTHER                                                           
             DISPLAY 'BB14K500 - READ ERROR'                                    
           END-EVALUATE.                                                        
                                                                                
           MOVE F190-NUM-JUNK-MAIL    TO WS-JUNK-MAIL-FLD.                      
                                                                                
           WRITE BP13L265-REC   FROM WS-COLUMN.                                 
           ADD   1                TO WS-LINE-CTR.                               
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================                                                  
       9999-CLOSE-FILES.                                                        
      *=======================                                                  
           DISPLAY '*****************'.                                         
           DISPLAY 'PROGRAM: BP13C265'.                                         
           DISPLAY '*****************'.                                         
           DISPLAY 'FILE STATISTICS'.                                           
           DISPLAY '@BM06F190'.                                                 
           DISPLAY '  =>RECORDS READ    = ' F190-READ-CTR.                      
           DISPLAY '@BM06K510'.                                                 
           DISPLAY '  =>RECORDS READ    = ' K510-READ-CTR.                      
           DISPLAY '@BB14K500'.                                                 
           DISPLAY '  =>RECORDS READ    = ' K500-READ-CTR.                      
           CLOSE BM06F190                                                       
                 BM06K510                                                       
                 BB14K500                                                       
                 BP13L265.                                                      
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
