       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY83.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  14/06/2011.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO READ BP13F595 AND START BROWSE BP13K828         *        
      *                                                                *        
      * INPUT  FILES : BP13F595                                        *        
      *                BP13K828                                        *        
      * OUTPUT FILES : BP13LY83                                        *        
      *                P13F595A                                        *        
      *                                                                *        
      * CHG-NO   BY    ON    - DESCRIPTION                             *        
      * -------------------------------------------------------------- *        
      * BP134255 PCL3 14/06/11 NEW PROGRAM                             *        
      * BP135350 SMR2 30/05/14 BYPASS NON-CITIZEN MOTHER, FATHER,      *        
      *                        BROTHER/SISTER & BRO/SIS-IN-LAW         *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   BP13F595      ASSIGN TO BP13F595.                           
           SELECT   P13F595A      ASSIGN TO P13F595A.                           
                                                                                
           SELECT   BP13K828      ASSIGN TO BP13K828                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K828-KEY-FLD                        
                             FILE STATUS IS K828-STATUS.                        
                                                                                
           SELECT   BP13LY83      ASSIGN TO BP13LY83.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F595                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F595.                                                           
                                                                                
       FD  BP13K828                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K828.                                                           
                                                                                
       FD   BP13LY83                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 BP13LY83-REC  PIC X(132).                                             
                                                                                
       FD   P13F595A                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 P13F595A-REC  PIC X(500).                                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  LY83-HEADER01.                                                       
           05  LY83-TITLE-HDR      PIC X(17)     VALUE 'BP13LY83'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LY83-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LY83-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LY83-HEADER02.                                                       
           05  FILLER              PIC X(45)     VALUE SPACES.                  
           05  FILLER              PIC X(52)     VALUE                          
               'REPORT ON NON-CITIZEN SPOUSE'.                                  
                                                                                
       01  LY83-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(07)    VALUE '    S/N'.                
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'NRIC'.                   
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE 'RELN '.                  
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE 'CTZ'.                    
           05  FILLER              PIC X(02)    VALUE SPACES.                   
                                                                                
       01  LY83-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LY83-SN             PIC ZZZZZZ9      VALUE ZEROES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LY83-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LY83-NRIC           PIC X(09)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LY83-RELN           PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LY83-CTZ            PIC X(03)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F595-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-WRITE                    PIC 9(7)  VALUE ZEROS.              
           05  CNT-WRITE1                   PIC 9(7)  VALUE ZEROS.              
           05  WS-SNO                       PIC 9(7)  VALUE ZEROS.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  K828-STATUS                  PIC 99    VALUE ZEROES.             
           05  WS-SPOUSE-FD                 PIC X(1)  VALUE 'N'.                
           05  WS-K828-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-F595-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-K828-FND                  PIC X(01) VALUE SPACE.              
               88  K828-FND                 VALUE 'Y'.                          
           05 WS-SYSTEM-DATE                PIC 9(8)  VALUE ZEROES.             
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      *===============================================================*         
       0000-MAIN.                                                               
      *===============================================================*         
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F595       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL WS-F595-EOF = 'Y'.                                           
                                                                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       1000-OPEN-FILES.                                                         
      *===============================================================*         
                                                                                
           OPEN INPUT BP13F595                                                  
                      BP13K828                                                  
               OUTPUT P13F595A                                                  
                      BP13LY83.                                                 
                                                                                
                                                                                
           IF K828-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K828 , STATUS = ' K828-STATUS          
              MOVE K828-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM 2100-PRINT-HEADER       THRU 2100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       2000-READ-BP13F595.                                                      
      *===============================================================*         
                                                                                
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                TO WS-F595-EOF                          
                GO                      TO 2000-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO CNT-F595-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       2100-PRINT-HEADER.                                                       
      *===============================================================*         
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)          TO WS-SYSTEM-DATE.          
           MOVE 1                                   TO LY83-PAGE-HDR.           
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
                  DELIMITED BY SIZE INTO LY83-DATE-HDR.                         
                                                                                
           WRITE BP13LY83-REC    FROM LY83-HEADER01 AFTER PAGE.                 
           WRITE BP13LY83-REC    FROM LY83-HEADER02 AFTER 2.                    
           WRITE BP13LY83-REC    FROM LY83-HEADER03.                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3000-PROCESS-RECORD.                                                     
      *===============================================================*         
                                                                                
           PERFORM 3100-START-BP13K828             THRU 3100-EXIT.              
                                                                                
           IF K828-FND                                                          
              PERFORM UNTIL WS-K828-EOF = 'Y'                                   
                   OR WS-SPOUSE-FD      = 'Y'                                   
                   PERFORM 3200-READNEXT-BP13K828  THRU 3200-EXIT               
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           IF WS-SPOUSE-FD NOT = 'Y'                                            
              PERFORM 5000-WRITE-P13F595A          THRU 5000-EXIT               
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F595              THRU 2000-EXIT.              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3100-START-BP13K828.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                            TO BP13K828-REC               
           INITIALIZE                                BP13K828-REC.              
           MOVE 'N'                               TO WS-K828-EOF                
                                                     WS-K828-FND                
                                                     WS-SPOUSE-FD.              
                                                                                
           MOVE F595-NUM-REGN                     TO K828-NUM-REGN              
                                                                                
           START BP13K828 KEY >= K828-KEY-FLD.                                  
                                                                                
           IF K828-STATUS = 00 OR 02                                            
              MOVE 'Y'                            TO WS-K828-FND                
           ELSE                                                                 
              IF K828-STATUS = 23 OR 10                                         
                 MOVE 'N'                         TO WS-K828-FND                
              ELSE                                                              
                 DISPLAY 'BP13K828 - ERROR STARTING : ' K828-STATUS             
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3200-READNEXT-BP13K828.                                                  
      *===============================================================*         
                                                                                
           READ BP13K828 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K828-EOF                                      
                GO          TO 3200-EXIT                                        
           END-READ.                                                            
                                                                                
           IF K828-STATUS = 00 OR 02                                            
              IF K828-NUM-REGN = F595-NUM-REGN                                  
                 IF (K828-NUM-RELATIONSHIP = '20' OR                            
                    (BP13F595-REC(500:1) = 'P' AND                              
                    (K828-NUM-RELATIONSHIP = '02' OR '12')) OR                  
                    (BP13F595-REC(500:1) = 'S' AND                              
                    (K828-NUM-RELATIONSHIP = '06' OR '07'))) AND                
                    (K828-NUM-CITIZENSHIP NOT = '10' AND '20')                  
                    PERFORM 4000-WRITE-BP13LY83 THRU 4000-EXIT                  
                    MOVE 'Y'                      TO WS-SPOUSE-FD               
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'                         TO WS-K828-EOF                
              END-IF                                                            
           ELSE                                                                 
              IF K828-STATUS = 23 OR 10                                         
                 MOVE 'Y'                         TO WS-K828-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K828 - ERROR READING NEXT : ' K828-STATUS         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4000-WRITE-BP13LY83.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                               TO BP13LY83-REC.           
           INITIALIZE                                   BP13LY83-REC.           
                                                                                
           ADD 1                                     TO WS-SNO                  
                                                                                
           MOVE WS-SNO                               TO LY83-SN.                
           MOVE K828-NUM-REGN                        TO LY83-REGN.              
           MOVE K828-NUM-NRIC                        TO LY83-NRIC.              
           MOVE K828-NUM-RELATIONSHIP                TO LY83-RELN.              
           MOVE K828-NUM-CITIZENSHIP                 TO LY83-CTZ.               
                                                                                
           WRITE BP13LY83-REC                      FROM LY83-DETAIL01.          
           ADD 1                                     TO CNT-WRITE.              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       5000-WRITE-P13F595A.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                               TO P13F595A-REC.           
           INITIALIZE                                   P13F595A-REC.           
                                                                                
           WRITE P13F595A-REC                      FROM BP13F595-REC.           
           ADD 1                                     TO CNT-WRITE1.             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       9000-CLOSE-FILES.                                                        
      *===============================================================*         
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CY83'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13F595 READ          : ' CNT-F595-READ.            
           DISPLAY 'NO. OF RECS SPOUSE FOUND      : ' CNT-WRITE.                
           DISPLAY 'NO. OF RECS WRITTEN (P13F595A): ' CNT-WRITE1.               
                                                                                
           CLOSE BP13F595                                                       
                 BP13K828                                                       
                 P13F595A                                                       
                 BP13LY83.                                                      
                                                                                
           IF K828-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K828 , STATUS = ' K828-STATUS          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
