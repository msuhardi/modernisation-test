  /*===============================================================*/           
  /*   OBJECTIVES : TO PRINT REPORT ON NUMBER OF APPLICATIONS      */           
  /*                BY ZONE/FLAT TYPE/HH-TYPE/SCHEME/ETHNIC/       */           
  /*                PIGEONHOLE TIME                                */           
  /*                                                               */           
  /*  REMARKS : COMBINE REPORTS P13L726A P13L739 BP13L740 BP13L725 */           
  /*                                                               */           
  /*                                                               */           
  /*---------------------------------------------------------------*/           
  /* CHG-NO  BY   DATE   DESCRIPTION                               */           
  /* ------- ---  ------ -----------                               */           
  /* C940786 YCH  030894 INCLUDE "3" AND "T" IN HH_CDE             */           
  /* N940014 YCH  101094 CHANGE OF REPORT FORMAT                   */           
  /* C950206 YCH  130395 PIGEON HOLE TIME                          */           
  /* BP130643MGLR 100499 ADD SUBHEADING                            */           
  /* BP133440MSD1 151008 CHANGE POSITION FOR CDE_HH                */           
  /* BP133589 YCH 300609 CHANGE " " ALL WITH A SPACE IN BETWEEN    */           
  /*                                                               */           
  /*===============================================================*/           
                                                                                
  PROC FORMAT;                                                                  
   VALUE $HH_CDE  "3"="3RM-UPG  "                                               
                  "G"="UPGRADERS"                                               
                  "H"="FIRST TIMER"                                             
                  "T"="FIRST TIMER TENANT";                                     
                                                                                
   VALUE $RM_TYP  "1"="1-RM"                                                    
                  "2"="2-RM"                                                    
                  "3"="3-RM"                                                    
                  "4"="4-RM"                                                    
                  "5"="5-RM"                                                    
                  "E"="EXEC";                                                   
                                                                                
   VALUE $CAT_FMT  "1"="CAT1"                                                   
                   "2"="CAT2"                                                   
                   "3"="CAT3";                                                  
                                                                                
   %GLOBAL DATE;                                                                
   OPTIONS NODATE;                                                              
                                                                                
   DATA BP13F520;                                                               
      INFILE BP13F520 END=EOF;                                                  
      INPUT @78  REQ_ZN   $CHAR2.                                               
            @187 SEL_NT   $CHAR3.  @211 FLT_TYP $CHAR2.                         
            @80  COHORT   $CHAR2.  @461 CDE_HH  $CHAR1.                         
            @213 ALC_SCH  $CHAR3.  @225 CAT     $CHAR1.                         
            @165 PIGEN_TM $CHAR2.                                               
            @226 SUCC_NT  $CHAR3.                                               
            @229 UNSUCC   $CHAR1.  @114 WAIT_TM $CHAR2.;                        
                                                                                
       IF _N_=1 THEN                                                            
          DO;                                                                   
             CUR_DTE=PUT(TODAY(),DDMMYY8.);                                     
             CALL SYMPUT('DATE',CUR_DTE);                                       
          END;                                                                  
                                                                                
       IF REQ_ZN=" " THEN                                                       
          CODE_NT=SEL_NT;                                                       
       ELSE                                                                     
          CODE_NT=REQ_ZN;                                                       
                                                                                
       IF CAT="4" THEN CAT="3";                                                 
                                                                                
   RUN;                                                                         
                                                                                
   PROC SORT; BY ALC_SCH;                                                       
   RUN;                                                                         
                                                                                
   DATA BP13PRIO;                                                               
        INFILE PRIOFMT;                                                         
        INPUT @2 ALC_SCH $CHAR3. @9 PRIO_SCH $CHAR3.;                           
   RUN;                                                                         
                                                                                
   PROC SORT; BY ALC_SCH;                                                       
   RUN;                                                                         
                                                                                
   DATA BP13SCH;                                                                
   MERGE BP13F520(IN=M) BP13PRIO(IN=T); BY ALC_SCH;                             
         IF M;                                                                  
   RUN;                                                                         
                                                                                
  /*  REPORT 1 */                                                               
                                                                                
   OPTIONS MISSING=0;                                                           
   PROC PRINTTO UNIT=18 NEW;                                                    
   PROC TABULATE MISSING FORMAT=6.;                                             
                                                                                
TITLE1 "P13L726A   HDB3              S Y S T E M   O F   C O M M I T M E N T    
                           DATE : &DATE  PAGE  :   ";                           
TITLE2 "                        FOR CANCELLATION/TRANSFERS AND OTHER BREAKDOWNS 
                                                   ";                           
TITLE3 "                                                                        
                                                   ";                           
TITLE4 "                                  REPORT ON NUMBER OF APPLICATIONS      
                                                   ";                           
TITLE5 "                       BY ZONE/FLAT TYPE/HH TYPE/SCHEME/ETHNIC/PIGEONHOL
E TIME                                             ";                           
                                                                                
   CLASS  CODE_NT PIGEN_TM CAT FLT_TYP PRIO_SCH CDE_HH;                         
                                                                                
   FORMAT CDE_HH  $HH_CDE.                                                      
          CAT     $CAT_FMT.                                                     
          FLT_TYP $RM_TYP.;                                                     
                                                                                
   KEYLABEL SUM  ="UNIT "                                                       
            ALL  ="TOTAL";                                                      
                                                                                
   LABEL CODE_NT  ="ZONE/NEW TOWN :"                                            
         PIGEN_TM  ="PIGEONHOLE TIME"                                           
         FLT_TYP  ="FLAT TYPE";                                                 
                                                                                
   TABLE CODE_NT,                                                               
         (PRIO_SCH="SCHEME")*                                                   
         (FLT_TYP="FLAT TYP")*(PIGEN_TM="PIGEONHOLE TIME " ALL="TOT"),          
         (CDE_HH=" " ALL="TOTAL")*                                              
         (CAT=" "*N=" " ALL="SUB-TOTAL"*N=" ")/RTS=18                           
         BOX="HOUSEHOLD TYPE/   ETHNIC";                                        
                                                                                
   RUN;                                                                         
                                                                                
  /*  REPORT 2 = SUMMARY*/                                                      
                                                                                
   PROC TABULATE MISSING FORMAT=6.;                                             
                                                                                
TITLE1 "P13L726B   HDB3              S Y S T E M   O F   C O M M I T M E N T    
                           DATE : &DATE  PAGE  :   ";                           
TITLE2 "                        FOR CANCELLATION/TRANSFERS AND OTHER BREAKDOWNS 
                                                   ";                           
TITLE3 "                                                                        
                                                   ";                           
TITLE4 "                               SUMMARY REPORT ON NUMBER OF APPLICATION  
                                                   ";                           
TITLE5 "                             BY PRIORITY & PA (WITH PIGEONHOLE TIME)    
                                                   ";                           
                                                                                
   CLASS  CODE_NT PIGEN_TM CAT FLT_TYP PRIO_SCH CDE_HH;                         
                                                                                
   FORMAT CDE_HH  $HH_CDE.                                                      
          CAT     $CAT_FMT.                                                     
          FLT_TYP $RM_TYP.;                                                     
                                                                                
   KEYLABEL SUM  ="UNIT "                                                       
            ALL  ="TOTAL";                                                      
                                                                                
   LABEL CODE_NT  ="ZONE :"                                                     
         PIGEN_TM  ="PIGEONHOLE TIME"                                           
         FLT_TYP  ="FLAT TYPE";                                                 
                                                                                
   TABLE PRIO_SCH=" ",                                                          
         (FLT_TYP="FLAT TYP")*(PIGEN_TM="PIGEONHOLE TIME " ALL="TOT"),          
         (CDE_HH=" " ALL="TOTAL")*                                              
         (CAT=" "*N=" " ALL="SUB-TOTAL"*N=" ")/RTS=18                           
         BOX="HOUSEHOLD TYPE/   ETHNIC";                                        
                                                                                
   RUN;                                                                         
