      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.   BP13C113.                                                  
       AUTHOR.       EG1.                                                       
      *DATE-WRITTEN. FEB 26, 1996                                               
      ******************************************************************        
      *    SYSTEM:       SYSTEM OF COMMITMENT                          *        
      *    SYSTEM ID :   BP13                                          *        
      *    OBJECTIVES:   TO SEPERATE THE EX-APPLT CD TRANSFER          *        
      *                  (REFUNDABLE AMOUNT WITHOUT THE NEED OF        *        
      *                  REPLACEMENT BY NEW TRANSFEREES) FROM THOSE    *        
      *                  CDS THAT HAVE TO WAIT FOR REPLACEMENTS.       *        
      *                                                                *        
      *     INPUT FILES:                                               *        
      *      1. CASHTRAN FILE     (BP13F120)                           *        
      *                                                                *        
      *     OUTPUT FILES:                                              *        
      *      1. EX-APPLT FILE     (BP13K885)                           *        
      *      1. L113              (CONTROL LIST)                       *        
      *      1. L113A             (ERROR   LIST)                       *        
      *                                                                *        
      * CHG-NO    BY    DATE   DESCRIPTION                             *        
      * -------- ---- -------- --------------------------------------- *        
      * BP130103  EG1. 26/02/1996 TO CATER FOR SUB-TRANSFER.                    
      * BP130418  RHB. 05/06/1998 Y2K                                           
      *                                                                *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *================================================================*        
       CONFIGURATION SECTION.                                                   
      *================================================================*        
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
           SELECT BP13F120 ASSIGN TO BP13F120.                                  
                                                                                
           SELECT BP13K885 ASSIGN TO BP13K885                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K885-KEY-FLD                               
                  FILE STATUS     IS K885-STATUS.                               
                                                                                
           SELECT BP13K880 ASSIGN TO BP13K880                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K880-KEY-FLD                               
                  FILE STATUS     IS K880-STATUS.                               
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13L113 ASSIGN TO BP13L113.                                  
                                                                                
           SELECT BP13L13A ASSIGN TO BP13L13A.                                  
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
       FD  BP13F120                                                             
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F120.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K880                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K880.                                                           
                                                                                
       FD  BP13K885                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K885.                                                           
                                                                                
       FD  BP13L113                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC-1                 PIC X(132).                              
                                                                                
       FD  BP13L13A                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC-2                 PIC X(132).                              
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *   COMPUTE VARIABLES                                            *        
      *----------------------------------------------------------------*        
       01 WS-COMPUTE-VARIABLES.                                                 
          05 WS-TOPUP-TOTAL            PIC S9(10)V99.                           
          05 WS-REFUND-TOTAL           PIC S9(10)V99.                           
          05 WS-CD-TR-EXCESS           PIC S9(10)V99.                           
          05 WS-SUSPENSE-TOTAL         PIC S9(10)V99.                           
          05 WS-AMT-POST-TOTAL         PIC S9(10)V99 VALUE +0.                  
                                                                                
      *----------------------------------------------------------------*        
      *   MISCELLEANEOUS VARIABLES                                     *        
      *----------------------------------------------------------------*        
       01  WS-CURRENT-DATE.                                                     
           03  WS-CURR-CC         PIC X(2).                                     
           03  WS-CURR-YY         PIC X(2).                                     
           03  WS-CURR-MM         PIC X(2).                                     
           03  WS-CURR-DD         PIC X(2).                                     
                                                                                
       01  WS-DATE-CCYYMMDD.                                                    
           03  WS-DATE-CCYY.                                                    
               05 WS-DTE1-CC      PIC X(2).                                     
               05 WS-DTE1-YY      PIC X(2).                                     
           03  WS-CURR-MMDD.                                                    
               05 WS-DTE1-MM      PIC X(2).                                     
               05 WS-DTE1-DD      PIC X(2).                                     
                                                                                
      *----------------------------------------------------------------*        
      *   COUNTER VARIABLES                                            *        
      *----------------------------------------------------------------*        
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F120-READ           PIC 9(4)   VALUE ZEROS.               
           05  WS-CNT-F120-SUS-READ       PIC 9(4)   VALUE ZEROS.               
           05  WS-CNT-F120-SUS-25         PIC 9(4)   VALUE ZEROS.               
           05  WS-CNT-K885-REWRITE        PIC 9(4)   VALUE ZEROS.               
                                                                                
      *----------------------------------------------------------------*        
      *   FLAG    VARIABLES                                            *        
      *----------------------------------------------------------------*        
       01  WS-STATUS.                                                           
           05  K880-STATUS         PIC X(2).                                    
           05  K885-STATUS         PIC X(2).                                    
           05  K800-STATUS         PIC X(2).                                    
           05  WS-F120-EOF-FL      PIC X VALUE 'N'.                             
               88  F120-EOF        VALUE 'Y'.                                   
           05  WS-K885-EOF-FL      PIC X VALUE 'N'.                             
               88  K885-EOF        VALUE 'Y'.                                   
           05  WS-SUSP-COMP-FL     PIC X.                                       
                                                                                
      *----------------------------------------------------------------*        
      *   LIST CONTROL VARIABLES                                       *        
      *----------------------------------------------------------------*        
       01  WS-LIST-VARIABLES.                                                   
           05 WS-CNT-CASES-ON-PAGE           PIC 9(2)      VALUE 66.            
           05 WS-CNT-CASES-ON-PAGEA          PIC 9(2)      VALUE 66.            
           05 WS-CNT-CASES                   PIC 9(7)      VALUE 0.             
           05 WS-CNT-CASESA                  PIC 9(7)      VALUE 0.             
           05 WS-CNT-PAGE                    PIC 9(5)      VALUE 0.             
           05 WS-CNT-PAGEA                   PIC 9(5)      VALUE 0.             
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *   CONTROL LIST                                                 *        
      *----------------------------------------------------------------*        
       01  L13S1-PR-HEAD-01.                                                    
           05  FILLER              PIC X(8)      VALUE 'BP13L113'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  13S1-DATE           PIC X(10).                                   
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  13S1-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L13S1-PR-HEAD-02.                                                    
           05  FILLER              PIC X(40)     VALUE SPACES .                 
           05  FILLER              PIC X(20)     VALUE                          
                                                 '       CONTROL LISTI'.        
           05  FILLER              PIC X(20)     VALUE                          
                                                 'NG FOR DAILY TRANSAC'.        
           05  FILLER              PIC X(13)     VALUE                          
                                                 'TION'.                        
           05  FILLER              PIC X(40)     VALUE SPACES.                  
                                                                                
                                                                                
       01  L13S1-PR-HEAD-03.                                                    
           05  FILLER              PIC X(13)                                    
                  VALUE '    REGN-NO  '.                                        
           05  FILLER              PIC X(12)                                    
                  VALUE 'PAYMENT-TY  '.                                         
           05  FILLER              PIC X(24)                                    
                  VALUE 'TRANS-TY    SCH-ACCT    '.                             
           05  FILLER              PIC X(27)                                    
                  VALUE 'CPF-NO   CDE-APPLN  AMT    '.                          
           05  FILLER              PIC X(15)                                    
                  VALUE '               '.                                      
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(15)                                    
                  VALUE 'RECEIPT/VOUCHER'.                                      
           05  FILLER              PIC X(22)                                    
                  VALUE '       RECEIPT-DATE   '.                               
                                                                                
       01  L13S1-PR-DETAILS.                                                    
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  13S1-REGN-NO        PIC X(08).                                   
           05  FILLER              PIC X(06)     VALUE SPACES .                 
           05  13S1-PAY-TY         PIC X(03).                                   
           05  FILLER              PIC X(08)     VALUE SPACES .                 
           05  13S1-TRANS-TY       PIC X(02).                                   
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  13S1-SCH-ACCT       PIC X(11).                                   
           05  FILLER              PIC X(04)     VALUE SPACES .                 
           05  13S1-CPF-NO         PIC X(09).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  13S1-CDE-APPLN      PIC X(01).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  13S1-AMT            PIC $$$$,$$9.99DB.                           
           05  FILLER              PIC X(06)     VALUE SPACES .                 
           05  FILLER              PIC X(03).                                   
           05  FILLER              PIC X(10)     VALUE SPACES .                 
           05  13S1-RECEIPTVOUCHER PIC X(14).                                   
           05  FILLER              PIC X(08)     VALUE SPACES .                 
           05  13S1-RECEIPT-DATE   PIC X(10).                                   
           05  FILLER              PIC X(05)     VALUE SPACES .                 
                                                                                
      *----------------------------------------------------------------*        
      *   ERROR LIST VARIABLES                                         *        
      *----------------------------------------------------------------*        
       01  L13S2-PR-HEAD-01.                                                    
           05  FILLER              PIC X(8)     VALUE 'BP13L13A'.               
           05  FILLER              PIC X(5)     VALUE SPACES .                  
           05  FILLER              PIC X(8)     VALUE 'HDBCAT 3'.               
           05  FILLER              PIC X(24)    VALUE SPACES.                   
           05  FILLER              PIC X(20)    VALUE                           
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)    VALUE                           
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)    VALUE SPACES.                   
           05  FILLER              PIC X(7)     VALUE 'DATE :'.                 
           05  13S2-DATE           PIC X(10).                                   
           05  FILLER              PIC X(4)     VALUE SPACES.                   
           05  FILLER              PIC X(7)     VALUE 'PAGE : '.                
           05  13S2-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L13S2-PR-HEAD-02.                                                    
           05  FILLER              PIC X(40)     VALUE SPACES .                 
           05  FILLER              PIC X(20)     VALUE                          
                                                 '         ERROR LISTI'.        
           05  FILLER              PIC X(20)     VALUE                          
                                                 'NG FOR DAILY TRANSAC'.        
           05  FILLER              PIC X(13)     VALUE                          
                                                 'TION'.                        
           05  FILLER              PIC X(40)     VALUE SPACES.                  
                                                                                
       01  L13S2-PR-HEAD-03.                                                    
           05  FILLER              PIC X(13)                                    
                  VALUE '   REGN-NO   '.                                        
           05  FILLER              PIC X(12)                                    
                  VALUE 'PAYMENT-TY  '.                                         
           05  FILLER              PIC X(25)                                    
                  VALUE 'TRANS-TY  SCH-ACCN-NO    '.                            
           05  FILLER              PIC X(26)                                    
                  VALUE 'CPF-NO   CDE-APPLN  AMT   '.                           
           05  FILLER              PIC X(15)                                    
                  VALUE '               '.                                      
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(15)                                    
                  VALUE 'RECEIPT/VOUCHER'.                                      
           05  FILLER              PIC X(22)                                    
                  VALUE '       RECEIPT-DATE   '.                               
                                                                                
       01  L13S2-PR-DETAILS.                                                    
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  13S2-REGN-NO        PIC X(08).                                   
           05  FILLER              PIC X(06)     VALUE SPACES .                 
           05  13S2-PAY-TY         PIC X(03).                                   
           05  FILLER              PIC X(08)     VALUE SPACES .                 
           05  13S2-TRANS-TY       PIC X(02).                                   
           05  FILLER              PIC X(06)     VALUE SPACES .                 
           05  13S2-SCH-ACCN-NO    PIC X(11).                                   
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  13S2-CPF-NO         PIC X(09).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  13S2-CDE-APPLN      PIC 9(01).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  13S2-AMT            PIC 9(10).                                   
           05  FILLER              PIC X(08)     VALUE SPACES .                 
           05  FILLER              PIC X(03).                                   
           05  FILLER              PIC X(10)     VALUE SPACES .                 
           05  13S2-RECEIPTVOUCHER PIC X(14).                                   
           05  FILLER              PIC X(08)     VALUE SPACES .                 
           05  13S2-RECEIPT-DATE   PIC X(10).                                   
           05  FILLER              PIC X(05)     VALUE SPACES .                 
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
                                                                                
           PERFORM 2000-MAIN-PROCESS   THRU 2000-EXIT                           
             UNTIL F120-EOF.                                                    
                                                                                
           PERFORM 9000-WRAP-UP        THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  BP13F120                                                 
                       BP13K880                                                 
                I-O    BP13K800                                                 
                       BP13K885                                                 
                OUTPUT BP13L113                                                 
                       BP13L13A.                                                
                                                                                
           IF K880-STATUS NOT = '00' AND '97'                                   
              DISPLAY 'BP13K880 OPEN ERROR CODE(' K880-STATUS ')'               
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           IF K885-STATUS NOT = '00' AND '97'                                   
              DISPLAY 'BP13K885 OPEN ERROR CODE(' K885-STATUS ')'               
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = '00' AND '97'                                   
              DISPLAY 'BP13K800 OPEN ERROR CODE(' K800-STATUS ')'               
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
           MOVE 'N'   TO WS-F120-EOF-FL.                                        
                                                                                
           PERFORM 8000-READ-F120      THRU 8000-EXIT.                          
                                                                                
           MOVE 60            TO WS-CNT-CASES-ON-PAGE.                          
           MOVE 60            TO WS-CNT-CASES-ON-PAGEA.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2000-MAIN-PROCESS.                                                       
      *================================================================*        
           MOVE F120-NUM-REGN            TO K800-NUM-REGN.                      
           PERFORM 8100-READ-BP13K800    THRU 8100-EXIT.                        
           IF K800-STATUS = '00'                                                
              PERFORM 3000-PROCESS-PYMT  THRU 3000-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 8000-READ-F120 THRU 8000-EXIT.                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3000-PROCESS-PYMT.                                                       
      *================================================================*        
           EVALUATE F120-CDE-PAYMENT-TYPE                                       
           WHEN     'REG'                                                       
                CONTINUE                                                        
           WHEN     'BK '                                                       
                CONTINUE                                                        
           WHEN     'CD '                                                       
                CONTINUE                                                        
           WHEN     'SOC'                                                       
                CONTINUE                                                        
           WHEN     'SUS'                                                       
                PERFORM 3100-COMPUTE-SUSPENSE THRU 3100-EXIT                    
           WHEN     OTHER                                                       
                PERFORM 9100-ERROR-LIST-PRINT THRU 9100-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
      * COMPUTE SUSPENSE AMOUNT.                                                
      ****************************************************************          
      *================================================================*        
       3100-COMPUTE-SUSPENSE.                                                   
      *================================================================*        
                                                                                
            ADD 1                    TO WS-CNT-F120-SUS-READ.                   
            IF F120-CDE-TRANS-TYPE = '25'                                       
               ADD 1                 TO WS-CNT-F120-SUS-25                      
               PERFORM 4000-PROCESS-SUSPENSE THRU 4000-EXIT                     
            ELSE                                                                
               PERFORM 9100-ERROR-LIST-PRINT THRU 9100-EXIT                     
            END-IF.                                                             
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ****************************************************************          
      * PROCESS SUSPENSE RECORD.                                                
      ****************************************************************          
      *================================================================*        
       4000-PROCESS-SUSPENSE.                                                   
      *================================================================*        
            MOVE F120-NUM-REGN         TO K880-NUM-REGN.                        
            PERFORM 8200-READ-BP13K880 THRU 8200-EXIT.                          
            IF K880-STATUS = '00'                                               
               PERFORM 4100-COMPUTE-CD-TR    THRU 4100-EXIT                     
            END-IF.                                                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ****************************************************************          
      * PROCESS CD-TRANSFER RECORDS.                                            
      ****************************************************************          
      *================================================================*        
       4100-COMPUTE-CD-TR.                                                      
      *================================================================*        
            COMPUTE WS-TOPUP-TOTAL = K880-AMT-TOPUP-CD-CASH-CGO-PA              
                                   + K880-AMT-TOPUP-CD-CPF1-CGO-PA              
                                   + K880-AMT-TOPUP-CD-CPF2-CGO-PA              
                                   + K880-AMT-TOPUP-CD-CPF3-CGO-PA              
                                   + K880-AMT-TOPUP-CD-CPF4-CGO-PA              
                                   + K880-AMT-TOPUP-CD-CASH                     
                                   + K880-AMT-TOPUP-CD-CPF1                     
                                   + K880-AMT-TOPUP-CD-CPF2                     
                                   + K880-AMT-TOPUP-CD-CPF3                     
                                   + K880-AMT-TOPUP-CD-CPF4.                    
                                                                                
            ADD F120-AMT-RECEIPT      TO WS-AMT-POST-TOTAL.                     
            COMPUTE WS-CD-TR-EXCESS = F120-AMT-RECEIPT.                         
            IF WS-CD-TR-EXCESS > 0                                              
               PERFORM 8300-START-BP13K885   THRU 8300-EXIT                     
               IF WS-K885-EOF-FL NOT = 'Y'                                      
                  PERFORM 8400-READ-NEXT-BP13K885 THRU 8400-EXIT                
                  PERFORM 4200-UPDATE-K885        THRU 4200-EXIT                
                    UNTIL WS-K885-EOF-FL = 'Y'                                  
                  PERFORM 9300-CONTROL-LIST-PRINT THRU 9300-EXIT                
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4200-UPDATE-K885.                                                        
      *================================================================*        
           IF WS-TOPUP-TOTAL > 0                                                
              PERFORM 4300-PARTIAL-CD-TR THRU 4300-EXIT                         
           ELSE                                                                 
              PERFORM 4350-FULL-CD-TR    THRU 4350-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K885-EOF-FL NOT = 'Y'                                          
              PERFORM 8500-REWRITE-BP13K885 THRU 8500-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 8400-READ-NEXT-BP13K885 THRU 8400-EXIT.                      
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
      * PROCESS CD-TRANSFER RECORDS.                                            
      ****************************************************************          
      *================================================================*        
       4300-PARTIAL-CD-TR.                                                      
      *================================================================*        
            IF WS-CD-TR-EXCESS > K885-AMT-CD-PAID                               
               MOVE K885-AMT-CD-PAID      TO K885-AMT-CD-TRANSFER               
               ADD  K885-AMT-CD-TRANSFER  TO K885-AMT-SUSPENSE                  
               SUBTRACT K885-AMT-CD-PAID  FROM WS-CD-TR-EXCESS                  
               INITIALIZE                    K885-AMT-CD-PAID                   
            ELSE                                                                
               IF WS-CD-TR-EXCESS > 0                                           
                  MOVE WS-CD-TR-EXCESS       TO K885-AMT-CD-TRANSFER            
                  ADD  K885-AMT-CD-TRANSFER  TO K885-AMT-SUSPENSE               
                  SUBTRACT WS-CD-TR-EXCESS FROM K885-AMT-CD-PAID                
                  INITIALIZE WS-CD-TR-EXCESS                                    
               ELSE                                                             
                  MOVE 'Y'                   TO WS-K885-EOF-FL                  
               END-IF                                                           
            END-IF.                                                             
                                                                                
            COMPUTE WS-SUSPENSE-TOTAL = K885-AMT-REFUND   +                     
                                        K885-AMT-INTEREST.                      
                                                                                
            IF WS-SUSPENSE-TOTAL = K885-AMT-SUSPENSE                            
               MOVE 'Y'                TO K885-CDE-VOUCHER                      
            END-IF.                                                             
                                                                                
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4350-FULL-CD-TR.                                                         
      *================================================================*        
            MOVE K885-AMT-CD-PAID      TO K885-AMT-CD-TRANSFER.                 
            ADD  K885-AMT-CD-TRANSFER  TO K885-AMT-SUSPENSE.                    
            SUBTRACT K885-AMT-CD-PAID  FROM WS-CD-TR-EXCESS.                    
            INITIALIZE                    K885-AMT-CD-PAID.                     
                                                                                
            COMPUTE WS-SUSPENSE-TOTAL = K885-AMT-REFUND   +                     
                                        K885-AMT-INTEREST.                      
                                                                                
            IF WS-SUSPENSE-TOTAL = K885-AMT-SUSPENSE                            
               MOVE 'Y'                TO K885-CDE-VOUCHER                      
            END-IF.                                                             
                                                                                
       4350-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       8000-READ-F120.                                                          
      *================================================================*        
           READ BP13F120                                                        
                AT END   MOVE 'Y'    TO WS-F120-EOF-FL                          
                         GO TO 8000-EXIT.                                       
           ADD 1            TO WS-CNT-F120-READ.                                
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       8100-READ-BP13K800.                                                      
      *================================================================*        
           READ BP13K800                                                        
           INVALID KEY   DISPLAY 'BP13K800 NOT FOUND: ' F120-NUM-REGN           
                         GO TO 8100-EXIT.                                       
                                                                                
           IF K800-STATUS NOT = '00'                                            
              DISPLAY 'BP13K800 READ ERROR CODE(' K800-STATUS ')'               
              PERFORM 9900-HANDLE-ABEND     THRU 9900-EXIT                      
           END-IF.                                                              
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       8200-READ-BP13K880.                                                      
      *================================================================*        
           READ BP13K880                                                        
           INVALID KEY   DISPLAY 'BP13K880 NOT FOUND: ' F120-NUM-REGN           
                         GO TO 8200-EXIT.                                       
                                                                                
           IF K880-STATUS NOT = '00'                                            
              DISPLAY 'BP13K880 READ ERROR CODE(' K880-STATUS ')'               
              PERFORM 9900-HANDLE-ABEND     THRU 9900-EXIT                      
           END-IF.                                                              
                                                                                
       8200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       8300-START-BP13K885.                                                     
      *================================================================*        
           MOVE LOW-VALUES           TO WS-K885-EOF-FL.                         
           MOVE LOW-VALUES           TO K885-KEY-FLD.                           
           MOVE F120-NUM-REGN        TO K885-NUM-REGN.                          
           START BP13K885 KEY >= K885-KEY-FLD                                   
                 INVALID KEY MOVE 'Y'      TO WS-K885-EOF-FL                    
                             GO TO 8300-EXIT.                                   
           IF K885-STATUS NOT = '00'                                            
              DISPLAY 'BP13K885 START ERROR CODE(' K885-STATUS ')'              
              PERFORM 9900-HANDLE-ABEND      THRU 9900-EXIT                     
           END-IF.                                                              
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       8400-READ-NEXT-BP13K885.                                                 
      *================================================================*        
           READ BP13K885 NEXT                                                   
                AT END   MOVE 'Y'      TO WS-K885-EOF-FL                        
                         GO TO 8400-EXIT.                                       
                                                                                
           IF K885-STATUS NOT = '00'                                            
              DISPLAY 'BP13K885 READ ERROR CODE(' K885-STATUS ')'               
              PERFORM 9900-HANDLE-ABEND      THRU 9900-EXIT                     
           END-IF.                                                              
                                                                                
           IF K885-NUM-REGN NOT = F120-NUM-REGN                                 
              MOVE 'Y'          TO WS-K885-EOF-FL                               
           END-IF.                                                              
                                                                                
       8400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       8500-REWRITE-BP13K885.                                                   
      *================================================================*        
           REWRITE BP13K885-REC.                                                
                                                                                
           IF K885-STATUS NOT = '00'                                            
              DISPLAY 'BP13K885 REWRITE ERROR CODE(' K885-STATUS ')'            
              PERFORM 9900-HANDLE-ABEND      THRU 9900-EXIT                     
           END-IF.                                                              
           ADD 1     TO WS-CNT-K885-REWRITE.                                    
                                                                                
       8500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-WRAP-UP.                                                            
      *================================================================*        
           MOVE SPACES                TO L13S1-PR-DETAILS.                      
           MOVE 'TOTAL'               TO 13S1-CPF-NO.                           
           MOVE WS-AMT-POST-TOTAL     TO 13S1-AMT.                              
           WRITE PRINT-REC-1 FROM L13S1-PR-DETAILS AFTER 2.                     
                                                                                
           CLOSE       BP13F120                                                 
                       BP13K880                                                 
                       BP13K800                                                 
                       BP13K885                                                 
                       BP13L113                                                 
                       BP13L13A.                                                
                                                                                
           DISPLAY '********************************'.                          
           DISPLAY '*  BP13C113 CONTROL TOTALS     *'.                          
           DISPLAY '********************************'.                          
           DISPLAY '* TOTAL NO OF F120 RECS READ :    '                         
                                                 WS-CNT-F120-READ.              
           DISPLAY '* TOTAL NO OF (SUS) F120 RECS:    '                         
                                                 WS-CNT-F120-SUS-READ.          
           DISPLAY '* TOTAL NO OF BP13K885 REWRITTEN: '                         
                                                 WS-CNT-K885-REWRITE.           
           DISPLAY '********************************'.                          
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9100-ERROR-LIST-PRINT.                                                   
      *================================================================*        
            IF WS-CNT-CASES-ON-PAGEA > 50                                       
               PERFORM 9210-PRINT-HEADING THRU 9210-EXIT                        
            END-IF.                                                             
            PERFORM 9220-PRINT-DETAIL THRU 9220-EXIT.                           
                                                                                
       9100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9210-PRINT-HEADING.                                                      
      *================================================================*        
            ADD 1                   TO WS-CNT-PAGEA.                            
            MOVE ZERO               TO WS-CNT-CASES-ON-PAGEA.                   
            MOVE WS-CNT-PAGE        TO 13S2-PAGENO.                             
            STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY          
                   DELIMITED BY SIZE INTO 13S2-DATE.                            
            MOVE SPACES             TO PRINT-REC-2.                             
            WRITE PRINT-REC-2 FROM L13S2-PR-HEAD-01 AFTER PAGE.                 
            WRITE PRINT-REC-2 FROM L13S2-PR-HEAD-02 AFTER 2.                    
            WRITE PRINT-REC-2 FROM L13S2-PR-HEAD-03 AFTER 2.                    
            INSPECT PRINT-REC-2 REPLACING CHARACTERS BY '-'.                    
            WRITE PRINT-REC-2.                                                  
            MOVE SPACES             TO PRINT-REC-2.                             
            WRITE PRINT-REC-2 AFTER 2.                                          
                                                                                
       9210-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9220-PRINT-DETAIL.                                                       
      *================================================================*        
            ADD 1                       TO WS-CNT-CASES-ON-PAGEA.               
            MOVE ZEROES                 TO 13S2-CDE-APPLN.                      
            MOVE F120-NUM-REGN          TO 13S2-REGN-NO.                        
            MOVE F120-CDE-PAYMENT-TYPE  TO 13S2-PAY-TY.                         
            MOVE F120-CDE-TRANS-TYPE    TO 13S2-TRANS-TY.                       
            MOVE F120-NUM-SCH-ACCT      TO 13S2-SCH-ACCN-NO.                    
            MOVE F120-NUM-CPF-APPL      TO 13S2-CPF-NO.                         
            MOVE F120-AMT-RECEIPT       TO 13S2-AMT.                            
      *     MOVE K800-HIST-COUNT        TO 13S2-HISTORY-CNT.                    
            MOVE F120-NUM-RECPT-JRNO    TO 13S2-RECEIPTVOUCHER.                 
            MOVE F120-DTE-TRANS         TO WS-DATE-CCYYMMDD.                    
            STRING WS-DTE1-DD '/' WS-DTE1-MM '/' WS-DTE1-CC WS-DTE1-YY          
                   DELIMITED BY SIZE  INTO 13S2-RECEIPT-DATE.                   
            WRITE PRINT-REC-2 FROM L13S2-PR-DETAILS AFTER 1.                    
                                                                                
       9220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9300-CONTROL-LIST-PRINT.                                                 
      *================================================================*        
           IF WS-CNT-CASES-ON-PAGE  > 50                                        
              PERFORM 9400-PRINT-HEADING THRU 9400-EXIT.                        
                                                                                
           PERFORM 9450-PRINT-DETAIL THRU 9450-EXIT.                            
                                                                                
       9300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9400-PRINT-HEADING.                                                      
      *================================================================*        
            ADD 1                 TO WS-CNT-PAGE.                               
            MOVE ZERO             TO WS-CNT-CASES-ON-PAGE.                      
            MOVE WS-CNT-PAGE      TO 13S1-PAGENO.                               
            STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY          
                   DELIMITED BY SIZE INTO 13S1-DATE.                            
            MOVE SPACES           TO PRINT-REC-1.                               
            WRITE PRINT-REC-1 FROM L13S1-PR-HEAD-01 AFTER PAGE.                 
            WRITE PRINT-REC-1 FROM L13S1-PR-HEAD-02 AFTER 2.                    
            WRITE PRINT-REC-1 FROM L13S1-PR-HEAD-03 AFTER 2.                    
            INSPECT PRINT-REC-1 REPLACING CHARACTERS BY '-'.                    
            WRITE PRINT-REC-1 .                                                 
            MOVE SPACES TO PRINT-REC-1.                                         
            WRITE PRINT-REC-1 AFTER 2.                                          
                                                                                
       9400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9450-PRINT-DETAIL.                                                       
      *================================================================*        
                                                                                
            ADD 1                       TO WS-CNT-CASES-ON-PAGE.                
            MOVE F120-NUM-REGN          TO 13S1-REGN-NO.                        
            MOVE F120-CDE-PAYMENT-TYPE  TO 13S1-PAY-TY.                         
            MOVE F120-CDE-TRANS-TYPE    TO 13S1-TRANS-TY.                       
            MOVE ZEROES                 TO 13S1-CDE-APPLN.                      
            MOVE F120-NUM-SCH-ACCT      TO 13S1-SCH-ACCT.                       
            MOVE F120-NUM-CPF-APPL      TO 13S1-CPF-NO.                         
            MOVE F120-AMT-RECEIPT       TO 13S1-AMT.                            
            MOVE F120-NUM-RECPT-JRNO    TO 13S1-RECEIPTVOUCHER.                 
            MOVE F120-DTE-TRANS         TO WS-DATE-CCYYMMDD.                    
            STRING WS-DTE1-DD '/' WS-DTE1-MM '/' WS-DTE1-CC WS-DTE1-YY          
                   DELIMITED BY SIZE  INTO 13S1-RECEIPT-DATE.                   
            WRITE PRINT-REC-1 FROM L13S1-PR-DETAILS AFTER 1.                    
                                                                                
       9450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9900-HANDLE-ABEND.                                                       
      *================================================================*        
           CLOSE       BP13F120                                                 
                       BP13K880                                                 
                       BP13K800                                                 
                       BP13K885                                                 
                       BP13L113                                                 
                       BP13L13A.                                                
                                                                                
           DISPLAY '********************************'.                          
           DISPLAY '*  BP13C113 ABEND   TOTALS     *'.                          
           DISPLAY '********************************'.                          
           DISPLAY '* TOTAL NO OF F120 RECS READ :    '                         
                                                 WS-CNT-F120-READ.              
           DISPLAY '* TOTAL NO OF (SUS) F120 RECS:    '                         
                                                 WS-CNT-F120-SUS-READ.          
           DISPLAY '* TOTAL NO OF BP13K885 REWRITTEN: '                         
                                                 WS-CNT-K885-REWRITE.           
           DISPLAY '********************************'.                          
                                                                                
       9900-EXIT.                                                               
           EXIT.                                                                
