       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD79.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  06/05/2015.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE  :  GENERATE REPORT FOR PRIORITY SCHEMES WITH NO   *         
      *                QUOTA                                          *         
      *                                                               *         
      *  INPUT      :  BP13F730 - FULL FILE                           *         
      *                BP13K811 - PRIORITY SCHEMES WITH NO QUOTA      *         
      *  OUTPUT     :  BP13LD79 - REPORT GENERATED                    *         
      *                                                               *         
      *===============================================================*         
      * CHG-NO   DATE     BY   DESCRIPTION                            *         
      * -------- -------- ---- -------------------------------------- *         
      * BP135774 06052015 SMR2 NEW PROGRAM                            *         
      * BP137310 03052018 JF7  EXCL FLAT TYPE '2F' AND ELDERLY 'S'/'F'*         
      *                        FOR 'SBF' SCHEME WITH 'MCP'            *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT BP13K811 ASSIGN TO BP13K811                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K811-KEY-FLD                               
                  FILE STATUS     IS WS-K811-STATUS.                            
                                                                                
           SELECT BP13LD79 ASSIGN TO BP13LD79.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORD     IS STANDARD.                                       
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K811                                                            
            RECORD CONTAINS  80 CHARACTERS.                                     
       COPY BP13K811.                                                           
                                                                                
       FD   BP13LD79                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE OMITTED.                                        
       01   BP13LD79-REC               PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K811-STATUS          PIC X(02) VALUE SPACES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF             PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-FILE-COUNTERS.                                                    
           05  WS-F730-READ            PIC 9(06) VALUE ZEROES.                  
           05  WS-LD79-WRITE           PIC 9(06) VALUE ZEROES.                  
           05  WS-LD79-SNO             PIC 9(05) VALUE ZEROES.                  
           05  WS-LD79-PAGE            PIC 9(05) VALUE ZEROES.                  
           05  WS-LD79-LINE            PIC 9(02) VALUE 60.                      
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-EDT-DATE             PIC X(10) VALUE SPACES.                  
           05  WS-CUR-DATE             PIC X(08) VALUE SPACES.                  
           05  WS-EDIT1                PIC Z(05)9.                              
                                                                                
       01  WS-F730-KEY-FLD.                                                     
           05  WS-NUM-SALES-MODE       PIC X(03) VALUE SPACES.                  
           05  WS-DTE-BALLOT           PIC X(06) VALUE SPACES.                  
           05  WS-CDE-NEW-TOWN         PIC X(03) VALUE SPACES.                  
           05  WS-CDE-FLAT-TYPE        PIC X(02) VALUE SPACES.                  
           05  WS-CDE-ALLOC-SCH        PIC X(03) VALUE SPACES.                  
           05  WS-CDE-HOUSEHOLD        PIC X(01) VALUE SPACES.                  
                                                                                
      **************************************************************            
      *            BP13LD79 - R E P O R T   L A Y O U T            *            
      **************************************************************            
       01  LD79-HEADING-01.                                                     
           05  FILLER              PIC X(08)     VALUE 'BP13LD79'.              
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDB3'.                  
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LD79-RUN-DATE       PIC X(10).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LD79-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  LD79-HEADING-02.                                                     
           05  FILLER              PIC X(43)     VALUE SPACES.                  
           05  FILLER              PIC X(45)     VALUE                          
               'LIST OF CASES : PRIORITY SCHEME WITH NO QUOTA'.                 
                                                                                
       01  LD79-HEADING-03.                                                     
           05  FILLER              PIC X(46)     VALUE                          
               'S/N     REGNO      SALES MODE   BALLOT DATE   '.                
           05  FILLER              PIC X(25)     VALUE                          
               'NT    FT   ALLOC SCH   HH'.                                     
                                                                                
       01  LD79-HEADING-04.                                                     
           05  FILLER              PIC X(132)    VALUE ALL '-'.                 
                                                                                
       01  LD79-HEADING-05.                                                     
           05  FILLER              PIC X(132)    VALUE SPACES.                  
                                                                                
       01  LD79-DETAIL-01.                                                      
           05  LD79-S-NO           PIC ZZZZ9     VALUE ZEROES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD79-REGN           PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD79-SMODE          PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  LD79-DTEBAL         PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  LD79-NT             PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD79-FT             PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD79-ALLOC-SCH      PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  LD79-HH             PIC X(01)     VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT                       
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F730                                                 
                       BP13K811                                                 
                OUTPUT BP13LD79.                                                
                                                                                
           IF WS-K811-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K811 : ' WS-K811-STATUS             
              MOVE WS-K811-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2) '/' WS-CUR-DATE(5:2) '/'                     
                  WS-CUR-DATE(1:4)                                              
           DELIMITED BY SIZE INTO WS-EDT-DATE.                                  
           MOVE WS-EDT-DATE                TO LD79-RUN-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F730.                                                      
      *---------------------------------------------------------------*         
           READ BP13F730                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F730-EOF                       
                MOVE HIGH-VALUES           TO WS-F730-KEY-FLD                   
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F730-READ.                     
                                                                                
           MOVE F730-NUM-ALLO-CAT          TO WS-NUM-SALES-MODE.                
           MOVE F730-DTE-BALLOT            TO WS-DTE-BALLOT.                    
           MOVE F730-CDE-NT1               TO WS-CDE-NEW-TOWN.                  
           MOVE F730-CDE-FLAT-TYPE         TO WS-CDE-FLAT-TYPE.                 
           IF (F730-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T') AND                   
              (F730-NUM-MCPS = 'M' OR 'C' OR 'P' OR 'B')                        
              MOVE 'MCP'                   TO WS-CDE-ALLOC-SCH                  
           ELSE                                                                 
              IF F730-NUM-PORT-ELIG-TAG = 'P'                                   
                 MOVE 'SER'                TO WS-CDE-ALLOC-SCH                  
              ELSE                                                              
                 MOVE F730-CDE-ALLOC-SCH   TO WS-CDE-ALLOC-SCH                  
              END-IF                                                            
           END-IF.                                                              
           IF (F730-CDE-HOUSEHOLD = 'H' OR 'T') OR                              
              (F730-NUM-FT2T = 'Y')                                             
              MOVE 'H'                     TO WS-CDE-HOUSEHOLD                  
           ELSE                                                                 
              MOVE F730-CDE-HOUSEHOLD      TO WS-CDE-HOUSEHOLD                  
           END-IF.                                                              
                                                                                
           IF (F730-NUM-ALLO-CAT = 'SBF') AND                                   
              (WS-CDE-ALLOC-SCH  = 'MCP')                                       
              IF (F730-CDE-FLAT-TYPE = '2F') AND                                
                 (F730-NUM-ELDERLY = 'S' OR 'F')                                
                 GO TO 2000-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13K811      THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-READ-BP13K811.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                      TO BP13K811-REC.                    
           INITIALIZE                          BP13K811-REC.                    
                                                                                
           MOVE WS-F730-KEY-FLD             TO K811-KEY-FLD.                    
                                                                                
           READ BP13K811.                                                       
                                                                                
           EVALUATE WS-K811-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 4000-WRITE-BP13LD79   THRU 4000-EXIT                      
                                                                                
           WHEN 23                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K811 : ' WS-K811-STATUS             
              MOVE WS-K811-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-WRITE-BP13LD79.                                                     
      *---------------------------------------------------------------*         
           IF WS-LD79-LINE > 55                                                 
              PERFORM 4100-WRITE-HEAD-LD79 THRU 4100-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 4200-WRITE-DET-LD79     THRU 4200-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-WRITE-HEAD-LD79.                                                    
      *---------------------------------------------------------------*         
           ADD 1                       TO WS-LD79-PAGE.                         
           MOVE WS-LD79-PAGE           TO LD79-PAGE-NO.                         
                                                                                
           WRITE BP13LD79-REC          FROM LD79-HEADING-01 AFTER PAGE.         
           WRITE BP13LD79-REC          FROM LD79-HEADING-02.                    
           WRITE BP13LD79-REC          FROM LD79-HEADING-05.                    
           WRITE BP13LD79-REC          FROM LD79-HEADING-05.                    
           WRITE BP13LD79-REC          FROM LD79-HEADING-03.                    
           WRITE BP13LD79-REC          FROM LD79-HEADING-04.                    
                                                                                
           MOVE 6                      TO WS-LD79-LINE.                         
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-WRITE-DET-LD79.                                                     
      *---------------------------------------------------------------*         
           MOVE SPACES                      TO LD79-DETAIL-01.                  
                                                                                
           ADD 1                            TO WS-LD79-SNO                      
                                               WS-LD79-WRITE                    
                                               WS-LD79-LINE.                    
                                                                                
           MOVE WS-LD79-SNO                 TO LD79-S-NO.                       
           MOVE F730-NUM-REGN               TO LD79-REGN.                       
           MOVE K811-NUM-SALES-MODE         TO LD79-SMODE.                      
           MOVE K811-DTE-BALLOT             TO LD79-DTEBAL.                     
           MOVE K811-CDE-NEW-TOWN           TO LD79-NT.                         
           MOVE K811-CDE-FLAT-TYPE          TO LD79-FT.                         
           MOVE K811-CDE-ALLOC-SCH          TO LD79-ALLOC-SCH.                  
           MOVE K811-CDE-HOUSEHOLD          TO LD79-HH.                         
           WRITE BP13LD79-REC               FROM LD79-DETAIL-01.                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13CD79       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F730-READ               TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13F730        : ' WS-EDIT1.          
           MOVE WS-LD79-WRITE              TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WRITTEN TO BP13LD79       : ' WS-EDIT1.          
           DISPLAY ' '.                                                         
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE BP13F730                                                       
                 BP13K811                                                       
                 BP13LD79.                                                      
                                                                                
           IF WS-K811-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN CLOSING BP13K811 : ' WS-K811-STATUS             
              MOVE WS-K811-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
