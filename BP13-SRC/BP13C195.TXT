       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C195.                                                    
      *AUTHOR.     SURESH.                                                      
      *DATE-WRITTEN. 17/07/1999.                                                
      * ====================================================== *                
      *              SYSTEM OF COMMITMENT (BP13)               *                
      * ====================================================== *                
      * PURPOSE     : TO UPDATE MOVE IN AND MOVE OUT DATE      *                
      *               FROM MOBILITY RECORD TO BP13F716(HOUSE-  *                
      *               -HOLD INFO FROM RDBII)                   *                
      *                                                        *                
      * INPUT       : P13F716A - (MOBILITY FROM CDB)           *                
      * I-O         : BP13F716 - (HOUSHOLD INFO FROM RDBII)    *                
      *                                                        *                
      * OUTPUT FILE : P13F716B - (UPDATED HOSEHOLD INFO)       *                
      * ------------------------------------------------------ *                
      * CHG-NO     OIC    DATE      DESCRIPTION                *                
      * ======    =====   ====      ============               *                
      * BP130630   SRN  24/05/1999  NEW PROGRAM                *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716   ASSIGN       TO BP13F716.                          
                                                                                
           SELECT P13F716A   ASSIGN       TO P13F716A.                          
                                                                                
           SELECT P13F716B   ASSIGN       TO P13F716B.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F716.                                                           
                                                                                
       FD   P13F716A                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 P13F716A-REC.                                                         
          05 F716A-KEYFLD           PIC X(17).                                  
          05 FILLER                 PIC X(97).                                  
          05 F716A-DTE-MOVING-IN    PIC X(8).                                   
          05 F716A-DTE-MOVING-OUT   PIC X(8).                                   
          05 FILLER                 PIC X(219).                                 
                                                                                
       FD   P13F716B                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 P13F716B-REC             PIC X(350).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F716-READ          PIC 9(8)    VALUE 0.                         
          05 WS-F716A-READ         PIC 9(8)    VALUE 0.                         
          05 WS-F716-WRITE         PIC 9(8)    VALUE 0.                         
                                                                                
       01 WS-PREV-REGN             PIC X(8)    VALUE SPACES.                    
                                                                                
       01 WS-F716-KEYFLD           PIC X(17)   VALUE SPACES.                    
                                                                                
       01 WS-SYS-DATE              PIC X(8)    VALUE SPACES.                    
                                                                                
       01 WS-EDIT-DATE             PIC X(10)   VALUE SPACES.                    
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
       0000-MAIN.                                                               
            PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                         
                                                                                
            PERFORM 2000-READ-BP13F716  THRU 2000-EXIT.                         
                                                                                
            PERFORM 3000-READ-P13F716A  THRU 3000-EXIT.                         
                                                                                
            PERFORM 4000-PROCESS        THRU 4000-EXIT UNTIL                    
                    WS-F716-KEYFLD = HIGH-VALUES.                               
                                                                                
            PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPEN-FILES.                                                         
            OPEN INPUT  P13F716A                                                
                        BP13F716                                                
                 OUTPUT P13F716B.                                               
                                                                                
            ACCEPT WS-SYS-DATE   FROM DATE YYYYMMDD.                            
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-READ-BP13F716.                                                      
            READ BP13F716  AT END                                               
                           MOVE HIGH-VALUES  TO WS-F716-KEYFLD                  
                           GO         TO  2000-EXIT.                            
                                                                                
            STRING F716-NUM-REGN, F716-NUM-NRIC                                 
                   DELIMITED BY SIZE                                            
                   INTO                                                         
                   WS-F716-KEYFLD.                                              
                                                                                
            ADD 1                     TO  WS-F716-READ.                         
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3000-READ-P13F716A.                                                      
            READ P13F716A  AT END                                               
                           MOVE HIGH-VALUES  TO F716A-KEYFLD                    
                           GO         TO  3000-EXIT.                            
                                                                                
            ADD 1                     TO  WS-F716A-READ.                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4000-PROCESS.                                                            
            IF WS-F716-KEYFLD < F716A-KEYFLD                                    
               PERFORM 4500-WRITE-P13F716B   THRU 4500-EXIT                     
               PERFORM 2000-READ-BP13F716    THRU 2000-EXIT                     
            ELSE                                                                
            IF WS-F716-KEYFLD > F716A-KEYFLD                                    
               PERFORM 3000-READ-P13F716A    THRU 3000-EXIT                     
            ELSE                                                                
               MOVE F716A-DTE-MOVING-IN      TO F716-DTE-MOVING-IN              
               MOVE F716A-DTE-MOVING-OUT     TO F716-DTE-MOVING-OUT             
               PERFORM 4500-WRITE-P13F716B   THRU 4500-EXIT                     
               PERFORM 2000-READ-BP13F716    THRU 2000-EXIT                     
               PERFORM 3000-READ-P13F716A    THRU 3000-EXIT                     
            END-IF.                                                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4500-WRITE-P13F716B.                                                     
                                                                                
            WRITE P13F716B-REC           FROM BP13F716-REC.                     
            ADD  1                       TO WS-F716-WRITE.                      
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9000-CLOSE-FILES.                                                        
            STRING WS-SYS-DATE(7:2) DELIMITED BY SIZE,                          
                   '/',                                                         
                   WS-SYS-DATE(5:2) DELIMITED BY SIZE,                          
                   '/',                                                         
                   WS-SYS-DATE(1:4) DELIMITED BY SIZE                           
                   INTO                                                         
                   WS-EDIT-DATE.                                                
                                                                                
            DISPLAY '____________________________________________'              
            DISPLAY ' '                                                         
            DISPLAY 'CONTROL TOTALS  PGM:- BP13C195 DATE ' WS-EDIT-DATE         
            DISPLAY 'RECORDS READ FROM BP13F716  - ' WS-F716-READ               
            DISPLAY 'RECORDS READ FROM P13F716A  - ' WS-F716A-READ              
            DISPLAY 'RECORDS WRITTEN TO P13F716B - ' WS-F716-WRITE              
            DISPLAY '____________________________________________'              
                                                                                
            CLOSE      BP13F716                                                 
                       P13F716A                                                 
                       P13F716B.                                                
            STOP RUN.                                                           
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
