      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C32F.                                                 
      *AUTHOR.        GURU..                                                    
      *DATE-WRITTEN.  08/09/2020.                                               
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *    1.   THIS IS A PROGRAM TO PRINT A REPORT ON BOOKING *                
      *         FEES NOT REFUNDED > 14 DAYS                    *                
      *                                                        *                
      *    INPUT FILES :                                       *                
      *    1.   BP13K800 (SOC  MASTER)                         *                
      *                                                        *                
      *    OUTPUT LISTING :                                    *                
      *    1.   BP13L32F - SUMMARY LIST OF BOOKING FEES NOT    *                
      *                    FEFUNDED AFTER MORE THAN 14 DAYS    *                
      *                    FROM DATE OF AO.                    *                
      **********************************************************                
      * CHG-NO.    BY      ON       DESCRIPTION                *                
      * --------  ----  --------  -----------------------------*                
      * BP138445  KR13  08092020  CLONE OF BP13C329.           *                
      **********************************************************                
                                                                                
      *************************                                                 
       ENVIRONMENT DIVISION.                                                    
      *************************                                                 
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *************************                                                 
       INPUT-OUTPUT SECTION.                                                    
      *************************                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800   ASSIGN TO BP13F800.                                
                                                                                
           SELECT SY02F001   ASSIGN TO SY02F001.                                
                                                                                
           SELECT BP13L32F   ASSIGN TO BP13L32F.                                
                                                                                
                                                                                
      *************************                                                 
       DATA DIVISION.                                                           
      *************************                                                 
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13L32F                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  L32F-REC                    PIC X(132).                              
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
       01  WS-F800-READ                PIC 9(8)    VALUE ZEROES.                
       01  WS-F800-BYPASS              PIC 9(8)    VALUE ZEROES.                
       01  WS-F800-PROCESS             PIC 9(8)    VALUE ZEROES.                
       01  WS-LINE                     PIC 9(2)    VALUE 65.                    
       01  WS-CASE                     PIC 9(4)    VALUE ZEROS.                 
       01  WS-PAGE                     PIC 9(4)    VALUE ZEROES.                
       01  WS-GRAND-TOT1               PIC S9(6)V99 VALUE ZEROES.               
       01  WS-GRAND-TOT2               PIC S9(8)V99 VALUE ZEROES.               
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CCYY            PIC 9(4).                                
           05  WS-CURR-MM              PIC 99.                                  
           05  WS-CURR-DD              PIC 99.                                  
       01  WS-DATE-9 REDEFINES WS-CURRENT-DATE PIC 9(8).                        
                                                                                
       01  WS-AGMT-DATE.                                                        
           05  WS-AGMT-CCYY            PIC X(4).                                
           05  WS-AGMT-MM              PIC X(2).                                
           05  WS-AGMT-DD              PIC X(2).                                
                                                                                
       01  WS-DTE-SALE-AGMT            PIC 9(08).                               
       01  WS-DATE-INTEGER             PIC S9(9).                               
       01  WS-DTE-TEMP-INTEGER         PIC S9(9).                               
       01  WS-DIFF-DAY                 PIC S9(3) VALUE ZEROES.                  
                                                                                
       01 WS-F001-EOF                  PIC X VALUE 'N'.                         
          88 EOF-F001                        VALUE 'Y'.                         
                                                                                
      *----------------------*                                                  
      * LINKAGE FOR BP13C913 *                                                  
      *----------------------*                                                  
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH          PIC X(4).                           
               10  WS-LINK-NUM-ACC          PIC X(5).                           
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      **********************************************************                
      *    WS FOR SUMMARY LIST OF AGREEMENT ORDER RAISED       *                
      **********************************************************                
                                                                                
       01  L32F-HDG1.                                                           
           05  FILLER              PIC X(8)    VALUE 'BP13L32F'.                
           05  FILLER              PIC X(17)   VALUE SPACES.                    
           05  FILLER              PIC X(8)    VALUE 'HDB3    '.                
           05  FILLER              PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(16)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE: '.                  
           05  L32F-PRT-DATE.                                                   
               10  L32F-PRT-DD     PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  L32F-PRT-MM     PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  L32F-PRT-CCYY   PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'PAGE: '.                  
           05  L32F-PAGE           PIC ZZZZ9.                                   
                                                                                
       01  L32F-HDG2.                                                           
           05  FILLER              PIC X(49)   VALUE SPACES.                    
           05  FILLER              PIC X(30)   VALUE                            
              'SALES SECTION (AGREEMENT TEAM)'.                                 
       01  L32F-HDG3.                                                           
           05  FILLER              PIC X(51)   VALUE SPACES.                    
           05  FILLER              PIC X(90)   VALUE                            
              'BOOKING FEES NOT REFUNDED'.                                      
                                                                                
       01  L32F-HDG4.                                                           
           05  FILLER              PIC X(42)   VALUE SPACES.                    
           05  FILLER              PIC X(90)   VALUE                            
              'MORE THAN 14 DAYS FROM DATE OF AGREEMENT ORDER'.                 
                                                                                
       01  L32F-HDG5.                                                           
           05  FILLER              PIC X(08)   VALUE SPACES.                    
           05  FILLER              PIC X(15)   VALUE 'AGREEMENT ORDER'.         
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'CURRENT'.                 
           05  FILLER              PIC X(32)   VALUE SPACES.                    
           05  FILLER              PIC X(15)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE ' BOOKING FEE '.           
           05  FILLER              PIC X(07)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE 'SOC DEBTOR'.              
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(11)   VALUE 'DATE ACTION'.             
                                                                                
       01  L32F-HDG6.                                                           
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'S/N '.                    
           05  FILLER              PIC X(06)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'DATE'.                    
           05  FILLER              PIC X(09)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'REGN NO.'.                
           05  FILLER              PIC X(19)   VALUE SPACES.                    
           05  FILLER              PIC X(19)   VALUE 'ADDRESS'.                 
           05  FILLER              PIC X(07)   VALUE SPACES.                    
           05  FILLER              PIC X(17) VALUE 'TO BE REFUNDED($)'.         
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE ' STATUS'.                 
           05  FILLER              PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'TAKEN  '.                 
                                                                                
       01  L32F-DETAIL.                                                         
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  L32F-NUM            PIC ZZZ9    VALUE ZEROS.                     
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  L32F-AGR-ORDER-DATE.                                             
               10  L32F-DD-SALE-AGR PIC X(2)   VALUE SPACES.                    
               10  FILLER           PIC X       VALUE '/'.                      
               10  L32F-MM-SALE-AGR PIC X(2)   VALUE SPACES.                    
               10  FILLER           PIC X       VALUE '/'.                      
               10  L32F-CCYY-SALE-AGR PIC X(4) VALUE SPACES.                    
           05  FILLER              PIC X(4)   VALUE SPACES.                     
           05  L32F-CURR-REGNO     PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L32F-BLK-DES        PIC X(05)   VALUE SPACES.                    
           05  L32F-BLK-NO         PIC X(05).                                   
           05  L32F-FLAT-NO.                                                    
               10  L32F-HASH-DES   PIC X(02)   VALUE ' #'.                      
               10  L32F-LEVEL-NO   PIC X(02).                                   
               10  L32F-DASH-DES   PIC X(01)   VALUE '-'.                       
               10  L32F-UNIT-NO    PIC X(04).                                   
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  L32F-STREET         PIC X(22)   VALUE SPACES.                    
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  L32F-BK-FEE-PAID    PIC Z,ZZ9.99- VALUE ZEROS.                   
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  L32F-SOC-DEBTOR     PIC Z,ZZZ,ZZ9.99- VALUE ZEROS.               
           05  FILLER              PIC X(04)   VALUE SPACES.                    
                                                                                
       01  L32F-FTR1.                                                           
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(18)   VALUE                            
              'NUMBER OF CASES = '.                                             
           05  L32F-NOC            PIC ZZZZZ9.                                  
           05  FILLER              PIC X(38)   VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE                            
              'GRAND TOTALS =  '.                                               
           05  L32F-GRAND-TOT1     PIC ZZZ,ZZ9.99- VALUE ZEROS.                 
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  L32F-GRAND-TOT2     PIC ZZ,ZZZ,ZZ9.99- VALUE ZEROS.              
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      ******************************************************************        
       MAIN-ROUTINE SECTION.                                                    
      ******************************************************************        
           PERFORM 10-OPEN-FILES    THRU 10-EXIT.                               
                                                                                
           PERFORM 20-READ-BP13F800 THRU 20-EXIT                                
                                                                                
           PERFORM 30-PROCESS-F800  THRU 30-EXIT                                
             UNTIL  F800-NUM-REGN = HIGH-VALUE.                                 
                                                                                
           PERFORM 48-PRINT-TOTAL   THRU 48-EXIT.                               
                                                                                
           IF WS-F800-PROCESS   = ZEROS                                         
              MOVE ZEROS            TO L32F-BK-FEE-PAID                         
              MOVE ZEROS            TO L32F-SOC-DEBTOR                          
              PERFORM 32-PRINT-DTL  THRU 32-EXIT.                               
              PERFORM 33-PRINT-FTR  THRU 33-EXIT.                               
                                                                                
           PERFORM 50-CLOSE-ROUTINE THRU 50-EXIT.                               
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       10-OPEN-FILES.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F800                                                 
                       SY02F001                                                 
                OUTPUT BP13L32F.                                                
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
                                                                                
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              GO TO 50-CLOSE-ROUTINE                                            
           ELSE                                                                 
              MOVE F001-DTE-CURRENT       TO WS-CURRENT-DATE                    
           END-IF.                                                              
                                                                                
           MOVE WS-CURR-DD   TO L32F-PRT-DD.                                    
           MOVE WS-CURR-MM   TO L32F-PRT-MM.                                    
           MOVE WS-CURR-CCYY TO L32F-PRT-CCYY.                                  
                                                                                
           COMPUTE WS-DATE-INTEGER =                                            
                             FUNCTION INTEGER-OF-DATE (WS-DATE-9).              
                                                                                
       10-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       20-READ-BP13F800.                                                        
      ******************************************************************        
           READ BP13F800 AT END                                                 
                MOVE HIGH-VALUE TO F800-NUM-REGN                                
                GO TO 20-EXIT.                                                  
                                                                                
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       20-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       30-PROCESS-F800.                                                         
      ******************************************************************        
           IF F800-DTE-SALE-AGMT IS NOT NUMERIC                                 
               MOVE ZEROS TO F800-DTE-SALE-AGMT.                                
           IF F800-DTE-BK-APPT IS NOT NUMERIC                                   
               MOVE ZEROS TO F800-DTE-BK-APPT.                                  
           IF F800-DTE-CANCEL  IS NOT NUMERIC                                   
               MOVE ZEROS TO F800-DTE-CANCEL.                                   
           IF F800-NUM-ALLOC-DATE IS NOT NUMERIC                                
               MOVE ZEROS TO F800-NUM-ALLOC-DATE.                               
           IF F800-AMT-REGN-DEPOSIT-CASH IS NOT NUMERIC                         
               MOVE ZEROS TO F800-AMT-REGN-DEPOSIT-CASH.                        
           IF F800-AMT-REGN-DEPOSIT-CPF1 IS NOT NUMERIC                         
               MOVE ZEROS TO F800-AMT-REGN-DEPOSIT-CPF1.                        
           IF F800-AMT-REGN-DEPOSIT-CPF2 IS NOT NUMERIC                         
               MOVE ZEROS TO F800-AMT-REGN-DEPOSIT-CPF2.                        
           IF F800-AMT-REGN-DEPOSIT-CPF3 IS NOT NUMERIC                         
               MOVE ZEROS TO F800-AMT-REGN-DEPOSIT-CPF3.                        
           IF F800-AMT-REGN-DEPOSIT-CPF4 IS NOT NUMERIC                         
               MOVE ZEROS TO F800-AMT-REGN-DEPOSIT-CPF4.                        
           IF F800-AMT-CD IS NOT NUMERIC                                        
               MOVE ZEROS TO F800-AMT-CD.                                       
           IF F800-AMT-SOC-DEBTOR IS NOT NUMERIC                                
               MOVE ZEROS TO F800-AMT-SOC-DEBTOR.                               
           IF F800-AMT-BK-PAID IS NOT NUMERIC                                   
               MOVE ZEROS TO F800-AMT-BK-PAID.                                  
                                                                                
           IF F800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              PERFORM 31-GET-UNIT-ADDRESS THRU 31-EXIT                          
           END-IF.                                                              
                                                                                
           IF F800-DTE-SALE-AGMT > ZEROS                                        
              MOVE F800-DTE-SALE-AGMT        TO WS-DTE-SALE-AGMT                
              PERFORM 35-COMPUTE-DAYS       THRU 35-EXIT                        
           ELSE                                                                 
              MOVE ZEROS TO WS-DIFF-DAY.                                        
                                                                                
           IF (F800-DTE-SALE-AGMT > ZEROES)  AND                                
              (F800-AMT-BK-PAID   > ZEROES)  AND                                
              (F800-NUM-HALF-IDP NOT = 'D' AND 'F')                             
                                                                                
               IF WS-DIFF-DAY > 14                                              
                   ADD 1 TO WS-F800-PROCESS                                     
                   PERFORM 32-PRINT-DTL        THRU 32-EXIT                     
               ELSE                                                             
                   ADD 1 TO WS-F800-BYPASS.                                     
                                                                                
           MOVE ZEROES TO WS-DIFF-DAY.                                          
                                                                                
           PERFORM 20-READ-BP13F800 THRU 20-EXIT.                               
                                                                                
       30-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       31-GET-UNIT-ADDRESS.                                                     
      ******************************************************************        
           MOVE SPACES                 TO BP13COMM8-REC.                        
           INITIALIZE                     BP13COMM8-REC.                        
                                                                                
           MOVE F800-NUM-SCH-ACC       TO WS-LINK-REC.                          
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR = 0                                              
              MOVE COMM8-NUM-BLK       TO L32F-BLK-NO                           
              MOVE COMM8-NME-STREET    TO L32F-STREET                           
              MOVE COMM8-NUM-UNIT-MAIN TO L32F-UNIT-NO                          
              MOVE COMM8-NUM-LEVEL     TO L32F-LEVEL-NO                         
              MOVE 'BLK  '             TO L32F-BLK-DES                          
              MOVE ' #'                TO L32F-HASH-DES                         
              MOVE '-'                 TO L32F-DASH-DES                         
           ELSE                                                                 
              DISPLAY 'SQL CODE = ' COMM8-CDE-SYSERR                            
              MOVE SPACES TO L32F-BLK-NO                                        
                             L32F-STREET                                        
                             L32F-UNIT-NO                                       
                             L32F-LEVEL-NO                                      
                             L32F-BLK-DES                                       
                             L32F-HASH-DES                                      
                             L32F-DASH-DES.                                     
       31-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       32-PRINT-DTL.                                                            
      ******************************************************************        
           ADD 1 TO WS-CASE.                                                    
                                                                                
           IF WS-LINE > 52                                                      
              ADD 1 TO WS-PAGE                                                  
              MOVE WS-PAGE TO L32F-PAGE                                         
              WRITE L32F-REC FROM L32F-HDG1 AFTER PAGE                          
              WRITE L32F-REC FROM L32F-HDG2 AFTER 1                             
              WRITE L32F-REC FROM L32F-HDG3 AFTER 2                             
              WRITE L32F-REC FROM L32F-HDG4 AFTER 1                             
              WRITE L32F-REC FROM L32F-HDG5 AFTER 3                             
              WRITE L32F-REC FROM L32F-HDG6 AFTER 1                             
              MOVE SPACES TO L32F-REC                                           
              WRITE L32F-REC AFTER 1                                            
              MOVE 6 TO WS-LINE.                                                
                                                                                
           MOVE WS-CASE                TO L32F-NUM.                             
           MOVE F800-NUM-REGN          TO L32F-CURR-REGNO.                      
           MOVE F800-DTE-SALE-AGMT     TO WS-AGMT-DATE.                         
           MOVE WS-AGMT-CCYY           TO L32F-CCYY-SALE-AGR.                   
           MOVE WS-AGMT-MM             TO L32F-MM-SALE-AGR.                     
           MOVE WS-AGMT-DD             TO L32F-DD-SALE-AGR.                     
           MOVE F800-AMT-BK-PAID       TO L32F-BK-FEE-PAID.                     
           MOVE F800-AMT-SOC-DEBTOR    TO L32F-SOC-DEBTOR.                      
                                                                                
           COMPUTE WS-GRAND-TOT1 = WS-GRAND-TOT1 + F800-AMT-BK-PAID.            
           COMPUTE WS-GRAND-TOT2 = WS-GRAND-TOT2 + F800-AMT-SOC-DEBTOR.         
                                                                                
           WRITE L32F-REC FROM L32F-DETAIL AFTER 1.                             
           ADD 1 TO WS-LINE.                                                    
                                                                                
       32-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       33-PRINT-FTR.                                                            
      ******************************************************************        
           MOVE WS-CASE                TO L32F-NOC.                             
           MOVE WS-GRAND-TOT1          TO L32F-GRAND-TOT1.                      
           MOVE WS-GRAND-TOT2          TO L32F-GRAND-TOT2.                      
           WRITE L32F-REC FROM L32F-FTR1.                                       
       33-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       35-COMPUTE-DAYS.                                                         
      ******************************************************************        
                                                                                
           COMPUTE WS-DTE-TEMP-INTEGER =                                        
                           FUNCTION INTEGER-OF-DATE (WS-DTE-SALE-AGMT)          
                                                                                
           COMPUTE WS-DIFF-DAY = WS-DATE-INTEGER - WS-DTE-TEMP-INTEGER.         
                                                                                
       35-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       48-PRINT-TOTAL.                                                          
      ******************************************************************        
           DISPLAY '-- BP13C32F  CONTROL COUNT --  '.                           
           DISPLAY ' '.                                                         
           DISPLAY 'F800-READ             : ' WS-F800-READ.                     
           DISPLAY 'F800-BYPASS-PRINTING  : ' WS-F800-BYPASS.                   
           DISPLAY 'F800-PROCESS-PRINTING : ' WS-F800-PROCESS.                  
                                                                                
       48-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       50-CLOSE-ROUTINE.                                                        
      ******************************************************************        
           CLOSE BP13F800 BP13L32F.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       50-EXIT.                                                                 
           EXIT.                                                                
