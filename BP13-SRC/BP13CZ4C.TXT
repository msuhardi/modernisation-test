       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CZ4C.                                                 
      *AUTHOR.        SMP3.                                                     
      *DATE-WRITTEN.  22/02/2021.                                               
      * ============================================================= *         
      *                  SYSTEM OF COMMITMENT (BP13)                  *         
      * ============================================================= *         
      *                                                               *         
      *  OBJECTIVE  :  TO TALLY SHORTLISTED APPLICATIONS, CALCULATE   *         
      *                BALANCE FLAT SUPPLY AND ETHNIC QUOTA FOR       *         
      *                REMAINING APPLICATIONS AND UPDATE IN BP13K010  *         
      *                PA QUOTA                                       *         
      *                                                               *         
      *  INPUT FILE :  BP13F740  CCA SHORTLISTED RECORDS              *         
      *  I-O FILE   :  BP13K010  BP13.K010.TENDER                     *         
      *                                                               *         
      * ============================================================= *         
      * CHG-REF#  BY  DDMMCCYY DESCRIPTION                            *         
      * -------- ---- -------- -------------------------------------- *         
      * BP138600 SMP3 22022021 NEW PROGRAM                            *         
      * ============================================================= *         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS       STANDARD.                                       
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K010                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BP13K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K010-STATUS             PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ               PIC 9(08) VALUE ZEROES.               
           05  WS-K010-FND                PIC 9(08) VALUE ZEROES.               
           05  WS-K010-NTFND              PIC 9(08) VALUE ZEROES.               
           05  WS-K010-UPD                PIC 9(08) VALUE ZEROES.               
           05  WS-K010-NOFS               PIC 9(08) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF                PIC X(01) VALUE 'N'.                  
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-SYSTEM-DATE-TIME.                                             
               10  WS-SYSTEM-DATE.                                              
                   15  WS-SYS-CCYY        PIC X(04) VALUE SPACES.               
                   15  WS-SYS-MM          PIC X(02) VALUE SPACES.               
                   15  WS-SYS-DD          PIC X(02) VALUE SPACES.               
               10  WS-SYSTEM-TIME         PIC X(08) VALUE SPACES.               
           05  WS-EDITED-DATE             PIC X(10) VALUE SPACES.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-BP13F740   THRU 3000-EXIT                       
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F740                                                 
                  I-O  BP13K010.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K010, STATUS: ' WS-K010-STATUS         
              MOVE WS-K010-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-SYSTEM-DATE-TIME.              
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE INTO WS-EDITED-DATE.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y'                   TO WS-F740-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F740-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-BP13F740.                                                   
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4000-READ-BP13K010      THRU 4000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13K010.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                     TO BP13K010-TENDER.                  
           INITIALIZE                         BP13K010-TENDER.                  
                                                                                
           MOVE F740-CDE-NT1               TO K010-CDE-NT-ZONE.                 
           MOVE F740-CDE-FLAT-TYPE         TO K010-CDE-FLAT-TY.                 
                                                                                
           READ BP13K010.                                                       
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                      TO WS-K010-FND                       
                IF K010-NUM-TOT-PA > 0                                          
                   PERFORM 5000-UPD-BP13K010  THRU 5000-EXIT                    
                ELSE                                                            
                   ADD 1                   TO WS-K010-NOFS                      
                   DISPLAY 'NO MORE FLAT SUPPLY FOR PA: ' K010-KEY-FLD          
                END-IF                                                          
                                                                                
           WHEN 23                                                              
                ADD 1                      TO WS-K010-NTFND                     
                DISPLAY 'RECORD NOT FOUND IN BP13K010: ' K010-KEY-FLD           
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K010, STATUS: '                      
                         WS-K010-STATUS                                         
                MOVE WS-K010-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-UPD-BP13K010.                                                       
      *-------------------------------------------------------------            
                                                                                
           COMPUTE K010-NUM-TOT-PA = K010-NUM-TOT-PA - 1.                       
                                                                                
           EVALUATE F740-CDE-CATEGORY                                           
           WHEN '1'                                                             
                IF K010-NUM-MAX-PA1 > 0                                         
                   COMPUTE K010-NUM-MAX-PA1 = K010-NUM-MAX-PA1 - 1              
                ELSE                                                            
                   MOVE ZEROES             TO K010-NUM-MAX-PA1                  
                END-IF                                                          
                                                                                
           WHEN '2'                                                             
                IF K010-NUM-MAX-PA2 > 0                                         
                   COMPUTE K010-NUM-MAX-PA2 = K010-NUM-MAX-PA2 - 1              
                ELSE                                                            
                   MOVE ZEROES             TO K010-NUM-MAX-PA2                  
                END-IF                                                          
                                                                                
           WHEN '3'                                                             
           WHEN '4'                                                             
                IF K010-NUM-MAX-PA3 > 0                                         
                   COMPUTE K010-NUM-MAX-PA3 = K010-NUM-MAX-PA3 - 1              
                ELSE                                                            
                   MOVE ZEROES             TO K010-NUM-MAX-PA3                  
                END-IF                                                          
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-SYSTEM-DATE             TO K010-DTE-UPDATE.                  
           MOVE WS-SYSTEM-TIME             TO K010-TME-SYSTEM.                  
           MOVE 'P13CZ4C'                  TO K010-USERID.                      
                                                                                
           REWRITE BP13K010-TENDER.                                             
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                      TO WS-K010-UPD                       
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR UPDATING BP13K010, STATUS: '                     
                         WS-K010-STATUS                                         
                MOVE WS-K010-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '=========================================='.                
           DISPLAY '      PROGRAM : BP13CZ4C  ' WS-EDITED-DATE.                 
           DISPLAY '=========================================='.                
           DISPLAY ' '.                                                         
           DISPLAY ' 1. BP13F740 RECORDS READ      : ' WS-F740-READ.            
           DISPLAY ' 2. BP13K010 RECORDS FOUND     : ' WS-K010-FND.             
           DISPLAY ' 3. BP13K010 RECORDS NOT FOUND : ' WS-K010-NTFND.           
           DISPLAY ' 4. BP13K010 RECORDS UPDATED   : ' WS-K010-UPD.             
           DISPLAY ' 5. NO MORE FLAT SUPPLY FOR PA : ' WS-K010-NOFS.            
           DISPLAY ' '.                                                         
           DISPLAY ' CHECK : '.                                                 
           DISPLAY '    1 = 2 + 3 '.                                            
           DISPLAY '    2 = 4 + 5 '.                                            
           DISPLAY ' '.                                                         
           DISPLAY '=========================================='.                
                                                                                
           CLOSE BP13F740                                                       
                 BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K010, STATUS: ' WS-K010-STATUS         
              MOVE WS-K010-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
