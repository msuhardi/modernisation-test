       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C941.                                                 
      *AUTHOR.        JAM.                                                      
      *DATE-CREATED.  09/06/2001.                                               
      ******************************************************                    
      *                                                    *                    
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                *                    
      *                                                    *                    
      *  SYSTEM ID   : BP13                                *                    
      *                                                    *                    
      *  OBJECTIVE   :                                     *                    
      *  1. TO UPDATE RECORDS FOR BP13K940 & BP13K943      *                    
      *     USING SEQUENTIAL FILES BP18F300 & BP18F310.    *                    
      *     CONVERTING ETHNIC CODES & CITIZENSHIP CODES.   *                    
      *                                                    *                    
      *  INPUT FILE(S):                                    *                    
      *  1. BP18F300   - SEQUENTIAL                        *                    
      *  2. BP18F310   - SEQUENTIAL                        *                    
      *                                                    *                    
      *  OUTPUT FILE(S):                                   *                    
      *  1. BP13K940   - VSAM                              *                    
      *  2. BP13K943   - VSAM                              *                    
      *                                                    *                    
      *====================================================*                    
      * CHNGE #    BY       ON       DESCRIPTION           *                    
      * --------- ------ -------- -------------------------*                    
      * BP131041  JAM    09062001 NEW PROGRAM              *                    
      * BP131041  JAM    25072001 CHANGED CODING FOR ETHNIC*                    
      *                           GROUP AS FOLLOWS:        *                    
      *                           5 = M (MALAY),  PREV = I *                    
      *                           6 = I (INDIAN), PREV = M *                    
      * BP131059  JAM    01092001 CONVERT RELATIONSHIP     *                    
      *                           CODES FROM RDB TO PIDB   *                    
      * BP131046  JAM    07032002 READ F300 AND F310       *                    
      *                           TOGETHER FOR UPDATING    *                    
      *                           ROLE IN BP13K943         *                    
      * BP137319  SRG1   25052018 MOVED SALE-WITHIN-MOP    *                    
      *                           FROM BP18F300 TO BP13K940*                    
      ******************************************************                    
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP18F300             ASSIGN        TO BP18F300.               
                                                                                
           SELECT BP18F310             ASSIGN        TO BP18F310.               
                                                                                
           SELECT BP13K940             ASSIGN       TO BP13K940                 
                                       ACCESS MODE  IS RANDOM                   
                                       ORGANIZATION IS INDEXED                  
                                       RECORD KEY   IS K940-KEY-NEW             
                                       FILE STATUS  IS WS-K940-STATUS.          
                                                                                
           SELECT BP13K943             ASSIGN        TO BP13K943                
                                       ACCESS MODE   IS RANDOM                  
                                       ORGANIZATION  IS INDEXED                 
                                       RECORD KEY    IS K943-KEY-FLD            
                                       FILE STATUS   IS WS-K943-STATUS.         
                                                                                
           SELECT BP13KD05             ASSIGN        TO BP13KD05                
                                       ACCESS MODE   IS RANDOM                  
                                       ORGANIZATION  IS INDEXED                 
                                       RECORD KEY    IS KD05-KEY-FLD            
                                       FILE STATUS   IS WS-KD05-STATUS.         
                                                                                
           SELECT BP13KD07             ASSIGN        TO BP13KD07                
                                       ACCESS MODE   IS RANDOM                  
                                       ORGANIZATION  IS INDEXED                 
                                       RECORD KEY    IS KD07-KEY-FLD            
                                       FILE STATUS   IS WS-KD07-STATUS.         
                                                                                
           SELECT BP13L941             ASSIGN        TO BP13L941.               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP18F300                                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP18F300.                                                           
                                                                                
       FD  BP18F310                                                             
           RECORD CONTAINS 250 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP18F310.                                                           
                                                                                
       FD  BP13K940                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K940.                                                           
                                                                                
       FD  BP13KD07                                                             
           RECORD CONTAINS 700 CHARACTERS.                                      
       COPY BP13KD07.                                                           
                                                                                
       FD  BP13KD05                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13KD05.                                                           
                                                                                
       FD  BP13K943                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K943.                                                           
                                                                                
       FD  BP13L941                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS OMITTED.                                               
       01  BP13L941-REC    PIC X(132).                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FLAGS.                                                            
           05  WS-F300-EOF             PIC X(01) VALUE 'N'.                     
           05  WS-F310-EOF             PIC X(01) VALUE 'N'.                     
           05  WS-UPD-ETH-FLAG         PIC X(01) VALUE 'N'.                     
           05  WS-UPD-CTZ-FLAG         PIC X(01) VALUE 'N'.                     
           05  WS-UPD-REL-FLAG         PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-F300-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-F310-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K940-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K943-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-KD05-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-KD07-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-FILE-COUNTERS.                                                    
           05  WS-F300-READ            PIC 9(06) VALUE ZEROES.                  
           05  WS-F310-READ            PIC 9(06) VALUE ZEROES.                  
           05  WS-K940-WRITE           PIC 9(06) VALUE ZEROES.                  
           05  WS-K943-WRITE           PIC 9(06) VALUE ZEROES.                  
           05  WS-K940-EXIST           PIC 9(06) VALUE ZEROES.                  
           05  WS-K943-EXIST           PIC 9(06) VALUE ZEROES.                  
           05  WS-K943-ETH-CTR         PIC 9(06) VALUE ZEROES.                  
           05  WS-K943-CTZ-CTR         PIC 9(06) VALUE ZEROES.                  
           05  WS-K943-REL-CTR         PIC 9(06) VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(06) VALUE 99.                      
           05  WS-PAGE-CTR             PIC 9(06) VALUE ZEROES.                  
           05  WS-MAX-LINE-CTR         PIC 9(06) VALUE 58.                      
           05  WS-VALID-ETHNIC-CODES   PIC X(01).                               
               88  WS-VALID-ETHNIC    VALUES 'C' 'I' 'M' 'N' 'O' ' '.           
           05  WS-VALID-CTZ-CODES      PIC X(02).                               
               88  WS-VALID-CTZ       VALUES                                    
               'UN' 'ID' 'TH' 'IN' 'LK' 'BD' 'PH' 'TW' 'SG' 'UN' 'MY'           
               'SC' 'SP' 'SI' 'AL' 'OC' 'NA' ' '.                               
           05  WS-VALID-REL-CODES      PIC X(02).                               
               88  WS-VALID-REL       VALUES                                    
                  '00' '01' '02' '03' '04' '05' '06' '07' '08' '09'             
                  '10' '13' '14' '20' '21' '22' '30' '40' '42'                  
                  '50' '52'.                                                    
                                                                                
       01  WS-SYSTEM-DATETIME.                                                  
           05  WS-SYS-DATE             PIC 9(08) VALUE ZEROES.                  
           05  WS-SYS-TIME             PIC 9(08) VALUE ZEROES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
                                                                                
       01  WS-DISP-DATE.                                                        
           05  WS-DISP-DD              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-MM              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-CCYY            PIC X(04) VALUE SPACES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SENT-DATE            PIC 9(08)  VALUE ZEROES.                 
           05  WS-DIFF-DAY             PIC S9(06) VALUE ZEROES.                 
           05  WS-CURR-DATE-INT        PIC S9(09) VALUE ZEROES.                 
           05  WS-SENT-DATE-INT        PIC S9(09) VALUE ZEROES.                 
           05  WS-NUM-RLTNSP           PIC X(02)  VALUE SPACES.                 
           05  WS-NUM-ETHNIC           PIC X(01)  VALUE SPACES.                 
           05  WS-NUM-CTZNSP           PIC X(02)  VALUE SPACES.                 
           05  WS-REGN                 PIC X(08)  VALUE SPACES.                 
                                                                                
       01  WS-LINER                 PIC X(132) VALUE ALL '~'.                   
                                                                                
       01  WS-L941-LINER1.                                                      
           10  FILLER               PIC X(132) VALUE ALL "-".                   
                                                                                
       01  WS-L941-HEADER1.                                                     
           10  FILLER               PIC X(10)    VALUE SPACES.                  
           10  FILLER               PIC X(20) VALUE                             
               'K943 KEY FIELD    '.                                            
           10  FILLER               PIC X(24) VALUE                             
               '¦OLD ETHNIC ¦NEW ETHNIC '.                                      
           10  FILLER               PIC X(24) VALUE                             
               '¦OLD CTZNSHP¦NEW CTZNSHP'.                                      
           10  FILLER               PIC X(24) VALUE                             
               '¦OLD RELNSHP¦NEW RELNSHP'.                                      
           10  FILLER               PIC X(24) VALUE                             
               '¦ R  E  M  A  R  K  S   '.                                      
           10  FILLER               PIC X(20)    VALUE SPACES.                  
                                                                                
       01  WS-L941-TITLE1.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L941'.              
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'HDB3'.                  
           05  FILLER              PIC X(36)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'SOC  SYSTEM'.           
           05  FILLER              PIC X(32)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  WS-L941-CUR-DATE.                                                
              10  WS-L941-CUR-DD   PIC X(02).                                   
              10  FILLER           PIC X(01) VALUE '/'.                         
              10  WS-L941-CUR-MM   PIC X(02).                                   
              10  FILLER           PIC X(01) VALUE '/'.                         
              10  WS-L941-CUR-CC   PIC X(02).                                   
              10  WS-L941-CUR-YY   PIC X(02).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  WS-L941-PAGENO      PIC ZZZZ.                                    
                                                                                
       01  WS-L941-TITLE2.                                                      
           05  FILLER              PIC X(40)     VALUE SPACES.                  
           05  FILLER              PIC X(44)     VALUE                          
           'CONVERSION OF ETHNIC/CTZSHIP/RELSHIP CODES '.                       
           05  FILLER              PIC X(48)     VALUE SPACES.                  
                                                                                
       01  WS-L941-DETAIL1.                                                     
           05  FILLER               PIC X(10)    VALUE SPACES.                  
           05  WS-L941-KEY-FLD      PIC X(17).                                  
           05  FILLER               PIC X(09)    VALUE SPACES.                  
           05  WS-L941-OLD-ETH      PIC X(01).                                  
           05  FILLER               PIC X(10)    VALUE SPACES.                  
           05  WS-L941-NEW-ETH      PIC X(01).                                  
           05  FILLER               PIC X(12)    VALUE SPACES.                  
           05  WS-L941-OLD-CTZ      PIC X(02).                                  
           05  FILLER               PIC X(10)    VALUE SPACES.                  
           05  WS-L941-NEW-CTZ      PIC X(02).                                  
           05  WS-L941-REL.                                                     
              10  FILLER               PIC X(10)    VALUE SPACES.               
              10  WS-L941-OLD-REL      PIC X(02).                               
              10  FILLER               PIC X(08)    VALUE SPACES.               
              10  WS-L941-NEW-REL      PIC X(02).                               
              10  FILLER               PIC X(04)    VALUE SPACES.               
           05 WS-REMARKS               PIC X(32).                               
                                                                                
      ***************************************************************           
      * CONVERT RDB CODES TO RDB II CODES                           *           
      ***************************************************************           
      *COPY RDBTRDB2.                                                           
      **************************************************************            
      * ETHNIC GROUP DECODING TABLE (RDB CONVENTION)               *            
      **************************************************************            
       01  ETH-GP.                                                              
           05 FILLER               PIC X(4)  VALUE '0C C'.                      
           05 FILLER               PIC X(4)  VALUE '5M M'.                      
           05 FILLER               PIC X(4)  VALUE '6I I'.                      
           05 FILLER               PIC X(4)  VALUE '7O O'.                      
                                                                                
       01  ETH-GP-TABLE REDEFINES ETH-GP.                                       
           05 ETH-TAB OCCURS  4 TIMES INDEXED BY ETH.                           
              10 ETH-CD            PIC X.                                       
              10 ETH-DES           PIC XX.                                      
              10 ETH-CDB-CD        PIC X.                                       
                                                                                
      **************************************************************            
      * CITIZENSHIP DECODING TABLE.                                *            
      **************************************************************            
       01  CTZ-SHIP.                                                            
           05 FILLER               PIC X(7)  VALUE 'NIUNUNA'.                   
           05 FILLER               PIC X(7)  VALUE '10SCSCC'.                   
           05 FILLER               PIC X(7)  VALUE '20SPSPP'.                   
           05 FILLER               PIC X(7)  VALUE '99UNUNA'.                   
                                                                                
       01  CTZ-TABLE REDEFINES CTZ-SHIP.                                        
           05  CTZ-TAB OCCURS 4 TIMES INDEXED BY CTZ.                           
              10 CTZ-CD            PIC XX.                                      
              10 CTZ-DES           PIC XX.                                      
              10 CTZ-CDB-CD        PIC XX.                                      
              10 CTZ-PR-CD         PIC X.                                       
                                                                                
      **************************************************************            
      * RELATIONSHIP CONVERSION TABLE                              *            
      **************************************************************            
       01  RTN-SHIP.                                                            
           05 FILLER               PIC X(4)  VALUE '0000'.                      
           05 FILLER               PIC X(4)  VALUE '0102'.                      
           05 FILLER               PIC X(4)  VALUE '0201'.                      
           05 FILLER               PIC X(4)  VALUE '0304'.                      
           05 FILLER               PIC X(4)  VALUE '0414'.                      
           05 FILLER               PIC X(4)  VALUE '0505'.                      
           05 FILLER               PIC X(4)  VALUE '0603'.                      
           05 FILLER               PIC X(4)  VALUE '0713'.                      
           05 FILLER               PIC X(4)  VALUE '0806'.                      
           05 FILLER               PIC X(4)  VALUE '0910'.                      
           05 FILLER               PIC X(4)  VALUE '1030'.                      
           05 FILLER               PIC X(4)  VALUE '1102'.                      
           05 FILLER               PIC X(4)  VALUE '1201'.                      
           05 FILLER               PIC X(4)  VALUE '1304'.                      
           05 FILLER               PIC X(4)  VALUE '1414'.                      
           05 FILLER               PIC X(4)  VALUE '1505'.                      
           05 FILLER               PIC X(4)  VALUE '1911'.                      
           05 FILLER               PIC X(4)  VALUE '2020'.                      
           05 FILLER               PIC X(4)  VALUE '2122'.                      
           05 FILLER               PIC X(4)  VALUE '2221'.                      
           05 FILLER               PIC X(4)  VALUE '2314'.                      
           05 FILLER               PIC X(4)  VALUE '2414'.                      
           05 FILLER               PIC X(4)  VALUE '2505'.                      
           05 FILLER               PIC X(4)  VALUE '2613'.                      
           05 FILLER               PIC X(4)  VALUE '2713'.                      
           05 FILLER               PIC X(4)  VALUE '2806'.                      
           05 FILLER               PIC X(4)  VALUE '3122'.                      
           05 FILLER               PIC X(4)  VALUE '3221'.                      
           05 FILLER               PIC X(4)  VALUE '3314'.                      
           05 FILLER               PIC X(4)  VALUE '4008'.                      
           05 FILLER               PIC X(4)  VALUE '4140'.                      
           05 FILLER               PIC X(4)  VALUE '4250'.                      
           05 FILLER               PIC X(4)  VALUE '4342'.                      
           05 FILLER               PIC X(4)  VALUE '4452'.                      
           05 FILLER               PIC X(4)  VALUE '4507'.                      
           05 FILLER               PIC X(4)  VALUE '4607'.                      
           05 FILLER               PIC X(4)  VALUE '4708'.                      
           05 FILLER               PIC X(4)  VALUE '4808'.                      
           05 FILLER               PIC X(4)  VALUE '4908'.                      
           05 FILLER               PIC X(4)  VALUE '5009'.                      
           05 FILLER               PIC X(4)  VALUE '5109'.                      
           05 FILLER               PIC X(4)  VALUE '5209'.                      
           05 FILLER               PIC X(4)  VALUE '5309'.                      
           05 FILLER               PIC X(4)  VALUE '5509'.                      
           05 FILLER               PIC X(4)  VALUE '5609'.                      
           05 FILLER               PIC X(4)  VALUE '5709'.                      
           05 FILLER               PIC X(4)  VALUE '5809'.                      
           05 FILLER               PIC X(4)  VALUE '5909'.                      
           05 FILLER               PIC X(4)  VALUE '6109'.                      
           05 FILLER               PIC X(4)  VALUE '6209'.                      
           05 FILLER               PIC X(4)  VALUE '7009'.                      
           05 FILLER               PIC X(4)  VALUE '7109'.                      
           05 FILLER               PIC X(4)  VALUE '7609'.                      
           05 FILLER               PIC X(4)  VALUE '7809'.                      
           05 FILLER               PIC X(4)  VALUE '8209'.                      
           05 FILLER               PIC X(4)  VALUE '9109'.                      
           05 FILLER               PIC X(4)  VALUE '9209'.                      
           05 FILLER               PIC X(4)  VALUE '9309'.                      
           05 FILLER               PIC X(4)  VALUE '9609'.                      
           05 FILLER               PIC X(4)  VALUE '9709'.                      
                                                                                
       01  RTN-TABLE REDEFINES RTN-SHIP.                                        
           05  RTN-TAB OCCURS 60 TIMES INDEXED BY RTN.                          
              10 RDB-RTN-CD        PIC XX.                                      
              10 RDB2-RTN-CD       PIC XX.                                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP18F300  THRU 2000-EXIT.                          
           PERFORM 5000-READ-BP18F310  THRU 5000-EXIT.                          
           MOVE    F310-NUM-REGN  TO WS-REGN.                                   
           PERFORM 3000-PROCESS-RECORD THRU 3000-EXIT                           
             UNTIL WS-F300-EOF = 'Y'.                                           
           PERFORM 9999-CLOSE-FILES    THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-OPEN-FILES.                                                         
      *--------------------------------*                                        
                                                                                
           OPEN   INPUT  BP18F300                                               
                         BP18F310                                               
                         BP13KD07                                               
                         BP13KD05                                               
                  I-O    BP13K940                                               
                         BP13K943                                               
                  OUTPUT BP13L941.                                              
                                                                                
                                                                                
           IF WS-K940-STATUS NOT = ZEROES AND 97                                
             DISPLAY 'OPEN FAILED FOR BP13K940 ' WS-K940-STATUS                 
             MOVE WS-K940-STATUS        TO   RETURN-CODE                        
             PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                         
                                                                                
           IF WS-K943-STATUS NOT = ZEROES AND 97                                
             DISPLAY 'OPEN FAILED FOR BP13K943 ' WS-K943-STATUS                 
             MOVE WS-K943-STATUS        TO   RETURN-CODE                        
             PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                         
                                                                                
           IF WS-KD05-STATUS NOT = ZEROES AND 97                                
             DISPLAY 'OPEN FAILED FOR BP13KD05 ' WS-KD05-STATUS                 
             MOVE WS-KD05-STATUS        TO   RETURN-CODE                        
             PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                         
                                                                                
           IF WS-KD07-STATUS NOT = ZEROES AND 97                                
             DISPLAY 'OPEN FAILED FOR BP13KD07 ' WS-KD07-STATUS                 
             MOVE WS-KD07-STATUS        TO   RETURN-CODE                        
             PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                         
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATETIME.                  
           MOVE WS-SYS-DATE(1:4)        TO WS-DISP-CCYY.                        
           MOVE WS-SYS-DATE(5:2)        TO WS-DISP-MM.                          
           MOVE WS-SYS-DATE(7:2)        TO WS-DISP-DD.                          
           MOVE WS-DISP-DATE            TO WS-L941-CUR-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-READ-BP18F300.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP18F300 AT END                                                 
             MOVE 'Y'                  TO WS-F300-EOF                           
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1                       TO WS-F300-READ.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-PROCESS-RECORD.                                                     
      *--------------------------------*                                        
                                                                                
           PERFORM 6000-PROCESS-RECORD  THRU 6000-EXIT                          
                   UNTIL (WS-REGN NOT = F310-NUM-REGN) OR                       
                         (WS-F310-EOF = 'Y')                                    
           MOVE    F310-NUM-REGN  TO WS-REGN.                                   
           PERFORM 4000-WRITE-BP13K940  THRU 4000-EXIT                          
           PERFORM 2000-READ-BP18F300   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4000-WRITE-BP13K940.                                                     
      *--------------------------------*                                        
                                                                                
           MOVE SPACES                  TO BP13K940-REC.                        
           INITIALIZE BP13K940-REC.                                             
                                                                                
           MOVE F300-DTE-FILE-SENT      TO K940-DTE-FILE-SENT.                  
      *    MOVE F300-DTE-CPF-GRANT-RQST TO K940-DTE-CPF-GRANT-REQ.              
      *    MOVE F300-NUM-CPF-GRANT      TO K940-NUM-CPF-GRANT.                  
      *    MOVE F300-DTE-CPF-GRANT-PAID TO K940-DTE-CPF-GRANT-PAID.             
           MOVE SPACES                  TO BP13KD07-REC.                        
           MOVE F310-NUM-REGN           TO KD07-NUM-REGN                        
           PERFORM 6300-READ-BP13KD07 THRU 6300-EXIT.                           
           MOVE KD07-DTE-HSGNT-CPFREQ   TO K940-DTE-CPF-GRANT-REQ.              
           IF KD07-DTE-HSGNT-APPROVE NOT = SPACES AND LOW-VALUES                
                                                  AND ZEROES                    
              MOVE 'Y'                   TO K940-NUM-CPF-GRANT                  
           ELSE                                                                 
              MOVE SPACES                TO K940-NUM-CPF-GRANT                  
           END-IF.                                                              
           MOVE KD07-DTE-HSGNT-APPROVE  TO K940-DTE-CPF-GRANT-PAID.             
                                                                                
           MOVE F300-DTE-INDVDL-TOP     TO K940-DTE-IND-TOP.                    
           MOVE F300-DTE-LEGAL-TOP      TO K940-DTE-LEGAL-TOP.                  
           MOVE F300-NUM-REF            TO K940-NUM-REF.                        
           MOVE F300-NUM-NRIC1          TO K940-NUM-NRIC1.                      
           MOVE F300-NUM-NRIC2          TO K940-NUM-NRIC2.                      
           MOVE F300-NUM-NRIC3          TO K940-NUM-NRIC3.                      
           MOVE F300-NUM-NRIC4          TO K940-NUM-NRIC4.                      
           MOVE F300-NUM-APLCTN-TYPE    TO K940-NUM-APPLN.                      
           MOVE F300-NUM-APLCTN-OFFER   TO K940-NUM-OFFER.                      
           MOVE F300-NUM-APLCTN-REFER   TO K940-NUM-REFER.                      
           MOVE F300-DTE-HDB-CNSNT      TO K940-DTE-HDB-CONSENT.                
           MOVE F300-AMT-TRNSCT-SELL-PRICE TO K940-AMT-SELLING-PRICE.           
           MOVE F300-AMT-PREM-PAID      TO K940-AMT-PREMIUM.                    
           MOVE F300-DTE-OPTION         TO K940-DTE-OPTION.                     
           MOVE F300-NUM-HSHLD          TO K940-CDE-HOUSEHOLD.                  
           MOVE F300-NUM-RESALE-LEVY    TO K940-CDE-RESALE-LEVY.                
           MOVE F300-NUM-REGN           TO K940-NUM-REGN.                       
           MOVE F300-NUM-CURR-REGN      TO K940-NUM-CUR-REGN.                   
           MOVE F300-DTE-CANCEL         TO K940-DTE-CANCEL.                     
           MOVE F300-DTE-PREM-PAID      TO K940-DTE-PREMIUM-PAID.               
           MOVE F300-NUM-GRO-RANK       TO K940-NUM-GRO-RANK.                   
           MOVE F300-NUM-LESSEE-CAT     TO K940-NUM-LESSEE-CAT.                 
           MOVE F300-NUM-PRJCT-APPLY    TO K940-CDE-NT1.                        
           MOVE F300-NUM-PRJCT-SCSFL    TO K940-CDE-SUCC-NT-ZONE.               
                                                                                
           IF F300-NUM-REFER-QUEUE = SPACES OR LOW-VALUES                       
              MOVE F300-NUM-SHRLST-QUEUE-ALPHA                                  
                TO K940-NUM-HHTY-QUEUE-ALPHA                                    
              MOVE F300-NUM-SHRLST-QUEUE-SERIAL                                 
                TO K940-NUM-HHTY-QUEUE-SERIAL                                   
           ELSE                                                                 
              MOVE F300-NUM-REFER-QUEUE-ALPHA                                   
                TO K940-NUM-HHTY-QUEUE-ALPHA                                    
              MOVE F300-NUM-REFER-QUEUE-SERIAL                                  
                TO K940-NUM-HHTY-QUEUE-SERIAL                                   
           END-IF.                                                              
                                                                                
           MOVE F300-NUM-5YR-OCPTN      TO K940-NUM-5YR-OCCUPN.                 
           MOVE F300-NUM-PPO            TO K940-NUM-PPO.                        
           MOVE F300-DTE-REINSTATED     TO K940-DTE-REINSTATE.                  
           MOVE F300-DTE-SALE-PRCHS     TO K940-DTE-SALE-PURCHASE.              
           MOVE F300-NUM-ALCTN-SCHEME   TO K940-CDE-ALLOC-SCH.                  
           MOVE F300-DTE-FRESH-SALE-PRCHS TO                                    
                                          K940-DTE-FRESH-SALE-PURCHASE.         
           MOVE F300-AMT-HSHLD-INCOME   TO K940-AMT-HSE-INCOME.                 
           MOVE F300-DTE-REGN           TO K940-DTE-REGN.                       
           MOVE F300-NUM-ELGBTY-SCHEME  TO K940-CDE-ELIG-SCH.                   
           MOVE F300-NUM-ELGBTY-TOP     TO K940-NUM-ELGBTY-TOP.                 
           MOVE F300-NUM-ELGBTY         TO K940-CDE-ELIG-TAG.                   
           MOVE F300-DTE-FRESH-OPTION   TO K940-DTE-FRESH-OPTION.               
           MOVE F300-NUM-USERID         TO K940-NUM-USER-ID.                    
           MOVE F300-DTE-UPDATE         TO K940-DTE-UPDATE.                     
           MOVE F300-NUM-SUB-TRNSFR     TO K940-NUM-SUB-TRANSFER.               
           MOVE F300-NUM-SALE-WITHIN-MOP TO K940-NUM-SALE-WITHIN-MOP.           
                                                                                
           WRITE BP13K940-REC.                                                  
           EVALUATE WS-K940-STATUS                                              
           WHEN 00                                                              
             ADD 1                     TO WS-K940-WRITE                         
           WHEN 02                                                              
           WHEN 22                                                              
             DISPLAY 'RECORD ALREADY EXIST IN BP13K940!! : '                    
               K940-KEY-NEW                                                     
             ADD 1 TO WS-K940-EXIST                                             
           WHEN OTHER                                                           
             DISPLAY 'BP13K940 ERROR IN WRITING!' WS-K940-STATUS                
             MOVE WS-K940-STATUS       TO RETURN-CODE                           
             PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5000-READ-BP18F310.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP18F310 AT END                                                 
             MOVE 'Y'                  TO WS-F310-EOF                           
             GO TO 5000-EXIT.                                                   
                                                                                
           ADD 1                       TO WS-F310-READ.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       6000-PROCESS-RECORD.                                                     
      *--------------------------------*                                        
                                                                                
           IF WS-REGN  = F300-NUM-REGN                                          
              PERFORM 6100-WRITE-K943   THRU 6100-EXIT                          
           ELSE                                                                 
              DISPLAY 'F310 REGN NOT FOUND IN F300: ' WS-REGN                   
           END-IF.                                                              
           PERFORM 5000-READ-BP18F310   THRU 5000-EXIT.                         
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       6100-WRITE-K943.                                                         
      *--------------------------------*                                        
                                                                                
           MOVE SPACES                  TO BP13K943-REC.                        
           INITIALIZE BP13K943-REC.                                             
                                                                                
           MOVE BP18F310-REC (38:97)    TO BP13K943-REC (19:97).                
           IF F310-NUM-NRIC = F300-NUM-NRIC1                                    
              MOVE 'OWN' TO K943-NUM-OCPR-ROLE                                  
           ELSE                                                                 
              IF F310-NUM-NRIC = F300-NUM-NRIC2                                 
                 MOVE 'CO ' TO K943-NUM-OCPR-ROLE                               
              ELSE                                                              
                 IF F310-NUM-NRIC = F300-NUM-NRIC3                              
                    MOVE 'CO ' TO K943-NUM-OCPR-ROLE                            
                 ELSE                                                           
                    IF F310-NUM-NRIC = F300-NUM-NRIC4                           
                       MOVE 'CO ' TO K943-NUM-OCPR-ROLE                         
                    ELSE                                                        
                       MOVE 'OC ' TO K943-NUM-OCPR-ROLE                         
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
           MOVE F310-NUM-REGN           TO K943-NUM-REGN.                       
           MOVE F310-NUM-NRIC           TO K943-NUM-NRIC.                       
           MOVE F310-DTE-UPDATE         TO K943-DTE-UPDATE.                     
           MOVE F310-NUM-5YR-OCPTN      TO K943-NUM-5YR-OCCUPN.                 
           MOVE F310-NUM-LESSEE-CHECK   TO K943-NUM-LESSEE-CHECK.               
                                                                                
      *    MOVE F310-AMT-CPF-GRANT-PYBL TO K943-AMT-CPF-GRANT-PAYABLE.          
      *    MOVE F310-AMT-CPF-GRANT-PAID TO K943-AMT-CPF-GRANT-PAID.             
           MOVE SPACES                  TO BP13KD05-REC.                        
           MOVE F310-NUM-REGN           TO KD05-NUM-REGN                        
           MOVE F310-NUM-NRIC           TO KD05-NUM-NRIC                        
           PERFORM 6200-READ-BP13KD05 THRU 6200-EXIT.                           
           MOVE KD05-AMT-HSGNT          TO K943-AMT-CPF-GRANT-PAYABLE.          
           MOVE KD05-AMT-HSGNT          TO K943-AMT-CPF-GRANT-PAID.             
                                                                                
           MOVE F310-AMT-REGN-DEP-OWING TO K943-AMT-REGN-DEP-OWING.             
           MOVE F310-NUM-CTZNSP         TO K943-NUM-CITIZENSHIP.                
           MOVE F310-NUM-RLTNSP         TO K943-NUM-RELATIONSHIP.               
           MOVE F310-NUM-ETHNIC         TO K943-NUM-ETHNIC.                     
           MOVE 'N' TO WS-UPD-CTZ-FLAG.                                         
           PERFORM 7000-CONVERT-CTZSHP THRU 7000-EXIT.                          
           PERFORM 7100-CHECK-VALID-CTZSHP THRU 7100-EXIT.                      
           MOVE 'N' TO WS-UPD-ETH-FLAG.                                         
           PERFORM 8000-CONVERT-ETHNIC THRU 8000-EXIT.                          
           PERFORM 8100-CHECK-VALID-ETHNIC THRU 8100-EXIT.                      
           MOVE 'N' TO WS-UPD-REL-FLAG.                                         
           PERFORM 9000-CONVERT-RLTNSP THRU 9000-EXIT.                          
           PERFORM 9100-CHECK-VALID-REL THRU 9100-EXIT.                         
           WRITE BP13K943-REC.                                                  
                                                                                
           EVALUATE WS-K943-STATUS                                              
           WHEN 00                                                              
             ADD  1                    TO WS-K943-WRITE                         
           WHEN 02                                                              
           WHEN 22                                                              
              DISPLAY 'RECORD ALREADY EXIST!! : ' F310-KEY-FLD                  
      *       MOVE WS-K943-STATUS       TO RETURN-CODE                          
              ADD 1 TO WS-K943-EXIST                                            
           WHEN OTHER                                                           
              DISPLAY 'BP13K943 ERROR IN WRITING!' WS-K943-STATUS               
              MOVE WS-K943-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                          
           END-EVALUATE.                                                        
           EVALUATE TRUE                                                        
              WHEN WS-UPD-CTZ-FLAG = 'Y' AND WS-UPD-ETH-FLAG = 'Y'              
               AND WS-UPD-REL-FLAG = 'Y'                                        
                   MOVE 'ETHNIC, CTZ & RELSHP UPDATED    '                      
                        TO WS-REMARKS                                           
              WHEN WS-UPD-CTZ-FLAG = 'Y' AND WS-UPD-ETH-FLAG = 'Y'              
                   MOVE 'ETHNIC & CTZNSHIP CODE UPDATED' TO WS-REMARKS          
                                                                                
              WHEN WS-UPD-CTZ-FLAG = 'Y' AND WS-UPD-REL-FLAG = 'Y'              
                   MOVE 'CTZNSHIP & RELNSHIP CODE UPDATED'                      
                        TO WS-REMARKS                                           
                                                                                
              WHEN WS-UPD-ETH-FLAG = 'Y' AND WS-UPD-REL-FLAG = 'Y'              
                   MOVE 'ETHNIC & RELNSHIP CODE UPDATED  '                      
                        TO WS-REMARKS                                           
                                                                                
              WHEN WS-UPD-ETH-FLAG = 'Y'                                        
                   MOVE 'ETHNIC CODE UPDATED             '                      
                        TO WS-REMARKS                                           
                                                                                
              WHEN WS-UPD-CTZ-FLAG = 'Y'                                        
                   MOVE 'CITIZENSHIP CODE UPDATED        '                      
                        TO WS-REMARKS                                           
                                                                                
              WHEN WS-UPD-REL-FLAG = 'Y'                                        
                   MOVE 'RELATIONSHIP CODE UPDATED       '                      
                        TO WS-REMARKS                                           
           END-EVALUATE.                                                        
                                                                                
           IF WS-UPD-CTZ-FLAG = 'Y' OR WS-UPD-ETH-FLAG = 'Y'                    
              OR WS-UPD-REL-FLAG = 'Y'                                          
              MOVE F310-KEY-FLD TO WS-L941-KEY-FLD                              
              PERFORM 9200-PRT-REPORT THRU 9200-EXIT.                           
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       6200-READ-BP13KD05.                                                      
      *----------------------*                                                  
           READ  BP13KD05.                                                      
                                                                                
           EVALUATE WS-KD05-STATUS                                              
           WHEN 00                                                              
             CONTINUE                                                           
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND    !! : ' KD05-KEY-FLD                  
              MOVE SPACES       TO BP13KD05-REC                                 
              INITIALIZE            BP13KD05-REC                                
           WHEN OTHER                                                           
              DISPLAY 'BP13KD05 ERROR IN READING!' WS-KD05-STATUS               
              MOVE WS-KD05-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       6300-READ-BP13KD07.                                                      
      *----------------------*                                                  
           READ  BP13KD07.                                                      
                                                                                
           EVALUATE WS-KD07-STATUS                                              
           WHEN 00                                                              
             CONTINUE                                                           
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND    !! : ' KD07-KEY-FLD                  
              MOVE SPACES       TO BP13KD07-REC                                 
              INITIALIZE           BP13KD07-REC                                 
           WHEN OTHER                                                           
              DISPLAY 'BP13KD07 ERROR IN READING!' WS-KD07-STATUS               
              MOVE WS-KD07-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       7000-CONVERT-CTZSHP.                                                     
      *----------------------*                                                  
           MOVE F310-NUM-CTZNSP               TO   WS-NUM-CTZNSP,               
                                                   WS-L941-OLD-CTZ,             
                                                   WS-L941-NEW-CTZ,             
                                                   WS-VALID-CTZ-CODES.          
           SET  CTZ                           TO   1.                           
                                                                                
           SEARCH CTZ-TAB                                                       
           AT END                                                               
                IF F310-NUM-CTZNSP NOT = 'SC' AND 'SP' AND 'UN' AND 'AL'        
                   MOVE 'AL' TO K943-NUM-CITIZENSHIP, WS-L941-NEW-CTZ           
                   MOVE 'Y'  TO WS-UPD-CTZ-FLAG                                 
                END-IF                                                          
           WHEN CTZ-CD (CTZ) = WS-NUM-CTZNSP                                    
              IF CTZ-CDB-CD (CTZ) NOT = WS-L941-OLD-CTZ                         
                MOVE CTZ-CDB-CD (CTZ)      TO   K943-NUM-CITIZENSHIP            
                                            WS-L941-NEW-CTZ                     
                MOVE 'Y' TO WS-UPD-CTZ-FLAG                                     
              END-IF                                                            
           END-SEARCH.                                                          
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       7100-CHECK-VALID-CTZSHP.                                                 
           IF WS-UPD-CTZ-FLAG = 'Y'                                             
              ADD 1 TO WS-K943-CTZ-CTR.                                         
                                                                                
       7100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------*                                                  
       8000-CONVERT-ETHNIC.                                                     
      *----------------------*                                                  
           MOVE F310-NUM-ETHNIC               TO   WS-NUM-ETHNIC,               
                                              WS-L941-OLD-ETH,                  
                                              WS-L941-NEW-ETH,                  
                                              WS-VALID-ETHNIC-CODES.            
                                                                                
           SET  ETH                           TO   1.                           
                                                                                
           SEARCH ETH-TAB                                                       
           AT END                                                               
              CONTINUE                                                          
           WHEN   ETH-CD (ETH) = WS-NUM-ETHNIC                                  
              IF  ETH-CDB-CD (ETH) NOT = WS-L941-OLD-ETH                        
                  MOVE ETH-CDB-CD (ETH)       TO   K943-NUM-ETHNIC              
                                              WS-L941-NEW-ETH                   
                  MOVE 'Y' TO WS-UPD-ETH-FLAG                                   
              END-IF                                                            
           END-SEARCH.                                                          
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       8100-CHECK-VALID-ETHNIC.                                                 
           IF WS-UPD-ETH-FLAG NOT = 'Y'                                         
              IF NOT WS-VALID-ETHNIC                                            
                 DISPLAY 'ETHNIC CODE NOT FOUND ' WS-NUM-ETHNIC                 
              END-IF                                                            
           ELSE                                                                 
              ADD 1 TO WS-K943-ETH-CTR.                                         
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------*                                                  
       9000-CONVERT-RLTNSP.                                                     
      *----------------------*                                                  
           MOVE F310-NUM-RLTNSP               TO   WS-NUM-RLTNSP,               
                                              WS-L941-OLD-REL,                  
                                              WS-L941-NEW-REL,                  
                                              WS-VALID-REL-CODES.               
           SET  RTN                           TO   1.                           
                                                                                
           SEARCH RTN-TAB                                                       
           AT END                                                               
              CONTINUE                                                          
           WHEN   RDB-RTN-CD (RTN) = WS-NUM-RLTNSP                              
              IF  RDB2-RTN-CD (RTN) NOT = WS-L941-OLD-REL                       
                  MOVE RDB2-RTN-CD (RTN)      TO   K943-NUM-RELATIONSHIP        
                                              WS-L941-NEW-REL                   
                  MOVE 'Y' TO WS-UPD-REL-FLAG                                   
              END-IF                                                            
           END-SEARCH.                                                          
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9100-CHECK-VALID-REL.                                                    
           IF WS-UPD-REL-FLAG NOT = 'Y'                                         
              IF NOT WS-VALID-REL                                               
                 DISPLAY 'RELATION CODE NOT FOUND ' WS-NUM-RLTNSP               
              END-IF                                                            
           ELSE                                                                 
              ADD 1 TO WS-K943-REL-CTR.                                         
       9100-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9200-PRT-REPORT.                                                         
           IF WS-LINE-CTR > WS-MAX-LINE-CTR                                     
              ADD 1 TO WS-PAGE-CTR                                              
              MOVE WS-PAGE-CTR TO WS-L941-PAGENO                                
              WRITE BP13L941-REC FROM WS-L941-TITLE1 AFTER PAGE                 
              WRITE BP13L941-REC FROM WS-L941-TITLE2 AFTER 1                    
              WRITE BP13L941-REC FROM WS-L941-LINER1 AFTER 2                    
              WRITE BP13L941-REC FROM WS-L941-HEADER1 AFTER 1                   
              WRITE BP13L941-REC FROM WS-L941-LINER1 AFTER 1                    
              MOVE 5 TO WS-LINE-CTR                                             
           END-IF.                                                              
           WRITE BP13L941-REC FROM WS-L941-DETAIL1 AFTER 1.                     
           ADD 1 TO WS-LINE-CTR.                                                
                                                                                
       9200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9999-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP18C941'.                                   
           DISPLAY 'CURRENT-DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS READ FROM BP18F300:' WS-F300-READ.            
           DISPLAY 'NO OF RECORDS READ FROM BP18F310:' WS-F310-READ.            
           DISPLAY 'NO OF RECS WRITTEN INTO BP13K940:' WS-K940-WRITE.           
           DISPLAY 'NO OF RECS WRITTEN INTO BP13K943:' WS-K943-WRITE.           
           DISPLAY 'NO OF RECS EXISTING IN  BP13K940:' WS-K940-EXIST.           
           DISPLAY 'NO OF RECS EXISTING IN  BP13K943:' WS-K943-EXIST.           
           DISPLAY 'NO OF RECS W/ CTIZENSHIP CNVRTED:' WS-K943-CTZ-CTR.         
           DISPLAY 'NO OF RECS W/ ETHNIC CDE CNVRTED:' WS-K943-ETH-CTR.         
           DISPLAY 'NO OF RECS W/ RELATNSHIP CNVRTED:' WS-K943-REL-CTR.         
                                                                                
           CLOSE BP18F300                                                       
                 BP18F310                                                       
                 BP13K940                                                       
                 BP13KD05                                                       
                 BP13KD07                                                       
                 BP13K943                                                       
                 BP13L941.                                                      
                                                                                
                                                                                
           IF WS-K940-STATUS NOT = ZEROES                                       
             DISPLAY 'CLOSE FAILED BP13K940 ' WS-K940-STATUS                    
             MOVE WS-K940-STATUS TO RETURN-CODE                                 
           END-IF.                                                              
                                                                                
           IF WS-K943-STATUS NOT = ZEROES                                       
             DISPLAY 'CLOSE FAILED BP13K943 ' WS-K943-STATUS                    
             MOVE WS-K943-STATUS TO RETURN-CODE                                 
           END-IF.                                                              
                                                                                
           IF WS-KD05-STATUS NOT = ZEROES                                       
             DISPLAY 'CLOSE FAILED BP13KD05 ' WS-KD05-STATUS                    
             MOVE WS-KD05-STATUS TO RETURN-CODE                                 
           END-IF.                                                              
                                                                                
           IF WS-KD07-STATUS NOT = ZEROES                                       
             DISPLAY 'CLOSE FAILED BP13KD07 ' WS-KD07-STATUS                    
             MOVE WS-KD07-STATUS TO RETURN-CODE                                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
