      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY30.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  16/06/08.                                                 
      *****************************************************************         
      * OBJECTIVE -                                                             
      *                                                                         
      *  TO EXTRACT BOOKED CASES USING BP13K205 CONTROL REC FOR                 
      *  REPORTING.                                                             
      *                                                                         
      *  MATCH BP13K205 AND BP13F800                                            
      *  BY DTE-BALLOT , ALLO-CAT , AND SCH-ACC NOT = SPACES                    
      *                                                                         
      *  K205-NUM-FORM-TYPE : 'B' FOR BOOKING PROFILE                           
      *                       'N' FOR NOT BOOK PROFILE                          
      *     INPUT FILE :                                                        
      *                  1. BP13F205                                            
      *                  2. BP13F800                                            
      *                  3. BP13K816                                            
      *                                                                         
      *     OUTPUT FILE:                                                        
      *                  1. BP13F730                                            
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS                        
      * -------   --------  ---  ------------------------                       
      * BP133340  14042008  JB8  NEW PROGRAM.                                   
      * BP133434  21112008  JB8  CATER FOR NOT BOOK CASES                       
      * BP133490  03122008  JB8  CHECK NT                                       
      * BP133614  13052009  PCL3 ADD ALLOC-TAG & BALLOT-HOUSEHOLD               
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F205  ASSIGN        TO BP13F205.                         
           SELECT  BP13F800  ASSIGN        TO BP13F800.                         
           SELECT  BP13F730  ASSIGN        TO BP13F730.                         
           SELECT  BP13K816  ASSIGN        TO BP13K816                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K816-NUM-ZONE                     
                             FILE STATUS   IS WS-K816-STATUS.                   
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F205                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 80 CHARACTERS            
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F800                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 2000 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F730                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K816                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K816.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  WORK-FILE-VARIABLE.                                                  
           05  WS-EOF-F800             PIC X     VALUE 'N'.                     
           05  WS-K816-STATUS          PIC 99    VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-F730-WRITE           PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F800-NT              PIC X(03) VALUE SPACES.                  
           05  WS-NT-EQ                PIC X(01) VALUE 'N'.                     
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F205      THRU 2000-EXIT.                      
           PERFORM 2100-READ-BP13F800      THRU 2100-EXIT                       
                                                                                
           PERFORM 3000-PROCESS-BP13F800   THRU 3000-EXIT                       
             UNTIL WS-EOF-F800 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13F800                                                 
                       BP13K816                                                 
                OUTPUT BP13F730.                                                
                                                                                
           IF WS-K816-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K816 - ERROR OPENING : ' WS-K816-STATUS              
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205           AT END                                       
                GO                 TO 9999-CLOSE-ROUTINE.                       
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2100-READ-BP13F800.                                                      
      ******************************************************************        
                                                                                
           READ BP13F800           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F800                             
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F800-READ.                           
                                                                                
       2100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-BP13F800.                                                   
      ******************************************************************        
                                                                                
           IF F800-DTE-BALLOT      = F205-DTE-BALLOT       AND                  
              F800-NUM-ALLO-CAT    = F205-NUM-SELECTION                         
                                                                                
              IF (F205-NUM-FORM-TYPE = SPACES) OR                               
                 (F205-NUM-FORM-TYPE = 'B' AND                                  
                  F800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES) OR              
                 (F205-NUM-FORM-TYPE = 'N' AND                                  
                  F800-NUM-SCH-ACC = SPACES OR LOW-VALUES)                      
                 IF F205-NUM-NT-ZONE NOT = SPACES AND LOW-VALUES                
                    PERFORM 5000-CHECK-NT           THRU 5000-EXIT              
                    IF WS-NT-EQ = 'Y'                                           
                       PERFORM 4000-WRITE-BP13F730  THRU 4000-EXIT              
                    END-IF                                                      
                 ELSE                                                           
                    PERFORM 4000-WRITE-BP13F730     THRU 4000-EXIT              
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF F800-DTE-BALLOT > F205-DTE-BALLOT                              
                 MOVE 'Y'                TO WS-EOF-F800                         
                 GO TO 3000-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-BP13F800  THRU 2100-EXIT.                          
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      *********************                                                     
       4000-WRITE-BP13F730.                                                     
      *********************                                                     
                                                                                
           MOVE SPACES                   TO BP13F730-REC.                       
           INITIALIZE                       BP13F730-REC.                       
                                                                                
           MOVE F800-NUM-REGN            TO F730-NUM-REGN.                      
           MOVE F800-NUM-FLAT-TYPE       TO F730-CDE-FLAT-TYPE.                 
           MOVE F800-NUM-APPLN-TYPE      TO F730-NUM-APPLN-TYPE.                
           IF F800-NUM-NEW-TOWN = SPACES OR LOW-VALUES                          
              MOVE F800-NUM-ZONE         TO F730-CDE-NT1                        
           ELSE                                                                 
              MOVE F800-NUM-NEW-TOWN     TO F730-CDE-NT1                        
           END-IF.                                                              
           MOVE F800-NUM-ALLO-SCHEME     TO F730-CDE-ALLOC-SCH.                 
           MOVE F800-NUM-ELIG-SCHEME     TO F730-CDE-ELIG-SCH.                  
           MOVE F800-NUM-CAT             TO F730-CDE-CATEGORY.                  
           MOVE F800-NUM-HOUSEHOLD       TO F730-CDE-HOUSEHOLD.                 
           MOVE F800-NUM-ALLO-CAT        TO F730-NUM-ALLO-CAT.                  
           MOVE F800-DTE-REGN            TO F730-DTE-REQUEST.                   
           MOVE F800-DTE-BALLOT          TO F730-DTE-BALLOT.                    
           MOVE F800-NUM-NRIC1           TO F730-NUM-NRIC1.                     
           MOVE F800-NUM-NRIC2           TO F730-NUM-NRIC2.                     
           MOVE F800-NUM-NRIC3           TO F730-NUM-NRIC3.                     
           MOVE F800-NUM-NRIC4           TO F730-NUM-NRIC4.                     
           MOVE F800-NUM-ZONE            TO F730-CDE-ZONE.                      
           MOVE F800-NUM-SMS-STATUS      TO F730-NUM-SMS-STATUS.                
           MOVE F800-NUM-SMS-FT          TO F730-NUM-SMS-FT.                    
           MOVE F800-NUM-RSL-STATUS      TO F730-NUM-RSL-STATUS.                
           MOVE F800-NUM-RSL-FT          TO F730-NUM-RSL-FT.                    
           MOVE F800-NUM-RDB-STATUS      TO F730-NUM-RDB-STATUS.                
           MOVE F800-NUM-RDB-FT          TO F730-NUM-RDB-FT.                    
           MOVE F800-NUM-MORT-LOAN-POLICY TO F730-NUM-MORT-LOAN-POLICY.         
                                                                                
      ****************                                                          
      *    BP133614  *                                                          
      ****************                                                          
           MOVE F800-NUM-NT-FT-QUEUE      TO F730-NUM-NT-FT-QUEUE.              
                                                                                
           IF (F800-NUM-SCH-ACC NOT = SPACES) OR                                
              (F800-NUM-ALLOC-TAG = 'AI' OR 'AG' OR 'RI' OR 'RG')               
              MOVE 'BF'                   TO F730-NUM-BOOK-STATUS               
           ELSE                                                                 
              MOVE F800-NUM-ALLOC-TAG     TO F730-NUM-BOOK-STATUS               
           END-IF.                                                              
                                                                                
           IF F800-NUM-ALLOC-TAG = 'NE'                                         
              MOVE 'C'                    TO F730-CDE-REQUEST-STATUS            
           END-IF.                                                              
                                                                                
           MOVE F800-CDE-BALLOT-HOUSEHOLD TO F730-CDE-BALLOT-HOUSEHOLD.         
           MOVE F800-DTE-BK-APPT          TO F730-DTE-BK-APPT.                  
           MOVE 'BP13F800'                TO F730-NUM-USER-ID.                  
                                                                                
           WRITE BP13F730-REC.                                                  
           ADD 1                         TO WS-F730-WRITE.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-CHECK-NT.                                                           
      ******************************************************************        
                                                                                
           IF F800-NUM-NEW-TOWN NOT = SPACES AND LOW-VALUES                     
              IF F800-NUM-NEW-TOWN NOT NUMERIC                                  
                 MOVE F800-NUM-NEW-TOWN          TO WS-F800-NT                  
              ELSE                                                              
                 MOVE F800-NUM-NEW-TOWN          TO K816-NUM-ZONE               
                 PERFORM 6000-READ-BP13K816    THRU 6000-EXIT                   
              END-IF                                                            
           ELSE                                                                 
              MOVE F800-NUM-ZONE                 TO K816-NUM-ZONE               
              PERFORM 6000-READ-BP13K816       THRU 6000-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-F800-NT = F205-NUM-NT-ZONE                                     
              MOVE 'Y'                           TO WS-NT-EQ                    
           ELSE                                                                 
              MOVE 'N'                           TO WS-NT-EQ                    
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-READ-BP13K816.                                                      
      ******************************************************************        
                                                                                
           READ BP13K816.                                                       
                                                                                
           IF WS-K816-STATUS = 00                                               
              MOVE K816-CDE-NT              TO WS-F800-NT                       
           ELSE                                                                 
              IF WS-K816-STATUS = 23                                            
                 MOVE K816-NUM-ZONE         TO WS-F800-NT                       
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K816 , STATUS = '                      
                          WS-K816-STATUS                                        
                 DISPLAY '  KEY : ' K816-NUM-ZONE                               
                 GO TO 9999-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CY30'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F800 RECORDS READ..........: '                    
                    WS-F800-READ.                                               
           DISPLAY 'NO OF BP13F730 RECORDS WRITTEN.......: '                    
                    WS-F730-WRITE.                                              
                                                                                
           CLOSE BP13F205                                                       
                 BP13F800                                                       
                 BP13K816                                                       
                 BP13F730.                                                      
                                                                                
           IF WS-K816-STATUS NOT = 0                                            
              DISPLAY 'BP13K816 - ERROR CLOSING : ' WS-K816-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
