       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C27F.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  26 JUL 2017.                                              
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE   :  PROGRAM TO UPDATE K230-NUM-RP FOR ROF SALES *            
      *                LAUNCH.                                     *            
      *                                                            *            
      *  INPUT FILES :  1)  BP13F230  -  FLATLIST FILE(RT 01,02,03)*            
      *                 2)  BM06K110  -  FLAT INVENTORY  FILE      *            
      *                                                            *            
      *  OUTPUT FILE :  1)  BP13K230  -  FLATLIST FILE(RT 01,02,03)*            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHGE NO  OIC   DATE       DESCRIPTION                      *            
      * -------- ----  ------     -----------                      *            
      * BP136835 FNP1  26/07/2017 NEW PROGRAM                      *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F230  ASSIGN        TO  BP13F230.                         
                                                                                
           SELECT BM06K110  ASSIGN        TO  BM06K110                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K110-KEY-FLD                      
                            FILE STATUS   IS  K110-STATUS.                      
                                                                                
           SELECT BP13K230  ASSIGN        TO  BP13K230                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K230-KEY-FLD                      
                            FILE STATUS   IS  K230-STATUS.                      
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K230                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K230.                                                           
                                                                                
       FD   BP13F230                                                            
            RECORDING MODE  IS  F                                               
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS                                      
            LABEL  RECORDS  ARE STANDARD.                                       
       COPY BP13F230.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-F230                PIC X       VALUE 'N'.                
           05  K110-STATUS                PIC 9(2)    VALUE  00.                
           05  K230-STATUS                PIC 9(2)    VALUE  00.                
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-F230-READ               PIC 9(5)    VALUE ZEROS.              
           05  WS-K230-UPDATED            PIC 9(5)    VALUE ZEROS.              
           05  WS-K110-FND                PIC 9(5)    VALUE ZEROS.              
                                                                                
       01  WS-NUM-RP                      PIC X(01)   VALUE SPACES.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-FILES        THRU   100-EXIT.                      
           PERFORM  200-READ-BP13F230     THRU   200-EXIT.                      
           PERFORM  300-PROCESS-RECORD    THRU   300-EXIT                       
                    UNTIL WS-EOF-F230 = 'Y'.                                    
           PERFORM  9999-CLOSE-ROUTINE     THRU   9999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F230                                                 
                       BM06K110                                                 
                I-O    BP13K230.                                                
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BM06K110 - OPEN ERROR, K110-STATUS ' K110-STATUS         
              MOVE     K110-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF K230-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K230 - OPEN ERROR, K230-STATUS ' K230-STATUS         
              MOVE     K230-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F230.                                                       
      *-------------------------------------------------------------            
           READ BP13F230                                                        
                AT END MOVE 'Y'         TO     WS-EOF-F230                      
                       GO TO 200-EXIT.                                          
                                                                                
           ADD     1                    TO     WS-F230-READ.                    
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES             TO K230-KEY-FLD.                             
           MOVE F230-NUM-SCH-ACC   TO K230-KEY-FLD.                             
                                                                                
           PERFORM 350-READ-BP13K230  THRU 350-EXIT.                            
                                                                                
           PERFORM 200-READ-BP13F230  THRU 200-EXIT.                            
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-READ-BP13K230.                                                       
      *-----------------------------------------------------------              
                                                                                
           READ BP13K230.                                                       
           EVALUATE K230-STATUS                                                 
           WHEN 00                                                              
              PERFORM 500-UPDATE-BP13K230 THRU 500-EXIT                         
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND BP13K230 : ' K230-KEY-FLD               
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K230 : ' K230-STATUS                   
              MOVE K230-STATUS            TO  RETURN-CODE                       
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-READ-BM06K110.                                                       
      *-----------------------------------------------------------              
                                                                                
           MOVE SPACES             TO K110-KEY-FLD.                             
           MOVE F230-NUM-SCH-ACC   TO K110-KEY-FLD.                             
                                                                                
           READ BM06K110.                                                       
           EVALUATE K110-STATUS                                                 
           WHEN 00                                                              
              PERFORM 450-GET-RP-CODE     THRU 450-EXIT                         
              ADD 1 TO WS-K110-FND                                              
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND BM06K110 : ' K110-KEY-FLD               
              MOVE SPACES                 TO WS-NUM-RP                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K110 : ' K110-STATUS                   
              MOVE K110-STATUS            TO  RETURN-CODE                       
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-GET-RP-CODE.                                                         
      *-------------------------------------------------------------            
           MOVE SPACES              TO WS-NUM-RP.                               
                                                                                
           IF K110-NUM-SELECTION = '3R '                                        
              MOVE 'N'         TO WS-NUM-RP                                     
           ELSE                                                                 
              IF K110-FLAT-STATUS = 'RP' OR                                     
                 (K110-FLAT-STATUS = 'ST' AND                                   
                 (K110-REPOSSESSED-CODE = '40' OR '20'))                        
                 MOVE 'Y'      TO WS-NUM-RP                                     
              ELSE                                                              
                 IF K110-FLAT-STATUS = 'CV'                                     
                    MOVE 'V'      TO WS-NUM-RP                                  
                 ELSE                                                           
                    MOVE 'N'      TO WS-NUM-RP                                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-UPDATE-BP13K230.                                                     
      *-------------------------------------------------------------            
                                                                                
           PERFORM 400-READ-BM06K110  THRU 400-EXIT.                            
                                                                                
           MOVE WS-NUM-RP           TO K230-NUM-RP.                             
                                                                                
           REWRITE K230-DETAIL-REC.                                             
           EVALUATE K230-STATUS                                                 
           WHEN 00                                                              
              ADD 1 TO WS-K230-UPDATED                                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR UPDATING BP13K230 : ' K230-STATUS                  
              MOVE K230-STATUS            TO  RETURN-CODE                       
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C27F CONTROL LIST ----------*'.            
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BP13F230 READ        = ' WS-F230-READ.                
           DISPLAY 'NO OF BM06K110 FOUND       = ' WS-K110-FND.                 
           DISPLAY 'NO OF BP13K230 UPDATED     = ' WS-K230-UPDATED.             
                                                                                
           CLOSE BP13F230                                                       
                 BM06K110                                                       
                 BP13K230.                                                      
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BM06K110 CLOSE ERROR, K110-STATUS ' K110-STATUS          
              MOVE     K110-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF K230-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K230 CLOSE ERROR, K230-STATUS ' K230-STATUS          
              MOVE     K230-STATUS      TO       RETURN-CODE.                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
