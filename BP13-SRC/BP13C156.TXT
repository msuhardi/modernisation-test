      *---------------------------------------------------------------*         
       IDENTIFICATION DIVISION.                                                 
      *---------------------------------------------------------------*         
       PROGRAM-ID. BP13C156.                                                    
      *AUTHOR.     SK.                                                          
      *DATE-WRITTEN. 20/11/2003.                                                
      *---------------------------------------------------------------*         
      *---------------------------------------------------------------*         
      *             SYSTEM  OF COMMITMENT (BP13)                      *         
      *---------------------------------------------------------------*         
      *  OBJECTIVES : 1. TO DELETE FROM SOC MASTER,OCCUPIER,ADDRESS,  *         
      *                  APPL HISTORY FILE & FIS HISTORY FILE         *         
      *---------------------------------------------------------------*         
      * FILES USED :                                                  *         
      *                                                               *         
      *      INPUT : 1. BP13F800                                      *         
      *                                                               *         
      *      I-O   : 1. BP13K800                                      *         
      *              2. BP13K820                                      *         
      *              3. BP13K830                                      *         
      *              4. BP13K840                                      *         
      *              5. BM06K950                                      *         
      *                                                               *         
      *     OUTPUT : 1. BP13L156                                      *         
      *---------------------------------------------------------------*         
      * CHG NO#  DATE      BY   REMARKS                               *         
      * -------- --------  ---- --------------------------------------*         
      * BP132387 20112003  SK   NEW PGM                               *         
      * BP132627 16112004  GAN1 CHG F800/F820 TO K893/K895            *         
      * BP132693 25052005  CG7  REMOVE WRITE TO K893 AND K895         *         
      *---------------------------------------------------------------*         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *---------------------------------------------------------------*         
                                                                                
      *---------------------------------------------------------------*         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *---------------------------------------------------------------*         
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS WS-K820-STATUS.                            
                                                                                
           SELECT  BP13K830 ASSIGN TO BP13K830                                  
                   ACCESS MODE     IS DYNAMIC                                   
                   ORGANIZATION    IS INDEXED                                   
                   RECORD KEY      IS K830-KEY-FLD                              
                   FILE STATUS     IS WS-K830-STATUS.                           
                                                                                
           SELECT  BP13K840 ASSIGN TO BP13K840                                  
                   ACCESS MODE     IS DYNAMIC                                   
                   ORGANIZATION    IS INDEXED                                   
                   RECORD KEY      IS K840-KEY-FLD                              
                   FILE STATUS     IS WS-K840-STATUS.                           
                                                                                
           SELECT  BM06K950 ASSIGN TO BM06K950                                  
                   ACCESS MODE     IS DYNAMIC                                   
                   ORGANIZATION    IS INDEXED                                   
                   RECORD KEY      IS K950-KEY-FLD                              
                   FILE STATUS     IS WS-K950-STATUS.                           
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
                                                                                
           SELECT BP13L156 ASSIGN TO BP13L156.                                  
                                                                                
      *---------------------------------------------------------------*         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *---------------------------------------------------------------*         
                                                                                
       FD BP13K800                                                              
            RECORD CONTAINS 2000 CHARACTERS.                                    
                                                                                
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
                                                                                
       COPY BP13K820.                                                           
                                                                                
       FD BP13K830                                                              
            RECORD CONTAINS 250 CHARACTERS.                                     
                                                                                
       COPY BP13K830.                                                           
                                                                                
       FD BP13K840                                                              
            RECORD CONTAINS 500 CHARACTERS.                                     
                                                                                
       COPY BP13K840.                                                           
                                                                                
       FD   BM06K950                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
                                                                                
       COPY BM06K950.                                                           
                                                                                
       FD BP13F800                                                              
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F800.                                                           
                                                                                
       FD BP13L156                                                              
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01 BP13L156-REC    PIC X(132).                                           
                                                                                
      *---------------------------------------------------------------*         
       WORKING-STORAGE SECTION.                                                 
      *---------------------------------------------------------------*         
                                                                                
       01 WORK-FILE-VARIABLE.                                                   
          05 WS-F800-EOF              PIC X(1)    VALUE 'N'.                    
          05 WS-READ-F800             PIC 9(8)    VALUE 0.                      
                                                                                
          05 WS-DEL-K800              PIC 9(8)    VALUE 0.                      
          05 WS-DEL-K820              PIC 9(8)    VALUE 0.                      
          05 WS-DEL-K830              PIC 9(8)    VALUE 0.                      
          05 WS-DEL-K840              PIC 9(8)    VALUE 0.                      
          05 WS-DEL-K950              PIC 9(8)    VALUE 0.                      
                                                                                
          05 WS-K800-STATUS           PIC 9(2)    VALUE 0.                      
          05 WS-K820-STATUS           PIC 9(2)    VALUE 0.                      
          05 WS-K830-STATUS           PIC 9(2)    VALUE 0.                      
          05 WS-K840-STATUS           PIC 9(2)    VALUE 0.                      
          05 WS-K950-STATUS           PIC 9(2)    VALUE 0.                      
                                                                                
       01 WORK-PRINT-VARIABLE.                                                  
          05 WS-CNT-LINE-ON-PAGE      PIC 9(8)    VALUE 66.                     
          05 WS-CNT-PRINT-L156        PIC 9(8)    VALUE 1.                      
          05 WS-CNT-WRITE-L156        PIC 9(8)    VALUE 0.                      
          05 WS-NO-OF-PAGE            PIC 9(8)    VALUE 0.                      
                                                                                
       01 WORK-OTHER-VARIABLE.                                                  
          05 WS-K800-REMARK           PIC X(15)   VALUE SPACES.                 
          05 WS-K820-REMARK           PIC X(15)   VALUE SPACES.                 
          05 WS-K830-REMARK           PIC X(15)   VALUE SPACES.                 
          05 WS-K840-REMARK           PIC X(15)   VALUE SPACES.                 
          05 WS-K950-REMARK           PIC X(15)   VALUE SPACES.                 
          05 WS-REMARKS               PIC X(25)   VALUE SPACES.                 
                                                                                
          05 WS-CNT                   PIC 9(2)    VALUE 0.                      
          05 WS-FLAG                  PIC X(1)    VALUE SPACES.                 
                                                                                
          05 WS-EDITED-DATE           PIC X(10)   VALUE SPACES.                 
          05 WS-SOC-AMOUNT            PIC 9(8)    VALUE 0.                      
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4)    VALUE 0.                      
           05  WS-SYS-MM              PIC 9(2)    VALUE 0.                      
           05  WS-SYS-DD              PIC 9(2)    VALUE 0.                      
                                                                                
      *-----------------------------------------------------------*             
      *                 BP13L156 - LAYOUT                         *             
      *-----------------------------------------------------------*             
                                                                                
       01  L156-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L156'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L156-PRINT-DATE          PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L156-PRINT-PAGE          PIC ZZZ9.                               
                                                                                
       01  L156-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(45) VALUE SPACES.                 
           05  FILLER                   PIC X(75) VALUE                         
                'MONTHLY REPORT ON CASES DELETED FROM SOC MASTER'.              
                                                                                
       01  L156-PR-HEAD-03.                                                     
           05  FILLER                   PIC X(55) VALUE SPACES.                 
           05  FILLER                   PIC X(75) VALUE                         
                "CANCELLED UNDER 'C2'".                                         
                                                                                
       01  L156-PR-HEAD-04.                                                     
           05  FILLER           PIC X(05)  VALUE ' S-NO'.                       
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(10)  VALUE 'REGN NO'.                     
           05  FILLER           PIC X(09)  VALUE 'CAN CDE'.                     
           05  FILLER           PIC X(13)  VALUE 'K800 STATUS'.                 
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(13)  VALUE 'K820 STATUS'.                 
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(13)  VALUE 'K830 STATUS'.                 
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(13)  VALUE 'K840 STATUS'.                 
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(13)  VALUE 'K950 STATUS'.                 
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(10)  VALUE 'DTE CANCEL'.                  
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(25)  VALUE 'REMARKS'.                     
                                                                                
       01  L156-PR-HEAD-05.                                                     
           05  FILLER                PIC X(132) VALUE ALL '-'.                  
                                                                                
       01  L156-PR-DETAILS.                                                     
           05  L156-NUMBER           PIC ZZZZ9.                                 
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L156-REGN-NO          PIC X(10)  VALUE SPACES.                   
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L156-CNCL-CDE         PIC X(07)  VALUE SPACES.                   
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L156-K800-STAT        PIC X(13)  VALUE SPACES.                   
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L156-K820-STAT        PIC X(13)  VALUE SPACES.                   
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L156-K830-STAT        PIC X(13)  VALUE SPACES.                   
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L156-K840-STAT        PIC X(13)  VALUE SPACES.                   
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L156-K950-STAT        PIC X(13)  VALUE SPACES.                   
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L156-DTE-CANCEL       PIC X(10)  VALUE SPACES.                   
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L156-REMARKS          PIC X(25)  VALUE SPACES.                   
                                                                                
      *---------------------------------------------------------------*         
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN.                                                               
      *---------------------------------------------------------------*         
                                                                                
           PERFORM 1000-OPEN          THRU 1000-EXIT.                           
           PERFORM 2000-READ-F800     THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
              UNTIL WS-F800-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN.                                                               
      *---------------------------------------------------------------*         
                                                                                
           OPEN INPUT BP13F800                                                  
                  I-O BP13K800                                                  
                      BP13K820                                                  
                      BP13K830                                                  
                      BP13K840                                                  
                      BM06K950                                                  
               OUTPUT BP13L156.                                                 
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K800-STATUS TO RETURN-CODE                             
                 DISPLAY 'OPEN BP13K800 FAIL. STATUS=' WS-K800-STATUS           
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K820-STATUS TO RETURN-CODE                             
                 DISPLAY 'OPEN BP13K820 FAIL. STATUS=' WS-K820-STATUS           
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K830-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K830-STATUS TO RETURN-CODE                             
                 DISPLAY 'OPEN BP13K830 FAIL. STATUS=' WS-K830-STATUS           
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K840-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K840-STATUS TO RETURN-CODE                             
                 DISPLAY 'OPEN BP13K840 FAIL. STATUS=' WS-K840-STATUS           
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K950-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K950-STATUS TO RETURN-CODE                             
                 DISPLAY 'OPEN BM06K950 FAIL. STATUS=' WS-K950-STATUS           
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
                                                                                
           STRING  WS-SYSTEM-DATE(7:2) '/' WS-SYSTEM-DATE(5:2) '/'              
                   WS-SYSTEM-DATE(1:4) DELIMITED BY SIZE                        
                   INTO WS-EDITED-DATE                                          
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-F800.                                                          
      *---------------------------------------------------------------*         
                                                                                
           READ BP13F800                                                        
                AT END MOVE 'Y' TO WS-F800-EOF                                  
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F800.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS.                                                            
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES            TO WS-K800-REMARK                             
                                     WS-K820-REMARK                             
                                     WS-K830-REMARK                             
                                     WS-K840-REMARK                             
                                     WS-K950-REMARK                             
                                     WS-REMARKS.                                
                                                                                
           PERFORM 3100-CHECK-F800 THRU 3100-EXIT.                              
                                                                                
           IF WS-FLAG = 'Y'                                                     
              PERFORM 3500-READ-K800  THRU 3500-EXIT                            
           ELSE                                                                 
              MOVE 'OUTSTANDING AMOUNT IN SOC'                                  
                                      TO WS-REMARKS                             
           END-IF.                                                              
                                                                                
           PERFORM 4700-MOVE-DATA  THRU 4700-EXIT.                              
                                                                                
           PERFORM 2000-READ-F800  THRU 2000-EXIT.                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-CHECK-F800.                                                         
      *---------------------------------------------------------------*         
                                                                                
           MOVE 'Y' TO WS-FLAG.                                                 
                                                                                
           IF F800-AMT-BK-PAID IS NOT NUMERIC                                   
              MOVE 'N' TO WS-FLAG                                               
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F800-AMT-BK-PAID NOT = ZERO                                       
              MOVE 'N' TO WS-FLAG                                               
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
           COMPUTE WS-SOC-AMOUNT =   F800-AMT-SOC-CASH                          
                                   + F800-AMT-CPF1-PAID                         
                                   + F800-AMT-CPF2-PAID                         
                                   + F800-AMT-CPF3-PAID                         
                                   + F800-AMT-CPF4-PAID.                        
                                                                                
           IF WS-SOC-AMOUNT NOT = ZERO OR                                       
              F800-AMT-SOC-DEBTOR NOT = ZERO                                    
              MOVE 'N' TO WS-FLAG                                               
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F800-AMT-REGN-DEPOSIT-CASH > ZEROS OR                             
              F800-AMT-REGN-DEPOSIT-CPF1 > ZEROS OR                             
              F800-AMT-REGN-DEPOSIT-CPF2 > ZEROS OR                             
              F800-AMT-REGN-DEPOSIT-CPF3 > ZEROS OR                             
              F800-AMT-REGN-DEPOSIT-CPF4 > ZEROS                                
              MOVE 'N' TO WS-FLAG                                               
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F800-AMT-CD NOT = ZEROS                                           
              MOVE 'N' TO WS-FLAG                                               
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3500-READ-K800.                                                          
      *---------------------------------------------------------------*         
                                                                                
           MOVE LOW-VALUES TO K800-NUM-REGN.                                    
           MOVE F800-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                 PERFORM 4000-DELETE-K800 THRU 4000-EXIT                        
              WHEN 23                                                           
                 MOVE 'NOT FND'   TO WS-K800-REMARK                             
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K800, STATUS = '                   
                         WS-K800-STATUS                                         
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-DELETE-K800.                                                        
      *---------------------------------------------------------------*         
                                                                                
           DELETE BP13K800.                                                     
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-DEL-K800                                           
                 MOVE 'DELETED'   TO WS-K800-REMARK                             
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13K800, STATUS = '                  
                      WS-K800-STATUS                                            
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           PERFORM 4100-READ-K820  THRU 4100-EXIT.                              
           PERFORM 4300-READ-K830  THRU 4300-EXIT.                              
           PERFORM 4450-READ-K840  THRU 4450-EXIT.                              
                                                                                
           IF F800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              PERFORM 4500-READ-K950 THRU 4500-EXIT                             
           ELSE                                                                 
              MOVE 'NO SCH ACC'      TO WS-K950-REMARK                          
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-READ-K820.                                                          
      *---------------------------------------------------------------*         
                                                                                
           MOVE 0             TO WS-CNT.                                        
           MOVE LOW-VALUES    TO K820-KEY-FLD.                                  
           MOVE F800-NUM-REGN TO K820-NUM-REGN.                                 
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
              EVALUATE WS-K820-STATUS                                           
              WHEN 00                                                           
                 MOVE 'Y'      TO WS-FLAG                                       
                 PERFORM UNTIL WS-FLAG = 'N'                                    
                    READ BP13K820 NEXT RECORD                                   
                    EVALUATE WS-K820-STATUS                                     
                    WHEN 00                                                     
                      IF K820-NUM-REGN = F800-NUM-REGN                          
                         PERFORM 4200-DELETE-K820 THRU 4200-EXIT                
                      ELSE                                                      
                         MOVE 'N'         TO WS-FLAG                            
                      END-IF                                                    
                    WHEN 10                                                     
                    WHEN 23                                                     
                      MOVE 'N'         TO WS-FLAG                               
                    END-EVALUATE                                                
                 END-PERFORM                                                    
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K820. STATUS IS '                   
                          WS-K820-STATUS                                        
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           IF WS-CNT NOT = 0                                                    
              STRING 'DELETED (' WS-CNT ')'                                     
                     DELIMITED BY SIZE                                          
                     INTO WS-K820-REMARK                                        
              END-STRING                                                        
           ELSE                                                                 
              MOVE 'NOT FND'  TO WS-K820-REMARK                                 
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-DELETE-K820.                                                        
      *---------------------------------------------------------------*         
                                                                                
           DELETE BP13K820.                                                     
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-DEL-K820 WS-CNT                                    
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13K820, STATUS = '                  
                         WS-K820-STATUS ' REGN-NO ' F800-NUM-REGN               
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4300-READ-K830.                                                          
      *---------------------------------------------------------------*         
                                                                                
           MOVE LOW-VALUES    TO K830-KEY-FLD.                                  
           MOVE F800-NUM-REGN TO K830-KEY-FLD.                                  
                                                                                
           READ BP13K830.                                                       
                                                                                
           EVALUATE WS-K830-STATUS                                              
              WHEN 00                                                           
                 PERFORM 4400-DELETE-K830 THRU 4400-EXIT                        
              WHEN 23                                                           
                 MOVE 'NOT FND'   TO WS-K830-REMARK                             
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K830. STATUS IS '                   
                         WS-K830-STATUS                                         
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4400-DELETE-K830.                                                        
      *---------------------------------------------------------------*         
                                                                                
           DELETE BP13K830.                                                     
                                                                                
           EVALUATE WS-K830-STATUS                                              
              WHEN 00                                                           
                  ADD 1 TO WS-DEL-K830                                          
                  MOVE 'DELETED'   TO WS-K830-REMARK                            
               WHEN OTHER                                                       
                  DISPLAY 'ERRROR DELETING BP13K830, STATUS = '                 
                          WS-K830-STATUS                                        
                  PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4450-READ-K840.                                                          
      *---------------------------------------------------------------*         
                                                                                
           MOVE LOW-VALUES    TO K840-KEY-FLD.                                  
           MOVE F800-NUM-REGN TO K840-KEY-FLD.                                  
                                                                                
           READ BP13K840.                                                       
                                                                                
           EVALUATE WS-K840-STATUS                                              
              WHEN 00                                                           
                 PERFORM 4460-DELETE-K840 THRU 4460-EXIT                        
              WHEN 23                                                           
                 MOVE 'NOT FND'   TO WS-K840-REMARK                             
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K840, STATUS = '                   
                         WS-K840-STATUS                                         
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4460-DELETE-K840.                                                        
      *---------------------------------------------------------------*         
                                                                                
           DELETE BP13K840.                                                     
                                                                                
           EVALUATE WS-K840-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-DEL-K840                                           
                 MOVE 'DELETED'   TO WS-K840-REMARK                             
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13K840, STATUS = '                  
                      WS-K800-STATUS                                            
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4460-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4500-READ-K950.                                                          
      *---------------------------------------------------------------*         
                                                                                
           MOVE 0               TO WS-CNT.                                      
           MOVE LOW-VALUES      TO K950-KEY-FLD.                                
           MOVE F800-NUM-SCH-ACC TO K950-NUM-SCH-ACC.                           
                                                                                
           START BM06K950 KEY >= K950-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K950-STATUS                                              
              WHEN 00                                                           
                MOVE 'Y' TO WS-FLAG                                             
                PERFORM UNTIL WS-FLAG = 'N'                                     
                   READ BM06K950 NEXT RECORD                                    
                   EVALUATE WS-K950-STATUS                                      
                   WHEN 00                                                      
                     IF (F800-NUM-SCH-ACC = K950-NUM-SCH-ACC AND                
                         F800-NUM-REGN = K950-REGN-NO)                          
                        PERFORM 4600-DELETE-K950 THRU 4600-EXIT                 
                     ELSE                                                       
                        MOVE 'N' TO WS-FLAG                                     
                     END-IF                                                     
                   WHEN 10                                                      
                   WHEN 23                                                      
                     MOVE 'N'         TO WS-FLAG                                
                   END-EVALUATE                                                 
                END-PERFORM                                                     
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BM06K950. STATUS IS '                   
                         WS-K950-STATUS                                         
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
           IF WS-CNT NOT = 0                                                    
              STRING 'DELETED (' WS-CNT ')'                                     
                     DELIMITED BY SIZE                                          
                     INTO WS-K950-REMARK                                        
              END-STRING                                                        
           ELSE                                                                 
              MOVE 'NOT FND'  TO WS-K950-REMARK                                 
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4600-DELETE-K950.                                                        
      *---------------------------------------------------------------*         
                                                                                
           DELETE BM06K950.                                                     
                                                                                
           EVALUATE WS-K950-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-DEL-K950 WS-CNT                                    
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BM06K950, STATUS = '                  
                         WS-K950-STATUS                                         
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4700-MOVE-DATA.                                                          
      *---------------------------------------------------------------*         
                                                                                
           MOVE F800-NUM-REGN                TO L156-REGN-NO.                   
                                                                                
           STRING F800-DTE-CANCEL(7:2) '/'                                      
                  F800-DTE-CANCEL(5:2) '/'                                      
                  F800-DTE-CANCEL(1:4)                                          
                  DELIMITED BY SIZE                                             
                  INTO L156-DTE-CANCEL                                          
           END-STRING.                                                          
                                                                                
           MOVE F800-NUM-CANCEL-REASON       TO L156-CNCL-CDE.                  
           MOVE WS-K800-REMARK               TO L156-K800-STAT.                 
           MOVE WS-K820-REMARK               TO L156-K820-STAT.                 
           MOVE WS-K830-REMARK               TO L156-K830-STAT.                 
           MOVE WS-K840-REMARK               TO L156-K840-STAT.                 
           MOVE WS-K950-REMARK               TO L156-K950-STAT.                 
           MOVE WS-REMARKS                   TO L156-REMARKS.                   
                                                                                
           PERFORM 4800-WRITE-BP13L156       THRU 4800-EXIT.                    
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4800-WRITE-BP13L156.                                                     
      *---------------------------------------------------------------*         
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 53                                          
              PERFORM 4850-PRINT-HEADING THRU 4850-EXIT                         
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           MOVE WS-CNT-PRINT-L156  TO    L156-NUMBER.                           
                                                                                
           WRITE BP13L156-REC      FROM  L156-PR-DETAILS   AFTER 1.             
                                                                                
           ADD   1                 TO    WS-CNT-LINE-ON-PAGE                    
                                         WS-CNT-PRINT-L156                      
                                         WS-CNT-WRITE-L156.                     
                                                                                
           MOVE  SPACES            TO    L156-PR-DETAILS.                       
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4850-PRINT-HEADING.                                                      
      *---------------------------------------------------------------*         
                                                                                
           ADD  1                TO     WS-NO-OF-PAGE.                          
           MOVE WS-NO-OF-PAGE    TO     L156-PRINT-PAGE.                        
           MOVE WS-EDITED-DATE   TO     L156-PRINT-DATE.                        
                                                                                
           MOVE SPACES           TO     BP13L156-REC.                           
                                                                                
           WRITE BP13L156-REC    AFTER  PAGE.                                   
           WRITE BP13L156-REC    FROM   L156-PR-HEAD-01 AFTER 1.                
           WRITE BP13L156-REC    FROM   L156-PR-HEAD-02 AFTER 1.                
           WRITE BP13L156-REC    FROM   L156-PR-HEAD-03 AFTER 1.                
           WRITE BP13L156-REC    FROM   L156-PR-HEAD-04 AFTER 1.                
           WRITE BP13L156-REC    FROM   L156-PR-HEAD-05 AFTER 1.                
                                                                                
           MOVE 5  TO WS-CNT-LINE-ON-PAGE.                                      
                                                                                
       4850-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
                                                                                
           DISPLAY ' ******  BP13C156 *******'.                                 
           DISPLAY ' DATE  : ' WS-EDITED-DATE.                                  
           DISPLAY SPACES.                                                      
           DISPLAY '   BP13F800 READ      : ' WS-READ-F800.                     
           DISPLAY '   BP13K800 DELETE    : ' WS-DEL-K800.                      
           DISPLAY '   BP13K820 DELETE    : ' WS-DEL-K820.                      
           DISPLAY '   BP13K830 DELETE    : ' WS-DEL-K830.                      
           DISPLAY '   BP13K840 DELETE    : ' WS-DEL-K840.                      
           DISPLAY '   BM06K950 DELETE    : ' WS-DEL-K950.                      
           DISPLAY SPACES.                                                      
           DISPLAY ' ************************'.                                 
                                                                                
           CLOSE BP13F800                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13K840                                                       
                 BM06K950.                                                      
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K800-STATUS TO RETURN-CODE                             
                 DISPLAY 'CLOSE BP13K800 FAIL. STATUS=' WS-K800-STATUS          
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K820-STATUS TO RETURN-CODE                             
                 DISPLAY 'CLOSE BP13K820 FAIL. STATUS=' WS-K820-STATUS          
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K830-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K830-STATUS TO RETURN-CODE                             
                 DISPLAY 'CLOSE BP13K830 FAIL. STATUS=' WS-K830-STATUS          
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K840-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K840-STATUS TO RETURN-CODE                             
                 DISPLAY 'CLOSE BP13K840 FAIL. STATUS=' WS-K840-STATUS          
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K950-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K950-STATUS TO RETURN-CODE                             
                 DISPLAY 'CLOSE BM06K950 FAIL. STATUS=' WS-K950-STATUS          
           END-EVALUATE.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
