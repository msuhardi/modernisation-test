      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB03.                                                 
      *AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  16 SEPTEMBER 2005.                                        
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM VALIDATES TRANSACTIONS PASSED  *           
      *                 FROM PAYMENT SYSTEM TO PROCESS ADDITIONAL   *           
      *                 CPF HOUSING GRANT PAYMENTS.                 *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  AG07F630 -- VOUCHER TRANSACTION FILE                  *           
      *   2.  P13F633A -- SORTED SOC AHG VOUCHER OIC FILE           *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13K633 -- SOC AHG VOUCHER OIC FILE                  *           
      *   2.  BP13K800 -- SOC MASTER FILE                           *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13F125 -- EDITED FIRST TRANSACTION FILE             *           
      *   2.  P13F633B -- EDITED SOC AHG VOUCHER OIC FILE           *           
      *   3.  BP13LB03 -- CONTROL LISTING                           *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP132801 16/09/2005 SD10 NEW PROGRAM                        *           
      * BP132871 01/08/2006 SD10 REMOVE PRINTING OF REPORT FOR CASES*           
      *                          FOUND IN BOTH INPUT FILES          *           
      * BP134209 23/05/2011 JB8  TO CATER FOR SHG                   *           
      * BP134506 23/07/2012 AB9  TO CATER FOR NEW GL CODE           *           
      * BP135221 14/02/2014 CT2  TO CATER FOR NEW GL CODE FOR SUG   *           
      * BP135265 07/03/2014 PCL4 TO UPDATE AHG/SHG/SUG-GRANT PAYMENT*           
      * BP138015 15/10/2019 PP11 TO UPDATE EHG GRANT PAYMENT        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K633 ASSIGN TO BP13K633                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K633-KEY-FLD                               
                  FILE STATUS     IS BP13K633-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13KD07 ASSIGN TO BP13KD07                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD07-KEY-FLD                               
                  FILE STATUS     IS BP13KD07-STATUS.                           
                                                                                
           SELECT AG07F630   ASSIGN       TO AG07F630.                          
                                                                                
           SELECT P13F633A   ASSIGN       TO P13F633A.                          
                                                                                
           SELECT P13F633B   ASSIGN       TO P13F633B.                          
                                                                                
           SELECT BP13F125   ASSIGN       TO BP13F125.                          
                                                                                
           SELECT BP13LB03   ASSIGN       TO BP13LB03.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K633                                                            
            RECORD CONTAINS  450 CHARACTERS.                                    
       COPY BP13K633.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13KD07                                                            
            RECORD CONTAINS 700 CHARACTERS.                                     
       COPY BP13KD07.                                                           
                                                                                
       FD   AG07F630                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  130 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY AG07F630.                                                           
                                                                                
       FD   P13F633A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  450 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F633.                                                           
                                                                                
       FD   P13F633B                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  450 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01   WS-BP13F633-REC       PIC X(450).                           00055007
                                                                                
       FD   BP13F125                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F125.                                                           
                                                                                
       FD   BP13LB03                                                            
            RECORD CONTAINS   132 CHARACTERS                                    
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   PRINT-REC             PIC X(132).                                   
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-AMT-RECEIPT         PIC 9(06) VALUE ZEROES.                       
       01  WS-NRIC                PIC X(09) VALUE SPACES.                       
       01  WS-NUM-CPF-PAYEE       PIC X(09) VALUE SPACES.                       
       01  CNT-LINE-ON-PAGE       PIC 9(02) VALUE 66.                           
       01  CNT-F125-WRITE         PIC 9(05) VALUE ZEROES.                       
       01  CNT-F630-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-F633-WRITE         PIC 9(05) VALUE ZEROES.                       
       01  CNT-K633-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-K633-DELETE        PIC 9(05) VALUE ZEROES.                       
       01  CNT-K800-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-K800-UPDATE        PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD07-UPDATE        PIC 9(05) VALUE ZEROES.                       
       01  CNT-PRT-CASES          PIC 9(06) VALUE ZEROES.                       
       01  CNT-PAGE               PIC 9(03) VALUE ZEROES.                       
       01  CNT-F633-READ          PIC 9(08) VALUE ZEROES.                       
       01  WS-MATCH-REC           PIC 9(08) VALUE ZEROES.                       
       01  WS-AHG-FOUND           PIC X     VALUE SPACE.                        
       01  WS-SHG-FOUND           PIC X     VALUE SPACE.                        
       01  WS-SUG-FOUND           PIC X     VALUE SPACE.                        
       01  WS-F630-EOF            PIC X     VALUE SPACE.                        
       01  WS-F633-EOF            PIC X     VALUE SPACE.                        
       01  BP13K633-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K800-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KD07-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  WS-K633-REGN-FND       PIC X     VALUE SPACE.                        
       01  WS-DATE                PIC X(08) VALUE SPACES.                       
       01  WS-REMARK              PIC X(20) VALUE SPACES.                       
                                                                                
       01  WS-SCHEDULE-NO.                                                      
           05  WS-SCHEDULE-NUM    PIC X(08).                                    
           05  WS-DOC-NO          PIC X(06).                                    
                                                                                
       01  LB03-PR-HEAD-01.                                                     
           05  FILLER             PIC X(08) VALUE 'BP13LB03'.                   
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  FILLER             PIC X(08) VALUE 'HDBCAT 3'.                   
           05  FILLER             PIC X(24) VALUE SPACES.                       
           05  FILLER             PIC X(39) VALUE                               
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER             PIC X(17) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'DATE: '.                     
           05  LB03-DATE          PIC X(10).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'PAGE :'.                     
           05  LB03-PAGE          PIC ZZ9 .                                     
                                                                                
       01  LB03-PR-HEAD-02.                                                     
           05  FILLER             PIC X(40) VALUE SPACES.                       
           05  FILLER             PIC X(47) VALUE                               
               '  LIST OF TRANSACTIONS PASSED FROM VOUCHER FILE'.               
           05  FILLER             PIC X(33) VALUE SPACES.                       
                                                                                
       01  LB03-PR-HEAD-03.                                                     
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  FILLER             PIC X(51) VALUE                               
               'S/NO  VOUCHER NO   REGN NO   CPF NO          AMOUNT'.           
           05  FILLER             PIC X(04) VALUE SPACES.                       
           05  FILLER             PIC X(47) VALUE                               
               'RECEIPT NO      RELEASE DTE  PAYMT TYPE  REMARK'.               
                                                                                
       01  LB03-PR-DETAILS.                                                     
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LB03-SNO           PIC ZZZZZ9.                                   
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB03-VOUCHER-NO    PIC X(11).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB03-REG-NO        PIC X(08).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB03-CPF-NO        PIC X(09).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB03-AMT           PIC $$$$$$$9.99DB.                            
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB03-NUM-RECEIPT   PIC X(14).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB03-DTE-POST      PIC X(10).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  LB03-PAYMENT-TYPE  PIC X(09).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB03-REMARK        PIC X(20).                                    
                                                                                
       01  LB03-CONTROL-LINE.                                                   
           05  FILLER             PIC X(20) VALUE  SPACES.                      
           05  CNTL-ITEM          PIC X(30) VALUE  SPACES.                      
           05  CNTL-CNT           PIC ZZZZ9 .                                   
           05  FILLER             PIC X(58) VALUE  SPACES.                      
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F630    THRU 2000-EXIT.                            
           PERFORM 2100-READ-F633    THRU 2100-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F630-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  AG07F630                                                 
                       P13F633A                                                 
                I-O    BP13K633                                                 
                       BP13K800                                                 
                       BP13KD07                                                 
                OUTPUT BP13F125                                                 
                       P13F633B                                                 
                       BP13LB03.                                                
                                                                                
           IF BP13K633-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K633 : ' BP13K633-STATUS             
              MOVE BP13K633-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD07-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD07 : ' BP13KD07-STATUS             
              MOVE BP13KD07-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
              DELIMITED BY SIZE INTO LB03-DATE.                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F630.                                                          
      *---------------*                                                         
                                                                                
           READ AG07F630 AT END                                                 
                MOVE HIGH-VALUES TO AG07F630-REC                                
                MOVE 'Y' TO WS-F630-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-F630-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2100-READ-F633.                                                          
      *---------------*                                                         
                                                                                
           READ P13F633A AT END                                                 
                MOVE HIGH-VALUES TO BP13F633-REC                                
                MOVE 'Y' TO WS-F633-EOF                                         
                GO TO 2100-EXIT.                                                
           ADD 1 TO CNT-F633-READ.                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF F630-NUM-VR = F633-NUM-VR                                         
              ADD  1                         TO WS-MATCH-REC                    
              PERFORM 3100-PROCESS-REC     THRU 3100-EXIT                       
              PERFORM 2100-READ-F633       THRU 2100-EXIT                       
           ELSE                                                                 
              IF F630-NUM-VR > F633-NUM-VR                                      
                 PERFORM 2100-READ-F633    THRU 2100-EXIT                       
              ELSE                                                              
                 MOVE 'VOUCHER NO NOT FOUND' TO WS-REMARK                       
                 PERFORM 4000-PRINT-REPORT THRU 4000-EXIT                       
                 PERFORM 2000-READ-F630    THRU 2000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3100-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE SPACES                       TO K633-KEY-FLD                    
           MOVE F633-NUM-REGN                TO K633-NUM-REGN                   
           PERFORM 3200-READ-BP13K633      THRU 3200-EXIT                       
           MOVE SPACES                       TO WS-BP13F633-REC                 
           MOVE BP13K633-REC                 TO WS-BP13F633-REC                 
           WRITE WS-BP13F633-REC.                                               
           ADD 1                             TO CNT-F633-WRITE                  
                                                                                
           MOVE 'N'                          TO WS-AHG-FOUND                    
                                                WS-SHG-FOUND                    
                                                WS-SUG-FOUND.                   
                                                                                
           PERFORM 3150-OUTPUT-F125        THRU 3150-EXIT                       
             UNTIL F630-NUM-VR NOT = F633-NUM-VR                                
                OR WS-F630-EOF = 'Y'.                                           
                                                                                
           PERFORM 3500-UPDATE-BP13K800    THRU 3500-EXIT.                      
           PERFORM 3600-REWRITE-BP13K800   THRU 3600-EXIT.                      
                                                                                
           IF WS-AHG-FOUND = 'Y' OR                                             
              WS-SHG-FOUND = 'Y' OR                                             
              WS-SUG-FOUND = 'Y'                                                
              PERFORM 3650-UPDATE-BP13KD07 THRU 3650-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 3700-DELETE-BP13K633    THRU 3700-EXIT.                      
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3150-OUTPUT-F125.                                                        
      *-----------------*                                                       
                                                                                
           PERFORM 3300-MOVE-DATA-BP13F125        THRU 3300-EXIT.               
                                                                                
           PERFORM 2000-READ-F630                 THRU 2000-EXIT.               
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3200-READ-BP13K633.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13K633.                                                       
           IF BP13K633-STATUS = 0                                               
              MOVE 'Y'    TO WS-K633-REGN-FND                                   
           ELSE                                                                 
              IF BP13K633-STATUS = 23                                           
                 MOVE 'N' TO WS-K633-REGN-FND                                   
                 DISPLAY 'REGNO NOT FOUND IN BP13K633 : ' K633-NUM-REGN         
                 MOVE BP13K633-STATUS     TO RETURN-CODE                        
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K633 : ' BP13K633-STATUS          
                 MOVE BP13K633-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3300-MOVE-DATA-BP13F125.                                                 
      *------------------------*                                                
                                                                                
           MOVE SPACES       TO BP13F125-CASHTRAN.                              
                                                                                
           MOVE '60'         TO F125-CDE-TRANS-TYPE.                            
                                                                                
           MOVE F633-NUM-REGN      TO F125-NUM-REGN.                            
                                                                                
           IF F630-NUM-GL-ACCT = '8407600408785' OR '0407600403488'             
              MOVE 'AHG'                       TO F125-CDE-PAYMENT-TYPE         
              MOVE 'Y'                         TO WS-AHG-FOUND                  
                                                                                
              IF F630-NUM-CPF = K633-NUM-CPF-PAYEE(1) AND                       
                 F630-AMT-TRANS = K633-AMT-CPF-GRANT-PAYEE(1)                   
                 MOVE K633-NUM-CPF-PAYEE(1)    TO F125-CDE-CPF-APPL             
                 MOVE K633-NUM-UIN-PAYEE(1)    TO F125-NUM-NRIC                 
                 MOVE F630-AMT-TRANS           TO F125-AMT-RECEIPT              
              ELSE                                                              
                 IF F630-NUM-CPF = K633-NUM-CPF-PAYEE(2) AND                    
                    F630-AMT-TRANS = K633-AMT-CPF-GRANT-PAYEE(2)                
                    MOVE K633-NUM-CPF-PAYEE(2) TO F125-CDE-CPF-APPL             
                    MOVE K633-NUM-UIN-PAYEE(2) TO F125-NUM-NRIC                 
                    MOVE F630-AMT-TRANS        TO F125-AMT-RECEIPT              
                 ELSE                                                           
                    MOVE 'VOUCHER NOT MATCH'   TO WS-REMARK                     
                    PERFORM 4000-PRINT-REPORT  THRU 4000-EXIT                   
                    GO TO 3300-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF F630-NUM-GL-ACCT = '8407000408785' OR '0407000403488'          
                 MOVE 'SHG'        TO F125-CDE-PAYMENT-TYPE                     
                 MOVE 'Y'          TO WS-SHG-FOUND                              
                                                                                
                 IF F630-NUM-CPF = K633-NUM-CPF-SHG(1) AND                      
                    F630-AMT-TRANS = K633-AMT-CPF-GRANT-SHG(1)                  
                    MOVE K633-NUM-CPF-SHG(1)   TO F125-CDE-CPF-APPL             
                    MOVE K633-NUM-UIN-PAYEE(1) TO F125-NUM-NRIC                 
                    MOVE F630-AMT-TRANS        TO F125-AMT-RECEIPT              
                 ELSE                                                           
                    IF F630-NUM-CPF = K633-NUM-CPF-SHG(2) AND                   
                       F630-AMT-TRANS = K633-AMT-CPF-GRANT-SHG(2)               
                       MOVE K633-NUM-CPF-SHG(2)   TO F125-CDE-CPF-APPL          
                       MOVE K633-NUM-UIN-PAYEE(2) TO F125-NUM-NRIC              
                       MOVE F630-AMT-TRANS        TO F125-AMT-RECEIPT           
                    ELSE                                                        
                       MOVE 'VOUCHER NOT MATCH'   TO WS-REMARK                  
                       PERFORM 4000-PRINT-REPORT  THRU 4000-EXIT                
                       GO TO 3300-EXIT                                          
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
               IF F630-NUM-GL-ACCT = '0407100403488'                            
                 MOVE 'SUG'        TO F125-CDE-PAYMENT-TYPE                     
                 MOVE 'Y'          TO WS-SUG-FOUND                              
                                                                                
                 IF F630-NUM-CPF = K633-NUM-CPF-SUG(1) AND                      
                    F630-AMT-TRANS = K633-AMT-CPF-GRANT-SUG(1)                  
                    MOVE K633-NUM-CPF-SUG(1)   TO F125-CDE-CPF-APPL             
                    MOVE K633-NUM-UIN-SUG(1)   TO F125-NUM-NRIC                 
                    MOVE F630-AMT-TRANS        TO F125-AMT-RECEIPT              
                 ELSE                                                           
                    IF F630-NUM-CPF = K633-NUM-CPF-SUG(2) AND                   
                       F630-AMT-TRANS = K633-AMT-CPF-GRANT-SUG(2)               
                       MOVE K633-NUM-CPF-SUG(2)   TO F125-CDE-CPF-APPL          
                       MOVE K633-NUM-UIN-SUG(2)   TO F125-NUM-NRIC              
                       MOVE F630-AMT-TRANS        TO F125-AMT-RECEIPT           
                    ELSE                                                        
                       MOVE 'VOUCHER NOT MATCH'   TO WS-REMARK                  
                       PERFORM 4000-PRINT-REPORT  THRU 4000-EXIT                
                       GO TO 3300-EXIT                                          
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
               IF F630-NUM-GL-ACCT = '0407900403488'                            
                  MOVE 'EHG'                  TO F125-CDE-PAYMENT-TYPE          
                  MOVE 'Y'                    TO WS-AHG-FOUND                   
                                                                                
                IF F630-NUM-CPF = K633-NUM-CPF-PAYEE(1) AND                     
                   F630-AMT-TRANS = K633-AMT-CPF-GRANT-PAYEE(1)                 
                   MOVE K633-NUM-CPF-PAYEE(1)    TO F125-CDE-CPF-APPL           
                   MOVE K633-NUM-UIN-PAYEE(1)    TO F125-NUM-NRIC               
                   MOVE F630-AMT-TRANS           TO F125-AMT-RECEIPT            
                ELSE                                                            
                   IF F630-NUM-CPF = K633-NUM-CPF-PAYEE(2) AND                  
                      F630-AMT-TRANS = K633-AMT-CPF-GRANT-PAYEE(2)              
                      MOVE K633-NUM-CPF-PAYEE(2) TO F125-CDE-CPF-APPL           
                      MOVE K633-NUM-UIN-PAYEE(2) TO F125-NUM-NRIC               
                      MOVE F630-AMT-TRANS        TO F125-AMT-RECEIPT            
                   ELSE                                                         
                      MOVE 'VOUCHER NOT MATCH'   TO WS-REMARK                   
                      PERFORM 4000-PRINT-REPORT  THRU 4000-EXIT                 
                      GO TO 3300-EXIT                                           
                   END-IF                                                       
              END-IF                                                            
              END-IF                                                            
              END-IF                                                            
             END-IF                                                             
           END-IF.                                                              
           MOVE SPACES             TO F125-NME-NAME.                            
                                                                                
           MOVE F630-NUM-BATCH     TO WS-SCHEDULE-NUM.                          
           MOVE F630-NUM-VR-SERIAL TO WS-DOC-NO.                                
           MOVE WS-SCHEDULE-NO     TO F125-NUM-RECPT-JRNO.                      
           MOVE F630-NUM-VR-DDD    TO F125-NUM-VR-DDD.                          
           MOVE F630-NUM-VR-YY     TO F125-NUM-VR-YY.                           
           MOVE F630-DTE-TRANS     TO F125-DTE-TRANS.                           
           MOVE WS-DATE            TO F125-DTE-POST.                            
           MOVE SPACES             TO F125-CDE-APPLT-CPF.                       
           MOVE ZERO               TO F125-COUNT-HISTORY.                       
                                                                                
           PERFORM 3400-WRITE-BP13F125  THRU 3400-EXIT.                         
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3400-WRITE-BP13F125.                                                     
      *---------------------*                                                   
                                                                                
           WRITE BP13F125-CASHTRAN.                                             
           ADD 1       TO CNT-F125-WRITE.                                       
                                                                                
           MOVE SPACES TO WS-NRIC.                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3500-UPDATE-BP13K800.                                                    
      *---------------------*                                                   
                                                                                
           MOVE SPACES           TO BP13K800-MASTER.                            
           MOVE F633-NUM-REGN    TO K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
           IF BP13K800-STATUS = 0                                               
              ADD 1    TO CNT-K800-READ                                         
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3600-REWRITE-BP13K800.                                                   
      *----------------------*                                                  
                                                                                
           MOVE WS-DATE             TO K800-DTE-UPDATE                          
                                       K800-DTE-AHG-PAYMENT.                    
           MOVE 'BP13CB03'          TO K800-NUM-USERID.                         
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF BP13K800-STATUS = 0                                               
              ADD 1    TO CNT-K800-UPDATE                                       
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3650-UPDATE-BP13KD07.                                                    
      *---------------------*                                                   
                                                                                
           MOVE SPACES           TO BP13KD07-REC.                               
           MOVE F633-NUM-REGN    TO KD07-KEY-FLD.                               
                                                                                
           READ BP13KD07.                                                       
                                                                                
           IF BP13KD07-STATUS = 0                                               
              PERFORM  3660-REWRITE-BP13KD07       THRU 3660-EXIT               
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13KD07 : ' BP13KD07-STATUS             
           END-IF.                                                              
                                                                                
       3650-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3660-REWRITE-BP13KD07.                                                   
      *----------------------*                                                  
                                                                                
           IF WS-AHG-FOUND = 'Y'                                                
              MOVE WS-DATE       TO KD07-DTE-AHG-PAYMENT                        
           END-IF.                                                              
                                                                                
           IF WS-SHG-FOUND = 'Y'                                                
              MOVE WS-DATE       TO KD07-DTE-SHG-PAYMENT                        
           END-IF.                                                              
                                                                                
           IF WS-SUG-FOUND = 'Y'                                                
              MOVE WS-DATE       TO KD07-DTE-SUG-PAYMENT                        
           END-IF.                                                              
                                                                                
           REWRITE BP13KD07-REC.                                                
                                                                                
           IF BP13KD07-STATUS = 0                                               
              ADD 1    TO CNT-KD07-UPDATE                                       
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BP13KD07 : ' BP13KD07-STATUS            
           END-IF.                                                              
                                                                                
       3660-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3700-DELETE-BP13K633.                                                    
      *---------------------*                                                   
                                                                                
           DELETE BP13K633.                                                     
                                                                                
           EVALUATE BP13K633-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO CNT-K633-DELETE                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR DELETING BP13K633-REC ...'                   
                    MOVE BP13K633-STATUS TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-PRINT-REPORT.                                                       
      *------------------*                                                      
                                                                                
           IF CNT-LINE-ON-PAGE > 46                                             
              MOVE 1 TO CNT-LINE-ON-PAGE                                        
              PERFORM 4100-REPORT-HEADING THRU 4100-EXIT                        
           ELSE                                                                 
              ADD 1 TO CNT-LINE-ON-PAGE                                         
           END-IF.                                                              
                                                                                
           MOVE SPACES                   TO LB03-PR-DETAILS.                    
                                                                                
           ADD 1 TO CNT-PRT-CASES.                                              
           MOVE CNT-PRT-CASES            TO LB03-SNO.                           
                                                                                
           MOVE F630-NUM-REGN            TO LB03-REG-NO                         
           MOVE F630-NUM-VR              TO LB03-VOUCHER-NO                     
           MOVE F630-NUM-CPF             TO LB03-CPF-NO.                        
                                                                                
           IF F630-NUM-GL-ACCT = '8407600408785' OR '0407600403488'             
              MOVE 'AHG'              TO LB03-PAYMENT-TYPE                      
           ELSE                                                                 
              IF F630-NUM-GL-ACCT = '8407000408785' OR '0407000403488'          
                 MOVE 'SHG'           TO LB03-PAYMENT-TYPE                      
           ELSE                                                                 
              IF F630-NUM-GL-ACCT = '0407900403488'                             
                 MOVE 'EHG'           TO LB03-PAYMENT-TYPE                      
              END-IF                                                            
             END-IF                                                             
           END-IF.                                                              
           MOVE F630-DTE-TRANS        TO LB03-DTE-POST.                         
           MOVE F630-NUM-BATCH        TO WS-SCHEDULE-NUM.                       
           MOVE F630-NUM-VR-SERIAL    TO WS-DOC-NO.                             
           MOVE WS-SCHEDULE-NO        TO LB03-NUM-RECEIPT.                      
           MOVE F630-AMT-TRANS        TO LB03-AMT.                              
           MOVE WS-REMARK             TO LB03-REMARK                            
           WRITE PRINT-REC          FROM LB03-PR-DETAILS.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4100-REPORT-HEADING.                                                     
      *--------------------*                                                    
                                                                                
            ADD 1           TO CNT-PAGE.                                        
            MOVE CNT-PAGE   TO LB03-PAGE.                                       
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER PAGE.                                         
            WRITE PRINT-REC FROM LB03-PR-HEAD-01 AFTER 2.                       
            WRITE PRINT-REC FROM LB03-PR-HEAD-02 AFTER 3.                       
            WRITE PRINT-REC FROM LB03-PR-HEAD-03 AFTER 3.                       
            MOVE ALL '-'    TO PRINT-REC.                                       
            WRITE PRINT-REC.                                                    
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER 1.                                            
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB03 *************'.                            
           DISPLAY 'NO OF RECS MATCHED F630/F633    : ' WS-MATCH-REC.           
           DISPLAY 'NO OF RECS WRITTEN TO BP13F125  : ' CNT-F125-WRITE.         
           DISPLAY 'NO OF F633 WRITTEN TO BP13F633  : ' CNT-F633-WRITE.         
           DISPLAY 'NO OF RECS READ FROM  AG07F630  : ' CNT-F630-READ.          
           DISPLAY 'NO OF RECS READ FROM  BP13K633  : ' CNT-K633-READ.          
           DISPLAY 'NO OF RECS READ FROM  BP13K800  : ' CNT-K800-READ.          
           DISPLAY 'NO OF RECS READ FROM  BP13F633  : ' CNT-F633-READ.          
           DISPLAY 'NO OF RECS UPDATED IN BP13K800  : ' CNT-K800-UPDATE.        
           DISPLAY 'NO OF RECS UPDATED IN BP13KD07  : ' CNT-KD07-UPDATE.        
           DISPLAY 'NO OF RECS WRITTEN TO BP13LB03  : ' CNT-PRT-CASES.          
           DISPLAY 'NO OF RECS DELETED FROM BP13K633: ' CNT-K633-DELETE.        
                                                                                
           CLOSE BP13K633                                                       
                 BP13K800                                                       
                 BP13KD07                                                       
                 AG07F630                                                       
                 P13F633A                                                       
                 P13F633B                                                       
                 BP13F125                                                       
                 BP13LB03.                                                      
                                                                                
           IF BP13K633-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K633-STATUS ' BP13K633-STATUS          
            MOVE BP13K633-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KD07-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD07-STATUS ' BP13KD07-STATUS          
            MOVE BP13KD07-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
