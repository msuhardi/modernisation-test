       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB35.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  15/11/2016.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVES  : GENERATE REPORT HIGHLIGHTING APPLICATIONS   *            
      *                CONTAINING NRICS BELONGING TO DECEASED      *            
      *                PERSON(S)                                   *            
      *                                                            *            
      *  INPUT FILE  : BP13L506 - DECEASED CASES                   *            
      *                BP13K730 - REQUEST FILE                     *            
      *  OUTPUT FILE : BP13LB35 - REPORT GENERATED                 *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF# BY   DATE     DESCRIPTION                         *            
      * -------- ---- -------- ----------------------------------- *            
      * BP136505 SMR2 15112016 NEW PROGRAM                         *            
      * BP136762 SMR2 26052017 EXPAND BP13L506 FROM 20 TO 50 BYTES *            
      *                        / ADD PAGE BREAK ON SALES MODE AND  *            
      *                        BALLOT QTR                          *            
      * BP139682 AM25 12122023 NEW REPORT FILE IN CSV FORMAT       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13L506 ASSIGN       TO BP13L506.                            
                                                                                
           SELECT BP13K730 ASSIGN       TO BP13K730                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K730-NUM-REGN                        
                           FILE STATUS  IS WS-K730-STATUS.                      
                                                                                
           SELECT BP13LB35 ASSIGN       TO BP13LB35.                            
           SELECT P13LB35A ASSIGN       TO P13LB35A.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13L506                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS   50 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       01   BP13L506-REC.                                                       
            05  L506-NUM-REGN              PIC X(08).                           
            05  L506-NUM-DECEASED-NRIC     PIC X(09).                           
            05  L506-NUM-ALLO-CAT          PIC X(03).                           
            05  L506-DTE-BALLOT            PIC X(06).                           
            05  FILLER                     PIC X(24).                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K730.                                                           
                                                                                
       FD   BP13LB35                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  132 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORDS    ARE OMITTED.                                       
       01   BP13LB35-REC                   PIC X(132).                          
                                                                                
       FD   P13LB35A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORDS    ARE STANDARD.                                      
       01   P13LB35A-REC                   PIC X(500).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K730-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-L506-READ                PIC 9(05)   VALUE ZEROES.            
           05  WS-LB35-SNO                 PIC 9(05)   VALUE ZEROES.            
           05  WS-LB35-WRITE               PIC 9(05)   VALUE ZEROES.            
           05  WS-LB35A-WRITE              PIC 9(05)   VALUE ZEROES.            
           05  WS-L506-DUPL                PIC 9(05)   VALUE ZEROES.            
           05  WS-K730-FOUND               PIC 9(05)   VALUE ZEROES.            
           05  WS-K730-NTFND               PIC 9(05)   VALUE ZEROES.            
           05  WS-LB35-PAGE                PIC 9(05)   VALUE ZEROES.            
           05  WS-LB35-LINE                PIC 9(02)   VALUE 60.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-L506-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K730-FND                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CUR-DATE                 PIC X(08)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-DET-APPLN                PIC X(01)   VALUE SPACES.            
           05  WS-PREV-REGN                PIC X(08)   VALUE SPACES.            
           05  WS-L506-KEY-FLD.                                                 
               10  WS-L506-ALLO-CAT        PIC X(03)   VALUE SPACES.            
               10  WS-L506-DTE-BAL         PIC X(06)   VALUE SPACES.            
           05  WS-PREV-KEY-FLD             PIC X(09)   VALUE SPACES.            
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 LB35-HEADER1.                                                      
             10 LB35-RPTNAME      PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(06)  VALUE SPACES.                      
             10 FILLER            PIC X(04)  VALUE 'HDB3'.                      
             10 FILLER            PIC X(28)  VALUE SPACES.                      
             10 FILLER            PIC X(39)  VALUE                              
                'S Y S T E M   O F   C O M M I T M E N T'.                      
             10 FILLER            PIC X(13)  VALUE SPACES.                      
             10 FILLER            PIC X(07)  VALUE 'DATE : '.                   
             10 LB35-DATE         PIC X(10)  VALUE SPACES.                      
             10 FILLER            PIC X(04)  VALUE SPACES.                      
             10 FILLER            PIC X(07)  VALUE 'PAGE : '.                   
             10 LB35-PAGE-NO      PIC ZZZZ9 .                                   
          05 LB35-HEADER2.                                                      
             10 FILLER            PIC X(25)  VALUE SPACES.                      
             10 FILLER            PIC X(50)  VALUE                              
                'REPORT HIGHLIGHTING APPLICATIONS CONTAINING NRICS '.           
             10 FILLER            PIC X(31)  VALUE                              
                'BELONGING TO DECEASED PERSON(S)'.                              
          05 LB35-HEADER2A.                                                     
             10 FILLER            PIC X(14)  VALUE ' SALES MODE : '.            
             10 LB35-ALLO-CAT     PIC X(03)  VALUE SPACES.                      
          05 LB35-HEADER2B.                                                     
             10 FILLER            PIC X(14)  VALUE ' BALLOT QTR : '.            
             10 LB35-DTE-BAL      PIC X(06)  VALUE SPACES.                      
          05 LB35-HEADER3.                                                      
             10 FILLER            PIC X(43)  VALUE                              
                'S/NO.   REGN NO.   NT    FT   APPLN DATE   '.                  
             10 FILLER            PIC X(53)  VALUE                              
                'HA1 NRIC    HA2 NRIC    DECEASED NRIC   ROLE IN APPLN'.        
          05 LB35-BLANK.                                                        
             10 FILLER            PIC X(132) VALUE SPACES.                      
          05 LB35-DASH.                                                         
             10 FILLER            PIC X(132) VALUE ALL '-'.                     
          05 LB35-RECORD.                                                       
             10 LB35-SNO          PIC X(05)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE SPACES.                      
             10 LB35-REGN         PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE SPACES.                      
             10 LB35-NT           PIC X(03)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE SPACES.                      
             10 LB35-FT           PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE SPACES.                      
             10 LB35-DTE-APPLN    PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(05)  VALUE SPACES.                      
             10 LB35-HA1-NRIC     PIC X(09)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE SPACES.                      
             10 LB35-HA2-NRIC     PIC X(09)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE SPACES.                      
             10 LB35-DETAIL       PIC X(20)  VALUE SPACES.                      
          05 LB35-DET-REC.                                                      
             10 LB35-DET-DEC-NRIC PIC X(09)  VALUE SPACES.                      
             10 FILLER            PIC X(07)  VALUE SPACES.                      
             10 LB35-DET-ROLE     PIC X(04)  VALUE SPACES.                      
          05 LB35A-HEADER1.                                                     
             10 FILLER            PIC X(08)  VALUE 'REGN NO.'.                  
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(10)  VALUE 'SALES MODE'.                
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(10)  VALUE 'BALLOT QTR'.                
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(03)  VALUE 'NT'.                        
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(02)  VALUE 'FT'.                        
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(10)  VALUE 'APPLN DATE'.                
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(09)  VALUE 'HA1 NRIC'.                  
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(09)  VALUE 'HA2 NRIC'.                  
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(13)  VALUE 'DECEASED NRIC'.             
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(13)  VALUE 'ROLE IN APPLN'.             
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(403) VALUE SPACES.                      
          05 LB35A-RECORD.                                                      
             10 LB35A-REGN        PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 LB35A-ALLO-CAT    PIC X(03)  VALUE SPACES.                      
             10 FILLER            PIC X(07)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 LB35A-DTE-BAL     PIC X(06)  VALUE SPACES.                      
             10 FILLER            PIC X(04)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 LB35A-NT          PIC X(03)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 LB35A-FT          PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 LB35A-DTE-APPLN   PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 LB35A-HA1-NRIC    PIC X(09)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 LB35A-HA2-NRIC    PIC X(09)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 LB35A-DEC-NRIC    PIC X(09)  VALUE SPACES.                      
             10 FILLER            PIC X(04)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 LB35A-ROLE        PIC X(04)  VALUE SPACES.                      
             10 FILLER            PIC X(09)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(403) VALUE SPACES.                      
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13L506      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECS       THRU 3000-EXIT                       
             UNTIL WS-L506-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13L506                                                 
                       BP13K730                                                 
                OUTPUT BP13LB35                                                 
                       P13LB35A.                                                
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K730 FILE ERROR ' WS-K730-STATUS             
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4) DELIMITED BY SIZE                            
             INTO LB35-DATE.                                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13L506.                                                      
      *********************                                                     
                                                                                
           READ BP13L506 AT END                                                 
                MOVE 'Y'                   TO WS-L506-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-L506-READ.                     
                                                                                
           MOVE L506-NUM-ALLO-CAT          TO WS-L506-ALLO-CAT.                 
           MOVE L506-DTE-BALLOT            TO WS-L506-DTE-BAL.                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       3000-PROCESS-RECS.                                                       
      ********************                                                      
                                                                                
           MOVE SPACES                     TO WS-DET-APPLN.                     
                                                                                
           IF L506-NUM-REGN NOT = WS-PREV-REGN                                  
              PERFORM 4000-READ-BP13K730   THRU 4000-EXIT                       
              IF WS-K730-FND = 'Y'                                              
                 MOVE L506-NUM-REGN        TO WS-PREV-REGN                      
                 MOVE '1'                  TO WS-DET-APPLN                      
                 ADD 1                     TO WS-LB35-SNO                       
                                              WS-LB35-WRITE                     
                                              WS-K730-FOUND                     
                 PERFORM 8000-WRITE-BP13LB35  THRU 8000-EXIT                    
              ELSE                                                              
                 ADD 1                     TO WS-K730-NTFND                     
              END-IF                                                            
           ELSE                                                                 
              ADD 1                        TO WS-K730-FOUND                     
                                              WS-L506-DUPL                      
              PERFORM 8000-WRITE-BP13LB35  THRU 8000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13L506      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13K730.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BP13K730-REC.                     
           INITIALIZE                         BP13K730-REC.                     
           MOVE 'N'                        TO WS-K730-FND.                      
                                                                                
           MOVE L506-NUM-REGN              TO K730-NUM-REGN.                    
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K730-FND                       
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K730 : ' K730-NUM-REGN           
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K730 : ' WS-K730-STATUS                
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8000-WRITE-BP13LB35.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO BP13LB35-REC.                     
                                                                                
           IF (WS-LB35-LINE > 55) OR                                            
              (WS-L506-KEY-FLD NOT = WS-PREV-KEY-FLD)                           
              ADD 1                        TO WS-LB35-PAGE                      
              MOVE WS-LB35-PAGE            TO LB35-PAGE-NO                      
                                                                                
              MOVE WS-L506-ALLO-CAT        TO LB35-ALLO-CAT                     
              MOVE WS-L506-DTE-BAL         TO LB35-DTE-BAL                      
                                                                                
              MOVE 'BP13LB35'              TO LB35-RPTNAME                      
              WRITE BP13LB35-REC           FROM LB35-HEADER1 AFTER PAGE         
              WRITE BP13LB35-REC           FROM LB35-HEADER2                    
              WRITE BP13LB35-REC           FROM LB35-BLANK                      
              WRITE BP13LB35-REC           FROM LB35-HEADER2A                   
              WRITE BP13LB35-REC           FROM LB35-HEADER2B                   
              WRITE BP13LB35-REC           FROM LB35-BLANK                      
              WRITE BP13LB35-REC           FROM LB35-HEADER3                    
              WRITE BP13LB35-REC           FROM LB35-DASH                       
                                                                                
              MOVE 8                       TO WS-LB35-LINE                      
              IF WS-L506-KEY-FLD NOT = WS-PREV-KEY-FLD                          
                 MOVE 1                    TO WS-LB35-SNO                       
              END-IF                                                            
              MOVE WS-L506-KEY-FLD         TO WS-PREV-KEY-FLD                   
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LB35-RECORD.                      
           IF WS-DET-APPLN = '1'                                                
              MOVE WS-LB35-SNO             TO LB35-SNO                          
              MOVE L506-NUM-REGN           TO LB35-REGN                         
              MOVE K730-CDE-NT1            TO LB35-NT                           
              MOVE K730-CDE-FLAT-TYPE      TO LB35-FT                           
              MOVE K730-DTE-REQUEST        TO LB35-DTE-APPLN                    
              MOVE K730-NUM-NRIC1          TO LB35-HA1-NRIC                     
              IF K730-NUM-NRIC2(1:1) NOT = '#'                                  
                 MOVE K730-NUM-NRIC2       TO LB35-HA2-NRIC                     
              END-IF                                                            
                                                                                
              IF WS-LB35-LINE > 8                                               
                 WRITE BP13LB35-REC        FROM LB35-BLANK                      
                 ADD 1                     TO WS-LB35-LINE                      
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES                  TO LB35-SNO                          
                                              LB35-REGN                         
                                              LB35-NT                           
                                              LB35-FT                           
                                              LB35-DTE-APPLN                    
                                              LB35-HA1-NRIC                     
                                              LB35-HA2-NRIC                     
           END-IF.                                                              
                                                                                
           MOVE L506-NUM-DECEASED-NRIC     TO LB35-DET-DEC-NRIC.                
           IF L506-NUM-DECEASED-NRIC = K730-NUM-NRIC1 OR                        
                                       K730-NUM-NRIC2 OR                        
                                       K730-NUM-NRIC3 OR                        
                                       K730-NUM-NRIC4                           
              MOVE 'APPL'                  TO LB35-DET-ROLE                     
           ELSE                                                                 
              MOVE 'OCCP'                  TO LB35-DET-ROLE                     
           END-IF.                                                              
                                                                                
           MOVE LB35-DET-REC               TO LB35-DETAIL.                      
           WRITE BP13LB35-REC              FROM LB35-RECORD.                    
           ADD 1                           TO WS-LB35-LINE.                     
                                                                                
           PERFORM 8500-WRITE-P13LB35A THRU 8500-EXIT.                          
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8500-WRITE-P13LB35A.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                  TO P13LB35A-REC.                        
                                                                                
           IF WS-LB35A-WRITE = ZEROES                                           
              WRITE P13LB35A-REC FROM LB35A-HEADER1                             
           END-IF.                                                              
                                                                                
           MOVE L506-NUM-REGN           TO LB35A-REGN.                          
           MOVE LB35-ALLO-CAT           TO LB35A-ALLO-CAT.                      
           MOVE LB35-DTE-BAL            TO LB35A-DTE-BAL.                       
           MOVE K730-CDE-NT1            TO LB35A-NT.                            
           MOVE K730-CDE-FLAT-TYPE      TO LB35A-FT.                            
           MOVE K730-DTE-REQUEST        TO LB35A-DTE-APPLN.                     
           MOVE K730-NUM-NRIC1          TO LB35A-HA1-NRIC.                      
           IF K730-NUM-NRIC2(1:1) NOT = '#'                                     
              MOVE K730-NUM-NRIC2       TO LB35A-HA2-NRIC                       
           ELSE                                                                 
              MOVE SPACES               TO LB35A-HA2-NRIC                       
           END-IF.                                                              
           MOVE LB35-DET-DEC-NRIC       TO LB35A-DEC-NRIC.                      
           MOVE LB35-DET-ROLE           TO LB35A-ROLE.                          
                                                                                
           WRITE P13LB35A-REC FROM LB35A-RECORD.                                
           ADD 1                        TO WS-LB35A-WRITE.                      
                                                                                
       8500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY '*================ BP13CB35 ================*'.              
           DISPLAY ' NO OF L506 RECORDS READ        = ' WS-L506-READ.           
           DISPLAY '   NO OF K730 RECORDS FOUND     = ' WS-K730-FOUND.          
           DISPLAY '     NO OF LB35 RECORDS WRITTEN = ' WS-LB35-WRITE.          
           DISPLAY '     NO OF LB35A RECORDS WRITTEN= ' WS-LB35A-WRITE.         
           DISPLAY '     NO OF L506 RECORDS DUPL    = ' WS-L506-DUPL.           
           DISPLAY '   NO OF K730 RECORDS NOT FOUND = ' WS-K730-NTFND.          
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13L506                                                       
                 BP13K730                                                       
                 BP13LB35                                                       
                 P13LB35A.                                                      
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K730 ERROR ' WS-K730-STATUS                  
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        9000-EXIT.                                                              
            EXIT.                                                               
                                                                                
