       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CG01.                                                  
      *AUTHOR.       ZDD1.                                                      
      *DATE-WRITTEN. 21/11/2013.                                                
      * ========================================================== *            
      * FLAT INVENTORY SYSTEM (BP13)                               *            
      * ========================================================== *            
      *  UPDATE - BP13KB20                                         *            
      *                                                            *            
      *  INPUT   :  BP13FA40                                       *            
      *  OUTPUT  :  BP13KB20                                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA40 ASSIGN TO BP13FA40.                                  
                                                                                
                                                                                
           SELECT BP13KB20 ASSIGN TO BP13KB20                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KB20-KEY-FLD                               
                  FILE STATUS     IS WS-KB20-STATUS.                            
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13FA40                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 150  CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13FA40.                                                           
                                                                                
       FD   BP13KB20                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB20.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FA40-EOF                   PIC X    VALUE 'N'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FA40-READ              PIC 9(7) VALUE ZEROS.                  
           05  WS-KB20-WRITE             PIC 9(7) VALUE ZEROS.                  
           05  WS-KB20-READ              PIC 9(7) VALUE ZEROS.                  
           05  WS-KB20-STATUS            PIC X(2) VALUE SPACES.                 
                                                                                
       01  WS-SYSTEM-DATETIME.                                                  
           05  WS-SYS-DATE             PIC 9(08) VALUE ZEROES.                  
           05  WS-SYS-TIME             PIC 9(08) VALUE ZEROES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-FA40    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-FA40 THRU 300-EXIT                              
                    UNTIL WS-FA40-EOF = 'Y'.                                    
            PERFORM 900-CLOSE-FILES  THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13FA40                                                 
                I-O    BP13KB20.                                                
                                                                                
           IF WS-KB20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KB20 FILE, ' WS-KB20-STATUS            
              PERFORM 900-CLOSE-FILES         THRU 900-EXIT.                    
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATETIME.                  
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-FA40.                                                           
      *-------------------------------------------------------------            
           READ BP13FA40 AT END                                                 
                MOVE 'Y' TO WS-FA40-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1         TO WS-FA40-READ.                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-FA40.                                                        
      *-------------------------------------------------------------            
                                                                                
           PERFORM 600-READ-BP13KB20   THRU 600-EXIT.                           
           PERFORM 200-READ-FA40       THRU 200-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-READ-BP13KB20.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES            TO KB20-KEY-FLD.                              
           MOVE FA40-REGN-NO      TO KB20-KEY-FLD.                              
                                                                                
           READ BP13KB20.                                                       
                                                                                
           IF WS-KB20-STATUS = 00                                               
              ADD 1    TO WS-KB20-READ                                          
              PERFORM 700-REWRITE-BP13KB20 THRU 700-EXIT                        
           ELSE                                                                 
              IF WS-KB20-STATUS = 23                                            
                 DISPLAY 'REC NOT FOUND IN BP13KB20 : ' KB20-KEY-FLD            
                        '  STATUS ' WS-KB20-STATUS                              
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13KB20 : ' WS-KB20-STATUS             
                 MOVE WS-KB20-STATUS     TO RETURN-CODE                         
                 PERFORM 900-CLOSE-FILES THRU 900-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-REWRITE-BP13KB20.                                                    
      *-------------------------------------------------------------            
           MOVE SPACES              TO KB20-NUM-SCH-ACC                         
                                       KB20-NUM-CPFHG-REQ                       
                                       KB20-NUM-AHG-REQ                         
                                       KB20-NUM-CPFHG-PRIORITY                  
                                       KB20-DTE-ACCEPT                          
                                       KB20-NUM-PAYMENT-SCHEME.                 
           MOVE WS-SYS-DATE         TO KB20-DTE-UPDATE                          
           MOVE WS-SYS-TIME         TO KB20-TME-UPDATE.                         
                                                                                
           REWRITE BP13KB20-REC.                                                
                                                                                
           IF WS-KB20-STATUS = 00 OR 02                                         
              ADD 1 TO WS-KB20-WRITE                                            
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13KB20 : ' KB20-NUM-REGN               
                      '  STATUS ' WS-KB20-STATUS                                
              MOVE WS-KB20-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '- EDTKB78 CONTROL TOTAL -----------'.                       
           DISPLAY '1) NO OF REC READ    FA40  = ' WS-FA40-READ.                
           DISPLAY '2) NO OF REC READ    KB20  = ' WS-KB20-READ.                
           DISPLAY '3) NO OF REC WRITE   KB20  = ' WS-KB20-WRITE.               
                                                                                
           CLOSE BP13FA40                                                       
                 BP13KB20.                                                      
                                                                                
           IF WS-KB20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KB20 FILE, ' WS-KB20-STATUS.           
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
