       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C234.                                                 
      *AUTHOR.        JOEL E PARRENO.                                           
      *DATE-WRITTEN.  14/10/93.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE : 1. THIS IS A PROGRAM TO DELETE THE FLATLIST   *            
      *                 FILES BP13K230 & BP13K240, SO THAT AT THE  *            
      *                 END OF BOOKING EXERCISE, THE RECS WILL NOT *            
      *                 BE PASSED TO INTV.                         *            
      *                                                            *            
      *              2. THE DELETION IS BASED ON BP13F205 FILE.    *            
      *                                                            *            
      *  INPUT     :  BP13F205                                     *            
      *               BP13K060                                     *            
      *  I-O       :  BP13K230                                     *            
      *               BP13K240.                                    *            
      * ---------------------------------------------------------- *            
      * CHGE NO  BY   DATE    DESCRIPTION                          *            
      * -------- ---- ------  -----------                          *            
      * SOCPH8   TON  030594  CHANGE BM06CTL TO BP13F205.          *            
      * SOCPH9   GD   051194  ADD CHK FOR ZONE, FLAT TYPE          *            
      *                       BEFORE DELETING K230, K240.          *            
      * N950012  LMS  010395  TO INCLUDE CHECKING OF 3R ZONE.      *            
      * BP130393 SSS  201197  INCLUDE PG ZONE                      *            
      * BP130418 MPA  030498  YEAR 2000 CHANGES                    *            
      * BP130956 AKT  171100  CHGE BP13K230/BP13K240 LRECL FROM    *            
      *                       130 TO 250.                          *            
      * BP131017 JAM  240501  CHGE DELETION OF RECORDS FOR         *            
      *                       BP13K230 & BP13K240 TO WRITING TO    *            
      *                       BP13F230 & BP13F240 FLAT FILES.      *            
      * BP131059 LMS  011101  TO CATER FOR BTO.                    *            
      * BP132283 MTD  140103  CHANGE K230 TO K240 IN PARAGRAPH     *            
      *                       660-CHECK-NT-ALLOCDTE                *            
      * BP132278 JF4  280303  CHGE BP13F230/F240 LRECL TO 450.     *            
      * BP133026 LMS1 280307  TO CATER FOR 3R AND SMALLER FLATS.   *            
      * BP133210 RB12 301107  CHANGE CHECK FROM K230 TO K240       *            
      * BP133610 ESA1 120509  TO REPLACE BP13K767 W/ BP13K816      *            
      * BP133779 RB12 301209  CATER FOR ZONE EE                    *            
      * BP134841 ESA1 270313  CATER FOR NEW ZONE LAYOUT            *            
      * BP134769 ZDD1 180413  CHANGE PUNGGOL 21 TO PUNGGOL         *            
      * BP135281 ESA1 300514  TO REPLACE BP13K816 W/ BP13K813      *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN       TO BP13F205.                   00470000
                                                                                
           SELECT BP13F230  ASSIGN       TO BP13F230.                   00470000
                                                                                
           SELECT BP13F240  ASSIGN       TO BP13F240.                   00470000
                                                                                
           SELECT BP13K060  ASSIGN       TO BP13K060                    00470000
                            ORGANIZATION IS INDEXED                     00490000
                            ACCESS MODE  IS RANDOM                      00480000
                            RECORD KEY   IS K060-KEY-FLD                00500000
                            FILE STATUS  IS WS-K060-STATUS.             00510000
                                                                                
           SELECT BP13K230  ASSIGN       TO BP13K230                    00470000
                            ORGANIZATION IS INDEXED                     00490000
                            ACCESS MODE  IS DYNAMIC                     00480000
                            RECORD KEY   IS K230-KEY-FLD                00500000
                            FILE STATUS  IS WS-K230-STATUS.             00510000
                                                                                
           SELECT BP13K240  ASSIGN       TO BP13K240                    00470000
                            ORGANIZATION IS INDEXED                     00490000
                            ACCESS MODE  IS DYNAMIC                     00480000
                            RECORD KEY   IS K240-KEY-FLD                00500000
                            FILE STATUS  IS WS-K240-STATUS.             00510000
                                                                                
           SELECT BP13K813  ASSIGN       TO BP13K813                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K813-KEY-FLD                        
                            FILE STATUS  IS WS-K813-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F230                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS.                                     
       01   F230-REC        PIC X(450).                                         
                                                                                
       FD   BP13F240                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS.                                     
       01   F240-REC        PIC X(450).                                         
                                                                                
       FD   BP13K060                                                    00640000
            RECORD CONTAINS 25  CHARACTERS.                                     
       COPY BP13K060.                                                   00680000
                                                                                
       FD   BP13K230                                                    00640000
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K230.                                                   00680000
                                                                                
       FD   BP13K240                                                    00640000
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K240.                                                   00680000
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-K060-STATUS               PIC 9(2)  VALUE  00.                
           05  WS-K230-STATUS               PIC 9(2)  VALUE  00.                
           05  WS-K240-STATUS               PIC 9(2)  VALUE  00.                
           05  WS-K813-STATUS               PIC 9(2)  VALUE  00.                
           05  WS-F205-EOF-IND              PIC X     VALUE 'N'.                
           05  WS-K230-EOF-IND              PIC X     VALUE 'N'.                
           05  WS-K240-EOF-IND              PIC X     VALUE 'N'.                
           05  WS-NT-ZONE-DESC-FND          PIC X     VALUE 'N'.                
           05  WS-K060-REC-FND              PIC X     VALUE 'N'.                
               88  WS-K060-NT-ZONE-FOUND              VALUE 'Y'.                
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-CNT-READ-F205             PIC 9(2)  VALUE ZEROS.              
           05  WS-CNT-WRITE-F230            PIC 9(6)  VALUE ZEROS.              
           05  WS-CNT-WRITE-F240            PIC 9(6)  VALUE ZEROS.              
           05  BP13F230-WRITE-CTR           PIC 9(6)  VALUE ZEROS.              
           05  BP13F240-WRITE-CTR           PIC 9(6)  VALUE ZEROS.              
                                                                                
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-NT-ZONE-DESC              PIC X(20) VALUE SPACES.             
           05  WS-FLAT-TYPE                 PIC X(02) VALUE SPACES.             
           05  WS-DATE-CCYYMMDD.                                                
               10  WS-DATE-CC               PIC X(2).                           
               10  WS-DATE-YYMMDD           PIC X(6).                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-ROUTINE        THRU    100-EXIT.                   
           PERFORM  200-READ-BP13F205       THRU    200-EXIT                    
           PERFORM  300-PROCESS-RECORDS     THRU    300-EXIT                    
              UNTIL   WS-F205-EOF-IND  = 'Y'.                                   
           PERFORM  700-DISPLAY-STATISTICS  THRU    700-EXIT.                   
           PERFORM  900-CLOSE-ROUTINE       THRU    900-EXIT.                   
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT BP13F205                                                  
                      BP13K060                                                  
                      BP13K230                                                  
                      BP13K240                                                  
                      BP13K813                                                  
               OUTPUT BP13F230                                                  
                      BP13F240.                                                 
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K060, STATUS = ' WS-K060-STATUS        
              MOVE     WS-K060-STATUS    TO    RETURN-CODE                      
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K230-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K230, STATUS = ' WS-K230-STATUS        
              MOVE     WS-K230-STATUS    TO    RETURN-CODE                      
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K240-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K240, STATUS = ' WS-K240-STATUS        
              MOVE     WS-K240-STATUS    TO    RETURN-CODE                      
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K813, STATUS = ' WS-K813-STATUS        
              MOVE     WS-K813-STATUS    TO    RETURN-CODE                      
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-BP13F205.                                                       
      *-------------------------------------------------------------            
           READ BP13F205                                                        
                AT END MOVE 'Y' TO WS-F205-EOF-IND                              
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1                  TO WS-CNT-READ-F205.                         
           MOVE F205-DTE-ALLOCN    TO WS-DATE-CCYYMMDD.                         
           PERFORM 210-DECODE-FLAT-TYPE THRU 210-EXIT.                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       210-DECODE-FLAT-TYPE.                                                    
      *-------------------------------------------------------------            
           EVALUATE F205-NUM-FLAT-TYPE                                          
             WHEN 'E '                                                          
                   MOVE '6 '         TO WS-FLAT-TYPE                            
             WHEN OTHER                                                         
                   MOVE F205-NUM-FLAT-TYPE TO WS-FLAT-TYPE                      
           END-EVALUATE.                                                        
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-RECORDS.                                                     
      *-------------------------------------------------------------            
           MOVE 'N' TO WS-NT-ZONE-DESC-FND.                                     
           MOVE SPACES TO WS-NT-ZONE-DESC.                                      
                                                                                
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              MOVE 'N'               TO WS-K060-REC-FND                         
              MOVE  F205-NUM-NT-ZONE TO K060-CODE                               
              MOVE '02'              TO K060-SERIAL-NO                          
              PERFORM 400-READ-BP13K060 THRU  400-EXIT                          
           ELSE                                                                 
              IF F205-NUM-NT-ZONE-SW = 'Z'                                      
                 IF F205-NUM-SELECTION = 'BTO'                                  
                    PERFORM 350-READ-BP13K813 THRU 350-EXIT                     
                    DISPLAY 'NT : ' WS-NT-ZONE-DESC                             
                 ELSE                                                           
                   EVALUATE F205-NUM-NT-ZONE                                    
                    WHEN 'PG'                                                   
                          MOVE 'PUNGGOL   ' TO WS-NT-ZONE-DESC                  
                          MOVE 'Y'          TO WS-NT-ZONE-DESC-FND              
                    WHEN 'NN'                                                   
                          MOVE 'NORTH'      TO WS-NT-ZONE-DESC                  
                          MOVE 'Y'          TO WS-NT-ZONE-DESC-FND              
                    WHEN 'WW'                                                   
                          MOVE 'WEST'       TO WS-NT-ZONE-DESC                  
                          MOVE 'Y'          TO WS-NT-ZONE-DESC-FND              
                    WHEN 'EE'                                                   
                          MOVE 'EAST'       TO WS-NT-ZONE-DESC                  
                          MOVE 'Y'          TO WS-NT-ZONE-DESC-FND              
                    WHEN 'NE'                                                   
                          MOVE 'NORTH-EAST' TO WS-NT-ZONE-DESC                  
                          MOVE 'Y'          TO WS-NT-ZONE-DESC-FND              
                    WHEN '3R'                                                   
                          MOVE '          ' TO WS-NT-ZONE-DESC                  
                          MOVE 'Y'          TO WS-NT-ZONE-DESC-FND              
                 END-EVALUATE                                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-NT-ZONE-DESC-FND = 'Y'                                         
              MOVE    'N'                    TO     WS-K230-EOF-IND             
              PERFORM 500-STARTBR-BP13K230   THRU   500-EXIT                    
              PERFORM 560-CHECK-NT-ALLOCDTE  THRU   560-EXIT                    
                          UNTIL WS-K230-EOF-IND  = 'Y'                          
              MOVE    'N'                     TO    WS-K240-EOF-IND             
              PERFORM 600-STARTBR-BP13K240    THRU  600-EXIT                    
              PERFORM 660-CHECK-NT-ALLOCDTE   THRU  660-EXIT                    
                          UNTIL WS-K240-EOF-IND  = 'Y'                          
           ELSE                                                                 
              DISPLAY 'NEW TOWN/ZONE DESCRIPTION NOT FOUND  = '                 
                       F205-NUM-NT-ZONE.                                        
                                                                                
           PERFORM 200-READ-BP13F205 THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       350-READ-BP13K813.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                    TO BP13K813-REC.                      
           INITIALIZE                        BP13K813-REC.                      
                                                                                
           MOVE F205-NUM-NT-ZONE          TO K813-NUM-ZONE.                     
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD                                   
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    READ BP13K813 NEXT RECORD                                   
                    IF K813-NUM-ZONE = F205-NUM-NT-ZONE                         
                       MOVE K813-NME-SITE TO WS-NT-ZONE-DESC                    
                       MOVE 'Y'           TO WS-NT-ZONE-DESC-FND                
                    ELSE                                                        
                       DISPLAY 'BP13K813 - NOT FND: ' K813-KEY-FLD              
                       INITIALIZE BP13K813-REC                                  
                    END-IF                                                      
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE WS-K813-STATUS               TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13K813: ' WS-K813-STATUS           
                            ' KEY : ' K813-NUM-ZONE                             
                    PERFORM  900-CLOSE-ROUTINE                                  
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-READ-BP13K060.                                                       
      *-------------------------------------------------------------            
           READ BP13K060.                                                       
                                                                                
           IF WS-K060-STATUS = 00                                               
              MOVE  K060-DESC  TO WS-NT-ZONE-DESC                               
              MOVE  'Y'        TO WS-NT-ZONE-DESC-FND                           
           ELSE                                                                 
           IF WS-K060-STATUS = 23                                               
              MOVE SPACES      TO WS-NT-ZONE-DESC                               
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K060, STATUS = ' WS-K060-STATUS        
              DISPLAY '                  K060-KEY-FLD = ' K060-KEY-FLD          
              MOVE     WS-K060-STATUS    TO               RETURN-CODE           
              PERFORM  900-CLOSE-ROUTINE.                                       
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    POSITION POINTER TO THE BEGINNING OF THE FILE BP13K230      *        
      ******************************************************************        
       500-STARTBR-BP13K230.                                                    
      *-------------------------------------------------------------            
           MOVE    LOW-VALUES   TO K230-KEY-FLD.                                
           START   BP13K230 KEY  > K230-KEY-FLD.                                
                                                                                
           IF WS-K230-STATUS NOT = 00                                           
              DISPLAY 'START ERROR, BP13K230-STATUS = ' WS-K230-STATUS          
           ELSE                                                                 
              PERFORM 550-READNXT-BP13K230 THRU 550-EXIT.                       
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       550-READNXT-BP13K230.                                                    
      *-------------------------------------------------------------            
           READ BP13K230 NEXT                                                   
                AT END MOVE 'Y' TO WS-K230-EOF-IND.                             
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CHECK K230-DATA IF IT IS EQUAL TO CONTORL NT/ALLOC-DATE     *        
      ******************************************************************        
       560-CHECK-NT-ALLOCDTE.                                                   
      *-------------------------------------------------------------            
                                                                                
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              IF (WS-NT-ZONE-DESC   =  K230-NUM-NEW-TOWN)   AND                 
                 (WS-DATE-CCYYMMDD  =  K230-DTE-ALLOC)      AND                 
                 ((WS-FLAT-TYPE(1:1) =  K230-NUM-FLAT-TYPE(1:1)) OR             
                  (WS-FLAT-TYPE(1:1) =  K230-NUM-FLAT-TYPE(2:1)) OR             
                  ((WS-FLAT-TYPE = 'SE') AND                                    
                   (K230-NUM-FLAT-TYPE = '1A' OR '2A' OR                        
                                         '02' OR '03' OR                        
                                         '04' OR '4D' OR '4P' OR                
                                         '05' OR '5D' OR '5P')) OR              
                  ((WS-FLAT-TYPE = '1A') AND                                    
                   (K230-NUM-FLAT-TYPE   = 'SA')) OR                            
                  ((F205-NUM-NT-ZONE = 'CT') AND                                
                   (K230-NUM-FLAT-TYPE   = 'S1' OR 'S2')))                      
                   PERFORM 580-WRITE-BP13F230-REC THRU 580-EXIT                 
              END-IF                                                            
           ELSE                                                                 
              IF F205-NUM-NT-ZONE-SW = 'Z'                                      
                 IF (WS-NT-ZONE-DESC   =  K230-NUM-ZONE)           AND          
                    (WS-DATE-CCYYMMDD  =  K230-DTE-ALLOC)          AND          
                    ((WS-FLAT-TYPE(1:1) =  K230-NUM-FLAT-TYPE(1:1)) OR          
                     (WS-FLAT-TYPE(1:1) =  K230-NUM-FLAT-TYPE(2:1)) OR          
                     (WS-FLAT-TYPE = '00')                          OR          
                    ((WS-FLAT-TYPE      =  '1A') AND                            
                     (K230-NUM-FLAT-TYPE = 'SA'))                   OR          
                    ((F205-NUM-NT-ZONE (1:2)  = '3R')              AND          
                    ((K230-NUM-FLAT-TYPE(1:1) = '1' OR '2' OR '3') OR           
                     (K230-NUM-FLAT-TYPE = 'SA' OR '01' OR                      
                                           '02' OR '03'))))                     
                      PERFORM 580-WRITE-BP13F230-REC THRU 580-EXIT              
                 ELSE                                                           
                    IF (F205-NUM-SELECTION = 'BTO')         AND                 
                       (WS-NT-ZONE-DESC   = K230-NUM-NEW-TOWN)     AND          
                       (WS-DATE-CCYYMMDD  = K230-DTE-ALLOC)        AND          
                      ((WS-FLAT-TYPE      = '00')                  OR           
                      ((WS-FLAT-TYPE      =  '1A') AND                          
                       (K230-NUM-FLAT-TYPE = 'SA')))                            
                        PERFORM 580-WRITE-BP13F230-REC THRU 580-EXIT            
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 550-READNXT-BP13K230 THRU 550-EXIT.                          
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    WRITE BP13F230 RECORD W/C EQUAL TO CONTROL NT/ALLOC-DATE    *        
      ******************************************************************        
       580-WRITE-BP13F230-REC.                                                  
      *-------------------------------------------------------------            
           WRITE  F230-REC FROM K230-DETAIL-REC.                                
           ADD 1 TO BP13F230-WRITE-CTR.                                         
                                                                                
       580-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-STARTBR-BP13K240.                                                    
      *-------------------------------------------------------------            
           MOVE    LOW-VALUES   TO K240-KEY-FLD.                                
           START   BP13K240 KEY  > K240-KEY-FLD.                                
                                                                                
           IF WS-K240-STATUS NOT = 00                                           
              DISPLAY 'START ERROR, BP13K240-STATUS = ' WS-K240-STATUS          
           ELSE                                                                 
              PERFORM 650-READNXT-BP13K240    THRU   650-EXIT.                  
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       650-READNXT-BP13K240.                                                    
      *-------------------------------------------------------------            
           READ BP13K240 NEXT                                                   
                AT END MOVE 'Y' TO WS-K240-EOF-IND.                             
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CHECK K240-DATA IF IT IS EQUAL TO CONTROL NT/ALLOC-DATE     *        
      ******************************************************************        
       660-CHECK-NT-ALLOCDTE.                                                   
      *-------------------------------------------------------------            
                                                                                
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              IF (WS-NT-ZONE-DESC   =  K240-NUM-NEW-TOWN)   AND                 
                 (WS-DATE-CCYYMMDD  =  K240-DTE-ALLOC)      AND                 
                 ((WS-FLAT-TYPE(1:1) =  K240-NUM-FLAT-TYPE(1:1)) OR             
                  (WS-FLAT-TYPE(1:1) =  K240-NUM-FLAT-TYPE(2:1)) OR             
                  ((WS-FLAT-TYPE = 'SE') AND                                    
                   (K240-NUM-FLAT-TYPE = '1A' OR '2A' OR                        
                                         '02' OR '03' OR                        
                                         '04' OR '4D' OR '4P' OR                
                                         '05' OR '5D' OR '5P')) OR              
                  ((WS-FLAT-TYPE = '1A') AND                                    
                   (K240-NUM-FLAT-TYPE   = 'SA')) OR                            
                  ((F205-NUM-NT-ZONE = 'CT') AND                                
                   (K240-NUM-FLAT-TYPE   = 'S1' OR 'S2')))                      
                   PERFORM 680-WRITE-BP13F240-REC THRU 680-EXIT                 
              END-IF                                                            
           ELSE                                                                 
              IF F205-NUM-NT-ZONE-SW = 'Z'                                      
                 IF (WS-NT-ZONE-DESC   =  K240-NUM-ZONE)           AND          
                    (WS-DATE-CCYYMMDD  =  K240-DTE-ALLOC)          AND          
                    ((WS-FLAT-TYPE(1:1) =  K240-NUM-FLAT-TYPE(1:1)) OR          
                     (WS-FLAT-TYPE(1:1) =  K240-NUM-FLAT-TYPE(2:1)) OR          
                     (WS-FLAT-TYPE      =  '00')                    OR          
                    ((WS-FLAT-TYPE      =  '1A') AND                            
                     (K240-NUM-FLAT-TYPE = 'SA'))                   OR          
                    ((F205-NUM-NT-ZONE (1:2)  = '3R')              AND          
                    ((K240-NUM-FLAT-TYPE(1:1) = '1' OR '2' OR '3') OR           
                     (K240-NUM-FLAT-TYPE = 'SA' OR '01' OR                      
                                           '02' OR '03'))))                     
                      PERFORM 680-WRITE-BP13F240-REC THRU 680-EXIT              
                 ELSE                                                           
                    IF (F205-NUM-SELECTION = 'BTO')         AND                 
                       (WS-NT-ZONE-DESC   = K240-NUM-NEW-TOWN)     AND          
                       (WS-DATE-CCYYMMDD  = K240-DTE-ALLOC)        AND          
                      ((WS-FLAT-TYPE      = '00')                  OR           
                      ((WS-FLAT-TYPE      =  '1A') AND                          
                       (K240-NUM-FLAT-TYPE = 'SA')))                            
                        PERFORM 680-WRITE-BP13F240-REC THRU 680-EXIT            
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 650-READNXT-BP13K240 THRU 650-EXIT.                          
                                                                                
       660-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    WRITE BP13F240 RECORD W/C EQUAL TO CONTROL NT/ALLOC-DATE    *        
      ******************************************************************        
       680-WRITE-BP13F240-REC.                                                  
      *-------------------------------------------------------------            
           WRITE  F240-REC FROM K240-HEADER-REC.                                
           ADD 1 TO BP13F240-WRITE-CTR.                                         
                                                                                
       680-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       700-DISPLAY-STATISTICS.                                                  
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '*--------- BP13C234 CONTROL LIST ---------*'.               
           DISPLAY 'BP13F205 READ    RECORDS  = ' WS-CNT-READ-F205.             
           DISPLAY 'BP13K230 RECORDS WRITTEN  = ' WS-CNT-WRITE-F230.            
           DISPLAY 'BP13K240 RECORDS WRITTEN  = ' WS-CNT-WRITE-F240.            
           DISPLAY 'BP13F230 RECORDS WRITTEN  = ' BP13F230-WRITE-CTR.           
           DISPLAY 'BP13F240 RECORDS WRITTEN  = ' BP13F240-WRITE-CTR.           
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           CLOSE BP13F205                                                       
                 BP13F230                                                       
                 BP13K060                                                       
                 BP13K813                                                       
                 BP13K230                                                       
                 BP13K240.                                                      
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K060, STATUS = ' WS-K060-STATUS        
              MOVE     WS-K060-STATUS    TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K230-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K230, STATUS = ' WS-K230-STATUS        
              MOVE     WS-K230-STATUS    TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K240-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K240, STATUS = ' WS-K240-STATUS        
              MOVE     WS-K240-STATUS    TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K813, STATUS = ' WS-K813-STATUS        
              MOVE     WS-K813-STATUS    TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
