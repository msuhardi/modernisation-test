       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C02N.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  27/08/2004.                                               
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *   OBJECTIVE  :  TO GENERATE REPORT ON MCPS POSTAL CODE.     *           
      *                                                             *           
      *   INPUT      :  BP13F725 -- SEQUENTIAL FILE                 *           
      *                 BP13F730 -- REQ FILE                        *           
      *   OUTPUT     :  BP13L02N                                    *           
      *                                                             *           
      *   AMENDMENTS:                                               *           
      *    REF      DATE     BY          DESCRIPTIONS               *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP132583 27/08/2004 SD10 NEW PROGRAM                        *           
      * BP133323 22/07/2008 IL5  UPDATE PROJECT NAME                *           
      * BP133389 01/08/2008 MSD1 ADD DETAIL FOR REGN                *           
      * BP133324 30/10/2008 LSB1 UPDATE PROJECT NAME                *           
      * BP133542 23/03/2009 ZDD1 CHANGE REPORT  NAME                *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F725 ASSIGN TO BP13F725.                                  
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
           SELECT BP13L02N ASSIGN TO BP13L02N.                                  
                                                                                
           SELECT BP13KB78  ASSIGN       TO BP13KB78                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KB78-KEY-FLD                        
                            FILE STATUS  IS BP13KB78-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F725       BLOCK  CONTAINS 0 RECORDS                             
                          RECORD CONTAINS 211 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F725.                                                           
                                                                                
       FD  BP13F730       BLOCK  CONTAINS 0 RECORDS                             
                          RECORD CONTAINS 500 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F730.                                                           
                                                                                
       FD   BP13KB78                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB78.                                                           
                                                                                
       FD   BP13L02N                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       01   BP13L02N-REC           PIC X(132).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  BP13KB78-STATUS         PIC 99      VALUE 00.                        
       01  WS-EOF-F725             PIC X       VALUE SPACE.                     
       01  WS-EOF-F730             PIC X       VALUE SPACE.                     
                                                                                
       01  WS-F725-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F730-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-L02N-WRITE           PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-L02N-LINECTR         PIC 9(2)    VALUE 60.                        
       01  WS-L02N-PAGECTR         PIC 9(5)    VALUE ZEROES.                    
       01  WS-L02N-SNO             PIC 9(3)    VALUE ZEROES.                    
                                                                                
       01  WS-REGN                 PIC X(8)    VALUE SPACES.                    
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC   PIC 9(2).                                    
                   15  WS-SYS-YY   PIC 9(2).                                    
               10  WS-SYS-MM       PIC 9(2).                                    
               10  WS-SYS-DD       PIC 9(2).                                    
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-MM    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-YY    PIC X(4)    VALUE SPACES.                    
                                                                                
      *============================================================*            
      *         BP13L02N -   R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  L02N-HEADER-01.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L02N'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L02N-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L02N-PAGE-NO        PIC ZZZZ9.                                   
                                                                                
       01  L02N-HEADER-02.                                                      
           05  FILLER              PIC X(12)     VALUE                          
               'PROJECT  : '.                                                   
           05  L02N-PROJ-NAME      PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
               'DAILY REPORT ON MCPS REQUEST WHERE PARENT/CHILD'.               
                                                                                
       01  L02N-HEADER-2B.                                                      
           05  FILLER              PIC X(53)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
               'RESIDENCE IS AN NON HDB FLAT'.                                  
                                                                                
       01  L02N-HEADER-03.                                                      
           05  FILLER              PIC X(03)     VALUE 'SNO'.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'REGN'.                  
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'APPL POSTAL/'.          
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'APPL BLK '.             
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'APPL STREET/'.          
           05  FILLER              PIC X(22)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'APPL UNIT/'.            
                                                                                
       01  L02N-HEADER-3A.                                                      
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'NRIC'.                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'MHA POSTAL'.            
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'MHA BLK'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'MHA STREET'.            
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'MHA UNIT'.              
           05  FILLER              PIC X(16)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REMARKS'.               
                                                                                
       01  L02N-HEADER-04.                                                      
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  L02N-DETAILS.                                                        
           05  L02N-SNO            PIC X(03).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02N-REGNO          PIC X(08).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02N-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L02N-POSTAL         PIC X(06).                                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  L02N-ADDR1          PIC X(05).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L02N-ADDR2          PIC X(32).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02N-ADDR3          PIC X(09).                                   
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  L02N-REMARKS        PIC X(11).                                   
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 3000-READ-F725     THRU 3000-EXIT.                           
           PERFORM 4000-READ-F730     THRU 4000-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
              UNTIL WS-EOF-F730 = 'Y' OR                                        
                    WS-EOF-F725 = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F725                                                 
                       BP13F730                                                 
                       BP13KB78                                                 
                OUTPUT BP13L02N.                                                
           IF BP13KB78-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13KB78-STATUS ' BP13KB78-STATUS         
              MOVE     BP13KB78-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           MOVE 'N' TO WS-EOF-F725.                                             
           MOVE 'N' TO WS-EOF-F730.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
           MOVE   WS-EDITED-DATE      TO L02N-RUN-DATE.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-PROCESS.                                                            
      *****************************************************************         
      *  MATCH REGN-NO AND NRIC AND POSTAL CDE                                  
           IF F730-NUM-REGN = F725-NUM-REGN                                     
              PERFORM 2100-PRINT-RECORD    THRU 2100-EXIT                       
              PERFORM 3000-READ-F725       THRU 3000-EXIT                       
           ELSE                                                                 
              IF F730-NUM-REGN < F725-NUM-REGN                                  
                 PERFORM 4000-READ-F730    THRU 4000-EXIT                       
              ELSE                                                              
                 PERFORM 3000-READ-F725    THRU 3000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2100-PRINT-RECORD.                                                       
           IF F730-NUM-REGN NOT = WS-REGN                                       
              MOVE SPACES                  TO L02N-DETAILS                      
              PERFORM 5000-PRINT-RPT     THRU 5000-EXIT                         
                                                                                
              ADD                        1 TO WS-L02N-SNO                       
              MOVE WS-L02N-SNO             TO L02N-SNO                          
              MOVE F730-NUM-REGN           TO L02N-REGNO                        
              MOVE F730-NUM-MCPS-POSTAL    TO L02N-POSTAL                       
                                                                                
              PERFORM 2200-READ-BP13KB78 THRU 2200-EXIT                         
              MOVE KB78-NUM-CP-BLK         TO L02N-ADDR1                        
              MOVE KB78-NME-CP-STREET      TO L02N-ADDR2                        
              IF KB78-NUM-CP-LEVEL NOT = SPACE AND LOW-VALUES                   
                 STRING '#' KB78-NUM-CP-LEVEL '-'                               
                       KB78-NUM-CP-MAIN-UNIT KB78-NUM-CP-SUB-UNIT               
                 DELIMITED BY SIZE INTO L02N-ADDR3                              
              END-IF                                                            
                                                                                
              PERFORM 5000-PRINT-RPT     THRU 5000-EXIT                         
              MOVE F730-NUM-REGN           TO WS-REGN                           
           END-IF.                                                              
                                                                                
           MOVE SPACES                  TO L02N-DETAILS                         
           MOVE F725-NUM-NRIC           TO L02N-NRIC                            
           MOVE F725-CDE-POSTAL         TO L02N-POSTAL                          
           MOVE F725-NUM-BLK-HSE        TO L02N-ADDR1                           
           MOVE F725-NME-STREET         TO L02N-ADDR2                           
           IF F725-NUM-STOREY NOT = SPACE AND LOW-VALUES                        
              STRING '#' F725-NUM-STOREY '-' F725-NUM-UNIT                      
                    DELIMITED BY SIZE INTO L02N-ADDR3                           
              END-STRING                                                        
           END-IF                                                               
           IF F725-NUM-NRIC = F730-NUM-RES-NRIC1 OR                             
                              F730-NUM-RES-NRIC2                                
              IF ((F725-CDE-POSTAL = F730-NUM-MCPS-POSTAL) AND                  
                  (F725-NUM-NRIC = F730-NUM-RES-NRIC1))                         
                 MOVE 'P1 / C1'         TO L02N-REMARKS                         
                 PERFORM 5000-PRINT-RPT THRU 5000-EXIT                          
              ELSE                                                              
                 IF ((F725-CDE-POSTAL = F730-NUM-MCPS-POSTAL) AND               
                     (F725-NUM-NRIC = F730-NUM-RES-NRIC2))                      
                    MOVE 'P2 / C2'         TO L02N-REMARKS                      
                    PERFORM 5000-PRINT-RPT THRU 5000-EXIT                       
                 ELSE                                                           
                    MOVE 'NOT MATCHED'     TO L02N-REMARKS                      
                    PERFORM 5000-PRINT-RPT THRU 5000-EXIT.                      
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2200-READ-BP13KB78.                                                      
           MOVE  SPACES             TO BP13KB78-REC.                            
           INITIALIZE                  BP13KB78-REC.                            
                                                                                
           MOVE F730-NUM-REGN       TO KB78-KEY-FLD.                            
           READ BP13KB78.                                                       
                                                                                
           EVALUATE BP13KB78-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
                CONTINUE                                                        
           WHEN 23                                                              
              DISPLAY 'BP13KB78 REC MISSING, STATUS IS' KB78-KEY-FLD            
           WHEN OTHER                                                           
              DISPLAY 'BP13KB78 REC MISSING, STATUS IS' KB78-KEY-FLD            
           END-EVALUATE.                                                        
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-F725.                                                          
      *****************************************************************         
                                                                                
           READ BP13F725 AT END MOVE 'Y' TO WS-EOF-F725                         
                GO TO 3000-EXIT.                                                
            ADD 1 TO WS-F725-READ.                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-READ-F730.                                                          
      *****************************************************************         
                                                                                
           READ BP13F730 AT END MOVE 'Y' TO WS-EOF-F730                         
                GO TO 4000-EXIT.                                                
           ADD 1 TO WS-F730-READ.                                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-PRINT-RPT.                                                          
      *****************************************************************         
                                                                                
           IF WS-L02N-LINECTR >= 54                                             
              PERFORM 6000-CHECK-PROJECT THRU 6000-EXIT                         
              ADD                  1 TO WS-L02N-PAGECTR                         
              MOVE WS-L02N-PAGECTR   TO L02N-PAGE-NO                            
              WRITE BP13L02N-REC   FROM L02N-HEADER-01 AFTER PAGE               
              WRITE BP13L02N-REC   FROM L02N-HEADER-02                          
              WRITE BP13L02N-REC   FROM L02N-HEADER-2B                          
              WRITE BP13L02N-REC   FROM L02N-HEADER-03 AFTER 2                  
              WRITE BP13L02N-REC   FROM L02N-HEADER-3A                          
              WRITE BP13L02N-REC   FROM L02N-HEADER-04                          
              MOVE                 7 TO WS-L02N-LINECTR                         
           END-IF.                                                              
                                                                                
           WRITE BP13L02N-REC   FROM L02N-DETAILS                               
           ADD                  1 TO WS-L02N-LINECTR                            
                                     WS-L02N-WRITE.                             
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6000-CHECK-PROJECT.                                                      
      *****************************************************************         
                                                                                
            EVALUATE F730-CDE-NT1                                               
                WHEN 'BD '                                                      
                   MOVE 'BELVIA             ' TO L02N-PROJ-NAME                 
                WHEN 'HG'                                                       
                   MOVE 'PARKLAND RESIDENCES' TO L02N-PROJ-NAME                 
                WHEN 'JW'                                                       
                   MOVE 'LAKE VISTA         ' TO L02N-PROJ-NAME                 
                WHEN 'CL'                                                       
                   MOVE 'TRIVELIS           ' TO L02N-PROJ-NAME                 
                WHEN 'PRC'                                                      
                   MOVE 'PASIR RIS ONE      ' TO L02N-PROJ-NAME                 
                WHEN OTHER                                                      
                   MOVE SPACES         TO L02N-PROJ-NAME                        
            END-EVALUATE.                                                       
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
      *****************************************************************         
       9000-CLOSE-ROUTINE.                                                      
      *****************************************************************         
                                                                                
           DISPLAY '*-----------  BP13C02N ------------*'.                      
           DISPLAY ' F725-READ       ' WS-F725-READ.                            
           DISPLAY ' F730-READ       ' WS-F730-READ.                            
           DISPLAY ' L02N-WRITE      ' WS-L02N-WRITE.                           
           CLOSE      BP13F725                                                  
                      BP13F730                                                  
                      BP13L02N.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
