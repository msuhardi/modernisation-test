      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C664.                                                 
       AUTHOR.        ELGER DE GUZMAN.                                          
      *DATE-WRITTEN.  03/09/10.                                                 
                                                                                
      * ============================================================== *        
      *                  SYSTEM OF COMMITMENT  (BP13)                  *        
      * ============================================================== *        
      *                                                                *        
      *   OBJECTIVE   :  1. PROGRAM WILL CREATE A GIRO LISTING         *        
      *                     OF GIRO SUBMITTED FOR PAYMENT OF MORTGAGE  *        
      *                     LOAN INSTALLMENTS FOR DBSS.                *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13K315  -  TRANS FILE                   *        
      *                  2.  BP13KD40  -  DBSS PAYMENT FILE            *        
      *                  3.  BP13K800  -  SOC MASTER FILE              *        
      *                                                                *        
      *   LISTINGS    :  1.  GIRO FORM PRINTED CASES LIST.             *        
      *                      (BP13L664)                                *        
      *                  2.  LETTER FORM.                              *        
      *                      (P13L664A)                                *        
      *                                                                *        
      *  CHGE-NO   BY    DATE   DESCRIPTION                            *        
      *  -------   ---  ------  -----------                            *        
      *  BP133781  ESD1 130910  NEW PGM.                               *        
      ******************************************************************        
                                                                                
      *-------------------------*                                               
       ENVIRONMENT DIVISION.                                                    
      *-------------------------*                                               
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-------------------------*                                               
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------*                                               
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F315  ASSIGN       TO BP13F315.                           
                                                                                
           SELECT BP13KD40  ASSIGN       TO BP13KD40                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KD40-KEY-FLD                        
                            FILE STATUS  IS WS-KD40-STATUS.                     
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13L664  ASSIGN       TO BP13L664.                           
           SELECT P13L664A  ASSIGN       TO P13L664A.                           
                                                                                
      *-------------------------*                                               
       DATA DIVISION.                                                           
      *-------------------------*                                               
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F315                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F315.                                                           
                                                                                
       FD   BP13KD40                                                            
            RECORD CONTAINS 1500 CHARACTERS.                                    
       COPY BP13KD40.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD  BP13L664                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L664-REC                     PIC X(132).                         
                                                                                
       FD  P13L664A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  P13L664A-REC                     PIC X(132).                         
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *-------------------------*                                               
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-F315-EOF                PIC X     VALUE 'N'.                  
           05  KD40-REC-FND               PIC X     VALUE 'N'.                  
           05  K800-REC-FND               PIC X     VALUE 'N'.                  
           05  WS-KD40-STATUS             PIC 9(2)  VALUE ZEROS.                
           05  WS-K800-STATUS             PIC 9(2)  VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F315                PIC 9(3)  VALUE ZEROS.                
           05  WS-PAGE-NO                 PIC 9(4)  VALUE ZEROS.                
           05  WS-CNT                     PIC 9(2)  VALUE ZEROS.                
           05  WS-SEQ-NO                  PIC 9(4)  VALUE ZEROS.                
           05  WS-CNT-PROC                PIC 9(3)  VALUE ZEROS.                
           05  WS-PRT-LINE                PIC 9(2)  VALUE ZEROS.                
           05  WS-CUR-DATE.                                                     
               09 WS-CUR-CCYY             PIC 9(4).                             
               09 WS-CUR-MM               PIC 9(2).                             
               09 WS-CUR-DD               PIC 9(2).                             
                                                                                
       01  L664-HEAD-1.                                                         
           05  FILLER                     PIC X(08)  VALUE 'BP13L664'.          
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  FILLER                     PIC X(19)  VALUE                      
                                         'S O C   S Y S T E M'.                 
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  L664-PRT-DATE              PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(06)  VALUE 'PAGE: '.            
           05  L664-NUM-PAGE              PIC ZZZ9.                             
                                                                                
       01  L664-HEAD-2.                                                         
           05  FILLER                     PIC X(26)  VALUE SPACES.              
           05  FILLER                     PIC X(72)  VALUE                      
               'DBSS CASES OPTED TO USE GIRO FOR SUBSEQUENT HOUSING INST        
      -     'ALMENT'.                                                           
       01  L664-HEAD-3.                                                         
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE 'S/N'.               
           05  FILLER                     PIC X(06)  VALUE SPACES.              
           05  FILLER                     PIC X(08)  VALUE 'REGN NO.'.          
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(04)  VALUE 'NAME'.              
           05  FILLER                     PIC X(46)  VALUE SPACES.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(11)  VALUE 'SCH A/C'.           
           05  FILLER                     PIC X(07)  VALUE SPACES.              
           05  FILLER                     PIC X(17)  VALUE                      
                'PP ORDER DATE   '.                                             
                                                                                
       01  L664-DETAIL.                                                         
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L664-SNO                   PIC ZZZ9.                             
           05  FILLER                     PIC X(05)  VALUE SPACES.              
           05  L664-REGN-NO               PIC X(08)  VALUE SPACES.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L664-NAME                  PIC X(50)  VALUE SPACES.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L664-SCH-ACC               PIC X(11)  VALUE SPACES.              
           05  FILLER                     PIC X(07)  VALUE SPACES.              
           05  L664-SALE-ORD-DTE          PIC X(10)  VALUE SPACES.              
                                                                                
       01  L664A-HEAD-1.                                                        
           05  FILLER                     PIC X(08)  VALUE 'P13L664A'.          
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  FILLER                     PIC X(19)  VALUE                      
                                         'S O C   S Y S T E M'.                 
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  L664A-PRT-DATE             PIC X(10)  VALUE SPACES.              
                                                                                
       01  L664A-LINE1.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(30)  VALUE                      
                'TO :    ACCOUNTANT'.                                           
                                                                                
       01  L664A-LINE2.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(30)  VALUE                      
                '        HAD FINANCE'.                                          
                                                                                
       01  L664A-LINE3.                                                         
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  FILLER                     PIC X(20)  VALUE                      
                ' PLEASE ACKNOWLEDGE' .                                         
           05  L664A-NO-APLN              PIC Z,ZZ9.                            
           05  FILLER                     PIC X(25)  VALUE                      
                ' GIRO APPLICATION FORMS'.                                      
                                                                                
       01  L664A-LINE4.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(35)  VALUE                      
                ' ________________________________'.                            
                                                                                
       01  L664A-LINE5.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(35)  VALUE                      
                ' FOR SALES UNIT (AGREEMENT TEAM)'.                             
                                                                                
       01  L664A-LINE6.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(30)  VALUE                      
                'TO :    EEO (SALES)'.                                          
                                                                                
       01  L664A-LINE7.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(30)  VALUE                      
                '        SALES UNIT'.                                           
                                                                                
       01  L664A-LINE8.                                                         
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  FILLER                     PIC X(31)  VALUE                      
                'WE ACKNOWLEDGE THE RECEIPT OF'.                                
           05  L664A-APLN-NO              PIC Z,ZZ9 .                           
           05  FILLER                     PIC X(28)  VALUE                      
                ' GIRO APPLICATION FORMS'.                                      
                                                                                
       01  L664A-LINE9.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(28)  VALUE                      
                '__________________________'.                                   
                                                                                
       01  L664A-LINE10.                                                        
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(28)  VALUE                      
                'HAD FINANCE '.                                                 
                                                                                
       01  L664A-LINE11.                                                        
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(10)  VALUE                      
                'DATE  : '.                                                     
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       100-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           PERFORM 200-OPEN-FILES              THRU    200-EXIT.                
           MOVE    60                          TO WS-PRT-LINE.                  
           PERFORM 300-READ-BP13F315           THRU    300-EXIT.                
           PERFORM 400-PRINT-REPORT            THROUGH 400-EXIT                 
                   UNTIL WS-F315-EOF = 'Y' OR F315-CDE-TRANS NOT = 'DG'.        
           PERFORM 500-PRINT-LETTER            THROUGH 500-EXIT                 
           PERFORM 1000-CLOSE-ROUTINE          THRU    1000-EXIT.               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-OPEN-FILES.                                                          
      ******************************************************************        
           OPEN INPUT   BP13F315                                                
                        BP13KD40                                                
                        BP13K800                                                
                OUTPUT  BP13L664                                                
                        P13L664A.                                               
                                                                                
            IF WS-KD40-STATUS NOT = 00 AND 97                                   
               DISPLAY '***********   ERROR MESSAGE   ***********'              
               DISPLAY 'ERROR ENCOUNTERED WHEN OPENING BP13KD40'                
               DISPLAY 'STATUS CODE = ' WS-KD40-STATUS                          
               PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                       
                                                                                
            IF WS-K800-STATUS NOT = 00 AND 97                                   
               DISPLAY '***********   ERROR MESSAGE   ***********'              
               DISPLAY 'ERROR ENCOUNTERED WHEN OPENING BP13K800'                
               DISPLAY 'STATUS CODE = ' WS-K800-STATUS                          
               PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-READ-BP13F315.                                                       
      ******************************************************************        
           READ BP13F315                                                        
                AT END MOVE 'Y' TO WS-F315-EOF                                  
                       GO TO 300-EXIT.                                          
                                                                                
           ADD  1  TO  WS-CNT-F315.                                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       400-PRINT-REPORT.                                                        
      ******************************************************************        
                                                                                
           MOVE SPACES TO KD40-KEY-FLD K800-NUM-REGN.                           
           MOVE F315-REGN-NO        TO   KD40-NUM-REGN  K800-NUM-REGN.          
           MOVE F315-CDE-SALES-TYPE TO   KD40-NUM-PP-SCHDL.                     
           PERFORM 460-READ-BP13K800 THRU 460-EXIT.                             
           PERFORM 450-READ-BP13KD40 THRU 450-EXIT.                             
                                                                                
           IF WS-PRT-LINE > 55   AND   KD40-REC-FND = 'Y'                       
              ADD   1                          TO  WS-PAGE-NO                   
              MOVE  WS-PAGE-NO                 TO  L664-NUM-PAGE                
              MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE                  
              STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                    
              DELIMITED BY SIZE  INTO L664-PRT-DATE                             
              MOVE L664-PRT-DATE   TO L664A-PRT-DATE                            
              WRITE BP13L664-REC FROM L664-HEAD-1 AFTER ADVANCING PAGE          
              WRITE BP13L664-REC FROM L664-HEAD-2 AFTER 1                       
              MOVE  ALL '-'      TO   BP13L664-REC                              
              WRITE BP13L664-REC AFTER ADVANCING 2                              
              WRITE BP13L664-REC FROM L664-HEAD-3 AFTER 1                       
              MOVE  ALL '-'      TO   BP13L664-REC                              
              WRITE BP13L664-REC AFTER ADVANCING 2                              
              MOVE  5            TO   WS-PRT-LINE                               
           END-IF.                                                              
                                                                                
           IF KD40-REC-FND = 'Y'                                                
              MOVE  SPACES                 TO  BP13L664-REC                     
              ADD      1                   TO  WS-SEQ-NO                        
                                               WS-CNT-PROC                      
              MOVE  WS-SEQ-NO              TO  L664-SNO                         
              MOVE  F315-REGN-NO           TO  L664-REGN-NO                     
              MOVE  KD40-NME-APPL1         TO  L664-NAME                        
              MOVE  KD40-NUM-SCH-ACC       TO  L664-SCH-ACC                     
              MOVE  K800-DTE-SALE-AGMT     TO  L664-SALE-ORD-DTE                
              WRITE BP13L664-REC  FROM L664-DETAIL AFTER 2                      
              ADD   2             TO  WS-PRT-LINE                               
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           PERFORM    300-READ-BP13F315      THROUGH  300-EXIT.                 
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       450-READ-BP13KD40.                                                       
      ******************************************************************        
                                                                                
            READ BP13KD40.                                                      
                                                                                
            EVALUATE WS-KD40-STATUS                                             
              WHEN 00                                                           
                   MOVE  'Y' TO KD40-REC-FND                                    
              WHEN 23                                                           
                   MOVE  'N' TO KD40-REC-FND                                    
                   DISPLAY 'REGISTRATION NOT FOUND ON BP13KD40 : '              
                            F315-REGN-NO                                        
              WHEN OTHER                                                        
                                                                                
                DISPLAY '***********   ERROR MESSAGE   ***********'             
                DISPLAY 'ERROR ENCOUNTERED WHEN READING BP13KD40'               
                DISPLAY 'STATUS CODE = ' WS-KD40-STATUS                         
                PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                       
                                                                                
            END-EVALUATE.                                                       
                                                                                
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       460-READ-BP13K800.                                                       
      ******************************************************************        
                                                                                
            READ BP13K800.                                                      
                                                                                
            EVALUATE WS-K800-STATUS                                             
              WHEN 00                                                           
                   MOVE  'Y' TO K800-REC-FND                                    
              WHEN 23                                                           
                   MOVE  'N' TO K800-REC-FND                                    
                   DISPLAY 'REGISTRATION NOT FOUND ON BP13K800 : '              
                            F315-REGN-NO                                        
              WHEN OTHER                                                        
                                                                                
                DISPLAY '***********   ERROR MESSAGE   ***********'             
                DISPLAY 'ERROR ENCOUNTERED WHEN READING BP13K800'               
                DISPLAY 'STATUS CODE = ' WS-K800-STATUS                         
                PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                       
                                                                                
            END-EVALUATE.                                                       
                                                                                
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-PRINT-LETTER.                                                        
      ******************************************************************        
           IF WS-SEQ-NO = ZEROES OR LOW-VALUES OR SPACES                        
              GO TO 500-EXIT                                                    
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE                     
           STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                       
           DELIMITED BY SIZE  INTO    L664A-PRT-DATE.                           
                                                                                
           MOVE   WS-SEQ-NO      TO   L664A-APLN-NO                             
                                      L664A-NO-APLN.                            
           MOVE   SPACES         TO   P13L664A-REC.                             
                                                                                
           WRITE    P13L664A-REC FROM L664A-HEAD-1 AFTER ADVANCING PAGE         
           MOVE     ALL '-'      TO   P13L664A-REC                              
           WRITE    P13L664A-REC AFTER ADVANCING 2                              
           WRITE    P13L664A-REC FROM L664A-LINE1 AFTER 8.                      
           WRITE    P13L664A-REC FROM L664A-LINE2.                              
                                                                                
           WRITE    P13L664A-REC FROM L664-HEAD-2 AFTER 3.                      
                                                                                
           WRITE    P13L664A-REC FROM L664A-LINE3 AFTER 3.                      
                                                                                
           WRITE    P13L664A-REC FROM L664A-LINE4 AFTER 4.                      
           WRITE    P13L664A-REC FROM L664A-LINE5.                              
                                                                                
           MOVE     ALL '='      TO   P13L664A-REC.                             
           WRITE    P13L664A-REC AFTER 4.                                       
                                                                                
           WRITE    P13L664A-REC FROM L664A-LINE6 AFTER 4.                      
           WRITE    P13L664A-REC FROM L664A-LINE7.                              
                                                                                
           WRITE    P13L664A-REC FROM L664-HEAD-2 AFTER 4.                      
                                                                                
           WRITE    P13L664A-REC FROM L664A-LINE8 AFTER 2.                      
                                                                                
           WRITE    P13L664A-REC FROM L664A-LINE9 AFTER 4.                      
           WRITE    P13L664A-REC FROM L664A-LINE10.                             
                                                                                
           WRITE    P13L664A-REC FROM L664A-LINE11 AFTER 2.                     
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
            DISPLAY ' '.                                                        
            IF WS-CNT-PROC > ZERO                                               
               MOVE SPACES      TO    BP13L664-REC                              
               STRING '                            '                            
                      'TOTAL NO. OF CASES LISTED : '                            
                       WS-CNT-PROC DELIMITED BY SIZE                            
                       INTO   BP13L664-REC                                      
               WRITE BP13L664-REC AFTER 2                                       
            END-IF.                                                             
            DISPLAY ' ***  BP13C664 CONTROL TOTALS  ***'.                       
            DISPLAY ' '.                                                        
            DISPLAY ' NO READ FROM F315 : ' WS-CNT-F315.                        
            DISPLAY ' NO CASES PRINTED  : ' WS-CNT-PROC.                        
                                                                                
            CLOSE BP13F315                                                      
                  BP13KD40                                                      
                  BP13K800                                                      
                  BP13L664                                                      
                  P13L664A.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
