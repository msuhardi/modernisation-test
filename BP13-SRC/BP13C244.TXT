       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C244.                                                  
       AUTHOR.       ANNALYN BANTA.                                             
       DATE-WRITTEN. 23.03.94.                                                  
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *  SYSTEM ID   : BP13                                      *              
      *  OBJECTIVE   : TO DELETE BKNG APPMT FILE, APPMT WKPLAN   *              
      *                FILE AND ALLOCN RESULT FILE               *              
      *               (BP13K200, BP13K210, BP13K222).            *              
      *                                                          *              
      *  BP13F205    : CONTROL FILE                              *              
      *  BP13K200    : BKNG APPMT FILE                           *              
      *  BP13K210    : BKAPPMT WKPLAN FILE                       *              
      *  BP13K222    : SUMMARY ALLOCN FILE                       *              
      *  BP13KQ00    : E-BKNG REGN & APPMT FILE                  *              
      *  BP13K816    : BTO DATA FILE                             *              
      *  BP13K060    : CODE FILE                                 *              
      *                                                          *              
      * CHG-NO  BY   DATE    DESCRIPTION                         *              
      * ------  ---  ------  -----------                         *              
      * N940009 LMS  030694  TO ADD ALLOC DATE TO BP13K222.      *              
      * SOC-PH9 GD   181194  TO CHECK FLAT TYPE WHEN DELETING    *              
      * JAR9297 GD   200595  TO CHANGE WS-REC-PTR TO 9(2).       *              
      *BP130067 LMS  191295  TO RE-COMPILE PGM DUE TO CHANGE IN  *              
      *                      FILE BP13K222.                      *              
      *BP131017 LMS  250501  TO REMOVE UPDATE TO BP13K222.       *              
      *BP133551 RB12 190509  ADD DELETION OF BP13KQ00            *              
      *BP133886 ESA1 260310  TO CATER FOR CHANGE IN KQ00-KEY-FLD *              
      *BP135281 ESA1 300514  TO REPLACE BP13K816 W/ BP13K813     *              
      *BP136836 ESA1 240817  TO CATER FOR 'RF' FLAT TYPE FOR ROF *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN       TO BP13F205.                            
           SELECT BP13F222 ASSIGN       TO BP13F222                             
                  FILE STATUS  IS WS-F222-STATUS.                               
                                                                                
           SELECT BP13K200 ASSIGN       TO BP13K200                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K200-KEY-FLD                         
                           FILE STATUS  IS WS-K200-STATUS.                      
                                                                                
           SELECT BP13K210 ASSIGN       TO BP13K210                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K210-KEY-FLD                         
                           FILE STATUS  IS WS-K210-STATUS.                      
                                                                                
           SELECT BP13K222 ASSIGN       TO BP13K222                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K222-KEY-FLD                         
                           FILE STATUS  IS WS-K222-STATUS.                      
                                                                                
           SELECT BP13KQ00 ASSIGN       TO BP13KQ00                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KQ00-KEY-FLD                         
                           FILE STATUS  IS WS-KQ00-STATUS.                      
                                                                                
           SELECT BP13KP00 ASSIGN       TO BP13KP00                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS KP00-KEY-FLD                         
                           FILE STATUS  IS WS-KP00-STATUS.                      
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-STATUS.                      
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS WS-K813-STATUS.                      
                                                                                
           SELECT BP13K060 ASSIGN       TO BP13K060                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K060-KEY-FLD                         
                           FILE STATUS  IS WS-K060-STATUS.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F205                                                             
           RECORD CONTAINS 80  CHARACTERS                                       
           BLOCK  CONTAINS 0   RECORDS                                          
           LABEL  RECORDS  ARE STANDARD                                         
           RECORDING MODE  IS  F.                                               
       COPY BP13F205.                                                           
                                                                                
       FD  BP13K200                                                             
           RECORD CONTAINS 300 CHARACTERS.                                      
       COPY BP13K200.                                                           
                                                                                
       FD  BP13K210                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K210.                                                           
                                                                                
       FD  BP13KQ00                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13KQ00.                                                           
                                                                                
       FD  BP13KP00                                                             
           RECORD CONTAINS 3800 CHARACTERS.                                     
       COPY BP13KP00.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS 25 CHARACTERS.                                       
       COPY BP13K060.                                                           
                                                                                
       FD  BP13K222                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K222.                                                           
                                                                                
       FD  BP13F222                                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           BLOCK  CONTAINS 0   RECORDS                                          
           LABEL  RECORDS  ARE STANDARD                                         
           RECORDING MODE  IS  F.                                               
       COPY BP13F222.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  FILE-STATUS.                                                         
           05  WS-K200-STATUS      PIC 9(2)    VALUE ZERO.                      
           05  WS-K210-STATUS      PIC 9(2)    VALUE ZERO.                      
           05  WS-KQ00-STATUS      PIC 9(2)    VALUE ZERO.                      
           05  WS-KP00-STATUS      PIC 9(2)    VALUE ZERO.                      
           05  WS-K800-STATUS      PIC 9(2)    VALUE ZERO.                      
           05  WS-K813-STATUS      PIC 9(2)    VALUE ZERO.                      
           05  WS-K060-STATUS      PIC 9(2)    VALUE ZERO.                      
           05  WS-K222-STATUS      PIC 9(2)    VALUE ZERO.                      
           05  WS-F222-STATUS      PIC 9(2)    VALUE ZERO.                      
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F205-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-K200-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-K210-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-K813-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-K060-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-K222-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-K800-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-KP00-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-KQ00-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-K200-DELETE      PIC 9(8)    VALUE ZERO.                      
           05  WS-K210-DELETE      PIC 9(8)    VALUE ZERO.                      
           05  WS-KQ00-DELETE      PIC 9(8)    VALUE ZERO.                      
           05  WS-KP00-DELETE      PIC 9(8)    VALUE ZERO.                      
           05  WS-F222-WRITE       PIC 9(8)    VALUE ZERO.                      
           05  WS-REC-PTR          PIC 9(2)    VALUE ZERO.                      
                                                                                
       01  WS-F205-SAVE-ARRAYS.                                                 
           05 WS-F205-INDEX            PIC S9(4) COMP.                          
           05 WS-F205-ARRAY            OCCURS 1  TO 99 TIMES                    
                                       DEPENDING ON WS-F205-INDEX.              
              10 WS-F205-ALLOCN-DTE    PIC X(8)    VALUE SPACES.                
              10 WS-F205-NT-ZONE       PIC X(3)    VALUE SPACES.                
              10 WS-F205-FLAT-TYPE     PIC X(2)    VALUE SPACES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  F205-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-F205                 VALUE 'Y'.                       
           05  K200-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-K200                 VALUE 'Y'.                       
           05  K210-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-K210                 VALUE 'Y'.                       
           05  KP00-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-KP00                 VALUE 'Y'.                       
           05  K222-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-K222                 VALUE 'Y'.                       
                                                                                
       01  WS-FLAT-TYPE-2          PIC X(02)   VALUE SPACES.                    
       01  WS-NEW-TOWN-3           PIC X(03)   VALUE SPACES.                    
       01  WS-FLAT-TYPE-9          PIC X(09)   VALUE SPACES.                    
       01  WS-NEW-TOWN-20          PIC X(20)   VALUE SPACES.                    
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN-LOGIC.                                                         
      *************************************************************             
           PERFORM 1000-OPEN-FILES  THRU 1000-EXIT.                             
           PERFORM 2000-READ-F205   THRU 2000-EXIT                              
                   UNTIL END-OF-F205.                                           
           PERFORM 3000-DELETE-K200 THRU 3000-EXIT.                             
           PERFORM 4000-DELETE-K210 THRU 4000-EXIT.                             
           PERFORM 5000-READ-K222   THRU 5000-EXIT.                             
           PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                             
                                                                                
      *************************************************************             
       1000-OPEN-FILES.                                                         
      *************************************************************             
           OPEN  INPUT BP13F205,                                                
                       BP13K060                                                 
                       BP13K222                                                 
                       BP13K813                                                 
                       BP13K800                                                 
                 I-O   BP13K200,                                                
                       BP13K210                                                 
                       BP13KP00                                                 
                       BP13KQ00                                                 
                OUTPUT BP13F222.                                                
                                                                                
           IF WS-K200-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR ON FILE BP13K200 : ' WS-K200-STATUS           
              MOVE WS-K200-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           IF WS-K210-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR ON FILE BP13K210 : ' WS-K210-STATUS           
              MOVE WS-K210-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           IF WS-KQ00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR ON FILE BP13KQ00 : ' WS-KQ00-STATUS           
              MOVE WS-KQ00-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           IF WS-KP00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR ON FILE BP13KP00 : ' WS-KP00-STATUS           
              MOVE WS-KP00-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR ON FILE BP13K800 : ' WS-K800-STATUS           
              MOVE WS-K800-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR ON FILE BP13K813 : ' WS-K813-STATUS           
              MOVE WS-K813-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR ON FILE BP13K060 : ' WS-K060-STATUS           
              MOVE WS-K060-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           IF WS-K222-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR ON FILE BP13K222 : ' WS-K222-STATUS           
              MOVE WS-K222-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           IF WS-F222-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR ON FILE BP13F222 : ' WS-F222-STATUS           
              MOVE WS-F222-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           MOVE ZEROES          TO WS-SWITCHES                                  
                                   WS-COUNTERS                                  
                                   WS-F205-INDEX.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2000-READ-F205.                                                          
      *************************************************************             
           READ BP13F205                                                        
             AT END                                                             
                MOVE 'Y' TO F205-EOF                                            
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F205-READ.                                               
           ADD 1 TO WS-REC-PTR.                                                 
           ADD 1 TO WS-F205-INDEX.                                              
                                                                                
           MOVE F205-DTE-ALLOCN     TO WS-F205-ALLOCN-DTE (WS-REC-PTR).         
           MOVE F205-NUM-NT-ZONE    TO WS-F205-NT-ZONE    (WS-REC-PTR).         
           MOVE F205-NUM-FLAT-TYPE  TO WS-F205-FLAT-TYPE  (WS-REC-PTR).         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3000-DELETE-K200.                                                        
      *************************************************************             
           MOVE SPACES TO K200-KEY-FLD.                                         
           START BP13K200 KEY > K200-KEY-FLD.                                   
                                                                                
           IF WS-K200-STATUS = 00                                               
              PERFORM 3100-READNEXT-K200 THRU 3100-EXIT                         
                UNTIL END-OF-K200                                               
           ELSE                                                                 
           IF WS-K200-STATUS = 23                                               
              DISPLAY 'RECORD NOT FOUND IN BP13K210 : ' K210-KEY-FLD            
              GO TO 3000-EXIT                                                   
           ELSE                                                                 
              MOVE WS-K200-STATUS     TO RETURN-CODE                            
              DISPLAY 'ERROR START ON BP13K200 : ' WS-K200-STATUS               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       3100-READNEXT-K200.                                                      
      *------------------*                                                      
           READ BP13K200 NEXT                                                   
                AT END                                                          
                   MOVE 'Y' TO K200-EOF                                         
                   GO 3100-EXIT.                                                
                                                                                
           ADD 1 TO WS-K200-READ.                                               
                                                                                
           PERFORM 3200-COMPARE-K200-F205 THRU 3200-EXIT                        
                   VARYING WS-REC-PTR FROM 1 BY 1                               
                     UNTIL WS-REC-PTR > WS-F205-INDEX.                          
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3200-COMPARE-K200-F205.                                                  
      *************************************************************             
           IF (K200-DTE-ALLOC     = WS-F205-ALLOCN-DTE(WS-REC-PTR)) AND         
              (K200-NUM-NT-ZONE   = WS-F205-NT-ZONE(WS-REC-PTR))    AND         
              (K200-NUM-FLAT-TYPE = WS-F205-FLAT-TYPE(WS-REC-PTR))              
               DELETE BP13K200                                                  
               IF WS-K200-STATUS = 00                                           
                  ADD 1            TO WS-K200-DELETE                            
                  PERFORM 6000-DELETE-BP13KQ00 THRU 6000-EXIT                   
                  PERFORM 7000-READ-BP13K800   THRU 7000-EXIT                   
               ELSE                                                             
                  MOVE WS-K200-STATUS TO RETURN-CODE                            
                  DISPLAY 'ERROR DELETING BP13K200 : ' WS-K200-STATUS           
                  PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                      
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4000-DELETE-K210.                                                        
      *************************************************************             
           MOVE SPACES TO K210-KEY-FLD.                                         
           START BP13K210 KEY > K210-KEY-FLD.                                   
                                                                                
           IF WS-K210-STATUS = 00                                               
              PERFORM 4100-READNEXT-K210 THRU 4100-EXIT                         
                UNTIL END-OF-K210                                               
           ELSE                                                                 
              IF WS-K210-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K210 : ' K210-KEY-FLD         
                 GO TO 4000-EXIT                                                
              ELSE                                                              
                 MOVE WS-K210-STATUS     TO RETURN-CODE                         
                 DISPLAY 'ERROR START ON BP13K210 : ' WS-K210-STATUS            
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4100-READNEXT-K210.                                                      
      *------------------*                                                      
           READ BP13K210 NEXT                                                   
                AT END                                                          
                   MOVE 'Y' TO K210-EOF                                         
                   GO 4100-EXIT.                                                
                                                                                
           ADD 1 TO WS-K210-READ.                                               
                                                                                
           PERFORM 4200-COMPARE-K210-F205 THRU 4200-EXIT                        
                   VARYING WS-REC-PTR FROM 1 BY 1                               
                     UNTIL WS-REC-PTR > WS-F205-INDEX.                          
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4200-COMPARE-K210-F205.                                                  
      *************************************************************             
           IF (K210-DTE-ALLOC     = WS-F205-ALLOCN-DTE(WS-REC-PTR)) AND         
              (K210-NUM-NT-ZONE   = WS-F205-NT-ZONE(WS-REC-PTR))    AND         
              (K210-NUM-FLAT-TYPE = WS-F205-FLAT-TYPE(WS-REC-PTR))              
               DELETE BP13K210                                                  
               IF WS-K210-STATUS = 00                                           
                  ADD 1 TO WS-K210-DELETE                                       
               ELSE                                                             
                  MOVE WS-K210-STATUS TO RETURN-CODE                            
                  DISPLAY 'ERROR DELETING BP13K210 : ' WS-K210-STATUS           
                  PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                      
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       5000-READ-K222.                                                          
      *************************************************************             
           MOVE SPACES TO K222-KEY-FLD.                                         
           START BP13K222 KEY > K222-KEY-FLD.                                   
                                                                                
           IF WS-K222-STATUS = 00                                               
              PERFORM 5100-READNEXT-K222 THRU 5100-EXIT                         
                UNTIL END-OF-K222                                               
           ELSE                                                                 
              IF WS-K222-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K222 : ' K222-KEY-FLD         
                 GO TO 5000-EXIT                                                
              ELSE                                                              
                 MOVE WS-K222-STATUS TO RETURN-CODE                             
                 DISPLAY 'ERROR START ON BP13K222 : ' WS-K222-STATUS            
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       5100-READNEXT-K222.                                                      
      *------------------*                                                      
           READ BP13K222 NEXT                                                   
                AT END                                                          
                   MOVE 'Y' TO K222-EOF                                         
                   GO 5100-EXIT.                                                
                                                                                
           ADD 1 TO WS-K222-READ.                                               
                                                                                
           PERFORM 5200-COMPARE-K222-F205 THRU 5200-EXIT                        
                   VARYING WS-REC-PTR FROM 1 BY 1                               
                     UNTIL WS-REC-PTR > WS-F205-INDEX.                          
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       5200-COMPARE-K222-F205.                                                  
      *************************************************************             
           IF (K222-DTE-ALLOC     = WS-F205-ALLOCN-DTE(WS-REC-PTR)) AND         
              (K222-NUM-NT-ZONE   = WS-F205-NT-ZONE(WS-REC-PTR))    AND         
              (K222-NUM-FLAT-TYPE = WS-F205-FLAT-TYPE(WS-REC-PTR))              
               WRITE BP13F222-REC FROM BP13K222-REC                             
               IF WS-F222-STATUS = 00                                           
                  ADD 1 TO WS-F222-WRITE                                        
               ELSE                                                             
                  MOVE WS-F222-STATUS TO RETURN-CODE                            
                  DISPLAY 'ERROR WRITING BP13F222 : ' WS-F222-STATUS            
                  PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                      
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6000-DELETE-BP13KQ00.                                                    
      *************************************************************             
           MOVE K200-CDE-PROJECT           TO   KQ00-NUM-ALLO-MODE2.            
           IF K200-CDE-PROJECT = 'BTO'                                          
              PERFORM 6100-READ-BP13K813   THRU 6100-EXIT                       
              MOVE K813-CDE-NT             TO   WS-NEW-TOWN-3                   
              MOVE K813-NUM-FLAT-TYPE      TO   WS-FLAT-TYPE-2                  
           ELSE                                                                 
              MOVE K200-NUM-NT-ZONE        TO   WS-NEW-TOWN-3                   
              MOVE K200-NUM-FLAT-TYPE      TO   WS-FLAT-TYPE-2                  
              IF WS-FLAT-TYPE-2 = 'SE'                                          
                 MOVE K200-NUM-ACTUAL-FT   TO   WS-FLAT-TYPE-2                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 6200-DECODE-NEW-TOWN    THRU 6200-EXIT.                      
                                                                                
           MOVE WS-NEW-TOWN-20             TO   KQ00-NUM-NEW-TOWN2.             
                                                                                
           PERFORM 6300-DECODE-FLAT-TYPE   THRU 6300-EXIT.                      
                                                                                
           MOVE WS-FLAT-TYPE-9             TO   KQ00-NUM-FLAT-TYPE2.            
                                                                                
           MOVE K200-DTE-BALLOT            TO   KQ00-DTE-BALLOT2.               
           MOVE K200-NUM-REGN              TO   KQ00-NUM-REGN.                  
                                                                                
           PERFORM 6400-READ-BP13KQ00      THRU 6400-EXIT.                      
                                                                                
           DELETE BP13KQ00.                                                     
                                                                                
           EVALUATE WS-KQ00-STATUS                                              
           WHEN  00                                                             
               ADD 1 TO WS-KQ00-DELETE                                          
           WHEN  23                                                             
               CONTINUE                                                         
           WHEN  OTHER                                                          
               DISPLAY 'ERROR IN DELETING KQ00 = ' WS-KQ00-STATUS               
               DISPLAY '      KQ00-KEY         = ' KQ00-KEY-FLD                 
               MOVE WS-KQ00-STATUS TO RETURN-CODE                               
               PERFORM 9000-CLOSE-FILES                                         
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6100-READ-BP13K813.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES             TO K813-KEY-FLD.                             
           MOVE K200-NUM-NT-ZONE   TO K813-NUM-ZONE.                            
           READ BP13K813 KEY IS K813-KEY-FLD.                                   
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD                                   
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    READ BP13K813 NEXT RECORD                                   
                    IF K813-NUM-ZONE = K200-NUM-NT-ZONE                         
                       ADD   1  TO WS-K813-READ                                 
                    ELSE                                                        
                       DISPLAY 'BP13K813 - NOT FND: ' K813-KEY-FLD              
                       INITIALIZE BP13K813-REC                                  
                       MOVE SPACES TO K813-CDE-NT                               
                                      K813-NUM-FLAT-TYPE                        
                    END-IF                                                      
               WHEN 23                                                          
                   DISPLAY 'RECORD NOT FOUND IN BP13K813:' K813-KEY-FLD         
                   MOVE SPACES TO K813-CDE-NT                                   
                                  K813-NUM-FLAT-TYPE                            
               WHEN OTHER                                                       
                   MOVE '4'  TO RETURN-CODE                                     
                   DISPLAY  'READ ERROR IN BP13K813 ' K813-KEY-FLD              
                   GO        TO 9000-CLOSE-FILES                                
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       6200-DECODE-NEW-TOWN.                                                    
      *************************************************************             
                                                                                
           MOVE SPACES             TO K060-KEY-FLD.                             
           MOVE 02                 TO K060-SERIAL-NO.                           
           MOVE WS-NEW-TOWN-3      TO K060-CODE.                                
                                                                                
           READ BP13K060 KEY IS K060-KEY-FLD.                                   
                                                                                
           IF WS-K060-STATUS = 0                                                
              MOVE K060-DESC TO WS-NEW-TOWN-20                                  
              ADD   1  TO WS-K060-READ                                          
           ELSE                                                                 
           IF WS-K060-STATUS = 23                                               
              MOVE SPACES    TO WS-NEW-TOWN-20                                  
              DISPLAY 'ESTATE NOT FOUND IN BP13K060:' K060-KEY-FLD              
           ELSE                                                                 
           IF WS-K060-STATUS NOT = 0                                            
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'READ ERROR IN BP13K060 ' K060-KEY-FLD                   
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6300-DECODE-FLAT-TYPE.                                                   
      *************************************************************             
                                                                                
           EVALUATE WS-FLAT-TYPE-2(1:1)                                         
              WHEN '1'                                                          
                 IF WS-FLAT-TYPE-2(2:1) = 'A'                                   
                    MOVE 'SA       ' TO WS-FLAT-TYPE-9                          
                 ELSE                                                           
                    MOVE '1-ROOM   ' TO WS-FLAT-TYPE-9                          
                 END-IF                                                         
              WHEN '2'                                                          
                 IF WS-FLAT-TYPE-2(2:1) = 'A'                                   
                    MOVE 'SA       ' TO WS-FLAT-TYPE-9                          
                 ELSE                                                           
                    MOVE '2-ROOM   ' TO WS-FLAT-TYPE-9                          
                 END-IF                                                         
              WHEN '3'                                                          
                 MOVE '3-ROOM   '    TO WS-FLAT-TYPE-9                          
              WHEN '4'                                                          
                 MOVE '4-ROOM   '    TO WS-FLAT-TYPE-9                          
              WHEN '5'                                                          
                 MOVE '5-ROOM   '    TO WS-FLAT-TYPE-9                          
              WHEN 'E'                                                          
                 MOVE 'EXECUTIVE'    TO WS-FLAT-TYPE-9                          
              WHEN 'R'                                                          
                 MOVE 'RF       '    TO WS-FLAT-TYPE-9                          
              WHEN OTHER                                                        
                 MOVE SPACES         TO WS-FLAT-TYPE-9                          
                 DISPLAY 'ERROR - FLAT TYPE NOT SUPPORTED: '                    
                                        WS-FLAT-TYPE-2                          
           END-EVALUATE.                                                        
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6400-READ-BP13KQ00.                                                      
      *************************************************************             
                                                                                
           READ BP13KQ00 KEY IS KQ00-KEY-FLD.                                   
                                                                                
           IF WS-KQ00-STATUS = 0                                                
              ADD   1  TO WS-KQ00-READ                                          
           ELSE                                                                 
           IF WS-KQ00-STATUS = 23                                               
              DISPLAY 'RECORD NOT FOUND IN BP13KQ00:' KQ00-KEY-FLD              
           ELSE                                                                 
           IF WS-KQ00-STATUS NOT = 0                                            
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'READ ERROR IN BP13KQ00 ' KQ00-KEY-FLD                   
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       6400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       7000-READ-BP13K800.                                                      
      *************************************************************             
                                                                                
           MOVE KQ00-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           IF WS-K800-STATUS = 0                                                
              ADD   1  TO WS-K800-READ                                          
              IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES OR ZEROES              
                 PERFORM 7500-DELETE-BP13KP00 THRU 7500-EXIT                    
              END-IF                                                            
           ELSE                                                                 
           IF WS-K800-STATUS = 23                                               
              DISPLAY 'RECORD NOT FOUND IN BP13K800:' K800-NUM-REGN             
           ELSE                                                                 
           IF WS-K800-STATUS NOT = 0                                            
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'READ ERROR IN BP13K800 ' K800-NUM-REGN                  
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       7500-DELETE-BP13KP00.                                                    
      *************************************************************             
           MOVE SPACES TO KP00-KEY-FLD.                                         
           MOVE 'N'    TO KP00-EOF.                                             
           MOVE KQ00-NUM-REGN TO KP00-NUM-REGN.                                 
           START BP13KP00 KEY > KP00-KEY-FLD.                                   
                                                                                
           IF WS-KP00-STATUS = 00                                               
              PERFORM 7510-READNEXT-KP00 THRU 7510-EXIT                         
                UNTIL END-OF-KP00                                               
           ELSE                                                                 
              IF WS-KP00-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13KP00 : ' KP00-KEY-FLD         
              ELSE                                                              
                 MOVE WS-KP00-STATUS     TO RETURN-CODE                         
                 DISPLAY 'ERROR START ON BP13KP00 : ' WS-KP00-STATUS            
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                       
                                                                                
       7500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       7510-READNEXT-KP00.                                                      
      *************************************************************             
           READ BP13KP00 NEXT                                                   
                AT END                                                          
                   MOVE 'Y' TO KP00-EOF                                         
                   GO 7510-EXIT.                                                
                                                                                
           IF KP00-NUM-REGN = KQ00-NUM-REGN                                     
              ADD 1 TO WS-KP00-READ                                             
              PERFORM 7520-DELETE-BP13KP00 THRU 7520-EXIT                       
           ELSE                                                                 
              MOVE 'Y' TO KP00-EOF                                              
           END-IF.                                                              
                                                                                
       7510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       7520-DELETE-BP13KP00.                                                    
      *************************************************************             
           DELETE BP13KP00.                                                     
                                                                                
           IF WS-KP00-STATUS = 00                                               
              ADD 1 TO WS-KP00-DELETE                                           
           ELSE                                                                 
           IF WS-KP00-STATUS = 23                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE WS-KP00-STATUS TO RETURN-CODE                                
              DISPLAY 'ERROR DELETING BP13KP00 : ' WS-KP00-STATUS               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
       7520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-FILES.                                                        
      *************************************************************             
           DISPLAY '***************** BP13C244 *****************'.              
           DISPLAY 'TOTAL RECS READ    FROM BP13F205 = ' WS-F205-READ.          
           DISPLAY 'TOTAL RECS READ    FROM BP13K200 = ' WS-K200-READ.          
           DISPLAY 'TOTAL RECS DELETED FROM BP13K200 = ' WS-K200-DELETE.        
           DISPLAY 'TOTAL RECS READ    FROM BP13K210 = ' WS-K210-READ.          
           DISPLAY 'TOTAL RECS DELETED FROM BP13K210 = ' WS-K210-DELETE.        
           DISPLAY 'TOTAL RECS READ    FROM BP13K813 = ' WS-K813-READ.          
           DISPLAY 'TOTAL RECS READ    FROM BP13K060 = ' WS-K060-READ.          
           DISPLAY 'TOTAL RECS READ    FROM BP13K800 = ' WS-K800-READ.          
           DISPLAY 'TOTAL RECS READ    FROM BP13KQ00 = ' WS-KQ00-READ.          
           DISPLAY 'TOTAL RECS DELETED FROM BP13KQ00 = ' WS-KQ00-DELETE.        
           DISPLAY 'TOTAL RECS READ    FROM BP13KP00 = ' WS-KP00-READ.          
           DISPLAY 'TOTAL RECS DELETED FROM BP13KP00 = ' WS-KP00-DELETE.        
           DISPLAY 'TOTAL RECS READ    FROM BP13K222 = ' WS-K222-READ.          
           DISPLAY 'TOTAL RECS WRITTEN TO   BP13F222 = ' WS-F222-WRITE.         
                                                                                
           CLOSE BP13F205                                                       
                 BP13F222                                                       
                 BP13K060                                                       
                 BP13K200                                                       
                 BP13K210                                                       
                 BP13K800                                                       
                 BP13KP00                                                       
                 BP13KQ00                                                       
                 BP13K813                                                       
                 BP13K222.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
