*===============================================================                
*                      PROGRAM PROFILE                                          
*===============================================================                
* APPLICATION        : SOC                                                      
* PROGRAM ID         : BP13S534                                                 
* JCL                : BP13A512                                                 
* AUTHOR (PROFILE)   : IL5              DATE : 15 JUN 2007                      
*                                                                               
*----------------------------------------------------------------               
* PURPOSE OF PROGRAM :                                                          
* ------------------                                                            
*    PRINT REPORT ON FLAT APPLICATIONS WITH RESALE STATUS = 'SN'                
*                                                                               
*----------------------------------------------------------------               
* MODIFICATION LOG                                                              
* ----------------                                                              
*  MODIFY  DATE     CHGE REQ  DESCRIPTION OF CHANGE                             
*  ------ ------    --------  ---------------------                             
* IL5    30/01/08  BP133254  ADD ZONE                                           
*                                                                               
*===============================================================                
*                     RELATED DOCUMENTS                                         
*===============================================================                
* SCREEN LAYOUT   : NIL                                                         
*                                                                               
* FILE LAYOUT     : BP13.BATCH.COPYCOB(BP13K740)                                
*                   BP13.BATCH.COPYCOB(BP13K800)                                
*                                                                               
* DATA DEFINITION : NIL                                                         
*                                                                               
* PROCESSING RULE : NIL                                                         
*                                                                               
* PHYSICAL FLOW   :                                                             
*                                                                               
* MESSAGE FILE    : NIL                                                         
*                                                                               
* SUBROUTINE USED : NIL                                                         
*                                                                               
*===============================================================;               
OPTIONS SORTPGM=BEST;                                                           
                                                                                
*---------------------------------------------------------------                
* TO READ IN DATA FROM INFILE BP13F510 AND BP13FD11                             
*---------------------------------------------------------------;               
                                                                                
DATA K740;                                                                      
     INFILE BP13K740;                                                           
     INPUT @123 REGN     $CHAR8.;                                               
                                                                                
PROC SORT DATA=K740 ;                                                           
          BY REGN   ;                                                           
                                                                                
     DATA K800;                                                                 
     INFILE BP13K800;                                                           
         INPUT @1   REGN     $CHAR8.                                            
               @09  FLATTYP  $CHAR1.                                            
               @10  NT       $CHAR3.                                            
               @13  HHCAT    $CHAR1.                                            
               @14  NRIC1    $CHAR9.                                            
               @23  NRIC2    $CHAR9.                                            
               @32  HHTYP    $CHAR1.                                            
               @33  QUEUE    $CHAR5.                                            
               @38  BALDTE   $CHAR6.                                            
               @44  ALLOCAT  $CHAR3.                                            
               @47  LOANTAG  $CHAR1.                                            
               @48  ZONE     $CHAR2.;                                           
                                                                                
               IF NT = ' ' THEN                                                 
                  ESTATE = ZONE;                                                
               ELSE                                                             
                  ESTATE = NT;                                                  
                                                                                
PROC SORT DATA=K800 ;                                                           
     BY REGN;                                                                   
                                                                                
DATA OUT1;                                                                      
     MERGE K740(IN=A) K800(IN=B);                                               
     BY REGN;                                                                   
     IF A AND B;                                                                
                                                                                
*---------------------------------------------------------------                
* PRINT ( BP13-L534 )                                                           
*---------------------------------------------------------------;               
                                                                                
PROC SORT DATA=OUT1;                                                            
    BY ESTATE FLATTYP QUEUE;                                                    
                                                                                
DATA _NULL_;                                                                    
     FILE BP13L534 HEADER=HEADING1 NOTITLES LINESLEFT=LL;                       
     IF TOTAL = 0 AND EOF THEN DO;                                              
        PUT //// @45 '****  END OF REPORT ****';                                
     END;                                                                       
                                                                                
     SET OUT1 END=EOF NOBS=TOTAL;                                               
     BY ESTATE FLATTYP QUEUE;;                                                  
                                                                                
     IF FIRST.ESTATE OR FIRST.FLATTYP AND _N_ > 1 THEN DO;                      
        PUT _PAGE_;                                                             
     END;                                                                       
                                                                                
     PUT   @01   REGN     $CHAR8.                                               
           @14   NRIC1    $CHAR9.                                               
           @26   NRIC2    $CHAR9.                                               
           @39   BALDTE   $CHAR6.                                               
           @54   QUEUE    $CHAR5.                                               
           @68   ALLOCAT  $CHAR3.                                               
           @82   HHTYP    $CHAR1.                                               
           @92   HHCAT    $CHAR1.                                               
           @102  LOANTAG  $CHAR1.;                                              
                                                                                
                                                                                
     IF EOF THEN                                                                
        PUT //@45 '**** END OF REPORT (' PAGENO1 ' PAGES) ****';                
                                                                                
RETURN;                                                                         
                                                                                
HEADING1:                                                                       
   CURRENT = TODAY();                                                           
   PAGENO1 + 1;                                                                 
                                                                                
   PUT  @1   'BP13-L534'                                                        
        @14  'HDB3'                                                             
        @48  'SYSTEM OF COMMITMENT'                                             
        @98  'DATE: ' CURRENT DDMMYY10.                                         
        @120 'PAGE: ' PAGENO1                                                   
       /@37  "LIST OF FLAT APPLICATIONS WITH RESALE STATUS = 'SN'"              
                                                                                
      //@1  'NEW TOWN  : ' ESTATE $CHAR3.                                       
                                                                                
      /@01  'FLAT TYPE : ' FLATTYP $CHAR1.                                      
                                                                                
      //@01  'REG NO.'                                                          
        @14  'NRIC1'                                                            
        @26  'NRIC2'                                                            
        @39  'BALLOT DATE'                                                      
        @54  'QUEUE NO.'                                                        
        @67  'ALLO CAT'                                                         
        @79  'HH TYPE'                                                          
        @90  'HH CAT'                                                           
        @100 'LOAN TAG'                                                         
       /@1   132*'-';                                                           
                                                                                
RETURN;                                                                         
                                                                                
                                                                                
