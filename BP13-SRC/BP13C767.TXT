       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C767.                                                    
      *AUTHOR. IMC.                                                             
      *DATE-WRITTEN. 24/07/13.                                                  
      * ========================================================== *    00050020
      * SYSTEM OF COMMITMENT  (BP13)                               *    00060020
      * ========================================================== *    00070020
      *                                                            *    00080020
      *  OBJECTIVE   : PROGRAM TO MATCH SOC APPLICATION WITH DATA  *    00090020
      *                EXTRACTED FROM RDB. MATCH BY REGN/NRIC.     *    00100020
      *                                                            *    00120020
      *  INPUT FILES : 1.  BP13F558  -  SOC RECORD                 *    00130020
      *                2.  BP13F716  -  RDB RECORD                 *    00140020
      *  OUTPUT      : 3.  OUTPUT MACTHED REGN/NRIC                *    00130020
      *                                                            *    00240020
      * ---------------------------------------------------------- *    00250020
      * CHG REF  BY    DATE    DESCRIPTION                         *    00260020
      * -------- ---  ------   -----------                         *    00270020
      * BP135002 IMC  20130725 NEW PROGRAM.                        *    00280020
      * ========================================================== *    00682020
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
                                                                                
           SELECT P13F716A ASSIGN TO P13F716A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F716       BLOCK  CONTAINS 0 RECORDS                             
                          RECORD CONTAINS 350 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F716.                                                           
                                                                                
       FD  BP13F558       BLOCK  CONTAINS 0 RECORDS                             
                          RECORD CONTAINS 600 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F558.                                                           
                                                                                
       FD   P13F716A      RECORD CONTAINS 350 CHARACTERS                        
                          BLOCK CONTAINS 0 RECORDS                              
                          RECORDING MODE IS F                                   
                          LABEL RECORDS ARE STANDARD.                           
       01   P13F716A-REC           PIC X(350).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F716-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-MATCH           PIC 9(8)    VALUE ZERO.                      
       01  WS-F716A-WRITE          PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-KEY.                                                         
           05  WS-F716-REGN        PIC X(9)    VALUE SPACES.                    
           05  WS-F716-NRIC        PIC X(9)    VALUE SPACES.                    
       01  WS-F558-KEY.                                                         
           05  WS-F558-REGN        PIC X(9)    VALUE SPACES.                    
           05  WS-F558-NRIC        PIC X(9)    VALUE SPACES.                    
                                                                                
       01  WS-EOF-F716             PIC X       VALUE SPACE.                     
       01  WS-EOF-F558             PIC X       VALUE SPACE.                     
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-LOGIC.                                                         
           PERFORM 1100-INITIALIZATION  THRU 1100-EXIT.                         
           PERFORM 2000-READ-F716       THRU 2000-EXIT.                         
           PERFORM 3000-READ-F558       THRU 3000-EXIT.                         
           PERFORM 1000-PROCESS         THRU 1000-EXIT                          
             UNTIL WS-EOF-F716 = 'Y' OR                                         
                   WS-EOF-F558 = 'Y'.                                           
           PERFORM 9000-FINALIZATION    THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1100-INITIALIZATION.                                                     
           OPEN INPUT  BP13F716                                                 
                       BP13F558                                                 
                OUTPUT P13F716A.                                                
           MOVE 'N' TO WS-EOF-F716.                                             
           MOVE 'N' TO WS-EOF-F558.                                             
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH SEQ-NO AND JB-PARENT-REGN                              *         
      *****************************************************************         
       1000-PROCESS.                                                            
           IF WS-F716-KEY = WS-F558-KEY                                         
              ADD 1 TO WS-F558-MATCH                                            
                                                                                
              PERFORM 4000-OUTOUT-P13F716A THRU 4000-EXIT                       
                UNTIL WS-F558-KEY > WS-F716-KEY                                 
                   OR WS-EOF-F558 = 'Y'                                         
                                                                                
              PERFORM 2000-READ-F716       THRU 2000-EXIT                       
           ELSE                                                                 
              IF WS-F716-KEY < WS-F558-KEY                                      
                 PERFORM 2000-READ-F716    THRU 2000-EXIT                       
              ELSE                                                              
                 PERFORM 3000-READ-F558    THRU 3000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F716.                                                          
           READ BP13F716 AT END MOVE 'Y' TO WS-EOF-F716                         
                MOVE HIGH-VALUES TO F716-NUM-REGN                               
                GO TO 2000-EXIT.                                                
           ADD 1 TO WS-F716-READ.                                               
                                                                                
           MOVE F716-NUM-REGN     TO WS-F716-REGN                               
           MOVE F716-NUM-NRIC     TO WS-F716-NRIC.                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-F558.                                                          
           READ BP13F558 AT END MOVE 'Y' TO WS-EOF-F558                         
                MOVE HIGH-VALUES TO WS-F558-KEY                                 
                GO TO 3000-EXIT.                                                
           ADD 1 TO WS-F558-READ.                                               
                                                                                
           MOVE F558-NUM-REGN     TO WS-F558-REGN                               
           MOVE F558-NUM-NRIC     TO WS-F558-NRIC.                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-OUTOUT-P13F716A.                                                    
                                                                                
           IF F716-NUM-HDB-REF  =  F558-NUM-HDB-REF-SMS                         
              MOVE F558-NUM-APPLN  TO F716-NUM-APPLN                            
              MOVE BP13F716-REC    TO P13F716A-REC                              
              WRITE P13F716A-REC                                                
              ADD 1 TO WS-F716A-WRITE                                           
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-F558  THRU 3000-EXIT.                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-FINALIZATION.                                                       
           DISPLAY '*-----------  BP13C767  ------------*'.                     
           DISPLAY ' F716-READ       ' WS-F716-READ.                            
           DISPLAY ' F558-READ       ' WS-F558-READ.                            
           DISPLAY ' F558-MATCH      ' WS-F558-MATCH.                           
           DISPLAY ' F716A-WRITE     ' WS-F716A-WRITE.                          
                                                                                
           CLOSE     BP13F558                                                   
                     BP13F716                                                   
                     P13F716A.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
