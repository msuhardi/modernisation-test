       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C65A.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  26 MARCH 2009.                                            
      **********************************************************                
      * OBJECTIVE    :                                         *                
      *  TO DELETE REFUND CASES BASED ON RETURN FILES FROM AG07*                
      *                                                        *                
      * INPUT FILE :                                           *                
      *     1. AG07.F235.EGPVR.RETURN.SOC                      *                
      *     1. AG07.F237.EGPVRTX.RETURN.SOC                    *                
      *     1. AG07.F630.SOC                                   *                
      * I-O   FILE :                                           *                
      *     1. BP13T.K650.PREPVRCH (BP13K650)                  *                
      * REPORT :                                               *                
      *     1. BP13L65A   - ERROR LISTING                      *                
      *                                                        *                
      * REVISION HISTORY :                                     *                
      *--------------------------------------------------------*                
      * CHG-NO   BY  DATE    DESCRIPTION                       *                
      *--------------------------------------------------------*                
      * BP133172 JB8 260309  NEW PROGRAM.                      *                
      * BP135616 JGO1090315  CHANGE LENGTH OF F650/K650 TO 500 *                
      * BP135832 JGO1290615  CHANGE LENGTH OF F245 TO 700      *                
      **********************************************************                
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT    DIVISION.                                                 
      *-----------------------*                                                 
       CONFIGURATION  SECTION.                                                  
      *-----------------------*                                                 
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F650 ASSIGN       TO BP13F650.                            
           SELECT AG07F235 ASSIGN       TO AG07F235.                            
           SELECT BP13K650 ASSIGN       TO BP13K650                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS DYNAMIC                               
                           RECORD KEY IS K650-KEY-FLD                           
                           FILE STATUS IS WS-K650-STATUS.                       
           SELECT BP13L65A ASSIGN       TO BP13L65A.                            
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
       FD   AG07F235                                                            
            BLOCK  CONTAINS    0 RECORDS                                        
            RECORD CONTAINS  700 CHARACTERS                                     
            LABEL  RECORDS STANDARD.                                            
       COPY AG07F235.                                                           
                                                                                
       FD   BP13F650                                                            
            BLOCK  CONTAINS    0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            LABEL  RECORDS STANDARD.                                            
       01   BP13F650-REC     PIC X(500).                                        
                                                                                
       FD   BP13K650                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K650.                                                           
                                                                                
       FD   BP13L65A                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS  OMITTED.                                             
       01   BP13L65A-REC       PIC X(132).                                      
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  WS-VARIABLES.                                                        
           05  WS-EOF-F235         PIC X(1)      VALUE 'N'.                     
           05  WS-K650-STATUS      PIC 99        VALUE ZEROES.                  
           05  WS-COUNT            PIC 9(5)      VALUE ZERO.                    
           05  WS-F235-READ        PIC 9(5)      VALUE ZERO.                    
           05  WS-K650-READ        PIC 9(5)      VALUE ZERO.                    
           05  WS-K650-DELETE      PIC 9(5)      VALUE ZERO.                    
           05  WS-F650-WRITE       PIC 9(5)      VALUE ZERO.                    
           05  WS-LINE             PIC 9(2)      VALUE 61.                      
           05  WS-PAGE             PIC 9(4)      VALUE ZEROES.                  
                                                                                
       01  WS-K650-FND             PIC X(01)     VALUE 'N'.                     
           88  K650-FND            VALUE 'Y'.                                   
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CC          PIC X(2)      VALUE SPACES.                  
           05  WS-CURR-YY          PIC X(2)      VALUE SPACES.                  
           05  WS-CURR-MM          PIC X(2)      VALUE SPACES.                  
           05  WS-CURR-DD          PIC X(2)      VALUE SPACES.                  
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD        PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X         VALUE '/'.                     
           05  WS-EDITED-MM        PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X         VALUE '/'.                     
           05  WS-EDITED-CC        PIC X(2)      VALUE SPACES.                  
           05  WS-EDITED-YY        PIC X(2)      VALUE SPACES.                  
                                                                                
      *----------------------------------------*                                
      *   FORMAT OF THE ERROR   LISTING        *                                
      *----------------------------------------*                                
                                                                                
       01  WS-L65A-HDG1.                                                        
           05  FILLER              PIC X(8)      VALUE 'BP13L65A'.              
           05  FILLER              PIC X(39)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
           'S Y S T E M   O F   C O M M I T M E N T'.                           
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE : '.               
           05  L65A-DATE           PIC X(10).                                   
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L65A-PAGE           PIC 9(3).                                    
                                                                                
       01  WS-L65A-HDG2.                                                        
           05  FILLER              PIC X(44)     VALUE SPACES.                  
           05  FILLER              PIC X(45)     VALUE                          
                 'ERROR REPORT FOR FILE BP13K650 WHILE DELETION'.               
                                                                                
       01  WS-L65A-HDG3.                                                        
           05  FILLER              PIC X         VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'REGN    '.              
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'REMARK'.                
                                                                                
       01  WS-L65A-DTL.                                                         
           05  FILLER              PIC X         VALUE SPACES.                  
           05  L65A-REGNO          PIC X(8).                                    
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  L65A-REMARK         PIC X(30).                                   
                                                                                
                                                                                
      *-----------------------*                                                 
       PROCEDURE      DIVISION.                                                 
      *-----------------------*                                                 
                                                                                
      *--------------*                                                          
       0000-CONTROL.                                                            
      *--------------*                                                          
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-F235      THRU 2000-EXIT.                          
           PERFORM 3000-MAIN-ROUTINE   THRU 3000-EXIT                           
             UNTIL WS-EOF-F235 = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       1000-OPEN-FILES.                                                         
      *---------------*                                                         
                                                                                
           OPEN INPUT AG07F235                                                  
                  I-O BP13K650                                                  
               OUTPUT BP13F650                                                  
                      BP13L65A.                                                 
                                                                                
           IF WS-K650-STATUS NOT = 0 AND 97                                     
              DISPLAY 'BP13K650 - ERROR OPENING : ' WS-K650-STATUS              
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       2000-READ-F235.                                                          
      *--------------*                                                          
                                                                                
           READ AG07F235                                                        
             AT END                                                             
                MOVE  'Y'   TO WS-EOF-F235                                      
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F235-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       2500-READ-K650.                                                          
      *--------------*                                                          
                                                                                
           MOVE 'N'                TO WS-K650-FND.                              
           MOVE F235-NUM-REG       TO K650-KEY-FLD.                             
                                                                                
           READ BP13K650.                                                       
                                                                                
           IF WS-K650-STATUS = 00                                               
              MOVE 'Y'             TO WS-K650-FND                               
              ADD 1                TO WS-K650-READ                              
           ELSE                                                                 
              IF WS-K650-STATUS = 23                                            
                 MOVE 'RECORD NOT FOUND IN BP13K650  ' TO L65A-REMARK           
                 PERFORM 5000-ERROR-LISTING          THRU 5000-EXIT             
              ELSE                                                              
                 DISPLAY 'BP13K650 - ERROR READING : ' WS-K650-STATUS           
                         ' K650-NUM-REGN ' K650-NUM-REGN                        
                 MOVE WS-K650-STATUS                   TO RETURN-CODE           
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3000-MAIN-ROUTINE.                                                       
      *-------------------*                                                     
                                                                                
           IF F235-NUM-REGN = SPACES OR LOW-VALUES                              
              PERFORM 2000-READ-F235    THRU 2000-EXIT                          
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 2500-READ-K650       THRU 2500-EXIT.                         
           IF K650-FND                                                          
              PERFORM 4500-WRITE-F650   THRU 4500-EXIT                          
              PERFORM 4000-DELETE-K650  THRU 4000-EXIT                          
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F235       THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4000-DELETE-K650.                                                        
      *-------------------*                                                     
                                                                                
           DELETE BP13K650.                                                     
                                                                                
           IF WS-K650-STATUS = 00                                               
              ADD 1                  TO WS-K650-DELETE                          
           ELSE                                                                 
              DISPLAY 'BP13K650 - ERROR DELETING : ' WS-K650-STATUS             
                      ' REGN : ' K650-NUM-REGN                                  
              MOVE WS-K650-STATUS    TO RETURN-CODE                             
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4500-WRITE-F650.                                                         
      *-------------------*                                                     
                                                                                
           MOVE BP13K650-REC         TO BP13F650-REC.                           
           WRITE BP13F650-REC.                                                  
           ADD 1                     TO WS-F650-WRITE.                          
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5000-ERROR-LISTING.                                                      
      *-------------------*                                                     
                                                                                
           ADD 1                      TO WS-LINE                                
           IF WS-LINE > 60                                                      
              PERFORM 6000-PRINT-HEADING THRU 6000-EXIT                         
              ADD 1                   TO WS-LINE                                
           END-IF.                                                              
                                                                                
           MOVE F235-NUM-REGN         TO L65A-REGNO.                            
           WRITE BP13L65A-REC FROM WS-L65A-DTL AFTER 1.                         
           ADD 1 TO WS-COUNT.                                                   
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       6000-PRINT-HEADING.                                                      
      *-------------------*                                                     
                                                                                
           ADD     1               TO   WS-PAGE.                                
           MOVE    WS-PAGE         TO   L65A-PAGE.                              
                                                                                
           MOVE   FUNCTION CURRENT-DATE(1:8) TO WS-CURR-DATE.                   
                                                                                
           MOVE    WS-CURR-CC      TO WS-EDITED-CC.                             
           MOVE    WS-CURR-YY      TO WS-EDITED-YY.                             
           MOVE    WS-CURR-MM      TO WS-EDITED-MM.                             
           MOVE    WS-CURR-DD      TO WS-EDITED-DD.                             
           MOVE    WS-EDITED-DATE  TO L65A-DATE.                                
                                                                                
           WRITE   BP13L65A-REC FROM    WS-L65A-HDG1 AFTER PAGE.                
           WRITE   BP13L65A-REC FROM    WS-L65A-HDG2 AFTER 1.                   
           WRITE   BP13L65A-REC FROM    WS-L65A-HDG3 AFTER 2.                   
           MOVE    SPACES          TO   BP13L65A-REC.                           
           WRITE   BP13L65A-REC                   AFTER  1.                     
           MOVE    5               TO   WS-LINE.                                
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       9000-CLOSE-ROUTINE.                                                      
      *-------------------*                                                     
                                                                                
           DISPLAY  'PROGRAM ID  :  BP13C65A'.                                  
           DISPLAY  ' '.                                                        
           DISPLAY                                                              
              'NO OF RECORDS READ FROM AG07F235    = ' WS-F235-READ.            
           DISPLAY                                                              
              'NO OF RECORDS READ FROM BP13K650    = ' WS-K650-READ.            
           DISPLAY                                                              
              'NO OF RECORDS DELETED FROM BP13K650 = ' WS-K650-DELETE.          
           DISPLAY                                                              
              'NO OF RECORDS WRITTEN TO   BP13F650 = ' WS-F650-WRITE.           
           DISPLAY                                                              
              'NO OF ERROR RECORDS PRINTED         = ' WS-COUNT.                
                                                                                
           CLOSE AG07F235                                                       
                 BP13K650                                                       
                 BP13F650                                                       
                 BP13L65A.                                                      
                                                                                
           IF WS-K650-STATUS NOT = 00                                           
              DISPLAY 'BP13K650 - ERROR CLOSING : ' WS-K650-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
