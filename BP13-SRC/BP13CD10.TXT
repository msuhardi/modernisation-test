       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD10.                                                 
      *AUTHOR.        MSD1.                                                     
      *DATE-WRITTEN.  24/01/2008.                                               
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      **********************************************************                
      *                                                        *                
      *    OBJECTIVES  :                                       *                
      *                                                        *                
      *    1.  TO GENERATE REPORT ON BALLOTING RESULT FOR GRO  *                
      *                                                        *                
      *    INPUT FILES : BP13F740 -                            *                
      *                  BP13K740 -                            *                
      *                  BP13K825 -                            *                
      *                                                        *                
      *    OUTPUT FILE : BP13LD10 -                            *                
      *                  BP13FD10 -                            *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY    DATE   DETAILS                          *                
      * ======= ====  ======  ================================ *                
      * BP133244 MSD1 240108  NEW PROGRAM                      *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
             SELECT BP13F740  ASSIGN TO BP13F740.                               
                                                                                
             SELECT BP13K740                                                    
                       ASSIGN        TO BP13K740                                
                       ACCESS MODE   IS RANDOM                                  
                       ORGANIZATION  IS INDEXED                                 
                       RECORD KEY    IS K740-KEY-FLD                            
                       FILE STATUS   IS WS-K740-STATUS.                         
                                                                                
             SELECT BP13K825                                                    
                       ASSIGN        TO BP13K825                                
                       ACCESS MODE   IS RANDOM                                  
                       ORGANIZATION  IS INDEXED                                 
                       RECORD KEY    IS K825-KEY-FLD                            
                       FILE STATUS   IS WS-K825-STATUS.                         
                                                                                
             SELECT BP13FD10  ASSIGN TO BP13FD10.                               
             SELECT BP13LD10  ASSIGN TO BP13LD10.                               
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13F740             RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS  F .                           
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740           RECORD CONTAINS 500 CHARACTERS.                  
       COPY BP13K740.                                                           
                                                                                
       FD   BP13K825           RECORD CONTAINS 200 CHARACTERS.                  
       COPY BP13K825.                                                           
                                                                                
                                                                                
       FD BP13FD10             RECORD CONTAINS 132 CHARACTERS                   
                               RECORDING MODE IS  F .                           
       01 BP13FD10-REC.                                                         
          05 FD10-SNO                     PIC X(04).                            
          05 FIL01                        PIC X(01).                            
          05 FD10-NME-OCCP                PIC X(55).                            
          05 FIL02                        PIC X(01).                            
          05 FD10-NUM-NRIC                PIC X(09).                            
          05 FIL03                        PIC X(01).                            
          05 FD10-NUM-REGN                PIC X(08).                            
          05 FIL04                        PIC X(01).                            
          05 FD10-CDE-PRIO                PIC X(03).                            
          05 FIL05                        PIC X(01).                            
          05 FD10-NUM-RANK                PIC X(02).                            
          05 FIL06                        PIC X(01).                            
          05 FD10-NUM-QUE                 PIC X(05).                            
          05 FIL07                        PIC X(01).                            
          05 FD10-NUM-REGN2               PIC X(08).                            
          05 FIL08                        PIC X(01).                            
          05 FD10-CDE-PRIO2               PIC X(03).                            
          05 FIL09                        PIC X(01).                            
          05 FD10-CDE-STAT                PIC X(01).                            
          05 FIL10                        PIC X(01).                            
          05 FD10-NUM-QUE2                PIC X(05).                            
          05 FILLER                       PIC X(19).                            
                                                                                
       FD BP13LD10             LABEL RECORDS ARE OMITTED.                       
       01 BP13LD10-REC                    PIC X(132).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 WS-F740-EOF                  PIC X(01)  VALUE 'N'.                 
          05 WS-K740-STATUS               PIC 9(02)  VALUE ZERO.                
          05 WS-K825-STATUS               PIC 9(02)  VALUE ZERO.                
                                                                                
       01 WS-VBLES.                                                             
          05 WS-SNO                       PIC 9(04)  VALUE ZERO.                
          05 WS-RETURN-CODE               PIC 9(03)  VALUE ZERO.                
          05 WS-DATE                      PIC X(08)  VALUE SPACE.               
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-CTR                       PIC 9(02)  VALUE ZERO.                
          05 WS-PAGENO                    PIC 9(03)  VALUE ZERO.                
          05 WS-LINE-CNT                  PIC 9(02)  VALUE ZERO.                
          05 WS-F740-READ                 PIC 9(08)  VALUE ZERO.                
          05 WS-LD10-WRITTEN              PIC 9(08)  VALUE ZERO.                
          05 WS-K740-FND                  PIC 9(08)  VALUE ZERO.                
          05 WS-K740-NOT-FND              PIC 9(08)  VALUE ZERO.                
          05 WS-K825-FND                  PIC 9(08)  VALUE ZERO.                
          05 WS-K825-NOT-FND              PIC 9(08)  VALUE ZERO.                
                                                                                
       01 BP13FD10-HEADER.                                                      
          05 HD10-SNO                 PIC X(04) VALUE 'SNO '.                   
          05 FILLER                   PIC X     VALUE ';'.                      
          05 HD10-NME-OCCP            PIC X(55) VALUE 'APPLICANT NAME'.         
          05 FILLER                   PIC X     VALUE ';'.                      
          05 HD10-NUM-NRIC            PIC X(09) VALUE 'NRIC'.                   
          05 FILLER                   PIC X     VALUE ';'.                      
          05 HD10-NUM-REGN            PIC X(08) VALUE 'GRO-REGN'.               
          05 FILLER                   PIC X     VALUE ';'.                      
          05 HD10-CDE-PRIO            PIC X(04) VALUE 'PRIO'.                   
          05 FILLER                   PIC X     VALUE ';'.                      
          05 HD10-NUM-RANK            PIC X(04) VALUE 'RANK'.                   
          05 FILLER                   PIC X     VALUE ';'.                      
          05 HD10-NUM-QUE             PIC X(05) VALUE 'QUEUE'.                  
          05 FILLER                   PIC X     VALUE ';'.                      
          05 HD10-NUM-REGN2           PIC X(08) VALUE 'PA-REGN'.                
          05 FILLER                   PIC X     VALUE ';'.                      
          05 HD10-CDE-PRIO2           PIC X(04) VALUE 'PRIO'.                   
          05 FILLER                   PIC X     VALUE ';'.                      
          05 HD10-CDE-STAT            PIC X(06) VALUE 'RESULT'.                 
          05 FILLER                   PIC X     VALUE ';'.                      
          05 HD10-NUM-QUE2            PIC X(05) VALUE 'QUEUE'.                  
                                                                                
      ***************************                                               
      * PRINT AREA                                                              
      ***************************                                               
       01 LD10-HEADER1.                                                         
          05 FILLER                      PIC X(08) VALUE 'BP13LD10'.            
          05 FILLER                      PIC X(06) VALUE SPACES.                
          05 FILLER                      PIC X(04) VALUE 'HDB3'.                
          05 FILLER                      PIC X(38) VALUE SPACES.                
          05 FILLER                      PIC X(20) VALUE                        
                                         'SYSTEM OF COMMITMENT'.                
          05 FILLER                      PIC X(26) VALUE SPACES.                
          05 FILLER                      PIC X(07) VALUE 'DATE : '.             
          05 LD10-DATE                   PIC X(10).                             
          05 FILLER                      PIC X(02) VALUE SPACES.                
          05 FILLER                      PIC X(07) VALUE 'PAGE : '.             
          05 LD10-PAGE                   PIC Z(04).                             
                                                                                
       01 LD10-HEADER2.                                                         
          05 FILLER                      PIC X(46) VALUE SPACES.                
          05 FILLER                      PIC X(40) VALUE                        
             "REPORT ON DBSS SHORTLISTING RESULT - GRO".                        
                                                                                
       01 LD10-HEADER3.                                                         
          05 FILLER                    PIC X(05) VALUE 'SNO '.                  
          05 FILLER                    PIC X(41) VALUE 'APPLICANT NAME'.        
          05 FILLER                    PIC X(10) VALUE 'NRIC'.                  
          05 FILLER                    PIC X(09) VALUE 'GRO-REGN'.              
          05 FILLER                    PIC X(05) VALUE 'PRIO'.                  
          05 FILLER                    PIC X(05) VALUE 'RANK'.                  
          05 FILLER                    PIC X(15) VALUE 'QUEUE'.                 
          05 FILLER                    PIC X(09) VALUE 'PA-REGN'.               
          05 FILLER                    PIC X(05) VALUE 'PRIO'.                  
          05 FILLER                    PIC X(07) VALUE 'RESULT'.                
          05 FILLER                    PIC X(05) VALUE 'QUEUE'.                 
                                                                                
       01 LD10-DASH-LINE                  PIC X(132) VALUE ALL '-'.             
                                                                                
       01 LD10-DETAILS.                                                         
          05 LD10-SNO                     PIC Z(04).                            
          05 FILLER                       PIC X(01).                            
          05 LD10-NME-OCCP                PIC X(40).                            
          05 FILLER                       PIC X(01).                            
          05 LD10-NUM-NRIC                PIC X(09).                            
          05 FILLER                       PIC X(01).                            
          05 LD10-NUM-REGN                PIC X(08).                            
          05 FILLER                       PIC X(01).                            
          05 LD10-CDE-PRIO                PIC X(03).                            
          05 FILLER                       PIC X(02).                            
          05 LD10-NUM-RANK                PIC X(02).                            
          05 FILLER                       PIC X(03).                            
          05 LD10-NUM-QUE                 PIC X(05).                            
          05 FILLER                       PIC X(10).                            
          05 LD10-NUM-REGN2               PIC X(08).                            
          05 FILLER                       PIC X(01).                            
          05 LD10-CDE-PRIO2               PIC X(03).                            
          05 FILLER                       PIC X(04).                            
          05 LD10-CDE-STAT                PIC X(01).                            
          05 FILLER                       PIC X(04).                            
          05 LD10-NUM-QUE2                PIC X(05).                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 1500-READ-F740       THRU 1500-EXIT.                         
           PERFORM 2000-PROCESS-RTN     THRU 2000-EXIT                          
                   UNTIL WS-F740-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F740                                                 
                       BP13K825                                                 
                       BP13K740                                                 
                OUTPUT BP13LD10                                                 
                       BP13FD10.                                                
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K740 : ' WS-K740-STATUS                
              MOVE     WS-K740-STATUS         TO WS-RETURN-CODE                 
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K825 : ' WS-K825-STATUS                
              MOVE     WS-K825-STATUS         TO WS-RETURN-CODE                 
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-DATE.                      
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO LD10-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1500-READ-F740.                                                          
      *-------------------------------------------------------------            
           READ BP13F740 AT END                                                 
                MOVE 'Y' TO WS-F740-EOF                                         
                GO TO 1500-EXIT.                                                
                                                                                
            ADD 1 TO WS-F740-READ.                                              
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-PROCESS-RTN.                                                        
      *-------------------------------------------------------------            
           PERFORM 3000-READ-BP13K740       THRU 3000-EXIT.                     
           PERFORM 4000-READ-BP13K825       THRU 4000-EXIT.                     
           PERFORM 5000-PRINT-DETAIL        THRU 5000-EXIT.                     
                                                                                
           PERFORM 1500-READ-F740           THRU 1500-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K740.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES            TO BP13K740-REC.                              
           INITIALIZE                BP13K740-REC.                              
                                                                                
           STRING F740-NUM-RANDOM(1:27) 'P'                                     
              DELIMITED BY SIZE INTO K740-KEY-FLD.                              
                                                                                
           READ BP13K740.                                                       
           EVALUATE WS-K740-STATUS                                              
           WHEN 00                                                              
                ADD   1           TO WS-K740-FND                                
           WHEN 23                                                              
                ADD   1           TO WS-K740-NOT-FND                            
                DISPLAY 'BP13K740 NOT FOUND KEY : ' K740-KEY-FLD                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K740 - ' WS-K740-STATUS              
                MOVE WS-K740-STATUS        TO  WS-RETURN-CODE                   
                PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-READ-BP13K825.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES            TO BP13K825-REC.                              
           INITIALIZE                BP13K825-REC.                              
                                                                                
           MOVE F740-NUM-REGN     TO K825-NUM-REGN.                             
           MOVE F740-NUM-NRIC1    TO K825-NUM-NRIC.                             
                                                                                
           READ BP13K825.                                                       
           EVALUATE WS-K825-STATUS                                              
           WHEN 00                                                              
                ADD   1           TO WS-K825-FND                                
           WHEN 23                                                              
                ADD   1           TO WS-K825-NOT-FND                            
                DISPLAY 'BP13K825 NOT FOUND KEY : ' K825-KEY-FLD                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K825 - ' WS-K825-STATUS              
                MOVE WS-K825-STATUS        TO  WS-RETURN-CODE                   
                PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-PRINT-DETAIL.                                                       
      *-------------------------------------------------------------            
           IF WS-PAGENO = ZERO                                                  
              PERFORM 8000-PRINT-HEADER THRU 8000-EXIT                          
              WRITE BP13FD10-REC      FROM BP13FD10-HEADER                      
           ELSE                                                                 
              IF WS-LINE-CNT > 55                                               
                 PERFORM 8000-PRINT-HEADER THRU 8000-EXIT.                      
                                                                                
           MOVE SPACES                  TO LD10-DETAILS                         
                                           BP13FD10-REC.                        
           ADD   1                      TO WS-SNO WS-LINE-CNT.                  
                                                                                
           MOVE WS-SNO                  TO LD10-SNO FD10-SNO.                   
           MOVE K825-NME-OCCP           TO LD10-NME-OCCP FD10-NME-OCCP.         
           MOVE F740-NUM-NRIC1          TO LD10-NUM-NRIC FD10-NUM-NRIC.         
           MOVE F740-NUM-REGN           TO LD10-NUM-REGN FD10-NUM-REGN.         
           IF F740-CDE-REQ-ALLOC-SCH = 'TCP'                                    
              MOVE F740-CDE-REQ-ALLOC-SCH                                       
                                        TO LD10-CDE-PRIO FD10-CDE-PRIO          
           ELSE                                                                 
              IF  (F740-NUM-MCPS NOT = SPACES)                                  
              AND (F740-CDE-RESIDENT-HA1 NOT = 'H' AND 'P' AND 'M')             
                  MOVE  'MCP'           TO LD10-CDE-PRIO FD10-CDE-PRIO          
              ELSE                                                              
                  MOVE  'NO '           TO LD10-CDE-PRIO FD10-CDE-PRIO          
              END-IF                                                            
           END-IF.                                                              
           MOVE F740-NUM-GRO-RANK       TO LD10-NUM-RANK FD10-NUM-RANK.         
           MOVE F740-NUM-NT-FT-QUEUE    TO LD10-NUM-QUE  FD10-NUM-QUE.          
                                                                                
      ****BP17K640                                                              
           MOVE K740-NUM-REGN           TO LD10-NUM-REGN2                       
                                           FD10-NUM-REGN2.                      
           IF K740-CDE-ALLOC-SCH = 'TCP'                                        
              MOVE K740-CDE-ALLOC-SCH   TO LD10-CDE-PRIO2                       
                                           FD10-CDE-PRIO2                       
           ELSE                                                                 
              IF  (K740-NUM-MCPS NOT = SPACES)                                  
              AND (K740-CDE-RESIDENT-HA1 NOT = 'H' AND 'P' AND 'M')             
                  MOVE  'MCP'           TO LD10-CDE-PRIO2                       
                                           FD10-CDE-PRIO2                       
              ELSE                                                              
                  MOVE  'NO '           TO LD10-CDE-PRIO                        
                                           FD10-CDE-PRIO2                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE K740-CDE-REQUEST-STATUS TO LD10-CDE-STAT FD10-CDE-STAT.         
           MOVE K740-NUM-NT-FT-QUEUE    TO LD10-NUM-QUE2 FD10-NUM-QUE2.         
                                                                                
           WRITE BP13LD10-REC         FROM LD10-DETAILS.                        
           ADD  1                       TO WS-LD10-WRITTEN.                     
                                                                                
           MOVE ';' TO FIL01 FIL02 FIL03 FIL04 FIL05                            
                       FIL06 FIL07 FIL08 FIL09 FIL10.                           
           WRITE BP13FD10-REC.                                                  
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       8000-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
           ADD   1                    TO WS-PAGENO.                             
           MOVE WS-PAGENO             TO LD10-PAGE.                             
                                                                                
           WRITE BP13LD10-REC       FROM LD10-HEADER1   AFTER PAGE.             
           WRITE BP13LD10-REC       FROM LD10-HEADER2   AFTER 1.                
           WRITE BP13LD10-REC       FROM LD10-HEADER3   AFTER 2.                
           WRITE BP13LD10-REC       FROM LD10-DASH-LINE AFTER 1.                
                                                                                
           MOVE   5                   TO WS-LINE-CNT.                           
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           CLOSE      BP13F740                                                  
                      BP13K740                                                  
                      BP13K825                                                  
                      BP13LD10                                                  
                      BP13FD10.                                                 
                                                                                
           IF WS-K740-STATUS NOT = ZEROES                                       
              DISPLAY 'ERROR CLOSING BP13K740 : ' WS-K740-STATUS                
              MOVE WS-K740-STATUS              TO WS-RETURN-CODE                
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES                                       
              DISPLAY 'ERROR CLOSING BP13K825 : ' WS-K825-STATUS                
              MOVE WS-K825-STATUS              TO WS-RETURN-CODE                
           END-IF.                                                              
                                                                                
           DISPLAY ' NO. OF BP13F740 RECORDS READ ' WS-F740-READ                
           DISPLAY ' NO. OF BP13LD10 WRITTEN      ' WS-LD10-WRITTEN             
           DISPLAY ' NO. OF BP13K740 FOUND        ' WS-K740-FND                 
           DISPLAY ' NO. OF BP13K740 NOT FOUND    ' WS-K740-NOT-FND             
           DISPLAY ' NO. OF BP13K825 FOUND        ' WS-K825-FND                 
           DISPLAY ' NO. OF BP13K825 NOT FOUND    ' WS-K825-NOT-FND             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
