       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C45N.                                                  
      *AUTHOR.       RJB1.                                                      
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  OBJECTIVE: TO LIST SATP CASES FOR SELFBOOKING             *            
      *                                                            *            
      *  I    :  BP13F800 - SOC MASTER FILE                        *            
      *          BP13K435 - KEY BORROW FILE                        *            
      *          BP27K800 - HLE MASTER (HDB LOAN ELIGIBILITY)      *            
      *          BM06K110 - FLAT INV FILE                          *            
      *          BM06K100 - BLOCK                                  *            
      *  O    :  P13F800A - SOC MASTER FILE - SATP CASES           *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136980 RJB1 17/10/2017 NEW PROGRAM                       *            
      *               30/10/2017 TO INCLUDE ONLY HLE STATUS = 'A'  *            
      *                          AND TO CORRECT PULLING OUT SOU CASES           
      * BP138047 KAC1 25/11/2019 RECOMPILE FOR BP27 COPYBOOK UPDATE*            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K435  ASSIGN TO BP13K435                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K435-KEY-FLD                              
                  ACCESS           IS DYNAMIC                                   
                  FILE STATUS      IS WS-K435-STATUS.                           
                                                                                
           SELECT BP27K800  ASSIGN TO BP27K800                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K800-NUM-HLA                              
                  ACCESS           IS DYNAMIC                                   
                  FILE STATUS      IS WS-K800-STATUS.                           
                                                                                
           SELECT BM06K110  ASSIGN TO BM06K110                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K110-KEY-FLD                              
                  ACCESS           IS DYNAMIC                                   
                  FILE STATUS      IS WS-K110-STATUS.                           
                                                                                
           SELECT BM06K100  ASSIGN TO BM06K100                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K100-KEY-FLD                              
                  ACCESS           IS DYNAMIC                                   
                  FILE STATUS      IS WS-K100-STATUS.                           
                                                                                
           SELECT P13F800A ASSIGN TO P13F800A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K435                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K435.                                                           
                                                                                
       FD   BP27K800                                                            
            RECORD CONTAINS 1200 CHARACTERS.                                    
       COPY BP27K800.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   P13F800A                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 2000 CHARACTERS.                                    
       01   P13F800A-REC                 PIC X(2000).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-K800-STATUS            PIC 99    VALUE ZEROS.                 
           05  WS-K100-STATUS            PIC 99    VALUE ZEROS.                 
           05  WS-K110-STATUS            PIC 99    VALUE ZEROS.                 
           05  WS-K435-STATUS            PIC 99    VALUE ZEROS.                 
           05  WS-F800-EOF               PIC X     VALUE 'N'.                   
           05  WS-K435-EOF               PIC X     VALUE 'N'.                   
           05  WS-K800-FOUND             PIC X     VALUE 'N'.                   
               88 K800-FND                         VALUE 'Y'.                   
           05  WS-K435-FOUND             PIC X     VALUE 'N'.                   
               88 K435-FND                         VALUE 'Y'.                   
           05  WS-K110-FOUND             PIC X     VALUE 'N'.                   
               88 K110-FND                         VALUE 'Y'.                   
           05  WS-K100-FOUND             PIC X     VALUE 'N'.                   
               88 K100-FND                         VALUE 'Y'.                   
           05  WS-F800-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K435-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K100-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K110-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-F800-WRITE             PIC 9(6)  VALUE ZEROS.                 
           05  WS-INVALID-CASE           PIC X     VALUE 'N'.                   
               88 WS-INVALID                       VALUE 'Y'.                   
           05  WS-SKIPPED                PIC 9(6)  VALUE ZEROS.                 
                                                                                
       01  WS-DATE                       PIC 9(8)  VALUE ZEROES.                
       01  WS-DATE-INT1                  PIC 9(8)  VALUE ZEROES.                
       01  WS-DATE-INT2                  PIC 9(8)  VALUE ZEROES.                
       01  WS-DATE-COMP                  PIC 9(8)  VALUE ZEROES.                
       01  WS-NUM-DAYS                   PIC 9(8)  VALUE ZEROES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES      THRU 100-EXIT.                          
            PERFORM 200-READ-BP13F800   THRU 200-EXIT.                          
            PERFORM 300-PROCESS         THRU 300-EXIT                           
                UNTIL WS-F800-EOF = 'Y'                                         
            PERFORM 999-CLOSE-FILES     THRU 999-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F800                                                 
                       BP13K435                                                 
                       BP27K800                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                OUTPUT P13F800A.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K435-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K435-STATUS ' WS-K435-STATUS              
              MOVE WS-K435-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K100-STATUS ' WS-K100-STATUS              
              MOVE WS-K100-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                          
           COMPUTE WS-DATE-INT1 = FUNCTION INTEGER-OF-DATE(WS-DATE).            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F800.                                                       
      *-------------------------------------------------------------            
           READ BP13F800 AT END                                                 
           MOVE 'Y'                      TO WS-F800-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F800-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS.                                                             
      *-------------------------------------------------------------            
                                                                                
      *-------------------------------------------------------------            
      * SKIP CASES WITH ALLO-CAT = SER OR NPL OR SHS                            
      *-------------------------------------------------------------            
           IF F800-NUM-ALLO-CAT = 'SER' OR 'NPL' OR 'SHS'                       
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' ALLOC MODE IS ' F800-NUM-ALLO-CAT         
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      * SKIP CASES WITH SO APPOINTMENT DATE                                     
      *-------------------------------------------------------------            
           IF F800-DTE-SO-APPT > ZEROES                                         
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' DTE SO APPT IS ' F800-DTE-SO-APPT         
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      * SKIP CASES WITH BUY SELL TAG = B                                        
      *-------------------------------------------------------------            
           IF F800-NUM-BUY-SELL-TAG = 'B'                                       
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' BUY-SELL TAG IS '                         
                      F800-NUM-BUY-SELL-TAG                                     
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      * SKIP CASES WITH PORT ELIG TAG = 'Y'                                     
      *-------------------------------------------------------------            
           IF F800-NUM-PORT-ELIG-TAG = 'Y'                                      
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' SERS PORT TAG IS '                        
                      F800-NUM-PORT-ELIG-TAG                                    
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      * SKIP CASES WITH AHG REQ DATE AND AHG STATUS NOT = A                     
      *-------------------------------------------------------------            
           IF F800-DTE-AHG-REQ > ZEROES                                         
              IF F800-NUM-AHG-STATUS NOT = 'A'                                  
                 ADD 1                  TO WS-SKIPPED                           
                 DISPLAY F800-NUM-REGN ' AHG STATUS IS '                        
                         F800-NUM-AHG-STATUS                                    
                 GO TO 300-READ-NEXT-REC                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      * SKIP 2F FLAT TYPE CASES                                                 
      *-------------------------------------------------------------            
           IF F800-NUM-FLAT-TYPE = '2F'                                         
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' FLAT TYP IS ' F800-NUM-FLAT-TYPE          
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      * SKIP CASES WITH LOAN TAG NOT = H                                        
      *-------------------------------------------------------------            
           IF F800-NUM-LOAN-TAG NOT = 'H'                                       
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' LOAN TAG IS ' F800-NUM-LOAN-TAG           
              GO TO 300-READ-NEXT-REC                                           
           ELSE                                                                 
              PERFORM 400-CHECK-HLE-EXP THRU 400-EXIT                           
                                                                                
              IF NOT K800-FND                                                   
                 GO TO 300-READ-NEXT-REC                                        
              END-IF                                                            
                                                                                
              IF WS-INVALID                                                     
                 ADD 1               TO WS-SKIPPED                              
                 GO TO 300-READ-NEXT-REC                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      * SKIP CASES WITH SOC DEBTOR, SOC CASH, AND CPF PAID AMOUNTS              
      *-------------------------------------------------------------            
           IF F800-AMT-SOC-DEBTOR > ZEROES                                      
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' SOC DEBTOR = ' F800-AMT-SOC-DEBTOR        
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
           IF F800-AMT-SOC-CASH > ZEROES                                        
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' SOC CASH = ' F800-AMT-SOC-CASH            
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
           IF F800-AMT-CPF1-PAID > ZEROES                                       
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' CPF1 PAID = ' F800-AMT-CPF1-PAID          
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
           IF F800-AMT-CPF2-PAID > ZEROES                                       
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' CPF2 PAID = ' F800-AMT-CPF2-PAID          
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
           IF F800-AMT-CPF3-PAID > ZEROES                                       
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' CPF3 PAID = ' F800-AMT-CPF3-PAID          
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
           IF F800-AMT-CPF4-PAID > ZEROES                                       
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY F800-NUM-REGN ' CPF4 PAID = ' F800-AMT-CPF4-PAID          
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
           PERFORM 700-READ-BM06K110                                            
              THRU 700-EXIT.                                                    
                                                                                
           IF K110-FND                                                          
              PERFORM 800-READ-BM06K100                                         
                 THRU 800-EXIT                                                  
                                                                                
              IF K100-FND                                                       
                 IF K100-DTE-KEY-AVAIL > ZEROES                                 
                    PERFORM 850-GET-KEY-LOCATION THRU 850-EXIT                  
                    IF WS-INVALID                                               
                       ADD 1            TO WS-SKIPPED                           
                       DISPLAY 'NOT SOU CASE - ' F800-NUM-REGN                  
                       GO TO 300-READ-NEXT-REC                                  
                    END-IF                                                      
                 ELSE                                                           
                    ADD 1               TO WS-SKIPPED                           
                    DISPLAY 'KEY NOT AVAIL - ' F800-NUM-REGN                    
                    GO TO 300-READ-NEXT-REC                                     
                 END-IF                                                         
              ELSE                                                              
                 ADD 1                  TO WS-SKIPPED                           
                 DISPLAY 'NOT FND IN K100 - ' F800-NUM-REGN                     
                 GO TO 300-READ-NEXT-REC                                        
              END-IF                                                            
           ELSE                                                                 
              ADD 1                     TO WS-SKIPPED                           
              DISPLAY 'NOT FND IN K110 - ' F800-NUM-REGN                        
              GO TO 300-READ-NEXT-REC                                           
           END-IF.                                                              
                                                                                
           PERFORM 600-WRITE-BP13K800 THRU 600-EXIT.                            
                                                                                
       300-READ-NEXT-REC.                                                       
                                                                                
           PERFORM 200-READ-BP13F800  THRU 200-EXIT.                            
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-CHECK-HLE-EXP.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES                 TO K800-NUM-HLA.                         
           MOVE F800-NUM-HLA           TO K800-NUM-HLA.                         
           MOVE 'N'                    TO WS-INVALID-CASE.                      
                                                                                
           READ BP27K800.                                                       
                                                                                
      ***TESTING                                                                
      *    MOVE ZEROES                 TO WS-K800-STATUS                        
                                                                                
           IF WS-K800-STATUS = 00                                               
              MOVE 'Y' TO WS-K800-FOUND                                         
              ADD 1    TO WS-K800-READ                                          
                                                                                
              IF K800-NUM-CASE-STATUS NOT = 'AC '                               
                 MOVE 'Y'               TO WS-INVALID-CASE                      
                 DISPLAY F800-NUM-REGN ' HLA NOT ACTIVE ' F800-NUM-HLA          
                        '-' K800-NUM-CASE-STATUS                                
                 GO TO 400-EXIT                                                 
              END-IF                                                            
                                                                                
              IF K800-DTE-HLA-EXPIRY > ZEROES                                   
                 IF WS-DATE > K800-DTE-HLA-EXPIRY                               
                    MOVE 'Y'            TO WS-INVALID-CASE                      
                    DISPLAY F800-NUM-REGN ' HLA EXPIRED ' F800-NUM-HLA          
                        ' ' K800-DTE-HLA-EXPIRY                                 
                 ELSE                                                           
                    PERFORM 500-COMPUTE-DATE  THRU 500-EXIT                     
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'               TO WS-INVALID-CASE                      
                 DISPLAY F800-NUM-REGN 'INVALID HLA EXP - '                     
                         K800-DTE-HLA-EXPIRY                                    
              END-IF                                                            
           ELSE                                                                 
              IF WS-K800-STATUS = 10 OR 23                                      
                 MOVE 'N'               TO WS-K800-FOUND                        
                 ADD 1                  TO WS-SKIPPED                           
                 DISPLAY F800-NUM-REGN ' HLA NOT FOUND ' F800-NUM-HLA           
              ELSE                                                              
                 MOVE 'N'               TO WS-K800-FOUND                        
                 ADD 1                  TO WS-SKIPPED                           
                 DISPLAY 'BP27K800 - READ ERROR (' WS-K800-STATUS               
                         ') ,KEY IS ('  K800-NUM-HLA ') ' F800-NUM-REGN         
                 PERFORM 999-CLOSE-FILES                                        
                    THRU 999-EXIT                                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-COMPUTE-DATE.                                                        
      *-------------------------------------------------------------            
      ***TESTING                                                                
      *    MOVE 20170927              TO K800-DTE-HLA-EXPIRY                    
      *    IF F800-NUM-REGN  = '1558178F' OR '3238344J'                         
      *       MOVE 20171012           TO K800-DTE-HLA-EXPIRY                    
      *    END-IF                                                               
                                                                                
           MOVE K800-DTE-HLA-EXPIRY   TO WS-DATE-COMP.                          
                                                                                
           COMPUTE WS-DATE-INT2 = FUNCTION INTEGER-OF-DATE                      
                                           (WS-DATE-COMP)                       
                                                                                
           COMPUTE WS-NUM-DAYS  = WS-DATE-INT2 - WS-DATE-INT1                   
                                                                                
           IF      WS-NUM-DAYS  < 30                                            
              MOVE 'Y'                TO WS-INVALID-CASE                        
              DISPLAY F800-NUM-REGN ' HLE EXPIRY '                              
                      K800-DTE-HLA-EXPIRY ' IS < 30 DAYS'                       
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-WRITE-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                TO P13F800A-REC.                          
                                                                                
           WRITE P13F800A-REC       FROM BP13F800-MASTER.                       
                                                                                
           ADD 1                      TO WS-F800-WRITE.                         
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-READ-BM06K110.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES                 TO K110-KEY-FLD                          
           MOVE F800-NUM-SCH-ACC       TO K110-KEY-FLD.                         
                                                                                
           READ BM06K110.                                                       
                                                                                
           IF WS-K110-STATUS = 00                                               
              MOVE 'Y' TO WS-K110-FOUND                                         
              ADD 1    TO WS-K110-READ                                          
           ELSE                                                                 
              IF WS-K110-STATUS = 10 OR 23                                      
                 MOVE 'N'               TO WS-K110-FOUND                        
              ELSE                                                              
                 ADD 1                  TO WS-SKIPPED                           
                 MOVE 'N'               TO WS-K110-FOUND                        
                 DISPLAY 'BM06K110 - READ ERROR (' WS-K110-STATUS               
                         ') ,KEY IS ('  K110-KEY-FLD ') ' F800-NUM-REGN         
                 PERFORM 999-CLOSE-FILES                                        
                    THRU 999-EXIT                                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-READ-BM06K100.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                  TO K100-KEY-FLD.                        
           MOVE K110-AIX1               TO K100-KEY-FLD.                        
                                                                                
           READ BM06K100.                                                       
                                                                                
           IF WS-K100-STATUS = 00                                               
              MOVE 'Y' TO WS-K100-FOUND                                         
              ADD 1    TO WS-K100-READ                                          
           ELSE                                                                 
              IF WS-K100-STATUS = 10 OR 23                                      
                 MOVE 'N'               TO WS-K100-FOUND                        
              ELSE                                                              
                 ADD 1                  TO WS-SKIPPED                           
                 MOVE 'N'               TO WS-K100-FOUND                        
                 DISPLAY 'BM06K100 - READ ERROR (' WS-K100-STATUS               
                         ') ,KEY IS ('  K100-KEY-FLD ') ' F800-NUM-REGN         
                 PERFORM 999-CLOSE-FILES                                        
                    THRU 999-EXIT                                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       850-GET-KEY-LOCATION.                                                    
      *-------------------------------------------------------------            
           MOVE 'N'                        TO   WS-K435-EOF                     
                                                WS-K435-FOUND                   
                                                WS-INVALID-CASE.                
           MOVE SPACES                     TO   BP13K435-REC.                   
           MOVE F800-NUM-SCH-ACC           TO   K435-NUM-HDB-REF.               
                                                                                
           PERFORM 860-BP13K435-STARTBR    THRU 860-EXIT.                       
           PERFORM 870-BP13K435-READNEXT   THRU 870-EXIT                        
                   UNTIL WS-K435-EOF = 'Y'.                                     
                                                                                
           IF K435-FND                                                          
             MOVE 'Y'                      TO   WS-INVALID-CASE                 
           ELSE                                                                 
             IF K110-DTE-KEY-AVAIL = SPACES OR LOW-VALUES OR ZEROES             
                MOVE 'Y'                   TO   WS-INVALID-CASE                 
             ELSE                                                               
                CONTINUE                                                        
             END-IF                                                             
           END-IF.                                                              
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
       860-BP13K435-STARTBR.                                                    
      *-------------------------------------------------------------            
                                                                                
           START BP13K435 KEY >= K435-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K435-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-K435-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START BP13K435 ERROR '   WS-K435-STATUS                  
              MOVE    WS-K435-STATUS       TO   RETURN-CODE                     
              PERFORM 999-CLOSE-FILES      THRU 999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       860-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       870-BP13K435-READNEXT.                                                   
      *-------------------------------------------------------------            
                                                                                
           READ BP13K435 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K435-STATUS                                              
           WHEN 00                                                              
              IF K435-NUM-HDB-REF = F800-NUM-SCH-ACC                            
                 MOVE 'Y'                  TO   WS-K435-FOUND                   
              ELSE                                                              
                 MOVE 'Y'                  TO   WS-K435-EOF                     
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-K435-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READNEXT BP13K435 ERROR ' WS-K435-STATUS                 
              MOVE    WS-K435-STATUS       TO   RETURN-CODE                     
              PERFORM 999-CLOSE-FILES      THRU 999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       870-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13C45N ------------- * '.                  
           DISPLAY 'NO OF REC READ    BP13F800 = ' WS-F800-READ.                
           DISPLAY 'NO OF REC READ    BP13K435 = ' WS-K435-READ.                
           DISPLAY 'NO OF REC READ    BP27K800 = ' WS-K800-READ.                
           DISPLAY 'NO OF REC READ    BM06K110 = ' WS-K110-READ.                
           DISPLAY 'NO OF REC READ    BM06K100 = ' WS-K100-READ.                
           DISPLAY 'NO OF REC WRITTEN BP13F800 = ' WS-F800-WRITE.               
           DISPLAY 'NO OF REC SKIPPED BP13F800 = ' WS-SKIPPED.                  
                                                                                
           CLOSE BP13F800                                                       
                 BP13K435                                                       
                 BP27K800                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 P13F800A.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP27K800, STATUS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K435-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K435, STATUS ' WS-K435-STATUS          
              MOVE WS-K435-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BM06K110, STATUS ' WS-K110-STATUS          
              MOVE WS-K110-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BM06K100, STATUS ' WS-K100-STATUS          
              MOVE WS-K100-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
