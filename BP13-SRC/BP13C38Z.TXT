      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C38Z.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  DECEMBER 22, 2016.                                        
      *===============================================================*         
      * OBJECTIVE  :  TO PRINT REFUND OF EXCESS PAYMENT CASES         *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F330                                     *         
      *               2. BP13K410                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13L38Z                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP136522 22/12/2016 PCL4 NEW PROGRAM                          *         
      * BP139175 02/09/2022 KAC1 ADDED HE_Jialing@hdb.gov.sg          *         
      *                            Kathijah_Bee_ALI_MOHAMED@hdb.gov.sg*         
      *                            Nor'ain_MOHAMAD_JOHAR@hdb.gov.sg   *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F330  ASSIGN        TO BP13F330.                         
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
           SELECT  BP13L38Z ASSIGN        TO BP13L38Z.                          
           SELECT  MAILC38Z ASSIGN        TO MAILC38Z.                          
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K410.                                                           
                                                                                
       FD   BP13F330                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K330.                                                           
                                                                                
       FD   BP13L38Z                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13L38Z-REC               PIC X(132).                              
                                                                                
       FD   MAILC38Z                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  100 CHARACTERS.                                    
       01   MAILC38Z-REC              PIC X(100).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F410             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-F330             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-F330-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L38Z-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01  L38Z-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L38Z'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L38Z-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L38Z-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L38Z-HEADER02.                                                       
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  L38Z-HDR02          PIC X(100)   VALUE SPACES.                   
                                                                                
       01  L38Z-HEADER03.                                                       
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(05) VALUE '  S/N'.                     
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(08) VALUE 'REGN NO '.                  
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(14) VALUE 'SCHEME A/C NO.'.            
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(16) VALUE 'TRANSACTION TYPE'.          
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(16) VALUE 'TRANSACTION DATE'.          
           05  FILLER              PIC X(02) VALUE SPACES.                      
                                                                                
       01  L38Z-DETAIL01.                                                       
           05  FILLER1             PIC X(01)        VALUE SPACES.               
           05  L38Z-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER2             PIC X(02)        VALUE SPACES.               
           05  L38Z-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER3             PIC X(02)        VALUE SPACES.               
           05  L38Z-SCC-ACCT       PIC X(14)        VALUE SPACES.               
           05  FILLER4             PIC X(08)        VALUE SPACES.               
           05  L38Z-TRANS-TYPE     PIC X(04)        VALUE SPACES.               
           05  FILLER5             PIC X(10)        VALUE SPACES.               
           05  L38Z-TRANS-DATE     PIC X(10)        VALUE SPACES.               
           05  FILLER6             PIC X(02)        VALUE SPACES.               
                                                                                
       01  WS-RPT-BP13L38Z.                                                     
           05  WS-NUM-REGN         PIC X(08)        VALUE SPACES.               
           05  WS-SCC-ACCT         PIC X(11)        VALUE SPACES.               
           05  WS-TRANS-TYPE       PIC X(04)        VALUE SPACES.               
           05  WS-TRANS-DATE       PIC X(10)        VALUE SPACES.               
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-EMPTY-RPT.                                                    
               10 FILLER                  PIC X(30) VALUE SPACES.               
               10 FILLER                  PIC X(21)                             
                                          VALUE '<   EMPTY  REPORT   >'.        
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(50)                               
               VALUE 'RCPT TO:<CHONG_YENG_MUI@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT4             PIC X(50)                               
               VALUE 'RCPT TO:<IVY_M_BALANAG@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT5             PIC X(50)                               
               VALUE 'RCPT TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT5A            PIC X(50)                               
               VALUE 'RCPT TO:<HE_Jialing@hdb.gov.sg>'.                         
           05  WS-MAIL-RPT5B            PIC X(50)                               
               VALUE 'RCPT TO:<Kathijah_Bee_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT5C            PIC X(50)                               
               VALUE "RCPT TO:<Nor'ain_MOHAMAD_JOHAR@hdb.gov.sg>".              
           05  WS-MAIL-RPT6             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT7             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT8             PIC X(50)                               
               VALUE 'TO:<CHONG_YENG_MUI@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT9             PIC X(50)                               
               VALUE 'TC:<IVY_M_BALANAG@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT10            PIC X(50)                               
               VALUE 'CC:<SERENA_JM_TEH@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT10A           PIC X(50)                               
               VALUE 'CC:<HE_Jialing@hdb.gov.sg>'.                              
           05  WS-MAIL-RPT10B           PIC X(50)                               
               VALUE 'CC:<Kathijah_Bee_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT10C           PIC X(50)                               
               VALUE "CC:<Nor'ain_MOHAMAD_JOHAR@hdb.gov.sg>".                   
           05  WS-MAIL-RPT11            PIC X(100)                              
               VALUE 'SUBJECT: EXCEPTION REPORT FOR FTS'.                       
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F330      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F330 = 'Y'.                                           
                                                                                
           PERFORM 4000-READ-BP13F410      THRU 4000-EXIT.                      
           PERFORM 5001-PROCESS-RECORDS    THRU 5001-EXIT                       
             UNTIL WS-EOF-F410 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F330                                                 
                       BP13F410                                                 
              OUTPUT   BP13L38Z                                                 
                       MAILC38Z.                                                
                                                                                
           PERFORM 7100-GET-DATE           THRU 7100-EXIT.                      
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F330.                                                      
      ******************************************************************        
                                                                                
           READ BP13F330           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F330                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F330-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO WS-RPT-BP13L38Z.            
           INITIALIZE                               WS-RPT-BP13L38Z.            
                                                                                
           IF (K330-DTE-SALE-AGRM  >= WS-PRIO-DTE)    AND                       
              (K330-DTE-SALE-AGRM  <= WS-SYSTEM-DATE) AND                       
              (K330-CDE-REFUND      = 'Y')                                      
                                                                                
               PERFORM 3500-PROCESS-SOCA-REFUND  THRU 3500-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F330            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-PROCESS-SOCA-REFUND.                                                
      ******************************************************************        
                                                                                
           MOVE K330-CUR-REG                 TO WS-NUM-REGN.                    
           MOVE K330-SCH-ACC                 TO WS-SCC-ACCT.                    
           MOVE 'SOCA'                       TO WS-TRANS-TYPE.                  
                                                                                
           IF K330-DTE-SALE-AGRM NOT = SPACES AND LOW-VALUES AND ZEROES         
                                                                                
              STRING K330-DTE-SALE-AGRM(7:2) '/'                                
                     K330-DTE-SALE-AGRM(5:2) '/'                                
                     K330-DTE-SALE-AGRM(1:4)                                    
                   DELIMITED BY SIZE INTO WS-TRANS-DATE                         
                                                                                
           ELSE                                                                 
              MOVE SPACES              TO WS-TRANS-DATE                         
           END-IF.                                                              
                                                                                
           PERFORM 6000-PRINT-REPORT   THRU 6000-EXIT.                          
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F410                             
                GO                 TO   4000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4500-PROCESS-SATP-REFUND.                                                
      ******************************************************************        
                                                                                
           MOVE K410-NUM-REGN                TO WS-NUM-REGN.                    
           MOVE K410-NUM-SCH-ACC             TO WS-SCC-ACCT.                    
                                                                                
           IF K410-CDE-SA-TP = 'Y'                                              
              MOVE 'SATP'                    TO WS-TRANS-TYPE                   
           ELSE                                                                 
              MOVE ' TP '                    TO WS-TRANS-TYPE                   
           END-IF.                                                              
                                                                                
           IF K410-DTE-SO-EXECUTION NOT = SPACES AND LOW-VALUES                 
                                                 AND ZEROES                     
                                                                                
              STRING K410-DTE-SO-EXECUTION(7:2) '/'                             
                     K410-DTE-SO-EXECUTION(5:2) '/'                             
                     K410-DTE-SO-EXECUTION(1:4)                                 
                   DELIMITED BY SIZE INTO WS-TRANS-DATE                         
                                                                                
           ELSE                                                                 
              MOVE SPACES              TO WS-TRANS-DATE                         
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM 6000-PRINT-REPORT   THRU 6000-EXIT.                          
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5001-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO WS-RPT-BP13L38Z.            
           INITIALIZE                               WS-RPT-BP13L38Z.            
                                                                                
           IF (K410-DTE-SO-EXECUTION >= WS-PRIO-DTE)    AND                     
              (K410-DTE-SO-EXECUTION <= WS-SYSTEM-DATE) AND                     
              (K410-CDE-REFUND        = 'Y')                                    
                                                                                
               PERFORM 4500-PROCESS-SATP-REFUND  THRU 4500-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-BP13F410            THRU 4000-EXIT.                
                                                                                
       5001-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       5000-HEADER-EMAIL.                                                       
      ******************************************************************        
                                                                                
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT1.                               
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT2.                               
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT3.                               
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT4.                               
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT5.                               
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT5A.                              
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT5B.                              
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT5C.                              
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT6.                               
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT7.                               
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT8.                               
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT9.                               
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT10.                              
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT10A.                             
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT10B.                             
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT10C.                             
                                                                                
            STRING 'SUBJECT: REFUND OF EXCESS PAYMENT CASES FROM '              
                    WS-PRIO-DD '/' WS-PRIO-MM '/' WS-PRIO-CCYY ' TO '           
                    WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                  
              DELIMITED BY SIZE INTO WS-MAIL-RPT11.                             
                                                                                
            WRITE MAILC38Z-REC FROM WS-MAIL-RPT11.                              
                                                                                
                                                                                
            STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                   
                    DELIMITED BY SIZE INTO WS-DATE-RPT.                         
                                                                                
            WRITE MAILC38Z-REC FROM WS-DATE-RPT-DTL                             
                                                                                
            MOVE SPACES TO MAILC38Z-REC.                                        
            WRITE MAILC38Z-REC.                                                 
                                                                                
            WRITE MAILC38Z-REC FROM L38Z-HEADER01.                              
            WRITE MAILC38Z-REC FROM L38Z-HEADER02.                              
                                                                                
            MOVE SPACES TO MAILC38Z-REC.                                        
            WRITE MAILC38Z-REC.                                                 
                                                                                
            WRITE MAILC38Z-REC FROM L38Z-HEADER03.                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO L38Z-DETAIL01.                  
           INITIALIZE                           L38Z-DETAIL01.                  
                                                                                
           ADD 1                             TO WS-SNO.                         
           MOVE WS-SNO                       TO WS-SNO-ZZZZ9.                   
           MOVE WS-SNO-ZZZZ9                 TO L38Z-SN.                        
           MOVE WS-NUM-REGN                  TO L38Z-REGN.                      
           MOVE WS-SCC-ACCT                  TO L38Z-SCC-ACCT.                  
           MOVE WS-TRANS-TYPE                TO L38Z-TRANS-TYPE.                
           MOVE WS-TRANS-DATE                TO L38Z-TRANS-DATE.                
                                                                                
           WRITE BP13L38Z-REC              FROM L38Z-DETAIL01.                  
           WRITE MAILC38Z-REC              FROM L38Z-DETAIL01.                  
           ADD 1                             TO WS-CNT-L38Z-WRITE.              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-SYSTEM-DATE               
                                                   WS-CURR-DATE.                
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L38Z-DATE-HDR.                             
                                                                                
            STRING 'REFUND OF EXCESS PAYMENT CASES FROM '                       
                    WS-PRIO-DD '/' WS-PRIO-MM '/' WS-PRIO-CCYY ' TO '           
                    WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                  
                     DELIMITED BY SIZE INTO L38Z-HDR02.                         
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L38Z-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L38Z-PAGE-HDR.                
                                                                                
           WRITE BP13L38Z-REC    FROM L38Z-HEADER01 AFTER PAGE.                 
           WRITE BP13L38Z-REC    FROM L38Z-HEADER02.                            
           WRITE BP13L38Z-REC    FROM L38Z-HEADER03 AFTER 2.                    
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
           PERFORM 5000-HEADER-EMAIL           THRU 5000-EXIT.                  
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7100-GET-DATE.                                                           
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO  WS-PRIOR-DTE.                
                                                                                
           COMPUTE WS-NUM-DTE-APPT =                                            
                   FUNCTION INTEGER-OF-DATE(WS-PRIOR-DTE).                      
                                                                                
           SUBTRACT 6 FROM  WS-NUM-DTE-APPT.                                    
                                                                                
           COMPUTE  WS-PRIOR-DTE =                                              
                    FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-APPT).                  
                                                                                
           MOVE WS-PRIOR-DTE        TO WS-PRIO-DTE.                             
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C38Z'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F330 RECORDS READ (SOCA)...... : '            
                    WS-F330-READ.                                               
           DISPLAY '(2) NO OF BP13F410 RECORDS READ (SA/TP)..... : '            
                    WS-F410-READ.                                               
           DISPLAY '(3) NO OF BP13L38Z RECORDS WRITTEN.......... : '            
                    WS-CNT-L38Z-WRITE.                                          
                                                                                
           CLOSE    BP13F410                                                    
                    BP13F330                                                    
                    BP13L38Z                                                    
                    MAILC38Z.                                                   
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
