       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD16.                                                 
      *AUTHOR.        ED1                                                       
      *DATE-WRITTEN.  03MAR2008.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *  1. UPDATE BP13KD38/FD38 ACCUM W/ NEW CPFS AMTS        *                
      *            COMPUTED FROM PENDING AMOUNT.               *                
      *  2. SINCE CPF FORM ALREADY PRINTED THUR ONLINE BP13B0G7*                
      *     AND SENT TO CPFB FOR PROCESSING UNDER PREFIX SS,   *                
      *     SO THIS REVISED INSTAL WILL BE BYPASS IN BP13M714. *                
      *                                                        *                
      *    INPUT FILES  : BP13FD38 - PP TRANS ACCUM FILE       *                
      *                   BP13FD92 - TRANSACTION FILE, RATE CHG*                
      *    I-O            BP13KD38 - PP TRANS FILE             *                
      *    OUTPUT         P13FD38A - PP TRANS ACCUM W/         *                
      *                                 NEW CPF AMTS           *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP13     ED1 20080303  NEW PGM                          *                
      *BP133876 JB8 20100406  NOT MOVE 'Y' TO FD38-NUM-OMIT-REVISE-CPF*         
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD38 ASSIGN TO BP13FD38.                                  
           SELECT BP13FD92 ASSIGN TO BP13FD92.                                  
           SELECT P13FD38A ASSIGN TO P13FD38A.                                  
                                                                                
           SELECT BP13KD38 ASSIGN TO BP13KD38                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD38-KEY-FLD                               
                  FILE STATUS     IS WS-KD38-STATUS.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FD38                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13FD38.                                                           
                                                                                
       FD   BP13FD92                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13FD92.                                                           
                                                                                
       FD   BP13KD38                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13KD38.                                                           
                                                                                
       FD   P13FD38A                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 400 CHARACTERS.                                     
       01 P13FD38A-REC PIC X(400).                                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-KD38-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-CNT-READ-FD38               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-FD92               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-WRT-FD38A               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-UPD-KD38                PIC 9(6)  VALUE ZEROS.            
           05  WS-MATCHED                     PIC 9(6)  VALUE ZEROS.            
                                                                                
       01  WS-FD92-KEY.                                                         
           05  WS-FD92-REGN              PIC X(08).                             
           05  WS-FD92-SCHED             PIC X(02).                             
                                                                                
       01  WS-FD38-KEY.                                                         
           05  WS-FD38-REGN              PIC X(08).                             
           05  WS-FD38-SCHED             PIC X(02).                             
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
                                                                                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 2000-READ-FD38         THRU 2000-EXIT.                       
           PERFORM 2100-READ-FD92         THRU 2100-EXIT.                       
                                                                                
           PERFORM 3000-PROCESS-MATCH     THRU 3000-EXIT                        
                  UNTIL WS-FD92-KEY = HIGH-VALUES AND                           
                        WS-FD38-KEY = HIGH-VALUES.                              
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13FD38                                                 
                       BP13FD92                                                 
                I-O    BP13KD38                                                 
                OUTPUT P13FD38A.                                                
                                                                                
           IF WS-KD38-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD38 OPEN ERROR, STATUS IS ' WS-KD38-STATUS          
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-FD38.                                                          
      ****************************************************************          
                                                                                
           READ BP13FD38                                                        
              AT END                                                            
                     MOVE HIGH-VALUES   TO WS-FD38-KEY                          
                     GO TO 2000-EXIT                                            
              NOT AT END                                                        
                     MOVE FD38-NUM-REGN     TO WS-FD38-REGN                     
                     MOVE FD38-NUM-SCHEDULE TO WS-FD38-SCHED                    
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-FD38.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2100-READ-FD92.                                                          
      ****************************************************************          
                                                                                
           READ BP13FD92                                                        
              AT END                                                            
                     MOVE HIGH-VALUES   TO WS-FD92-KEY                          
                     GO TO 2100-EXIT                                            
              NOT AT END                                                        
                     MOVE FD92-NUM-REGN          TO WS-FD92-REGN                
                     MOVE FD92-PP-SCHDL-CPF-RATE TO WS-FD92-SCHED               
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-FD92.                          
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-MATCH.                                                      
      ****************************************************************          
           IF WS-FD38-KEY = WS-FD92-KEY                                         
              ADD 1 TO WS-MATCHED                                               
              PERFORM 4000-WRITE-FD38A    THRU 4000-EXIT                        
              PERFORM 4100-UPDATE-KD38    THRU 4100-EXIT                        
              PERFORM 2000-READ-FD38      THRU 2000-EXIT                        
              PERFORM 2100-READ-FD92      THRU 2100-EXIT                        
                                                                                
           ELSE                                                                 
              IF WS-FD38-KEY < WS-FD92-KEY                                      
                 PERFORM 4001-WRITE-FD38A THRU 4001-EXIT                        
                 PERFORM 2000-READ-FD38   THRU 2000-EXIT                        
              ELSE                                                              
                 PERFORM 2100-READ-FD92   THRU 2100-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4000-WRITE-FD38A.                                                        
      ****************************************************************          
            PERFORM 4010-INIT-AMTS  THRU 4010-EXIT.                             
                                                                                
            MOVE FD92-CPF-INSTAL-CASH-NEW  TO FD38-AMT-INSTAL-NEW-CASH.         
            MOVE FD92-CPF-INSTAL-CPF1-NEW  TO FD38-AMT-INSTAL-NEW-CPF1.         
            MOVE FD92-CPF-INSTAL-CPF2-NEW  TO FD38-AMT-INSTAL-NEW-CPF2.         
            MOVE FD92-CPF-INSTAL-CPF3-NEW  TO FD38-AMT-INSTAL-NEW-CPF3.         
            MOVE FD92-CPF-INSTAL-CPF4-NEW  TO FD38-AMT-INSTAL-NEW-CPF4.         
                                                                                
            MOVE BP13FD38-TRANS-REC TO P13FD38A-REC.                            
            WRITE                      P13FD38A-REC.                            
            ADD  1 TO WS-CNT-WRT-FD38A.                                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4001-WRITE-FD38A.                                                        
      ****************************************************************          
                                                                                
            MOVE BP13FD38-TRANS-REC TO P13FD38A-REC.                            
            WRITE                      P13FD38A-REC.                            
            ADD  1 TO WS-CNT-WRT-FD38A.                                         
                                                                                
       4001-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4010-INIT-AMTS.                                                          
      ****************************************************************          
            IF FD92-CPF-INSTAL-TOT-NEW  IS NOT NUMERIC                          
               MOVE 0 TO FD92-CPF-INSTAL-TOT-NEW                                
            END-IF.                                                             
            IF FD92-CPF-INSTAL-CASH-NEW IS NOT NUMERIC                          
               MOVE 0 TO FD92-CPF-INSTAL-CASH-NEW                               
            END-IF.                                                             
            IF FD92-CPF-INSTAL-CPF1-NEW IS NOT NUMERIC                          
               MOVE 0 TO FD92-CPF-INSTAL-CPF1-NEW                               
            END-IF.                                                             
            IF FD92-CPF-INSTAL-CPF2-NEW IS NOT NUMERIC                          
               MOVE 0 TO FD92-CPF-INSTAL-CPF2-NEW                               
            END-IF.                                                             
            IF FD92-CPF-INSTAL-CPF3-NEW IS NOT NUMERIC                          
               MOVE 0 TO FD92-CPF-INSTAL-CPF3-NEW                               
            END-IF.                                                             
            IF FD92-CPF-INSTAL-CPF4-NEW IS NOT NUMERIC                          
               MOVE 0 TO FD92-CPF-INSTAL-CPF4-NEW                               
            END-IF.                                                             
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4100-UPDATE-KD38.                                                        
      ****************************************************************          
           MOVE FD38-KEY-FLD         TO KD38-KEY-FLD.                           
                                                                                
           READ BP13KD38.                                                       
                                                                                
           IF WS-KD38-STATUS = 00                                               
              PERFORM 4200-MOVE-TO-KD38  THRU 4200-EXIT                         
              PERFORM 4300-REWRITE-KD38  THRU 4300-EXIT                         
                                                                                
           ELSE                                                                 
              IF WS-KD38-STATUS = 23                                            
                 DISPLAY 'REC NOT FND IN BP13KD38. KEY IS '                     
                          KD38-KEY-FLD                                          
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13KD38. STATUS IS '                   
                          WS-KD38-STATUS ' ' KD38-KEY-FLD                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4200-MOVE-TO-KD38.                                                       
      ****************************************************************          
                                                                                
            MOVE FD92-CPF-INSTAL-CASH-NEW  TO KD38-AMT-INSTAL-NEW-CASH.         
            MOVE FD92-CPF-INSTAL-CPF1-NEW  TO KD38-AMT-INSTAL-NEW-CPF1.         
            MOVE FD92-CPF-INSTAL-CPF2-NEW  TO KD38-AMT-INSTAL-NEW-CPF2.         
            MOVE FD92-CPF-INSTAL-CPF3-NEW  TO KD38-AMT-INSTAL-NEW-CPF3.         
            MOVE FD92-CPF-INSTAL-CPF4-NEW  TO KD38-AMT-INSTAL-NEW-CPF4.         
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4300-REWRITE-KD38.                                                       
      ****************************************************************          
            REWRITE BP13KD38-TRANS-REC.                                         
                                                                                
            EVALUATE WS-KD38-STATUS                                             
            WHEN 00                                                             
               ADD   1   TO  WS-CNT-UPD-KD38                                    
            WHEN OTHER                                                          
               DISPLAY 'ERROR WRITING BP13KD38. STATUS IS '                     
                        WS-KD38-STATUS                                          
            END-EVALUATE.                                                       
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13FD38                                                       
                 BP13FD92                                                       
                 BP13KD38                                                       
                 P13FD38A.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY '*===========BP13CD16 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13FD38 RECORDS READ   = ' WS-CNT-READ-FD38.               
           DISPLAY 'BP13FD92 RECORDS READ   = ' WS-CNT-READ-FD92.               
           DISPLAY 'RECORDS MATCHED         = ' WS-MATCHED.                     
           DISPLAY 'BP13KD38 RECS UPDATED   = ' WS-CNT-UPD-KD38.                
           DISPLAY 'P13FD38A RECS WRITTEN   = ' WS-CNT-WRT-FD38A.               
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
