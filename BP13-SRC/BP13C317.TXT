       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C317.                                                 
       AUTHOR.        TAN M K.                                                  
       DATE-WRITTEN.  11/09/89.                                                 
       DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  THIS IS A PROGRAM TO PRINT A LIST OF          *                
      *          CASES ALREADY CANCELLED FOR MORE              *                
      *          THAN 3 MONTHS STILL WITH OUTSTANDING          *                
      *          BALANCE FOR MONTH ENDING DD/MM/YY.            *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *      1. APPLICATION MASTER (BP13F800)                  *                
      *                                                        *                
      *     LISTING:                                           *                
      *     1. CONTROL LISTING (BP13L317)                      *                
      *                                                        *                
      * SOCPH8B BSL 30/07/93 TO CONVERT FROM P10F020 TO BP13F800                
      * SOCPH8B LJL 07/02/94 TO HANDLE NON-NUMERIC FIELDS.     *                
      * BP130418CLT 27/08/98 TO CATER FOR Y2K.                 *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800   ASSIGN TO BP13F800.                                
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE IS RANDOM                                         
                  RECORD KEY IS K820-KEY-FLD                                    
                  FILE STATUS IS K820-FILE-STATUS.                              
                                                                                
           SELECT BP13L317   ASSIGN TO BP13L317.                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800               BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORD IS STANDARD.                    
       COPY BP13F800.                                                           
                                                                                
       FD  BP13K820                BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 400 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13K820.                                                           
                                                                                
       FD  BP13L317                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  L317-PRINT-REC              PIC X(132).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS2-COUNTERS.                                                        
           05  WS2-CNT-K020-READ       PIC 9(3)    VALUE ZEROS.                 
           05  WS2-CNT-PRINT-L317      PIC 9(4)    VALUE ZEROS.                 
           05  WS2-CNT-LINE-ON-L317    PIC 9(2)    VALUE 66.                    
           05  WS2-CNT-NO-PAGE         PIC 9(5)    VALUE ZEROS.                 
           05  WS-FLAG                 PIC 9(2)    VALUE ZEROS.                 
                                                                                
       01  WS3-ACCUM-FIELDS.                                                    
           05  WS3-TOT-CASH            PIC S9(8)V99 COMP-3 VALUE +0.            
           05  WS3-TOT-CPF1            PIC S9(8)V99 COMP-3 VALUE +0.            
           05  WS3-TOT-CPF2            PIC S9(8)V99 COMP-3 VALUE +0.            
           05  WS3-TOT-CPF3            PIC S9(8)V99 COMP-3 VALUE +0.            
           05  WS3-TOT-CPF4            PIC S9(8)V99 COMP-3 VALUE +0.            
           05  WS3-TOT-BK              PIC S9(8)V99 COMP-3 VALUE +0.            
           05  WS3-TOT-CD              PIC S9(8)V99 COMP-3 VALUE +0.            
           05  WS3-TOT-DEBTOR          PIC S9(8)V99 COMP-3 VALUE +0.            
                                                                                
       01  WS4-SWITCHES.                                                        
           05  WS4-EOF-IND         PIC X(1)    VALUE 'N'.                       
               88 WS4-EOF          VALUE 'Y'.                                   
                                                                                
       01  WS5-DATES-VARIABLES.                                                 
           05  WS5-DATE.                                                        
               10 WS5-CCYY         PIC 9(4)  VALUE 0.                           
               10 WS5-MM           PIC 99    VALUE 0.                           
               10 WS5-DD           PIC 99    VALUE 0.                           
                                                                                
           05  WS5-CUR-DATE.                                                    
               10 WS5-CUR-CC1      PIC 99.                                      
               10 WS5-CUR-YY1      PIC 99.                                      
               10 WS5-CUR-MM1      PIC 99.                                      
               10 WS5-CUR-DD1      PIC 99.                                      
                                                                                
           05  WS5-CURRENT-DATE.                                                
               10 WS5-CUR-MM       PIC 99.                                      
               10 FILLER           PIC X.                                       
               10 WS5-CUR-DD       PIC 99.                                      
               10 FILLER           PIC X.                                       
               10 WS5-CUR-CCYY.                                                 
                  15 WS5-CUR-CC    PIC 99.                                      
                  15 WS5-CUR-YY    PIC 99.                                      
               10 WS5-CUR-CCYY1 REDEFINES WS5-CUR-CCYY PIC 9(4).                
                                                                                
           05  WS5-PRT-DATE.                                                    
               10 WS5-PRT-DD       PIC XX.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS5-PRT-MM       PIC XX.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS5-PRT-CCYY     PIC XXXX.                                    
                                                                                
           05  WS5-PERIOD          PIC S9(8) COMP-3 VALUE +0.                   
                                                                                
       01  WS6-DEFAULT-VALUES.                                                  
           05  WS6-MAX-LINES       PIC 9(02)   VALUE  20.                       
           05  WS6-SR-SLASH        PIC X(01)   VALUE  '/'.                      
                                                                                
       01  WS-CNT                  PIC 9       VALUE 0.                         
                                                                                
       01  WS-NUM-CPF1             PIC X(9)    VALUE SPACES.                    
                                                                                
       01  K820-FILE-STATUS        PIC 9(2)    VALUE 0.                         
                                                                                
       01  WS7-SCHEME-ACCT.                                                     
           05  WS7-SCH-ACC.                                                     
               10  WS7-SCH-1-4     PIC X(4)    VALUE SPACES.                    
               10  WS7-SCH-5-8     PIC X(4)    VALUE SPACES.                    
               10  WS7-SCH-9-9     PIC X(1)    VALUE SPACES.                    
               10  WS7-SCH-10-11   PIC X(2)    VALUE SPACES.                    
                                                                                
           05  WS7-SCH-ACC-2.                                                   
               10  WS7-SCH-1-4     PIC X(4)    VALUE SPACES.                    
               10  FILLER          PIC X(01)   VALUE '-'.                       
               10  WS7-SCH-5-8     PIC X(4)    VALUE SPACES.                    
               10  FILLER          PIC X(01)   VALUE '-'.                       
               10  WS7-SCH-9-9     PIC X(1)    VALUE SPACES.                    
               10  FILLER          PIC X(01)   VALUE '-'.                       
               10  WS7-SCH-10-11   PIC X(2)    VALUE SPACES.                    
                                                                                
      **********************************************************                
      *           WS FOR REPORT LAY-OUT                        *                
      **********************************************************                
                                                                                
       01  L317-PRT-HEAD-1.                                                     
           05  L317-HD1-RPT-ID     PIC X(8)    VALUE 'BP13L317'.                
           05  FILLER              PIC X(6)    VALUE SPACES.                    
           05  FILLER              PIC X(4)    VALUE 'HDB3'.                    
           05  FILLER              PIC X(28)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)   VALUE SPACES.                    
           05  FILLER              PIC X(6)    VALUE 'DATE: '.                  
           05  L317-HD1-DATE       PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(2)    VALUE SPACES.                    
           05  FILLER              PIC X(6)    VALUE 'PAGE: '.                  
           05  L317-HD1-PAGE-NO    PIC ZZZZ9.                                   
                                                                                
       01  L317-PRT-HEAD-1A.                                                    
           05  FILLER              PIC X(57)   VALUE SPACES.                    
           05  FILLER              PIC X(14)   VALUE 'AGREEMENT TEAM'.          
                                                                                
       01  L317-PRT-HEAD-2.                                                     
           05  FILLER              PIC X(32)   VALUE SPACES.                    
           05  FILLER              PIC X(47)   VALUE                            
               'SOC - CASES ALREADY CANCELLED FOR MORE THAN 3 '.                
           05  FILLER              PIC X(15)   VALUE                            
               'MONTHS STILL'.                                                  
                                                                                
       01  L317-PRT-HEAD-3.                                                     
           05  FILLER              PIC X(38)   VALUE SPACES.                    
           05  FILLER              PIC X(42)   VALUE                            
              'WITH OUTSTANDING BALANCE FOR MONTH ENDING '.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L317-HD3-DATE       PIC X(10)   VALUE SPACES.                    
                                                                                
       01  L317-PRT-HEAD-4.                                                     
           05  FILLER              PIC X(07)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'DOC NO/'.                 
           05  FILLER              PIC X(08)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'DATE'.                    
           05  FILLER              PIC X(09)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'CPF1 NO'.                 
           05  FILLER              PIC X(06)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'CPF3 NO'.                 
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(12)   VALUE 'BOOKING FEE/'.            
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE 'SOC DEBTOR'.              
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'SOC-CPF1'.                
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'SOC-CPF3'.                
                                                                                
       01  L317-PRT-HEAD-5.                                                     
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE 'NO'.                      
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE 'SALES REGN NO'.           
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(09)   VALUE 'CANCELLED'.               
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'CPF2 NO'.                 
           05  FILLER              PIC X(06)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'CPF4 NO'.                 
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'CD PAID'.                 
           05  FILLER              PIC X(07)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'SOC CASH'.                
           05  FILLER              PIC X(06)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'SOC-CPF2'.                
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'SOC-CPF4'.                
                                                                                
       01  L317-PRT-DETAIL-1.                                                   
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L317-NO             PIC Z(3)9   VALUE ZEROES.                    
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  L317-SCH-ACC        PIC X(14)   VALUE SPACES.                    
           05  L317-SALES-REF REDEFINES L317-SCH-ACC.                           
               10  L317-ZONE       PIC X(03).                                   
               10  L317-SR-SLASH   PIC X(01).                                   
               10  L317-CUR-REG    PIC X(10).                                   
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  L317-DTE-CANCELLED  PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  L317-CPF13-NO.                                                   
               10  L317-CPF1-NO        PIC X(09).                               
               10  FILLER              PIC X(05).                               
               10  L317-CPF3-NO        PIC X(09).                               
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  L317-AMT-BK-PAID    PIC ZZZ9.99.                                 
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  L317-SOC-DEBTOR     PIC ZZZZZZ9.99.                              
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  L317-CPF13-AMTS.                                                 
               10  L317-CPF1-AMT       PIC ZZZZZZ9.99.                          
               10  FILLER              PIC X(02).                               
               10  L317-CPF3-AMT       PIC ZZZZZZ9.99.                          
           05  L317-CPF13-AMTS-X   REDEFINES L317-CPF13-AMTS                    
                                   PIC X(22).                                   
                                                                                
       01  L317-PRT-DETAIL-2.                                                   
           05  FILLER              PIC X(35)   VALUE SPACES.                    
           05  L317-CPF24-NO.                                                   
               10  L317-CPF2-NO        PIC X(09).                               
               10  FILLER              PIC X(05).                               
               10  L317-CPF4-NO        PIC X(09).                               
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  L317-AMT-CD         PIC ZZZZZZ9.99.                              
           05  FILLER              PIC X(04)    VALUE SPACES.                   
           05  L317-CASH-AMT       PIC ZZZZZZ9.99.                              
           05  FILLER              PIC X(04)    VALUE SPACES.                   
           05  L317-CPF24-AMTS.                                                 
               10  L317-CPF2-AMT       PIC ZZZZZZ9.99.                          
               10  FILLER              PIC X(02).                               
               10  L317-CPF4-AMT       PIC ZZZZZZ9.99.                          
           05  L317-CPF24-AMTS-X   REDEFINES L317-CPF24-AMTS                    
                                   PIC X(22).                                   
                                                                                
       01  L317-PRT-TOTAL-DASH.                                                 
           05  FILLER              PIC X(35)   VALUE SPACES.                    
           05  FILLER              PIC X(75)   VALUE ALL '-'.                   
                                                                                
       01  L317-PRT-TOTAL-1.                                                    
           05  FILLER                  PIC X(59)   VALUE SPACES.                
           05  L317-TOT-1              PIC -$$$$$$9.99.                         
           05  FILLER                  PIC X(03)   VALUE SPACES.                
           05  L317-TOT-2              PIC -$$$$$$9.99.                         
           05  FILLER                  PIC X(03)   VALUE SPACES.                
           05  L317-TOT-3              PIC -$$$$$$9.99.                         
           05  FILLER                  PIC X(01)   VALUE SPACES.                
           05  L317-TOT-4              PIC -$$$$$$9.99.                         
                                                                                
                                                                                
                                                                                
                                                                                
      **********************************************************                
       PROCEDURE DIVISION.                                                      
      **********************************************************                
                                                                                
      *================================================================*        
       MAIN-ROUTINE SECTION.                                                    
      *================================================================*        
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-MAIN-ROUTINE  THRU 200-EXIT UNTIL WS4-EOF.               
           PERFORM 400-PRINT-TOTAL   THRU 400-EXIT.                             
           PERFORM 500-CLOSE-ROUTINE THRU 500-EXIT.                             
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       100-OPEN-FILES.                                                          
      *================================================================*        
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K820                                                 
                OUTPUT BP13L317.                                                
                                                                                
           IF K820-FILE-STATUS NOT = 0 AND 97                                   
              DISPLAY 'ERROR IN OPENNING BP13K820 ' K820-FILE-STATUS            
              GO TO 500-CLOSE-ROUTINE.                                          
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS5-CUR-DATE.                          
           MOVE   WS5-CUR-CC1  TO   WS5-CUR-CC.                                 
           MOVE   WS5-CUR-YY1  TO   WS5-CUR-YY.                                 
           MOVE   WS5-CUR-MM1  TO   WS5-CUR-MM.                                 
           MOVE   WS5-CUR-DD1  TO   WS5-CUR-DD.                                 
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       200-MAIN-ROUTINE.                                                        
      *================================================================*        
      **************************************************************            
      *                                                            *            
      *   1. READ FILE BP13F800.                                   *            
      *   2. PROCESS ONLY CANCELLED CASES(> 3 MTHS WITH OS BALANCE)*            
      *   3. PROCESS NECESSARY DETAILS FOR PRINTING.               *            
      *   4. CALL ROUTINE TO PRINT REPORT.                         *            
      *                                                            *            
      **************************************************************            
           READ BP13F800 AT END                                                 
                MOVE 'Y' TO WS4-EOF-IND                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS2-CNT-K020-READ.                                          
                                                                                
           IF F800-NUM-STATUS NOT = 'C'                                         
                GO TO 200-EXIT.                                                 
                                                                                
           MOVE ZEROES               TO WS5-PERIOD                              
                                        WS5-DATE.                               
           MOVE ZEROS                TO WS-FLAG.                                
                                                                                
           IF F800-DTE-CANCEL  IS NOT NUMERIC                                   
           MOVE ZEROS TO F800-DTE-CANCEL.                                       
                                                                                
           MOVE F800-DTE-CANCEL      TO WS5-DATE.                               
                                                                                
           COMPUTE WS5-PERIOD = (WS5-CUR-CCYY1 * 12 + WS5-CUR-MM) -             
                                (WS5-CCYY   * 12 + WS5-MM    ).                 
                                                                                
           IF F800-AMT-BK-PAID IS NOT NUMERIC                                   
              MOVE ZEROS TO  F800-AMT-BK-PAID.                                  
           IF F800-AMT-CD      IS NOT NUMERIC                                   
              MOVE ZEROS TO  F800-AMT-CD.                                       
           IF F800-AMT-SOC-CASH IS NOT NUMERIC                                  
              MOVE ZEROS TO  F800-AMT-SOC-CASH.                                 
           IF F800-AMT-CPF1-PAID IS NOT NUMERIC                                 
              MOVE ZEROS TO  F800-AMT-CPF1-PAID.                                
           IF F800-AMT-CPF2-PAID IS NOT NUMERIC                                 
              MOVE ZEROS TO  F800-AMT-CPF2-PAID.                                
           IF F800-AMT-CPF3-PAID IS NOT NUMERIC                                 
              MOVE ZEROS TO  F800-AMT-CPF3-PAID.                                
           IF F800-AMT-CPF4-PAID IS NOT NUMERIC                                 
              MOVE ZEROS TO  F800-AMT-CPF4-PAID.                                
                                                                                
           IF F800-AMT-BK-PAID NOT   = 0 OR                                     
              F800-AMT-CD       NOT  = 0 OR                                     
              F800-AMT-SOC-CASH NOT  = 0 OR                                     
              F800-AMT-CPF1-PAID NOT = 0 OR                                     
              F800-AMT-CPF2-PAID NOT = 0 OR                                     
              F800-AMT-CPF3-PAID NOT = 0 OR                                     
              F800-AMT-CPF4-PAID NOT = 0                                        
              MOVE 1 TO WS-FLAG                                                 
              ELSE                                                              
              MOVE 2 TO WS-FLAG                                                 
           END-IF.                                                              
                                                                                
           IF WS5-PERIOD > 3 AND WS-FLAG = 1                                    
              PERFORM 210-MV-PRT THRU 210-EXIT.                                 
                                                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       210-MV-PRT.                                                              
      *================================================================*        
      **********************************************************                
      *                                                        *                
      *   MOVE NECESSARY DETAILS TO REPORT AND PRINT           *                
      *                                                        *                
      **********************************************************                
                                                                                
      *-------------------------*                                               
      * INITIALIZE PRINT FIELDS *                                               
      *-------------------------*                                               
           MOVE SPACES                 TO L317-CPF13-NO                         
                                          L317-CPF24-NO                         
                                          L317-CPF13-AMTS-X                     
                                          L317-CPF24-AMTS-X                     
                                          L317-SCH-ACC.                         
           MOVE ZEROES                 TO L317-CASH-AMT.                        
                                                                                
           IF F800-NUM-SCH-ACC = SPACES OR LOW-VALUES                           
              MOVE F800-NUM-NEW-TOWN   TO L317-ZONE                             
              MOVE WS6-SR-SLASH        TO L317-SR-SLASH                         
              MOVE F800-NUM-REGN       TO L317-CUR-REG                          
           ELSE                                                                 
              MOVE F800-NUM-SCH-ACC    TO WS7-SCH-ACC                           
              MOVE CORR WS7-SCH-ACC    TO WS7-SCH-ACC-2                         
              MOVE WS7-SCH-ACC-2       TO L317-SCH-ACC.                         
                                                                                
           MOVE F800-AMT-BK-PAID       TO L317-AMT-BK-PAID.                     
           ADD  F800-AMT-BK-PAID       TO WS3-TOT-BK.                           
           MOVE F800-AMT-CD            TO L317-AMT-CD.                          
           ADD  F800-AMT-CD            TO WS3-TOT-CD.                           
           MOVE F800-AMT-SOC-DEBTOR    TO L317-SOC-DEBTOR.                      
           ADD  F800-AMT-SOC-DEBTOR    TO WS3-TOT-DEBTOR.                       
                                                                                
           IF F800-DTE-CANCEL = ZEROES                                          
              MOVE SPACES TO L317-DTE-CANCELLED                                 
           ELSE                                                                 
              MOVE F800-DTE-CANCEL       TO WS5-DATE                            
              MOVE WS5-CCYY              TO WS5-PRT-CCYY                        
              MOVE WS5-MM                TO WS5-PRT-MM                          
              MOVE WS5-DD                TO WS5-PRT-DD                          
              MOVE WS5-PRT-DATE          TO L317-DTE-CANCELLED.                 
                                                                                
           IF (F800-AMT-SOC-CASH NOT = 0) OR                                    
              (F800-AMT-BK-PAID NOT  = 0)                                       
              MOVE F800-AMT-SOC-CASH TO L317-CASH-AMT                           
              ADD  F800-AMT-SOC-CASH TO WS3-TOT-CASH.                           
                                                                                
           MOVE SPACES TO WS-NUM-CPF1.                                          
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4                  
                                                                                
                   PERFORM 220-READ-BP13K820 THRU 220-EXIT                      
                                                                                
                   EVALUATE WS-CNT                                              
                      WHEN 1                                                    
                           IF (F800-AMT-CPF1-PAID NOT = 0) AND                  
                              (WS-NUM-CPF1 NOT = SPACES)                        
                               MOVE K820-NUM-CPF       TO L317-CPF1-NO          
                               MOVE F800-AMT-CPF1-PAID TO L317-CPF1-AMT         
                               ADD  F800-AMT-CPF1-PAID TO WS3-TOT-CPF1          
                           END-IF                                               
                      WHEN 2                                                    
                           IF (F800-AMT-CPF2-PAID NOT = 0) AND                  
                              (WS-NUM-CPF1 NOT = SPACES)                        
                              MOVE K820-NUM-CPF       TO L317-CPF2-NO           
                              MOVE F800-AMT-CPF2-PAID TO L317-CPF2-AMT          
                              ADD  F800-AMT-CPF2-PAID TO WS3-TOT-CPF2           
                           END-IF                                               
                      WHEN 3                                                    
                           IF (F800-AMT-CPF3-PAID NOT = 0) AND                  
                              (WS-NUM-CPF1 NOT = SPACES)                        
                              MOVE K820-NUM-CPF       TO L317-CPF3-NO           
                              MOVE F800-AMT-CPF3-PAID TO L317-CPF3-AMT          
                              ADD  F800-AMT-CPF3-PAID TO WS3-TOT-CPF3           
                           END-IF                                               
                      WHEN 4                                                    
                           IF (F800-AMT-CPF4-PAID NOT = 0) AND                  
                              (WS-NUM-CPF1 NOT = SPACES)                        
                              MOVE K820-NUM-CPF       TO L317-CPF4-NO           
                              MOVE F800-AMT-CPF4-PAID TO L317-CPF4-AMT          
                              ADD  F800-AMT-CPF4-PAID TO WS3-TOT-CPF4           
                           END-IF                                               
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           PERFORM 300-PRINT THRU 300-EXIT.                                     
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       220-READ-BP13K820.                                                       
      *================================================================*        
      ******************************************************************        
      *                                                                *        
      *                      GET OCCUPIER INFO                         *        
      *                                                                *        
      ******************************************************************        
                                                                                
           MOVE F800-NUM-REGN TO K820-NUM-REGN.                                 
                                                                                
           EVALUATE WS-CNT                                                      
              WHEN 1                                                            
                   MOVE F800-NUM-NRIC1 TO K820-NUM-NRIC                         
              WHEN 2                                                            
                   MOVE F800-NUM-NRIC2 TO K820-NUM-NRIC                         
              WHEN 3                                                            
                   MOVE F800-NUM-NRIC3 TO K820-NUM-NRIC                         
              WHEN 4                                                            
                   MOVE F800-NUM-NRIC4 TO K820-NUM-NRIC                         
           END-EVALUATE.                                                        
                                                                                
           IF K820-NUM-NRIC = SPACES OR LOW-VALUES                              
              GO TO 220-EXIT.                                                   
                                                                                
           READ BP13K820 INVALID KEY                                            
                DISPLAY 'RECORD NOT FOUND IN BP13K820 ' K820-KEY-FLD            
                GO TO 220-EXIT.                                                 
                                                                                
           IF K820-FILE-STATUS NOT = ZEROES                                     
              DISPLAY 'ERROR IN READING BP13K820 ' K820-FILE-STATUS             
              GO TO 220-EXIT.                                                   
                                                                                
           IF WS-CNT = 1                                                        
              MOVE K820-NUM-CPF TO WS-NUM-CPF1.                                 
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       300-PRINT.                                                               
      *================================================================*        
      ******************************************************************        
      *                                                                *        
      *       PRINT DETAIL LINE FOR REPORT L317                        *        
      *                                                                *        
      ******************************************************************        
                                                                                
           PERFORM 310-CHECK-LINECT THRU 310-EXIT.                              
                                                                                
           ADD 1                   TO WS2-CNT-PRINT-L317                        
                                      WS2-CNT-LINE-ON-L317.                     
           MOVE WS2-CNT-PRINT-L317 TO L317-NO.                                  
           WRITE L317-PRINT-REC    FROM L317-PRT-DETAIL-1 AFTER 1.              
           MOVE SPACES             TO L317-PRINT-REC.                           
                                                                                
           ADD 1                   TO WS2-CNT-LINE-ON-L317.                     
           WRITE L317-PRINT-REC    FROM L317-PRT-DETAIL-2 AFTER 1.              
           MOVE SPACES             TO L317-PRINT-REC.                           
           WRITE L317-PRINT-REC.                                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       310-CHECK-LINECT.                                                        
      *================================================================*        
      ******************************************************************        
      *                                                                *        
      *          CHECK LINE COUNT TO DETERMINE PAGE THROW              *        
      *                FOR REPORT                                      *        
      *                                                                *        
      ******************************************************************        
           IF (WS2-CNT-LINE-ON-L317 > WS6-MAX-LINES)                            
              PERFORM 320-PRINT-RPT-HEADING THRU 320-EXIT                       
              MOVE 1 TO WS2-CNT-LINE-ON-L317.                                   
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       320-PRINT-RPT-HEADING.                                                   
      *================================================================*        
      ****************************************************************          
      *                                                              *          
      * PRINT HEADING EITHER FOR L317 OR L31A DEPENDING ON INDICATOR *          
      *                                                              *          
      ****************************************************************          
           ADD  1                     TO WS2-CNT-NO-PAGE.                       
           MOVE WS2-CNT-NO-PAGE       TO L317-HD1-PAGE-NO.                      
           MOVE WS5-CUR-DD            TO WS5-PRT-DD.                            
           MOVE WS5-CUR-MM            TO WS5-PRT-MM.                            
           MOVE WS5-CUR-CCYY1         TO WS5-PRT-CCYY.                          
           MOVE WS5-PRT-DATE          TO L317-HD1-DATE                          
                                         L317-HD3-DATE.                         
                                                                                
           MOVE SPACES          TO   L317-PRINT-REC.                            
           WRITE L317-PRINT-REC AFTER PAGE.                                     
           WRITE L317-PRINT-REC FROM L317-PRT-HEAD-1  AFTER 4.                  
           WRITE L317-PRINT-REC FROM L317-PRT-HEAD-1A AFTER 1.                  
           WRITE L317-PRINT-REC FROM L317-PRT-HEAD-2  AFTER 2.                  
           WRITE L317-PRINT-REC FROM L317-PRT-HEAD-3  AFTER 1.                  
           WRITE L317-PRINT-REC FROM L317-PRT-HEAD-4  AFTER 2.                  
           WRITE L317-PRINT-REC FROM L317-PRT-HEAD-5  AFTER 1.                  
           MOVE SPACES          TO   L317-PRINT-REC                             
                                     L317-HD1-RPT-ID.                           
           WRITE L317-PRINT-REC AFTER 2.                                        
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       400-PRINT-TOTAL.                                                         
      *================================================================*        
      ******************************************************************        
      *                                                                *        
      *       PRINT TOTAL FOR AMOUNTS (L317)                           *        
      *                                                                *        
      ******************************************************************        
                                                                                
           PERFORM 310-CHECK-LINECT THRU 310-EXIT.                              
                                                                                
           ADD 1                TO  WS2-CNT-LINE-ON-L317.                       
           WRITE L317-PRINT-REC FROM L317-PRT-TOTAL-DASH AFTER 2.               
           MOVE SPACES          TO L317-PRINT-REC.                              
                                                                                
           MOVE WS3-TOT-BK      TO   L317-TOT-1.                                
           MOVE WS3-TOT-DEBTOR  TO   L317-TOT-2.                                
           MOVE WS3-TOT-CPF1    TO   L317-TOT-3.                                
           MOVE WS3-TOT-CPF3    TO   L317-TOT-4.                                
           WRITE L317-PRINT-REC FROM L317-PRT-TOTAL-1 AFTER 1.                  
           MOVE SPACES          TO L317-PRINT-REC.                              
                                                                                
           MOVE WS3-TOT-CD      TO   L317-TOT-1.                                
           MOVE WS3-TOT-CASH    TO   L317-TOT-2.                                
           MOVE WS3-TOT-CPF2    TO   L317-TOT-3.                                
           MOVE WS3-TOT-CPF4    TO   L317-TOT-4.                                
           WRITE L317-PRINT-REC FROM L317-PRT-TOTAL-1 AFTER 1.                  
           MOVE SPACES          TO L317-PRINT-REC.                              
                                                                                
           WRITE L317-PRINT-REC FROM L317-PRT-TOTAL-DASH AFTER 1.               
           MOVE SPACES          TO L317-PRINT-REC.                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       500-CLOSE-ROUTINE.                                                       
      *================================================================*        
      ****************************************************************          
      *                                                              *          
      *            CLOSE FILES AND END PROCESSING                    *          
      *                                                              *          
      ****************************************************************          
                                                                                
           DISPLAY '*--------------*'.                                          
           DISPLAY '*   BP13C317   *'.                                          
           DISPLAY '*--------------*'.                                          
           DISPLAY 'NO OF RECORDS READ FROM FILE (BP13F800)  = '                
                   WS2-CNT-K020-READ.                                           
           DISPLAY 'NO OF RECORDS PRINTED (BP13L317)         = '                
                   WS2-CNT-PRINT-L317.                                          
                                                                                
           CLOSE BP13F800                                                       
                 BP13K820                                                       
                 BP13L317.                                                      
                                                                                
           IF K820-FILE-STATUS NOT = 0                                          
              DISPLAY 'ERROR IN CLOSING BP13K820 ' K820-FILE-STATUS.            
                                                                                
           STOP RUN.                                                            
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
      *---------------*                                                         
      * END OF SOURCE *                                                         
      *---------------*                                                         
