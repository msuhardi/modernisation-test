       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C25C.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  13 JANUARY 2018.                                          
      *========================================================*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO UPDATE BP13KL00 AFTER              *                
      *                  SENDING THE EMAIL IN BP13D807         *                
      *                                                        *                
      *    INPUT       : BP13.F23C.EMAIL.A1                    *                
      *                  BP13.F23C.EMAIL.A2                  *                  
      *    I-O         : BP13.KL00.LETTER                      *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      *--------- ---- -------- --------------------------------*                
      * BP137145 ESA1 20180113 NEW PROGRAM                     *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F23C ASSIGN TO BP13F23C.                                  
                                                                                
           SELECT BP13KL00 ASSIGN TO BP13KL00                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KL00-KEY-FLD                               
                  FILE STATUS     IS KL00-STATUS.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F23C                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 250 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F23C.                                                           
                                                                                
       FD  BP13KL00                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13KL00.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 KL00-STATUS                 PIC 9(2)  VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-READ-CNT-F23C            PIC 9(5)  VALUE ZEROS.                
           05 WS-CNT-KL00-UPD             PIC 9(5)  VALUE ZEROS.                
                                                                                
       01  WS-EOF-F23C                    PIC X     VALUE 'N'.                  
       01  WS-CUR-DATE                    PIC X(8)  VALUE ZEROES.               
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F23C      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL WS-EOF-F23C = 'Y'                                            
           PERFORM 999-CLOSE-ROUTINE  THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F23C                                                 
                I-O    BP13KL00.                                                
                                                                                
           IF KL00-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13KL00  FILE, STATUS '                  
                      KL00-STATUS                                               
              MOVE KL00-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CUR-DATE.                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-F23C.                                                           
      ******************************************************************        
                                                                                
           READ BP13F23C AT END                                                 
                MOVE 'Y' TO WS-EOF-F23C                                         
                GO TO 200-EXIT.                                                 
           ADD   1     TO WS-READ-CNT-F23C.                                     
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-RTN.                                                         
      ******************************************************************        
           PERFORM 400-READ-KL00   THRU 400-EXIT.                               
                                                                                
           PERFORM 200-READ-F23C   THRU 200-EXIT.                               
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-READ-KL00.                                                           
      ******************************************************************        
                                                                                
           MOVE SPACES              TO KL00-KEY-FLD.                            
           MOVE F23C-NUM-REGN       TO KL00-NUM-REGN.                           
           MOVE 'BK'                TO KL00-NUM-STAGE.                          
           MOVE 'EINV'              TO KL00-NUM-DOC-TYPE.                       
                                                                                
           READ BP13KL00.                                                       
                                                                                
           IF KL00-STATUS = 0                                                   
              PERFORM 500-REWRITE-KL00    THRU 500-EXIT                         
           ELSE                                                                 
              DISPLAY 'REC NOT FOUND IN BP13KL00  : ' F23C-NUM-REGN             
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-REWRITE-KL00.                                                        
      ******************************************************************        
           MOVE F23C-NUM-STATUS     TO KL00-CDE-EMAIL.                          
           MOVE F23C-DTE-SEND-EMAIL TO KL00-DTE-SEND-EMAIL.                     
           MOVE F23C-TME-SEND-EMAIL TO KL00-TME-SEND-EMAIL.                     
           MOVE 'BP13C25C'          TO KL00-NUM-USERID                          
                                       KL00-LAST-UPDATE-BY.                     
           MOVE FUNCTION CURRENT-DATE                                           
                                    TO KL00-LAST-UPDATE-DTE.                    
           ACCEPT KL00-LAST-UPDATE-TME FROM TIME.                               
                                                                                
           REWRITE BP13KL00-LETTER.                                             
                                                                                
            EVALUATE KL00-STATUS                                                
               WHEN 00                                                          
               WHEN 02                                                          
                    ADD 1    TO WS-CNT-KL00-UPD                                 
               WHEN OTHER                                                       
                    DISPLAY 'ERROR UPDATING BP13KL00. STATUS IS '               
                            KL00-STATUS                                         
                    PERFORM 999-CLOSE-ROUTINE  THRU 999-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-ROUTINE.                                                       
      ******************************************************************        
           CLOSE BP13F23C                                                       
                 BP13KL00.                                                      
                                                                                
           IF KL00-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR CLOSING BP13KL00  FILE, STATUS '                  
                  KL00-STATUS                                                   
              MOVE KL00-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C25C ================*'.              
           DISPLAY '*======= RUN DATE : ' WS-CUR-DATE ' =======*'.              
           DISPLAY '    TOTAL NO OF REC READ    = ' WS-READ-CNT-F23C.           
           DISPLAY '    NO. OF KL00 UPDATED     = ' WS-CNT-KL00-UPD.            
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
