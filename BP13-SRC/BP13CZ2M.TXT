      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CZ2M.                                                 
      *AUTHOR.        GURU.                                                     
      *DATE-WRITTEN.  30/01/18.                                                 
      *****************************************************************         
      *OBJECTIVE - WRITE NOT-BOOKED RECORDS IF ITS COUNT IS > 1       *         
      *            & BOOKED FOR SAME NRIC IS ZERO AND WRITE A REPORT  *         
      *            FOR ALL RECORDS.                                   *         
      *                                                               *         
      * E.G XX/XXX - NF       -NOT BOOKED                             *         
      *     XX/XXX - NF       -NOT BOOKED                             *         
      *     XX/XXX - FN OR WF -BOOKED                                 *         
      *     XX/XXX - NF       -NOT BOOKED                             *         
      *          ======> SYSTEM WILL IGNORE THIS RECORD               *         
      *                                                               *         
      *     INPUT FILE : 1. BP13F740                                  *         
      *                  2. BP13F595                                  *         
      *     OUTPUT FILE: 1. BP13F730                                  *         
      *                  2. BP13LZ2M (REPORT)                         *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP137101 300118     KR13 NEW PGM                              *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740  ASSIGN  TO BP13F740.                                
                                                                                
           SELECT BP13F595  ASSIGN  TO BP13F595.                                
                                                                                
           SELECT BP13K813  ASSIGN  TO BP13K813                                 
                       ACCESS MODE  IS RANDOM                                   
                       ORGANIZATION IS INDEXED                                  
                       RECORD KEY   IS K813-KEY-FLD                             
                       FILE STATUS  IS WS-K813-STATUS.                          
                                                                                
           SELECT BP13F730  ASSIGN  TO BP13F730.                                
                                                                                
           SELECT BP13LZ2M  ASSIGN  TO BP13LZ2M.                                
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F740.                                                           
                                                                                
       FD   BP13F595                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F595.                                                           
                                                                                
       FD   BP13K813.                                                           
       COPY BP13K813.                                                           
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13LZ2M                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   BP13LZ2M-REC    PIC X(132).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F740-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-F595-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-K813-STATUS          PIC 9(2).                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-F595-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-F730-WRITE           PIC 9(07) VALUE ZEROES.                  
           05  WS-BKD-CNT              PIC 9(07) VALUE ZEROES.                  
           05  WS-NO-MATCH             PIC 9(07) VALUE ZEROES.                  
           05  WS-F740-MATCH-L         PIC 9(07) VALUE ZEROES.                  
           05  WS-F595-MATCH-L         PIC 9(07) VALUE ZEROES.                  
           05  WS-INX                  PIC 9(02) VALUE ZEROES.                  
           05  CUR-DTE                 PIC 9(06) VALUE ZEROES.                  
           05  CURR-DTE.                                                        
               10 CURR-Y               PIC 9(04) VALUE ZEROES.                  
               10 CURR-M               PIC 9(02) VALUE ZEROES.                  
           05  PREV.                                                            
               10 PREV-Y               PIC 9(04) VALUE ZEROES.                  
               10 PREV-M               PIC 9(02) VALUE ZEROES.                  
           05  WS-NBKD-CNT             PIC 9(07) VALUE ZEROES.                  
           05  WS-NRIC-NOTBK-CNT       PIC 9(07) VALUE ZEROES.                  
           05  WS-NRIC-BK-CNT          PIC 9(07) VALUE ZEROES.                  
           05  PREV-DTE-BALLOT         PIC 9(06) VALUE ZEROES.                  
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
                                                                                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
       01  WS-LZ2M-CONTROLS.                                                    
           05  WS-LZ2M-LINECTR                PIC 9(2)  VALUE 60.               
           05  WS-LZ2M-PAGECTR                PIC 9(5)  VALUE ZEROS.            
           05  WS-LZ2M-SNO                    PIC 9(5)  VALUE 0.                
           05  WS-LZ2M-ATTEMPT                PIC 9(5)  VALUE 0.                
           05  WS-LZ2M-REGNUM                 PIC X(08) VALUE SPACES.           
                                                                                
                                                                                
      *============================================================*            
      *         BP13LZ2M  -  R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  LZ2M-HEADER-01.                                                      
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'BP13LZ2M'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(54)     VALUE                          
               'RESIDENTS OF LORONG 3 GEYLANG WITH > 2 UNSUCC ATTEMPTS'.        
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LZ2M-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
                                                                                
       01  LZ2M-HEADER-02.                                                      
           05  FILLER              PIC X(05)     VALUE ' S/N '.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'NRIC1   '.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE ' REGN    '.             
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'NT '.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'FT'.                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'MODE'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'D-BALLOT'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'ALC'.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'HH'.                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'BHH'.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'R-ST'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'BK-ST'.                 
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'PASS-ATTMP'.            
                                                                                
       01  LZ2M-HEADER-03.                                                      
           05  FILLER              PIC X(130)    VALUE ALL ' '.                 
                                                                                
       01  LZ2M-DETAIL.                                                         
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LZ2M-SNO            PIC ZZZZ9.                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LZ2M-NRIC1          PIC X(09).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LZ2M-REGNUM         PIC X(08).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LZ2M-NT             PIC X(03).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LZ2M-FLAT-TYPE      PIC X(02).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LZ2M-MODE           PIC X(03).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  LZ2M-DTE-BALLOT     PIC X(06).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LZ2M-ALLOC          PIC X(03).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LZ2M-HH             PIC X(01).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LZ2M-BHH            PIC X(01).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LZ2M-RQST-STAT      PIC X(01).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LZ2M-BOOK-STAT      PIC X(02).                                   
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  LZ2M-ATTEMPT        PIC ZZZZZ.                                   
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
                                                                                
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
           PERFORM 3000-READ-BP13F595       THRU 3000-EXIT.                     
                                                                                
           PERFORM 4000-PROCESS-RECORDS     THRU 4000-EXIT                      
             UNTIL WS-F740-EOF = 'Y' OR WS-F595-EOF = 'Y'.                      
                                                                                
           PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13F595                                                 
                       BP13K813                                                 
               OUTPUT  BP13F730                                                 
                       BP13LZ2M.                                                
                                                                                
           IF WS-K813-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K813 : ' WS-K813-STATUS              
              MOVE     WS-K813-STATUS            TO RETURN-CODE                 
              PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYSTEM-DATE.                  
           MOVE WS-SYS-DD                   TO WS-EDITED-DD.                    
           MOVE WS-SYS-MM                   TO WS-EDITED-MM.                    
           MOVE WS-SYS-CCYY                 TO WS-EDITED-YY.                    
           MOVE WS-EDITED-DATE              TO LZ2M-RUN-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F740.                                                      
      ******************************************************************        
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y'                    TO WS-F740-EOF                      
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD  1                           TO WS-F740-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-READ-BP13F595.                                                      
      ******************************************************************        
                                                                                
           READ BP13F595 AT END                                                 
                MOVE 'Y'                    TO WS-F595-EOF                      
                GO TO 3000-EXIT                                                 
           END-READ.                                                            
           ADD  1                           TO WS-F595-READ.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE 'N'                         TO WS-F595-EOF.                     
           MOVE ZEROS                       TO WS-BKD-CNT WS-NBKD-CNT           
                                               PREV-DTE-BALLOT.                 
           MOVE 0                           TO WS-LZ2M-ATTEMPT.                 
                                                                                
           IF F740-NUM-NRIC1 = F595-NUM-NRIC1                                   
                                                                                
              PERFORM 4100-WRITE-REPORT-F740   THRU 4100-EXIT                   
                                                                                
              PERFORM 4200-CHECK-BK-STAT    THRU 4200-EXIT                      
                UNTIL F740-NUM-NRIC1 NOT = F595-NUM-NRIC1                       
                OR WS-F595-EOF  = 'Y'                                           
                                                                                
              IF WS-NBKD-CNT > 1 AND WS-BKD-CNT = 0                             
                 PERFORM 4300-WRITE-OUTPUT  THRU 4300-EXIT                      
              END-IF                                                            
                                                                                
              PERFORM 2000-READ-BP13F740    THRU 2000-EXIT                      
           ELSE                                                                 
              IF F740-NUM-NRIC1 < F595-NUM-NRIC1                                
                 PERFORM 4100-WRITE-REPORT-F740   THRU 4100-EXIT                
                 ADD +1                     TO WS-F740-MATCH-L                  
                 PERFORM 2000-READ-BP13F740 THRU 2000-EXIT                      
              ELSE                                                              
                 ADD +1                     TO WS-F595-MATCH-L                  
                 PERFORM 3000-READ-BP13F595 THRU 3000-EXIT                      
              END-IF                                                            
              ADD +1                        TO WS-NO-MATCH                      
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4100-WRITE-REPORT-F740.                                                  
      ****************************************************************          
                                                                                
           INITIALIZE  LZ2M-DETAIL.                                             
                                                                                
           PERFORM 4110-POP-F740-DATA       THRU 4110-EXIT                      
                                                                                
           PERFORM 4115-WRITE-RECORD        THRU 4115-EXIT.                     
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4110-POP-F740-DATA.                                                      
      ******************************************************************        
           MOVE F740-NUM-REGN               TO LZ2M-REGNUM                      
                                               WS-LZ2M-REGNUM                   
           MOVE F740-NUM-NRIC1              TO LZ2M-NRIC1                       
           MOVE F740-NUM-ALLO-CAT           TO LZ2M-MODE                        
           MOVE F740-CDE-ALLOC-SCH          TO LZ2M-ALLOC                       
           MOVE F740-CDE-HOUSEHOLD          TO LZ2M-HH                          
           MOVE F740-CDE-BALLOT-HOUSEHOLD   TO LZ2M-BHH                         
           MOVE F740-CDE-REQUEST-STATUS     TO LZ2M-RQST-STAT                   
           MOVE F740-NUM-BOOK-STATUS        TO LZ2M-BOOK-STAT                   
           MOVE F740-CDE-FLAT-TYPE          TO LZ2M-FLAT-TYPE                   
           MOVE F740-DTE-BALLOT             TO LZ2M-DTE-BALLOT                  
           IF F740-CDE-NT1 IS NUMERIC                                           
              PERFORM 4120-READ-K813        THRU 4120-EXIT                      
           ELSE                                                                 
              MOVE F740-CDE-NT1             TO LZ2M-NT                          
           END-IF.                                                              
                                                                                
       4110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4115-WRITE-RECORD.                                                       
      ******************************************************************        
                                                                                
           ADD  1                           TO WS-LZ2M-SNO                      
           MOVE WS-LZ2M-SNO                 TO LZ2M-SNO                         
           MOVE WS-LZ2M-ATTEMPT             TO LZ2M-ATTEMPT                     
                                                                                
           IF WS-LZ2M-SNO = 1                                                   
              WRITE BP13LZ2M-REC    FROM LZ2M-HEADER-01                         
              WRITE BP13LZ2M-REC    FROM LZ2M-HEADER-02                         
              WRITE BP13LZ2M-REC    FROM LZ2M-HEADER-03                         
           END-IF                                                               
                                                                                
           WRITE BP13LZ2M-REC       FROM LZ2M-DETAIL.                           
                                                                                
       4115-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4120-READ-K813.                                                          
      ******************************************************************        
                                                                                
           INITIALIZE  BP13K813-REC.                                            
                                                                                
           MOVE F595-CDE-NT1                TO K813-NUM-ZONE                    
           MOVE F595-CDE-FLAT-TYPE          TO K813-NUM-FLAT-TYPE               
           MOVE F595-DTE-BALLOT             TO K813-DTE-BALLOT                  
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    MOVE K813-CDE-NT        TO LZ2M-NT                          
               WHEN 23                                                          
                    DISPLAY 'RECORD NOT FOUND IN BP13K813: '                    
                       WS-K813-STATUS ' KEY : ' K813-KEY-FLD                    
               WHEN OTHER                                                       
                    MOVE WS-K813-STATUS     TO RETURN-CODE                      
                    DISPLAY 'ERROR READING BP13K813: ' WS-K813-STATUS           
                            ' KEY : ' K813-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4120-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4200-CHECK-BK-STAT.                                                      
      ****************************************************************          
                                                                                
           PERFORM 4250-WRITE-REPORT-F595   THRU 4250-EXIT                      
                                                                                
           IF (F595-NUM-BOOK-STATUS = 'BF' OR 'LF' OR 'WF' OR 'LA' OR           
                                 'WA'  OR 'LG' OR 'WG' OR 'LS' OR 'WS')         
              MOVE ZEROS                    TO WS-BKD-CNT WS-NBKD-CNT           
              ADD 1                         TO WS-NRIC-BK-CNT                   
           ELSE                                                                 
              IF (F595-NUM-BOOK-STATUS = 'NC' OR 'NF' OR 'NV' OR 'NS'           
                                          OR 'SC' OR 'SF' OR ' ')               
                 ADD 1                      TO WS-NBKD-CNT                      
                                               WS-NRIC-NOTBK-CNT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF PREV-DTE-BALLOT NOT = ZEROS                                       
             COMPUTE CUR-DTE = FUNCTION NUMVAL(F595-DTE-BALLOT)                 
             MOVE CUR-DTE                   TO CURR-DTE                         
             MOVE PREV-DTE-BALLOT           TO PREV                             
             IF CURR-Y - PREV-Y = 1                                             
                IF PREV-M - CURR-M = 9                                          
                   CONTINUE                                                     
                ELSE                                                            
                   MOVE ZEROS               TO WS-BKD-CNT WS-NBKD-CNT           
                END-IF                                                          
             ELSE                                                               
                IF CURR-M - PREV-M NOT = 3                                      
                   MOVE ZEROS               TO WS-BKD-CNT WS-NBKD-CNT           
                END-IF                                                          
             END-IF                                                             
           END-IF                                                               
                                                                                
           IF F595-DTE-BALLOT = SPACES                                          
              MOVE ZEROS                    TO F595-DTE-BALLOT                  
           END-IF                                                               
                                                                                
           COMPUTE PREV-DTE-BALLOT = FUNCTION NUMVAL(F595-DTE-BALLOT).          
                                                                                
           PERFORM 3000-READ-BP13F595       THRU 3000-EXIT.                     
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4250-WRITE-REPORT-F595.                                                  
      ****************************************************************          
           INITIALIZE  LZ2M-DETAIL.                                             
                                                                                
           IF F740-NUM-REGN NOT = F595-NUM-REGN                                 
              PERFORM 4255-POP-F595-DATA    THRU 4255-EXIT                      
                                                                                
              IF LZ2M-RQST-STAT NOT = 'C'                                       
                 ADD  1                     TO WS-LZ2M-ATTEMPT                  
              END-IF                                                            
              PERFORM 4115-WRITE-RECORD     THRU 4115-EXIT                      
           END-IF.                                                              
                                                                                
       4250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4255-POP-F595-DATA.                                                      
      ******************************************************************        
           MOVE F595-NUM-REGN               TO LZ2M-REGNUM                      
                                               WS-LZ2M-REGNUM                   
           MOVE F595-NUM-NRIC1              TO LZ2M-NRIC1                       
           MOVE F595-NUM-ALLO-CAT           TO LZ2M-MODE                        
           MOVE F595-CDE-ALLOC-SCH          TO LZ2M-ALLOC                       
           MOVE F595-CDE-HOUSEHOLD          TO LZ2M-HH                          
           MOVE F595-CDE-BALLOT-HOUSEHOLD   TO LZ2M-BHH                         
           MOVE F595-CDE-REQUEST-STATUS     TO LZ2M-RQST-STAT                   
           MOVE F595-NUM-BOOK-STATUS        TO LZ2M-BOOK-STAT                   
           MOVE F595-CDE-FLAT-TYPE          TO LZ2M-FLAT-TYPE                   
           MOVE F595-DTE-BALLOT             TO LZ2M-DTE-BALLOT                  
           IF F595-CDE-NT1 IS NUMERIC                                           
              PERFORM 4120-READ-K813        THRU 4120-EXIT                      
           ELSE                                                                 
              MOVE F595-CDE-NT1             TO LZ2M-NT                          
           END-IF.                                                              
                                                                                
       4255-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4300-WRITE-OUTPUT.                                                       
      ****************************************************************          
                                                                                
           INITIALIZE BP13F730-REC.                                             
                                                                                
           MOVE BP13F740-REC                TO BP13F730-REC.                    
                                                                                
           WRITE BP13F730-REC.                                                  
                                                                                
           ADD 1                            TO WS-F730-WRITE.                   
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CZ2M'                                      
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F740 RECORDS READ ........ : '                
                    WS-F740-READ.                                               
           DISPLAY '(2) NO OF BP13F595 RECORDS READ ........ : '                
                    WS-F595-READ.                                               
           DISPLAY '(3) NO OF BP13F730 RECORDS WRITTEN ..... : '                
                    WS-F730-WRITE.                                              
           DISPLAY '(4) NO OF BOOKED NRICS ................. : '                
                    WS-NRIC-BK-CNT.                                             
           DISPLAY '(5) NO OF NOT BOOKED NRICS ............. : '                
                    WS-NRIC-NOTBK-CNT.                                          
           DISPLAY '(6) NO OF MISMATCH F740 != F595......... : '                
                    WS-NO-MATCH.                                                
           DISPLAY '(7) NO OF F740 NO-MATCH ................ : '                
                    WS-F740-MATCH-L.                                            
           DISPLAY '(8) NO OF F750 NO-MATCH ................ : '                
                    WS-F595-MATCH-L.                                            
                                                                                
           CLOSE BP13F740                                                       
                 BP13F595                                                       
                 BP13K813                                                       
                 BP13F730                                                       
                 BP13LZ2M.                                                      
                                                                                
           IF WS-K813-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR CLOSING - BP13K813 : ' WS-K813-STATUS              
              MOVE     WS-K813-STATUS            TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
