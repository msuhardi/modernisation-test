       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC73.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  22/05/2009.                                               
      *****************************************************************         
      *                                                               *         
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                           *         
      *                                                               *         
      *  SYSTEM ID   : BP13                                           *         
      *                                                               *         
      *  OBJECTIVE   : GENERATE REPORT ON PROJECT OF MTH-END LOAN AMT,*         
      *                FOR CASES ALREADY TRANSFERRED TO SMS AND CASES *         
      *                PENDING TRANSFER TO SMS FOR MONTH              *         
      *                                                               *         
      *             1) INPUT FILE IN DUE-DATE ORDER                   *         
      *             2) FOR RECORDS OUTREC FROM F410 , DTE-INTEREST IS *         
      *                KEY-ISSUE DATE , DUE-DATE IS BLANK             *         
      *                                                               *         
      *  INPUT FILES:                                                 *         
      *        1. SY02F001                                            *         
      *        2. BP13FD38 (SORT BY REGN/DUE-DATE)                    *         
      *        3. BP13KD00                                            *         
      *        4. BP13KD03                                            *         
      *                                                               *         
      *  OUTPUT FILE:                                                 *         
      *        1. BP13LC73                                            *         
      *                                                               *         
      *  CHG-NO  BY   DATE     DESCRIPTION                            *         
      * -------- ---- -------- -------------------------------------- *         
      * BP133612 JB8  22/05/09 NEW PROGRAM.                           *         
      *****************************************************************         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13FD38 ASSIGN TO BP13FD38.                                  
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                      ACCESS MODE IS RANDOM                                     
                     ORGANIZATION IS INDEXED                                    
                       RECORD KEY IS KD00-KEY-FLD                               
                      FILE STATUS IS KD00-STATUS.                               
                                                                                
           SELECT BP13KD03 ASSIGN TO BP13KD03                                   
                      ACCESS MODE IS RANDOM                                     
                     ORGANIZATION IS INDEXED                                    
                       RECORD KEY IS KD03-KEY-FLD                               
                      FILE STATUS IS KD03-STATUS.                               
                                                                                
           SELECT BP13LC73 ASSIGN TO BP13LC73.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FD38                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FD38.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD03                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD03.                                                           
                                                                                
       FD   BP13LC73                                                            
            BLOCK  CONTAINS    0 RECORDS                                        
            RECORD CONTAINS  132 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   LC73-PRTREC        PIC X(132).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FST-REC                     PIC X        VALUE SPACE.             
           88  FST-REC                                 VALUE 'Y'.               
       01  WS-LST-REC                     PIC X        VALUE SPACE.             
           88  LST-REC                                 VALUE 'Y'.               
       01  WS-FND-KD00                    PIC X        VALUE SPACE.             
           88  FND-KD00                                VALUE 'Y'.               
       01  WS-FND-KD03                    PIC X        VALUE SPACE.             
           88  FND-KD03                                VALUE 'Y'.               
       01  WS-EOF-FD38                    PIC X        VALUE SPACE.             
           88  EOF-FD38                                VALUE 'Y'.               
       01  CNT-FD38-READ                  PIC 9(05)    VALUE ZEROES.            
       01  CNT-KD00-FND                   PIC 9(05)    VALUE ZEROES.            
       01  CNT-KD03-FND                   PIC 9(05)    VALUE ZEROES.            
       01  CNT-LC73-WRITE                 PIC 9(05)    VALUE ZEROES.            
       01  CNT-BYPASS                     PIC 9(05)    VALUE ZEROES.            
       01  KD00-STATUS                    PIC 9(02)    VALUE ZEROES.            
       01  KD03-STATUS                    PIC 9(02)    VALUE ZEROES.            
       01  WS-CURR-DATE                   PIC X(08)    VALUE SPACES.            
       01  WS-PREV-REGN                   PIC X(08)    VALUE SPACES.            
       01  WS-PREV-PP                     PIC X(02)    VALUE SPACES.            
       01  WS-PREV-DTE-DUE                PIC X(08)    VALUE SPACES.            
       01  WS-NUM-SCH-ACCT                PIC X(11)    VALUE SPACES.            
       01  WS-DTE-EFF                     PIC X(08)    VALUE SPACES.            
       01  WS-AMT-LOAN-BAL                PIC 9(07)V99 VALUE ZEROES.            
       01  WS-AMT-INT-NEWLOAN             PIC 9(07)V99 VALUE ZEROES.            
       01  WS-AMT-LOAN-DISBURSED          PIC 9(07)V99 VALUE ZEROES.            
       01  WS-AMT-PD-LN-DISBURSED         PIC 9(07)V99 VALUE ZEROES.            
       01  WS-AMT-TOT-TRF-SMS             PIC 9(08)V99 VALUE ZEROES.            
       01  WS-AMT-TOT-BF-22ND             PIC 9(08)V99 VALUE ZEROES.            
       01  WS-AMT-TOT-AF-22ND             PIC 9(08)V99 VALUE ZEROES.            
       01  WS-AMT-TOTAL                   PIC 9(08)V99 VALUE ZEROES.            
       01  WS-AMT-TOTAL-FINAL             PIC 9(10)V99 VALUE ZEROES.            
       01  WS-PAGE-NO                     PIC 9(05)    VALUE ZEROES.            
       01  WS-SNO                         PIC 9(05)    VALUE ZEROES.            
       01  WS-REC-NO                      PIC 9(02)    VALUE 60.                
       01  REPORT-TYPE                    PIC X(01)    VALUE SPACE.             
       01  WS-TRF-SMS-FST                 PIC X(01)    VALUE 'Y'.               
           88  FST-TRF-SMS                             VALUE 'Y'.               
       01  WS-BF-22ND-FST                 PIC X(01)    VALUE 'Y'.               
           88  FST-BF-22ND                             VALUE 'Y'.               
       01  WS-AF-22ND-FST                 PIC X(01)    VALUE 'Y'.               
           88  FST-AF-22ND                             VALUE 'Y'.               
                                                                                
      ******************************************************************        
      *  LAYOUT OF REPORT                                              *        
      ******************************************************************        
       01  LC73-HEADER01.                                                       
           05  LC73-REPORT-TITLE   PIC X(8)      VALUE SPACES.                  
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LC73-PRT-DATE       PIC X(10)  VALUE SPACES.                     
           05  FILLER              PIC X(2)   VALUE SPACES.                     
           05  FILLER              PIC X(7)   VALUE 'PAGE : '.                  
           05  LC73-PRT-PAGE       PIC ZZZZ9  VALUE ZEROES.                     
                                                                                
       01  LC73-HEADER02.                                                       
           05  FILLER              PIC X(43)  VALUE SPACES .                    
           05  FILLER              PIC X(45)  VALUE                             
               'REPORT ON PROJECTION OF MONTH-END LOAN AMOUNT'.                 
           05  FILLER              PIC X(44)  VALUE SPACES .                    
                                                                                
       01  LC73-HEADER03.                                                       
           05  FILLER              PIC X(48)  VALUE  SPACES.                    
           05  FILLER              PIC X(35)  VALUE                             
               '    CASES ALREADY TRANSFERED TO SMS'.                           
           05  FILLER              PIC X(49)  VALUE  SPACES.                    
                                                                                
       01  LC73-HEADER04.                                                       
           05  FILLER              PIC X(132) VALUE                             
               ' SNO  REGN NO    HDB REF NO   LOAN BALANCE  TP DATE   PP        
      -        ' DUE DATE   DISBURSED   PEND DISBURS INTEREST   TOTAL LO        
      -        'AN    TRF-TO-SMS   '.                                           
                                                                                
       01  LC73-HEADER05.                                                       
           05  FILLER              PIC X(05)  VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(08)  VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(14)  VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(12)  VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(10)  VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(02)  VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(10)  VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(12)  VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(12)  VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(09)  VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(13)  VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(10)  VALUE ALL '-'.                    
                                                                                
       01  LC73-DETAIL.                                                         
           05  LC73-PRT-SNO        PIC ZZZZZ.                                   
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  LC73-PRT-REGN       PIC X(08).                                   
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  LC73-PRT-HDB-REF    PIC X(14).                                   
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  LC73-PRT-LOAN-BAL   PIC $,$$$,$$$.$$.                            
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  LC73-PRT-TP-DATE    PIC X(10).                                   
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  LC73-PRT-PP         PIC X(02).                                   
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  LC73-PRT-DUE-DATE   PIC X(10).                                   
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  LC73-PRT-DISBURSED  PIC $,$$$,$$$.$$.                            
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  LC73-PRT-PEND-DISB  PIC $,$$$,$$$.$$.                            
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  LC73-PRT-INTEREST   PIC $$,$$$.$$.                               
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  LC73-PRT-TOT-LOAN   PIC $$,$$$,$$$.$$.                           
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  LC73-PRT-EFF-DATE   PIC X(10).                                   
                                                                                
       01  LC73-SUB-TITLE1.                                                     
           05  FILLER              PIC X(28)  VALUE                             
               ' 1) TRANSFERRED IN THE MONTH'.                                  
                                                                                
       01  LC73-SUB-TITLE2.                                                     
           05  FILLER              PIC X(42)  VALUE                             
               ' 2) ADDITIONAL LOAN DISBURSED IN THE MONTH'.                    
                                                                                
       01  LC73-SUB-TITLE3.                                                     
           05  FILLER              PIC X(48)  VALUE                             
               ' 3) ADDITIONAL LOAN TO BE DISBURSED IN THE MONTH'.              
                                                                                
       01  LC73-SUB-TOTAL1.                                                     
           05  FILLER              PIC X(14)  VALUE ' SUB   TOTAL :'.           
           05  FILLER              PIC X(87)  VALUE  SPACES.                    
           05  LC73-PRT-SUB-TOT1   PIC $,$$$,$$$,$$$.$$.                        
           05  FILLER              PIC X(14)  VALUE  SPACES.                    
                                                                                
       01  LC73-SUB-TOTAL2.                                                     
           05  FILLER              PIC X(14)  VALUE ' SUB   TOTAL :'.           
           05  FILLER              PIC X(87)  VALUE  SPACES.                    
           05  LC73-PRT-SUB-TOT2   PIC $,$$$,$$$,$$$.$$.                        
           05  FILLER              PIC X(14)  VALUE  SPACES.                    
                                                                                
       01  LC73-SUB-TOTAL3.                                                     
           05  FILLER              PIC X(14)  VALUE ' SUB   TOTAL :'.           
           05  FILLER              PIC X(87)  VALUE  SPACES.                    
           05  LC73-PRT-SUB-TOT3   PIC $,$$$,$$$,$$$.$$.                        
           05  FILLER              PIC X(14)  VALUE  SPACES.                    
                                                                                
       01  LC73-GRT-TOTAL.                                                      
           05  FILLER              PIC X(14)  VALUE ' GRANT TOTAL :'.           
           05  FILLER              PIC X(87)  VALUE  SPACES.                    
           05  LC73-PRT-TOTAL      PIC $,$$$,$$$,$$$.$$.                        
           05  FILLER              PIC X(14)  VALUE  SPACES.                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      *        MAIN PROCESS                                            *        
      ******************************************************************        
       MAIN-PROCESS.                                                            
           PERFORM 1000-OPEN-ROUTINE            THRU 1000-EXIT.                 
           PERFORM 2000-READ-BP13FD38           THRU 2000-EXIT.                 
           PERFORM 3000-PROCESS-RECORDS         THRU 3000-EXIT                  
             UNTIL EOF-FD38.                                                    
           PERFORM 5300-GENERATE-TOTAL          THRU 5300-EXIT.                 
           PERFORM 9000-CLOSE-ROUTINE           THRU 9000-EXIT.                 
                                                                                
      *==================*                                                      
       1000-OPEN-ROUTINE.                                                       
      *==================*                                                      
                                                                                
           OPEN INPUT  SY02F001                                                 
                       BP13FD38                                                 
                       BP13KD00                                                 
                       BP13KD03                                                 
                OUTPUT BP13LC73.                                                
                                                                                
           IF KD00-STATUS NOT = 0 AND = 97                                      
              DISPLAY ' OPENING ERROR, BP13KD00 STATUS ' KD00-STATUS            
              MOVE KD00-STATUS                TO RETURN-CODE                    
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF KD03-STATUS NOT = 0 AND = 97                                      
              DISPLAY ' OPENING ERROR, BP13KD03 STATUS ' KD03-STATUS            
              MOVE KD03-STATUS                TO RETURN-CODE                    
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT              TO WS-CURR-DATE.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==================*                                                      
       2000-READ-BP13FD38.                                                      
      *==================*                                                      
                                                                                
           READ BP13FD38                                                        
             AT END                                                             
                MOVE 'Y'         TO WS-EOF-FD38                                 
                GO               TO 2000-EXIT                                   
           END-READ.                                                            
                                                                                
           ADD 1                 TO CNT-FD38-READ.                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       3000-PROCESS-RECORDS.                                                    
      *=====================*                                                   
                                                                                
           IF FD38-NUM-SCHEDULE = 'XX'                                          
              IF FD38-DTE-INTEREST(1:6) NOT = WS-CURR-DATE(1:6)                 
                 PERFORM 2000-READ-BP13FD38    THRU 2000-EXIT                   
                 GO TO 3000-EXIT                                                
              END-IF                                                            
           ELSE                                                                 
              IF FD38-DTE-DUE(1:6) NOT = WS-CURR-DATE(1:6)                      
                 PERFORM 2000-READ-BP13FD38    THRU 2000-EXIT                   
                 GO TO 3000-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE SPACES                           TO LC73-DETAIL                 
           INITIALIZE                               LC73-DETAIL.                
                                                                                
           IF FD38-AMT-LOAN-BAL NOT NUMERIC                                     
              MOVE 0                             TO FD38-AMT-LOAN-BAL           
           END-IF.                                                              
                                                                                
           ADD 1                                 TO WS-SNO                      
           MOVE WS-SNO                           TO LC73-PRT-SNO                
                                                                                
           MOVE FD38-NUM-REGN                    TO LC73-PRT-REGN               
                                                                                
           IF FD38-NUM-SCHEDULE = 'XX'                                          
              PERFORM 3100-PROCESS-TRF-SMS     THRU 3100-EXIT                   
           ELSE                                                                 
              PERFORM 3200-PROCESS-DISBURSED   THRU 3200-EXIT                   
           END-IF.                                                              
                                                                                
           COMPUTE WS-AMT-TOTAL-FINAL = WS-AMT-TOTAL-FINAL +                    
                                        WS-AMT-TOTAL.                           
                                                                                
           PERFORM 5100-GENERATE-REPORT        THRU 5100-EXIT.                  
                                                                                
           PERFORM 2000-READ-BP13FD38          THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       3100-PROCESS-TRF-SMS.                                                    
      ***********************                                                   
                                                                                
           PERFORM 4100-READ-BP13KD03          THRU 4100-EXIT.                  
                                                                                
           IF FD38-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              STRING FD38-NUM-SCH-ACC(1:4) '-'                                  
                     FD38-NUM-SCH-ACC(5:4) '-'                                  
                     FD38-NUM-SCH-ACC(9:1) '-'                                  
                     FD38-NUM-SCH-ACC(10:2)                                     
                     DELIMITED BY SIZE INTO LC73-PRT-HDB-REF                    
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           COMPUTE WS-AMT-TOT-TRF-SMS = WS-AMT-TOT-TRF-SMS +                    
                                        FD38-AMT-LOAN-BAL.                      
                                                                                
           COMPUTE WS-AMT-TOTAL = FD38-AMT-LOAN-BAL                             
                                                                                
           MOVE FD38-AMT-LOAN-BAL                TO LC73-PRT-LOAN-BAL           
                                                    LC73-PRT-TOT-LOAN           
           IF FD38-DTE-INTEREST NOT = SPACES AND LOW-VALUES                     
              STRING FD38-DTE-INTEREST(7:2) '/'                                 
                     FD38-DTE-INTEREST(5:2) '/'                                 
                     FD38-DTE-INTEREST(1:4)                                     
                     DELIMITED BY SIZE INTO LC73-PRT-TP-DATE                    
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           IF KD03-DTE-TRANSFER-SMS NOT = SPACES AND LOW-VALUES                 
              STRING KD03-DTE-TRANSFER-SMS(7:2) '/'                             
                     KD03-DTE-TRANSFER-SMS(5:2) '/'                             
                     KD03-DTE-TRANSFER-SMS(1:4)                                 
                     DELIMITED BY SIZE INTO LC73-PRT-EFF-DATE                   
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           IF FST-TRF-SMS                                                       
              PERFORM 5020-WRITE-SUB-TITLE1    THRU 5020-EXIT                   
              MOVE 'N'                           TO WS-TRF-SMS-FST              
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************                                                 
       3200-PROCESS-DISBURSED.                                                  
      *************************                                                 
                                                                                
           PERFORM 4000-READ-BP13KD00          THRU 4000-EXIT.                  
                                                                                
           IF KD00-NUM-SCH-ACCNT NOT = SPACES AND LOW-VALUES                    
              STRING KD00-NUM-SCH-ACCNT(1:4) '-'                                
                     KD00-NUM-SCH-ACCNT(5:4) '-'                                
                     KD00-NUM-SCH-ACCNT(9:1) '-'                                
                     KD00-NUM-SCH-ACCNT(10:2)                                   
                     DELIMITED BY SIZE INTO LC73-PRT-HDB-REF                    
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           MOVE FD38-NUM-SCHEDULE                TO LC73-PRT-PP.                
                                                                                
           IF FD38-DTE-DUE NOT = SPACES AND LOW-VALUES                          
              STRING FD38-DTE-DUE(7:2) '/'                                      
                     FD38-DTE-DUE(5:2) '/'                                      
                     FD38-DTE-DUE(1:4)                                          
                     DELIMITED BY SIZE INTO LC73-PRT-DUE-DATE                   
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           IF FD38-DTE-DUE(7:2) < WS-CURR-DATE(7:2)                             
              MOVE FD38-AMT-LOAN-NEW             TO LC73-PRT-DISBURSED          
           ELSE                                                                 
              MOVE FD38-AMT-LOAN-NEW             TO LC73-PRT-PEND-DISB          
           END-IF.                                                              
                                                                                
           MOVE FD38-AMT-INTEREST-NEWLOAN        TO LC73-PRT-INTEREST.          
                                                                                
           COMPUTE WS-AMT-TOTAL = FD38-AMT-LOAN-NEW +                           
                                  FD38-AMT-INTEREST-NEWLOAN                     
           MOVE WS-AMT-TOTAL                     TO LC73-PRT-TOT-LOAN.          
                                                                                
           IF FD38-DTE-DUE(7:2) < WS-CURR-DATE(7:2)                             
              COMPUTE WS-AMT-TOT-BF-22ND = WS-AMT-TOT-BF-22ND +                 
                                           WS-AMT-TOTAL                         
           ELSE                                                                 
              COMPUTE WS-AMT-TOT-AF-22ND = WS-AMT-TOT-AF-22ND +                 
                                           WS-AMT-TOTAL                         
           END-IF.                                                              
                                                                                
           IF KD00-DTE-TRANSFER-SMS NOT = SPACES AND LOW-VALUES                 
              STRING KD00-DTE-TRANSFER-SMS(7:2) '/'                             
                     KD00-DTE-TRANSFER-SMS(5:2) '/'                             
                     KD00-DTE-TRANSFER-SMS(1:4)                                 
                     DELIMITED BY SIZE INTO LC73-PRT-EFF-DATE                   
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           IF FD38-DTE-DUE(7:2) < WS-CURR-DATE(7:2)                             
              IF FST-BF-22ND                                                    
                 PERFORM 5030-WRITE-SUB-TITLE2 THRU 5030-EXIT                   
                 MOVE 'N'                        TO WS-BF-22ND-FST              
              END-IF                                                            
           ELSE                                                                 
              IF FST-AF-22ND                                                    
                 PERFORM 5040-WRITE-SUB-TITLE3 THRU 5040-EXIT                   
                 MOVE 'N'                        TO WS-AF-22ND-FST              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       4000-READ-BP13KD00.                                                      
      *===================*                                                     
                                                                                
           MOVE SPACES                           TO BP13KD00-REC.               
           INITIALIZE                               BP13KD00-REC.               
           MOVE 'N'                              TO WS-FND-KD00                 
                                                    WS-FND-KD03.                
           MOVE SPACE                            TO REPORT-TYPE.                
                                                                                
           MOVE FD38-NUM-REGN                    TO KD00-NUM-REGN.              
                                                                                
           READ BP13KD00.                                                       
                                                                                
           EVALUATE KD00-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    ADD 1                        TO CNT-KD00-FND                
                    MOVE 'Y'                     TO WS-FND-KD00                 
               WHEN 23                                                          
                    PERFORM 4100-READ-BP13KD03 THRU 4100-EXIT                   
                    IF FND-KD03                                                 
                       ADD 1                     TO CNT-KD03-FND                
                       MOVE BP13KD03-REC         TO BP13KD00-REC                
                    ELSE                                                        
                       MOVE SPACES               TO BP13KD00-REC                
                       INITIALIZE                   BP13KD00-REC                
                       DISPLAY 'RECORD NOT FND IN KD03 : ' FD38-NUM-REGN        
                    END-IF                                                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13KD00 - ERROR READING : '                       
                            KD00-STATUS                                         
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       4100-READ-BP13KD03.                                                      
      *===================*                                                     
                                                                                
           MOVE SPACES                           TO BP13KD03-REC.               
           INITIALIZE                               BP13KD03-REC.               
                                                                                
           MOVE FD38-NUM-REGN                    TO KD03-NUM-REGN.              
                                                                                
           READ BP13KD03.                                                       
                                                                                
           EVALUATE KD03-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    IF (KD03-DTE-TRANSFER-SMS NOT = SPACES AND                  
                                                    LOW-VALUES) AND             
                       (KD03-DTE-EFF-TRANSFER     = SPACES OR                   
                                                    LOW-VALUES) AND             
                       (KD03-DTE-REPUDIATE        = SPACES OR                   
                                                    LOW-VALUES)                 
                       MOVE 'Y'                  TO WS-FND-KD03                 
                    ELSE                                                        
                       ADD 1                     TO CNT-BYPASS                  
                    END-IF                                                      
               WHEN 23                                                          
                    ADD 1                        TO CNT-BYPASS                  
               WHEN OTHER                                                       
                    DISPLAY 'BP13KD03 - ERROR READING : '                       
                            KD03-STATUS                                         
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       5010-PRINT-HEADER.                                                       
      *====================*                                                    
                                                                                
           MOVE 9                                 TO WS-REC-NO.                 
           ADD 1                                  TO WS-PAGE-NO.                
           MOVE WS-PAGE-NO                        TO LC73-PRT-PAGE.             
           STRING WS-CURR-DATE(7:2) '/'                                         
                  WS-CURR-DATE(5:2) '/'                                         
                  WS-CURR-DATE(1:4)                                             
                  DELIMITED BY SIZE INTO LC73-PRT-DATE                          
           END-STRING.                                                          
           MOVE 'BP13LC73'                        TO LC73-REPORT-TITLE.         
                                                                                
           MOVE SPACES                            TO LC73-PRTREC.               
           WRITE LC73-PRTREC AFTER PAGE.                                        
           WRITE LC73-PRTREC FROM LC73-HEADER01 AFTER 3.                        
           WRITE LC73-PRTREC FROM LC73-HEADER02.                                
           WRITE LC73-PRTREC FROM LC73-HEADER03.                                
           WRITE LC73-PRTREC FROM LC73-HEADER04 AFTER 2.                        
           WRITE LC73-PRTREC FROM LC73-HEADER05.                                
                                                                                
       5010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *======================*                                                  
       5020-WRITE-SUB-TITLE1.                                                   
      *======================*                                                  
                                                                                
           ADD 2                                  TO WS-REC-NO.                 
           IF WS-REC-NO > 60                                                    
              PERFORM 5010-PRINT-HEADER         THRU 5010-EXIT                  
              ADD 2                               TO WS-REC-NO                  
           END-IF.                                                              
                                                                                
           WRITE LC73-PRTREC FROM LC73-SUB-TITLE1 AFTER 2.                      
      **                                                                        
      ** IF NOT END OF THE PAGE , WRITE 1 MORE EMPTY LINE                       
      **                                                                        
           ADD 1                                  TO WS-REC-NO                  
           IF WS-REC-NO < 60                                                    
              MOVE SPACES                         TO LC73-PRTREC                
              WRITE LC73-PRTREC                                                 
           END-IF.                                                              
                                                                                
       5020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *======================*                                                  
       5030-WRITE-SUB-TITLE2.                                                   
      *======================*                                                  
      **                                                                        
      ** IF TRF-SMS RECORDS PRINTED , WRITE TRF-SMS TOTAL                       
      **                                                                        
           IF NOT FST-TRF-SMS                                                   
              ADD 2                               TO WS-REC-NO                  
              IF WS-REC-NO > 60                                                 
                 PERFORM 5010-PRINT-HEADER      THRU 5010-EXIT                  
                 ADD 2                            TO WS-REC-NO                  
              END-IF                                                            
                                                                                
              MOVE WS-AMT-TOT-TRF-SMS             TO LC73-PRT-SUB-TOT1          
              WRITE LC73-PRTREC FROM LC73-SUB-TOTAL1 AFTER 2                    
           END-IF.                                                              
      **                                                                        
      ** WRITE DISBURED RECORDS                                                 
      **                                                                        
           ADD 2                                  TO WS-REC-NO.                 
           IF WS-REC-NO > 60                                                    
              PERFORM 5010-PRINT-HEADER         THRU 5010-EXIT                  
              ADD 2                               TO WS-REC-NO                  
           END-IF.                                                              
                                                                                
           WRITE LC73-PRTREC FROM LC73-SUB-TITLE2 AFTER 2.                      
      **                                                                        
      ** IF NOT END OF THE PAGE , WRITE 1 MORE EMPTY LINE                       
      **                                                                        
           ADD 1                                  TO WS-REC-NO                  
           IF WS-REC-NO < 60                                                    
              MOVE SPACES                         TO LC73-PRTREC                
              WRITE LC73-PRTREC                                                 
           END-IF.                                                              
                                                                                
       5030-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *======================*                                                  
       5040-WRITE-SUB-TITLE3.                                                   
      *======================*                                                  
      **                                                                        
      ** IF DISBURED RECORDS PRINTED , WRITE DISBURSED TOTAL                    
      **                                                                        
           ADD 2                                  TO WS-REC-NO                  
           IF WS-REC-NO > 60                                                    
              PERFORM 5010-PRINT-HEADER         THRU 5010-EXIT                  
              ADD 2                               TO WS-REC-NO                  
           END-IF                                                               
                                                                                
           IF NOT FST-BF-22ND                                                   
              MOVE WS-AMT-TOT-BF-22ND             TO LC73-PRT-SUB-TOT2          
              WRITE LC73-PRTREC FROM LC73-SUB-TOTAL2 AFTER 2                    
           ELSE                                                                 
              IF NOT FST-TRF-SMS                                                
                 MOVE WS-AMT-TOT-TRF-SMS          TO LC73-PRT-SUB-TOT1          
                 WRITE LC73-PRTREC FROM LC73-SUB-TOTAL1 AFTER 2                 
              ELSE                                                              
                 NEXT SENTENCE                                                  
              END-IF                                                            
           END-IF                                                               
      **                                                                        
      ** WRITE TO BE DISBURED RECORDS                                           
      **                                                                        
           ADD 2                                  TO WS-REC-NO.                 
           IF WS-REC-NO > 60                                                    
              PERFORM 5010-PRINT-HEADER         THRU 5010-EXIT                  
              ADD 2                               TO WS-REC-NO                  
           END-IF.                                                              
                                                                                
           WRITE LC73-PRTREC FROM LC73-SUB-TITLE3 AFTER 2.                      
      **                                                                        
      ** IF NOT END OF THE PAGE , WRITE 1 MORE EMPTY LINE                       
      **                                                                        
           ADD 1                                  TO WS-REC-NO                  
           IF WS-REC-NO < 60                                                    
              MOVE SPACES                         TO LC73-PRTREC                
              WRITE LC73-PRTREC                                                 
           END-IF.                                                              
                                                                                
       5040-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================*                                                 
       5100-GENERATE-REPORT.                                                    
      *=======================*                                                 
                                                                                
           ADD 1                                  TO WS-REC-NO.                 
           IF WS-REC-NO > 60                                                    
              PERFORM 5010-PRINT-HEADER         THRU 5010-EXIT                  
              ADD 1                               TO WS-REC-NO                  
           END-IF.                                                              
                                                                                
           WRITE LC73-PRTREC FROM LC73-DETAIL.                                  
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       5300-GENERATE-TOTAL.                                                     
      *====================*                                                    
      **                                                                        
      ** IF TO BE DISBURED RECORDS PRINTED , WRITE TO BE DISBURSED TOTAL        
      **                                                                        
           ADD 2                                  TO WS-REC-NO                  
           IF WS-REC-NO > 60                                                    
              PERFORM 5010-PRINT-HEADER         THRU 5010-EXIT                  
              ADD 2                               TO WS-REC-NO                  
           END-IF.                                                              
                                                                                
           IF NOT FST-AF-22ND                                                   
              MOVE WS-AMT-TOT-AF-22ND             TO LC73-PRT-SUB-TOT3          
              WRITE LC73-PRTREC FROM LC73-SUB-TOTAL3 AFTER 2                    
           ELSE                                                                 
              IF NOT FST-BF-22ND                                                
                 MOVE WS-AMT-TOT-BF-22ND          TO LC73-PRT-SUB-TOT2          
                 WRITE LC73-PRTREC FROM LC73-SUB-TOTAL2 AFTER 2                 
              ELSE                                                              
                 IF NOT FST-TRF-SMS                                             
                    MOVE WS-AMT-TOT-TRF-SMS       TO LC73-PRT-SUB-TOT1          
                    WRITE LC73-PRTREC FROM LC73-SUB-TOTAL1 AFTER 2              
                 ELSE                                                           
                    GO TO 5300-EXIT                                             
                 END-IF                                                         
           END-IF.                                                              
      **                                                                        
      ** WRITE FINAL TOTAL                                                      
      **                                                                        
           ADD 2                                  TO WS-REC-NO.                 
           IF WS-REC-NO > 60                                                    
              PERFORM 5010-PRINT-HEADER         THRU 5010-EXIT                  
              ADD 1                               TO WS-REC-NO                  
           END-IF.                                                              
                                                                                
           MOVE WS-AMT-TOTAL-FINAL                TO LC73-PRT-TOTAL.            
           WRITE LC73-PRTREC FROM LC73-GRT-TOTAL AFTER 2.                       
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==================*                                                      
       9000-CLOSE-ROUTINE.                                                      
      *==================*                                                      
                                                                                
           DISPLAY '************  BP13CCAA  ************'.                      
           DISPLAY 'NO OF FD38 REC READ  : ' CNT-FD38-READ.                     
           DISPLAY 'NO OF KD00 REC READ  : ' CNT-KD00-FND.                      
           DISPLAY 'NO OF KD03 REC READ  : ' CNT-KD03-FND.                      
           DISPLAY 'NO OF LC73 WRITTEN   : ' CNT-LC73-WRITE.                    
                                                                                
           CLOSE SY02F001                                                       
                 BP13FD38                                                       
                 BP13KD00                                                       
                 BP13KD03                                                       
                 BP13LC73.                                                      
                                                                                
           IF KD00-STATUS NOT = 0 AND 97                                        
               DISPLAY ' CLOSING ERROR, BP13KD00 STATUS ' KD00-STATUS.          
                                                                                
           IF KD03-STATUS NOT = 0 AND 97                                        
               DISPLAY ' CLOSING ERROR, BP13KD03 STATUS ' KD03-STATUS.          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
