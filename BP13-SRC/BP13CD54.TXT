      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13CD54.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  20/11/08.                                                 
                                                                                
      *========================================================*                
      *    SYSTEM NAME   :   SYSTEM OF COMMITMENT              *                
      *    SYSTEM ID     :   BP13                              *                
      *                                                        *                
      *    OBJECTIVE     :   FOR DBSS FLAT :                   *                
      *                  :   UNDER SUB-TRANSFER, A NEW LESSEE  *                
      *                      FOR SCH-ACC-NO WAS CREATED. WHEN  *                
      *                      SUB-TRANSFER IS EFFECTIVE, PASS   *                
      *                      NEW LESSEE CODE TO SMS.           *                
      *     INPUT  FILE  :                                     *                
      *        1.  BP13FD00                                    *                
      *        2.  BP13F800                                    *                
      *     OUTPUT FILE  :                                     *                
      *        1.  AB03F021                                    *                
      *                                                        *                
      * CHG REQ  DATE      BY   DESCRIPTION                    *                
      * =======  ========  ===  ============================   *                
      * BP133091 20112008  JB8  NEW PGM                        *                
      * BP133505 03122008  JB8  DELETE BP13KD15 USING OLD-LSE  *                
      * BP133505 08012009  JB8  CHECK DTE-EFF = CURRENT-DATE   *                
      * BP133505 16012009  JB8  OUTPUT NEW REGN TO KD15        *                
      * BP13     16032009  JB8  CHANGE INPUT TO BP13FD00       *                
      * BP133662 03082009  JB8  TO CATER FOR SUB-TRANSFER      *                
      * BP133612 04082009  JB8  ONLY PROCESS ON CURRENT DATE   *                
      *========================================================*                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD00 ASSIGN       TO BP13FD00.                            
           SELECT BP13F800 ASSIGN       TO BP13F800.                            
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS WS-K820-STATUS.                      
           SELECT BP13KB20 ASSIGN       TO BP13KB20                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KB20-KEY-FLD                         
                           FILE STATUS  IS WS-KB20-STATUS.                      
           SELECT BP13KD15 ASSIGN       TO BP13KD15                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KD15-KEY-FLD                         
                          ALTERNATE KEY IS KD15-ALT-KEY2                        
                           FILE STATUS  IS WS-KD15-STATUS.                      
           SELECT AB03F021 ASSIGN       TO AB03F021.                            
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FD00                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 1600 CHARACTERS                                     
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BP13FD00.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400   CHARACTERS.                                   
       COPY BP13K820.                                                           
                                                                                
       FD   BP13KB20                                                            
            RECORD CONTAINS 3300  CHARACTERS.                                   
       COPY BP13KB20.                                                           
                                                                                
       FD   BP13KD15                                                            
            RECORD CONTAINS 300   CHARACTERS.                                   
       COPY BP13KD15.                                                           
                                                                                
       FD   AB03F021                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 150 CHARACTERS                                      
            LABEL  RECORDS  ARE  STANDARD.                                      
       01  AB03F021-REC.                                                        
           05  F021-CDE-TRANS-FIS                PIC X(01).                     
           05  FILLER                            PIC X(02).                     
           05  F021-NUM-HDB-REF                  PIC X(11).                     
           05  FILLER                            PIC X(70).                     
           05  F021-CDE-REASON-FOR-CHGE          PIC X(02).                     
           05  F021-DTE-CANCEL-WEF               PIC X(08).                     
           05  F021-DATA-ENTRY-TIME              PIC X(07).                     
           05  F021-DTE-UPDTE                    PIC X(08).                     
           05  F021-NUM-HDB-REF-EXLESSEE         PIC X(11).                     
           05  F021-CDE-SYSTEM                   PIC X(04).                     
           05  FILLER                            PIC X(25).                     
           05  F021-CDE-ACTION                   PIC X(01).                     
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-PREV-MTH               PIC X(06) VALUE ZEROES.                    
       01  WS-CUR-DATE               PIC 9(08) VALUE ZEROES.                    
       01  WS-CUR-TIME               PIC 9(08) VALUE ZEROES.                    
       01  WS-CUR-TIME-X REDEFINES WS-CUR-TIME PIC X(08).                       
       01  WS-K820-STATUS            PIC 9(02) VALUE ZEROES.                    
       01  WS-KB20-STATUS            PIC 9(02) VALUE ZEROES.                    
       01  WS-KD15-STATUS            PIC 9(02) VALUE ZEROES.                    
       01  WS-FD00-READ              PIC 9(07) VALUE ZEROES.                    
       01  WS-F800-READ              PIC 9(07) VALUE ZEROES.                    
       01  WS-KD15-DELETE            PIC 9(07) VALUE ZEROES.                    
       01  WS-KD15-WRITE             PIC 9(07) VALUE ZEROES.                    
       01  WS-KB20-DELETE            PIC 9(07) VALUE ZEROES.                    
       01  WS-KB20-WRITE             PIC 9(07) VALUE ZEROES.                    
       01  WS-F021-WRITE             PIC 9(07) VALUE ZEROES.                    
       01  WS-PREV-REGN              PIC X(08) VALUE SPACES.                    
       01  WS-PREV-KB20-REC          PIC X(3300) VALUE SPACES.                  
       01  WS-NRIC                   PIC X(09) VALUE SPACES.                    
       01  WS-PREV-SCH-ACCT.                                                    
           05  WS-PREV-SCH-ACC       PIC X(09).                                 
           05  WS-OLD-LESSEE-CODE    PIC X(02).                                 
       01  WS-PREV-DTE-EFF-TRANSFER  PIC X(08).                                 
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      **********************                                                    
       1000-MAIN.                                                               
      **********************                                                    
           PERFORM  2000-OPENING-ROUTINE    THRU  2000-EXIT.                    
           PERFORM  3000-READ-FD00-REC      THRU  3000-EXIT.                    
           PERFORM  3500-READ-F800-REC      THRU  3500-EXIT.                    
           PERFORM  4000-PROCESS-FD00-REC   THRU  4000-EXIT                     
             UNTIL  FD00-NUM-REGN = HIGH-VALUES                                 
               AND  F800-NUM-DBSS-REGN-SUBTRF = HIGH-VALUES                     
           PERFORM  9000-CLOSING-ROUTINE    THRU  9000-EXIT.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       2000-OPENING-ROUTINE.                                                    
      **********************                                                    
           OPEN INPUT  BP13FD00                                                 
                       BP13F800                                                 
                       SY02F001                                                 
                       BP13K820                                                 
                I-O    BP13KB20                                                 
                       BP13KD15                                                 
                OUTPUT AB03F021.                                                
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K820 , '                               
                      'STATUS = ' WS-K820-STATUS                                
              MOVE WS-K820-STATUS TO RETURN-CODE                                
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-KB20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KB20 , '                               
                      'STATUS = ' WS-KB20-STATUS                                
              MOVE WS-KB20-STATUS TO RETURN-CODE                                
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-KD15-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KD15 , '                               
                      'STATUS = ' WS-KD15-STATUS                                
              MOVE WS-KD15-STATUS TO RETURN-CODE                                
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-LAST-DAY-PREV(1:6)  TO WS-PREV-MTH.                    
           MOVE F001-DTE-CURRENT             TO WS-CUR-DATE.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *         READ INPUT FILE                                                 
      ******************************************************************        
      **********************                                                    
       3000-READ-FD00-REC.                                                      
      **********************                                                    
           READ BP13FD00 AT END                                                 
                         MOVE HIGH-VALUES  TO FD00-NUM-REGN                     
                         GO TO 3000-EXIT.                                       
                                                                                
           ADD 1 TO WS-FD00-READ.                                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       3500-READ-F800-REC.                                                      
      **********************                                                    
           READ BP13F800 AT END                                                 
                         MOVE HIGH-VALUES  TO F800-NUM-DBSS-REGN-SUBTRF         
                         GO TO 3500-EXIT.                                       
                                                                                
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***********************                                                   
       4000-PROCESS-FD00-REC.                                                   
      ***********************                                                   
                                                                                
           IF FD00-NUM-REGN = F800-NUM-DBSS-REGN-SUBTRF                         
              PERFORM 4500-MAIN-PROCESS        THRU 4500-EXIT                   
              PERFORM 3000-READ-FD00-REC       THRU 3000-EXIT                   
              PERFORM 3500-READ-F800-REC       THRU 3500-EXIT                   
           ELSE                                                                 
              IF FD00-NUM-REGN > F800-NUM-DBSS-REGN-SUBTRF                      
                 PERFORM 3500-READ-F800-REC    THRU 3500-EXIT                   
              ELSE                                                              
                 PERFORM 3000-READ-FD00-REC    THRU 3000-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       4500-MAIN-PROCESS.                                                       
      ********************                                                      
      * FD00 - FD00-NUM-REGN(OLD) , FD00-NUM-SCH-ACCNT(OLD)                     
      * F800 - F800-NUM-REGN(NEW) , F800-NUM-DBSS-REGN-SUBTRF(OLD)              
      *      - F800-NUM-SCH-ACC(NEW)                                            
                                                                                
           IF FD00-NUM-SCH-ACCNT(1:9) = F800-NUM-SCH-ACC(1:9)                   
              IF FD00-DTE-EFF-TRANSFER > '00000000' AND                         
                 FD00-DTE-EFF-TRANSFER = WS-CUR-DATE                            
                 PERFORM 6000-OUTPUT-AB03F021 THRU 6000-EXIT                    
                 PERFORM 7000-READ-BP13KD15         THRU 7000-EXIT              
                 PERFORM 7300-READ-BP13KB20         THRU 7300-EXIT              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************                                                  
       6000-OUTPUT-AB03F021.                                                    
      ************************                                                  
                                                                                
           ACCEPT WS-CUR-TIME FROM TIME.                                        
           MOVE SPACES                   TO AB03F021-REC.                       
           MOVE 'C'                      TO F021-CDE-TRANS-FIS.                 
           MOVE 'OT'                     TO F021-CDE-REASON-FOR-CHGE.           
           MOVE 'BP13'                   TO F021-CDE-SYSTEM.                    
           MOVE 'D'                      TO F021-CDE-ACTION.                    
           MOVE WS-CUR-DATE              TO F021-DTE-UPDTE.                     
           MOVE WS-CUR-TIME-X            TO F021-DATA-ENTRY-TIME.               
           MOVE SPACES                   TO F021-DTE-CANCEL-WEF.                
           MOVE F800-NUM-SCH-ACC         TO F021-NUM-HDB-REF.                   
           MOVE FD00-NUM-SCH-ACCNT       TO F021-NUM-HDB-REF-EXLESSEE.          
                                                                                
           WRITE AB03F021-REC.                                                  
           ADD 1                         TO WS-F021-WRITE.                      
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       7000-READ-BP13KD15.                                                      
      **********************                                                    
                                                                                
           MOVE SPACES                     TO BP13KD15-REC.                     
           INITIALIZE                         BP13KD15-REC.                     
                                                                                
           MOVE FD00-NUM-SCH-ACCNT         TO KD15-NUM-SCH-ACC.                 
                                                                                
           READ BP13KD15 KEY IS KD15-ALT-KEY2.                                  
                                                                                
           IF WS-KD15-STATUS = 00                                               
              PERFORM 7100-DELETE-BP13KD15   THRU 7100-EXIT                     
              PERFORM 7200-CREATE-BP13KD15   THRU 7200-EXIT                     
           ELSE                                                                 
              IF WS-KD15-STATUS = 23                                            
                 DISPLAY 'BP13KD15 - RECORD NOT FOUND : '                       
                         KD15-NUM-SCH-ACC                                       
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13KD15 , STATUS = '                      
                          WS-KD15-STATUS                                        
                 DISPLAY ' SCH-ACC = ' KD15-NUM-SCH-ACC                         
                 GO TO 9000-CLOSING-ROUTINE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       7100-DELETE-BP13KD15.                                                    
      ************************                                                  
                                                                                
           DELETE BP13KD15.                                                     
                                                                                
           IF WS-KD15-STATUS = 00                                               
              ADD 1 TO WS-KD15-DELETE                                           
           ELSE                                                                 
              DISPLAY 'ERROR DELETE BP13KD15 , STATUS = '                       
                       WS-KD15-STATUS                                           
              DISPLAY ' SCH-ACC = ' KD15-NUM-SCH-ACC                            
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       7200-CREATE-BP13KD15.                                                    
      ************************                                                  
                                                                                
           MOVE F800-NUM-SCH-ACC         TO KD15-NUM-SCH-ACC.                   
           MOVE F800-NUM-REGN            TO KD15-NUM-REGN.                      
           MOVE F800-NUM-REGN            TO KD15-NUM-REGN3.                     
                                                                                
           WRITE BP13KD15-REC.                                                  
                                                                                
           IF WS-KD15-STATUS = 00                                               
              ADD 1 TO WS-KD15-WRITE                                            
           ELSE                                                                 
              DISPLAY 'ERROR WRITE BP13KD15 , STATUS = '                        
                       WS-KD15-STATUS                                           
              DISPLAY ' SCH-ACC = ' KD15-NUM-SCH-ACC                            
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       7300-READ-BP13KB20.                                                      
      ************************                                                  
                                                                                
           MOVE FD00-NUM-REGN                  TO KB20-KEY-FLD.                 
                                                                                
           READ BP13KB20.                                                       
                                                                                
           IF WS-KB20-STATUS = 00                                               
              PERFORM 7400-DELETE-BP13KB20   THRU 7400-EXIT                     
              PERFORM 7500-CREATE-BP13KB20   THRU 7500-EXIT                     
           ELSE                                                                 
              IF WS-KB20-STATUS = 23                                            
                 DISPLAY 'BP13KB20 - RECORD NOT FOUND : '                       
                         KB20-KEY-FLD                                           
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13KB20 , STATUS = '                      
                          WS-KB20-STATUS                                        
                 DISPLAY ' SCH-ACC = ' KB20-KEY-FLD                             
                 GO TO 9000-CLOSING-ROUTINE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       7400-DELETE-BP13KB20.                                                    
      ************************                                                  
                                                                                
           MOVE BP13KB20-REC TO WS-PREV-KB20-REC.                               
           DELETE BP13KB20.                                                     
                                                                                
           IF WS-KB20-STATUS = 00                                               
              ADD 1 TO WS-KB20-DELETE                                           
           ELSE                                                                 
              DISPLAY 'ERROR DELETE BP13KB20 , STATUS = '                       
                       WS-KB20-STATUS                                           
              DISPLAY ' SCH-ACC = ' KB20-KEY-FLD                                
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
       7400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       7500-CREATE-BP13KB20.                                                    
      ************************                                                  
                                                                                
           MOVE WS-PREV-KB20-REC         TO BP13KB20-REC.                       
           MOVE F800-NUM-SCH-ACC         TO KB20-NUM-SCH-ACC.                   
           PERFORM 7520-PROCESS-BP13K820 THRU 7520-EXIT.                        
                                                                                
           WRITE BP13KB20-REC.                                                  
                                                                                
           IF WS-KB20-STATUS = 00                                               
              ADD 1 TO WS-KB20-WRITE                                            
           ELSE                                                                 
              DISPLAY 'ERROR WRITE BP13KB20 , STATUS = '                        
                       WS-KB20-STATUS                                           
              DISPLAY ' KEY = ' KB20-KEY-FLD                                    
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
       7500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       7520-PROCESS-BP13K820.                                                   
      ************************                                                  
                                                                                
           MOVE    F800-NUM-REGN      TO   KB20-NUM-REGN.                       
           MOVE    F800-NUM-NRIC1     TO   WS-NRIC.                             
           PERFORM 7530-READ-BP13K820 THRU 7530-EXIT.                           
           MOVE    F800-NUM-NRIC1     TO KB20-NUM-APPLT1-UIN.                   
           MOVE    K820-NME-OCCP      TO KB20-NME-APPLT1.                       
           MOVE    K820-DTE-BIRTH     TO KB20-DTE-APPLT1-BIRTH.                 
           MOVE    K820-NUM-SEX       TO KB20-NUM-APPLT1-SEX.                   
           MOVE    K820-NUM-MARITAL-STATUS                                      
             TO    KB20-NUM-APPLT1-MARITAL-STATUS.                              
           MOVE    K820-NUM-NRIC-SPOUSE                                         
             TO    KB20-NUM-APPLT1-SPOUSE-UIN.                                  
           MOVE    K820-AMT-INCOME    TO KB20-AMT-APPLT1-INCOME.                
           MOVE    K820-NUM-PHONE     TO KB20-NUM-APPLT1-PHONE.                 
           MOVE    K820-NUM-HP-PGR    TO KB20-NUM-APPLT1-HP-PAGER.              
           MOVE    K820-NUM-EDUCATION TO KB20-NUM-APPLT1-EDN-LVL.               
                                                                                
           IF F800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              MOVE    F800-NUM-REGN      TO   KB20-NUM-REGN                     
              MOVE    F800-NUM-NRIC2     TO   WS-NRIC                           
              PERFORM 7530-READ-BP13K820 THRU 7530-EXIT                         
              MOVE    F800-NUM-NRIC2     TO KB20-NUM-APPLT2-UIN                 
              MOVE    K820-NME-OCCP      TO KB20-NME-APPLT2                     
              MOVE    K820-DTE-BIRTH     TO KB20-DTE-APPLT2-BIRTH               
              MOVE    K820-NUM-SEX       TO KB20-NUM-APPLT2-SEX                 
              MOVE    K820-NUM-MARITAL-STATUS                                   
                TO    KB20-NUM-APPLT2-MARITAL-STATUS                            
              MOVE    K820-NUM-NRIC-SPOUSE                                      
                TO    KB20-NUM-APPLT2-SPOUSE-UIN                                
              MOVE    K820-AMT-INCOME    TO KB20-AMT-APPLT2-INCOME              
              MOVE    K820-NUM-RELATIONSHIP                                     
                TO    KB20-NUM-APPLT2-RLTNSP-APPLT1                             
              MOVE    K820-NUM-PHONE     TO KB20-NUM-APPLT2-PHONE               
              MOVE    K820-NUM-HP-PGR    TO KB20-NUM-APPLT2-HP-PAGER            
              MOVE    K820-NUM-EDUCATION TO KB20-NUM-APPLT2-EDN-LVL             
           END-IF.                                                              
                                                                                
           IF F800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              MOVE    F800-NUM-REGN      TO   KB20-NUM-REGN                     
              MOVE    F800-NUM-NRIC3     TO   WS-NRIC                           
              PERFORM 7530-READ-BP13K820 THRU 7530-EXIT                         
              MOVE    F800-NUM-NRIC3     TO KB20-NUM-APPLT3-UIN                 
              MOVE    K820-NME-OCCP      TO KB20-NME-APPLT3                     
              MOVE    K820-DTE-BIRTH     TO KB20-DTE-APPLT3-BIRTH               
              MOVE    K820-NUM-SEX       TO KB20-NUM-APPLT3-SEX                 
              MOVE    K820-NUM-MARITAL-STATUS                                   
                TO    KB20-NUM-APPLT3-MARITAL-STATUS                            
              MOVE    K820-NUM-NRIC-SPOUSE                                      
                TO    KB20-NUM-APPLT3-SPOUSE-UIN                                
              MOVE    K820-AMT-INCOME    TO KB20-AMT-APPLT3-INCOME              
              MOVE    K820-NUM-RELATIONSHIP                                     
                TO    KB20-NUM-APPLT3-RLTNSP-APPLT1                             
              MOVE    K820-NUM-PHONE     TO KB20-NUM-APPLT3-PHONE               
              MOVE    K820-NUM-HP-PGR    TO KB20-NUM-APPLT3-HP-PAGER            
              MOVE    K820-NUM-EDUCATION TO KB20-NUM-APPLT3-EDN-LVL             
           END-IF.                                                              
                                                                                
           IF F800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              MOVE    F800-NUM-REGN      TO   KB20-NUM-REGN                     
              MOVE    F800-NUM-NRIC4     TO   WS-NRIC                           
              PERFORM 7530-READ-BP13K820 THRU 7530-EXIT                         
              MOVE    F800-NUM-NRIC4     TO KB20-NUM-APPLT4-UIN                 
              MOVE    K820-NME-OCCP      TO KB20-NME-APPLT4                     
              MOVE    K820-DTE-BIRTH     TO KB20-DTE-APPLT4-BIRTH               
              MOVE    K820-NUM-SEX       TO KB20-NUM-APPLT4-SEX                 
              MOVE    K820-NUM-MARITAL-STATUS                                   
                TO    KB20-NUM-APPLT4-MARITAL-STATUS                            
              MOVE    K820-NUM-NRIC-SPOUSE                                      
                TO    KB20-NUM-APPLT4-SPOUSE-UIN                                
              MOVE    K820-AMT-INCOME    TO KB20-AMT-APPLT4-INCOME              
              MOVE    K820-NUM-RELATIONSHIP                                     
                TO    KB20-NUM-APPLT4-RLTNSP-APPLT1                             
              MOVE    K820-NUM-PHONE     TO KB20-NUM-APPLT4-PHONE               
              MOVE    K820-NUM-HP-PGR    TO KB20-NUM-APPLT4-HP-PAGER            
              MOVE    K820-NUM-EDUCATION TO KB20-NUM-APPLT4-EDN-LVL             
           END-IF.                                                              
                                                                                
       7520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       7530-READ-BP13K820.                                                      
      ************************                                                  
                                                                                
           MOVE F800-NUM-REGN  TO K820-NUM-REGN.                                
           MOVE WS-NRIC        TO K820-NUM-NRIC.                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K820-STATUS = 23                                            
                 DISPLAY 'BP13K820 - RECORD NOT FOUND : '                       
                         K820-KEY-FLD                                           
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K820 , STATUS = '                      
                          WS-K820-STATUS                                        
                 DISPLAY ' KEY = ' K820-KEY-FLD                                 
                 GO TO 9000-CLOSING-ROUTINE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7530-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       9000-CLOSING-ROUTINE.                                                    
      ************************                                                  
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CD54'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FD00 RECORDS READ............. : '                
                    WS-FD00-READ.                                               
           DISPLAY 'NO OF BP13KD15 RECORDS DELETED.......... : '                
                    WS-KD15-DELETE.                                             
           DISPLAY 'NO OF BP13KD15 RECORDS CREATED.......... : '                
                    WS-KD15-WRITE.                                              
           DISPLAY 'NO OF BP13KB20 RECORDS DELETED.......... : '                
                    WS-KB20-DELETE.                                             
           DISPLAY 'NO OF BP13KB20 RECORDS CREATED.......... : '                
                    WS-KB20-WRITE.                                              
           DISPLAY 'NO OF AB03F021 RECORDS WRITTEN.......... : '                
                    WS-F021-WRITE.                                              
                                                                                
           CLOSE BP13FD00                                                       
                 BP13F800                                                       
                 BP13K820                                                       
                 BP13KB20                                                       
                 BP13KD15                                                       
                 SY02F001                                                       
                 AB03F021.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K820 ,'                                
                      'STATUS = ' WS-K820-STATUS                                
              MOVE WS-K820-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           IF WS-KB20-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13KB20 ,'                                
                      'STATUS = ' WS-KB20-STATUS                                
              MOVE WS-KB20-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           IF WS-KD15-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13KD15 ,'                                
                      'STATUS = ' WS-KD15-STATUS                                
              MOVE WS-KD15-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
