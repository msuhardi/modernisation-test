      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C41Q.                                                 
       AUTHOR.        KARL ANDRUS MAGALONA CABUG.                               
       DATE-WRITTEN.  FEBRUARY 19, 2020.                                        
      *===============================================================*         
      * OBJECTIVE  :                                                            
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :  1. AB05F150                                     *         
      *               2. BP13K410                                     *         
      *               3. BP13K800                                     *         
      *               4. BP13K893                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. P13F0KIS - KEY ISSUED CASE                   *         
      *                  BP13T.F0KIS.INBOX.DEL                        *         
      *               2. P13F0CAN - CANCELLED CASE                    *         
      *                  BP13T.F0CAN.INBOX.DEL                        *         
      *               2. BP13F003 - UNMATCH CASES                     *         
      *                  BP13T.F003.INBOX.UNMATCH                     *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP137513 19/02/2020 KAC1 NEW PROGRAM                          *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  AB05F150  ASSIGN        TO AB05F150.                         
           SELECT  P13F0KIS  ASSIGN        TO P13F0KIS.                         
           SELECT  P13F0CAN  ASSIGN        TO P13F0CAN.                         
           SELECT  BP13F003  ASSIGN        TO BP13F003.                         
                                                                                
           SELECT  BP13K410  ASSIGN        TO BP13K410                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K410-NUM-REGN                     
                             FILE STATUS   IS WS-K410-STATUS.                   
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K893  ASSIGN        TO BP13K893                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K893-KEY-FLD                      
                             FILE STATUS   IS WS-K893-STATUS.                   
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   AB05F150                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  AB05F150-REC.                                                        
           05 F150-NUM-PRCS-CTGRY              PIC X(04).                       
           05 FILLER                           PIC X(12).                       
           05 F150-NUM-CASE-REF.                                                
              10 F150-NUM-REGN                 PIC X(08).                       
              10 FILLER                        PIC X(27).                       
           05 FILLER                           PIC X(02).                       
           05 F150-NUM-BRNCH                   PIC X(06).                       
           05 FILLER                           PIC X(05).                       
           05 F150-NUM-STAFF                   PIC X(06).                       
           05 FILLER                           PIC X(05).                       
           05 F150-NUM-HDB-REF.                                                 
              10 F150-NUM-SCHEME               PIC X(04).                       
              10 FILLER                        PIC X(08).                       
              10 F150-NUM-ACNT                 PIC X(05).                       
              10 FILLER                        PIC X(05).                       
              10 F150-NUM-TNCY                 PIC X(02).                       
              10 FILLER                        PIC X(08).                       
           05 F150-DTE-INBOX                   PIC X(10).                       
           05 FILLER                           PIC X(02).                       
           05 F150-TME-INBOX                   PIC X(08).                       
           05 FILLER                           PIC X(03).                       
           05 F150-DTE-DUE                     PIC X(10).                       
           05 FILLER                           PIC X(02).                       
           05 F150-TXT-TARGET-URL              PIC X(80).                       
           05 F150-TXT-REMARK                  PIC X(76).                       
           05 F150-NUM-BLDNG-GL                PIC X(14).                       
           05 F150-NUM-UNIT-ID                 PIC X(13).                       
           05 F150-TXT-MORE-INFO               PIC X(52).                       
           05 F150-TXT-TARGET-URL-MOB          PIC X(82).                       
           05 F150-NUM-PRCS-STAGE              PIC X(22).                       
           05 F150-DTE-SHOW                    PIC X(10).                       
           05 FILLER                           PIC X(1509).                     
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   P13F0KIS                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13F0KIS-RECORD              PIC X(2000).                          
                                                                                
       FD   P13F0CAN                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13F0CAN-RECORD              PIC X(2000).                          
                                                                                
       FD   BP13F003                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13F003-REC                  PIC X(2000).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       COPY BE08W008.                                                           
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-K410-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K893-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-EOF-F150             PIC X(1)  VALUE 'N'.                     
           05  WS-K893-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-K410-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-K800-DEL             PIC X(1)  VALUE 'N'.                     
           05  WS-K893-FND             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-F150-NUM-HDB-REF         PIC X(11) VALUE SPACES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-F150-CTR             PIC 9(7)  VALUE ZEROES.                  
           05  WS-UNMATCH-CTR          PIC 9(7)  VALUE ZEROES.                  
           05  WS-K410-FND-CTR         PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-FND-CTR         PIC 9(7)  VALUE ZEROES.                  
           05  WS-K893-FND-CTR         PIC 9(7)  VALUE ZEROES.                  
           05  WS-F0KIS-WRITE          PIC 9(7)  VALUE ZEROES.                  
           05  WS-F0CAN-WRITE          PIC 9(7)  VALUE ZEROES.                  
           05  WS-INBOX-DEL-CTR        PIC 9(7)  VALUE ZEROES.                  
           05  WS-INBOX-DEL-ERR        PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-NONCAN          PIC 9(7)  VALUE ZEROES.                  
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-AB05F150      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F150 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       1000-OPEN-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           OPEN INPUT  AB05F150                                                 
                       BP13K410                                                 
                       BP13K800                                                 
                       BP13K893                                                 
               OUTPUT  P13F0KIS                                                 
                       P13F0CAN                                                 
                       BP13F003.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       2000-READ-AB05F150.                                                      
      *------------------*                                                      
           READ AB05F150                                                        
             AT END                                                             
                MOVE 'Y'           TO WS-EOF-F150                               
                GO  TO 2000-EXIT.                                               
                                                                                
           ADD  1                  TO WS-F150-CTR.                              
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3000-PROCESS-RECORDS.                                                    
      *--------------------*                                                    
           MOVE 'N'                                TO WS-K410-FND               
                                                      WS-K800-DEL               
                                                      WS-K893-FND.              
                                                                                
           PERFORM 4000-READ-FILES               THRU 4000-EXIT.                
                                                                                
           IF WS-K410-FND = 'Y'                                                 
              WRITE BP13F0KIS-RECORD  FROM AB05F150-REC                         
              PERFORM 5000-PREPARE-DELETE        THRU 5000-EXIT                 
           ELSE                                                                 
             IF WS-K800-DEL = 'Y' OR                                            
                WS-K893-FND = 'Y'                                               
                WRITE BP13F0CAN-RECORD  FROM AB05F150-REC                       
                PERFORM 5000-PREPARE-DELETE      THRU 5000-EXIT                 
             ELSE                                                               
                ADD 1 TO WS-UNMATCH-CTR                                         
                WRITE BP13F003-REC      FROM AB05F150-REC                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-AB05F150            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-READ-FILES.                                                         
      *------------------*                                                      
           MOVE F150-NUM-REGN                      TO K410-NUM-REGN.            
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
               WHEN 00                                                          
                    ADD 1                       TO WS-K410-FND-CTR              
                    MOVE 'Y'                    TO WS-K410-FND                  
               WHEN 23                                                          
                    PERFORM 4100-READ-BP13K800 THRU 4100-EXIT                   
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K410 : ' WS-K410-STATUS          
                            ' REGN = ' K410-NUM-REGN                            
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4100-READ-BP13K800.                                                      
      *-------------------*                                                     
           MOVE SPACES                            TO BP13K800-MASTER.           
           MOVE F150-NUM-REGN                     TO K800-NUM-REGN.             
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                 IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                     
                    ADD 1                         TO WS-K800-FND-CTR            
                    MOVE 'Y'                      TO WS-K800-DEL                
                 ELSE                                                           
                    ADD 1                         TO WS-K800-NONCAN             
                 END-IF                                                         
               WHEN 23                                                          
                    PERFORM 4200-START-BP13K893 THRU 4200-EXIT                  
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800 : ' WS-K800-STATUS          
                            ' REGN = ' K800-NUM-REGN                            
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4200-START-BP13K893.                                                     
      *-------------------*                                                     
           MOVE 'N'                               TO WS-K893-EOF.               
           MOVE SPACE                             TO BP13K893-MASTER.           
           INITIALIZE                                BP13K893-MASTER.           
           MOVE F150-NUM-REGN                     TO K893-KEY-FLD.              
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
                    PERFORM 4300-READNEXT-BP13K893 THRU 4300-EXIT               
                      UNTIL K893-NUM-REGN-HIST NOT = F150-NUM-REGN              
                         OR WS-K893-EOF = 'Y'                                   
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'BP13K893 - NO RECORD FOUND, KEY : '                
      -                     F150-NUM-REGN                                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K893 - START ERROR,  ' WS-K893-STATUS          
                            ' REGN = ' K893-NUM-REGN-HIST                       
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------*                                                 
       4300-READNEXT-BP13K893.                                                  
      *-----------------------*                                                 
           READ BP13K893 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                         TO WS-K893-EOF                 
           END-READ.                                                            
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K893-NUM-REGN-HIST = F150-NUM-REGN                       
                       ADD 1                     TO WS-K893-FND-CTR             
                       MOVE 'Y'                  TO WS-K893-FND                 
                                                    WS-K893-EOF                 
                    ELSE                                                        
                       MOVE 'Y'                  TO WS-K893-EOF                 
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'                     TO WS-K893-EOF                 
                    DISPLAY 'RECORD NOT FOUND IN K893' F150-NUM-REGN            
               WHEN OTHER                                                       
                    DISPLAY 'BP13K893 - NEXT READ ERR,' WS-K893-STATUS          
                            ' REGN (' F150-NUM-REGN ')'                         
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       5000-PREPARE-DELETE.                                                     
      *-----------------------*                                                 
           MOVE SPACES                   TO BE08W008-COMMAREA.                  
           INITIALIZE                       BE08W008-COMMAREA.                  
                                                                                
           MOVE 'D '                     TO W008-IN-TXN-TYPE                    
           MOVE 'BP13'                   TO W008-IN-SYSTEM-ID.                  
           MOVE F150-NUM-PRCS-CTGRY      TO W008-IN-NUM-PRCS-CTGRY.             
           MOVE F150-NUM-CASE-REF        TO W008-IN-NUM-CASE-REF.               
           MOVE F150-NUM-BRNCH           TO W008-IN-NUM-BRNCH.                  
                                                                                
           PERFORM 5100-CALL-BE08C008  THRU 5100-EXIT.                          
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       5100-CALL-BE08C008.                                                      
      *-----------------------*                                                 
           CALL 'BE08C008' USING BE08W008-COMMAREA.                             
                                                                                
           IF W008-OUT-RTN-MSGCDE = '0'                                         
              ADD  1                   TO WS-INBOX-DEL-CTR                      
           ELSE                                                                 
             IF (W008-OUT-RTN-MSGCDE = 'W') AND                                 
                (W008-OUT-RTN-MSGNO = 'IB0001')                                 
                 DISPLAY 'ERROR - NO DATA TO DEL : ' W008-OUT-RTN-DESC          
                 DISPLAY 'KEY : ' W008-IN-INBOX-KEY                             
                 ADD  1                  TO  WS-INBOX-DEL-ERR                   
             ELSE                                                               
                 DISPLAY ' DELETE ERROR, KEY    : ' W008-IN-INBOX-KEY           
                 DISPLAY ' W008-OUT-RTN-MSGCDE  : ' W008-OUT-RTN-MSGCDE         
                 DISPLAY ' WS-W008-OUT-RTN-DESC :' W008-OUT-RTN-DESC            
                 DISPLAY ' WS-W008-OUT-RTN-MSGNO:' W008-OUT-RTN-MSGNO           
                 ADD  1                  TO  WS-INBOX-DEL-ERR                   
             END-IF                                                             
           END-IF.                                                              
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C41Q'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF INPUT RECORDS READ (AB05F150)..... : '            
                    WS-F150-CTR.                                                
           DISPLAY '(2) NO OF BP13K410 RECORDS FOUND............ : '            
                    WS-K410-FND-CTR.                                            
           DISPLAY '(3) NO OF BP13K800 CANCELLED CASE........... : '            
                    WS-K800-FND-CTR.                                            
           DISPLAY '(3) NO OF BP13K800 NON-CANCELLED............ : '            
                    WS-K800-NONCAN.                                             
           DISPLAY '(4) NO OF BP13K893 RECORDS FOUND............ : '            
                    WS-K893-FND-CTR.                                            
           DISPLAY '(5) NO OF SUCCESSFUL DELETE................. : '            
                    WS-INBOX-DEL-CTR.                                           
           DISPLAY '(6) NO OF UNSUCCESSFUL DELETE............... : '            
                    WS-INBOX-DEL-ERR.                                           
                                                                                
           CLOSE    AB05F150                                                    
                    BP13K410                                                    
                    BP13K800                                                    
                    BP13K893                                                    
                    BP13F003.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
