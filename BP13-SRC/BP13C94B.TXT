       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C94B.                                                    
       AUTHOR. KRISTINE LANTIN.                                                 
       DATE-WRITTEN. 18-04-2017.                                                
                                                                                
      *---------------------------------------------------------------*         
      * SOC (BP13)                                                    *         
      *---------------------------------------------------------------*         
      * PROGRAM   : BP13C94B                                          *         
      * AUTHOR    : KVL                                               *         
      * OBJECTIVE : TO PRINT EMAIL ALERT FOR CPF REFUNDED CASES       *         
      *             (CONTRA AND TEMP LOAN).                           *         
      *                                                               *         
      *  FILES :                                                      *         
      *  INPUT  - BP13F94A                                            *         
      *  OUTPUT - BP13L94B                                            *         
      *                                                               *         
      *----------------------------------------------------------------         
      *                                                               *         
      * CHG-NO   DATE     BY   DESCRIPTION                            *         
      * -------- -------- ---  -----------                            *         
      * BP136743 20170418 KVL1 NEW PROGRAM                            *         
      * BP137465 20171019 DN8  TO ADD NEW RECEPIENTS                  *         
      *                        CHUA_YEE_MIEN@HDB.GOV.SG AND           *         
      *                        SERENA_JM_TEH@HDB.GOV.SG               *         
      * BP137554 20171109 DN8  TO REMOVE SERENA_JM_TEH@HDB.GOV.SG     *         
      *                        ADD THANGATHURAI Y DEVI(HDB),          *         
      *                        SHAMUSURI SIBENGAT(HDB) AND            *         
      *                        CHIA MENG YONG(HDB)                    *         
      * BP138757 20210615 KV8  TO REMOVE LEE ADDED BERNICE            *         
      * BP139030 20220107 ZAR7 REPLACE CHUA YEE MIEN TO LIM KOK CHUN            
      * BP139030 11/01/2022 ZAR7 REPLACE Bernice JH GAN with Jialing HE         
      * BP139259 23/08/2022 ZAR7 REPLACE chia meng with Yusuf MOHAMMED          
      * BP139451 27/02/2023 KV8  ADDED ENG Siew Yan & SIM Xue Ying              
      * BP139276 20240405 KAC1 REPLACE LIM KOK CHUN BY                *         
      *                                KATHIJAH_BEE_ALI_MOHAMED       *         
      *----------------------------------------------------------------         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F94A ASSIGN TO BP13F94A.                                  
                                                                                
           SELECT BP13L94B ASSIGN TO BP13L94B.                                  
                                                                                
                                                                                
      *---------------------*                                                   
       DATA DIVISION.                                                           
      *---------------------*                                                   
       FILE SECTION.                                                            
                                                                                
       FD BP13F94A                                                              
            RECORD CONTAINS 130  CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F94A.                                                           
                                                                                
       FD BP13L94B                                                              
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01  BP13L94B-REC             PIC X(132).                                 
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-EOF-VARIABLES.                                                    
           05  WS-F94A-EOF            PIC X VALUE 'N'.                          
           05  WS-PRT-FLAG            PIC X VALUE 'Y'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F94A-READ           PIC 9(08) VALUE ZEROES.                   
           05  WS-L94B-WRITE          PIC 9(08) VALUE ZEROES.                   
           05  WS-LINE-CTR            PIC 9(02) VALUE 99.                       
           05  WS-PAGE-CTR            PIC 9(04) VALUE ZEROES.                   
           05  WS-SLNO                PIC 9(04) VALUE 1.                        
                                                                                
       01  WS-DATE                    PIC 9(08) VALUE ZEROES.                   
                                                                                
       01  WS-DATE1.                                                            
           05  WS-CCYY                PIC X(04).                                
           05  WS-MM                  PIC X(02).                                
           05  WS-DD                  PIC X(02).                                
                                                                                
       01  WS-RES-DATE.                                                         
           05  WS-CCYY-S              PIC X(04).                                
           05  WS-MM-S                PIC X(02).                                
           05  WS-DD-S                PIC X(02).                                
                                                                                
       01 WS-REF-DATE.                                                          
           05  WS-REF-CCYY            PIC 9(04).                                
           05  WS-REF-MM              PIC 9(02).                                
           05  WS-REF-DD              PIC 9(02).                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-REGN            PIC X(13) VALUE SPACES.                   
           05  WS-HDB-REF             PIC X(11) VALUE SPACES.                   
                                                                                
       01  WS-EMAIL-RPT-VARIABLES.                                              
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27) VALUE                         
                   'MAIL FROM:<OPCP@SGPHDB1>'.                                  
           05  WS-MAIL-RPT3             PIC X(60) VALUE                         
                   'RCPT TO:<HE_Jialing@hdb.gov.sg>'.                           
      *            'RCPT TO:<LEE_LI_PENG@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT3A            PIC X(60) VALUE                         
                   'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.             
           05  WS-MAIL-RPT3B            PIC X(60) VALUE                         
                   'RCPT TO:<THANGATHURAI_Y_DEVI@HDB.GOV.SG>'.                  
           05  WS-MAIL-RPT3C            PIC X(60) VALUE                         
                   'RCPT TO:<SHAMSURIANOR_SIBENGAT@HDB.GOV.SG>'.                
           05  WS-MAIL-RPT3D            PIC X(60) VALUE                         
                   'RCPT TO:<Yusuf_MOHAMMED@hdb.gov.sg>'.                       
           05  WS-MAIL-RPT4             PIC X(60) VALUE                         
                   'DATA'.                                                      
           05  WS-MAIL-RPT5             PIC X(60)  VALUE                        
                   'FROM:Soc System - Email Alert'.                             
           05  WS-MAIL-RPT6             PIC X(60) VALUE                         
                   'TO:<HE_Jialing@hdb.gov.sg>'.                                
      *            'TO:<LEE_LI_PENG@HDB.GOV.SG>'.                               
           05  WS-MAIL-RPT6A            PIC X(60) VALUE                         
                   'TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                  
           05  WS-MAIL-RPT6B            PIC X(60) VALUE                         
                   'TO:<THANGATHURAI_Y_DEVI@HDB.GOV.SG>'.                       
           05  WS-MAIL-RPT6C            PIC X(60) VALUE                         
                   'TO:<SHAMSURIANOR_SIBENGAT@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT6D            PIC X(60) VALUE                         
                   'TO:<Yusuf_MOHAMMED@hdb.gov.sg>'.                            
           05  WS-MAIL-RPT6E            PIC X(60) VALUE                         
                   'TO:<ENG_Siew_Yan@hdb.gov.sg>'.                              
           05  WS-MAIL-RPT6F            PIC X(60) VALUE                         
                   'TO:<SIM_Xue_Ying@hdb.gov.sg>'.                              
           05  WS-MAIL-RPT7             PIC X(100) VALUE                        
                   'SUBJECT: Contra/Temp Loan Cases - CPF Refunded'.            
                                                                                
       01  WS-L94B-HDNG1.                                                       
           05  FILLER                 PIC X(75) VALUE                           
              'To send out CPF withdrawal form to redeem contra/temp loa        
      -       'n.'.                                                             
           05  FILLER                 PIC X(41) VALUE SPACES.                   
                                                                                
       01  WS-L94B-HDNG2.                                                       
           05  FILLER                 PIC X(08)  VALUE 'BP13L94B'.              
           05  FILLER                 PIC X(04)  VALUE SPACES.                  
           05  FILLER                 PIC X(08)  VALUE 'HDB3'.                  
           05  FILLER                 PIC X(18)  VALUE SPACES.                  
           05  FILLER                 PIC X(39)  VALUE                          
                'S Y S T E M   O F   C O M M I T M E N T'.                      
           05  FILLER                 PIC X(15)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'DATE: '.                
           05  WS-L94B-DATE           PIC X(11)  VALUE SPACES.                  
           05  FILLER                 PIC X(08)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'PAGE: '.                
           05  WS-L94B-CNT-PAGE       PIC ZZZZ9  VALUE ZERO.                    
                                                                                
       01  WS-L94B-HDNG3.                                                       
           05  FILLER                 PIC X(30) VALUE SPACES.                   
           05  FILLER                 PIC X(68) VALUE                           
              'REPORT ON CPF REFUNDED - CONTRA AND TEMP LOAN CASES'.            
           05  FILLER                 PIC X(51) VALUE SPACES.                   
                                                                                
       01  WS-L94B-HDNG4.                                                       
           05  FILLER  PIC X(16) VALUE SPACES.                                  
           05  FILLER  PIC X(04) VALUE 'S/NO'.                                  
           05  FILLER  PIC X(07) VALUE SPACES.                                  
           05  FILLER  PIC X(09) VALUE 'Case Type'.                             
           05  FILLER  PIC X(04) VALUE SPACES.                                  
           05  FILLER  PIC X(11) VALUE 'Regn Number'.                           
           05  FILLER  PIC X(03) VALUE SPACES.                                  
           05  FILLER  PIC X(22) VALUE 'Resale Completion Date'.                
           05  FILLER  PIC X(03) VALUE SPACES.                                  
           05  FILLER  PIC X(17) VALUE 'CPF Refunded Date'.                     
           05  FILLER  PIC X(04) VALUE SPACES.                                  
                                                                                
       01  WS-L94B-DET1.                                                        
           05  FILLER                 PIC X(14)  VALUE SPACES.                  
           05  WS-L94B-CNT            PIC ZZZ99  VALUE ZEROES.                  
           05  FILLER                 PIC X(08)  VALUE SPACES.                  
           05  WS-L94B-CASE-TYPE      PIC X(10)  VALUE SPACES.                  
           05  FILLER                 PIC X(05)  VALUE SPACES.                  
           05  WS-L94B-NUM-REGN       PIC X(08)  VALUE SPACES.                  
           05  FILLER                 PIC X(09)  VALUE SPACES.                  
           05  WS-L94B-RES-DATE       PIC X(11)  VALUE SPACES.                  
           05  FILLER                 PIC X(11)  VALUE SPACES.                  
           05  WS-L94B-REF-DATE       PIC X(11)  VALUE SPACES.                  
                                                                                
       01  WS-L94B-NULL-REPORT.                                                 
           05  FILLER                 PIC X(45)  VALUE SPACES.                  
           05  WS-L94B-MESSAGE        PIC X(50)  VALUE SPACES.                  
           05  FILLER                 PIC X(52)  VALUE SPACES.                  
                                                                                
       01  WS-HDNG-LINE               PIC X(132) VALUE ALL '-'.                 
                                                                                
       01  WS-BLANK-LINE              PIC X(132) VALUE SPACES.                  
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *================================================================*        
       0000-CONTROL.                                                            
      *================================================================*        
                                                                                
            PERFORM 1000-OPEN-FILES       THRU  1000-EXIT.                      
                                                                                
            PERFORM 2000-READ-BP13F94A    THRU  2000-EXIT.                      
            PERFORM 3000-PROCESS-BP13F94A THRU  3000-EXIT                       
                    UNTIL WS-F94A-EOF = 'Y'.                                    
                                                                                
            PERFORM 9000-CLOSE-FILES      THRU  9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       1000-OPEN-FILES.                                                         
      *================================================================*        
            OPEN INPUT     BP13F94A                                             
                 OUTPUT    BP13L94B.                                            
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-DATE.                       
            MOVE WS-DATE     TO WS-DATE1.                                       
            STRING WS-DD, '/', WS-MM, '/', WS-CCYY                              
                DELIMITED BY SIZE INTO WS-L94B-DATE.                            
                                                                                
            PERFORM 5000-HEADER-EMAIL  THRU 5000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       2000-READ-BP13F94A.                                                      
      *================================================================*        
                                                                                
           READ BP13F94A AT END MOVE 'Y' TO WS-F94A-EOF                         
                           GO TO 2000-EXIT.                                     
                                                                                
              ADD 1 TO WS-F94A-READ.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3000-PROCESS-BP13F94A.                                                   
      *================================================================*        
                                                                                
           IF WS-LINE-CTR > 30                                                  
              PERFORM 4000-PRINT-HDNG      THRU 4000-EXIT                       
           END-IF.                                                              
                                                                                
           ADD 1                           TO WS-LINE-CTR.                      
                                                                                
           MOVE WS-SLNO                    TO WS-L94B-CNT.                      
                                                                                
           MOVE F94A-NUM-REGN              TO WS-L94B-NUM-REGN.                 
           IF F94A-CASE-TYPE = 'C'                                              
              MOVE 'CONTRA'                TO WS-L94B-CASE-TYPE                 
           ELSE                                                                 
              MOVE 'TEMP LOAN'             TO WS-L94B-CASE-TYPE                 
           END-IF                                                               
           MOVE F94A-DTE-RES-COMPLTN       TO WS-RES-DATE                       
           STRING WS-DD-S, '/', WS-MM-S, '/', WS-CCYY-S                         
               DELIMITED BY SIZE         INTO WS-L94B-RES-DATE.                 
           MOVE F94A-DTE-CPF-REFUND        TO WS-REF-DATE                       
           STRING WS-REF-DD, '/', WS-REF-MM, '/', WS-REF-CCYY                   
               DELIMITED BY SIZE         INTO WS-L94B-REF-DATE.                 
                                                                                
           WRITE BP13L94B-REC              FROM WS-L94B-DET1.                   
           ADD 1                           TO WS-L94B-WRITE.                    
                                                                                
           ADD 1                           TO WS-SLNO.                          
           PERFORM 2000-READ-BP13F94A      THRU  2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       4000-PRINT-HDNG.                                                         
      *================================================================*        
            ADD   1            TO   WS-PAGE-CTR.                                
            MOVE  WS-PAGE-CTR  TO   WS-L94B-CNT-PAGE.                           
            WRITE BP13L94B-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L94B-REC FROM WS-L94B-HDNG1.                              
            WRITE BP13L94B-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L94B-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L94B-REC FROM WS-L94B-HDNG2.                              
            WRITE BP13L94B-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L94B-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L94B-REC FROM WS-L94B-HDNG3.                              
            WRITE BP13L94B-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L94B-REC FROM WS-L94B-HDNG4.                              
            WRITE BP13L94B-REC FROM WS-HDNG-LINE.                               
            WRITE BP13L94B-REC FROM WS-BLANK-LINE.                              
            MOVE  13           TO   WS-LINE-CTR.                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5000-HEADER-EMAIL.                                                       
      *================================================================*        
                                                                                
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT1.                              
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT2.                              
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT3.                              
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT3A.                             
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT3B.                             
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT3C.                             
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT3D.                             
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT4.                              
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT5.                              
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT6.                              
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT6A.                             
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT6B.                             
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT6C.                             
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT6D.                             
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT6E.                             
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT6F.                             
            WRITE  BP13L94B-REC FROM WS-MAIL-RPT7.                              
            MOVE   WS-L94B-DATE   TO WS-DATE-RPT.                               
            WRITE  BP13L94B-REC FROM WS-DATE-RPT-DTL.                           
                                                                                
            MOVE   SPACES         TO BP13L94B-REC.                              
            WRITE  BP13L94B-REC.                                                
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-FILES.                                                        
      *================================================================*        
           IF WS-L94B-WRITE = ZEROES                                            
              PERFORM 4000-PRINT-HDNG   THRU  4000-EXIT                         
              WRITE BP13L94B-REC FROM WS-BLANK-LINE                             
              WRITE BP13L94B-REC FROM WS-BLANK-LINE                             
              MOVE 'NO CPF REFUNDED CASES ' TO                                  
                 WS-L94B-MESSAGE                                                
              WRITE BP13L94B-REC       FROM WS-L94B-NULL-REPORT                 
           END-IF.                                                              
                                                                                
           CLOSE BP13F94A BP13L94B.                                             
                                                                                
           DISPLAY '** BP13C94B **'.                                            
           DISPLAY 'F94A RECORDS READ    : ' WS-F94A-READ.                      
           DISPLAY 'L94B RECORDS WRITTEN : ' WS-L94B-WRITE.                     
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
