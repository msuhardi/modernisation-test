       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C70D                                                  
      *AUTHOR.        SHIERA.                                                   
      *DATE-WRITTEN.  29/07/2014.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  UPDATE COHORT IN BP13K740 FOR NT/FT THAT    *            
      *             :  DID NOT MEET FIRST TIMER %                  *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F740                                 *            
      *             :  2. BP13F707                                 *            
      *  I-O FILE   :  1. BP13K740                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP135392 SMRT 20140729 NEW PROGRAM                         *            
      * BP135625 SMR2 20150211 UPDATE REPORT LAYOUT                *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F707 ASSIGN TO BP13F707.                                  
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS RANDOM                                           
                RECORD KEY IS K740-KEY-FLD                                      
                FILE STATUS IS K740-STATUS.                                     
                                                                                
           SELECT BP13L70D ASSIGN TO BP13L70D.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F707                                                             
           RECORD CONTAINS 50 CHARACTERS                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F707.                                                           
                                                                                
       FD  BP13F740                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K740                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K740.                                                           
                                                                                
       FD  BP13L70D                                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13L70D-REC            PIC X(100).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-F707-READ        PIC 9(5)    VALUE ZEROS.                     
           05  WS-F740-READ        PIC 9(5)    VALUE ZEROS.                     
           05  WS-F707-UNMATCH     PIC 9(5)    VALUE ZEROS.                     
           05  WS-F740-UNMATCH     PIC 9(5)    VALUE ZEROS.                     
           05  WS-F740-MATCH       PIC 9(5)    VALUE ZEROS.                     
           05  WS-K740-REWRITE     PIC 9(5)    VALUE ZEROS.                     
           05  WS-L70D-WRITE       PIC 9(5)    VALUE ZEROS.                     
           05  WS-SN               PIC 9(5)    VALUE ZEROS.                     
           05  WS-LINE-CTR         PIC 9(3)    VALUE ZEROES.                    
           05  WS-PAGE-CTR         PIC 9(3)    VALUE ZEROES.                    
                                                                                
       01  WS-DATE                 PIC X(8)  VALUE SPACES.                      
       01  WS-EOF                  PIC X     VALUE 'N'.                         
       01  K740-STATUS             PIC 99    VALUE 0.                           
       01  WS-OLD-COHORT           PIC X(2)  VALUE SPACES.                      
                                                                                
       01  WS-F707-KEY.                                                         
           05  WS-F707-NT          PIC X(3).                                    
           05  WS-F707-FT          PIC X(3).                                    
           05  WS-F707-HH          PIC X(1).                                    
           05  WS-F707-STAT        PIC X(1).                                    
                                                                                
       01  WS-F740-KEY.                                                         
           05  WS-F740-NT          PIC X(3).                                    
           05  WS-F740-FT          PIC X(3).                                    
           05  WS-F740-HH          PIC X(1).                                    
           05  WS-F740-STAT        PIC X(1).                                    
                                                                                
       01  L70D-FORMAT.                                                         
           05  L70D-SN                 PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L70D-NT                 PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L70D-FT                 PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L70D-HH                 PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L70D-CDE-REQUEST        PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L70D-NUM-REGN           PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L70D-NUM-NRIC1          PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L70D-OLD-COHORT         PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L70D-NEW-COHORT         PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
                                                                                
       01  L70D-HEADER.                                                         
           05  FILLER                  PIC X(05) VALUE '-----'.                 
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE '---'.                   
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE '--'.                    
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE '----'.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE '---'.                   
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE '--------'.              
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE '---------'.             
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE '---'.                   
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE '---'.                   
           05  FILLER                  PIC X(02) VALUE SPACES.                  
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  L70D-HDG1.                                                           
           05  L70D-LISTING             PIC X(11) VALUE 'BP13L70D'.             
           05  FILLER                   PIC X(04) VALUE 'HDB3'.                 
           05  FILLER                   PIC X(07) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(07) VALUE SPACES.                 
           05  FILLER                   PIC X(07) VALUE 'DATE : '.              
           05  L70D-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(10) VALUE '   PAGE : '.           
           05  L70D-PAGE                PIC ZZ9.                                
                                                                                
       01  L70D-HDG2.                                                           
           05  FILLER                   PIC X(24) VALUE SPACES.                 
           05  FILLER                   PIC X(35) VALUE                         
               'CONTROL LIST FOR UPDATING OF COHORT'.                           
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F707        THRU 2000-EXIT.                        
           PERFORM 3000-READ-F740        THRU 3000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT BP13F707                                                  
                      BP13F740                                                  
                I-O   BP13K740                                                  
               OUTPUT BP13L70D.                                                 
                                                                                
           IF K740-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K740 - OPEN ERROR, STATUS: ' K740-STATUS             
              MOVE K740-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                    
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/'                             
                  WS-DATE(1:4) DELIMITED BY SIZE                                
                  INTO L70D-DATE.                                               
                                                                                
           MOVE 'S/N'                     TO L70D-SN                            
           MOVE 'NT'                      TO L70D-NT.                           
           MOVE 'FT'                      TO L70D-FT.                           
           MOVE 'B-HH'                    TO L70D-HH.                           
           MOVE 'REQ'                     TO L70D-CDE-REQUEST.                  
           MOVE 'REGN'                    TO L70D-NUM-REGN.                     
           MOVE 'NRIC1'                   TO L70D-NUM-NRIC1.                    
           MOVE 'OLD'                     TO L70D-OLD-COHORT.                   
           MOVE 'NEW'                     TO L70D-NEW-COHORT.                   
                                                                                
           ADD 1                          TO WS-PAGE-CTR                        
           MOVE WS-PAGE-CTR               TO L70D-PAGE                          
           WRITE BP13L70D-REC FROM L70D-HDG1                                    
           WRITE BP13L70D-REC FROM L70D-HDG2 AFTER 2                            
           WRITE BP13L70D-REC FROM L70D-FORMAT AFTER 3                          
           WRITE BP13L70D-REC FROM L70D-HEADER.                                 
           MOVE 7                         TO WS-LINE-CTR.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F707.                                                          
      *-------------------------------------------------------------            
            READ BP13F707 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 MOVE HIGH-VALUES TO WS-F707-KEY                                
                 GO TO 2000-EXIT.                                               
                                                                                
            MOVE F707-CDE-NT             TO WS-F707-NT.                         
            MOVE F707-CDE-FLAT-TYPE      TO WS-F707-FT.                         
            MOVE F707-CDE-BALLOT-HOUSEHOLD TO WS-F707-HH.                       
            MOVE F707-CDE-REQUEST-STATUS TO WS-F707-STAT.                       
                                                                                
            ADD 1 TO WS-F707-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-F740.                                                          
      *-------------------------------------------------------------            
            READ BP13F740 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 MOVE HIGH-VALUES TO WS-F740-KEY                                
                 GO TO 3000-EXIT.                                               
                                                                                
            MOVE F740-CDE-NT1            TO WS-F740-NT.                         
            MOVE F740-CDE-FLAT-TYPE      TO WS-F740-FT.                         
            MOVE F740-CDE-BALLOT-HOUSEHOLD  TO WS-F740-HH.                      
            MOVE F740-CDE-REQUEST-STATUS TO WS-F740-STAT.                       
                                                                                
            ADD 1 TO WS-F740-READ.                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF WS-F707-KEY = WS-F740-KEY                                         
              ADD  1                          TO  WS-F740-MATCH                 
               PERFORM 4200-UPDATE-K740-REC THRU 4200-EXIT                      
                 UNTIL WS-F740-KEY NOT = WS-F707-KEY                            
                    OR WS-F740-KEY = HIGH-VALUES                                
              PERFORM 2000-READ-F707             THRU  2000-EXIT                
           ELSE                                                                 
              IF WS-F707-KEY > WS-F740-KEY                                      
                 ADD  1                       TO  WS-F740-UNMATCH               
                 PERFORM 3000-READ-F740           THRU  3000-EXIT               
              ELSE                                                              
                 ADD  1                     TO  WS-F707-UNMATCH                 
                 PERFORM 2000-READ-F707         THRU  2000-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-UPDATE-K740-REC.                                                    
      *-------------------------------------------------------------            
            MOVE F740-KEY-FLD  TO K740-KEY-FLD.                                 
            READ BP13K740                                                       
            EVALUATE K740-STATUS                                                
            WHEN 00                                                             
                                                                                
                DISPLAY '          '                                            
                DISPLAY ' BF REGN: ' K740-NUM-REGN                              
                        ' NT= '    K740-CDE-NT1                                 
                        ' FT= '    K740-CDE-FLAT-TYPE                           
                        ' ST= '    K740-CDE-REQUEST-STATUS                      
                        ' CH= '    K740-CDE-COHORT                              
                 IF WS-F740-KEY     = WS-F707-KEY AND                           
                    F707-CDE-COHORT = K740-CDE-COHORT                           
                    PERFORM 4300-UPDATE-COHORT   THRU 4300-EXIT                 
                 END-IF                                                         
                 PERFORM 3000-READ-F740       THRU 3000-EXIT                    
                                                                                
            WHEN 23                                                             
                 DISPLAY 'BP13K740 - RECORD NOT FOUND, KEY: '                   
                          F740-NUM-REGN                                         
            WHEN OTHER                                                          
                 DISPLAY 'BP13K740 - ERROR READ, STATUS: '                      
                          K740-STATUS                                           
                 PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4300-UPDATE-COHORT.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE K740-CDE-COHORT        TO WS-OLD-COHORT                         
           MOVE F707-CDE-COHORT-NEW    TO K740-CDE-COHORT                       
                                                                                
           REWRITE BP13K740-REC.                                                
                                                                                
           EVALUATE K740-STATUS                                                 
           WHEN 00                                                              
                ADD 1 TO WS-K740-REWRITE                                        
                                                                                
                PERFORM 4400-WRITE-BP13L70D  THRU 4400-EXIT                     
                                                                                
                DISPLAY ' AF REGN: ' K740-NUM-REGN                              
                        ' NT= '    K740-CDE-NT1                                 
                        ' FT= '    K740-CDE-FLAT-TYPE                           
                        ' ST= '    K740-CDE-REQUEST-STATUS                      
                        ' CH= '    K740-CDE-COHORT                              
           WHEN OTHER                                                           
                DISPLAY 'BP13K740 - REWRITE ERROR, STATUS: '                    
                         K740-STATUS                                            
                PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4400-WRITE-BP13L70D.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF WS-LINE-CTR > 58                                                  
              PERFORM 8100-WRITE-HEADER THRU 8100-EXIT                          
              ADD 1                       TO WS-LINE-CTR                        
           END-IF                                                               
                                                                                
           INITIALIZE BP13L70D-REC                                              
                      L70D-FORMAT.                                              
                                                                                
           ADD 1 TO WS-SN                                                       
                                                                                
           MOVE WS-SN                     TO L70D-SN                            
           MOVE F707-CDE-NT               TO L70D-NT.                           
           MOVE F707-CDE-FLAT-TYPE        TO L70D-FT.                           
           MOVE F707-CDE-BALLOT-HOUSEHOLD TO L70D-HH.                           
           MOVE F707-CDE-REQUEST-STATUS   TO L70D-CDE-REQUEST.                  
           MOVE K740-NUM-REGN             TO L70D-NUM-REGN.                     
           MOVE K740-NUM-NRIC1            TO L70D-NUM-NRIC1.                    
           MOVE WS-OLD-COHORT             TO L70D-OLD-COHORT.                   
           MOVE F707-CDE-COHORT-NEW       TO L70D-NEW-COHORT.                   
                                                                                
           WRITE BP13L70D-REC FROM L70D-FORMAT.                                 
                                                                                
           ADD 1 TO WS-L70D-WRITE                                               
                    WS-LINE-CTR.                                                
                                                                                
           INITIALIZE WS-OLD-COHORT.                                            
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       8100-WRITE-HEADER.                                                       
      **************************************************************            
                                                                                
           ADD 1                          TO WS-PAGE-CTR                        
           MOVE WS-PAGE-CTR               TO L70D-PAGE                          
           WRITE BP13L70D-REC FROM L70D-HDG1 AFTER PAGE                         
           WRITE BP13L70D-REC FROM L70D-HDG2 AFTER 2                            
           WRITE BP13L70D-REC FROM L70D-FORMAT AFTER 3                          
           WRITE BP13L70D-REC FROM L70D-HEADER.                                 
           MOVE 7                         TO WS-LINE-CTR.                       
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13F707                                                       
                 BP13F740                                                       
                 BP13L70D                                                       
                 BP13K740.                                                      
                                                                                
           IF K740-STATUS NOT = '00'                                            
             DISPLAY 'CLOSING ERROR K740, STATUS ' K740-STATUS                  
             MOVE K740-STATUS TO RETURN-CODE.                                   
                                                                                
           DISPLAY ' BP13C70D CONTROL TOTAL '.                                  
           DISPLAY '---------------------------------'.                         
           DISPLAY ' NO. OF F740 REC READ   > ' WS-F740-READ.                   
           DISPLAY ' NO. OF F740 REC UPDATE > ' WS-K740-REWRITE.                
           DISPLAY ' NO. OF F740 MATCH      > ' WS-F740-MATCH.                  
           DISPLAY ' NO. OF F707 UNMATCH    > ' WS-F707-UNMATCH.                
           DISPLAY ' NO. OF F740 UNMATCH    > ' WS-F740-UNMATCH.                
           DISPLAY ' NO. OF L70D WRITE      > ' WS-L70D-WRITE.                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
