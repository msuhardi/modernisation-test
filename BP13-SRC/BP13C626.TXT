       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C626.                                                 
      *AUTHOR.        YCH2                                                      
      *DATE-WRITTEN : 28/07/97                                                  
      * =========================================================== *           
      *  SYSTEM OF COMMITMENT (BP13)                                *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE : THIS PROGRAM OUTPUT DIRECT PURCHANSE FLAT,     *           
      *              DROP OPEN MARKET FLAT IN BP13F715 RDB REC      *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHGE ID  OIC  DATE   DESCRIPTION                            *           
      * -------  ---  ----   -----------                            *           
      * BP130323 LSB  090697 MOVE MTH-RESIDE FROM BP13F715 TO       *           
      *                       BP13F558                              *           
      * BP130323 LSB  220897 RENAME FROM BP13U730                   *           
      * BP130445 SSS  050298 EXPAND F558 RECL FROM 150 TO 400       *           
      * BP130630 NOR  030299 EXPAND F715 RECL FROM 100 TO 250       *           
      * BP130630 NOR  250699 CHGE F715 TO F716 EXPAND TO 350        *           
      * BP130715 NOR  210799 IF THE DIFF. OF LAST MOVE OUT DATE &   *           
      *                      PREVIOUS MOVE IN DATE IS < 3 MONTHS FOR*           
      *                      MULTIPLE REC OF F716, OUTPUT THE LAST  *           
      *                      REC OTHERWISE OUTPUT THE PREVIOUS REC  *           
      * BP130819 SRA  110400 OUTPUT MOVEIN-DATE SAME AS SALES-TYPE- *           
      *                      SMS=8,FOR 7A,7J,8A,8B,8E               *           
      *                      DEBARMENT-CD = 62,64,66,GA,GB          *           
      * BP130819 SRA  280400 ADD 8D                                 *           
      * BP130897 AKT  030800 EXPAND F558 RECL FROM 400 TO 600       *           
      * BP132059 ANC1 110202 CHGE DEBARMENT-3 TO DEBARMENT-4        *           
      * BP139518 EAA2 280223 TO INCLUDE 6 SALES TYPE CODE IN        *           
      *                      HOUSEHOLD DETERMINATION                *           
      *                      (8L/8M/8N/8R/8S/8T)                    *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F716A ASSIGN TO P13F716A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F558                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F716                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 350 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F716.                                                           
                                                                                
       FD   P13F716A                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 350 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F716A-REC           PIC X(350).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F558-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-STORED          PIC 9(8)    VALUE ZERO.                      
                                                                                
       77  WS-EOF-F716             PIC X(3).                                    
           88  EOF-OF-F716                     VALUE 'YES'.                     
       77  WS-EOF-F558             PIC X(3).                                    
           88  EOF-OF-F558                     VALUE 'YES'.                     
                                                                                
       01  WS-F558-KEY-FLD.                                                     
           05  WS-F558-REGN        PIC X(8)    VALUE SPACES.                    
           05  WS-F558-UIN         PIC X(9)    VALUE SPACES.                    
                                                                                
       01  WS-F716-KEY-FLD.                                                     
           05  WS-F716-REGN        PIC X(8)    VALUE SPACES.                    
           05  WS-F716-UIN         PIC X(9)    VALUE SPACES.                    
                                                                                
       01  WS-DTE-CMOVEIN.                                                      
           05  WS-CMI-YY           PIC 9(04)   VALUE ZEROES.                    
           05  WS-CMI-MM           PIC 9(02)   VALUE ZEROES.                    
           05  WS-CMI-DD           PIC 9(02)   VALUE ZEROES.                    
                                                                                
       01  WS-DTE-CMOVEOUT.                                                     
           05  WS-CMO-YY           PIC 9(04)   VALUE ZEROES.                    
           05  WS-CMO-MM           PIC 9(02)   VALUE ZEROES.                    
           05  WS-CMO-DD           PIC 9(02)   VALUE ZEROES.                    
                                                                                
       01  WS-DTE-PMOVEIN.                                                      
           05  WS-PMI-YY           PIC 9(04)   VALUE ZEROES.                    
           05  WS-PMI-MM           PIC 9(02)   VALUE ZEROES.                    
           05  WS-PMI-DD           PIC 9(02)   VALUE ZEROES.                    
                                                                                
       01  WS-DTE-PMOVEOUT.                                                     
           05  WS-PMO-YY           PIC 9(04)   VALUE ZEROES.                    
           05  WS-PMO-MM           PIC 9(02)   VALUE ZEROES.                    
           05  WS-PMO-DD           PIC 9(02)   VALUE ZEROES.                    
                                                                                
       01  WS-LAPSE-DIFF           PIC 9(4)    VALUE ZEROS.                     
       01  WS-LAPSE-FLAG           PIC X(01).                                   
           88  WS-LAPSE            VALUE 'Y'.                                   
       01  WS-LAST-F716-REC        PIC X(350)  VALUE SPACES.                    
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
      *-------------------------------------------------------------            
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
                                                                                
           PERFORM 200-READ-F716      THRU 200-EXIT.                            
                                                                                
           PERFORM 300-READ-F558      THRU 300-EXIT.                            
                                                                                
           PERFORM 400-PROCESS        THRU 400-EXIT                             
                   UNTIL WS-F716-KEY-FLD = HIGH-VALUE                           
                    AND  WS-F558-KEY-FLD = HIGH-VALUE.                          
                                                                                
           PERFORM 900-FINALIZATION   THRU 900-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F716                                                 
                       BP13F558                                                 
                OUTPUT P13F716A.                                                
                                                                                
           MOVE 'NO' TO WS-EOF-F716.                                            
           MOVE 'NO' TO WS-EOF-F558.                                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F716.                                                           
      *-------------------------------------------------------------            
           READ BP13F716 AT END                                                 
                MOVE 'YES'      TO WS-EOF-F716                                  
                MOVE HIGH-VALUES TO WS-F716-KEY-FLD                             
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
            ADD 1 TO WS-F716-READ.                                              
            MOVE F716-NUM-REGN  TO WS-F716-REGN.                                
            MOVE F716-NUM-NRIC  TO WS-F716-UIN.                                 
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-READ-F558.                                                           
      *-------------------------------------------------------------            
           READ BP13F558 AT END                                                 
                MOVE 'YES'      TO WS-EOF-F558                                  
                MOVE HIGH-VALUES TO WS-F558-KEY-FLD                             
                GO TO 300-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F558-READ.                                               
            MOVE F558-NUM-REGN  TO WS-F558-REGN.                                
            MOVE F558-NUM-NRIC  TO WS-F558-UIN.                                 
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH  BY REGNNO                                             *         
      *****************************************************************         
       400-PROCESS.                                                             
      *-------------------------------------------------------------            
           IF WS-F716-KEY-FLD = WS-F558-KEY-FLD                                 
              ADD 1                    TO WS-MATCH                              
              PERFORM 450-CHECK-FILE-STATUS THRU 450-EXIT                       
                UNTIL WS-F716-KEY-FLD NOT = WS-F558-KEY-FLD                     
                 OR  WS-F716-KEY-FLD = HIGH-VALUES                              
              PERFORM 300-READ-F558 THRU 300-EXIT                               
           ELSE                                                                 
               IF WS-F716-KEY-FLD  < WS-F558-KEY-FLD                            
                   ADD 1 TO WS-F716-NOTMATCH                                    
                   PERFORM 200-READ-F716 THRU 200-EXIT                          
               ELSE                                                             
                   ADD 1 TO WS-F558-NOTMATCH                                    
                   PERFORM 300-READ-F558 THRU 300-EXIT                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       450-CHECK-FILE-STATUS.                                                   
      *-------------------------------------------------------------            
           IF F558-NUM-HDB-REF-SMS (1:9) = F716-NUM-SCH-ACCNT                   
              IF (F716-NUM-SALES-TYPE-SMS = '7C' OR '7J' OR '8A' OR '8B'        
                                         OR '7 ' OR '8E' OR '8L' OR '8M'        
                                         OR '8N' OR '8R' OR '8S'                
                                         OR '8T' ) AND                          
                 (F558-NUM-DEBARMENT-4 = '62' OR '64' OR '66' OR                
                                         'GA' OR 'GB')                          
                 PERFORM 455-CHECK-SALES-TYPE THRU 455-EXIT                     
              ELSE                                                              
                 IF F558-CDE-FILE-STATUS-SMS  NOT = 'OM'                        
                    PERFORM 455-CHECK-SALES-TYPE THRU 455-EXIT                  
                 ELSE                                                           
                    PERFORM 200-READ-F716 THRU 200-EXIT                         
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              PERFORM 200-READ-F716 THRU 200-EXIT                               
           END-IF.                                                              
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       455-CHECK-SALES-TYPE.                                                    
      *-------------------------------------------------------------            
           MOVE SPACES              TO  P13F716A-REC                            
                                        WS-LAST-F716-REC.                       
           MOVE BP13F716-REC        TO  P13F716A-REC                            
                                        WS-LAST-F716-REC.                       
           MOVE ZEROS               TO  WS-DTE-PMOVEIN                          
                                        WS-DTE-PMOVEOUT                         
                                        WS-DTE-CMOVEIN                          
                                        WS-DTE-CMOVEOUT.                        
                                                                                
           MOVE 'N'                   TO  WS-LAPSE-FLAG.                        
           PERFORM 460-DET-OUTPUT-FR-DTE-MOVIN THRU 460-EXIT                    
              UNTIL WS-F716-KEY-FLD NOT = WS-F558-KEY-FLD                       
                    OR WS-F716-KEY-FLD = HIGH-VALUES                            
                    OR F558-NUM-HDB-REF-SMS(1:9) NOT =                          
                       F716-NUM-SCH-ACCNT.                                      
           WRITE P13F716A-REC.                                                  
           ADD  1                   TO WS-F716-STORED.                          
                                                                                
       455-EXIT.                                                                
           EXIT.                                                                
                                                                                
       460-DET-OUTPUT-FR-DTE-MOVIN.                                             
      *-------------------------------------------------------------            
           IF F716-DTE-MOVING-IN IS NOT NUMERIC                                 
              MOVE ZEROS                   TO  WS-DTE-CMOVEIN                   
           ELSE                                                                 
              MOVE F716-DTE-MOVING-IN      TO  WS-DTE-CMOVEIN                   
           END-IF.                                                              
           IF F716-DTE-MOVING-OUT IS NOT NUMERIC                                
              MOVE ZEROS                   TO  WS-DTE-CMOVEOUT                  
           ELSE                                                                 
              MOVE F716-DTE-MOVING-OUT     TO  WS-DTE-CMOVEOUT                  
           END-IF.                                                              
                                                                                
           IF (WS-DTE-PMOVEIN NOT = ZEROS OR                                    
              WS-DTE-PMOVEOUT NOT = ZEROS) AND                                  
              WS-DTE-CMOVEOUT NOT = ZEROS                                       
              COMPUTE WS-LAPSE-DIFF = ((WS-PMI-YY * 12) + WS-PMI-MM)-           
                                      ((WS-CMO-YY * 12) + WS-CMO-MM)            
              IF WS-LAPSE-DIFF < 3                                              
                 IF NOT WS-LAPSE                                                
                    MOVE  BP13F716-REC         TO  P13F716A-REC                 
                 END-IF                                                         
              ELSE                                                              
                 IF NOT WS-LAPSE                                                
                    MOVE  WS-LAST-F716-REC  TO  P13F716A-REC                    
                    MOVE 'Y'                   TO  WS-LAPSE-FLAG                
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE  BP13F716-REC    TO  P13F716A-REC                            
           END-IF.                                                              
                                                                                
           MOVE WS-DTE-CMOVEIN             TO  WS-DTE-PMOVEIN.                  
           MOVE WS-DTE-CMOVEOUT            TO  WS-DTE-PMOVEOUT.                 
           MOVE BP13F716-REC               TO  WS-LAST-F716-REC.                
           PERFORM 200-READ-F716 THRU 200-EXIT.                                 
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-FINALIZATION.                                                        
      *-------------------------------------------------------------            
             COMPUTE WS-F716-NOTMATCH = WS-F716-READ - WS-MATCH.                
                                                                                
             DISPLAY '*--------BP13C626  -----------*'.                         
             DISPLAY '                               '.                         
             DISPLAY '* MATCH BP13F716 WITH BP13F558,   '.                      
             DISPLAY '* TO GET MTH-RESIDE BASED ON SMS'.                        
             DISPLAY '                               '.                         
             DISPLAY ' BP13F716-READ      ' WS-F716-READ.                       
             DISPLAY ' BP13F558-READ      ' WS-F558-READ.                       
             DISPLAY ' NO OF F716 MATCHED ' WS-MATCH.                           
             DISPLAY ' F716-NOT-MATCH     ' WS-F716-NOTMATCH.                   
             DISPLAY ' F558-NOT-MATCH     ' WS-F558-NOTMATCH.                   
             DISPLAY ' RECORDS WRITTEN    ' WS-F716-STORED.                     
                                                                                
           CLOSE      BP13F716                                                  
                      BP13F558                                                  
                      P13F716A.                                                 
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
