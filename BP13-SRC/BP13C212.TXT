       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C212.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  19 JULY 2017.                                             
      **************************************************************            
      * BP13 - SYSTEM OF COMMITMENT                                *            
      **************************************************************            
      * OBJECTIVE  : TO FILTER OUT FULLY BOOKED BLOCKS.            *            
      *------------------------------------------------------------*            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      *--------- -------- ----  -----------------------------------*            
      * BP136835 19/07/17 ESA1  NEW PROGRAM.                       *            
      **************************************************************            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F240  ASSIGN       TO BP13F240.                           
                                                                                
           SELECT P13F240A  ASSIGN       TO P13F240A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *------------------------------------------------------------             
                                                                                
       FD   BP13F240                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K240.                                                           
                                                                                
       FD   P13F240A                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 450 CHARACTERS.                                     
       01 P13F240A-REC                  PIC X(450).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE            PIC X(08) VALUE SPACES.               
               10  FILLER                 PIC X(13) VALUE SPACES.               
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD             PIC X(02) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE '/'.                  
               10  WS-DISP-MM             PIC X(02) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE '/'.                  
               10  WS-DISP-CCYY           PIC X(04) VALUE SPACES.               
           05  WS-NUM-TOT-CAT             PIC 9(05) VALUE ZEROES.               
           05  WS-FULL-CTR                PIC 9(05) VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F240-READ-CTR           PIC 9(05) VALUE ZEROES.               
           05  WS-ACCEPT-CTR              PIC 9(05) VALUE ZEROES.               
           05  WS-F240-WRT-CTR            PIC 9(05) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F240-SW          PIC X(01) VALUE SPACES.               
               88  C-END-OF-F240          VALUE 'Y'.                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       MAIN-ROUTINE SECTION.                                                    
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-PROCESS-RECORDS    THRU 2000-EXIT                       
             UNTIL C-END-OF-F240.                                               
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F240                                                 
                OUTPUT P13F240A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------            
                                                                                
           READ BP13F240 AT END                                                 
                MOVE 'Y'    TO WS-END-OF-F240-SW                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F240-READ-CTR.                                           
                                                                                
           PERFORM 3000-CHECK-BLOCK          THRU 3000-EXIT.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-CHECK-BLOCK.                                                        
      *-------------------------------------------------------------            
           MOVE ZEROES          TO WS-NUM-TOT-CAT.                              
                                                                                
           IF K240-NUM-TOT-CAT1 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT1                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOT-CAT2 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT2                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOT-CAT3 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT3                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOTAL-SPR  NOT NUMERIC                                   
              MOVE ZEROES       TO K240-NUM-TOTAL-SPR                           
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOTAL      NOT NUMERIC                                   
              MOVE ZEROES       TO K240-NUM-TOTAL                               
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-TOT-CAT = K240-NUM-TOT-CAT1 +                         
                                    K240-NUM-TOT-CAT2 +                         
                                    K240-NUM-TOT-CAT3 +                         
                                    K240-NUM-TOTAL-SPR.                         
                                                                                
           IF WS-NUM-TOT-CAT < K240-NUM-TOTAL                                   
              MOVE K240-HEADER-REC  TO P13F240A-REC                             
              WRITE P13F240A-REC                                                
              ADD  1     TO WS-F240-WRT-CTR                                     
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY 'PROGRAM ID   : BP13C212'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'NO. OF F240 RECORD          : ' WS-F240-READ-CTR.           
           DISPLAY 'NO. OF RECORDS WRITTEN TO P13F240A  : '                     
                                                    WS-F240-WRT-CTR.            
                                                                                
           CLOSE BP13F240                                                       
                 P13F240A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
