       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C24G.                                                  
       AUTHOR.       ELAINE S ARGA.                                             
      *DATE-WRITTEN. 08 AUGUST 2017.                                            
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *  SYSTEM ID   : BP13                                      *              
      *  OBJECTIVE   : TO CREATE ALLOCATION SUMMARY FILE BP13F222*              
      *                FOR RE-OFFER FOR FLATS(ROF)               *              
      *  BP13F205    : CONTROL REC                               *              
      *  BP13F222    : ALLOCATION SUMMARY RESULT FILE            *              
      *  BP13K249    : NT-FT FILE FOR FLAT AVAILABILITY          *              
      *  BP13K210    : BOOKING WORKPLAN                          *              
      *                                                          *              
      * CHG-NO   BY   DATE    DESCRIPTION                        *              
      * -------- ---- ------  -----------                        *              
      * BP136836 ESA1 080817  NEW PROGRAM PATTERNED TO BP13C24F  *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN       TO BP13F205.                            
                                                                                
           SELECT BP13F222 ASSIGN       TO BP13F222.                            
                                                                                
           SELECT BP13K249 ASSIGN       TO BP13K249                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K249-KEY-FLD                         
                           ALTERNATE KEY IS K249-ALT-KEY1                       
                           FILE STATUS  IS K249-STATUS.                         
                                                                                
           SELECT BP13K210 ASSIGN       TO BP13K210                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K210-KEY-FLD                         
                           FILE STATUS  IS K210-STATUS.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F205                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           LABEL  RECORDS  ARE STANDARD                                         
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 80  CHARACTERS.                                      
       COPY BP13F205.                                                           
                                                                                
       FD  BP13F222                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13F222.                                                           
                                                                                
       FD  BP13K249                                                             
           RECORD CONTAINS 450 CHARACTERS.                                      
       COPY BP13K249.                                                           
                                                                                
       FD  BP13K210                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K210.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  F222-STATUS         PIC 99      VALUE ZERO.                      
           05  K210-STATUS         PIC 99      VALUE ZERO.                      
           05  K249-STATUS         PIC 99      VALUE ZERO.                      
                                                                                
       01  WS-K210-FT              PIC X(2).                                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F205-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-F222-WRITE       PIC 9(8)    VALUE ZERO.                      
           05  WS-F222-UPDATE      PIC 9(8)    VALUE ZERO.                      
           05  WS-K210-READ        PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-SWITCHES.                                                         
           05  F205-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-F205                 VALUE 'Y'.                       
           05  K249-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-K249                 VALUE 'Y'.                       
           05  K210-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-K210                 VALUE 'Y'.                       
           05  WS-ZONE-FND-FLAG    PIC X       VALUE 'N'.                       
                                                                                
       01  WS-TEMP-VARIABLES.                                                   
           05  WS-CNT-BKAPPMT      PIC 9(4)    VALUE ZERO.                      
           05  WS-FLAT-OFFER       PIC 9(4)    VALUE ZERO.                      
           05  WS-ALLOC-TOTAL      PIC 9(4)    VALUE ZERO.                      
           05  WS-FT-NO            PIC 9(2)    VALUE ZERO.                      
           05  WS-START-DATE       PIC X(8)    VALUE SPACES.                    
           05  WS-END-DATE         PIC X(8)    VALUE SPACES.                    
           05  WS-WORK-END-DATE    PIC X(8)    VALUE SPACES.                    
           05  WS-DAY              PIC 9(2)    VALUE ZERO.                      
           05  WS-CTR              PIC 9(2)    VALUE ZERO.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN-LOGIC.                                                         
      *************************************************************             
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-F205       THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-RTN     THRU 3000-EXIT                          
             UNTIL END-OF-F205.                                                 
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
      *************************************************************             
       1000-OPEN-FILES.                                                         
      *************************************************************             
           OPEN  INPUT BP13F205                                                 
                       BP13K210                                                 
                       BP13K249                                                 
                OUTPUT BP13F222.                                                
                                                                                
           IF K210-STATUS NOT = 00 AND 97                                       
              MOVE K210-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BP13K210 FILE, ' K210-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
           IF K249-STATUS NOT = 00 AND 97                                       
              MOVE K249-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BP13K249 FILE, ' K249-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
           INITIALIZE      WS-SWITCHES                                          
                           WS-TEMP-VARIABLES                                    
                           WS-COUNTERS.                                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2000-READ-F205.                                                          
      *************************************************************             
           READ BP13F205                                                        
             AT END                                                             
                MOVE 'Y' TO F205-EOF                                            
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                   TO WS-F205-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3000-PROCESS-RTN.                                                        
      *************************************************************             
      *----------------------------------------------------*                    
      *PROCESS ONLY ROF CASES FOR BP13F222 CREATION                             
      *----------------------------------------------------*                    
           IF F205-NUM-NT-ZONE = 'ROF'                                          
              INITIALIZE  WS-TEMP-VARIABLES                                     
                          WS-SWITCHES                                           
                                                                                
              PERFORM 3100-START-BP13K210 THRU 3100-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F205      THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3100-START-BP13K210.                                                     
      *-----------------------------------------------------------*             
           MOVE 'N'                       TO K210-EOF.                          
           MOVE SPACES                    TO BP13K210-REC.                      
           MOVE F205-NUM-NT-ZONE          TO K210-NUM-NT-ZONE.                  
           MOVE ZEROS                     TO WS-CNT-BKAPPMT.                    
                                                                                
           START BP13K210 KEY NOT < K210-KEY-FLD.                               
                                                                                
           IF K210-STATUS = 00                                                  
              PERFORM 3200-READ-BP13K210 THRU 3200-EXIT                         
                UNTIL (F205-NUM-NT-ZONE   = K210-NUM-NT-ZONE    AND             
                       F205-DTE-ALLOCN    = K210-DTE-ALLOC      AND             
                       F205-NUM-FLAT-TYPE = K210-NUM-FLAT-TYPE)                 
                   OR  K210-EOF = 'Y'                                           
                                                                                
              IF (K210-EOF = 'Y') OR                                            
                 (F205-NUM-NT-ZONE   NOT = K210-NUM-NT-ZONE)    OR              
                 (F205-DTE-ALLOCN    NOT = K210-DTE-ALLOC)      OR              
                 (F205-NUM-FLAT-TYPE NOT = K210-NUM-FLAT-TYPE)                  
                 GO TO 3100-EXIT                                                
              ELSE                                                              
                 PERFORM 3250-PROCESS-BP13K210 THRU 3250-EXIT                   
              END-IF                                                            
           ELSE                                                                 
           IF K210-STATUS = 23                                                  
              DISPLAY 'START ERROR BP13K210 FILE' K210-STATUS                   
              DISPLAY 'K210-KEY-FLD : ' K210-KEY-FLD                            
           ELSE                                                                 
              MOVE K210-STATUS         TO RETURN-CODE                           
              DISPLAY 'ERROR START BP13K210 FILE, ' K210-STATUS                 
              DISPLAY 'K210-KEY-FLD = ' K210-KEY-FLD                            
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3200-READ-BP13K210.                                                      
      *-----------------------------------------------------------*             
                                                                                
           READ BP13K210 NEXT AT END  MOVE 'Y' TO K210-EOF                      
                GO TO 3200-EXIT.                                                
                                                                                
           ADD 1 TO WS-K210-READ.                                               
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3250-PROCESS-BP13K210.                                                   
      *-----------------------------------------------------------*             
                                                                                
           MOVE K210-NUM-FLAT-TYPE TO WS-K210-FT.                               
           PERFORM 3300-COUNT-SELECTION-DAY THRU 3300-EXIT                      
             UNTIL K210-NUM-NT-ZONE   > F205-NUM-NT-ZONE                        
                OR K210-NUM-FLAT-TYPE > WS-K210-FT                              
                OR K210-EOF = 'Y'.                                              
                                                                                
           PERFORM 3400-READ-K249 THRU 3400-EXIT.                               
                                                                                
       3250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3300-COUNT-SELECTION-DAY.                                                
      *-----------------------------------------------------------*             
                                                                                
           IF F205-DTE-ALLOCN = K210-DTE-ALLOC                                  
              IF K210-NUM-FLAT-TYPE = WS-K210-FT                                
                 ADD K210-NUM-BKAPPMT       TO WS-CNT-BKAPPMT                   
                 ADD 1 TO WS-DAY                                                
                 IF WS-DAY = 1                                                  
                    MOVE K210-DTE-BKAPPMT   TO WS-START-DATE                    
                 END-IF                                                         
                 MOVE K210-DTE-BKAPPMT      TO WS-END-DATE                      
                                               WS-WORK-END-DATE                 
              ELSE                                                              
                 PERFORM 3400-READ-K249   THRU 3400-EXIT                        
                 MOVE K210-NUM-FLAT-TYPE    TO WS-K210-FT                       
                 MOVE ZEROS                 TO WS-DAY                           
                                               WS-CNT-BKAPPMT                   
                 GO TO 3300-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3200-READ-BP13K210  THRU 3200-EXIT.                          
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3400-READ-K249.                                                          
      *-----------------------------------------------------------*             
                                                                                
           MOVE SPACES                    TO K249-ALT-KEY1.                     
           MOVE F205-NUM-SELECTION        TO K249-NUM-SELECTION1.               
           MOVE F205-DTE-BALLOT           TO K249-DTE-BALLOT1.                  
           MOVE ZEROS                     TO WS-FLAT-OFFER.                     
                                                                                
           START BP13K249 KEY NOT < K249-ALT-KEY1.                              
                                                                                
           EVALUATE K249-STATUS                                                 
             WHEN 00                                                            
             WHEN 02                                                            
              CONTINUE                                                          
             WHEN 23                                                            
              DISPLAY 'RECORD NOT FOUND IN BP13K249'                            
              DISPLAY 'K249-KEY-FLD = ' K249-KEY-FLD                            
              GO TO 3400-EXIT                                                   
             WHEN OTHER                                                         
              MOVE K249-STATUS           TO RETURN-CODE                         
              DISPLAY 'START ERROR READING BP13K249 FILE, ' K249-STATUS         
              DISPLAY 'K249-KEY-FLD = ' K249-KEY-FLD                            
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-EVALUATE.                                                        
                                                                                
           IF K249-STATUS = 0                                                   
              MOVE 'N'       TO K249-EOF                                        
              PERFORM 3500-ACCUM-FLATOFFER THRU 3500-EXIT                       
                UNTIL END-OF-K249.                                              
                                                                                
           PERFORM 3600-WRITETO-F222       THRU 3600-EXIT.                      
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3500-ACCUM-FLATOFFER.                                                    
      *-----------------------------------------------------------*             
           READ BP13K249 NEXT                                                   
                AT END                                                          
                   MOVE 'Y' TO K249-EOF                                         
                   GO 3500-EXIT.                                                
                                                                                
           IF F205-NUM-SELECTION = K249-NUM-SELECTION1 AND                      
              F205-DTE-BALLOT = K249-DTE-BALLOT1                                
              ADD K249-NUM-TOTAL TO WS-FLAT-OFFER                               
           ELSE                                                                 
              MOVE 'Y' TO K249-EOF                                              
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3600-WRITETO-F222.                                                       
      *-----------------------------------------------------------*             
           MOVE SPACES                    TO BP13F222-REC.                      
           MOVE ZEROS                     TO F222-NUM-BOOKED.                   
           MOVE WS-CNT-BKAPPMT            TO F222-NUM-BKAPPMT.                  
           MOVE WS-FLAT-OFFER             TO F222-NUM-FLAT-OFFER.               
           MOVE F205-NUM-NT-ZONE          TO F222-NUM-NT-ZONE.                  
           MOVE F205-DTE-ALLOCN           TO F222-DTE-ALLOC.                    
           MOVE F205-DTE-BALLOT           TO F222-DTE-BALLOT.                   
           MOVE WS-K210-FT                TO F222-NUM-FLAT-TYPE.                
           MOVE WS-START-DATE             TO F222-DTE-START-BKAPPMT.            
           MOVE WS-END-DATE               TO F222-DTE-END-BKAPPMT.              
           MOVE 'P13C24G'                 TO F222-NUM-USERID.                   
           MOVE FUNCTION CURRENT-DATE     TO F222-DTE-UPDATE.                   
                                                                                
           WRITE BP13F222-REC.                                                  
           ADD 1  TO WS-F222-WRITE.                                             
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-FILES.                                                        
      *************************************************************             
           DISPLAY '***************** BP13C24G *****************'.              
           DISPLAY 'TOTAL RECORDS READ    (BP13F205) = ' WS-F205-READ.          
           DISPLAY 'TOTAL RECORDS WRITTEN (BP13F222) = ' WS-F222-WRITE.         
                                                                                
           CLOSE BP13F205                                                       
                 BP13K210                                                       
                 BP13K249                                                       
                 BP13F222.                                                      
                                                                                
           IF K210-STATUS NOT = 00 AND 97                                       
              MOVE K210-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR CLOSING BP13K210 FILE, ' K210-STATUS               
           END-IF.                                                              
                                                                                
           IF K249-STATUS NOT = 00 AND 97                                       
              MOVE K249-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR CLOSING BP13K249 FILE, ' K249-STATUS               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
