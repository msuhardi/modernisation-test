       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C700.                                                 
      *AUTHOR.        LSB.                                                      
      *DATE-WRITTEN.  13/11/91.                                                 
      *DATE-COMPILED.                                                           
      *============================================================*            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      *============================================================*            
      *  OBJECTIVE :                                               *            
      *    1.  THIS IS A PROGRAM TO EXTRACT THE APPLICANTS FROM    *            
      *        BALLOT HISTORY LOG FILE BASE ON THE REQUEST APPLIED *            
      *        FOR NT WITH LAND (EXCLUDE STAFF/RESETTLEMENT).      *            
      *                                                            *            
      *     INPUT FILES:                                           *            
      *      1  BP13REQT (REQUEST REC)                             *            
      *      2  BP13HIST (HISTORY REC)                             *            
      *                                                            *            
      *     OUTPUT FILE :                                          *            
      *      1  BP13F706                                           *            
      *------------------------------------------------------------*            
      * BP130340 19971114 SSS  RECOMPILE                           *            
      * BP130780 19991227 MCC  DELETE DTE-BALLOT IN BP13REQT AND   *            
      *                        BP13HIST FILES.                     *            
      *============================================================*            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13REQT ASSIGN TO BP13REQT.                                  
                                                                                
           SELECT BP13HIST ASSIGN TO BP13HIST.                                  
                                                                                
           SELECT BP13F706 ASSIGN TO BP13F706.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13REQT                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            LABEL RECORD IS STANDARD.                                           
       01   BP13REQT-REC.                                                       
            05  REQT-NUM-NRIC                PIC X(08).                         
            05  REQT-NUM-REF                 PIC X(06).                         
            05  REQT-DTE-REQUEST             PIC X(08).                         
            05  REQT-CDE-REQUEST-STATUS      PIC X(01).                         
            05  REQT-CDE-NT-REQ              PIC X(03).                         
            05  FILLER                       PIC X(04).                         
            05  REQT-NUM-NEW-REF             PIC X(06).                         
            05  REQT-CDE-SUCC-NT             PIC X(03).                         
            05  REQT-CDE-NT-WITH-LAND        PIC X(01).                         
            05  FILLER                       PIC X(10).                         
                                                                                
       FD   BP13HIST                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 50   CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       01   BP13HIST-REC.                                                       
            05  HIST-NUM-NRIC                PIC X(08).                         
            05  HIST-NUM-REF                 PIC X(06).                         
            05  HIST-DTE-REQUEST             PIC X(08).                         
            05  HIST-CDE-REQUEST-STATUS      PIC X(01).                         
            05  HIST-CDE-NT-REQ              PIC X(03).                         
            05  FILLER                       PIC X(04).                         
            05  HIST-NUM-NEW-REF             PIC X(06).                         
            05  HIST-CDE-SUCC-NT             PIC X(03).                         
            05  HIST-CDE-NT-WITH-LAND        PIC X(01).                         
            05  FILLER                       PIC X(10).                         
                                                                                
       FD   BP13F706                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 50  CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F706.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       77  WS-REQT-CNT             PIC 9(6)    VALUE ZERO.                      
       77  WS-HIST-CNT             PIC 9(6)    VALUE ZERO.                      
       77  WS-MATCH                PIC 9(6)    VALUE ZERO.                      
       77  WS-OUT-F706             PIC 9(6)    VALUE ZERO.                      
       77  WS-HIST-FD              PIC 9(6)    VALUE ZERO.                      
       77  WS-REQ-FD               PIC 9(6)    VALUE ZERO.                      
       77  WS-EOF-REQT             PIC X(3).                                    
           88  EOF-OF-REQT                     VALUE 'YES'.                     
       77  WS-EOF-HIST             PIC X(3).                                    
           88  EOF-OF-HIST                     VALUE 'YES'.                     
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       10-MAIN-LOGIC.                                                           
      *-------------------------------------------------------------            
           PERFORM 20-INITIALIZATION  THRU 20-EXIT.                             
           PERFORM 200-READ-REQT      THRU 200-EXIT.                            
           PERFORM 300-READ-HIST      THRU 300-EXIT.                            
           PERFORM 400-MATCHING-ROUTINE THRU 400-EXIT                           
                   UNTIL EOF-OF-HIST AND EOF-OF-REQT.                           
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       20-INITIALIZATION.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  BP13REQT                                                 
                       BP13HIST                                                 
               OUTPUT  BP13F706.                                                
                                                                                
           MOVE 'NO' TO WS-EOF-REQT.                                            
           MOVE 'NO' TO WS-EOF-HIST.                                            
                                                                                
       20-EXIT.                                                                 
           EXIT.                                                                
                                                                                
       200-READ-REQT.                                                           
      *-------------------------------------------------------------            
           READ BP13REQT                                                        
                AT END MOVE 'YES' TO WS-EOF-REQT                                
                MOVE HIGH-VALUE TO REQT-NUM-NRIC                                
                GO TO 200-EXIT.                                                 
           ADD 1 TO WS-REQT-CNT.                                                
           PERFORM 500-OUTPUT-REQ-REC THRU 500-EXIT.                            
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-HIST.                                                           
      *-------------------------------------------------------------            
           READ BP13HIST                                                        
               AT END MOVE 'YES' TO WS-EOF-HIST                                 
               MOVE HIGH-VALUE TO HIST-NUM-NRIC                                 
               GO TO 300-EXIT.                                                  
           ADD 1 TO WS-HIST-CNT.                                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH REQ FILE WITH HISTORY FILE                             *         
      *****************************************************************         
       400-MATCHING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           IF REQT-NUM-NRIC = HIST-NUM-NRIC                                     
               PERFORM 450-MATCH-NRIC THRU 450-EXIT                             
                 UNTIL HIST-NUM-NRIC NOT = REQT-NUM-NRIC                        
               PERFORM 200-READ-REQT THRU 200-EXIT                              
           ELSE                                                                 
               IF REQT-NUM-NRIC > HIST-NUM-NRIC                                 
                   ADD 1 TO WS-HIST-FD                                  .       
                   PERFORM 300-READ-HIST THRU 300-EXIT                          
               ELSE                                                             
                   ADD 1 TO WS-REQ-FD                                   .       
                   PERFORM 200-READ-REQT  THRU 200-EXIT.                        
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-MATCH-NRIC.                                                          
      *-------------------------------------------------------------            
               ADD 1 TO WS-MATCH.                                       .       
               PERFORM 550-OUTPUT-HIST-REC THRU 550-EXIT.                       
               PERFORM 300-READ-HIST THRU 300-EXIT.                             
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-OUTPUT-REQ-REC.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES TO BP13F706-REC.                                         
           MOVE BP13REQT-REC TO BP13F706-REC.                                   
           MOVE REQT-NUM-REF TO F706-NUM-NEW-REF.                               
           MOVE 'Y'          TO F706-CDE-1ST-REQUEST.                           
           WRITE BP13F706-REC.                                                  
           ADD 1 TO WS-OUT-F706.                                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       550-OUTPUT-HIST-REC.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES TO BP13F706-REC.                                         
           MOVE BP13HIST-REC TO BP13F706-REC.                                   
           MOVE REQT-NUM-REF TO F706-NUM-NEW-REF.                               
           MOVE SPACE        TO F706-CDE-1ST-REQUEST.                           
           WRITE BP13F706-REC.                                                  
           ADD 1 TO WS-OUT-F706.                                                
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           DISPLAY '1. TOT NO REC READ FROM REQUEST  = ' WS-REQT-CNT.           
           DISPLAY '2. TOT NO REC READ FROM HISTORY  = ' WS-HIST-CNT.           
           DISPLAY '3. TOT NO REC  WRITE TO BP13F706 = '                .       
                                              WS-OUT-F706.              .       
           DISPLAY '4. TOT NO REQUEST MATCH HISTORY  = '                .       
                                              WS-MATCH.                 .       
           DISPLAY '5. TOT NO REC NOT MATCH HISTORY  = '                .       
                                              WS-HIST-FD.               .       
           DISPLAY '6. TOT NO REC NOT MATCH REQUEST  = '                .       
                                              WS-REQ-FD.                .       
           DISPLAY ' 3 = 1 + ( 2 - 5 )'.                                .       
                                                                                
           CLOSE      BP13HIST                                                  
                      BP13REQT                                                  
                      BP13F706.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
