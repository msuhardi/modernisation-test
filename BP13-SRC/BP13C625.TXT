       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C625.                                                 
      *AUTHOR.        YCH2                                                      
      *DATE-WRITTEN : 28/07/97                                                  
      * ========================================================== *            
      *  SYSTEM OF COMMITMENT (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  : THIS PROGRAM COMPARES THE REGNO AND NRIC TO  *            
      *               UPDATE THE MONTH RESIDE                      *            
      *                                                            *            
      * CHGE ID  OIC  DATE   DESCRIPTION                           *            
      * -------- ---  ----   -----------                           *            
      * BP130323 LSB  090697 MOVE MTH-RESIDE FROM BP13F715 TO      *            
      *                        BP13F558                            *            
      * BP130323 LSB  220897 RENAME FROM BP13U729                  *            
      * BP130377 YCH  220997 10 YEAR, UPDATE MTH-RESIDE FR 715     *            
      *                        TO F558                             *            
      * BP130410 LSB  211197 OUTPUT MOVE-IN-DATE TO FR BP13F715    *            
      *                        TO F558                             *            
      * ---------------------------------------------------------- *            
      * BP130445 19980205 SSS  EXPAND F558 RECL FROM 150 TO 400    *            
      * BP130445 19980212 LSB  MOVE MTH-RESIDE TO MTH-RESIDE-SMS   *            
      * BP130445 19980228 LSB  ADD CC TO MOVE-IN-DATE-RDB          *            
      * BP130630 19990114 NOR  CHGE F715 TO F716 EXPAND TO 350     *            
      * BP130897 20000803 AKT  EXPAND F558 RECL FROM 400 TO 600    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F558A ASSIGN TO P13F558A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F558                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F716                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 350 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F716.                                                           
                                                                                
       FD   P13F558A                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F558A-REC           PIC X(600).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F558-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-STORED          PIC 9(8)    VALUE ZERO.                      
       01  WS-NUM-MTH-RESIDE       PIC 9(4)    VALUE ZEROES.                    
       01  WS-F716-NUM-MTH-RESIDE  PIC 9(3)    VALUE ZEROES.                    
                                                                                
       77  WS-EOF-F716             PIC X(3).                                    
           88  EOF-OF-F716                     VALUE 'YES'.                     
       77  WS-EOF-F558             PIC X(3).                                    
           88  EOF-OF-F558                     VALUE 'YES'.                     
                                                                                
       01  WS-MOVE-IN-CCYY.                                                     
           05  WS-MOVE-IN-CC       PIC X(2).                                    
           05  WS-MOVE-IN-RDB.                                                  
               10  FILLER          PIC X(6).                                    
                                                                                
       01  WS-F558-KEY-FLD.                                                     
           05  WS-F558-REGN        PIC X(8)    VALUE SPACES.                    
           05  WS-F558-UIN         PIC X(9)    VALUE SPACES.                    
                                                                                
       01  WS-F716-KEY-FLD.                                                     
           05  WS-F716-REGN        PIC X(8)    VALUE SPACES.                    
           05  WS-F716-UIN         PIC X(9)    VALUE SPACES.                    
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
      *-------------------------------------------------------------            
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
                                                                                
           PERFORM 200-READ-F716      THRU 200-EXIT.                            
                                                                                
           PERFORM 300-READ-F558      THRU 300-EXIT.                            
                                                                                
           PERFORM 400-PROCESS        THRU 400-EXIT                             
                   UNTIL WS-F716-KEY-FLD = HIGH-VALUE                           
                    AND  WS-F558-KEY-FLD = HIGH-VALUE.                          
                                                                                
           PERFORM 900-FINALIZATION   THRU 900-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F716                                                 
                       BP13F558                                                 
                OUTPUT P13F558A.                                                
                                                                                
           MOVE 'NO' TO WS-EOF-F716.                                            
           MOVE 'NO' TO WS-EOF-F558.                                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F716.                                                           
      *-------------------------------------------------------------            
           READ BP13F716 AT END                                                 
                MOVE 'YES'      TO WS-EOF-F716                                  
                MOVE HIGH-VALUE TO WS-F716-KEY-FLD                              
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
            ADD 1 TO WS-F716-READ.                                              
            MOVE F716-NUM-REGN  TO WS-F716-REGN.                                
            MOVE F716-NUM-NRIC  TO WS-F716-UIN.                                 
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-READ-F558.                                                           
      *-------------------------------------------------------------            
           READ BP13F558 AT END                                                 
                MOVE 'YES'      TO WS-EOF-F558                                  
                MOVE HIGH-VALUE TO WS-F558-KEY-FLD                              
                GO TO 300-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F558-READ.                                               
            MOVE F558-NUM-REGN  TO WS-F558-REGN.                                
            MOVE F558-NUM-NRIC  TO WS-F558-UIN.                                 
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH  BY REGNNO                                             *         
      *****************************************************************         
       400-PROCESS.                                                             
      *-------------------------------------------------------------            
           IF WS-F716-KEY-FLD = WS-F558-KEY-FLD                                 
              ADD 1                    TO WS-MATCH                              
              PERFORM 410-COMPUTE-MTH-RESIDE THRU 410-EXIT                      
              WRITE P13F558A-REC FROM BP13F558-REC                              
              ADD  1                   TO WS-F558-STORED                        
              PERFORM 450-CHECK-MTH-RESIDE THRU 450-EXIT                        
                UNTIL WS-F716-KEY-FLD NOT = WS-F558-KEY-FLD                     
              PERFORM 300-READ-F558 THRU 300-EXIT                               
           ELSE                                                                 
               IF WS-F716-KEY-FLD < WS-F558-KEY-FLD                             
                   ADD 1 TO WS-F716-NOTMATCH                                    
                   PERFORM 200-READ-F716 THRU 200-EXIT                          
               ELSE                                                             
                   ADD 1 TO WS-F558-NOTMATCH                                    
                   WRITE P13F558A-REC FROM BP13F558-REC                         
                   ADD  1           TO WS-F558-STORED                           
                   PERFORM 300-READ-F558 THRU 300-EXIT                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       410-COMPUTE-MTH-RESIDE.                                                  
      *-------------------------------------------------------------            
                                                                                
           MOVE F716-DTE-MOVING-IN TO F558-DTE-MOVE-IN-RDB.                     
                                                                                
           IF F558-NUM-SUBLET-SMS IS NOT NUMERIC                                
              MOVE ZEROES TO F558-NUM-SUBLET-SMS.                               
                                                                                
           IF F716-NUM-MTH-RESIDE IS NOT NUMERIC                                
              MOVE ZEROES TO WS-F716-NUM-MTH-RESIDE                             
           ELSE                                                                 
              MOVE F716-NUM-MTH-RESIDE TO WS-F716-NUM-MTH-RESIDE                
           END-IF.                                                              
                                                                                
           IF WS-F716-NUM-MTH-RESIDE > ZEROES                                   
              COMPUTE WS-NUM-MTH-RESIDE = WS-F716-NUM-MTH-RESIDE                
                                        - F558-NUM-SUBLET-SMS                   
              MOVE WS-NUM-MTH-RESIDE   TO F558-NUM-MTH-RESIDE                   
              MOVE WS-NUM-MTH-RESIDE   TO F558-NUM-MTH-RESIDE-SMS               
           END-IF.                                                              
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       450-CHECK-MTH-RESIDE.                                                    
      *-------------------------------------------------------------            
           PERFORM 200-READ-F716 THRU 200-EXIT.                                 
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-FINALIZATION.                                                        
      *-------------------------------------------------------------            
             COMPUTE WS-F716-NOTMATCH = WS-F716-READ - WS-MATCH.                
                                                                                
             DISPLAY '*--------BP13C625  -----------*'.                         
             DISPLAY '                               '.                         
             DISPLAY '* MATCH REFERENCE NO IN F716 AND  '.                      
             DISPLAY '* F558 AND UPDATE MTH-RESIDE    '.                        
             DISPLAY '* FROM F716 TO F558             '.                        
             DISPLAY '                               '.                         
             DISPLAY ' BP13F716-READ      ' WS-F716-READ.                       
             DISPLAY ' BP13F558-READ      ' WS-F558-READ.                       
             DISPLAY ' NO OF F716 MATCHED ' WS-MATCH.                           
             DISPLAY ' F716-NOT-MATCH     ' WS-F716-NOTMATCH.                   
             DISPLAY ' F558-NOT-MATCH     ' WS-F558-NOTMATCH.                   
             DISPLAY ' RECORDS WRITTEN    ' WS-F558-STORED.                     
                                                                                
           CLOSE      BP13F716                                                  
                      BP13F558                                                  
                      P13F558A.                                                 
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
