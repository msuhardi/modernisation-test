      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C31U.                                                 
      *AUTHOR.        KARL ANDRUS MAGALONA CABUG.                               
      *DATE-WRITTEN.  17/04/23.                                                 
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *    PATCHING COTMA RECORDS IN FINANCIAL HIST. TRANS FILE*                
      *                                                        *                
      *                                                        *                
      *     INPUT FILE :                                       *                
      *       1. BP13F001                                      *                
      *                                                        *                
      *     I/O  FILES :                                       *                
      *       1. BP13K130                                      *                
      *       2. BP13K650                                      *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *       1. BP13L31U - UNMATCH AMOUNT                     *                
      * ====================================================== *                
      * CHG REF   BY    DATE   DESCRIPTION                     *                
      * -------- ---- -------- ----------                      *                
      * BP139567 KAC1 17/04/23 NEW PROGRAM                     *                
      * ====================================================== *                
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F001   ASSIGN       TO BP13F001.                          
                                                                                
           SELECT  BP13K130  ASSIGN       TO BP13K130                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K130-KEY-FLD                       
                             FILE STATUS  IS BP13K130-STATUS.                   
                                                                                
           SELECT  BP13K650  ASSIGN       TO BP13K650                           
                             ORGANIZATION IS INDEXED                            
                             ACCESS MODE  IS RANDOM                             
                             RECORD KEY   IS K650-NUM-REGN                      
                             FILE STATUS  IS BP13K650-STATUS.                   
                                                                                
           SELECT  BP13L31U  ASSIGN       TO BP13L31U.                          
                                                                                
      ***********************                                                   
       DATA DIVISION.                                                           
      ***********************                                                   
       FILE SECTION.                                                            
                                                                                
       FD   BP13K130  RECORD  CONTAINS 150 CHARACTERS.                          
       COPY BP13K130.                                                           
                                                                                
       FD   BP13K650  RECORD CONTAINS 500 CHARACTERS.                           
       COPY BP13K650.                                                           
                                                                                
       FD   BP13F001  BLOCK CONTAINS 0 RECORDS                                  
                     RECORD CONTAINS 100 CHARACTERS                             
                     RECORDING MODE IS F                                        
                     LABEL RECORDS ARE STANDARD.                                
       01  BP13F001-REC.                                                        
           05  F001-NUM-REGN                   PIC X(08).                       
           05  F001-NUM-NRIC                   PIC X(09).                       
           05  F001-BATCH-NO                   PIC X(08).                       
           05  F001-VR-NO                      PIC X(08).                       
           05  F001-RSLD-DATE                  PIC X(08).                       
           05  F001-AMT-PAYABLE                PIC 9(05)V99.                    
           05  FILLER                          PIC X(52).                       
                                                                                
       FD  BP13L31U                                                             
           RECORD CONTAINS 133 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED.                                           
       01  BP13L31U-RECORD                     PIC X(133).                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-PAGE                         PIC 9(04)  VALUE ZEROS.          
           05  WS-CNT                          PIC 9(02)  VALUE ZEROS.          
           05  WS-SEQ-NO                       PIC 9(04)  VALUE ZEROS.          
           05  WS-SEQ-NO-TOT                   PIC 9(05)  VALUE ZEROS.          
                                                                                
       01  WS-FLAGS.                                                            
           05  EOF-F001                        PIC X(01)  VALUE SPACES.         
           05  DTL1-SW                         PIC X(01)  VALUE SPACES.         
           05  WS-K130-FND                     PIC X(01)  VALUE SPACES.         
           05  WS-K650-FND                     PIC X(01)  VALUE SPACES.         
           05  WS-K130-WRITE-OK                PIC X(01)  VALUE SPACES.         
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-YY                           PIC 9(4).                        
           05  WS-MM                           PIC 99.                          
           05  WS-DD                           PIC 99.                          
                                                                                
       01  WS-VARIABLES.                                                        
           05 BP13K130-STATUS                  PIC 99     VALUE ZEROS.          
           05 BP13K650-STATUS                  PIC 99     VALUE ZEROS.          
           05 WS-K650-NTFND-CNT                PIC 9(05)  VALUE ZEROS.          
           05 WS-CNT-F001-TOT                  PIC 9(05)  VALUE ZEROS.          
           05 WS-CNT-K130-WRITE                PIC 9(05)  VALUE ZEROS.          
           05 WS-CNT-L31U-PROC                 PIC 9(05)  VALUE ZEROS.          
           05 WS-CNT-L31U-ERROR                PIC 9(05)  VALUE ZEROS.          
           05 WS-NUM-REGN                      PIC X(08)  VALUE SPACES.         
           05 WS-OUTFILE-REC                   PIC X(133) VALUE SPACES.         
           05 WS-AMT-PAYABLE                   PIC S9(05)V99.                   
                                                                                
      ************************************************************              
       PROCEDURE DIVISION.                                                      
      ************************************************************              
                                                                                
      *---------------------*                                                   
       0000-MAIN.                                                               
      *---------------------*                                                   
                                                                                
           PERFORM 1000-OPEN-FILES           THRU 1000-EXIT.                    
           PERFORM 1500-READ-BP13F001        THRU 1500-EXIT.                    
           PERFORM 2000-PROCESS-RTN          THRU 2000-EXIT                     
             UNTIL EOF-F001 = 'Y'.                                              
                                                                                
           GO TO 9000-CLOSE-ROUTINE.                                            
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       1000-OPEN-FILES.                                                         
      *---------------------*                                                   
           OPEN INPUT BP13F001                                                  
               OUTPUT BP13L31U                                                  
                  I-O BP13K650                                                  
                      BP13K130.                                                 
                                                                                
           IF BP13K130-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K130 : OPEN ERROR, STATUS = ' BP13K130-STATUS        
              MOVE     BP13K130-STATUS           TO RETURN-CODE                 
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF BP13K650-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K650 : OPEN ERROR, STATUS = ' BP13K650-STATUS        
              MOVE     BP13K650-STATUS           TO RETURN-CODE                 
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       1500-READ-BP13F001.                                                      
      *---------------------*                                                   
           READ BP13F001 AT END                                                 
                MOVE 'Y' TO EOF-F001                                            
                GO TO 1500-EXIT.                                                
           ADD 1 TO WS-CNT-F001-TOT.                                            
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       2000-PROCESS-RTN.                                                        
      *---------------------*                                                   
           PERFORM 2100-READ-BP13K650          THRU 2100-EXIT.                  
           PERFORM 3000-PROCESS-UPDATE         THRU 3000-EXIT.                  
                                                                                
           PERFORM 1500-READ-BP13F001          THRU 1500-EXIT.                  
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *---------------------*                                                   
       2100-READ-BP13K650.                                                      
      *---------------------*                                                   
           MOVE SPACES                           TO K650-NUM-REGN.              
           MOVE F001-NUM-REGN                    TO K650-NUM-REGN.              
                                                                                
           READ BP13K650.                                                       
                                                                                
           EVALUATE BP13K650-STATUS                                             
               WHEN '00'                                                        
                    MOVE 'Y'                     TO WS-K650-FND                 
               WHEN '23'                                                        
                    MOVE 'N'                     TO WS-K650-FND                 
                    ADD   1                      TO WS-K650-NTFND-CNT           
               WHEN OTHER                                                       
                 DISPLAY 'K650 : READ ERROR, STATUS = ' BP13K650-STATUS         
                 DISPLAY '             K650-KEY-FLD = ' K650-NUM-REGN           
                 MOVE BP13K650-STATUS            TO RETURN-CODE                 
                 GO TO 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3000-PROCESS-UPDATE.                                                     
      *---------------------*                                                   
           IF WS-K650-FND = 'Y'                                                 
              MOVE 'V'                          TO K650-CDE-PRINT               
              MOVE F001-BATCH-NO                TO K650-NUM-BATCH               
              MOVE F001-VR-NO(4:5)              TO K650-NUM-VOUCHER             
                                                                                
              MOVE F001-AMT-PAYABLE  TO WS-AMT-PAYABLE                          
              IF K650-AMT-COTMA-SETTLEMENT NOT NUMERIC                          
                 MOVE ZEROES  TO K650-AMT-COTMA-SETTLEMENT                      
              END-IF                                                            
                                                                                
              IF K650-AMT-COTMA-SETTLEMENT NOT = WS-AMT-PAYABLE                 
                 ADD 1                          TO WS-CNT-L31U-ERROR            
                 WRITE BP13L31U-RECORD FROM BP13F001-REC                        
              ELSE                                                              
                PERFORM 3100-REWRITE-BP13K650 THRU 3100-EXIT                    
                                                                                
                MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE                  
                MOVE SPACES                     TO BP13K130-CDHIST              
                                                                                
                MOVE '15'                       TO K130-TRANS-TYPE              
                MOVE F001-NUM-REGN              TO K130-NUM-ORIG-REGN           
                MOVE F001-RSLD-DATE             TO K130-DTE-POST                
                MOVE WS-CUR-DATE                TO K130-DTE-TRANS               
                MOVE 001                        TO K130-COUNT-HISTORY           
                MOVE F001-NUM-NRIC              TO K130-NUM-NRIC                
                                                                                
                MOVE F001-BATCH-NO              TO K130-NUM-JRNO                
                MOVE F001-VR-NO(4:5)            TO K130-NUM-DOC                 
                                                                                
      *         STRING F001-BATCH-NO F001-VR-NO(4:5) DELIMITED BY SIZE          
      *                INTO K130-NUM-RECPT-JRNO                                 
      *         END-STRING                                                      
                                                                                
                MOVE 'SSM'                      TO K130-PAYMENT-TYPE            
                MOVE F001-AMT-PAYABLE           TO K130-AMT-CASH                
                                                                                
                MOVE ZEROES                     TO WS-CNT                       
                MOVE 'N'                        TO WS-K130-WRITE-OK             
                                                                                
                WRITE BP13K130-CDHIST                                           
                IF BP13K130-STATUS = 00                                         
                   ADD 1 TO WS-CNT-K130-WRITE                                   
                   GO TO 3000-EXIT                                              
                END-IF                                                          
                                                                                
                IF BP13K130-STATUS  = 22                                        
                   PERFORM 3200-WRITE-BP13K130   THRU 3200-EXIT                 
                     UNTIL WS-CNT > 98 OR                                       
                         WS-K130-WRITE-OK = 'Y'                                 
                ELSE                                                            
                   DISPLAY 'BP13K130 FILE REWRITE ERROR, STATUS '               
                                               BP13K130-STATUS                  
                   MOVE BP13K130-STATUS        TO RETURN-CODE                   
                   GO TO 9000-CLOSE-ROUTINE                                     
                END-IF                                                          
                                                                                
              END-IF                                                            
           END-IF.                                                              
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3100-REWRITE-BP13K650.                                                   
      *---------------------*                                                   
           REWRITE BP13K650-REC.                                                
                                                                                
           EVALUATE BP13K650-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO WS-CNT-L31U-PROC                                   
               WHEN OTHER                                                       
                    DISPLAY 'BP13K650 FILE REWRITE ERROR, STATUS '              
                                                    BP13K650-STATUS             
                    MOVE BP13K650-STATUS   TO   RETURN-CODE                     
                    GO TO 9000-CLOSE-ROUTINE                                    
           END-EVALUATE.                                                        
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3200-WRITE-BP13K130.                                                     
      *---------------------*                                                   
           WRITE BP13K130-CDHIST.                                               
           IF BP13K130-STATUS = 00                                              
              MOVE 'Y'                      TO WS-K130-WRITE-OK                 
              ADD 1                         TO WS-CNT-K130-WRITE                
           ELSE                                                                 
             IF BP13K130-STATUS = 22                                            
                ADD 1                       TO K130-COUNT-HISTORY               
                                               WS-CNT                           
             ELSE                                                               
                DISPLAY 'BP13K130 FILE REWRITE ERROR, STATUS '                  
                                               BP13K130-STATUS                  
                MOVE BP13K130-STATUS        TO RETURN-CODE                      
                GO TO 9000-CLOSE-ROUTINE                                        
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       9000-CLOSE-ROUTINE.                                                      
      *---------------------*                                                   
           DISPLAY '*-------------------------------------*'                    
           DISPLAY '*             BP13C31U                *'                    
           DISPLAY '*-------------------------------------*'                    
           DISPLAY ' NO OF TOTAL     CASES  : ' WS-CNT-F001-TOT                 
           DISPLAY ' NO OF PROCESSED CASES  : ' WS-CNT-L31U-PROC                
           DISPLAY ' NO OF BP13K130 WRITE   : ' WS-CNT-K130-WRITE               
           DISPLAY ' NO OF UNMATCH  AMOUNT  : ' WS-CNT-L31U-ERROR               
           DISPLAY ' NO OF K650 NOT FOUND   : ' WS-K650-NTFND-CNT               
           CLOSE BP13F001                                                       
                 BP13L31U                                                       
                 BP13K130                                                       
                 BP13K650.                                                      
           STOP RUN.                                                            
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
