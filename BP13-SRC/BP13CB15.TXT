       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB15.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  19/08/2016.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVES  : GENERATE INTER-RACIAL COUPLE REPORT         *            
      *                                                            *            
      *  INPUT FILE  : BP13F730 - REQUEST DATA ENTRY FILE          *            
      *                BP13K757 - BUILT-TO-ORDER MASTER FILE       *            
      *                BP13K825 - REQUEST OCCUPIER FILE            *            
      *                BP13K060 - CODE TRANSCRIPTION FILE          *            
      *  OUTPUT FILE : BP13LB15 - INTER-RACIAL COUPLE REPORT       *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-REF# BY   DATE     DESCRIPTION                         *            
      * -------- ---- -------- ----------------------------------- *            
      * BP136358 SMR2 19082016 NEW PROGRAM                         *            
      * BP137048 SMR2 07112017 UPDATE CATEGORY IN BP13K730 AND     *            
      *                        ADD NEW COLUMN FOR CITIZENSHIP      *            
      * BP137494 AM25 26092018 ADDTL CHECKING OF HA2 TO HA4 AND    *            
      *                        SPOUSES ETHNIC AGAINST PREF ETHNIC  *            
      *                        TO PRINT APPL1 IF FOUND/NOT IN K825 *            
      * BP137494 AM25 10102018 REPORT FORMAT UPDATES               *            
      * BP137783 AM25 10042019 NEW REPORT FOR PRCAT=MHACAT RECORDS *            
      * BP139280 EAA2 27102023 EXPAND BP13K757 FROM 3000 TO 4000   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13K757 ASSIGN       TO BP13K757                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K757-KEY-FLD                         
                           FILE STATUS  IS WS-K757-STATUS.                      
                                                                                
           SELECT BP13K730 ASSIGN       TO BP13K730                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K730-NUM-REGN                        
                           FILE STATUS  IS WS-K730-STATUS.                      
                                                                                
           SELECT BP13K825 ASSIGN       TO BP13K825                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K825-KEY-FLD                         
                           FILE STATUS  IS WS-K825-STATUS.                      
                                                                                
           SELECT BP13K060 ASSIGN       TO BP13K060                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K060-KEY-FLD                         
                           FILE STATUS  IS WS-K060-STATUS.                      
                                                                                
           SELECT BP13LB15 ASSIGN       TO BP13LB15.                            
           SELECT P13LB15A ASSIGN       TO P13LB15A.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K757                                                            
            RECORD CONTAINS 4000 CHARACTERS.                                    
       COPY BP13K757.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K730.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS   25 CHARACTERS.                                    
       COPY BP13K060.                                                           
                                                                                
       FD   BP13LB15                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  132 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORDS    ARE OMITTED.                                       
       01   BP13LB15-REC                   PIC X(132).                          
                                                                                
       FD   P13LB15A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  132 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORDS    ARE OMITTED.                                       
       01   P13LB15A-REC                   PIC X(132).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K757-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K730-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K825-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K060-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(05)   VALUE ZEROES.            
           05  WS-LB15-SNO                 PIC 9(05)   VALUE ZEROES.            
           05  WS-LB15A-SNO                PIC 9(05)   VALUE ZEROES.            
           05  WS-F730-BYPASS              PIC 9(05)   VALUE ZEROES.            
           05  WS-F730-BLANK               PIC 9(05)   VALUE ZEROES.            
           05  WS-LB15-WRITE               PIC 9(05)   VALUE ZEROES.            
           05  WS-LB15A-WRITE              PIC 9(05)   VALUE ZEROES.            
           05  WS-F730-SAME                PIC 9(05)   VALUE ZEROES.            
           05  WS-K730-UPD                 PIC 9(05)   VALUE ZEROES.            
           05  WS-K730-PREFER              PIC 9(05)   VALUE ZEROES.            
           05  WS-K730-BLANK               PIC 9(05)   VALUE ZEROES.            
           05  WS-LB15-PAGE                PIC 9(05)   VALUE ZEROES.            
           05  WS-LB15-LINE                PIC 9(02)   VALUE 60.                
           05  WS-LB15A-PAGE               PIC 9(05)   VALUE ZEROES.            
           05  WS-LB15A-LINE               PIC 9(02)   VALUE 60.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K825-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K825-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-SAME-ETHNIC              PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CUR-DATE                 PIC X(08)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NRIC                     PIC X(09)   VALUE SPACES.            
           05  WS-DET-APPLN                PIC X(01)   VALUE SPACES.            
           05  WS-CAT                      PIC X(01)   VALUE SPACES.            
           05  WS-ETHNIC                   PIC X(01)   VALUE SPACES.            
           05  WS-CDE-CAT                  PIC X(01)   VALUE SPACES.            
           05  WS-APPL-RACE                PIC X(01)   VALUE SPACES.            
           05  WS-APPL-CAT                 PIC X(01)   VALUE SPACES.            
           05  WS-SPOUSE-CAT               PIC X(01)   VALUE SPACES.            
           05  WS-UPD-CAT                  PIC X(01)   VALUE SPACES.            
           05  WS-PREV-SMODE               PIC X(03)   VALUE SPACES.            
           05  WS-PREV-SMODEA              PIC X(03)   VALUE SPACES.            
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 LB15-HEADER1.                                                      
             10 LB15-RPTNAME      PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(06)  VALUE SPACES.                      
             10 FILLER            PIC X(04)  VALUE 'HDB3'.                      
             10 FILLER            PIC X(28)  VALUE SPACES.                      
             10 FILLER            PIC X(39)  VALUE                              
                'S Y S T E M   O F   C O M M I T M E N T'.                      
             10 FILLER            PIC X(13)  VALUE SPACES.                      
             10 FILLER            PIC X(07)  VALUE 'DATE : '.                   
             10 LB15-DATE         PIC X(10)  VALUE SPACES.                      
             10 FILLER            PIC X(04)  VALUE SPACES.                      
             10 FILLER            PIC X(07)  VALUE 'PAGE : '.                   
             10 LB15-PAGE-NO      PIC ZZZZ9 .                                   
          05 LB15-HEADER2.                                                      
             10 FILLER            PIC X(45)  VALUE SPACES.                      
             10 FILLER            PIC X(37)  VALUE                              
                'INTER-RACIAL RECLASSIFICATION REPORT '.                        
             10 LB15-SMODE        PIC X(05)  VALUE SPACES.                      
          05 LB15-SUB-HEADER2.                                                  
             10 FILLER            PIC X(51)  VALUE SPACES.                      
             10 FILLER            PIC X(29)  VALUE                              
                'PREFERRED ETHNIC = MHA ETHNIC'.                                
          05 LB15-HEADER3.                                                      
             10 FILLER            PIC X(43)  VALUE                              
                'S/NO.  NT   FT  REGN NO.  NRIC NO.         '.                  
             10 FILLER            PIC X(50)  VALUE                              
                'RELATIONSHIP          CITZ  MHA INDIVIDUAL CAT    '.           
             10 FILLER            PIC X(39)  VALUE                              
                'HH CAT     PREFERRED CAT  UPDATED CAT  '.                      
          05 LB15-BLANK.                                                        
             10 FILLER            PIC X(132) VALUE SPACES.                      
          05 LB15-DASH.                                                         
             10 FILLER            PIC X(132) VALUE ALL '-'.                     
          05 LB15-RECORD.                                                       
             10 LB15-SNO          PIC X(05)  VALUE SPACES.                      
             10 FILLER            PIC X(02)  VALUE SPACES.                      
             10 LB15-NT           PIC X(03)  VALUE SPACES.                      
             10 FILLER            PIC X(02)  VALUE SPACES.                      
             10 LB15-FT           PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(02)  VALUE SPACES.                      
             10 LB15-REGN         PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(02)  VALUE SPACES.                      
             10 LB15-DETAIL       PIC X(61)  VALUE SPACES.                      
             10 FILLER            PIC X(06)  VALUE SPACES.                      
             10 LB15-HH-CAT-DESC  PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE SPACES.                      
             10 LB15-PR-CAT-DESC  PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(07)  VALUE SPACES.                      
             10 LB15-UPD-CAT-DESC PIC X(08)  VALUE SPACES.                      
          05 LB15-DET-REC.                                                      
             10 LB15-DET-NRIC     PIC X(09)  VALUE SPACES.                      
             10 FILLER            PIC X(02)  VALUE SPACES.                      
             10 LB15-DET-APPLN    PIC X(04)  VALUE SPACES.                      
             10 FILLER            PIC X(02)  VALUE SPACES.                      
             10 LB15-DET-REL-DESC PIC X(20)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE SPACES.                      
             10 LB15-DET-CTZ      PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE SPACES.                      
             10 LB15-DET-CAT-DESC PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(08)  VALUE SPACES.                      
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECS       THRU 3000-EXIT                       
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K757                                                 
                       BP13K825                                                 
                       BP13K060                                                 
                I-O    BP13K730                                                 
                OUTPUT BP13LB15                                                 
                       P13LB15A.                                                
                                                                                
           IF WS-K757-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K757 FILE ERROR ' WS-K757-STATUS             
              MOVE WS-K757-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K730 FILE ERROR ' WS-K730-STATUS             
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K060 ERROR ' WS-K060-STATUS                  
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4) DELIMITED BY SIZE                            
             INTO LB15-DATE.                                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F730.                                                      
      *********************                                                     
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                   TO WS-F730-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F730-READ.                     
                                                                                
           EVALUATE F730-CDE-CATEGORY                                           
           WHEN '1'                                                             
              MOVE 'M'                     TO WS-CAT                            
                                                                                
           WHEN '2'                                                             
              MOVE 'C'                     TO WS-CAT                            
                                                                                
           WHEN '3'                                                             
              MOVE 'I'                     TO WS-CAT                            
                                                                                
           WHEN '4'                                                             
              MOVE 'O'                     TO WS-CAT                            
                                                                                
           WHEN OTHER                                                           
              MOVE F730-CDE-CATEGORY       TO WS-CAT                            
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       3000-PROCESS-RECS.                                                       
      ********************                                                      
                                                                                
           MOVE SPACES                     TO WS-APPL-CAT                       
                                              WS-SPOUSE-CAT                     
                                              WS-UPD-CAT                        
                                              WS-APPL-RACE.                     
                                                                                
           PERFORM 4000-READ-BP13K757      THRU 4000-EXIT.                      
                                                                                
      *** K757-NUM-APPL-RACE = M/C/I/O                                          
      *** F730-CDE-CATEGORY  = 1/2/3/4                                          
           IF K757-NUM-APPL-RACE NOT = SPACES AND LOW-VALUES                    
              IF K757-NUM-APPL-RACE NOT = WS-CAT                                
                 MOVE 'N'                  TO WS-K825-FND                       
                                              WS-SAME-ETHNIC                    
                                                                                
                 MOVE 'APPL'               TO LB15-DET-APPLN                    
                 MOVE '1'                  TO WS-DET-APPLN                      
                 MOVE F730-NUM-NRIC1       TO WS-NRIC                           
                 PERFORM 5000-READ-BP13K825   THRU 5000-EXIT                    
                 IF WS-K825-FND = 'Y'                                           
                    PERFORM 6500-STARTBR-K825-SP THRU 6500-EXIT                 
                 ELSE                                                           
                    MOVE SPACES            TO LB15-DET-REC                      
                 END-IF                                                         
                 PERFORM 4100-CHECK-APPL1     THRU 4100-EXIT                    
                 IF WS-SAME-ETHNIC = 'Y'                                        
                    ADD 1                  TO WS-LB15A-WRITE                    
                    PERFORM 7900-WRITE-P13LB15A THRU 7900-EXIT                  
                 ELSE                                                           
                    ADD 1                  TO WS-LB15-WRITE                     
                    PERFORM 8000-WRITE-BP13LB15 THRU 8000-EXIT                  
                 END-IF                                                         
                                                                                
                 PERFORM 5200-CHECK-OTHER-HA  THRU 5200-EXIT                    
                 PERFORM 6000-STARTBR-K825    THRU 6000-EXIT                    
                 PERFORM 7500-READ-BP13K730   THRU 7500-EXIT                    
              ELSE                                                              
                 ADD 1                     TO WS-F730-BYPASS                    
                                              WS-F730-SAME                      
              END-IF                                                            
           ELSE                                                                 
              ADD 1                        TO WS-F730-BYPASS                    
                                              WS-F730-BLANK                     
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13K757.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BP13K757-REC.                     
           INITIALIZE                         BP13K757-REC.                     
                                                                                
           MOVE F730-NUM-REGN              TO K757-NUM-REGN.                    
                                                                                
           READ BP13K757.                                                       
                                                                                
           EVALUATE WS-K757-STATUS                                              
           WHEN ZEROES                                                          
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K757 : ' K757-NUM-REGN           
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K757 : ' WS-K757-STATUS                
              MOVE WS-K757-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       4100-CHECK-APPL1.                                                        
      *******************                                                       
                                                                                
           MOVE WS-APPL-CAT            TO WS-APPL-RACE.                         
           PERFORM 4200-VALIDATE-APPL-RACE    THRU 4200-EXIT.                   
                                                                                
           IF WS-SAME-ETHNIC = 'N'                                              
              MOVE WS-SPOUSE-CAT       TO WS-APPL-RACE                          
              PERFORM 4200-VALIDATE-APPL-RACE THRU 4200-EXIT                    
                                                                                
              PERFORM 4300-CHECK-APPL2        THRU 4300-EXIT                    
           END-IF.                                                              
                                                                                
           IF WS-UPD-CAT = SPACES                                               
              ADD 1                    TO WS-K730-BLANK                         
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************                                                
       4200-VALIDATE-APPL-RACE.                                                 
      **************************                                                
                                                                                
           IF K757-NUM-APPL-RACE = WS-APPL-RACE                                 
              MOVE 'Y'                 TO WS-SAME-ETHNIC                        
                                                                                
              EVALUATE K757-NUM-APPL-RACE                                       
                 WHEN 'C'                                                       
                    MOVE '2'           TO WS-UPD-CAT                            
                 WHEN 'M'                                                       
                    MOVE '1'           TO WS-UPD-CAT                            
                 WHEN 'I'                                                       
                    MOVE '3'           TO WS-UPD-CAT                            
                 WHEN 'O'                                                       
                    MOVE '4'           TO WS-UPD-CAT                            
                 WHEN OTHER                                                     
                    MOVE K757-NUM-APPL-RACE                                     
                                       TO WS-UPD-CAT                            
              END-EVALUATE                                                      
                                                                                
              ADD 1                    TO WS-K730-PREFER                        
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       4300-CHECK-APPL2.                                                        
      *******************                                                       
                                                                                
           IF WS-SAME-ETHNIC = 'N'                                              
              IF (F730-NUM-NRIC2 NOT = SPACES AND LOW-VALUES)                   
                 IF F730-NUM-NRIC2(1:1) NOT = '#'                               
                    MOVE F730-NUM-REGN TO K825-NUM-REGN                         
                    MOVE F730-NUM-NRIC2                                         
                                       TO K825-NUM-NRIC                         
                    PERFORM 4400-READ-BP13K825     THRU 4400-EXIT               
                    PERFORM 4500-CHECK-APPL-SPOUSE THRU 4500-EXIT               
                                                                                
                    PERFORM 4600-CHECK-APPL3       THRU 4600-EXIT               
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 4600-CHECK-APPL3          THRU 4600-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4400-READ-BP13K825.                                                      
      *********************                                                     
                                                                                
           READ BP13K825.                                                       
                                                                                
           EVALUATE WS-K825-STATUS                                              
              WHEN 00                                                           
                 PERFORM 4450-GET-APPL-RACE      THRU 4450-EXIT                 
                 PERFORM 4200-VALIDATE-APPL-RACE THRU 4200-EXIT                 
              WHEN 23                                                           
                 MOVE SPACES           TO K825-NUM-NRIC-SPOUSE                  
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING ON BP13K825'                            
                 MOVE WS-K825-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4450-GET-APPL-RACE.                                                      
      *********************                                                     
                                                                                
           EVALUATE K825-NUM-ETHNIC                                             
              WHEN '0'                                                          
                 MOVE 'C'              TO WS-APPL-RACE                          
              WHEN '5'                                                          
                 MOVE 'M'              TO WS-APPL-RACE                          
              WHEN '6'                                                          
                 MOVE 'I'              TO WS-APPL-RACE                          
              WHEN '7'                                                          
                 MOVE 'O'              TO WS-APPL-RACE                          
              WHEN OTHER                                                        
                 MOVE K825-NUM-ETHNIC  TO WS-APPL-RACE                          
           END-EVALUATE.                                                        
                                                                                
       4450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************                                                 
       4500-CHECK-APPL-SPOUSE.                                                  
      *************************                                                 
                                                                                
           IF WS-SAME-ETHNIC = 'N'                                              
              IF (K825-NUM-NRIC-SPOUSE NOT = SPACES AND LOW-VALUES)             
                 MOVE F730-NUM-REGN    TO K825-NUM-REGN                         
                 MOVE K825-NUM-NRIC-SPOUSE                                      
                                       TO K825-NUM-NRIC                         
                 PERFORM 4400-READ-BP13K825 THRU 4400-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       4600-CHECK-APPL3.                                                        
      *******************                                                       
                                                                                
           IF WS-SAME-ETHNIC = 'N'                                              
              IF (F730-NUM-NRIC3 NOT = SPACES AND LOW-VALUES)                   
                 IF F730-NUM-NRIC3(1:1) NOT = '#'                               
                    MOVE F730-NUM-REGN TO K825-NUM-REGN                         
                    MOVE F730-NUM-NRIC3                                         
                                       TO K825-NUM-NRIC                         
                    PERFORM 4400-READ-BP13K825     THRU 4400-EXIT               
                    PERFORM 4500-CHECK-APPL-SPOUSE THRU 4500-EXIT               
                                                                                
                    PERFORM 4700-CHECK-APPL4       THRU 4700-EXIT               
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 4700-CHECK-APPL4          THRU 4700-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       4700-CHECK-APPL4.                                                        
      *******************                                                       
                                                                                
           IF WS-SAME-ETHNIC = 'N'                                              
              IF (F730-NUM-NRIC4 NOT = SPACES AND LOW-VALUES)                   
                 IF F730-NUM-NRIC4(1:1) NOT = '#'                               
                    MOVE F730-NUM-REGN TO K825-NUM-REGN                         
                    MOVE F730-NUM-NRIC4                                         
                                       TO K825-NUM-NRIC                         
                    PERFORM 4400-READ-BP13K825     THRU 4400-EXIT               
                    PERFORM 4500-CHECK-APPL-SPOUSE THRU 4500-EXIT               
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5000-READ-BP13K825.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE F730-NUM-REGN              TO K825-NUM-REGN.                    
           MOVE WS-NRIC                    TO K825-NUM-NRIC.                    
                                                                                
           READ BP13K825.                                                       
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K825-FND                       
              PERFORM 5100-MOVE-DETAILS    THRU 5100-EXIT                       
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K825-FND                       
              DISPLAY 'RECORD NOT FOUND IN BP13K825 : ' K825-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K825'                               
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       5100-MOVE-DETAILS.                                                       
      ********************                                                      
                                                                                
           MOVE K825-NUM-NRIC              TO LB15-DET-NRIC.                    
                                                                                
           MOVE SPACES                     TO BP13K060-REC.                     
           MOVE '35'                       TO K060-SERIAL-NO.                   
           MOVE K825-NUM-RELATIONSHIP      TO K060-CODE.                        
           PERFORM 7000-READ-BP13K060      THRU 7000-EXIT.                      
           MOVE K060-DESC                  TO LB15-DET-REL-DESC.                
                                                                                
           MOVE K825-NUM-CITIZENSHIP       TO LB15-DET-CTZ.                     
                                                                                
           EVALUATE K825-NUM-ETHNIC                                             
           WHEN '0'                                                             
              MOVE '2'                     TO WS-ETHNIC                         
           WHEN '5'                                                             
              MOVE '1'                     TO WS-ETHNIC                         
           WHEN '6'                                                             
              MOVE '3'                     TO WS-ETHNIC                         
           WHEN '7'                                                             
              MOVE '4'                     TO WS-ETHNIC                         
           WHEN OTHER                                                           
              MOVE K825-NUM-ETHNIC         TO WS-ETHNIC                         
           END-EVALUATE.                                                        
                                                                                
           MOVE SPACES                     TO BP13K060-REC.                     
           MOVE '09'                       TO K060-SERIAL-NO.                   
           MOVE WS-ETHNIC                  TO K060-CODE.                        
           PERFORM 7000-READ-BP13K060      THRU 7000-EXIT.                      
           MOVE K060-DESC(1:8)             TO LB15-DET-CAT-DESC.                
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       5200-CHECK-OTHER-HA.                                                     
      **********************                                                    
                                                                                
           IF F730-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC2(1:1) NOT = '#'                                  
                 MOVE 'APPL'               TO LB15-DET-APPLN                    
                 MOVE '2'                  TO WS-DET-APPLN                      
                 MOVE F730-NUM-NRIC2       TO WS-NRIC                           
                 PERFORM 5000-READ-BP13K825   THRU 5000-EXIT                    
                 IF WS-K825-FND = 'Y'                                           
                    PERFORM 7800-CHECK-SAME-ETHNIC THRU 7800-EXIT               
                 END-IF                                                         
              ELSE                                                              
                 GO TO 5200-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC3(1:1) NOT = '#'                                  
                 MOVE 'APPL'               TO LB15-DET-APPLN                    
                 MOVE '3'                  TO WS-DET-APPLN                      
                 MOVE F730-NUM-NRIC3       TO WS-NRIC                           
                 PERFORM 5000-READ-BP13K825   THRU 5000-EXIT                    
                 IF WS-K825-FND = 'Y'                                           
                    PERFORM 7800-CHECK-SAME-ETHNIC THRU 7800-EXIT               
                 END-IF                                                         
              ELSE                                                              
                 GO TO 5200-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC4(1:1) NOT = '#'                                  
                 MOVE 'APPL'               TO LB15-DET-APPLN                    
                 MOVE '4'                  TO WS-DET-APPLN                      
                 MOVE F730-NUM-NRIC4       TO WS-NRIC                           
                 PERFORM 5000-READ-BP13K825   THRU 5000-EXIT                    
                 IF WS-K825-FND = 'Y'                                           
                    PERFORM 7800-CHECK-SAME-ETHNIC THRU 7800-EXIT               
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       6000-STARTBR-K825.                                                       
      ********************                                                      
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K825-EOF.                      
                                                                                
           MOVE F730-NUM-REGN              TO K825-NUM-REGN.                    
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 6100-READNEXT-K825   THRU 6100-EXIT                       
              PERFORM 6200-PROCESS-K825    THRU 6200-EXIT                       
                UNTIL WS-K825-EOF = 'Y'                                         
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START ERROR BP13K825 : ' WS-K825-STATUS                  
                      ' REGN : ' K825-NUM-REGN                                  
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       6100-READNEXT-K825.                                                      
      *********************                                                     
                                                                                
           READ BP13K825 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K825 : ' K825-KEY-FLD            
              MOVE 'Y'                     TO WS-K825-EOF                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K825'                               
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       6200-PROCESS-K825.                                                       
      ********************                                                      
                                                                                
           IF F730-NUM-REGN = K825-NUM-REGN                                     
              IF K825-NUM-NRIC NOT = F730-NUM-NRIC1 AND                         
                                     F730-NUM-NRIC2 AND                         
                                     F730-NUM-NRIC3 AND                         
                                     F730-NUM-NRIC4                             
                 MOVE 'OCC '               TO LB15-DET-APPLN                    
                 MOVE 'O'                  TO WS-DET-APPLN                      
                 PERFORM 5100-MOVE-DETAILS    THRU 5100-EXIT                    
                 PERFORM 7800-CHECK-SAME-ETHNIC THRU 7800-EXIT                  
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                     TO WS-K825-EOF                       
           END-IF.                                                              
                                                                                
           PERFORM 6100-READNEXT-K825      THRU 6100-EXIT.                      
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       6500-STARTBR-K825-SP.                                                    
      ***********************                                                   
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K825-EOF.                      
                                                                                
           MOVE F730-NUM-REGN              TO K825-NUM-REGN.                    
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 6100-READNEXT-K825   THRU 6100-EXIT                       
              PERFORM 6600-PROCESS-K825-SP THRU 6600-EXIT                       
                UNTIL WS-K825-EOF = 'Y'                                         
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START ERROR BP13K825 : ' WS-K825-STATUS                  
                      ' REGN : ' K825-NUM-REGN                                  
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       6600-PROCESS-K825-SP.                                                    
      ***********************                                                   
                                                                                
           IF F730-NUM-REGN = K825-NUM-REGN                                     
              IF K825-NUM-RELATIONSHIP = '00' OR '20' OR '09'                   
                 EVALUATE K825-NUM-ETHNIC                                       
                 WHEN '0'                                                       
                    MOVE 'C'               TO WS-CDE-CAT                        
                 WHEN '5'                                                       
                    MOVE 'M'               TO WS-CDE-CAT                        
                 WHEN '6'                                                       
                    MOVE 'I'               TO WS-CDE-CAT                        
                 WHEN '7'                                                       
                    MOVE 'O'               TO WS-CDE-CAT                        
                 WHEN OTHER                                                     
                    MOVE K825-NUM-ETHNIC   TO WS-CDE-CAT                        
                 END-EVALUATE                                                   
                                                                                
                 IF K825-NUM-RELATIONSHIP = '00'                                
                    MOVE WS-CDE-CAT        TO WS-APPL-CAT                       
                 ELSE                                                           
                    IF K825-NUM-RELATIONSHIP = '20' OR '09'                     
                       MOVE WS-CDE-CAT     TO WS-SPOUSE-CAT                     
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                     TO WS-K825-EOF                       
           END-IF.                                                              
                                                                                
           PERFORM 6100-READNEXT-K825      THRU 6100-EXIT.                      
                                                                                
       6600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       7000-READ-BP13K060.                                                      
      *********************                                                     
                                                                                
           READ BP13K060.                                                       
                                                                                
           EVALUATE WS-K060-STATUS                                              
           WHEN ZEROES                                                          
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K060 : ' K060-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K060 : ' WS-K060-STATUS                
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       7500-READ-BP13K730.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BP13K730-REC.                     
           INITIALIZE                         BP13K730-REC.                     
                                                                                
           MOVE F730-NUM-REGN              TO K730-NUM-REGN.                    
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              PERFORM 7600-UPDATE-BP13K730 THRU 7600-EXIT                       
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K730 : ' K730-NUM-REGN           
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K730 : ' WS-K730-STATUS                
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       7500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       7600-UPDATE-BP13K730.                                                    
      ***********************                                                   
                                                                                
           MOVE WS-UPD-CAT                 TO K730-CDE-CATEGORY.                
                                                                                
           REWRITE BP13K730-REC.                                                
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K730-UPD                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING BP13K730 : ' WS-K730-STATUS                
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       7600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************                                                 
       7800-CHECK-SAME-ETHNIC.                                                  
      *************************                                                 
                                                                                
           IF WS-SAME-ETHNIC = 'Y'                                              
              PERFORM 7900-WRITE-P13LB15A THRU 7900-EXIT                        
           ELSE                                                                 
              PERFORM 8000-WRITE-BP13LB15 THRU 8000-EXIT                        
           END-IF.                                                              
                                                                                
       7800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       7900-WRITE-P13LB15A.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO P13LB15A-REC.                     
                                                                                
           IF F730-NUM-ALLO-CAT NOT = WS-PREV-SMODEA                            
              MOVE F730-NUM-ALLO-CAT       TO WS-PREV-SMODEA                    
              MOVE 60                      TO WS-LB15A-LINE                     
              MOVE ZEROES                  TO WS-LB15A-PAGE                     
                                              WS-LB15A-SNO                      
           END-IF.                                                              
                                                                                
           IF WS-LB15A-LINE > 55                                                
              ADD 1                        TO WS-LB15A-PAGE                     
              MOVE WS-LB15A-PAGE           TO LB15-PAGE-NO                      
                                                                                
              MOVE 'P13LB15A'              TO LB15-RPTNAME                      
              STRING '(' F730-NUM-ALLO-CAT ')'                                  
                     DELIMITED BY SIZE INTO LB15-SMODE                          
              WRITE P13LB15A-REC           FROM LB15-HEADER1 AFTER PAGE         
              WRITE P13LB15A-REC           FROM LB15-HEADER2                    
              WRITE P13LB15A-REC           FROM LB15-SUB-HEADER2                
              WRITE P13LB15A-REC           FROM LB15-BLANK                      
              WRITE P13LB15A-REC           FROM LB15-HEADER3                    
              WRITE P13LB15A-REC           FROM LB15-DASH                       
                                                                                
              MOVE 6                       TO WS-LB15A-LINE                     
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LB15-RECORD.                      
           IF WS-DET-APPLN = '1'                                                
              ADD 1                        TO WS-LB15A-SNO                      
              MOVE WS-LB15A-SNO            TO LB15-SNO                          
              MOVE F730-CDE-NT1            TO LB15-NT                           
              MOVE F730-CDE-FLAT-TYPE      TO LB15-FT                           
              MOVE F730-NUM-REGN           TO LB15-REGN                         
                                                                                
              MOVE SPACES                  TO BP13K060-REC                      
              MOVE '09'                    TO K060-SERIAL-NO                    
              MOVE F730-CDE-CATEGORY       TO K060-CODE                         
              PERFORM 7000-READ-BP13K060   THRU 7000-EXIT                       
              MOVE K060-DESC(1:8)          TO LB15-HH-CAT-DESC                  
                                                                                
              MOVE SPACES                  TO BP13K060-REC                      
              MOVE '31'                    TO K060-SERIAL-NO                    
              MOVE K757-NUM-APPL-RACE      TO K060-CODE                         
              PERFORM 7000-READ-BP13K060   THRU 7000-EXIT                       
              MOVE K060-DESC(1:8)          TO LB15-PR-CAT-DESC                  
                                                                                
              IF WS-UPD-CAT = SPACES                                            
                 MOVE SPACES               TO LB15-UPD-CAT-DESC                 
              ELSE                                                              
                 MOVE SPACES               TO BP13K060-REC                      
                 MOVE '09'                 TO K060-SERIAL-NO                    
                 MOVE WS-UPD-CAT           TO K060-CODE                         
                 PERFORM 7000-READ-BP13K060   THRU 7000-EXIT                    
                 MOVE K060-DESC(1:8)       TO LB15-UPD-CAT-DESC                 
              END-IF                                                            
                                                                                
              IF WS-LB15A-LINE > 6                                              
                 WRITE P13LB15A-REC        FROM LB15-BLANK                      
                 ADD 1                     TO WS-LB15A-LINE                     
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES                  TO LB15-SNO                          
                                              LB15-NT                           
                                              LB15-FT                           
                                              LB15-REGN                         
                                              LB15-HH-CAT-DESC                  
                                              LB15-PR-CAT-DESC                  
           END-IF.                                                              
                                                                                
           MOVE LB15-DET-REC               TO LB15-DETAIL.                      
           WRITE P13LB15A-REC              FROM LB15-RECORD.                    
           ADD 1                           TO WS-LB15A-LINE.                    
                                                                                
       7900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8000-WRITE-BP13LB15.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO BP13LB15-REC.                     
                                                                                
           IF F730-NUM-ALLO-CAT NOT = WS-PREV-SMODE                             
              MOVE F730-NUM-ALLO-CAT       TO WS-PREV-SMODE                     
              MOVE 60                      TO WS-LB15-LINE                      
              MOVE ZEROES                  TO WS-LB15-PAGE                      
                                              WS-LB15-SNO                       
           END-IF.                                                              
                                                                                
           IF WS-LB15-LINE > 55                                                 
              ADD 1                        TO WS-LB15-PAGE                      
              MOVE WS-LB15-PAGE            TO LB15-PAGE-NO                      
                                                                                
              MOVE 'BP13LB15'              TO LB15-RPTNAME                      
              STRING '(' F730-NUM-ALLO-CAT ')'                                  
                     DELIMITED BY SIZE INTO LB15-SMODE                          
              WRITE BP13LB15-REC           FROM LB15-HEADER1 AFTER PAGE         
              WRITE BP13LB15-REC           FROM LB15-HEADER2                    
              WRITE BP13LB15-REC           FROM LB15-BLANK                      
              WRITE BP13LB15-REC           FROM LB15-BLANK                      
              WRITE BP13LB15-REC           FROM LB15-HEADER3                    
              WRITE BP13LB15-REC           FROM LB15-DASH                       
                                                                                
              MOVE 6                       TO WS-LB15-LINE                      
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LB15-RECORD.                      
           IF WS-DET-APPLN = '1'                                                
              ADD 1                        TO WS-LB15-SNO                       
              MOVE WS-LB15-SNO             TO LB15-SNO                          
              MOVE F730-CDE-NT1            TO LB15-NT                           
              MOVE F730-CDE-FLAT-TYPE      TO LB15-FT                           
              MOVE F730-NUM-REGN           TO LB15-REGN                         
                                                                                
              MOVE SPACES                  TO BP13K060-REC                      
              MOVE '09'                    TO K060-SERIAL-NO                    
              MOVE F730-CDE-CATEGORY       TO K060-CODE                         
              PERFORM 7000-READ-BP13K060   THRU 7000-EXIT                       
              MOVE K060-DESC(1:8)          TO LB15-HH-CAT-DESC                  
                                                                                
              MOVE SPACES                  TO BP13K060-REC                      
              MOVE '31'                    TO K060-SERIAL-NO                    
              MOVE K757-NUM-APPL-RACE      TO K060-CODE                         
              PERFORM 7000-READ-BP13K060   THRU 7000-EXIT                       
              MOVE K060-DESC(1:8)          TO LB15-PR-CAT-DESC                  
                                                                                
              IF WS-UPD-CAT = SPACES                                            
                 MOVE SPACES               TO LB15-UPD-CAT-DESC                 
              ELSE                                                              
                 MOVE SPACES               TO BP13K060-REC                      
                 MOVE '09'                 TO K060-SERIAL-NO                    
                 MOVE WS-UPD-CAT           TO K060-CODE                         
                 PERFORM 7000-READ-BP13K060   THRU 7000-EXIT                    
                 MOVE K060-DESC(1:8)       TO LB15-UPD-CAT-DESC                 
              END-IF                                                            
                                                                                
              IF WS-LB15-LINE > 6                                               
                 WRITE BP13LB15-REC        FROM LB15-BLANK                      
                 ADD 1                     TO WS-LB15-LINE                      
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES                  TO LB15-SNO                          
                                              LB15-NT                           
                                              LB15-FT                           
                                              LB15-REGN                         
                                              LB15-HH-CAT-DESC                  
                                              LB15-PR-CAT-DESC                  
           END-IF.                                                              
                                                                                
           MOVE LB15-DET-REC               TO LB15-DETAIL.                      
           WRITE BP13LB15-REC              FROM LB15-RECORD.                    
           ADD 1                           TO WS-LB15-LINE.                     
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY '*================ BP13CB15 ================*'.              
           DISPLAY ' NO OF F730 RECORDS READ        = ' WS-F730-READ.           
           DISPLAY '   NO OF LB15 RECORDS WRITTEN   = ' WS-LB15-WRITE.          
           DISPLAY '   NO OF LB15A RECORDS WRITTEN  = ' WS-LB15A-WRITE.         
           DISPLAY '     NO OF K730 RECORDS UPDATED = ' WS-K730-UPD.            
           DISPLAY '       K757 CAT UPD TO K730     = ' WS-K730-PREFER.         
           DISPLAY '       BLANK OFF CAT IN K730    = ' WS-K730-BLANK.          
           DISPLAY '   NO OF F730 RECORDS BYPASSED  = ' WS-F730-BYPASS.         
           DISPLAY '     NO OF K757 RECORDS BLANK   = ' WS-F730-BLANK.          
           DISPLAY '     NO OF F730 & K757 SAME CAT = ' WS-F730-SAME.           
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F730                                                       
                 BP13K757                                                       
                 BP13K730                                                       
                 BP13K825                                                       
                 BP13K060                                                       
                 BP13LB15                                                       
                 P13LB15A.                                                      
                                                                                
           IF WS-K757-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K757 ERROR ' WS-K757-STATUS                  
              MOVE WS-K757-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K730 ERROR ' WS-K730-STATUS                  
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K060 ERROR ' WS-K060-STATUS                  
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        9000-EXIT.                                                              
            EXIT.                                                               
                                                                                
