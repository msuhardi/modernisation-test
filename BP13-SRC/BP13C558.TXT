       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C558.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  03 DECEMBER 2015.                                         
       DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO PRINT REPORT ON FLAT ALLOCATED UNDER       *                
      *          FIRST TIMER/UPGRADER FOR EACH ZONE/FT/ALO-SCH *                
      *          COHORT.                                       *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *      1. BP13K712                                       *                
      *      2. BP13F713                                       *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *      1. BP13L558                                       *                
      *                                                        *                
      * CHG-NO  BY   ON     DESCRIPTION                        *                
      * ------- ---- ------ --------------                     *                
      *BP136096 ESA1 031215 NEW PROGRAM(PATTERN AFTER BP13C552)*                
      *BP136107 SMR2 020216 CHANGE LOGIC, USE K010 AS BASE FILE*                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F713 ASSIGN TO BP13F713.                                  
                                                                                
           SELECT BP13K712 ASSIGN TO BP13K712                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K712-KEY-FLD                                    
                FILE STATUS  IS BP13K712-STATUS.                                
                                                                                
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                ACCESS MODE  IS SEQUENTIAL                                      
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K010-KEY-FLD                                    
                FILE STATUS  IS BP13K010-STATUS.                                
                                                                                
           SELECT BP13L558 ASSIGN TO BP13L558.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F713                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 20 CHARACTERS                                       
            RECORDING MODE IS F.                                                
       COPY BP13F713.                                                           
                                                                                
       FD   BP13K712                                                            
            BLOCK CONTAINS 0 RECORDS.                                           
       COPY BP13K712.                                                           
                                                                                
       FD   BP13K010                                                            
            BLOCK CONTAINS 600 RECORDS.                                         
       COPY BP13K010.                                                           
                                                                                
       FD   BP13L558                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED.                                            
                                                                                
       01   L558-PRINT-REC               PIC X(132).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  BP13K712-STATUS           PIC 9(02) VALUE ZEROS.                 
           05  BP13K010-STATUS           PIC 9(02) VALUE ZEROS.                 
           05  WS-TIME.                                                         
               10  WS-TIME-HH            PIC 9(02) VALUE 0.                     
               10  WS-TIME-MM            PIC 9(02) VALUE 0.                     
               10  WS-TIME-SS            PIC 9(02) VALUE 0.                     
           05  WS-CNT-K010               PIC 9(05) VALUE 0.                     
           05  WS-CNT-PAGE               PIC 9(04) VALUE 0.                     
           05  WS-CNT-SNO                PIC 9(05) VALUE 0.                     
           05  WS-MAX-LINE               PIC 9(02) VALUE 60.                    
           05  WS-CNT-LINE               PIC 9(02) VALUE 60.                    
           05  WS-K010-EOF               PIC X(01) VALUE SPACES.                
               88  K010-EOF                        VALUE 'Y'.                   
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-YY                     PIC X(02).                             
           05  WS-MM                     PIC X(02).                             
           05  WS-DD                     PIC X(02).                             
                                                                                
       01  WS-DTE-TODATE.                                                       
           05  WS-DD                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-MM                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-YY                     PIC X(02).                             
                                                                                
      ******************* REPORT LAYOUT *****************                       
                                                                                
       01  L558-HEAD1.                                                          
           05  FILLER                    PIC X(08) VALUE 'BP13L558'.            
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'HDBCAT3'.             
           05  FILLER                    PIC X(19) VALUE SPACES.                
           05  FILLER                    PIC X(42) VALUE                        
               'S Y S T E M     O F    C O M M I T M E N T'.                    
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'DATE : '.             
           05  L558-DTE-CURRENT          PIC X(08).                             
           05  FILLER                    PIC X(02) VALUE ' ('.                  
           05  L558-TME-HH               PIC 9(02).                             
           05  FILLER                    PIC X(01) VALUE ':'.                   
           05  L558-TME-MM               PIC 9(02).                             
           05  FILLER                    PIC X(02) VALUE ') '.                  
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'PAGE : '.             
           05  L558-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  L558-HEAD2.                                                          
           05  FILLER                    PIC X(91) VALUE SPACES.                
           05  FILLER                    PIC X(23) VALUE                        
               'USER (APPLICATION TEAM)'.                                       
                                                                                
       01  L558-HEAD3.                                                          
           05  FILLER                    PIC X(30) VALUE SPACES.                
           05  FILLER                    PIC X(47) VALUE                        
               'REPORT ON FLAT ALLOTED TO FIRST TIMER/UPGRADER'.                
           05  FILLER                    PIC X(14) VALUE SPACES.                
           05  FILLER                    PIC X(10) VALUE 'PROCESS : '.          
           05  L558-PROCESS              PIC X(15) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE SPACES.                
                                                                                
       01  L558-HEAD4.                                                          
           05  FILLER                    PIC X(54) VALUE                        
               ' S/NO.    NT   FT   CO   ALC   %NHH   %UPG   TOTAL    '.        
           05  FILLER                    PIC X(50) VALUE                        
               '         ELDER    ELDER    ELDER    ELDER    ELDER'.            
           05  FILLER                    PIC X(27) VALUE                        
               '     SPS       NON      NON'.                                   
                                                                                
       01  L558-HEAD5.                                                          
           05  FILLER                    PIC X(54) VALUE                        
               '                         SCH                 UNIT     '.        
           05  FILLER                    PIC X(52) VALUE                        
               '          MIN              MAX1     MAX2     MAX3'.             
           05  FILLER                    PIC X(25) VALUE                        
               '             SPS    ELDER'.                                     
                                                                                
       01  L558-DETAIL1.                                                        
           05  L558-SNO                  PIC ZZZZZ9.                            
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L558-NT                   PIC X(03).                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L558-FT                   PIC X(02).                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L558-CDE-COHORT           PIC X(02).                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L558-ALLOC-SCH            PIC X(03).                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L558-NUM-NHH              PIC Z9.9.                              
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L558-NUM-UPG              PIC Z9.9.                              
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L558-NUM-TOT-UNIT         PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE ' (FLAT) '.            
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L558-NUM-TOT-ELDER-MIN    PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-TOT-ELDER        PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-MAX-ELDER1       PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-MAX-ELDER2       PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-MAX-ELDER3       PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-TOT-ELDER-SPS    PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-TOT-ELDER-NONSPS PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-TOT-NON-ELDER    PIC ZZZZ9.                             
                                                                                
       01  L558-DETAIL2.                                                        
           05  FILLER                    PIC X(54) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE ' (REG ) '.            
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  L558-NUM-REG-ELDER        PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-REG-ELDER1       PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-REG-ELDER2       PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-REG-ELDER3       PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-REG-ELDER-SPS    PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-REG-ELDER-NONSPS PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-REG-NON-ELDER    PIC ZZZZ9.                             
                                                                                
       01  L558-DETAIL3.                                                        
           05  FILLER                    PIC X(54) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE ' (APP ) '.            
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  L558-NUM-APP-ELDER        PIC ZZZZ9.                             
           05  FILLER                    PIC X(31) VALUE SPACES.                
           05  L558-NUM-APP-ELDER-SPS    PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-APP-ELDER-NONSPS PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L558-NUM-APP-NON-ELDER    PIC ZZZZ9.                             
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************                                                        
       0000-MAIN-ROUTINE.                                                       
      ******************                                                        
                                                                                
           PERFORM 1000-OPEN-FILES                                              
              THRU 1000-EXIT.                                                   
                                                                                
           PERFORM 2000-READ-BP13K010                                           
              THRU 2000-EXIT.                                                   
                                                                                
           PERFORM 3000-MAIN-ROUTINE                                            
              THRU 3000-EXIT                                                    
             UNTIL K010-EOF.                                                    
                                                                                
           PERFORM 9000-CLOSE-ROUTINE                                           
              THRU 9000-EXIT.                                                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************                                                         
       1000-OPEN-FILES.                                                         
      *****************                                                         
                                                                                
           OPEN INPUT  BP13K712                                                 
                       BP13K010                                                 
                       BP13F713                                                 
                OUTPUT BP13L558.                                                
                                                                                
           IF BP13K712-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K712,STATUS : '                        
                        BP13K712-STATUS                                         
              MOVE BP13K712-STATUS       TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-IF.                                                              
                                                                                
           IF BP13K010-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K010,STATUS : '                        
                        BP13K010-STATUS                                         
              MOVE BP13K010-STATUS       TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-IF.                                                              
                                                                                
           ACCEPT WS-DTE-CURRENT         FROM DATE.                             
           MOVE CORR WS-DTE-CURRENT        TO WS-DTE-TODATE.                    
           MOVE WS-DTE-TODATE              TO L558-DTE-CURRENT.                 
                                                                                
           ACCEPT  WS-TIME                 FROM TIME.                           
           MOVE WS-TIME-HH                 TO L558-TME-HH.                      
           MOVE WS-TIME-MM                 TO L558-TME-MM.                      
                                                                                
           PERFORM 1500-READ-BP13F713                                           
              THRU 1500-EXIT.                                                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       1500-READ-BP13F713.                                                      
      ********************                                                      
                                                                                
            READ BP13F713 AT END                                                
                GO TO 1500-EXIT                                                 
            END-READ                                                            
                                                                                
            EVALUATE  F713-CDE-COHORT                                           
            WHEN  '01'                                                          
               MOVE 'COHORT 1       '    TO L558-PROCESS                        
            WHEN  '02'                                                          
               MOVE 'COHORT 2       '    TO L558-PROCESS                        
            WHEN  '03'                                                          
               MOVE 'COHORT 3       '    TO L558-PROCESS                        
            WHEN  '04'                                                          
               MOVE 'COHORT 4       '    TO L558-PROCESS                        
            WHEN  '05'                                                          
               MOVE 'COHORT 5       '    TO L558-PROCESS                        
            WHEN  '06'                                                          
               MOVE 'COHORT 6       '    TO L558-PROCESS                        
            WHEN  '07'                                                          
               MOVE 'COHORT 7       '    TO L558-PROCESS                        
            WHEN  '08'                                                          
               MOVE 'COHORT 8       '    TO L558-PROCESS                        
            WHEN  'MT'                                                          
               MOVE 'MTF/JB/TCP/RH  '    TO L558-PROCESS                        
            WHEN  'CC'                                                          
               MOVE 'GRO            '    TO L558-PROCESS                        
            WHEN  'PA'                                                          
               MOVE 'MATURE NT - PA '    TO L558-PROCESS                        
            WHEN  'NH'                                                          
               MOVE 'NHH/UPG        '    TO L558-PROCESS                        
            WHEN  'TR'                                                          
               MOVE 'TRANSITIONAL   '    TO L558-PROCESS                        
            WHEN  OTHER                                                         
               MOVE '               '    TO L558-PROCESS                        
            END-EVALUATE.                                                       
                                                                                
            MOVE F713-CDE-COHORT         TO L558-CDE-COHORT.                    
            MOVE 'PA '                   TO L558-ALLOC-SCH.                     
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       2000-READ-BP13K010.                                                      
      ********************                                                      
                                                                                
            READ BP13K010 NEXT                                                  
                 AT END                                                         
                 MOVE 'Y'  TO WS-K010-EOF                                       
                 GO TO 2000-EXIT                                                
            END-READ.                                                           
                                                                                
            ADD   1        TO WS-CNT-K010.                                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       3000-MAIN-ROUTINE.                                                       
      *******************                                                       
                                                                                
           IF  WS-CNT-LINE  >=  WS-MAX-LINE                                     
                                                                                
               ADD 1                     TO WS-CNT-PAGE                         
               MOVE WS-CNT-PAGE          TO L558-CNT-PAGE                       
                                                                                
               WRITE L558-PRINT-REC      FROM L558-HEAD1 AFTER PAGE             
               WRITE L558-PRINT-REC      FROM L558-HEAD2                        
               WRITE L558-PRINT-REC      FROM L558-HEAD3 AFTER 2                
               WRITE L558-PRINT-REC      FROM L558-HEAD4 AFTER 2                
               WRITE L558-PRINT-REC      FROM L558-HEAD5                        
                                                                                
               MOVE  9                   TO WS-CNT-LINE                         
                                                                                
           END-IF.                                                              
                                                                                
           IF K010-CDE-NT-ZONE > SPACES                                         
                                                                                
              ADD 1                         TO WS-CNT-SNO                       
                                                                                
              MOVE WS-CNT-SNO               TO L558-SNO                         
              MOVE K010-CDE-NT-ZONE         TO L558-NT                          
              MOVE K010-CDE-FLAT-TY         TO L558-FT                          
              MOVE K010-NUM-TOT-ELDER-MIN   TO L558-NUM-TOT-ELDER-MIN           
              MOVE K010-NUM-TOT-ELDER       TO L558-NUM-TOT-ELDER               
              MOVE K010-NUM-MAX-ELDER1      TO L558-NUM-MAX-ELDER1              
              MOVE K010-NUM-MAX-ELDER2      TO L558-NUM-MAX-ELDER2              
              MOVE K010-NUM-MAX-ELDER3      TO L558-NUM-MAX-ELDER3              
              MOVE K010-NUM-TOT-ELDER-SPS   TO L558-NUM-TOT-ELDER-SPS           
              MOVE K010-NUM-TOT-ELDER-NONSPS                                    
                                            TO L558-NUM-TOT-ELDER-NONSPS        
              MOVE K010-NUM-TOT-NON-ELDER   TO L558-NUM-TOT-NON-ELDER           
              MOVE K010-NUM-REG-ELDER       TO L558-NUM-REG-ELDER               
              MOVE K010-REG-ELDER1          TO L558-NUM-REG-ELDER1              
              MOVE K010-REG-ELDER2          TO L558-NUM-REG-ELDER2              
              MOVE K010-REG-ELDER3          TO L558-NUM-REG-ELDER3              
              MOVE K010-NUM-REG-NON-ELDER   TO L558-NUM-REG-NON-ELDER           
              MOVE K010-NUM-REG-ELDER-SPS   TO L558-NUM-REG-ELDER-SPS           
              MOVE K010-NUM-REG-ELDER-NONSPS                                    
                                            TO L558-NUM-REG-ELDER-NONSPS        
              MOVE K010-NUM-APP-ELDER       TO L558-NUM-APP-ELDER               
              MOVE K010-NUM-APP-NON-ELDER   TO L558-NUM-APP-NON-ELDER           
              MOVE K010-NUM-APP-ELDER-SPS   TO L558-NUM-APP-ELDER-SPS           
              MOVE K010-NUM-APP-ELDER-NONSPS                                    
                                            TO L558-NUM-APP-ELDER-NONSPS        
                                                                                
              PERFORM 4000-READ-BP13K712    THRU 4000-EXIT                      
              MOVE K712-NUM-NHH             TO L558-NUM-NHH                     
              MOVE K712-NUM-UPG             TO L558-NUM-UPG                     
              MOVE K712-NUM-TOT-UNIT        TO L558-NUM-TOT-UNIT                
                                                                                
              WRITE L558-PRINT-REC  FROM L558-DETAIL1 AFTER 2                   
              WRITE L558-PRINT-REC  FROM L558-DETAIL2                           
              WRITE L558-PRINT-REC  FROM L558-DETAIL3                           
                                                                                
              ADD  4                        TO WS-CNT-LINE                      
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13K010                                           
              THRU 2000-EXIT.                                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13K712.                                                      
      *********************                                                     
           MOVE SPACES             TO K712-KEY-FLD.                             
           MOVE K010-CDE-NT-ZONE   TO K712-CDE-NT-ZONE.                         
           MOVE K010-CDE-FLAT-TY   TO K712-CDE-FLAT-TY.                         
           MOVE F713-CDE-COHORT    TO K712-CDE-COHORT.                          
           MOVE 'PA '              TO K712-CDE-ALLOC-SCH.                       
                                                                                
           READ BP13K712.                                                       
                                                                                
           EVALUATE BP13K712-STATUS                                             
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K712, STATUS '              
                   BP13K712-STATUS ' KEY ' K712-KEY-FLD                         
                   INITIALIZE BP13K712-REC                                      
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K712, STATUS '                    
                   BP13K712-STATUS ' KEY ' K712-KEY-FLD                         
                   INITIALIZE BP13K712-REC                                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-ROUTINE.                                                      
      *******************                                                       
                                                                                
           CLOSE BP13K712                                                       
                 BP13K010                                                       
                 BP13F713                                                       
                 BP13L558.                                                      
                                                                                
           DISPLAY '*************** BP13C558 ***************'.                  
           DISPLAY ' '.                                                         
           DISPLAY 'NO. OF RECORDS READ FROM BP13K010 : ' WS-CNT-K010.          
           DISPLAY 'NO. OF RECORDS WRITTEN TO REPORT  : ' WS-CNT-SNO.           
           DISPLAY ' '.                                                         
                                                                                
           IF BP13K712-STATUS NOT = 0                                           
              DISPLAY '***  CLOSING ERROR, BP13K712-STATUS '                    
              BP13K712-STATUS                                                   
           END-IF.                                                              
                                                                                
           IF BP13K010-STATUS NOT = 0                                           
              DISPLAY '***  CLOSING ERROR, BP13K010-STATUS '                    
              BP13K010-STATUS                                                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
