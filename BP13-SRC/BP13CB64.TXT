       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB64.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI                                       
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 15 APR 2011                          *              
      *                                                          *              
      * 1. OBJECTIVE      : CREATE PROPERTY DATA FROM DEVELOPER  *              
      *                     FILE                                 *              
      *                   : TO READ BP13FA63 UNTIL EOF AND       *              
      *                   : WRITE TO BP13KA63.                 . *              
      *                                                          *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP134157 15/04/2011 PCL3 NEW PROGRAM.                   *              
      *  BP134209 20/05/2011 PCL3 TO FIX COMPUTATION OF INTERNAL *              
      *                           FLOOR-AREA.                    *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA63     ASSIGN        TO BP13FA63.                       
                                                                                
                                                                                
           SELECT BP13KA63     ASSIGN        TO BP13KA63                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS KA63-KEY-FLD                    
                               FILE STATUS   IS WS-KA63-STATUS.                 
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FA63                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 200 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13FA63.                                                           
                                                                                
       FD  BP13KA63                                                             
                               RECORD CONTAINS 200 CHARACTERS.                  
       COPY BP13KA63.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-KA63-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  WS-KA63-FOUND                 PIC X(1)  VALUE 'N'.                   
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-FA63-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-FA63-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-KA63-NFND              PIC 9(6)  VALUE ZEROES.                
           05  WS-KA63-FND               PIC 9(6)  VALUE ZEROES.                
           05  WS-CNT-WRITE              PIC 9(6)  VALUE ZEROES.                
           05  WS-CNT-UPDATE             PIC 9(6)  VALUE ZEROES.                
           05  WS-SNO                    PIC 9(6)  VALUE ZEROES.                
                                                                                
       01  WS-CURRENT-DATE               PIC X(8) VALUE SPACES.                 
       01  WS-TOTAL-FLR-AREA             PIC X(9) VALUE SPACES.                 
       01  WS-FLR-AREA                   PIC X(9) VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-BP13FA63   THRU 1100-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-FA63-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
                                                                                
           OPEN INPUT  BP13FA63                                                 
                  I-O  BP13KA63.                                                
                                                                                
           IF WS-KA63-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KA63 OPEN ERROR ' WS-KA63-STATUS                     
              MOVE WS-KA63-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-CURRENT-DATE.             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13FA63.                                                      
      *************************************************************             
                                                                                
           READ BP13FA63                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-FA63-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-FA63-READ                        
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
                                                                                
           PERFORM 3000-READ-BP13KA63           THRU 3000-EXIT.                 
                                                                                
           IF WS-KA63-FOUND = 'Y'                                               
              PERFORM 4000-REWRITE-BP13KA63     THRU 4000-EXIT                  
           ELSE                                                                 
              PERFORM 5000-WRITE-BP13KA63       THRU 5000-EXIT                  
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13FA63           THRU 1100-EXIT.                 
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3000-READ-BP13KA63.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13KA63-REC.                       
           INITIALIZE                       BP13KA63-REC.                       
                                                                                
           MOVE 'N'                      TO WS-KA63-FOUND.                      
           MOVE FA63-KEY-FLD             TO KA63-KEY-FLD.                       
                                                                                
           READ BP13KA63.                                                       
                                                                                
           EVALUATE WS-KA63-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'             TO WS-KA63-FOUND                       
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-KA63-NFND                        
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13KA63 READ ERROR ' WS-KA63-STATUS               
                    DISPLAY 'KA63 KEY: ' KA63-KEY-FLD                           
                    MOVE WS-KA63-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3500-CHECK-BP13FA63.                                                     
      *************************************************************             
                                                                                
            IF FA63-NUM-AIRCON-LEDGE-AREA NOT NUMERIC                           
               MOVE ZEROES       TO FA63-NUM-AIRCON-LEDGE-AREA                  
            END-IF.                                                             
            MOVE FA63-NUM-AIRCON-LEDGE-AREA                                     
                                 TO KA63-NUM-AIRCON-LEDGE-AREA.                 
                                                                                
            IF FA63-NUM-BAY-WINDOW-AREA NOT NUMERIC                             
               MOVE ZEROES       TO FA63-NUM-BAY-WINDOW-AREA                    
            END-IF.                                                             
            MOVE FA63-NUM-BAY-WINDOW-AREA                                       
                                 TO KA63-NUM-BAY-WINDOW-AREA.                   
                                                                                
            IF FA63-NUM-PLANTER-BOX-AREA NOT NUMERIC                            
               MOVE ZEROES       TO FA63-NUM-PLANTER-BOX-AREA                   
            END-IF.                                                             
            MOVE FA63-NUM-PLANTER-BOX-AREA                                      
                                 TO KA63-NUM-PLANTER-BOX-AREA.                  
                                                                                
            IF FA63-NUM-BALCONY-BOX-AREA NOT NUMERIC                            
               MOVE ZEROES       TO FA63-NUM-BALCONY-BOX-AREA                   
            END-IF.                                                             
            MOVE FA63-NUM-BALCONY-BOX-AREA                                      
                                 TO KA63-NUM-BALCONY-BOX-AREA.                  
                                                                                
            MOVE BP13FA63-REC(64:9)             TO WS-FLR-AREA.                 
                                                                                
            IF BP13FA63-REC(64:9) NOT = SPACES AND LOW-VALUES                   
               COMPUTE FA63-NUM-INTERNAL-FLR-AREA                               
                                  = FUNCTION NUMVAL(WS-FLR-AREA)                
            ELSE                                                                
               MOVE ZEROES       TO FA63-NUM-INTERNAL-FLR-AREA                  
            END-IF.                                                             
            COMPUTE FA63-NUM-INTERNAL-FLR-AREA                                  
                         =  FA63-NUM-INTERNAL-FLR-AREA / 100.                   
                                                                                
            MOVE FA63-NUM-INTERNAL-FLR-AREA                                     
                                 TO KA63-NUM-INTERNAL-FLR-AREA.                 
                                                                                
            MOVE BP13FA63-REC(73:9)             TO WS-TOTAL-FLR-AREA.           
                                                                                
            IF BP13FA63-REC(73:9) NOT = SPACES AND LOW-VALUES                   
               COMPUTE FA63-NUM-TOTAL-FLR-AREA                                  
                                  = FUNCTION NUMVAL(WS-TOTAL-FLR-AREA)          
            ELSE                                                                
               MOVE ZEROES       TO FA63-NUM-TOTAL-FLR-AREA                     
            END-IF.                                                             
            COMPUTE  FA63-NUM-TOTAL-FLR-AREA                                    
                                 =  FA63-NUM-TOTAL-FLR-AREA / 100.              
                                                                                
            MOVE FA63-NUM-TOTAL-FLR-AREA                                        
                                 TO KA63-NUM-TOTAL-FLR-AREA.                    
                                                                                
                                                                                
            IF FA63-NUM-DRYING-LEDGE-AREA NOT NUMERIC                           
               MOVE ZEROES                TO FA63-NUM-DRYING-LEDGE-AREA         
            END-IF.                                                             
            MOVE FA63-NUM-DRYING-LEDGE-AREA                                     
                                 TO KA63-NUM-DRYING-LEDGE-AREA.                 
                                                                                
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       4000-REWRITE-BP13KA63.                                                   
      *************************************************************             
                                                                                
           PERFORM 3500-CHECK-BP13FA63             THRU 3500-EXIT.              
                                                                                
           REWRITE BP13KA63-REC.                                                
                                                                                
           EVALUATE WS-KA63-STATUS                                              
               WHEN 00                                                          
                    ADD 1                          TO WS-CNT-UPDATE             
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13KA63 REWRITE ERROR ' WS-KA63-STATUS            
                    DISPLAY 'KA63 KEY: ' KA63-KEY-FLD                           
                    MOVE WS-KA63-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       5000-WRITE-BP13KA63.                                                     
      *************************************************************             
                                                                                
           MOVE BP13FA63-REC                       TO BP13KA63-REC.             
           PERFORM 3500-CHECK-BP13FA63           THRU 3500-EXIT.                
                                                                                
           WRITE BP13KA63-REC.                                                  
                                                                                
           EVALUATE WS-KA63-STATUS                                              
               WHEN 00                                                          
                    ADD 1                          TO WS-CNT-UPDATE             
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13KA63 WRITE ERROR ' WS-KA63-STATUS              
                    DISPLAY 'KA63 KEY: ' KA63-KEY-FLD                           
                    MOVE WS-KA63-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-CURRENT-DATE.                               
           DISPLAY '*--------- BP13CB64 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM FA63   : ' WS-FA63-READ.                  
           DISPLAY 'RECS WRITTEN TO BP13KA63 : ' WS-CNT-WRITE.                  
           DISPLAY 'RECS UPDATED TO BP13KA63 : ' WS-CNT-UPDATE.                 
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13FA63                                                       
                 BP13KA63.                                                      
                                                                                
           IF WS-KA63-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KA63 CLOSE ERROR ' WS-KA63-STATUS                    
              MOVE WS-KA63-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
