      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.   BP13C343.                                                  
      *AUTHOR.       MCC.                                                       
      *DTE-WRITTEN.  05DEC2002.                                                 
                                                                                
      * ======================================================== *              
      * SYSTEM OF COMMITMENT  (BP13)                             *              
      * ======================================================== *              
      *    OBJECTIVE  :                                          *              
      *            PROGRAM TO CREATE S&P FILE FOR SENDING BY     *              
      *            HDB TO CPF THROUGH IDNET.                     *              
      *                                                          *              
      *     INPUT FILES:                                         *              
      *         1. BP13F445 -                                    *              
      *                                                          *              
      *     OUTPUT FILES:                                        *              
      *         1. BP13F105  -                                   *              
      *                                                          *              
      * -------------------------------------------------------- *              
      * CHG NO   DATE     BY   DESCRIPTION                       *              
      * -------  -------- ---  -----------                       *              
      * BP132249 20021205 MCC  NEW-PROGRAM                       *              
      * BP132672 07032005 JF4  ADD NUM-TOWN                      *              
      * BP132814 10022006 LSB  TO IMPLEMENT ADDITIONAL CFP GRANT *              
      *                        FOR DBSS TO BE IMPLT LATER        *              
      * BP133082 12072007 CLT  AMEND LOAN TAG                    *              
      * BP133820 29012010 ESA1 TO MOVE ZEROES TO F105-HEADER-FLD *              
      *                        TO MOVE '999999999999' TO         *              
      *                        F105-CONTROL-FLD                  *              
      * BP134294 02122011 JB8  TO CATER FOR DUPLICATE SNP RECORDS*              
      * BP134872 11042013 ESD1 TO ADD LEASE COMMENCEMENT DATE,   *              
      *                        TENURE PERIOD AND STUDIO APT TAG. *              
      ************************************************************              
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F445 ASSIGN TO BP13F445.                                  
                                                                                
           SELECT BP13F105 ASSIGN TO BP13F105.                                  
                                                                                
           SELECT BP13L343 ASSIGN TO BP13L343.                                  
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  BP13F445                                                             
           RECORDING MODE IS F.                                                 
       COPY BP13F445.                                                           
                                                                                
       FD  BP13F105                                                             
           RECORDING MODE IS F.                                                 
       COPY BP13F105.                                                           
                                                                                
       FD  BP13L343                                                             
           RECORDING MODE IS F.                                                 
       01  BP13L343-REC  PIC X(132).                                            
                                                                                
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
       01  WS-VARIABLES.                                                        
           05  WS-CURRENT-DATE        PIC X(8)  VALUE SPACES.                   
           05  WS-BUYER-DETAILS.                                                
               10  WS-NME             PIC X(66) VALUE SPACES.                   
               10  WS-CPF             PIC X(9)  VALUE SPACES.                   
               10  WS-PCT-HOLDING     PIC X(7)  VALUE SPACES.                   
               10  WS-CPF-GRANT       PIC 9(7)V99  VALUE ZEROS.                 
                                                                                
       01  WS-FLAGS-AND-FILE-STATUS.                                            
           05  WS-F445-EOF            PIC X(1)  VALUE 'N'.                      
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F445-READ           PIC 9(5)  VALUE ZEROES.                   
           05  WS-PROP-CNT            PIC 9(7)  VALUE ZEROES.                   
           05  WS-BUYER-CNT           PIC 9(7)  VALUE ZEROES.                   
           05  WS-SELLER-CNT          PIC 9(7)  VALUE ZEROES.                   
           05  WS-TOTAL-REC-CNT       PIC 9(7)  VALUE ZEROES.                   
                                                                                
       01  WS-PREV-F445-REC.                                                    
           05  FILLER                 PIC X(09) VALUE SPACES.                   
           05  WS-PREV-HDBREF         PIC X(9) VALUE SPACES.                    
           05  FILLER                 PIC X(982) VALUE SPACES.                  
       01  WS-TMP-F445-REC            PIC X(1000) VALUE SPACES.                 
       01  WS-DUP-FND                 PIC X(01) VALUE 'N'.                      
                                                                                
      *---------------------------------------------------------------*         
      *- REPORT LAYOUT                                                *         
      *---------------------------------------------------------------*         
       01  WS-REPORT-VARS.                                                      
           05  CNT-PAGE-NO            PIC 9(05) VALUE ZEROES.                   
           05  CNT-REC                PIC 9(02) VALUE 60.                       
           05  CNT-SNO                PIC 9(02) VALUE ZEROES.                   
                                                                                
       01  L343-HEADER-01.                                                      
           05  FILLER                 PIC X(14) VALUE 'BP13L343'.               
           05  FILLER                 PIC X(8)  VALUE 'HDB3'.                   
           05  FILLER                 PIC X(18) VALUE SPACES.                   
           05  FILLER                 PIC X(39) VALUE                           
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                 PIC X(18) VALUE SPACES.                   
           05  FILLER                 PIC X(7)  VALUE 'DATE: '.                 
           05  L343-DATE              PIC X(10) VALUE SPACES.                   
           05  FILLER                 PIC X(8)  VALUE ' PAGE: '.                
           05  L343-PAGE              PIC ZZZZ9.                                
                                                                                
       01  L343-HEADER-02.                                                      
           05  FILLER                 PIC X(49) VALUE SPACES.                   
           05  FILLER                 PIC X(25) VALUE                           
                                                'DUPLICATE S&P RECORDS'.        
                                                                                
       01  L343-TITLE-01              PIC X(132) VALUE                          
           '  SNO  HDB REF NO      REGN NO  ACTION  BUYER1     BUYER2           
      -    '  ADDRESS'.                                                         
                                                                                
       01  L343-TITLE-02              PIC X(132) VALUE ALL '-'.                 
                                                                                
       01  L343-DETAIL.                                                         
           05  L343-SNO               PIC ZZZZ9  VALUE ZEROES.                  
           05  FILLER                 PIC X(02)  VALUE SPACES.                  
           05  L343-HDB-REF           PIC X(14)  VALUE SPACES.                  
           05  FILLER                 PIC X(02)  VALUE SPACES.                  
           05  L343-REGN-NO           PIC X(08)  VALUE SPACES.                  
           05  FILLER                 PIC X(04)  VALUE SPACES.                  
           05  L343-ACTION-TAG        PIC X(01)  VALUE SPACES.                  
           05  FILLER                 PIC X(04)  VALUE SPACES.                  
           05  L343-NUM-BUYER1        PIC X(09)  VALUE SPACES.                  
           05  FILLER                 PIC X(02)  VALUE SPACES.                  
           05  L343-NUM-BUYER2        PIC X(09)  VALUE SPACES.                  
           05  FILLER                 PIC X(02)  VALUE SPACES.                  
           05  L343-ADDRESS           PIC X(65)  VALUE SPACES.                  
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *---------------*                                                         
       000-MAIN-LOGIC.                                                          
      *---------------*                                                         
           PERFORM 100-OPEN-FILES        THRU 100-EXIT.                         
           PERFORM 200-READ-BP13F445     THRU 200-EXIT.                         
           PERFORM 300-WRITE-HEADER-REC  THRU 300-EXIT.                         
           PERFORM 400-PROCESS           THRU 400-EXIT                          
                    UNTIL WS-F445-EOF = 'Y'.                                    
           PERFORM 450-WRITE-LAST-REC    THRU 450-EXIT.                         
           PERFORM 700-WRITE-CONTROL-REC THRU 700-EXIT.                         
           PERFORM 999-CLOSE-FILES       THRU 999-EXIT.                         
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       100-OPEN-FILES.                                                          
      *---------------*                                                         
           OPEN INPUT  BP13F445                                                 
                OUTPUT BP13F105                                                 
                       BP13L343.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------*                                                          
       200-READ-BP13F445.                                                       
      *--------------*                                                          
           READ BP13F445 AT END                                                 
           MOVE 'Y'                      TO WS-F445-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F445-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------*                                                   
       300-WRITE-HEADER-REC.                                                    
      *---------------------*                                                   
      *---------------------------------------------------------------*         
      * CREATE HEADER REC IN THE BEGINNING OF EACH FILE                         
      *---------------------------------------------------------------*         
           MOVE SPACES TO BP13F105-HEADER-REC.                                  
           INITIALIZE     BP13F105-HEADER-REC.                                  
                                                                                
           MOVE ZEROES                TO F105-HEADER-FLD.                       
           MOVE WS-CURRENT-DATE       TO F105-DTE-HDB-SEND.                     
                                                                                
           WRITE BP13F105-HEADER-REC.                                           
           ADD 01 TO WS-TOTAL-REC-CNT.                                          
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------*                                                            
       400-PROCESS.                                                             
      *------------*                                                            
                                                                                
           IF WS-PREV-HDBREF = SPACES OR LOW-VALUES                             
              MOVE BP13F445-REC             TO WS-PREV-F445-REC                 
           ELSE                                                                 
              IF F445-NUM-HDB-REF(1:9) = WS-PREV-HDBREF                         
                 MOVE BP13F445-REC          TO WS-TMP-F445-REC                  
                 MOVE WS-PREV-F445-REC      TO BP13F445-REC                     
                 PERFORM 800-OUTPUT-DUP-REPORT THRU 800-EXIT                    
                 MOVE WS-TMP-F445-REC       TO WS-PREV-F445-REC                 
                 MOVE 'Y'                   TO WS-DUP-FND                       
              ELSE                                                              
                 IF WS-DUP-FND = 'Y'                                            
                    MOVE BP13F445-REC       TO WS-TMP-F445-REC                  
                    MOVE WS-PREV-F445-REC   TO BP13F445-REC                     
                    PERFORM 800-OUTPUT-DUP-REPORT THRU 800-EXIT                 
                    MOVE WS-TMP-F445-REC    TO WS-PREV-F445-REC                 
                    MOVE 'N'                TO WS-DUP-FND                       
                 ELSE                                                           
                    MOVE BP13F445-REC               TO WS-TMP-F445-REC          
                    MOVE WS-PREV-F445-REC           TO BP13F445-REC             
                    PERFORM 500-WRITE-PROP-DETAIL THRU 500-EXIT                 
                    PERFORM 600-WRITE-BUYER-DETAIL THRU 600-EXIT                
                    MOVE WS-TMP-F445-REC            TO WS-PREV-F445-REC         
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F445      THRU 200-EXIT.                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       450-WRITE-LAST-REC.                                                      
      *------------------*                                                      
                                                                                
           IF WS-DUP-FND = 'N'                                                  
              MOVE WS-PREV-F445-REC             TO BP13F445-REC                 
              PERFORM 500-WRITE-PROP-DETAIL   THRU 500-EXIT                     
              PERFORM 600-WRITE-BUYER-DETAIL  THRU 600-EXIT                     
           ELSE                                                                 
              MOVE WS-PREV-F445-REC             TO BP13F445-REC                 
              PERFORM 800-OUTPUT-DUP-REPORT   THRU 800-EXIT                     
           END-IF.                                                              
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       500-WRITE-PROP-DETAIL.                                                   
      *---------------------**                                                  
      *----------------------------------------------------------------*        
      * CREATE PROPERTY RECORD. BUYER OR SELLER TRANSACTIONS MUST HAVE          
      * ONE PROPERTY RECORD.                                                    
      *----------------------------------------------------------------*        
           MOVE SPACES TO BP13F105-PROPERTY-DETAIL.                             
           INITIALIZE     BP13F105-PROPERTY-DETAIL.                             
                                                                                
           MOVE 'SS'                    TO F105-CDE-PROP-TRANS-TYPE.            
           MOVE '1'                     TO F105-NUM-PROP-REC-TYP                
           MOVE F445-NUM-HDB-REF(1:9)   TO F105-NUM-PROP-HDB-REF.               
           MOVE F445-NUM-BLOCK          TO F105-NUM-BLOCK.                      
           MOVE F445-NME-STREET         TO F105-NME-STREET.                     
           MOVE F445-NUM-HSELEVEL       TO F105-NUM-HSELEVEL.                   
           MOVE F445-NUM-MAIN           TO F105-NUM-MAIN.                       
           MOVE F445-NUM-POSTAL         TO F105-NUM-POSTAL.                     
           MOVE F445-CDE-STREET         TO F105-CDE-STREET.                     
                                                                                
           MOVE F445-AMT-PURCHASE-PRICE TO F105-AMT-PURCHASE-PRICE.             
           MOVE F445-AMT-VALUATION      TO F105-AMT-VALUATION                   
           MOVE F445-DTE-BOOK           TO F105-DTE-BOOK.                       
           MOVE F445-DTE-SO-APPT        TO F105-DTE-SO-APPT.                    
           MOVE F445-DTE-KEY-ISSUE      TO F105-DTE-KEY-ISSUE.                  
           MOVE F445-DTE-APPL-RECEIVED  TO F105-DTE-APPL-RECEIVED.              
           MOVE F445-NUM-REGN           TO F105-NUM-ORIG-REGN.                  
           MOVE F445-AMT-CASH-DOWNPAYMENT TO F105-AMT-CASH-DOWNPAYMENT.         
           MOVE F445-NUM-FLAT-TYPE      TO F105-NUM-FLAT-TYPE.                  
           MOVE F445-NUM-ADJ-FLAT       TO F105-NUM-ADJ-FLAT.                   
           MOVE F445-NUM-COMB-FLAT      TO F105-NUM-COMB-FLAT.                  
           MOVE F445-DTE-LEASE-COMMENCEMENT  TO                                 
                                        F105-DTE-LEASE-COMMENCEMENT.            
           IF (F445-NUM-LEASE-TENURE NOT NUMERIC) OR                            
              (F445-NUM-LEASE-TENURE = ZEROES)                                  
              IF F445-NUM-STUDIO = 'Y'                                          
                 MOVE 30     TO F445-NUM-LEASE-TENURE                           
              ELSE                                                              
                 MOVE 99     TO F445-NUM-LEASE-TENURE                           
              END-IF                                                            
           END-IF.                                                              
           MOVE F445-NUM-LEASE-TENURE   TO F105-NUM-LEASE-TENURE.               
           MOVE F445-NUM-STUDIO         TO F105-NUM-STUDIO.                     
                                                                                
           IF F445-NUM-BO = 'AS'                                                
              MOVE 'UW'                 TO F105-NUM-BO                          
           ELSE                                                                 
              MOVE F445-NUM-BO          TO F105-NUM-BO                          
           END-IF.                                                              
                                                                                
           MOVE F445-NUM-TOWN           TO F105-NUM-TOWN.                       
           MOVE F445-NUM-DBSS           TO F105-NUM-DBSS.                       
           MOVE F445-NUM-PAYMENT-SCHEME TO F105-NUM-PAYMENT-SCHEME.             
           MOVE F445-NUM-STATUS-COMPLETION                                      
                                        TO F105-NUM-STATUS-COMPLETION.          
           MOVE F445-DTE-EXPECTED-TOP   TO F105-DTE-EXPECTED-TOP.               
           MOVE F445-CDE-ACTION-TAG     TO F105-CDE-ACTION-TAG.                 
           MOVE F445-NUM-BUYER-RET-HDB-REF TO                                   
                                           F105-NUM-BUYER-RET-HDB-REF.          
                                                                                
           WRITE BP13F105-PROPERTY-DETAIL.                                      
           ADD 01 TO WS-PROP-CNT                                                
                     WS-TOTAL-REC-CNT.                                          
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       600-WRITE-BUYER-DETAIL.                                                  
      *-----------------------*                                                 
      *---------------------------------------------------------------*         
      * CREATE BUYER DETAIL. MAY HAVE MULITPLE BUYER DETAIL FOR EACH            
      * PROPERTY DETAIL. UP TO 4 BUYERS ALLOWED.                                
      *---------------------------------------------------------------*         
           IF F445-BUYER1-DETAILS NOT = SPACES AND LOW-VALUES                   
              MOVE SPACES                      TO WS-BUYER-DETAILS              
              MOVE F445-NME-BUYER1             TO WS-NME                        
              MOVE F445-NUM-BUYER1-CPF         TO WS-CPF                        
              MOVE F445-NUM-BUYER1-PCT-HOLDING TO WS-PCT-HOLDING                
              IF F445-AMT-CPF-GRANT-BUYER1 IS NOT NUMERIC                       
                 MOVE ZEROS       TO F445-AMT-CPF-GRANT-BUYER1                  
              END-IF                                                            
              MOVE F445-AMT-CPF-GRANT-BUYER1   TO WS-CPF-GRANT                  
              PERFORM 650-WRITE-PER-BUYER-DETAIL  THRU 650-EXIT                 
           END-IF.                                                              
                                                                                
           IF F445-BUYER2-DETAILS NOT = SPACES AND LOW-VALUES                   
              MOVE SPACES                      TO WS-BUYER-DETAILS              
              MOVE F445-NME-BUYER2             TO WS-NME                        
              MOVE F445-NUM-BUYER2-CPF         TO WS-CPF                        
              MOVE F445-NUM-BUYER2-PCT-HOLDING TO WS-PCT-HOLDING                
              IF F445-AMT-CPF-GRANT-BUYER2 IS NOT NUMERIC                       
                 MOVE ZEROS       TO F445-AMT-CPF-GRANT-BUYER2                  
              END-IF                                                            
              MOVE F445-AMT-CPF-GRANT-BUYER2   TO WS-CPF-GRANT                  
              PERFORM 650-WRITE-PER-BUYER-DETAIL  THRU 650-EXIT                 
           END-IF.                                                              
                                                                                
           IF F445-BUYER3-DETAILS NOT = SPACES AND LOW-VALUES                   
              MOVE SPACES                      TO WS-BUYER-DETAILS              
              MOVE F445-NME-BUYER3             TO WS-NME                        
              MOVE F445-NUM-BUYER3-CPF         TO WS-CPF                        
              MOVE F445-NUM-BUYER3-PCT-HOLDING TO WS-PCT-HOLDING                
              IF F445-AMT-CPF-GRANT-BUYER3 IS NOT NUMERIC                       
                 MOVE ZEROS       TO F445-AMT-CPF-GRANT-BUYER3                  
              END-IF                                                            
              MOVE F445-AMT-CPF-GRANT-BUYER3   TO WS-CPF-GRANT                  
              PERFORM 650-WRITE-PER-BUYER-DETAIL  THRU 650-EXIT                 
           END-IF.                                                              
                                                                                
           IF F445-BUYER4-DETAILS NOT = SPACES AND LOW-VALUES                   
              MOVE SPACES                      TO WS-BUYER-DETAILS              
              MOVE F445-NME-BUYER4             TO WS-NME                        
              MOVE F445-NUM-BUYER4-CPF         TO WS-CPF                        
              MOVE F445-NUM-BUYER4-PCT-HOLDING TO WS-PCT-HOLDING                
              IF F445-AMT-CPF-GRANT-BUYER4 IS NOT NUMERIC                       
                 MOVE ZEROS       TO F445-AMT-CPF-GRANT-BUYER4                  
              END-IF                                                            
              MOVE F445-AMT-CPF-GRANT-BUYER4   TO WS-CPF-GRANT                  
              PERFORM 650-WRITE-PER-BUYER-DETAIL  THRU 650-EXIT                 
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------*                                             
       650-WRITE-PER-BUYER-DETAIL.                                              
      *---------------------------*                                             
           MOVE SPACES TO BP13F105-BUYER-DETAIL.                                
           INITIALIZE     BP13F105-BUYER-DETAIL.                                
                                                                                
           MOVE 'SS'                     TO F105-CDE-BUYER-TRANS-TYPE.          
           MOVE '2'                      TO F105-NUM-BUYER-REC-TYP.             
           MOVE F445-NUM-HDB-REF         TO F105-NUM-BUYER-HDB-REF.             
           MOVE WS-NME                   TO F105-NME-BUYER.                     
           MOVE WS-CPF                   TO F105-NUM-BUYER-CPF.                 
           MOVE WS-PCT-HOLDING           TO F105-NUM-BUYER-PCT-HOLDING.         
           MOVE F445-NUM-HOLDING-TYPE    TO F105-NUM-BUYER-HOLDING-TYPE.        
           MOVE WS-CPF-GRANT             TO F105-AMT-BUYER-HSE-GRANT.           
                                                                                
           IF F445-CDE-BUYER-LOAN-TAG = 'P' OR 'A'                              
              MOVE 'H'                      TO F105-CDE-BUYER-LOAN-TAG          
           ELSE                                                                 
              MOVE F445-CDE-BUYER-LOAN-TAG  TO F105-CDE-BUYER-LOAN-TAG          
           END-IF.                                                              
                                                                                
           MOVE F445-NME-BUYER-SOLICITOR TO F105-NME-BUYER-SOLICITOR.           
           MOVE F445-NUM-BUYER-SOLICITOR-ADDR TO                                
                F105-NUM-BUYER-SOLICITOR-ADDR.                                  
                                                                                
           WRITE BP13F105-BUYER-DETAIL.                                         
           ADD 01 TO WS-BUYER-CNT                                               
                     WS-TOTAL-REC-CNT.                                          
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       700-WRITE-CONTROL-REC.                                                   
      *----------------------*                                                  
      *----------------------------------------------------------------*        
      * CREATE CONTROL RECORD AT END OF EACH FILE. CONTAINS THE                 
      * SUMMARY COUNT OF NO OF PROPERTY,BUYER & SELLER RECORDS.                 
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES TO BP13F105-CONTROL-REC.                                 
           INITIALIZE     BP13F105-CONTROL-REC.                                 
           ADD 01      TO WS-TOTAL-REC-CNT.                                     
                                                                                
           MOVE '999999999999'      TO F105-CONTROL-FLD.                        
                                                                                
           IF WS-PROP-CNT NOT NUMERIC                                           
              MOVE ZEROES           TO F105-NUM-PROP-RECS                       
           ELSE                                                                 
              MOVE WS-PROP-CNT      TO F105-NUM-PROP-RECS.                      
                                                                                
           IF WS-BUYER-CNT NOT NUMERIC                                          
              MOVE ZEROES           TO F105-NUM-BUYER-RECS                      
           ELSE                                                                 
              MOVE WS-BUYER-CNT     TO F105-NUM-BUYER-RECS.                     
                                                                                
           IF WS-TOTAL-REC-CNT NOT NUMERIC                                      
              MOVE ZEROES           TO F105-NUM-TOTAL-RECS                      
           ELSE                                                                 
              MOVE WS-TOTAL-REC-CNT TO F105-NUM-TOTAL-RECS.                     
                                                                                
           MOVE ZEROES              TO F105-NUM-SELLER-RECS.                    
                                                                                
           WRITE BP13F105-CONTROL-REC.                                          
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       800-OUTPUT-DUP-REPORT.                                                   
      *--------------------------------------------------------------*          
                                                                                
                                                                                
           ADD 1                                  TO CNT-REC                    
           IF CNT-REC > 56                                                      
              PERFORM 810-OUTPUT-REPORT-HEADER  THRU 810-EXIT                   
              ADD 1                               TO CNT-REC                    
           END-IF.                                                              
                                                                                
           ADD 1                                  TO CNT-SNO.                   
           MOVE SPACES                            TO L343-DETAIL.               
           INITIALIZE                                L343-DETAIL.               
                                                                                
           MOVE CNT-SNO                           TO L343-SNO.                  
           STRING F445-NUM-HDB-REF(1:4) '-' F445-NUM-HDB-REF(5:4) '-'           
                  F445-NUM-HDB-REF(9:1) '-' F445-NUM-HDB-REF(10:2)              
                  DELIMITED BY SIZE INTO L343-HDB-REF.                          
           MOVE F445-NUM-REGN                     TO L343-REGN-NO               
           MOVE F445-CDE-ACTION-TAG               TO L343-ACTION-TAG            
           MOVE F445-NUM-BUYER1-NRIC              TO L343-NUM-BUYER1            
           MOVE F445-NUM-BUYER2-NRIC              TO L343-NUM-BUYER2            
           STRING 'BLK ' F445-NUM-BLOCK                                         
                  ' '    F445-NME-STREET                                        
                  ' #'   F445-NUM-HSELEVEL                                      
                  '-'    F445-NUM-MAIN                                          
                  ' S('  F445-NUM-POSTAL ')'                                    
                  DELIMITED BY SIZE INTO L343-ADDRESS.                          
                                                                                
           WRITE BP13L343-REC FROM L343-DETAIL.                                 
                                                                                
           MOVE 99                                TO RETURN-CODE.               
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       810-OUTPUT-REPORT-HEADER.                                                
      *--------------------------------------------------------------*          
                                                                                
           STRING WS-CURRENT-DATE(7:2) '/' WS-CURRENT-DATE(5:2) '/'             
                  WS-CURRENT-DATE(1:4) DELIMITED BY SIZE INTO L343-DATE.        
                                                                                
           ADD 1                           TO CNT-PAGE-NO.                      
           MOVE CNT-PAGE-NO                TO L343-PAGE.                        
                                                                                
           MOVE SPACES                     TO BP13L343-REC.                     
           WRITE BP13L343-REC AFTER PAGE.                                       
           WRITE BP13L343-REC FROM L343-HEADER-01 AFTER 3.                      
           WRITE BP13L343-REC FROM L343-HEADER-02.                              
                                                                                
           WRITE BP13L343-REC FROM L343-TITLE-01 AFTER 2.                       
           WRITE BP13L343-REC FROM L343-TITLE-02.                               
                                                                                
           MOVE 8                          TO CNT-REC.                          
                                                                                
       810-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       999-CLOSE-FILES.                                                         
      *----------------*                                                        
           DISPLAY ' '.                                                         
           DISPLAY '*----------------- BP13C343 -------------------'.           
                                                                                
           DISPLAY 'NO OF REC READ      BP13F445     = '                        
                    WS-F445-READ.                                               
           DISPLAY 'NO OF REC PROPERTY DTL WRITTEN   = '                        
                    WS-PROP-CNT.                                                
           DISPLAY 'NO OF REC BUYER    DTL WRITTEN   = '                        
                    WS-BUYER-CNT.                                               
           DISPLAY 'NO OF REC SELLER   DTL WRITTEN   = '                        
                    ZEROES.                                                     
           DISPLAY 'TOTAL NO OF RECS WRITTEN         = '                        
                    WS-TOTAL-REC-CNT.                                           
           DISPLAY ' (INCLUDING HEADER & CONTROL RECS)'.                        
                                                                                
           CLOSE BP13F445                                                       
                 BP13F105                                                       
                 BP13L343.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
