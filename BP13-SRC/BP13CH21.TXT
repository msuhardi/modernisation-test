       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH21                                                  
       AUTHOR.        JB8.                                                      
      *DATE-WRITTEN.  05/10/2009.                                               
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *   OBJECTIVE  :  COMPUTE NUMBER OF BF/LF/WF FOR BTO/BE/SBF.  *           
      *                                                             *           
      *   INPUT      :  BP13FH10 -- SEQUENTIAL FILE                 *           
      *                 BP13KH10 -- NOT BOOKED NRIC                 *           
      *                 BP13K595 -- LOG FILE OF BOOK STATUS         *           
      *   OUTPUT     :  P13FH10A -- REPORT ON NON-SELECTION         *           
      *                                                             *           
      *   AMENDMENTS:                                               *           
      *    REF      DATE     BY          DESCRIPTIONS               *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133735 05/10/2009 MSD1 NEW PROGRAM                        *           
      * BP133735 16/11/2009 JB8  TO CATER FOR NEW ALLO-CAT 'SBF'    *           
      * BP133954 12/08/2010 PCL3 TO READ BP13KH12                   *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13FH10  ASSIGN        TO BP13FH10.                          
           SELECT BP13KH10  ASSIGN        TO BP13KH10                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KH10-KEY-FLD                       
                            FILE STATUS   IS BP13KH10-STATUS.                   
           SELECT BP13KH12  ASSIGN        TO BP13KH12                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KH12-KEY-FLD                       
                            FILE STATUS   IS BP13KH12-STATUS.                   
           SELECT BP13K595  ASSIGN        TO BP13K595                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K595-KEY-FLD                       
                            FILE STATUS   IS BP13K595-STATUS.                   
           SELECT P13FH10A  ASSIGN        TO P13FH10A.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FH10                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FH10.                                                           
                                                                                
       FD   BP13KH10                                                            
            RECORD CONTAINS  2000 CHARACTERS.                                   
       COPY BP13KH10.                                                           
                                                                                
       FD   BP13KH12                                                            
            RECORD CONTAINS  2000 CHARACTERS.                                   
       COPY BP13KH12.                                                           
                                                                                
       FD   P13FH10A                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS  2000 CHARACTERS                                    
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13FH10A-REC.                                                       
            05  FILLER                      PIC X(75).                          
            05  FH10A-NUM-NON-SELECTION     PIC X(02).                          
            05  FILLER                      PIC X(1887).                        
            05  FH10A-NUM-LF-SBF            PIC X(02).                          
            05  FH10A-NUM-WF-SBF            PIC X(02).                          
            05  FH10A-NUM-LF-BTO            PIC X(02).                          
            05  FH10A-NUM-WF-BTO            PIC X(02).                          
            05  FH10A-NUM-LF-BE             PIC X(02).                          
            05  FH10A-NUM-WF-BE             PIC X(02).                          
            05  FILLER                      PIC X(24).                          
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K595.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01 WS-FILE-STATUS.                                                       
          05 BP13KH15-STATUS       PIC 9(02)   VALUE ZEROES.                    
          05 BP13K595-STATUS       PIC 9(02)   VALUE ZEROES.                    
          05 BP13KH10-STATUS       PIC 9(02)   VALUE ZEROES.                    
          05 BP13KH12-STATUS       PIC 9(02)   VALUE ZEROES.                    
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-FH10-READ          PIC 9(08)   VALUE ZEROES.                    
          05 WS-FH10A-WRITE        PIC 9(08)   VALUE ZEROES.                    
          05 WS-LF-SBF             PIC 9(02)   VALUE ZEROES.                    
          05 WS-LF-BTO             PIC 9(02)   VALUE ZEROES.                    
          05 WS-LF-BE              PIC 9(02)   VALUE ZEROES.                    
          05 WS-WF-SBF             PIC 9(02)   VALUE ZEROES.                    
          05 WS-WF-BTO             PIC 9(02)   VALUE ZEROES.                    
          05 WS-WF-BE              PIC 9(02)   VALUE ZEROES.                    
          05 WS-FH10-REGN          PIC X(08)   VALUE SPACES.                    
          05 WS-BOOK-STAT          PIC X(02)   VALUE SPACES.                    
                                                                                
       01 WS-NON-SELECTION.                                                     
          05 WS-SEL                PIC 9(02)   VALUE ZEROES.                    
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-SUB                PIC 9(02)   VALUE ZEROES.                    
          05 WS-DTE-UPDATE         PIC X(08)   VALUE ZEROES.                    
          05 WS-NUM-UIN            PIC X(09)   VALUE SPACES.                    
          05 WS-DTE-BALLOT         PIC X(06)   VALUE SPACES.                    
          05 WS-ALLO-CAT           PIC X(03)   VALUE SPACES.                    
                                                                                
          05 WS-KH10-CNT           PIC 9(02)   VALUE ZEROES.                    
          05 WS-KH10-REC OCCURS 15 TIMES.                                       
             10 WS-KH10-UIN        PIC X(09).                                   
             10 WS-KH10-NS-CNT     PIC X(02).                                   
             10 WS-KH10-REGN       PIC X(08).                                   
             10 WS-KH10-LIMIT      PIC X(06).                                   
          05 WS-CDE-HH             PIC X(01)   VALUE SPACE.                     
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-KH15-FND           PIC X       VALUE 'N'.                       
          05 WS-KH12-FND           PIC X       VALUE 'N'.                       
          05 WS-KH12-EOF           PIC X       VALUE 'N'.                       
          05 WS-K595-FND           PIC X       VALUE 'N'.                       
          05 WS-END-OF-TABLE       PIC X       VALUE 'N'.                       
             88 END-OF-TABLE                   VALUE 'Y'.                       
                                                                                
       01 WS-TIME-DATES.                                                        
          05 WS-SYSTEM-DATE.                                                    
             10 WS-SYS-CCYY.                                                    
                15 WS-SYS-CC       PIC 9(2).                                    
                15 WS-SYS-YY       PIC 9(2).                                    
             10 WS-SYS-MM          PIC 9(2).                                    
             10 WS-SYS-DD          PIC 9(2).                                    
          05 WS-EDITED-DATE.                                                    
             10 WS-EDITED-DD       PIC X(2)    VALUE SPACES.                    
             10 FILLER             PIC X       VALUE '/'.                       
             10 WS-EDITED-MM       PIC X(2)    VALUE SPACES.                    
             10 FILLER             PIC X       VALUE '/'.                       
             10 WS-EDITED-YY       PIC X(4)    VALUE SPACES.                    
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-READ-F000     THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
              UNTIL FH10-NUM-UIN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13FH10                                                 
                       BP13KH10                                                 
                       BP13KH12                                                 
                       BP13K595                                                 
                OUTPUT P13FH10A.                                                
                                                                                
           IF BP13KH10-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13KH10 : ' BP13KH10-STATUS             
              MOVE     BP13KH10-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
           IF BP13K595-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K595 : ' BP13K595-STATUS             
              MOVE     BP13K595-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
           IF BP13KH12-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13KH12 : ' BP13KH12-STATUS             
              MOVE     BP13KH12-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYSTEM-DATE.                  
           MOVE WS-SYS-DD                   TO WS-EDITED-DD.                    
           MOVE WS-SYS-MM                   TO WS-EDITED-MM.                    
           MOVE WS-SYS-CCYY                 TO WS-EDITED-YY.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-F000.                                                          
      *****************************************************************         
                                                                                
           READ BP13FH10 AT END MOVE                                            
                HIGH-VALUES TO FH10-NUM-UIN                                     
                GO TO 2000-EXIT.                                                
                                                                                
            ADD 1 TO WS-FH10-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-PROCESS.                                                            
      *****************************************************************         
                                                                                
           MOVE ZEROS TO  WS-LF-BTO                                             
                          WS-LF-BE                                              
                          WS-LF-SBF.                                            
           MOVE ZEROS TO  WS-WF-BTO                                             
                          WS-WF-BE                                              
                          WS-WF-SBF.                                            
           MOVE SPACES TO WS-BOOK-STAT.                                         
                                                                                
           PERFORM 6100-READ-BP13KH10 THRU 6100-EXIT.                           
                                                                                
           MOVE BP13KH10-REC TO P13FH10A-REC.                                   
           IF FH10A-NUM-NON-SELECTION IS NOT NUMERIC                            
              MOVE ZEROS TO FH10A-NUM-NON-SELECTION                             
           END-IF.                                                              
                                                                                
           MOVE FH10A-NUM-NON-SELECTION TO  WS-NON-SELECTION.                   
           COMPUTE WS-SEL = WS-SEL    +                                         
                            WS-WF-SBF +                                         
                            WS-WF-BTO +                                         
                            WS-WF-BE.                                           
                                                                                
           IF WS-SEL = ZEROS                                                    
              IF WS-LF-BE > 0 OR WS-LF-BTO > 0  OR WS-LF-SBF > 0                
                 COMPUTE WS-SEL  = WS-SEL    +                                  
                                   WS-LF-SBF +                                  
                                   WS-LF-BTO +                                  
                                   WS-LF-BE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-SEL                  TO FH10A-NUM-NON-SELECTION.             
           MOVE WS-LF-SBF               TO FH10A-NUM-LF-SBF.                    
           MOVE WS-WF-SBF               TO FH10A-NUM-WF-SBF.                    
           MOVE WS-LF-BTO               TO FH10A-NUM-LF-BTO.                    
           MOVE WS-WF-BTO               TO FH10A-NUM-WF-BTO.                    
           MOVE WS-LF-BE                TO FH10A-NUM-LF-BE.                     
           MOVE WS-WF-BE                TO FH10A-NUM-WF-BE.                     
           WRITE P13FH10A-REC.                                                  
           ADD 1                        TO WS-FH10A-WRITE.                      
                                                                                
           PERFORM 2000-READ-F000     THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-PROCESS-K595.                                                       
      *****************************************************************         
                                                                                
           MOVE 'N'             TO WS-K595-FND.                                 
           MOVE SPACES          TO WS-BOOK-STAT                                 
                                                                                
           MOVE SPACES          TO K595-KEY-FLD.                                
           MOVE WS-FH10-REGN    TO K595-NUM-REGN-REF.                           
                                                                                
           START BP13K595 KEY >= K595-KEY-FLD.                                  
           EVALUATE BP13K595-STATUS                                             
           WHEN 00                                                              
                PERFORM 4100-READNEXT-BP13K595 THRU 4100-EXIT                   
                   UNTIL K595-NUM-REGN-REF NOT = WS-FH10-REGN                   
                      OR K595-NUM-REGN-REF = HIGH-VALUES                        
           WHEN 10                                                              
           WHEN 23                                                              
                DISPLAY 'BP13K595 RECORD NOT FOUND  = ' K595-KEY-FLD            
           WHEN OTHER                                                           
                DISPLAY 'ERROR START BP13K595, STATUS ' BP13K595-STATUS         
                        ',  REGN ' K595-NUM-REGN-REF                            
                MOVE BP13K595-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       4100-READNEXT-BP13K595.                                                  
      ***************************************************************           
                                                                                
           READ BP13K595 NEXT RECORD                                            
             AT END                                                             
                MOVE HIGH-VALUES      TO K595-NUM-REGN-REF                      
           END-READ.                                                            
                                                                                
           EVALUATE BP13K595-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K595-NUM-REGN-REF = WS-FH10-REGN                         
                       IF K595-CDE-REQUEST-STATUS NOT = SPACES AND 'C'          
                          MOVE 'Y'                  TO WS-K595-FND              
                          MOVE K595-NUM-BOOK-STATUS TO WS-BOOK-STAT             
                       END-IF                                                   
                    ELSE                                                        
                       MOVE HIGH-VALUES          TO K595-NUM-REGN-REF           
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE HIGH-VALUES             TO K595-NUM-REGN-REF           
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K595, STATUS '                   
                        BP13K595-STATUS ',  REGN ' K595-NUM-REGN-REF            
                    MOVE BP13K595-STATUS         TO RETURN-CODE                 
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       6100-READ-BP13KH10.                                                      
      **************************************************************            
                                                                                
           MOVE FH10-NUM-UIN              TO KH10-NUM-UIN.                      
                                                                                
           READ BP13KH10.                                                       
                                                                                
           EVALUATE BP13KH10-STATUS                                             
               WHEN 00                                                          
                    MOVE  'N'             TO WS-END-OF-TABLE                    
                    PERFORM 6200-CHECK-BP13KH10 THRU 6200-EXIT                  
                            VARYING WS-SUB FROM 01 BY 1                         
                            UNTIL END-OF-TABLE OR WS-SUB > 15                   
               WHEN 23                                                          
                    PERFORM 6300-READ-BP13KH12 THRU 6300-EXIT                   
                                                                                
                    IF WS-KH12-FND = 'Y'                                        
                       MOVE  'N'          TO WS-END-OF-TABLE                    
                       PERFORM 6200-CHECK-BP13KH10 THRU 6200-EXIT               
                               VARYING WS-SUB FROM 01 BY 1                      
                               UNTIL END-OF-TABLE OR WS-SUB > 15                
                    END-IF                                                      
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13KH10 READ ERROR,STATUS: '                      
                        BP13KH10-STATUS ', KH10 KEY FLD: ' KH10-KEY-FLD         
                    MOVE BP13KH10-STATUS       TO RETURN-CODE                   
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       6200-CHECK-BP13KH10.                                                     
      **************************************************************            
                                                                                
           IF KH10-NUM-REGN (WS-SUB) =  SPACES OR LOW-VALUES                    
              SET END-OF-TABLE TO TRUE                                          
              GO TO 6200-EXIT.                                                  
                                                                                
           MOVE KH10-NUM-REGN (WS-SUB)       TO WS-FH10-REGN.                   
           PERFORM 4000-PROCESS-K595       THRU 4000-EXIT.                      
                                                                                
           IF WS-BOOK-STAT  = 'LF'                                              
              EVALUATE KH10-NUM-ALLO-CAT (WS-SUB)                               
                  WHEN 'BTO'                                                    
                       ADD 1 TO WS-LF-BTO                                       
                  WHEN 'BE '                                                    
                       ADD 1 TO WS-LF-BE                                        
                  WHEN 'SBF'                                                    
                       ADD 1 TO WS-LF-SBF                                       
              END-EVALUATE                                                      
           ELSE                                                                 
              IF WS-BOOK-STAT  = 'WF'                                           
                 EVALUATE KH10-NUM-ALLO-CAT (WS-SUB)                            
                     WHEN 'BTO'                                                 
                          ADD 1 TO WS-WF-BTO                                    
                     WHEN 'BE '                                                 
                          ADD 1 TO WS-WF-BE                                     
                     WHEN 'SBF'                                                 
                          ADD 1 TO WS-WF-SBF                                    
                 END-EVALUATE                                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6300-READ-BP13KH12.                                                      
      *****************************************************************         
                                                                                
           MOVE SPACES                         TO BP13KH12-REC                  
                                                  BP13KH10-REC.                 
                                                                                
           INITIALIZE                             BP13KH12-REC                  
                                                  BP13KH10-REC.                 
                                                                                
           MOVE SPACES                         TO KH12-KEY-FLD                  
                                                  WS-DTE-UPDATE.                
                                                                                
           MOVE FH10-NUM-UIN                   TO KH12-NUM-UIN.                 
                                                                                
           MOVE 'N'                            TO WS-KH12-EOF                   
                                                  WS-KH12-FND.                  
                                                                                
           START BP13KH12 KEY >= KH12-KEY-FLD.                                  
           EVALUATE BP13KH12-STATUS                                             
           WHEN 00                                                              
                PERFORM 6400-READNEXT-BP13KH12 THRU 6400-EXIT                   
                  UNTIL KH12-NUM-UIN NOT = FH10-NUM-UIN                         
                     OR WS-KH12-EOF = 'Y'                                       
           WHEN 10                                                              
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR START BP13KH12, STATUS  '                        
                        BP13KH12-STATUS                                         
                        ',  NRIC ' KH12-NUM-UIN                                 
                MOVE BP13KH12-STATUS            TO RETURN-CODE                  
                PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6400-READNEXT-BP13KH12.                                                  
      *****************************************************************         
                                                                                
           READ BP13KH12 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-KH12-EOF.                                 
                                                                                
           EVALUATE BP13KH12-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
                IF KH12-NUM-UIN = FH10-NUM-UIN                                  
                   IF KH12-DTE-UPDATE > WS-DTE-UPDATE                           
                      MOVE 'Y'                       TO WS-KH12-FND             
                      MOVE KH12-DTE-UPDATE           TO WS-DTE-UPDATE           
                      MOVE BP13KH12-REC              TO BP13KH10-REC            
                   END-IF                                                       
                ELSE                                                            
                   MOVE 'Y'                          TO WS-KH12-EOF             
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'                             TO WS-KH12-EOF             
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13KH12, STATUS '                       
                     BP13KH12-STATUS ',  NRIC ' KH12-NUM-UIN                    
                MOVE BP13KH12-STATUS                 TO RETURN-CODE             
                PERFORM 9000-CLOSE-ROUTINE         THRU 9000-EXIT               
           END-EVALUATE.                                                        
                                                                                
       6400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-ROUTINE.                                                      
      *****************************************************************         
                                                                                
           DISPLAY '*-----------  BP13CH21 ------------*'.                      
           DISPLAY ' FH10-READ       ' WS-FH10-READ.                            
           DISPLAY ' FH10A-WRITE     ' WS-FH10A-WRITE.                          
           CLOSE      BP13FH10                                                  
                      BP13KH10                                                  
                      BP13KH12                                                  
                      P13FH10A                                                  
                      BP13K595.                                                 
           IF BP13KH10-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KH10 : ' BP13KH10-STATUS             
              MOVE BP13KH10-STATUS TO RETURN-CODE.                              
           IF BP13K595-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K595 : ' BP13K595-STATUS             
              MOVE BP13K595-STATUS TO RETURN-CODE.                              
           IF BP13KH12-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KH12 : ' BP13KH12-STATUS             
              MOVE BP13KH12-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
