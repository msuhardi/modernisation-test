  /*==============================================================*/            
  /* SYSTEM OF COMMITMENT  (BP13)                                 */            
  /*==============================================================*/            
  /*                                                              */            
  /* PROGRAM    : BP13S908                                        */            
  /* OBJECTIVE  : PRINT CHILD MGPS FLAT LIST BY NT/FT             */            
  /*                                                              */            
  /*==============================================================*/            
  /* CHGE NO  DATE     OIC  DESCRIPTION                           */            
  /* -------- -------- ---- ------------------------------------- */            
  /* BP135080 08102013 SMR2 NEW SAS PROGRAM                       */            
  /*==============================================================*/            
  OPTIONS  NODATE NONUMBER;                                                     
  DATA BP13F100;                                                                
  INFILE BP13F100;                                                              
  INPUT  @120  NEW_TOWN     $CHAR3.                                             
         @72   FLAT_TYPE    $CHAR2.                                             
         @59   NEIGH        $CHAR4.                                             
         @63   CONTRACT     $CHAR4.                                             
         @132  PARENT_FT    $CHAR1.                                             
         @67   BLOCK        $CHAR5.                                             
         @132  PFT          $CHAR2.                                             
         @108  LEVEL        $CHAR2.                                             
         @110  MAIN         $CHAR4.                                             
         @114  SUB          $CHAR3.                                             
         @76   STREET       $CHAR32.                                            
         @1    SCH_ACC      $CHAR11.                                            
         @74   MODL         $CHAR2.                                             
         @19   RANDOM       $CHAR28.;                                           
                                                                                
      LENGTH UNIT $11.;                                                         
      LENGTH UNIT_REQ 2.;                                                       
      IF SUB = '   ' THEN                                                       
         UNIT=LEVEL||'-'||MAIN;                                                 
      ELSE                                                                      
         UNIT=LEVEL||'-'||MAIN||'-'||SUB;                                       
                                                                                
    DATA _NULLS_ ;                                                              
    SET BP13F100 END=FINAL;                                                     
    BY NEW_TOWN FLAT_TYPE NEIGH CONTRACT BLOCK PARENT_FT;                       
    OPTIONS PAGESIZE=55 LS=132 NODATE;                                          
    FILE BP13L908   HEADER=HH NOTITLES;                                         
                                                                                
   IF FIRST.NEW_TOWN OR FIRST.FLAT_TYPE OR FIRST.NEIGH OR                       
      FIRST.CONTRACT OR FIRST.BLOCK OR FIRST.PARENT_FT THEN                     
      DO;                                                                       
         PUT _PAGE_;                                                            
         LINK REQ_UNIT;                                                         
         PUT @1  'NEW TOWN  : ' NEW_TOWN     $CHAR3.;                           
         PUT @1  'FLAT TYPE : ' FLAT_TYPE    $CHAR2.;                           
         PUT @1  'NEIGH     : ' NEIGH        $CHAR4.;                           
         PUT @1  'CONTRACT  : ' CONTRACT     $CHAR4.;                           
         PUT @1  'PARENT FT : ' PARENT_FT    $CHAR1.;                           
         UNIT_REQ=K115REQ/2;                                                    
         PUT @1  'UNITS     : ' UNIT_REQ     2.;                                
         PUT @2  'S/NO    NGH   CONT  BLOCK   PFT   UNIT         '              
             @49 'STREET                            SCHEME A/C    '             
             @97 'MODL   RANDOM';                                               
         PUT @1  '------------------------------------------------'             
             @49 '------------------------------------------------'             
             @97 '------------------------------------';                        
         N=0;                                                                   
      END;                                                                      
                                                                                
         N+1;                                                                   
         PUT  @1   N            5.                                              
              @10  NEIGH        $CHAR4.                                         
              @16  CONTRACT     $CHAR4.                                         
              @22  BLOCK        $CHAR5.                                         
              @30  PFT          $CHAR2.                                         
              @36  UNIT         $CHAR11.                                        
              @49  STREET       $CHAR32.                                        
              @83  SCH_ACC      $CHAR11.                                        
              @97  MODL         $CHAR2.                                         
              @104 RANDOM       $CHAR28.;                                       
                                                                                
   RETURN;                                                                      
                                                                                
  REQ_UNIT:                                                                     
      LENGTH K115KEY $21.;                                                      
      K115KEY=NEW_TOWN||FLAT_TYPE||NEIGH||CONTRACT||BLOCK||                     
              'C'||PARENT_FT;                                                   
                                                                                
      INFILE BP13K115 VSAM KEY=K115KEY;                                         
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
         DO;                                                                    
            INPUT @32 K115REQ  2.;                                              
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            FILE LOG;                                                           
            PUT 'RECORD NOT FND IN BP13K115 ' K115KEY;                          
            _ERROR_ = 0;                                                        
         END;                                                                   
                                                                                
   RETURN;                                                                      
                                                                                
  HH:                                                                           
     PAGE+1;                                                                    
     CURRENT=DATE();                                                            
     PUT @1   'BP13L908' @14 'HDBCAT 3'                                         
         @51  'SYSTEM OF COMMITMENT'                                            
         @94  'DATE : ' @101 CURRENT DDMMYY10.                                  
         @117 'PAGE : ' @124 PAGE 6.;                                           
     PUT @42  'MGPS FLAT LIST BY NT/FLAT TYPE (CHILD)' //;                      
     RETURN;                                                                    
