      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CR4A.                                                 
       AUTHOR.        MRN1                                                      
       DATE-WRITTEN.  14/04/2015.                                               
                                                                                
      ***************************************************************           
      *            SYSTEM OF COMMITMENT (BP13)                      *           
      *=============================================================*           
      *  OBJECTIVE  : TO READ FROM GRANT AND RECOVERY TABLES      *             
      *  INPUT      : COM4A-NUM-CALL-ACTION                                     
      *               COM4A-NUM-RGSTRN-INPUT                                    
      *               COM4A-NUM-GRANT-TYPE-INPUT                  *             
      *               COM4A-NUM-NRIC-INPUT                        *             
      *  COMMAREA   : 1500 BYTES                                  *             
      *  ACTION CODE: 'R' -> RETRIEVE                             *             
      *************************************************************             
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHGE REQ #  DATE        AUTHOR  REMARKS                   *             
      * ----------  ----------  ------  ------------------------- *             
      * BP135620    14/04/2015  MRN1    NEW SUBROUTINE            *             
      *************************************************************             
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       DATA DIVISION.                                                           
      *-----------------------*                                                 
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND                 PIC S9(4) COMP VALUE +000.          
           05  WS-SQL-NOT-FOUND             PIC S9(4) COMP VALUE +100.          
                                                                                
       01  WS-SQLCODE                       PIC S9(4) COMP.                     
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-RGSTRN                PIC X(8).                           
           05  WS-NUM-GRANT-TYPE            PIC X(6).                           
           05  WS-CTR                       PIC 9(2).                           
           05  WS-CURSOR-EOF                PIC X VALUE 'N'.                    
           05  WS-SQL-CODE                  PIC 9(4).                           
           05  WS-SQL-CODE-X REDEFINES WS-SQL-CODE PIC X(4).                    
           05  WS-AMT-GRANT                 PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-AMT-RECOVERY              PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-GRANT-AMT-SUM             PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-EX-GRANT-AMT-SUM          PIC S9(7)V9(2) USAGE COMP-3.        
           05  WS-AMT-RECOVERY-INDNULL      PIC S9(4) COMP.                     
           05  WS-GRANT-AMT-SUM-INDNULL     PIC S9(4) COMP.                     
           05  WS-EX-GRANT-AMT-SUM-INDNULL  PIC S9(4) COMP.                     
                                                                                
           EXEC SQL INCLUDE P13VGAPN END-EXEC.                                  
           EXEC SQL INCLUDE P13VGXAP END-EXEC.                                  
           EXEC SQL INCLUDE P13VGRCV END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
      *-----------------------*                                                 
       LINKAGE SECTION.                                                         
      *-----------------------*                                                 
                                                                                
       COPY P13COM4A.                                                           
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION USING BP13COM4A-REC.                                  
      *------------------*                                                      
                                                                                
           EXEC SQL WHENEVER NOT FOUND                                          
                             CONTINUE END-EXEC.                                 
           EXEC SQL WHENEVER SQLERROR                                           
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
           EXEC SQL WHENEVER SQLWARNING                                         
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
                                                                                
      *-----------------------*                                                 
       0000-START-ROUTINE.                                                      
      *-----------------------*                                                 
           MOVE SPACE            TO COM4A-CDE-ERROR.                            
           MOVE ZEROES           TO COM4A-CDE-SYSERR.                           
                                                                                
           EVALUATE COM4A-NUM-CALL-ACTION                                       
              WHEN 'R'                                                          
                    PERFORM 1000-GET-GRANT-RCVRY    THRU 1000-EXIT              
              WHEN OTHER                                                        
                    MOVE 'E517'       TO COM4A-CDE-ERROR                        
                    MOVE 'INVALID ACTION CODE' TO COM4A-ERR-MSG                 
           END-EVALUATE.                                                        
                                                                                
           GOBACK.                                                              
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       1000-GET-GRANT-RCVRY.                                                    
      *-----------------------*                                                 
           MOVE ZEROES TO WS-AMT-GRANT                                          
                          WS-AMT-RECOVERY                                       
                          WS-GRANT-AMT-SUM                                      
                          WS-EX-GRANT-AMT-SUM.                                  
           MOVE COM4A-NUM-RGSTRN-INPUT TO WS-NUM-RGSTRN.                        
           MOVE COM4A-NUM-GRANT-TYPE-INPUT TO WS-NUM-GRANT-TYPE.                
           PERFORM 1100-GET-EX-GRANT-RCVRY THRU 1100-EXIT.                      
           PERFORM 1400-GET-GRANT-TOT      THRU 1400-EXIT.                      
           COMPUTE WS-AMT-GRANT = WS-GRANT-AMT-SUM +                            
                                  WS-EX-GRANT-AMT-SUM.                          
           MOVE WS-AMT-GRANT    TO COM4A-AMT-GRANT-TOTAL.                       
           MOVE WS-AMT-RECOVERY TO COM4A-AMT-RCVRY-TOTAL.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
      *---------------------------------------------------------------*         
       1100-GET-EX-GRANT-RCVRY.                                                 
      *---------------------------------------------------------------*         
                                                                                
            EXEC SQL                                                            
               SELECT SUM(T1.AMT_GRANT),                                        
                      SUM(T2.AMT_GRANT_RCVR)                                    
               INTO   :WS-EX-GRANT-AMT-SUM:WS-EX-GRANT-AMT-SUM-INDNULL,         
                      :WS-AMT-RECOVERY:WS-AMT-RECOVERY-INDNULL                  
               FROM                                                             
                 EX_GRANT_ASMNT_PERSON T1,                                      
                 GRANT_RCVRY T2                                                 
               WHERE                                                            
                 T1.NUM_RGSTRN     = :WS-NUM-RGSTRN          AND                
                 T1.NUM_GRANT_TYPE = :WS-NUM-GRANT-TYPE      AND                
                 T1.NUM_RGSTRN     = T2.NUM_RGSTRN           AND                
                 T1.NUM_GRANT_TYPE = T2.NUM_GRANT_TYPE       AND                
                 T1.NUM_IDNTY_NO   = T2.NUM_IDNTY_NO         AND                
                 T1.NUM_IDNTY_NO_TYPE = T2.NUM_IDNTY_NO_TYPE AND                
                 T1.TME_CREATE = T2.TME_CREATE                                  
            END-EXEC.                                                           
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-FOUND                                                    
             CONTINUE                                                           
           WHEN WS-SQL-NOT-FOUND                                                
             MOVE ZEROES             TO WS-EX-GRANT-AMT-SUM                     
                                        WS-AMT-RECOVERY                         
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM4A-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM4A-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE 'READ EX-GRANT-RCVRY ERROR' TO COM4A-ERR-MSG                  
             GO TO 1000-EXIT                                                    
           END-EVALUATE.                                                        
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1400-GET-GRANT-TOT.                                                      
      *---------------------------------------------------------------*         
                                                                                
            EXEC SQL                                                            
                 SELECT SUM(AMT_GRANT)                                          
                  INTO :WS-GRANT-AMT-SUM:WS-GRANT-AMT-SUM-INDNULL               
                  FROM  GRANT_ASMNT_PERSON                                      
                  WHERE NUM_RGSTRN = :WS-NUM-RGSTRN AND                         
                    NUM_GRANT_TYPE = :WS-NUM-GRANT-TYPE                         
            END-EXEC.                                                           
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-FOUND                                                    
             CONTINUE                                                           
           WHEN WS-SQL-NOT-FOUND                                                
             MOVE ZEROES             TO WS-GRANT-AMT-SUM                        
           WHEN OTHER                                                           
             MOVE SQLCODE            TO WS-SQL-CODE                             
             MOVE WS-SQL-CODE-X      TO COM4A-CDE-ERROR                         
             IF SQLCODE < ZEROES                                                
                MOVE '-'             TO COM4A-CDE-ERROR(1:1)                    
             END-IF                                                             
             MOVE 'READ GRANT_ASMNT_PERSON ERROR' TO COM4A-ERR-MSG              
             GO TO 1000-EXIT                                                    
           END-EVALUATE.                                                        
                                                                                
       1400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       9000-ABORT-PROCESS.                                                      
      *-----------------------*                                                 
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE       TO COM4A-CDE-SYSERR.                              
           MOVE SQLCODE       TO RETURN-CODE.                                   
           DISPLAY 'CR4A - SYSERR = ' COM4A-CDE-SYSERR.                         
           GOBACK.                                                              
