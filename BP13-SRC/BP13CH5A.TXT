       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH5A.                                                 
       AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  25/05/2015.                                               
      * ===============================================================*        
      * BP13 SYSTEM OF COMMITMENT                                      *        
      * ===============================================================*        
      * OBJECTIVE   : GET RECORDS FROM BM06K190 WHERE K190-SYS-DATE IS *        
      *               EQUAL TO CURRENT DATE AND K190-TRANS-TYPE IS IS  *        
      *               EQUAL TO B21500                                  *        
      *                                                                *        
      *   INPUT FILES :  1. BM06F190                                   *        
      *                  2. BM06K100                                   *        
      *                                                                *        
      *   OUTPUT      :  1. BP13F100                                   *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *   BP135711  RJB1  25/05/15 NEW PROGRAM                         *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06K100  ASSIGN        TO BM06K100                   00470002
                            ORGANIZATION     IS INDEXED                 00480002
                            ACCESS MODE      IS DYNAMIC                 00490002
                            RECORD KEY       IS K100-KEY-FLD            00500002
                            FILE STATUS      IS WS-K100-STATUS.         00510002
                                                                        00520002
           SELECT BM06F190  ASSIGN        TO BM06F190.                          
           SELECT BM06F100  ASSIGN        TO BM06F100.                          
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BM06F100                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 500 CHARACTERS.                                      
       01  BM06F100-REC        PIC X(500).                                      
                                                                                
       FD  BM06K100                                                     00950002
           RECORD CONTAINS 500 CHARACTERS.                              00960002
       COPY BM06K100.                                                   01030002
                                                                        01040002
       FD  BM06F190                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 650 CHARACTERS.                                      
       COPY BM06F190.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY             PIC 9(04).                               
           05  WS-CUR-MM               PIC 9(02).                               
           05  WS-CUR-DD               PIC 9(02).                               
                                                                                
       01  WS-REPORT-DATE.                                                      
           05  WS-REP-CCYY             PIC X(04).                               
           05  WS-REP-MM               PIC X(02).                               
                                                                                
       01  WS-COUNT.                                                            
           05  WS-F190-INPUT           PIC 9(08) VALUE 0.                       
           05  WS-F190-OUT             PIC 9(08) VALUE 0.                       
           05  WS-WRITTEN              PIC 9(08) VALUE 0.                       
                                                                                
       01  WS-TIME                     PIC 9(08) VALUE 0.                       
       01  WS-F190-EOF                 PIC X(01) VALUE SPACES.                  
       01  WS-FOUND-K100               PIC X(01) VALUE 'N'.                     
       01  WS-FILE-STATUS.                                                      
           05 WS-K100-STATUS           PIC 99    VALUE ZEROS.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-F190-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BM06F190                                                 
                       BM06K100                                                 
                OUTPUT BM06F100.                                                
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K100 ERROR ' WS-K100-STATUS                  
              MOVE     WS-K100-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           INITIALIZE  WS-WRITTEN.                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BM06F190                                                       
                 AT END MOVE 'Y'  TO WS-F190-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-F190-EOF NOT = 'Y'                                            
               ADD 1 TO WS-F190-INPUT                                           
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           IF F190-SYS-DATE = WS-CUR-DATE                                       
              PERFORM 4000-READ-BM06K100                                        
                 THRU 4000-EXIT                                                 
              IF WS-FOUND-K100 = 'Y'                                            
                 ADD 1 TO WS-WRITTEN                                            
                 WRITE BM06F100-REC FROM K100-REC                               
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'F190-SYS-DATE = ' F190-SYS-DATE                          
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-INPUT                                              
              THRU 2000-EXIT.                                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       4000-READ-BM06K100.                                                      
      *================================================================*        
                                                                                
           MOVE 'N'                     TO WS-FOUND-K100.                       
                                                                                
           MOVE F190-2-ESTATE           TO K100-ESTATE.                 04770002
           MOVE F190-2-NEIGHBOURHOOD    TO K100-NEIGHBOURHOOD.          04780002
           MOVE F190-2-CONTRACT-NO      TO K100-CONTRACT-NO.            04790002
           MOVE F190-2-BLK-NO           TO K100-BLK-NO.                 04800002
                                                                        04810002
           READ BM06K100.                                               04820002
                                                                        04830002
           EVALUATE WS-K100-STATUS                                              
              WHEN 00                                                           
                 IF K100-NUM-PACD = 'M'                                         
                    MOVE 'Y' TO WS-FOUND-K100                                   
                 ELSE                                                           
                    MOVE 'N' TO WS-FOUND-K100                                   
                    DISPLAY 'K100-NUM-PACD = ' K100-NUM-PACD                    
                            '/' K100-KEY-FLD                                    
                 END-IF                                                         
              WHEN 23                                                           
                 MOVE 'N' TO WS-FOUND-K100                                      
                 DISPLAY 'BM06K100 REC NOT FND : ' WS-K100-STATUS               
                         ' K100-KEY-FLD = ' K100-KEY-FLD                        
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BM06K100. STATUS IS '                   
                            WS-K100-STATUS                                      
                 MOVE WS-K100-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           DISPLAY 'PROGRAM: BP13CH5A'.                                         
           DISPLAY 'TOTAL INPUT RECORDS        : ' WS-F190-INPUT.               
           DISPLAY 'TOTAL RECORD WRITTEN       : ' WS-WRITTEN.                  
           DISPLAY '        '                                                   
                                                                                
           CLOSE BM06F190                                                       
                 BM06K100                                                       
                 BM06F100.                                                      
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K100 ERROR ' WS-K100-STATUS                  
              MOVE     WS-K100-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
