      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C409.                                                 
       AUTHOR.        TMK.                                                      
       DATE-WRITTEN.  07/11/89.                                                 
                                                                                
      *========================================================*                
      *    SYSTEM NAME   :   SYSTEM OF COMMITMENT              *                
      *    SYSTEM ID     :   BP13                              *                
      *                                                        *                
      *    OBJECTIVE     :                                     *                
      *        1.  TO OUTPUT FROM THE TEMPORARY SALES ORDER    *                
      *            FILE (BP13F410) A FEW FILES AS LISTED BELOW *                
      *                                                        *                
      *     INPUT  FILE  :                                     *                
      *        1.  BP13F410 (TEMPORARY SALES ORDER FILE)       *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *        1.  BP13F418   -   PUB UPDATE  (BP13F418)       *                
      *        2.  BP13F220   -   TEMP FILE FOR PRINTING       *                
      *                           REPORT TO RESETTLEMENT       *                
      *                           (BP13F220)                   *                
      *                                                        *                
      *     LISTING:                                           *                
      *           1. BP13L409 (CONTROL LIST)                   *                
      *                                                        *                
      *                                                        *                
      *  CHG REQ  DATE    BY   DESCRIPTION                     *                
      *  =======  ======  ==   ============================    *                
      *  SOC-PH8  090393  LMS  RENAME BM06F320 TO BP13F220.    *                
      *  SOC-PH8  072093  JEP  RENAME BM06F300 TO BP13F418.    *                
      *  BP130418 060898  RHB  Y2K                             *                
      *  BP130539 110399  LSB  TO MODIFY COMPUTATION OF LOAN   *                
      *                        AMT FOR RELOCATION APPLICANTS   *                
      *  BP130790 020200  SCK  TO ADD IN IN-SOURCE DOCUMENTATION*               
      *  BP130883 080800  RAS  TO CHANGE BP13C904 TO BP13C913  *                
      *  BP132713 060905  CG7  ADDED NEW COUNTERS TO BREAK DOWN*                
      *                        THE TOTAL NUMBER OF SALES ORDER *                
      *  BP137888 280819  DN8  TO MOVE SPACES TO NAME & NRIC   *                
      *========================================================*                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
           SELECT BP13F418 ASSIGN TO BP13F418.                                  
           SELECT BP13F220 ASSIGN TO BP13F220.                                  
           SELECT BP13L409 ASSIGN TO BP13L409.                                  
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BP13F410.                                                           
                                                                                
       FD   BP13F418                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 240  CHARACTERS                                     
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BP13F418.                                                           
                                                                                
       FD   BP13F220                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 350  CHARACTERS                                     
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BP13F220.                                                           
                                                                                
       FD   BP13L409                                                            
            RECORDING MODE  IS   F                                              
            RECORD CONTAINS 132  CHARACTERS                                     
            LABEL  RECORDS  ARE  OMITTED.                                       
       01   PRT-LINE                 PIC X(132).                                
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-PROGRAM-SWITCHES.                                                 
           05  WS-EOF                PIC X(2)   VALUE 'N'.                      
                                                                                
       01  WS-SQL-CODE               PIC 9(4)   VALUE ZEROS.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-LINE-CNT           PIC 9(2)   VALUE ZEROS.                    
           05  WS-PAGE-CNT           PIC 9(3)   VALUE ZEROS.                    
           05  WS-F410-CNT           PIC 9(5)   VALUE ZEROS.                    
           05  WS-PUB-CNT            PIC 9(5)   VALUE ZEROS.                    
           05  WS-RES-CNT            PIC 9(5)   VALUE ZEROS.                    
           05  WS-TP-CNT             PIC 9(5)   VALUE ZEROS.                    
           05  WS-SATP-CNT           PIC 9(5)   VALUE ZEROS.                    
           05  WS-BB-CNT             PIC 9(5)   VALUE ZEROS.                    
           05  WS-RP-CNT             PIC 9(5)   VALUE ZEROS.                    
           05  WS-STD-CNT            PIC 9(5)   VALUE ZEROS.                    
                                                                                
       01  WS-IDP-SCH.                                                          
           05  WS-IDP-SCH2           PIC X(2)   VALUE SPACES.                   
           05  FILLER                PIC X      VALUE SPACE.                    
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CC                 PIC 99     VALUE ZEROS.                    
           05  WS-CURRENT-DTE.                                                  
               10  WS-YY             PIC 99     VALUE ZEROS.                    
               10  WS-MM             PIC 99     VALUE ZEROS.                    
               10  WS-DD             PIC 99     VALUE ZEROS.                    
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD          PIC 99     VALUE ZEROS.                    
           05  FILLER                PIC X      VALUE '/'.                      
           05  WS-EDITED-MM          PIC 99     VALUE ZEROS.                    
           05  FILLER                PIC X      VALUE '/'.                      
           05  WS-EDITED-CC          PIC 99     VALUE ZEROS.                    
           05  WS-EDITED-YY          PIC 99     VALUE ZEROS.                    
                                                                                
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH   PIC X(4).                                  
               10  WS-LINK-NUM-ACC   PIC X(5).                                  
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *        HEADINGS FOR CONTROL LIST                              *         
      *---------------------------------------------------------------*         
       01  L409-HDG-1.                                                          
           05  FILLER                PIC X(14)  VALUE 'BP13L409'.               
           05  FILLER                PIC X(8)   VALUE 'HDBCAT 3'.               
           05  FILLER                PIC X(18)  VALUE SPACES.                   
           05  FILLER                PIC X(39)  VALUE                           
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                PIC X(18)  VALUE SPACES.                   
           05  FILLER                PIC X(7)   VALUE 'DATE : '.                
           05  L409-DATE             PIC X(10).                                 
           05  FILLER                PIC X(08)  VALUE ' PAGE : '.               
           05  L409-PG-CNT           PIC ZZZZ9.                                 
                                                                                
       01  L409-HDG-3.                                                          
           05  FILLER                PIC X(52)  VALUE SPACES.                   
           05  FILLER                PIC X(16)  VALUE                           
               'CONTROL LIST FOR'.                                              
                                                                                
       01  L409-HDG-4.                                                          
           05  FILLER                PIC X(40)  VALUE SPACES.                   
           05  FILLER                PIC X(51)  VALUE                           
               'CREATING PUB, REGN, RESETTLEMENT FILES'.                        
                                                                                
       01  L409-DL-1.                                                           
           05  FILLER                PIC X(40)  VALUE SPACES.                   
           05  FILLER                PIC X(30)  VALUE                           
               '1) NO OF S.O (F410) READ   ='.                                  
           05  L409-F410-CASES       PIC ZZZZ9.                                 
                                                                                
       01  L409-DL-2.                                                           
           05  FILLER                PIC X(40)  VALUE SPACES.                   
           05  FILLER                PIC X(30)  VALUE                           
               '2) NO OF PUB CASES CREATED ='.                                  
           05  L409-PUB-CASES        PIC ZZZZ9.                                 
                                                                                
       01  L409-DL-3.                                                           
           05  FILLER                PIC X(40)  VALUE SPACES.                   
           05  FILLER                PIC X(30)  VALUE                           
               '3) NO OF RESETTLEMENT CASE ='.                                  
           05  L409-RES-CASES        PIC ZZZZ9.                                 
                                                                                
       01  L409-DL-4.                                                           
           05  FILLER                PIC X(40)  VALUE SPACES.                   
           05  FILLER                PIC X(30)  VALUE                           
               '4) NO OF TP CASE           ='.                                  
           05  L409-TP-CASES         PIC ZZZZ9.                                 
                                                                                
       01  L409-DL-5.                                                           
           05  FILLER                PIC X(40)  VALUE SPACES.                   
           05  FILLER                PIC X(30)  VALUE                           
               '5) NO OF SATP CASE         ='.                                  
           05  L409-SATP-CASES       PIC ZZZZ9.                                 
                                                                                
       01  L409-DL-6.                                                           
           05  FILLER                PIC X(40)  VALUE SPACES.                   
           05  FILLER                PIC X(30)  VALUE                           
               '6) NO OF BB CASE           ='.                                  
           05  L409-BB-CASES         PIC ZZZZ9.                                 
                                                                                
       01  L409-DL-7.                                                           
           05  FILLER                PIC X(40)  VALUE SPACES.                   
           05  FILLER                PIC X(30)  VALUE                           
               '7) NO OF RP CASE           ='.                                  
           05  L409-RP-CASES         PIC ZZZZ9.                                 
                                                                                
       01  L409-DL-8.                                                           
           05  FILLER                PIC X(40)  VALUE SPACES.                   
           05  FILLER                PIC X(30)  VALUE                           
               '8) NO OF STD CASE          ='.                                  
           05  L409-STD-CASES        PIC ZZZZ9.                                 
                                                                                
       01  L409-DL-9.                                                           
           05  FILLER                PIC X(40)  VALUE SPACES.                   
           05  FILLER                PIC X(30)  VALUE 'PLEASE CHECK :'.         
                                                                                
       01  L409-DL-10.                                                          
           05  FILLER                PIC X(50)  VALUE SPACES.                   
           05  FILLER                PIC X(30)  VALUE '1) = 2) = 3)'.           
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
                                                                                
      **********************                                                    
       100-MAIN.                                                                
      **********************                                                    
           PERFORM  200-OPENING-ROUTINE     THRU  200-EXIT.                     
           PERFORM  300-READ-F410-REC       THRU  300-EXIT.                     
           PERFORM  400-PROCESS-F410-REC    THRU  400-EXIT                      
                        UNTIL WS-EOF = 'Y'.                                     
           PERFORM  500-PRINT-CONTROL-LIST  THRU  500-EXIT.                     
           PERFORM  600-CLOSING-ROUTINE     THRU  600-EXIT.                     
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       200-OPENING-ROUTINE.                                                     
      **********************                                                    
           OPEN INPUT  BP13F410                                                 
                OUTPUT BP13F418                                                 
                       BP13F220                                                 
                       BP13L409.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *         READ INPUT FILE - AT END SET INDICATOR                 *        
      ******************************************************************        
      **********************                                                    
       300-READ-F410-REC.                                                       
      **********************                                                    
           READ BP13F410 AT END                                                 
                         MOVE 'Y' TO WS-EOF                                     
                         GO TO 300-EXIT.                                        
           ADD 1 TO WS-F410-CNT.                                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       400-PROCESS-F410-REC.                                                    
      **********************                                                    
           PERFORM  420-CALL-BP13C913  THRU  420-EXIT.                          
           PERFORM  430-COUNT-CASES    THRU  430-EXIT.                          
           PERFORM  440-WRITE-F418     THRU  440-EXIT.                          
           PERFORM  460-WRITE-F220     THRU  460-EXIT.                          
           PERFORM  300-READ-F410-REC  THRU  300-EXIT.                          
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       420-CALL-BP13C913.                                                       
      **********************                                                    
           MOVE SPACES                TO BP13COMM8-REC.                         
           INITIALIZE                    BP13COMM8-REC.                         
                                                                                
           MOVE F410-NUM-SCH-ACC      TO WS-LINK-REC.                           
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR TO WS-SQL-CODE                              
              DISPLAY 'F410-NUM-SCH-ACC =  ' F410-NUM-SCH-ACC                   
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PIDB TABLE  ' WS-SQL-CODE        
              ELSE                                                              
                 DISPLAY 'ERROR READING PIDB TABLE  ' WS-SQL-CODE.              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       430-COUNT-CASES.                                                         
      **********************                                                    
           IF F410-CDE-SA-TP = ' ' OR 'N'                                       
              ADD 1        TO WS-TP-CNT.                                        
                                                                                
           IF F410-CDE-SA-TP = 'Y'                                              
              ADD 1        TO WS-SATP-CNT.                                      
                                                                                
           IF F410-CDE-SALES-TYPE = '3A'                                        
              ADD 1        TO WS-BB-CNT.                                        
                                                                                
           IF F410-CDE-SALES-TYPE = '03'                                        
              ADD 1        TO WS-RP-CNT.                                        
                                                                                
           IF F410-CDE-SALES-TYPE = '2B'                                        
              ADD 1        TO WS-STD-CNT.                                       
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       440-WRITE-F418.                                                          
      **********************                                                    
           MOVE    SPACES                 TO  BP13F418-REC.                     
           MOVE    F410-DTE-SO-EXECUTION  TO  F418-DATE-SOLD.                   
           MOVE    F410-NUM-SCH-ACC       TO  F418-SCHEME-ACCOUNT.              
           MOVE    COMM8-NUM-BLK          TO  F418-BLOCK.                       
           MOVE    COMM8-NME-STREET       TO  F418-STREETNAME.                  
           MOVE    COMM8-NUM-LEVEL        TO  F418-LEVEL.                       
           MOVE    COMM8-NUM-UNIT-MAIN    TO  F418-MAIN-UNIT.                   
           MOVE    COMM8-NUM-UNIT-SUB     TO  F418-SUB-UNIT.                    
           MOVE    COMM8-NUM-POSTAL-CODE  TO  F418-POSTCODE.                    
           MOVE    COMM8-CDE-ST           TO  F418-STCODE.                      
      *    MOVE    F410-NAME1             TO  F418-NAME.                        
      *    INSPECT F418-NAME REPLACING   ALL  LOW-VALUES BY ' '.                
      *    MOVE    F410-NRIC1             TO  F418-NRIC.                        
      *    INSPECT F418-NRIC REPLACING   ALL  LOW-VALUES BY ' '.                
           MOVE    SPACES                 TO  F418-NAME.                        
           MOVE    SPACES                 TO  F418-NRIC.                        
           MOVE    SPACES                 TO  F418-REMARKS.                     
           MOVE    WS-CURRENT-DATE        TO  F418-PROC-DATE.                   
           WRITE   BP13F418-REC.                                                
           ADD     1                      TO  WS-PUB-CNT.                       
                                                                                
       440-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *                                                               *         
      *                  OUTPUT TO F220 FILE                          *         
      *      (TEMPORARY FILE FOR PRINTING REPORT FOR RESETTLEMENT)    *         
      *                                                               *         
      *****************************************************************         
      *   ONLY OUTPUT THE RECORDS IF IT IS A RESETTLEMENT CASE                  
      *   I.E. CHECK THE IDP-SCH 'COS A RESETTLEMENT CASE MAY BE                
      *   RECOMMENDED BY RES DEPT. OR DUE ON ITS OWN AS PUBLIC OR               
      *   OTHERS. THE DATA IN IDP SCHEME COULD BE INACCURATE ON THE             
      *   LAST BYTE (FOR OLD CASES) WHEREBY 'O' IS CODED AS '0',                
      *   THEREFORE ONLY CHECK FIRST 2 BYTES.                                   
                                                                                
      **********************                                                    
       460-WRITE-F220.                                                          
      **********************                                                    
           MOVE F410-CDE-IDP-SCH  TO WS-IDP-SCH.                                
                                                                                
           IF WS-IDP-SCH2  = 'RH' OR 'RJ' OR 'RE'                               
              MOVE   SPACES               TO  BP13F220-REC                      
              MOVE   F410-NUM-REGN        TO  F220-NUM-REGN                     
              MOVE   F410-NAME1           TO  F220-NME-APPL1                    
              MOVE   F410-NAME2           TO  F220-NME-APPL2                    
              MOVE   F410-NAME3           TO  F220-NME-APPL3                    
              MOVE   F410-NAME4           TO  F220-NME-APPL4                    
              MOVE   COMM8-NUM-BLK        TO  F220-BLK-NO                       
              MOVE   COMM8-NME-STREET     TO  F220-STREET-NAME                  
              MOVE   COMM8-NUM-LEVEL      TO  F220-LEVEL-NO                     
              MOVE   COMM8-NUM-UNIT-MAIN  TO  F220-MAIN-UNIT                    
              MOVE   COMM8-NUM-UNIT-SUB   TO  F220-SUB-UNIT                     
              MOVE   COMM8-CDE-TYP-ACTLUSE TO F220-CDE-TYP-ACTUSE               
              MOVE   ZEROES               TO  F220-ACCEPT-REJECT-DATE           
              MOVE   F410-DTE-KEY-ISSUE   TO  F220-KEY-ISSUE-DATE               
              MOVE   'KI'                 TO  F220-ALLOC-STATUS                 
              MOVE   F410-CDE-ALLOC-SCH   TO  F220-ALLOC-SCH                    
              MOVE   F410-CDE-IDP-SCH     TO  F220-IDP-SCH                      
              WRITE  BP13F220-REC                                               
              ADD    1                    TO  WS-RES-CNT.                       
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************                                                  
       500-PRINT-CONTROL-LIST.                                                  
      ************************                                                  
           PERFORM 520-PRINT-HEADINGS THRU 520-EXIT.                            
           MOVE  SPACES      TO   PRT-LINE.                                     
           MOVE  WS-F410-CNT TO   L409-F410-CASES.                              
           WRITE PRT-LINE    FROM L409-DL-1  AFTER ADVANCING 2.                 
           MOVE  WS-PUB-CNT  TO   L409-PUB-CASES.                               
           WRITE PRT-LINE    FROM L409-DL-2  AFTER ADVANCING 2.                 
           MOVE  WS-RES-CNT  TO   L409-RES-CASES.                               
           WRITE PRT-LINE    FROM L409-DL-3  AFTER ADVANCING 2.                 
           MOVE  WS-TP-CNT   TO   L409-TP-CASES.                                
           WRITE PRT-LINE    FROM L409-DL-4  AFTER ADVANCING 2.                 
           MOVE  WS-SATP-CNT TO   L409-SATP-CASES.                              
           WRITE PRT-LINE    FROM L409-DL-5  AFTER ADVANCING 2.                 
           MOVE  WS-BB-CNT   TO   L409-BB-CASES.                                
           WRITE PRT-LINE    FROM L409-DL-6  AFTER ADVANCING 2.                 
           MOVE  WS-RP-CNT   TO   L409-RP-CASES.                                
           WRITE PRT-LINE    FROM L409-DL-7  AFTER ADVANCING 2.                 
           MOVE  WS-STD-CNT  TO   L409-STD-CASES.                               
           WRITE PRT-LINE    FROM L409-DL-8  AFTER ADVANCING 2.                 
           WRITE PRT-LINE    FROM L409-DL-9  AFTER ADVANCING 4.                 
           WRITE PRT-LINE    FROM L409-DL-10 AFTER ADVANCING 1.                 
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************                                                  
       520-PRINT-HEADINGS.                                                      
      ************************                                                  
           ADD   1               TO     WS-PAGE-CNT.                            
           MOVE  0               TO     WS-LINE-CNT.                            
           MOVE  WS-PAGE-CNT     TO     L409-PG-CNT.                            
           MOVE  WS-DD           TO     WS-EDITED-DD.                           
           MOVE  WS-MM           TO     WS-EDITED-MM.                           
           MOVE  WS-CC           TO     WS-EDITED-CC.                           
           MOVE  WS-YY           TO     WS-EDITED-YY.                           
           MOVE  WS-EDITED-DATE  TO     L409-DATE.                              
           MOVE  SPACES          TO     PRT-LINE.                               
           WRITE PRT-LINE        FROM   L409-HDG-1 AFTER PAGE.                  
           WRITE PRT-LINE        FROM   L409-HDG-3  AFTER ADVANCING 1.          
           WRITE PRT-LINE        FROM   L409-HDG-4  AFTER ADVANCING 2.          
           MOVE  SPACES          TO     PRT-LINE.                               
           WRITE PRT-LINE        AFTER  ADVANCING   12.                         
                                                                                
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************                                                  
       600-CLOSING-ROUTINE.                                                     
      ************************                                                  
           CLOSE BP13F410                                                       
                 BP13F418                                                       
                 BP13F220                                                       
                 BP13L409.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
