       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C595.                                                 
      *AUTHOR.        GD.                                                       
      *DATE-WRITTEN.  09/12/94.                                                 
      *REMARKS.                                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1.  TO UPDATE QUEUE TIME, QUEUE POSITION FOR        *                
      *        UNSUCCESSFUL APPLICANTS IN THE WAITING LIST,    *                
      *        AND WRITE A LOG RECORD.                         *                
      *                                                        *                
      *    2.  TO REPORT UPDATED BP13K520 SOC MASTER RECORDS.  *                
      *                                                        *                
      *    3.  THIS PROGRAM IS COPY FROM BP13C583 AND UPDATE   *                
      *        SHORTLIST-HHTY, CAT, PRIO-HHTY, PRIO-CAT        *                
      *        ANOTHER SIMILAR PROGRAM BP13C596                *                
      *                                                        *                
      *    INPUT FILES : BP13F520                              *                
      *                                                        *                
      *    I/O   FILE  : BP13K520                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE    DETAILS                          *                
      * ======   ==  ====== ================================== *                
      * SOC-PH9  GD  161294 CHGE F713-COHORT TO F520-TME-QUEUE *                
      * C950404  YPH 040495 STOP BP13K810 LOG                  *                
      * BP130030 MR  250995 UPDATE SHORTLIST-QUEUE BY ZNFT/CAT *                
      * BP130133 LSB 010696 UPDATE TO BP13K520 INSTEAD BP13K800*                
      *                     DELETE BP13K820                    *                
      * BP130652 NOR 200299 EXPAND F520 & K520 RECL 300 TO 500 *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
           SELECT BP13K520 ASSIGN TO BP13K520                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K520-NUM-REGN                                 
                  FILE STATUS  IS WS-K520-STATUS.                               
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F520                                                            
            RECORDING MODE  F                                                   
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F520.                                                           
                                                                                
       FD   BP13K520                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K520.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-K520-STATUS                 PIC 9(2)  VALUE ZEROS.            
           05  WS-F520-EOF-SW                 PIC X(1)  VALUE 'N'.              
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-F520-READ-CTR               PIC 9(5)  VALUE ZEROS.            
           05  WS-K520-READ-CTR               PIC 9(5)  VALUE ZEROS.            
           05  WS-K520-UPDATE-CTR             PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-TIME                 PIC X(8).                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-YY                  PIC 9(4).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
                                                                                
           PERFORM 2000-READ-F520      THRU 2000-EXIT.                          
                                                                                
           PERFORM 3000-PROCESS-F520                                            
              THRU 3000-EXIT                                                    
             UNTIL WS-F520-EOF-SW = 'Y'.                                        
                                                                                
           GO TO 9999-CLOSE-FILES.                                              
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-INITIALIZATION.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F520                                                 
                I-O    BP13K520.                                                
                                                                                
           IF WS-K520-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K520 ' WS-K520-STATUS                
              MOVE WS-K520-STATUS TO RETURN-CODE                                
              PERFORM 9999-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
           ACCEPT WS-SYSTEM-DATE      FROM DATE YYYYMMDD.                       
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-YY           TO WS-EDITED-YY.                          
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F520.                                                          
      ****************************************************************          
                                                                                
           READ BP13F520                                                        
              AT END                                                            
                 MOVE 'Y' TO WS-F520-EOF-SW                                     
                 GO  TO 2000-EXIT                                               
           END-READ.                                                            
                                                                                
           ADD  1                      TO WS-F520-READ-CTR.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F520.                                                       
      ****************************************************************          
                                                                                
           MOVE F520-NUM-REGN          TO K520-NUM-REGN.                        
                                                                                
           PERFORM 4000-PROCESS-K520 THRU 4000-EXIT.                            
                                                                                
           PERFORM 2000-READ-F520    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-PROCESS-K520.                                                       
      ****************************************************************          
                                                                                
           READ BP13K520   KEY IS K520-NUM-REGN.                                
                                                                                
           IF WS-K520-STATUS = ZEROS                                            
              NEXT SENTENCE                                                     
           ELSE                                                                 
           IF WS-K520-STATUS = 23                                               
              DISPLAY 'BP13K520 RECORD NOT FOUND ' K520-NUM-REGN                
              GO TO 4000-EXIT                                                   
           ELSE                                                                 
              DISPLAY 'BP13K520 READ ERROR, STATUS IS ' WS-K520-STATUS          
              DISPLAY 'BP13K520 RECORD KEY IS '         K520-NUM-REGN           
              GO TO 9999-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           ADD +1       TO WS-K520-READ-CTR.                                    
                                                                                
           PERFORM 4200-UPDATE-K520       THRU 4200-EXIT.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4200-UPDATE-K520.                                                        
      ****************************************************************          
                                                                                
           MOVE F520-NUM-SHORTLIST-PA-HHTY                                      
                                   TO K520-NUM-SHORTLIST-PA-HHTY.               
           MOVE F520-NUM-SHORTLIST-PA-CAT                                       
                                   TO K520-NUM-SHORTLIST-PA-CAT.                
           MOVE F520-NUM-SHORTLIST-PRIO-HHTY                                    
                                   TO K520-NUM-SHORTLIST-PRIO-HHTY.             
           MOVE F520-NUM-SHORTLIST-PRIO-CAT                                     
                                   TO K520-NUM-SHORTLIST-PRIO-CAT.              
                                                                                
           MOVE F520-NUM-SLIST-PA-ZNFT                                          
                                   TO K520-NUM-SLIST-PA-ZNFT.                   
           MOVE F520-NUM-SLIST-PA-ZNFT-CAT                                      
                                   TO K520-NUM-SLIST-PA-ZNFT-CAT.               
           MOVE F520-NUM-SLIST-PRIO-ZNFT                                        
                                   TO K520-NUM-SLIST-PRIO-ZNFT.                 
           MOVE F520-NUM-SLIST-PRIO-ZNFT-CAT                                    
                                   TO K520-NUM-SLIST-PRIO-ZNFT-CAT.             
                                                                                
           REWRITE BP13K520-REC.                                                
                                                                                
           IF WS-K520-STATUS = ZEROS                                            
              ADD  +1                  TO WS-K520-UPDATE-CTR                    
           ELSE                                                                 
              DISPLAY 'BP13K520 REWRITE ERROR,STATUS IS ' WS-K520-STATUS        
              DISPLAY 'BP13K520 RECORD KEY IS '           K520-NUM-REGN         
              GO TO 9999-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-FILES.                                                        
      ****************************************************************          
                                                                                
           CLOSE BP13F520                                                       
                 BP13K520.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C595 CONTROL TOTALS============*'.          
           DISPLAY '*======== UPDATE UNSUCC QUEUE POSITION ========*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F520 RECORDS READ      = ' WS-F520-READ-CTR.            
           DISPLAY 'BP13K520 RECORDS READ      = ' WS-K520-READ-CTR.            
           DISPLAY 'BP13K520 RECORDS UPDATED   = ' WS-K520-UPDATE-CTR.          
           DISPLAY SPACES.                                                      
           DISPLAY '*======================================*'.                  
                                                                                
           IF WS-K520-STATUS NOT = 00                                           
              DISPLAY 'BP13K520 CLOSE ERROR, STATUS IS ' WS-K520-STATUS         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
