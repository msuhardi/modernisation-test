      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13CA02.                                                 
      *AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  13/02/2006.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      * OBJECTIVE :                                                   *         
      *  1. PRINT REPORT ON FLATLIST FOR GRO & PRIORITY               *         
      *                 BY PRJCT/BLOCK/RANDOM.                        *         
      *---------------------------------------------------------------*         
      * CHG REF  BY    DATE    DESCRIPTION                            *         
      * -------- ---  -------- -----------                            *         
      * BP132789 LMS  13/02/06 NEW PROGRAM.                           *         
      * BP133514 ESA1 05/01/09 ADD NEW REPORT (BP13LA2A) FOR PRIORITY *         
      *                        FLAT QUOTA                             *         
      * BP133932 PCL3 03/09/10 TO CATER FOR HANDLING FT OF EC OR DBSS *         
      *===============================================================*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F100  ASSIGN       TO BP13F100.                           
           SELECT BP13FA25  ASSIGN       TO BP13FA25.                           
           SELECT BP13LA02  ASSIGN       TO BP13LA02.                           
           SELECT BP13LA2A  ASSIGN       TO BP13LA2A.                           
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F100                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       01  BP13F100-REC.                                                        
           05  F100-NUM-PRJCT          PIC X(03).                               
           05  F100-NUM-BLOCK          PIC X(05).                               
           05  F100-NUM-LEVEL          PIC X(02).                               
           05  F100-NUM-UNIT           PIC X(07).                               
           05  FILLER                  PIC X(01).                               
           05  F100-NUM-RANDOM         PIC X(28).                               
           05  F100-NUM-PRIO           PIC X(01).                               
           05  F100-NUM-FT-BALLOT      PIC X(02).                               
           05  F100-NUM-PRIO-UPDATE    PIC X(01).                               
           05  FILLER                  PIC X(21).                               
           05  F100-NUM-TYP-ACTUSE     PIC X(02).                               
           05  FILLER                  PIC X(727).                              
                                                                                
       FD  BP13FA25                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 80 CHARACTERS.                                       
       COPY BP13FA25.                                                           
                                                                                
       FD  BP13LA02                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01 BP13LA02-REC            PIC X(132).                                   
                                                                                
       FD  BP13LA2A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01 BP13LA2A-REC            PIC X(132).                                   
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
      *------------------------------*                                          
      *   WORKING STORAGE VARIABLES  *                                          
      *------------------------------*                                          
       01 WS-FA25-EOF             PIC X(01) VALUE 'N'.                          
          88 FA25-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-F100-EOF             PIC X(01) VALUE 'N'.                          
          88 F100-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-FA25-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-F100-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-LA02-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-LA2A-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-EDT-DATE             PIC X(10) VALUE SPACES.                       
       01 WS-CUR-DATE             PIC X(08) VALUE SPACES.                       
                                                                                
       01 WS-EDIT1                PIC Z(07)9.                                   
                                                                                
       01 WS-LINE-CNT             PIC 9(05) VALUE ZEROES.                       
       01 WS-LINE-CNT2            PIC 9(05) VALUE ZEROES.                       
       01 WS-PAGE-CNT             PIC 9(06) VALUE ZEROES.                       
       01 WS-PAGE-CNT2            PIC 9(06) VALUE ZEROES.                       
       01 WS-PRINT-UNIT-CNT       PIC 9(03) VALUE ZEROES.                       
       01 WS-PRINT-UNIT-CNT2      PIC 9(03) VALUE ZEROES.                       
       01 WS-MAX-LINES            PIC 9(05) VALUE ZEROES.                       
       01 WS-MAX-LINES2           PIC 9(05) VALUE ZEROES.                       
       01 WS-MAX-CTR              PIC 9(05) VALUE ZEROES.                       
       01 WS-SLNO                 PIC 9(05) VALUE ZEROES.                       
       01 WS-SLNO2                PIC 9(05) VALUE ZEROES.                       
       01 WS-PREV-FA25-KEY-FLD    PIC X(10) VALUE SPACES.                       
       01 WS-PREV-FA25-KEY-FLD1   PIC X(10) VALUE SPACES.                       
                                                                                
       01 WS-F100-KEY-FLD.                                                      
          05 WS-F100-PRJCT        PIC X(03).                                    
          05 WS-F100-BLOCK        PIC X(05).                                    
          05 WS-F100-FT           PIC X(02).                                    
                                                                                
       01 WS-FA25-KEY-FLD.                                                      
          05 WS-FA25-PRJCT        PIC X(03).                                    
          05 WS-FA25-BLOCK        PIC X(05).                                    
          05 WS-FA25-FT           PIC X(02).                                    
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 LA02-HEADER1.                                                      
             10 FILLER            PIC X(08) VALUE 'BP13LA02'.                   
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(08) VALUE 'HDBCAT 3'.                   
             10 FILLER            PIC X(29) VALUE SPACES.                       
             10 FILLER            PIC X(20) VALUE                               
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER            PIC X(23) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'DATE : '.                    
             10 LA02-DATE         PIC X(12) VALUE SPACES.                       
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'PAGE : '.                    
             10 LA02-PAGE         PIC Z(05)9.                                   
          05 LA02-HEADER2.                                                      
             10 FILLER            PIC X(45) VALUE SPACES.                       
             10 FILLER            PIC X(35) VALUE                               
                '    UNIT SET ASIDE FOR PRIORITY    '.                          
             10 FILLER            PIC X(45) VALUE SPACES.                       
          05 LA02-HEADER3.                                                      
             10 FILLER            PIC X(12) VALUE 'PRJCT     : '.               
             10 LA02-PROJECT      PIC X(03) VALUE SPACES.                       
          05 LA02-HEADER4.                                                      
             10 FILLER            PIC X(12) VALUE 'BLOCK     : '.               
             10 LA02-BLOCK        PIC X(05) VALUE SPACES.                       
          05 LA02-HEADER5.                                                      
             10 LA02-FT-HDR5      PIC X(12) VALUE 'FLAT TYPE : '.               
             10 LA02-FT           PIC X(05) VALUE SPACES.                       
          05 LA02-HEADER6.                                                      
             10 FILLER            PIC X(12) VALUE 'UNITS     : '.               
             10 LA02-UNITS        PIC Z9.                                       
          05 LA02-HEADER7.                                                      
             10 LA02-FT-HDR7      PIC X(41) VALUE                               
                ' S/NO    PRJCT  BLOCK   UNIT       FT'.                        
             10 FILLER            PIC X(07) VALUE                               
                ' RANDOM'.                                                      
             10 FILLER            PIC X(25) VALUE SPACES.                       
          05 LA02-HEADER8.                                                      
             10 FILLER            PIC X(132) VALUE ALL '-'.                     
          05 LA02-DETAILS.                                                      
             10 LA02-SLNO         PIC Z(4)9.                                    
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 LA02-PRJCT        PIC X(03) VALUE SPACES.                       
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 LA02-BLK          PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LA02-UNIT         PIC X(10) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LA02-FLAT-TYPE    PIC X(06) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LA02-RANDOM       PIC X(28) VALUE SPACES.                       
                                                                                
       01 WS-REPORT-VARS2.                                                      
          05 LA2A-HEADER1.                                                      
             10 FILLER            PIC X(08) VALUE 'BP13LA2A'.                   
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(08) VALUE 'HDBCAT 3'.                   
             10 FILLER            PIC X(29) VALUE SPACES.                       
             10 FILLER            PIC X(20) VALUE                               
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER            PIC X(23) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'DATE : '.                    
             10 LA2A-DATE         PIC X(12) VALUE SPACES.                       
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'PAGE : '.                    
             10 LA2A-PAGE         PIC Z(05)9.                                   
          05 LA2A-HEADER2.                                                      
             10 FILLER            PIC X(45) VALUE SPACES.                       
             10 FILLER            PIC X(40) VALUE                               
                'FLAT LIST BY PRJCT/BLOCK/FT FOR PRIORITY'.                     
             10 FILLER            PIC X(40) VALUE SPACES.                       
          05 LA2A-HEADER3.                                                      
             10 FILLER            PIC X(12) VALUE 'PRJCT     : '.               
             10 LA2A-PROJECT      PIC X(03) VALUE SPACES.                       
          05 LA2A-HEADER4.                                                      
             10 FILLER            PIC X(12) VALUE 'BLOCK     : '.               
             10 LA2A-BLOCK        PIC X(05) VALUE SPACES.                       
          05 LA2A-HEADER5.                                                      
             10 LA2A-FT-HDR5      PIC X(12) VALUE 'FLAT TYPE : '.               
             10 LA2A-FT           PIC X(05) VALUE SPACES.                       
          05 LA2A-HEADER6.                                                      
             10 FILLER            PIC X(12) VALUE 'UNITS     : '.               
             10 LA2A-UNITS        PIC Z9.                                       
          05 LA2A-HEADER7.                                                      
             10 LA2A-FT-HDR7      PIC X(41) VALUE                               
                ' S/NO    PRJCT  BLOCK   UNIT       FT'.                        
             10 FILLER            PIC X(07) VALUE                               
                ' RANDOM'.                                                      
             10 FILLER            PIC X(25) VALUE SPACES.                       
          05 LA2A-HEADER8.                                                      
             10 FILLER            PIC X(132) VALUE ALL '-'.                     
          05 LA2A-DETAILS.                                                      
             10 LA2A-SLNO         PIC Z(4)9.                                    
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 LA2A-PRJCT        PIC X(03) VALUE SPACES.                       
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 LA2A-BLK          PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LA2A-UNIT         PIC X(10) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LA2A-FLAT-TYPE    PIC X(06) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LA2A-RANDOM       PIC X(28) VALUE SPACES.                       
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13FA25      THRU 2000-EXIT.                      
           PERFORM 3000-READ-BP13F100      THRU 3000-EXIT.                      
           PERFORM 4000-PROCESS-DATA       THRU 4000-EXIT                       
                            UNTIL FA25-END-OF-FILE  AND                         
                                  F100-END-OF-FILE.                             
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN  INPUT  BP13FA25                                                
                 I-O    BP13F100                                                
                 OUTPUT BP13LA02                                                
                        BP13LA2A.                                               
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
           MOVE  '55'         TO WS-MAX-LINES.                                  
           MOVE  '60'         TO WS-LINE-CNT.                                   
           MOVE  '110'        TO WS-MAX-LINES2.                                 
           MOVE  '120'        TO WS-LINE-CNT2.                                  
           MOVE WS-EDT-DATE   TO LA02-DATE                                      
                                 LA2A-DATE.                                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13FA25.                                                      
      *---------------------------------------------------------------*         
           READ BP13FA25 AT END                                                 
                          MOVE 'Y' TO WS-FA25-EOF                               
                          MOVE HIGH-VALUES TO WS-FA25-KEY-FLD                   
                          GO TO 2000-EXIT.                                      
                                                                                
           MOVE FA25-NUM-PRJCT      TO WS-FA25-PRJCT.                           
           MOVE FA25-NUM-BLK        TO WS-FA25-BLOCK.                           
           MOVE FA25-NUM-FLAT-TYPE  TO WS-FA25-FT.                              
                                                                                
           ADD    1    TO WS-FA25-READ-CNT.                                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-READ-BP13F100.                                                      
      *---------------------------------------------------------------*         
           READ BP13F100 AT END                                                 
                          MOVE 'Y' TO WS-F100-EOF                               
                          MOVE HIGH-VALUES TO WS-F100-KEY-FLD                   
                          GO TO 3000-EXIT.                                      
                                                                                
           MOVE F100-NUM-PRJCT      TO WS-F100-PRJCT.                           
           MOVE F100-NUM-BLOCK      TO WS-F100-BLOCK.                           
           MOVE F100-NUM-TYP-ACTUSE TO WS-F100-FT.                              
                                                                                
           ADD    1    TO WS-F100-READ-CNT.                                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           EVALUATE TRUE                                                        
             WHEN WS-FA25-KEY-FLD  =  WS-F100-KEY-FLD                           
               MOVE  ZEROES   TO WS-PRINT-UNIT-CNT                              
               MOVE  ZEROES   TO WS-PRINT-UNIT-CNT2                             
               PERFORM 4100-PROCESS-F100   THRU 4100-EXIT                       
                   UNTIL (WS-F100-KEY-FLD  NOT =  WS-FA25-KEY-FLD)  OR          
                           (F100-END-OF-FILE)                                   
               PERFORM 2000-READ-BP13FA25  THRU 2000-EXIT                       
             WHEN WS-FA25-KEY-FLD  <  WS-F100-KEY-FLD                           
               PERFORM 2000-READ-BP13FA25  THRU 2000-EXIT                       
             WHEN WS-FA25-KEY-FLD  >  WS-F100-KEY-FLD                           
               PERFORM 3000-READ-BP13F100  THRU 3000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-PROCESS-F100.                                                       
      *---------------------------------------------------------------*         
           IF WS-PRINT-UNIT-CNT < FA25-NUM-FLAT-GRO                             
              ADD 1     TO WS-PRINT-UNIT-CNT                                    
              PERFORM 5000-PRINT-REPORT  THRU 5000-EXIT                         
              MOVE 'G' TO F100-NUM-PRIO                                         
              MOVE 'G' TO F100-NUM-PRIO-UPDATE                                  
              REWRITE BP13F100-REC                                              
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13F100   THRU 3000-EXIT.                         
                                                                                
           IF (WS-PRINT-UNIT-CNT + 1 > FA25-NUM-FLAT-GRO)                       
             IF (WS-PRINT-UNIT-CNT2 < FA25-NUM-FLAT-PRIO) AND                   
                (WS-FA25-KEY-FLD  =  WS-F100-KEY-FLD)                           
                ADD 1     TO WS-PRINT-UNIT-CNT2                                 
                PERFORM 6000-PRINT-REPORT2 THRU 6000-EXIT                       
                MOVE 'P' TO F100-NUM-PRIO                                       
                MOVE 'P' TO F100-NUM-PRIO-UPDATE                                
               REWRITE BP13F100-REC                                             
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-PRINT-REPORT.                                                       
      *---------------------------------------------------------------*         
           IF (WS-LINE-CNT  >  WS-MAX-LINES)   OR                               
              (WS-FA25-KEY-FLD  NOT =  WS-PREV-FA25-KEY-FLD)                    
              MOVE  SPACES      TO BP13LA02-REC                                 
              IF (WS-FA25-KEY-FLD  NOT =  WS-PREV-FA25-KEY-FLD)                 
                 MOVE WS-FA25-KEY-FLD      TO WS-PREV-FA25-KEY-FLD              
                 MOVE  ZEROES              TO WS-SLNO                           
                                              WS-PAGE-CNT                       
              END-IF                                                            
              ADD      1        TO WS-PAGE-CNT                                  
              MOVE     0        TO WS-LINE-CNT                                  
              MOVE WS-PAGE-CNT  TO LA02-PAGE                                    
              MOVE FA25-NUM-PRJCT       TO LA02-PROJECT                         
              MOVE FA25-NUM-BLK         TO LA02-BLOCK                           
              MOVE FA25-NUM-FLAT-TYPE   TO LA02-FT                              
              MOVE FA25-NUM-FLAT-GRO    TO LA02-UNITS                           
                                                                                
              IF FA25-NUM-PRJCT(1:1) = 'E'                                      
                 MOVE SPACES                           TO LA02-FT-HDR5          
                                                          LA02-FT               
                 MOVE ' S/NO    PRJCT  BLOCK   UNIT  ' TO LA02-FT-HDR7          
                                                                                
              ELSE                                                              
                 MOVE 'FLAT TYPE : '                   TO LA02-FT-HDR5          
                 MOVE ' S/NO    PRJCT  BLOCK   UNIT       FT'                   
                                                       TO LA02-FT-HDR7          
              END-IF                                                            
                                                                                
              WRITE BP13LA02-REC     FROM LA02-HEADER1 AFTER PAGE               
              WRITE BP13LA02-REC     FROM LA02-HEADER2                          
              WRITE BP13LA02-REC     FROM LA02-HEADER3                          
              WRITE BP13LA02-REC     FROM LA02-HEADER4                          
              WRITE BP13LA02-REC     FROM LA02-HEADER5                          
              WRITE BP13LA02-REC     FROM LA02-HEADER6                          
              WRITE BP13LA02-REC     FROM LA02-HEADER7 AFTER 2                  
              WRITE BP13LA02-REC     FROM LA02-HEADER8                          
              ADD     11        TO WS-LINE-CNT                                  
           END-IF.                                                              
                                                                                
           MOVE   SPACES        TO LA02-DETAILS.                                
           INITIALIZE   LA02-DETAILS.                                           
           ADD    1             TO WS-SLNO.                                     
           MOVE  WS-SLNO        TO LA02-SLNO.                                   
           MOVE F100-NUM-PRJCT         TO LA02-PRJCT.                           
           MOVE F100-NUM-BLOCK         TO LA02-BLK.                             
           MOVE F100-NUM-TYP-ACTUSE    TO LA02-FLAT-TYPE.                       
           MOVE F100-NUM-RANDOM        TO LA02-RANDOM.                          
           STRING F100-NUM-LEVEL  '-'                                           
                  F100-NUM-UNIT                                                 
                  DELIMITED BY SIZE  INTO LA02-UNIT.                            
                                                                                
           MOVE     SPACES             TO BP13LA02-REC.                         
           WRITE BP13LA02-REC        FROM LA02-DETAILS.                         
           ADD    1             TO WS-LINE-CNT.                                 
           ADD    1             TO WS-LA02-WRITE-CNT.                           
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6000-PRINT-REPORT2.                                                      
      *---------------------------------------------------------------*         
           IF (WS-LINE-CNT2 >  WS-MAX-LINES2) OR                                
              (WS-FA25-KEY-FLD  NOT =  WS-PREV-FA25-KEY-FLD1)                   
              MOVE  SPACES      TO BP13LA2A-REC                                 
              IF (WS-FA25-KEY-FLD  NOT =  WS-PREV-FA25-KEY-FLD1)                
                 MOVE WS-FA25-KEY-FLD      TO WS-PREV-FA25-KEY-FLD              
                                              WS-PREV-FA25-KEY-FLD1             
                 MOVE  ZEROES              TO WS-SLNO2                          
                                              WS-PAGE-CNT2                      
              END-IF                                                            
                                                                                
              ADD      1        TO WS-PAGE-CNT2                                 
              MOVE     0        TO WS-LINE-CNT2                                 
              MOVE WS-PAGE-CNT2 TO LA2A-PAGE                                    
              MOVE FA25-NUM-PRJCT       TO LA2A-PROJECT                         
              MOVE FA25-NUM-BLK         TO LA2A-BLOCK                           
              MOVE FA25-NUM-FLAT-TYPE   TO LA2A-FT                              
              MOVE FA25-NUM-FLAT-PRIO   TO LA2A-UNITS                           
                                                                                
              IF FA25-NUM-PRJCT(1:1) = 'E'                                      
                 MOVE SPACES                           TO LA2A-FT-HDR5          
                                                          LA2A-FT               
                 MOVE ' S/NO    PRJCT  BLOCK   UNIT  ' TO LA2A-FT-HDR7          
                                                                                
              ELSE                                                              
                 MOVE 'FLAT TYPE : '                   TO LA2A-FT-HDR5          
                 MOVE ' S/NO    PRJCT  BLOCK   UNIT       FT'                   
                                                       TO LA2A-FT-HDR7          
              END-IF                                                            
                                                                                
              WRITE BP13LA2A-REC     FROM LA2A-HEADER1 AFTER PAGE               
              WRITE BP13LA2A-REC     FROM LA2A-HEADER2                          
              WRITE BP13LA2A-REC     FROM LA2A-HEADER3                          
              WRITE BP13LA2A-REC     FROM LA2A-HEADER4                          
              WRITE BP13LA2A-REC     FROM LA2A-HEADER5                          
              WRITE BP13LA2A-REC     FROM LA2A-HEADER6                          
              WRITE BP13LA2A-REC     FROM LA2A-HEADER7 AFTER 2                  
              WRITE BP13LA2A-REC     FROM LA2A-HEADER8                          
              ADD     11        TO WS-LINE-CNT2                                 
                                                                                
           END-IF.                                                              
                                                                                
           MOVE   SPACES        TO LA2A-DETAILS.                                
           INITIALIZE   LA2A-DETAILS.                                           
           ADD    1             TO WS-SLNO2.                                    
           MOVE  WS-SLNO2       TO LA2A-SLNO.                                   
           MOVE F100-NUM-PRJCT         TO LA2A-PRJCT.                           
           MOVE F100-NUM-BLOCK         TO LA2A-BLK.                             
           MOVE F100-NUM-TYP-ACTUSE    TO LA2A-FLAT-TYPE.                       
           MOVE F100-NUM-RANDOM        TO LA2A-RANDOM.                          
           STRING F100-NUM-LEVEL  '-'                                           
                  F100-NUM-UNIT                                                 
                  DELIMITED BY SIZE  INTO LA2A-UNIT.                            
                                                                                
           MOVE     SPACES             TO BP13LA2A-REC.                         
           WRITE BP13LA2A-REC        FROM LA2A-DETAILS.                         
           ADD    1             TO WS-LA2A-WRITE-CNT.                           
                                                                                
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13CA02       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-FA25-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13FA25  : ' WS-EDIT1.                
           MOVE WS-F100-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13F100  : ' WS-EDIT1.                
           MOVE WS-LA02-WRITE-CNT  TO WS-EDIT1.                                 
           DISPLAY 'NO OF RECS WRITTEN TO BP13LA02 : ' WS-EDIT1.                
           MOVE WS-LA2A-WRITE-CNT  TO WS-EDIT1.                                 
           DISPLAY 'NO OF RECS WRITTEN TO BP13LA2A : ' WS-EDIT1.                
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13FA25                                                     
                   BP13F100                                                     
                   BP13LA02                                                     
                   BP13LA2A.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
