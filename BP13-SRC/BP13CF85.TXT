       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF85.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  22/04/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  GENERATE FILE OF DETAILED GRO APPLICATIONS *            
      *                                                            *            
      * INPUT FILE   :  BP13F740 - GRO INFO                        *            
      *                 BP13K825 - OCCUPIER INFO                   *            
      *                 BP13K022 - OIC INFO                        *            
      *                 BP13K857 - EMAIL INFO                      *            
      *                 BP13K740 - PA INFO                         *            
      * OUTPUT FILE  :  BP13LF85 - DETAILED GRO APPLICATIONS       *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134903  SMR2  22042013  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K825 ASSIGN TO BP13K825                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K825-KEY-FLD                               
                  FILE STATUS     IS WS-K825-STATUS.                            
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
           SELECT BP13K857 ASSIGN TO BP13K857                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K857-KEY-FLD                               
                  FILE STATUS     IS WS-K857-STATUS.                            
                                                                                
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K740-KEY-FLD                               
                  FILE STATUS     IS WS-K740-STATUS.                            
                                                                                
           SELECT BP13LF85 ASSIGN TO BP13LF85.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K740.                                                           
                                                                                
       FD   BP13LF85                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       01   BP13LF85-REC           PIC X(300).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K825-STATUS      PIC 9(2)    VALUE ZEROES.                    
           05  WS-K022-STATUS      PIC 9(2)    VALUE ZEROES.                    
           05  WS-K857-STATUS      PIC 9(2)    VALUE ZEROES.                    
           05  WS-K740-STATUS      PIC 9(2)    VALUE ZEROES.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ        PIC 9(6)    VALUE ZEROES.                    
           05  WS-K825-FND-CNT     PIC 9(6)    VALUE ZEROES.                    
           05  WS-K022-FND-CNT     PIC 9(6)    VALUE ZEROES.                    
           05  WS-K857-FND-CNT     PIC 9(6)    VALUE ZEROES.                    
           05  WS-K740-FND-CNT     PIC 9(6)    VALUE ZEROES.                    
           05  WS-K825-NTFND-CNT   PIC 9(6)    VALUE ZEROES.                    
           05  WS-K022-NTFND-CNT   PIC 9(6)    VALUE ZEROES.                    
           05  WS-K857-NTFND-CNT   PIC 9(6)    VALUE ZEROES.                    
           05  WS-K740-NTFND-CNT   PIC 9(6)    VALUE ZEROES.                    
           05  WS-LF85-WRITE       PIC 9(6)    VALUE ZEROES.                    
           05  WS-SNO-CNT          PIC 9(6)    VALUE ZEROES.                    
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF         PIC X(1)    VALUE 'N'.                       
           05  WS-K825-FND         PIC X(1)    VALUE 'N'.                       
           05  WS-K022-FND         PIC X(1)    VALUE 'N'.                       
           05  WS-K740-FND         PIC X(1)    VALUE 'N'.                       
                                                                                
       01  WS-DETAILS.                                                          
           05  WS-SNO              PIC ZZZZZ9  VALUE ZEROES.                    
           05  FILLER1             PIC X(01)   VALUE SPACES.                    
           05  WS-NT1              PIC X(03)   VALUE SPACES.                    
           05  FILLER2             PIC X(01)   VALUE SPACES.                    
           05  WS-FT               PIC X(02)   VALUE SPACES.                    
           05  FILLER3             PIC X(01)   VALUE SPACES.                    
           05  WS-REG-NUM          PIC X(08)   VALUE SPACES.                    
           05  FILLER4             PIC X(01)   VALUE SPACES.                    
           05  WS-NAME1            PIC X(66)   VALUE SPACES.                    
           05  FILLER5             PIC X(01)   VALUE SPACES.                    
           05  WS-NRIC1            PIC X(09)   VALUE SPACES.                    
           05  FILLER6             PIC X(01)   VALUE SPACES.                    
           05  WS-CAT              PIC X(01)   VALUE SPACES.                    
           05  FILLER7             PIC X(01)   VALUE SPACES.                    
           05  WS-HH               PIC X(01)   VALUE SPACES.                    
           05  FILLER8             PIC X(01)   VALUE SPACES.                    
           05  WS-BAL-HH           PIC X(01)   VALUE SPACES.                    
           05  FILLER9             PIC X(01)   VALUE SPACES.                    
           05  WS-WARD             PIC X(03)   VALUE SPACES.                    
           05  FILLER10            PIC X(01)   VALUE SPACES.                    
           05  WS-RANK             PIC X(02)   VALUE SPACES.                    
           05  FILLER11            PIC X(01)   VALUE SPACES.                    
           05  WS-MCPS             PIC X(01)   VALUE SPACES.                    
           05  FILLER12            PIC X(01)   VALUE SPACES.                    
           05  WS-GRO-REGN         PIC X(08)   VALUE SPACES.                    
           05  FILLER13            PIC X(01)   VALUE SPACES.                    
           05  WS-GRO-QUEUE        PIC X(05)   VALUE SPACES.                    
           05  FILLER14            PIC X(01)   VALUE SPACES.                    
           05  WS-PA-REGN          PIC X(08)   VALUE SPACES.                    
           05  FILLER15            PIC X(01)   VALUE SPACES.                    
           05  WS-PA-QUEUE         PIC X(05)   VALUE SPACES.                    
           05  FILLER16            PIC X(01)   VALUE SPACES.                    
           05  WS-OIC              PIC X(05)   VALUE SPACES.                    
           05  FILLER17            PIC X(01)   VALUE SPACES.                    
           05  WS-GRO-ALLOC        PIC X(03)   VALUE SPACES.                    
           05  FILLER18            PIC X(01)   VALUE SPACES.                    
           05  WS-PA-ALLOC         PIC X(03)   VALUE SPACES.                    
           05  FILLER19            PIC X(01)   VALUE SPACES.                    
           05  WS-EMAIL            PIC X(01)   VALUE SPACES.                    
           05  FILLER20            PIC X(01)   VALUE SPACES.                    
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-FILES      THRU 3000-EXIT                       
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F740                                                 
                       BP13K825                                                 
                       BP13K022                                                 
                       BP13K857                                                 
                       BP13K740                                                 
                OUTPUT BP13LF85.                                                
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K825 OPEN ERROR, STATUS IS ' WS-K825-STATUS          
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K022 OPEN ERROR, STATUS IS ' WS-K022-STATUS          
              MOVE WS-K022-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K857 OPEN ERROR, STATUS IS ' WS-K857-STATUS          
              MOVE WS-K857-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K740 OPEN ERROR, STATUS IS ' WS-K740-STATUS          
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO BP13LF85-REC.                     
           INITIALIZE                         BP13LF85-REC.                     
                                                                                
           STRING 'S/NO;NT;FT;REGN NO;NAME;NRIC NO;CAT;HH;BAL HH;'              
                  'WARD;RANK;MCPS;GRO REGN NO;GRO QUEUE;PA REGN NO;'            
                  'PA QUEUE;OIC;GRO ALLOC;PA ALLOC;EMAIL;'                      
                   DELIMITED BY SIZE INTO BP13LF85-REC.                         
           WRITE BP13LF85-REC.                                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
            READ BP13F740 AT END                                                
                 MOVE 'Y'                  TO WS-F740-EOF                       
                 GO TO 2000-EXIT.                                               
            ADD 1                          TO WS-F740-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           PERFORM 3100-INITIALIZE-REC     THRU 3100-EXIT.                      
                                                                                
           ADD 1                           TO WS-SNO-CNT.                       
                                                                                
           MOVE WS-SNO-CNT                 TO WS-SNO.                           
           MOVE F740-CDE-NT1               TO WS-NT1.                           
           MOVE F740-CDE-FLAT-TYPE         TO WS-FT.                            
           MOVE F740-NUM-REGN              TO WS-REG-NUM.                       
           MOVE F740-NUM-NRIC1             TO WS-NRIC1.                         
           MOVE F740-CDE-CATEGORY          TO WS-CAT.                           
           MOVE F740-CDE-HOUSEHOLD         TO WS-HH.                            
           MOVE F740-CDE-BALLOT-HOUSEHOLD  TO WS-BAL-HH.                        
           MOVE F740-NUM-WARD              TO WS-WARD.                          
           MOVE F740-NUM-GRO-RANK          TO WS-RANK.                          
           MOVE F740-NUM-REGN              TO WS-GRO-REGN.                      
           MOVE F740-NUM-NT-FT-QUEUE       TO WS-GRO-QUEUE.                     
           MOVE F740-CDE-ALLOC-SCH         TO WS-GRO-ALLOC.                     
                                                                                
           IF (F740-NUM-MCPS NOT = SPACES AND LOW-VALUES) AND                   
              (F740-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T')                       
              MOVE 'Y'                     TO WS-MCPS                           
           ELSE                                                                 
              MOVE 'N'                     TO WS-MCPS                           
           END-IF.                                                              
                                                                                
           IF F740-NUM-NRIC1 NOT = SPACES AND LOW-VALUES                        
              MOVE F740-NUM-REGN           TO K825-NUM-REGN                     
              MOVE F740-NUM-NRIC1          TO K825-NUM-NRIC                     
              PERFORM 3200-READ-BP13K825   THRU 3200-EXIT                       
              IF WS-K825-FND = 'Y'                                              
                 MOVE K825-NME-OCCP        TO WS-NAME1                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F740-NUM-REGN              TO K022-NUM-REGN.                    
           PERFORM 3300-READ-BP13K022      THRU 3300-EXIT.                      
           IF WS-K022-FND = 'Y'                                                 
              MOVE K022-NUM-OIC            TO WS-OIC                            
           END-IF.                                                              
                                                                                
           MOVE F740-NUM-REGN              TO K857-NUM-REGN.                    
           PERFORM 3400-READ-BP13K857      THRU 3400-EXIT.                      
                                                                                
           PERFORM 3500-READ-BP13K740      THRU 3500-EXIT.                      
           IF WS-K740-FND = 'Y'                                                 
              MOVE K740-NUM-REGN           TO WS-PA-REGN                        
              MOVE K740-NUM-NT-FT-QUEUE    TO WS-PA-QUEUE                       
              MOVE K740-CDE-ALLOC-SCH      TO WS-PA-ALLOC                       
           END-IF.                                                              
                                                                                
           WRITE BP13LF85-REC              FROM WS-DETAILS.                     
           ADD 1                           TO WS-LF85-WRITE.                    
                                                                                
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-INITIALIZE-REC.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES                     TO WS-DETAILS.                       
                                                                                
           MOVE ';'                        TO FILLER1    FILLER2                
                                              FILLER3    FILLER4                
                                              FILLER5    FILLER6                
                                              FILLER7    FILLER8                
                                              FILLER9    FILLER10               
                                              FILLER11   FILLER12               
                                              FILLER13   FILLER14               
                                              FILLER15   FILLER16               
                                              FILLER17   FILLER18               
                                              FILLER19   FILLER20.              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3200-READ-BP13K825.                                                      
      *-------------------------------------------------------------            
           READ BP13K825.                                                       
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K825-FND                       
              ADD 1                        TO WS-K825-FND-CNT                   
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K825-FND                       
              ADD 1                        TO WS-K825-NTFND-CNT                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OF BP13K825. STATUS IS '                   
                      WS-K825-STATUS                                            
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3300-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              MOVE 'Y'                     TO WS-K022-FND                       
              ADD 1                        TO WS-K022-FND-CNT                   
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K022-FND                       
              ADD 1                        TO WS-K022-NTFND-CNT                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OF BP13K022. STATUS IS '                   
                      WS-K022-STATUS                                            
              MOVE WS-K022-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3400-READ-BP13K857.                                                      
      *-------------------------------------------------------------            
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
           WHEN ZEROES                                                          
              IF K857-NUM-EMAIL NOT = SPACES AND LOW-VALUES                     
                 MOVE 'Y'                  TO WS-EMAIL                          
                 ADD 1                     TO WS-K857-FND-CNT                   
              ELSE                                                              
                 MOVE 'N'                  TO WS-EMAIL                          
                 ADD 1                     TO WS-K857-NTFND-CNT                 
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO WS-EMAIL                          
              ADD 1                        TO WS-K857-NTFND-CNT                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OF BP13K857. STATUS IS '                   
                      WS-K857-STATUS                                            
              MOVE WS-K857-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3500-READ-BP13K740.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                     TO K740-KEY-FLD.                     
           MOVE F740-KEY-FLD               TO K740-KEY-FLD.                     
           MOVE 'G'                        TO K740-KEY-FLD(28:1).               
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K740-FND                       
              ADD 1                        TO WS-K740-FND-CNT                   
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K740-FND                       
              ADD 1                        TO WS-K740-NTFND-CNT                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OF BP13K740. STATUS IS '                   
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '*============== BP13CF85 ==============*'.                  
           DISPLAY '  NO. OF F740 REC READ       =  ' WS-F740-READ.             
           DISPLAY '  NO. OF K825 REC FOUND      =  ' WS-K825-FND-CNT.          
           DISPLAY '  NO. OF K825 REC NOT FOUND  =  ' WS-K825-NTFND-CNT.        
           DISPLAY '  NO. OF K022 REC FOUND      =  ' WS-K022-FND-CNT.          
           DISPLAY '  NO. OF K022 REC NOT FOUND  =  ' WS-K022-NTFND-CNT.        
           DISPLAY '  NO. OF K857 REC FOUND      =  ' WS-K857-FND-CNT.          
           DISPLAY '  NO. OF K857 REC NOT FOUND  =  ' WS-K857-NTFND-CNT.        
           DISPLAY '  NO. OF K740 REC FOUND      =  ' WS-K740-FND-CNT.          
           DISPLAY '  NO. OF K740 REC NOT FOUND  =  ' WS-K740-NTFND-CNT.        
           DISPLAY '  NO. OF CASES WRITTEN       =  ' WS-LF85-WRITE.            
           DISPLAY '*======================================*'.                  
                                                                                
           CLOSE BP13F740                                                       
                 BP13K825                                                       
                 BP13K022                                                       
                 BP13K857                                                       
                 BP13K740                                                       
                 BP13LF85.                                                      
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K825 CLOSE ERROR, STAT IS ' WS-K825-STATUS           
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K022 CLOSE ERROR, STAT IS ' WS-K022-STATUS           
              MOVE WS-K022-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K857 CLOSE ERROR, STAT IS ' WS-K857-STATUS           
              MOVE WS-K857-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K740 CLOSE ERROR, STAT IS ' WS-K740-STATUS           
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
