       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC61.                                                 
      *AUTHOR.        ED1.                                                      
      *DATE-WRITTEN.  11/12/2007.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO CREATE NEW HISTORY FILE BP13KD39                *        
      *             BASED ON BP13KD38 TRANSACTION RECORD               *        
      *                                                                *        
      * INPUT  FILES : BP13FD38                                        *        
      * OUTPUT FILES : BP13KD39.                                       *        
      *                                                                *        
      * CHG-NO    BY    ON    - DESCRIPTION                            *        
      * ---------------------------------                              *        
      * BP132909 ED1 12/02/07   NEW PROGRAM                            *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   BP13FD38      ASSIGN TO BP13FD38.                           
                                                                                
           SELECT BP13KD39 ASSIGN TO BP13KD39                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD39-KEY-FLD                                    
                FILE STATUS  IS KD39-STATUS.                                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FD38               RECORD CONTAINS 400 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD                   
                                   RECORDING MODE IS F.                         
       COPY BP13FD38.                                                           
                                                                                
       FD   BP13KD39               RECORD CONTAINS 450 CHARACTERS.              
       COPY BP13KD39.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-FD38-READ                 PIC 9(07) VALUE ZEROS.              
           05  WS-KD39-WRITTEN              PIC 9(07) VALUE ZEROS.              
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                  PIC X(08) VALUE SPACES.             
           05  WS-SYS-TIME                  PIC X(08) VALUE SPACES.             
           05  FILLER                       PIC X(05) VALUE SPACES.             
                                                                                
       01  WS-VARIABLES.                                                        
           05 KD39-STATUS                  PIC 9(02) VALUE ZEROES.              
           05 WS-CURRDATE                  PIC X(08) VALUE SPACES.              
           05 WS-EOF                       PIC X     VALUE 'N'.                 
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-DD                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-MM                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-CCYY                 PIC 9(04) VALUE ZEROES.              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FD38       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL WS-EOF = 'Y'.                                                
           PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13FD38                                                 
                I-O    BP13KD39.                                                
                                                                                
           IF KD39-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13KD39, FILE STATUS : '                  
                                 KD39-STATUS                                    
              MOVE KD39-STATUS  TO RETURN-CODE                                  
              PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
           MOVE WS-SYS-DATE(1:4)      TO WS-CUR-CCYY.                           
           MOVE WS-SYS-DATE(5:2)      TO WS-CUR-MM.                             
           MOVE WS-SYS-DATE(7:2)      TO WS-CUR-DD.                             
           MOVE WS-SYS-DATE(1:8)      TO WS-CURRDATE.                           
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FD38.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FD38                                                        
             AT END MOVE 'Y' TO WS-EOF                                          
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD  1            TO WS-FD38-READ.                                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
           IF FD38-KEY-FLD = SPACES OR LOW-VALUES OR ZEROES                     
              PERFORM 2000-READ-BP13FD38  THRU  2000-EXIT                       
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           INITIALIZE                  BP13KD39-TRANS-REC.                      
           MOVE BP13FD38-TRANS-REC  TO BP13KD39-TRANS-REC.                      
           MOVE FD38-KEY-FLD        TO KD39-KEY-FLD.                            
           MOVE WS-SYS-DATE         TO KD39-DTE-UPDATE-HIST.                    
           MOVE WS-SYS-TIME         TO KD39-TME-UPDATE-HIST.                    
                                                                                
           PERFORM 4000-WRITE-BP13KD39 THRU  4000-EXIT.                         
                                                                                
           PERFORM 2000-READ-BP13FD38  THRU  2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-BP13KD39.                                                     
      *-------------------------------------------------------------            
                                                                                
           WRITE BP13KD39-TRANS-REC.                                            
           IF KD39-STATUS = ZEROES                                              
              ADD 1 TO WS-KD39-WRITTEN                                          
           ELSE                                                                 
              DISPLAY 'WRITE FAILED FOR BP13KD39,STATUS ' KD39-STATUS           
              MOVE KD39-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CC61'.                                
           DISPLAY 'RUN DATE       :  '  WS-CURRDATE.                           
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13FD38 READ     : ' WS-FD38-READ.                  
           DISPLAY 'NO. OF BP13KD39  WRITTEN : ' WS-KD39-WRITTEN.               
                                                                                
           CLOSE BP13FD38                                                       
                 BP13KD39.                                                      
                                                                                
           IF KD39-STATUS NOT = 00                                              
              MOVE KD39-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE BP13KD39 ERROR STATUS =  '                         
                       KD39-STATUS                                              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
