      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C269.                                                 
      *AUTHOR.        SONNY A.                                                  
      *DATE-WRITTEN.  07/03/97.                                                 
      ***************************************************************           
      *             SYSTEM OF COMMITMENT (SOC)                      *           
      *=============================================================*           
      *                                                             *           
      *  OBJECTIVE : TO UPDATE APPLICATION ADDRESS FOR CASES        *           
      *              FOUND IN OSCARS.                               *           
      *                                                             *           
      *  INPUT FILES (FOR RFS)                                      *           
      *      BE02F100  -- CHANGED TO BE02F200                       *           
      *      BP13K800  --                                           *           
      *      BP13K820  --                                           *           
      *                                                             *           
      *  INPUT FILES (FOR MATURE NT AND REQUEST)                    *           
      *      BP13K500  --                                           *           
      *      BP13K730  --                                           *           
      *      BP13K825  --                                           *           
      *                                                             *           
      *  I-O   FILES                                                *           
      *      BE02K900  --                                           *           
      *      BP13K830  --                                           *           
      *      BP13K838  --                                           *           
      *                                                             *           
      *  OUTPUT                                                     *           
      *      BP13L269  -- SUMMARY REPORT ON UPDATING OF MAILING     *           
      *                   ADDRESS UNDER OSCARS                      *           
      *                                                             *           
      *      P13L269A  -- ERROR REPORT                              *           
      *                                                             *           
      ***************************************************************           
      * MODIFICATIONS :                                             *           
      *                                                             *           
      * CHGE REQ #   DATE       AUTHOR    REMARKS                   *           
      * ----------   ----       ------    -------                   *           
      * BP130251     07/03/97    SIA      NEW PGM                   *           
      * BP130383     14/01/98    SIA      TO CATER FOR CHANGE       *           
      *                                   IN OSCARS FILE FROM       *           
      *                                   BE02F100 TO BE02F200.     *           
      * BP130418     11/04/98    MPA      TO CATER FOR Y2000.       *           
      * BP130693     17/06/99    LMS      TO RECTIFY STREET NAME    *           
      *                                   OF ADDR TYPE 'N'.         *           
      * BP130652     17/09/99    LSB      EXPAND F500,F730 RECL FROM*           
      *                                   300 TO 500 CHARACTERS     *           
      * BP132626     24/11/04    SK       CHANGE DTE-REGN TO DTE-REQ*           
      * BP133365     25/06/08    MTSR     TO FMT ADDRESS CORRECTLY  *           
      ***************************************************************           
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
      *======================                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *=====================                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BE02F200 ASSIGN TO BE02F200.                                  
                                                                                
           SELECT BP13K500 ASSIGN TO BP13K500                                   
                        ACCESS MODE          IS RANDOM                          
                        ORGANIZATION         IS INDEXED                         
                        RECORD KEY           IS K500-NUM-REGN                   
                        FILE STATUS          IS WS-K500-STATUS.                 
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                        ACCESS MODE          IS RANDOM                          
                        ORGANIZATION         IS INDEXED                         
                        RECORD KEY           IS K730-KEY-NEW                    
                        FILE STATUS          IS WS-K730-STATUS.                 
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                        ACCESS MODE          IS RANDOM                          
                        ORGANIZATION         IS INDEXED                         
                        RECORD KEY           IS K800-NUM-REGN                   
                        FILE STATUS          IS WS-K800-STATUS.                 
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                        ACCESS MODE          IS RANDOM                          
                        ORGANIZATION         IS INDEXED                         
                        RECORD KEY           IS K820-KEY-FLD                    
                        ALTERNATE RECORD KEY IS K820-NUM-NRIC                   
                                                WITH DUPLICATES                 
                        FILE STATUS          IS WS-K820-STATUS.                 
                                                                                
           SELECT BP13K825 ASSIGN TO BP13TMP                                    
                        ACCESS MODE          IS RANDOM                          
                        ORGANIZATION         IS INDEXED                         
                        RECORD KEY           IS K825-KEY-FLD                    
                        ALTERNATE RECORD KEY IS K825-NUM-NRIC                   
                                                WITH DUPLICATES                 
                        FILE STATUS          IS WS-K825-STATUS.                 
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                        ACCESS MODE          IS RANDOM                          
                        ORGANIZATION         IS INDEXED                         
                        RECORD KEY           IS K830-KEY-FLD                    
                        ALTERNATE RECORD KEY IS K830-NUM-REGN                   
                                                WITH DUPLICATES                 
                        FILE STATUS          IS WS-K830-STATUS.                 
                                                                                
           SELECT BP13K838 ASSIGN TO BP13K838                                   
                        ACCESS MODE          IS RANDOM                          
                        ORGANIZATION         IS INDEXED                         
                        RECORD KEY           IS K838-KEY-FLD                    
                        FILE STATUS          IS WS-K838-STATUS.                 
                                                                                
           SELECT BE02K900 ASSIGN TO BE02K900                                   
                        ACCESS MODE          IS RANDOM                          
                        ORGANIZATION         IS INDEXED                         
                        RECORD KEY           IS BE02K900-KEY                    
                        FILE STATUS          IS WS-K900-STATUS.                 
                                                                                
           SELECT BP13L269 ASSIGN TO BP13L269.                                  
                                                                                
           SELECT P13L269A ASSIGN TO P13L269A.                                  
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
      *=============                                                            
       COPY BE02F200.                                                           
                                                                                
       COPY BE02K900.                                                           
                                                                                
       FD  BP13K500                                                             
               RECORD CONTAINS 500 CHARACTERS.                                  
       COPY BP13K500.                                                           
                                                                                
       FD  BP13K730                                                             
               RECORD CONTAINS 500 CHARACTERS.                                  
       COPY BP13K730.                                                           
                                                                                
       FD  BP13K800                                                             
               RECORD CONTAINS 2000 CHARACTERS.                                 
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
               RECORD CONTAINS 400 CHARACTERS.                                  
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K825                                                             
               RECORD CONTAINS 200 CHARACTERS.                                  
       COPY BP13K825.                                                           
                                                                                
       FD  BP13K830                                                             
               RECORD CONTAINS 250 CHARACTERS.                                  
       COPY BP13K830.                                                           
                                                                                
       FD  BP13K838                                                             
               RECORD CONTAINS 400 CHARACTERS.                                  
       COPY BP13K838.                                                           
                                                                                
       FD  BP13L269                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  BP13L269-REC                PIC X(132).                              
                                                                                
       FD  P13L269A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  P13L269A-REC                PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-DTE-CURRENT-YYMMDD.                                               
           05   WS-CURRENT-YY          PIC X(4).                                
           05   WS-CURRENT-MM          PIC X(2).                                
           05   WS-CURRENT-DD          PIC X(2).                                
                                                                                
       01  WS-CURR-DTE.                                                         
           05   WS-CURR-YY             PIC X(4).                                
           05   WS-CURR-MM             PIC X(2).                                
           05   WS-CURR-DD             PIC X(2).                                
                                                                                
       01  WS-PRT-DTE.                                                          
           05   WS-PRT-YY              PIC X(4).                                
           05   FILLER                 PIC X  VALUE '/'.                        
           05   WS-PRT-MM              PIC X(2).                                
           05   FILLER                 PIC X  VALUE '/'.                        
           05   WS-PRT-DD              PIC X(2).                                
                                                                                
       01  WS-CURRENT-TIME             PIC X(8).                                
                                                                                
       01  WS-CURR-TIME.                                                        
           05   WS-CUR-HH              PIC X(2).                                
           05   WS-CUR-MM              PIC X(2).                                
           05   WS-CUR-SS              PIC X(2).                                
                                                                                
       01  WS-OLD-K830-ADDR            PIC X(99) VALUE   SPACES.                
       01  WS-OLD-K830-MAIL-ADDR.                                               
           05  WS-K830-OLD-MAIL-BLK     PIC X(16) VALUE SPACES.                 
           05  WS-K830-OLD-MAIL-FLAT    PIC X(11) VALUE SPACES.                 
           05  WS-K830-OLD-MAIL-STREET  PIC X(20) VALUE SPACES.                 
           05  WS-K830-OLD-MAIL-POSTAL  PIC X(6)  VALUE SPACES.                 
                                                                                
       01  WS-PG-CNT-L269              PIC 999  VALUE    ZEROS.                 
       01  WS-PG-CNT-L269A             PIC 999  VALUE    ZEROS.                 
                                                                                
       01  WS-K500-FOUND               PIC X(1)   VALUE  'N'.                   
       01  WS-K730-FOUND               PIC X(1)   VALUE  'N'.                   
       01  WS-K800-FOUND               PIC X(1)   VALUE  'N'.                   
       01  WS-K820-FOUND               PIC X(1)   VALUE  'N'.                   
       01  WS-K825-FOUND               PIC X(1)   VALUE  'N'.                   
       01  WS-NUM-COUNT                PIC 9      VALUE  ZEROS.                 
                                                                                
      *-------------*                                                           
      * EOF FLAGS   *                                                           
      *-------------*                                                           
                                                                                
       01  EOF-BE02F200                PIC X      VALUE  'N'.                   
       01  EOF-K820                    PIC X      VALUE  'N'.                   
       01  EOF-K825                    PIC X      VALUE  'N'.                   
       01  WS-K838-UPDATE              PIC X      VALUE  SPACE.                 
       01  WS-K838-AMIS                PIC X      VALUE  SPACE.                 
       01  WS-K838-REGN                PIC X(8)   VALUE  SPACES.                
       01  WS-K838-NRIC2               PIC X(9)   VALUE  SPACES.                
       01  WS-K838-NRIC3               PIC X(9)   VALUE  SPACES.                
       01  WS-K838-NRIC4               PIC X(9)   VALUE  SPACES.                
       01  WS-K838-NT-ZONE             PIC X(3)   VALUE  SPACES.                
       01  WS-K838-FLAT-TYPE           PIC X(2)   VALUE  SPACES.                
                                                                                
      *-------------*                                                           
      * FILE STATUS *                                                           
      *-------------*                                                           
                                                                                
       01  WS-K900-STATUS                 PIC 9(2) VALUE ZEROS.                 
       01  WS-K500-STATUS                 PIC 9(2) VALUE ZEROS.                 
       01  WS-K730-STATUS                 PIC 9(2) VALUE ZEROS.                 
       01  WS-K800-STATUS                 PIC 9(2) VALUE ZEROS.                 
       01  WS-K820-STATUS                 PIC 9(2) VALUE ZEROS.                 
       01  WS-K825-STATUS                 PIC 9(2) VALUE ZEROS.                 
       01  WS-K830-STATUS                 PIC 9(2) VALUE ZEROS.                 
       01  WS-K838-STATUS                 PIC 9(2) VALUE ZEROS.                 
                                                                                
      *--------*                                                                
      * COUNTS *                                                                
      *--------*                                                                
       01  WS-F100-READ                    PIC 9(6)  VALUE  ZEROS.              
       01  WS-K900-WRITE                   PIC 9(6)  VALUE  ZEROS.              
       01  WS-K500-READ                    PIC 9(6)  VALUE  ZEROS.              
       01  WS-K730-READ                    PIC 9(6)  VALUE  ZEROS.              
       01  WS-K800-READ                    PIC 9(6)  VALUE  ZEROS.              
       01  WS-K830-REWRITE                 PIC 9(6)  VALUE  ZEROS.              
       01  WS-K830-READ                    PIC 9(6)  VALUE  ZEROS.              
       01  WS-K820-DUP                     PIC 9(6)  VALUE  ZEROS.              
       01  WS-K820-BYPASS                  PIC 9(6)  VALUE  ZEROS.              
       01  WS-K820-2ND-READ                PIC 9(6)  VALUE  ZEROS.              
       01  WS-K820-READ                    PIC 9(6)  VALUE  ZEROS.              
       01  WS-K825-DUP                     PIC 9(6)  VALUE  ZEROS.              
       01  WS-K825-BYPASS                  PIC 9(6)  VALUE  ZEROS.              
       01  WS-K825-2ND-READ                PIC 9(6)  VALUE  ZEROS.              
       01  WS-K825-READ                    PIC 9(6)  VALUE  ZEROS.              
       01  WS-K838-WRITE                   PIC 9(6)  VALUE  ZEROS.              
       01  WS-COUNT-L269                   PIC 9(2)  VALUE  55.                 
       01  WS-COUNT-L269A                  PIC 9(2)  VALUE  ZEROS.              
       01  WS-SNO-L269                     PIC 9(6)  VALUE  ZEROS.              
       01  WS-SNO-L269A                    PIC 9(6)  VALUE  ZEROS.              
       01  WS-L269-WRITTEN                 PIC 9(5)  VALUE  ZEROS.              
       01  WS-L269A-WRITTEN                PIC 9(5)  VALUE  ZEROS.              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * HEADER LINES FOR REPORT BP13L269                               *        
      *----------------------------------------------------------------*        
       01  L269-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(8)    VALUE 'BP13L269'.           
           05  FILLER                   PIC X(7)     VALUE SPACES .             
           05  FILLER                   PIC X(8)    VALUE 'HDBCAT 3'.           
           05  FILLER                   PIC X(21)   VALUE SPACES.               
           05  FILLER                   PIC X(39)   VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(17)   VALUE SPACES.               
           05  FILLER                   PIC X(7)    VALUE 'DATE :'.             
           05  L269-PRT-DTE.                                                    
               07  L269-PRT-DD          PIC XX       VALUE SPACES.              
               07  FILLER               PIC X        VALUE '/'.                 
               07  L269-PRT-MM          PIC XX       VALUE SPACES.              
               07  FILLER               PIC X        VALUE '/'.                 
               07  L269-PRT-YY          PIC X(4)     VALUE SPACES.              
           05  FILLER                   PIC X(3)     VALUE SPACES.              
           05  FILLER                   PIC X(7)     VALUE 'PAGE : '.           
           05  L269-PAGE-NO             PIC ZZ9 .                               
                                                                                
       01  L269-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(30)   VALUE SPACES .              
           05  FILLER                   PIC X(48)   VALUE                       
               '   SUMMARY REPORT ON UPDATING OF MAILING ADDRESS'.              
           05  FILLER                   PIC X(14)   VALUE                       
                                        ' UNDER OSCARS'.                        
                                                                                
       01  L269-PR-HEAD-03.                                                     
           05  FILLER                   PIC X(54)   VALUE                       
               'S/NO  REGN NO/ OLD RESIDENTIAL         NEW RESIDENTIAL'.        
           05  FILLER                   PIC X(51)   VALUE                       
               '         CORRESPONDENCE             AMIS  DTE-UPD '.            
           05  FILLER                   PIC X(25)   VALUE                       
               '     DTE-REG     DTE-BAL'.                                      
                                                                                
       01  L269-PR-HEAD-04.                                                     
           05  FILLER                   PIC X(54)   VALUE                       
               '      UIN      ADDRESS                 ADDRESS        '.        
           05  FILLER                   PIC X(54)   VALUE                       
               '         ADDRESS                          DTE-BK '.             
           05  FILLER                   PIC X(20)   VALUE                       
               '  DTE-SA      DTE-TP'.                                          
                                                                                
      *----------------------------------------------------------------*        
      * DETAIL LINE FOR REPORT BP13L269                                *        
      *----------------------------------------------------------------*        
       01  L269-PR-DETAILS.                                                     
           05  L269-SNO                 PIC ZZZ9.                               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L269-REG-NO              PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L269-OLD-RES-ADDR.                                               
               10  L269-BLK-RES-O       PIC X(10)   VALUE SPACES.               
               10  FILLER               PIC X(1)    VALUE SPACES.               
               10  L269-NUM-FLAT-O.                                             
                   15  L269-CHAR-O      PIC X       VALUE SPACE.                
                   15  L269-LEVEL-O     PIC X(2)    VALUE SPACES.               
                   15  L269-DASH-O      PIC X       VALUE SPACE.                
                   15  L269-HSE-MAIN-O  PIC X(4)    VALUE SPACES.               
                   15  L269-HSE-SUB-O   PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L269-NEW-RES-ADDR.                                               
               10  L269-BLK-RES-N       PIC X(10)   VALUE SPACES.               
               10  FILLER               PIC X(1)    VALUE SPACE.                
               10  L269-NUM-FLAT-N.                                             
                   15  L269-CHAR-N      PIC X       VALUE SPACE.                
                   15  L269-LEVEL-N     PIC X(3)    VALUE SPACES.               
                   15  L269-DASH-N      PIC X       VALUE SPACE.                
                   15  L269-HSE-MAIN-N  PIC X(5)    VALUE SPACES.               
           05  FILLER                   PIC X(03)   VALUE SPACES.               
           05  L269-CORR-ADDR.                                                  
               10  L269-BLK-CORR        PIC X(10)   VALUE SPACES.               
               10  FILLER               PIC X(1)    VALUE SPACES.               
               10  L269-NUM-FLAT-CORR.                                          
                   15  L269-CHAR        PIC X       VALUE SPACE.                
                   15  L269-LEVEL       PIC X(2)    VALUE SPACES.               
                   15  L269-DASH        PIC X       VALUE SPACE.                
                   15  L269-HSE-MAIN    PIC X(4)    VALUE SPACES.               
                   15  L269-HSE-SUB     PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(6)    VALUE SPACES.               
           05  L269-AMIS                PIC X(1)    VALUE SPACE.                
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  L269-UPD-DATE.                                                   
               07 L269-UPD-DD           PIC X(2)    VALUE SPACES.               
               07 L269-UPD-SLASH1       PIC X       VALUE SPACES.               
               07 L269-UPD-MM           PIC X(2)    VALUE SPACES.               
               07 L269-UPD-SLASH2       PIC X       VALUE SPACES.               
               07 L269-UPD-YY           PIC X(4)    VALUE SPACES.               
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  L269-DATE-REG.                                                   
               07 L269-REG-DD           PIC X(2)    VALUE SPACES.               
               07 L269-REG-SLASH1       PIC X       VALUE SPACES.               
               07 L269-REG-MM           PIC X(2)    VALUE SPACES.               
               07 L269-REG-SLASH2       PIC X       VALUE SPACES.               
               07 L269-REG-YY           PIC X(4)    VALUE SPACES.               
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  L269-DATE-BALLOT.                                                
               07 L269-BALLOT-CC        PIC X(2)    VALUE SPACES.               
               07 L269-BALLOT-YY        PIC X(2)    VALUE SPACES.               
               07 L269-BALLOT-SLASH     PIC X       VALUE SPACES.               
               07 L269-BALLOT-MM        PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
                                                                                
       01  L269-RES-ADDR2.                                                      
           05  FILLER                   PIC X(5)    VALUE SPACES.               
           05  L269-NRIC                PIC X(9)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L269-NME-STREET-O        PIC X(20)   VALUE SPACES.               
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  L269-NME-STREET-N        PIC X(20)   VALUE SPACES.               
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  L269-NME-STREET-CORR     PIC X(20)   VALUE SPACES.               
           05  FILLER                   PIC X(13)   VALUE SPACES.               
           05  L269-BK-DATE.                                                    
               07 L269-BK-DD            PIC X(2)    VALUE SPACES.               
               07 L269-BK-SLASH1        PIC X       VALUE SPACES.               
               07 L269-BK-MM            PIC X(2)    VALUE SPACES.               
               07 L269-BK-SLASH2        PIC X       VALUE SPACES.               
               07 L269-BK-YY            PIC X(4)    VALUE SPACES.               
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  L269-SA-DATE.                                                    
               07 L269-SA-DD            PIC X(2)    VALUE SPACES.               
               07 L269-SA-SLASH1        PIC X       VALUE SPACES.               
               07 L269-SA-MM            PIC X(2)    VALUE SPACES.               
               07 L269-SA-SLASH2        PIC X       VALUE SPACES.               
               07 L269-SA-YY            PIC X(4)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L269-TP-DATE.                                                    
               07 L269-TP-DD            PIC X(2)    VALUE SPACES.               
               07 L269-TP-SLASH1        PIC X       VALUE SPACES.               
               07 L269-TP-MM            PIC X(2)    VALUE SPACES.               
               07 L269-TP-SLASH2        PIC X       VALUE SPACES.               
               07 L269-TP-YY            PIC X(4)    VALUE SPACES.               
                                                                                
       01  L269-POSTAL.                                                         
           05  FILLER                   PIC X(15)   VALUE SPACES.               
           05  L269-NUM-POSTAL-O        PIC X(9)    VALUE SPACES.               
           05  FILLER                   PIC X(15)   VALUE SPACES.               
           05  L269-NUM-POSTAL-N        PIC X(9)    VALUE SPACES.               
           05  FILLER                   PIC X(15)   VALUE SPACES.               
           05  L269-NUM-POSTAL-CORR     PIC X(9)    VALUE SPACES.               
                                                                                
       01  L269-DRAW-LINE.                                                      
           05  LINE1                    PIC X(132)  VALUE ALL '-'.              
                                                                                
      *----------------------------------------------------------------*        
      * HEADER LINES FOR REPORT P13L269A                               *        
      *----------------------------------------------------------------*        
       01  L269A-PR-HEAD-01.                                                    
           05  FILLER                   PIC X(8)    VALUE 'P13L269A'.           
           05  FILLER                   PIC X(7)    VALUE SPACES.               
           05  FILLER                   PIC X(8)    VALUE 'HDBCAT 3'.           
           05  FILLER                   PIC X(21)   VALUE SPACES.               
           05  FILLER                   PIC X(39)   VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(17)   VALUE SPACES.               
           05  FILLER                   PIC X(7)    VALUE 'DATE :'.             
           05  L269A-PRT-DTE.                                                   
               07  L269A-PRT-DD         PIC XX       VALUE SPACES.              
               07  FILLER               PIC X        VALUE '/'.                 
               07  L269A-PRT-MM         PIC XX       VALUE SPACES.              
               07  FILLER               PIC X        VALUE '/'.                 
               07  L269A-PRT-YY         PIC X(4)     VALUE SPACES.              
           05  FILLER                   PIC X(3)     VALUE SPACES.              
           05  FILLER                   PIC X(7)     VALUE 'PAGE : '.           
           05  L269A-PAGE-NO            PIC ZZ9.                                
                                                                                
       01  L269A-PR-HEAD-02.                                                    
           05  FILLER                   PIC X(50)   VALUE SPACES .              
           05  FILLER                   PIC X(48)   VALUE                       
               'ERROR REPORT ON OSCARS UPDATE'.                                 
                                                                                
       01  L269A-PR-HEAD-03.                                                    
           05  FILLER                   PIC X(45)   VALUE                       
               'S/N   UIN/REGN   NEW ADDRESS (FROM OSCARS)'.                    
           05  FILLER                   PIC X(28)   VALUE SPACES.               
           05  FILLER                   PIC X(07)   VALUE                       
               'REMARKS'.                                                       
                                                                                
      *----------------------------------------------------------------*        
      * DETAIL LINE FOR REPORT P13L269A                                *        
      *----------------------------------------------------------------*        
       01  L269A-PR-DETAILS.                                                    
           05  L269A-SNO                PIC ZZZ9.                               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L269A-UINFIN             PIC X(9).                               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L269A-NEW-ADDR.                                                  
               10  L269A-NEW-NUM-BLK       PIC X(10) VALUE SPACES.              
               10  FILLER                  PIC X(01) VALUE SPACE.               
               10  L269A-NEW-FLAT          PIC X(10) VALUE SPACES.              
               10  FILLER                  PIC X(02) VALUE SPACES.              
               10  L269A-NEW-STREET-NME    PIC X(20) VALUE SPACES.              
               10  FILLER                  PIC X(02) VALUE SPACES.              
               10  L269A-NEW-POSTAL-CDE    PIC X(09) VALUE SPACES.              
               10  FILLER                  PIC X(02) VALUE SPACES.              
               10  L269A-REMARKS           PIC X(27) VALUE SPACES.              
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-MAIN.                                                               
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES        THRU  1000-EXIT.                      
                                                                                
           PERFORM 1500-GET-DATE-TIME     THRU  1500-EXIT.                      
           MOVE    WS-CURR-DD             TO    WS-PRT-DD.                      
           MOVE    WS-CURR-MM             TO    WS-PRT-MM.                      
           MOVE    WS-CURR-YY             TO    WS-PRT-YY.                      
                                                                                
           MOVE     1    TO WS-SNO-L269.                                        
           MOVE     1    TO WS-PG-CNT-L269.                                     
           MOVE     1    TO WS-SNO-L269A.                                       
           MOVE     1    TO WS-PG-CNT-L269A.                                    
                                                                                
           PERFORM 8000-WRITE-START-BE02K900 THRU 8000-EXIT.                    
                                                                                
           PERFORM 3000-READ-BE02F200        THRU 3000-EXIT.                    
           PERFORM 4000-PROCESS-BE02F200     THRU 4000-EXIT                     
             UNTIL EOF-BE02F200 = 'Y'.                                          
           PERFORM 1500-GET-DATE-TIME        THRU 1500-EXIT.                    
           PERFORM 8100-WRITE-FINAL-BE02K900 THRU 8100-EXIT.                    
           PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                             
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BE02F200                                                 
                       BP13K500                                                 
                       BP13K730                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K825                                                 
                I-O    BP13K830                                                 
                       BE02K900                                                 
                       BP13K838                                                 
                OUTPUT BP13L269                                                 
                       P13L269A.                                                
                                                                                
           IF WS-K900-STATUS NOT = 0 AND 97                                     
              DISPLAY ' ERROR ENCOUNTERED IN OPENING FILE BE02K900 : '          
                                              WS-K900-STATUS                    
              MOVE WS-K900-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           IF WS-K500-STATUS NOT = 0 AND 97                                     
              DISPLAY ' ERROR ENCOUNTERED IN OPENING FILE BP13K500 : '          
                                              WS-K500-STATUS                    
              MOVE WS-K500-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           IF WS-K730-STATUS NOT = 0 AND 97                                     
              DISPLAY ' ERROR ENCOUNTERED IN OPENING FILE BP13K730 : '          
                                              WS-K730-STATUS                    
              MOVE WS-K730-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           IF WS-K800-STATUS NOT = 0 AND 97                                     
              DISPLAY ' ERROR ENCOUNTERED IN OPENING FILE BP13K800 : '          
                                              WS-K800-STATUS                    
              MOVE WS-K800-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           IF WS-K820-STATUS NOT = 0 AND 97                                     
              DISPLAY ' ERROR ENCOUNTERED IN OPENING FILE BP13K820 : '          
                                              WS-K820-STATUS                    
              MOVE WS-K820-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           IF WS-K825-STATUS NOT = 0 AND 97                                     
              DISPLAY ' ERROR ENCOUNTERED IN OPENING FILE BP13K825 : '          
                                              WS-K825-STATUS                    
              MOVE WS-K825-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           IF WS-K830-STATUS NOT = 0 AND 97                                     
              DISPLAY ' ERROR ENCOUNTERED IN OPENING FILE  BP13K830 : '         
                                              WS-K830-STATUS                    
              MOVE WS-K830-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           IF WS-K838-STATUS NOT = 0 AND 97                                     
              DISPLAY ' ERROR ENCOUNTERED IN OPENING FILE  BP13K838 : '         
                                              WS-K838-STATUS                    
              MOVE WS-K838-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1500-GET-DATE-TIME.                                                      
      *****************************************************************         
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DTE-CURRENT-YYMMDD.            
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-CURRENT-TIME.                  
                                                                                
           MOVE WS-CURRENT-DD   TO     WS-CURR-DD.                              
           MOVE WS-CURRENT-MM   TO     WS-CURR-MM.                              
           MOVE WS-CURRENT-YY   TO     WS-CURR-YY.                              
                                                                                
           MOVE WS-CURRENT-TIME (1:2)   TO   WS-CUR-HH.                         
           MOVE WS-CURRENT-TIME (3:2)   TO   WS-CUR-MM.                         
           MOVE WS-CURRENT-TIME (5:2)   TO   WS-CUR-SS.                         
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-BE02F200.                                                      
      *****************************************************************         
                                                                                
           READ BE02F200                                                        
                AT END  MOVE  'Y' TO    EOF-BE02F200.                           
                                                                                
           IF EOF-BE02F200 NOT = 'Y'                                            
              ADD   1             TO    WS-F100-READ.                           
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-PROCESS-BE02F200.                                                   
      *****************************************************************         
                                                                                
      *---------------------------------------------------------------*         
      * CHECKING FOR RFS CASES                                                  
      *---------------------------------------------------------------*         
                                                                                
           MOVE  SPACE             TO  WS-K838-UPDATE.                          
           MOVE  SPACE             TO  WS-K838-AMIS.                            
           MOVE  SPACES            TO  WS-K838-REGN.                            
           MOVE  SPACES            TO  WS-K838-NRIC2.                           
           MOVE  SPACES            TO  WS-K838-NRIC3.                           
           MOVE  SPACES            TO  WS-K838-NRIC4.                           
           MOVE  SPACES            TO  WS-K838-NT-ZONE.                         
           MOVE  SPACES            TO  WS-K838-FLAT-TYPE.                       
           MOVE  SPACES            TO  K820-KEY-FLD.                            
           MOVE  BE02F200-NUM-NRIC TO  K820-NUM-NRIC.                           
                                                                                
           MOVE     SPACES             TO   WS-OLD-K830-ADDR.                   
           MOVE     SPACES             TO   WS-OLD-K830-MAIL-ADDR.              
           PERFORM  5000-READ-BP13K820 THRU 5000-EXIT.                          
           IF WS-K820-FOUND = 'Y'                                               
                                                                                
              PERFORM 5100-READ-BP13K800 THRU 5100-EXIT                         
                                                                                
              IF WS-K800-FOUND = 'Y'                                            
                 INITIALIZE BP13K830-REC                                        
                 MOVE    K800-NUM-REGN              TO   K830-NUM-REGN          
                 PERFORM 5300-READ-BP13K830-BY-REGN THRU 5300-EXIT              
                 MOVE    K800-NUM-REGN              TO   WS-K838-REGN           
                 MOVE    K800-NUM-NRIC2             TO   WS-K838-NRIC2          
                 MOVE    K800-NUM-NRIC3             TO   WS-K838-NRIC3          
                 MOVE    K800-NUM-NRIC4             TO   WS-K838-NRIC4          
                 IF K800-NUM-BTO-ZONE NOT = SPACES AND LOW-VALUES               
                    MOVE K800-NUM-BTO-ZONE  TO  WS-K838-NT-ZONE                 
                 ELSE                                                           
                    IF K800-NUM-ALLO-CAT = 'SBF'                                
                       MOVE K800-NUM-NEW-TOWN  TO  WS-K838-NT-ZONE              
                    ELSE                                                        
                       MOVE K800-NUM-ZONE      TO  WS-K838-NT-ZONE              
                    END-IF                                                      
                 END-IF                                                         
                 MOVE K800-NUM-FLAT-TYPE TO WS-K838-FLAT-TYPE                   
              END-IF                                                            
                                                                                
              IF WS-K838-UPDATE = 'N'                                           
                 MOVE    SPACE                      TO   WS-K838-AMIS           
                 PERFORM 5400-WRITE-BP13K838-REC    THRU 5400-EXIT              
              END-IF                                                            
                                                                                
              IF WS-K838-UPDATE = 'U'                                           
                 PERFORM 5400-WRITE-BP13K838-REC    THRU 5400-EXIT              
                 PERFORM 7000-MOVE-VALUES-TO-REPORT THRU 7000-EXIT              
                 PERFORM 7200-WRITE-BP13L269        THRU 7200-EXIT              
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------*         
      * CHECKING FOR MATURE NT AND REQUEST CASES                                
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACE              TO  WS-K838-UPDATE.                          
           MOVE SPACE              TO  WS-K838-AMIS.                            
           MOVE SPACES             TO  WS-K838-REGN.                            
           MOVE SPACES             TO  WS-K838-NRIC2.                           
           MOVE SPACES             TO  WS-K838-NRIC3.                           
           MOVE SPACES             TO  WS-K838-NRIC4.                           
           MOVE SPACES             TO  WS-K838-NT-ZONE.                         
           MOVE SPACES             TO  WS-K838-FLAT-TYPE.                       
           MOVE SPACES             TO  K825-KEY-FLD.                            
           MOVE BE02F200-NUM-NRIC  TO  K825-NUM-NRIC.                           
                                                                                
           MOVE     SPACES             TO   WS-OLD-K830-ADDR.                   
           MOVE     SPACES             TO   WS-OLD-K830-MAIL-ADDR.              
                                                                                
           PERFORM  6000-READ-BP13K825 THRU 6000-EXIT.                          
                                                                                
           IF WS-K825-FOUND = 'Y'                                               
                                                                                
              IF K825-NUM-REGN(7:2) NOT = SPACES AND LOW-VALUES                 
                 MOVE SPACES        TO WS-OLD-K830-ADDR                         
                 MOVE K825-NUM-REGN TO K500-NUM-REGN                            
                 PERFORM 6500-READ-BP13K500 THRU 6500-EXIT                      
                                                                                
                 IF WS-K500-FOUND = 'Y'                                         
                    MOVE K500-NUM-REGN TO K830-NUM-REGN                         
                    PERFORM 5300-READ-BP13K830-BY-REGN THRU 5300-EXIT           
                    MOVE    K500-NUM-REGN           TO   WS-K838-REGN           
                    MOVE    K500-NUM-NRIC2          TO   WS-K838-NRIC2          
                    MOVE    K500-NUM-NRIC3          TO   WS-K838-NRIC3          
                    MOVE    K500-NUM-NRIC4          TO   WS-K838-NRIC4          
                    IF K500-CDE-ZONE = SPACES OR LOW-VALUES                     
                       MOVE K500-CDE-NT1       TO  WS-K838-NT-ZONE              
                    ELSE                                                        
                       MOVE K500-CDE-ZONE      TO  WS-K838-NT-ZONE              
                    END-IF                                                      
                    MOVE K500-CDE-FLAT-TYPE TO WS-K838-FLAT-TYPE                
                 END-IF                                                         
                                                                                
              ELSE                                                              
                 MOVE K825-NUM-REF  TO K730-KEY-NEW                             
                 PERFORM 6100-READ-BP13K730 THRU 6100-EXIT                      
                                                                                
                 IF WS-K730-FOUND = 'Y'                                         
                    MOVE SPACES        TO K830-KEY-FLD                          
                    MOVE K730-KEY-NEW  TO K830-KEY-FLD(1:6)                     
                    PERFORM 5350-READ-BP13K830-BY-REF THRU 5350-EXIT            
                    MOVE    K730-KEY-NEW            TO   WS-K838-REGN           
                    MOVE    K730-NUM-NRIC2          TO   WS-K838-NRIC2          
                    MOVE    K730-NUM-NRIC3          TO   WS-K838-NRIC3          
                    MOVE    K730-NUM-NRIC4          TO   WS-K838-NRIC4          
                    IF K730-CDE-ZONE = SPACES OR LOW-VALUES                     
                       MOVE K730-CDE-NT1       TO  WS-K838-NT-ZONE              
                    ELSE                                                        
                       MOVE K730-CDE-ZONE      TO  WS-K838-NT-ZONE              
                    END-IF                                                      
                    MOVE K730-CDE-FLAT-TYPE TO WS-K838-FLAT-TYPE                
                 END-IF                                                         
                                                                                
              END-IF                                                            
                                                                                
              IF WS-K838-UPDATE = 'N'                                           
                 MOVE    SPACES                     TO   WS-K838-AMIS           
                 PERFORM 5400-WRITE-BP13K838-REC    THRU 5400-EXIT              
              END-IF                                                            
                                                                                
              IF WS-K838-UPDATE = 'U'                                           
                 PERFORM 5400-WRITE-BP13K838-REC    THRU 5400-EXIT              
                 PERFORM 7000-MOVE-VALUES-TO-REPORT THRU 7000-EXIT              
                 PERFORM 7200-WRITE-BP13L269        THRU 7200-EXIT              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM  3000-READ-BE02F200         THRU 3000-EXIT.                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-READ-BP13K820.                                                      
      *****************************************************************         
                                                                                
      *----------------------------------------------------                     
      * READ BP13K820,                                                          
      *   (1)OBTAIN THE REGN-NO TO READ BP13K830 & BP13K800                     
      *   (1 A.) READ BP13K830,                                                 
      *             WRITE BP13K838                                              
      *             REWRITE BP13K830                                            
      *----------------------------------------------------                     
                                                                                
           MOVE  'N'      TO   WS-K820-FOUND.                                   
                                                                                
           READ BP13K820 KEY IS K820-NUM-NRIC.                                  
                                                                                
           EVALUATE  WS-K820-STATUS                                             
             WHEN 00                                                            
                  ADD   1  TO WS-K820-READ                                      
                  MOVE 'N' TO WS-K838-UPDATE                                    
                  IF K820-NUM-RELATIONSHIP = '00'                               
                     MOVE   'Y'                 TO   WS-K820-FOUND              
                     MOVE    SPACES             TO   K800-NUM-REGN              
                     MOVE    K820-NUM-REGN      TO   K800-NUM-REGN              
                  END-IF                                                        
             WHEN 02                                                            
                  ADD     1                        TO   WS-K820-DUP             
                  MOVE    'N'                      TO   WS-K838-UPDATE          
                  MOVE    K820-NUM-NRIC            TO   L269A-UINFIN            
                  MOVE    'DUP NRIC IN OCCUP FILE' TO   L269A-REMARKS           
                  PERFORM 7600-WRITE-BP13L269A     THRU 7600-EXIT               
             WHEN 23                                                            
                  DISPLAY 'RECORD NOT FOUND BP13K820 : ' K820-NUM-NRIC          
                  MOVE     SPACES                  TO WS-K838-UPDATE            
                  ADD      1                       TO WS-K820-BYPASS            
             WHEN OTHER                                                         
                  DISPLAY 'READ ERROR BP13K820 : ' WS-K820-STATUS               
                  DISPLAY 'NRIC TO BE UPDATED  : ' K820-NUM-NRIC                
                  GO TO 9999-CLOSE-FILES                                        
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5100-READ-BP13K800.                                                      
      *****************************************************************         
                                                                                
           MOVE 'N' TO WS-K800-FOUND.                                           
                                                                                
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN  00                                                             
                 ADD     1   TO  WS-K800-READ                                   
                 PERFORM 5110-MOVE-DATA-TO-PRINT THRU 5110-EXIT                 
                 MOVE    'Y' TO  WS-K800-FOUND                                  
           WHEN  23                                                             
                 MOVE    K820-NUM-NRIC            TO   L269A-UINFIN             
                 MOVE    'NRIC NOT IN SOC MASTER' TO   L269A-REMARKS            
                 PERFORM 7600-WRITE-BP13L269A     THRU 7600-EXIT                
                 DISPLAY 'RECORD NOT FOUND BP13K800 : ' K800-NUM-REGN           
                 DISPLAY 'NRIC TO BE UPDATED        : ' K820-NUM-NRIC           
           WHEN OTHER                                                           
                 DISPLAY 'READ ERROR BP13K800 : ' WS-K800-STATUS                
                 DISPLAY 'NRIC TO BE UPDATED  : ' K820-NUM-NRIC                 
                 DISPLAY 'REGN READ           : ' K800-NUM-REGN                 
                 PERFORM  9999-CLOSE-FILES THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5110-MOVE-DATA-TO-PRINT.                                                 
      *****************************************************************         
                                                                                
           IF K800-DTE-BK-APPT = SPACES OR LOW-VALUES                           
              MOVE SPACES                 TO L269-BK-DATE                       
           ELSE                                                                 
              MOVE K800-DTE-BK-APPT (7:2) TO L269-BK-DD                         
              MOVE K800-DTE-BK-APPT (5:2) TO L269-BK-MM                         
              MOVE K800-DTE-BK-APPT (1:4) TO L269-BK-YY                         
              MOVE '/'                    TO L269-BK-SLASH1                     
                                             L269-BK-SLASH2                     
           END-IF.                                                              
                                                                                
           IF K800-DTE-AO-APPT = SPACES OR LOW-VALUES                           
              MOVE SPACES                 TO L269-SA-DATE                       
           ELSE                                                                 
              MOVE K800-DTE-AO-APPT (7:2) TO L269-SA-DD                         
              MOVE K800-DTE-AO-APPT (5:2) TO L269-SA-MM                         
              MOVE K800-DTE-AO-APPT (1:4) TO L269-SA-YY                         
              MOVE '/'                    TO L269-SA-SLASH1                     
                                             L269-SA-SLASH2                     
           END-IF.                                                              
                                                                                
           IF K800-DTE-SO-APPT = SPACES OR LOW-VALUES                           
              MOVE SPACES                  TO L269-TP-DATE                      
           ELSE                                                                 
              MOVE K800-DTE-SO-APPT (1:4)  TO L269-TP-YY                        
              MOVE K800-DTE-SO-APPT (5:2)  TO L269-TP-MM                        
              MOVE K800-DTE-SO-APPT (7:2)  TO L269-TP-DD                        
              MOVE '/'                     TO L269-TP-SLASH1                    
                                              L269-TP-SLASH2                    
           END-IF.                                                              
                                                                                
           IF K800-DTE-BALLOT = SPACES OR LOW-VALUES                            
              MOVE SPACES                  TO L269-DATE-BALLOT                  
           ELSE                                                                 
              MOVE K800-DTE-BALLOT (1:2)   TO L269-BALLOT-CC                    
              MOVE K800-DTE-BALLOT (3:2)   TO L269-BALLOT-YY                    
              MOVE K800-DTE-BALLOT (5:2)   TO L269-BALLOT-MM                    
              MOVE '/'                     TO L269-BALLOT-SLASH                 
           END-IF.                                                              
                                                                                
           IF K800-DTE-REGN = SPACES OR LOW-VALUES OR ZEROES                    
              MOVE SPACES                  TO L269-DATE-REG                     
           ELSE                                                                 
              MOVE K800-DTE-REGN    (1:4) TO L269-REG-YY                        
              MOVE K800-DTE-REGN    (5:2) TO L269-REG-MM                        
              MOVE K800-DTE-REGN    (7:2) TO L269-REG-DD                        
              MOVE '/'                    TO L269-REG-SLASH1                    
                                             L269-REG-SLASH2                    
           END-IF.                                                              
                                                                                
       5110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5300-READ-BP13K830-BY-REGN.                                              
      *****************************************************************         
                                                                                
           READ  BP13K830 KEY IS K830-NUM-REGN.                                 
                                                                                
           EVALUATE WS-K830-STATUS                                              
             WHEN 00                                                            
                ADD 1   TO  WS-K830-READ                                        
                MOVE  K830-NUM-RESIDENT-ADDR TO WS-OLD-K830-ADDR                
                MOVE  K830-NUM-MAIL-BLK     TO WS-K830-OLD-MAIL-BLK             
                MOVE  K830-NUM-MAIL-FLAT    TO WS-K830-OLD-MAIL-FLAT            
                MOVE  K830-NME-MAIL-STREET  TO WS-K830-OLD-MAIL-STREET          
                MOVE  K830-NUM-MAIL-POSTAL  TO WS-K830-OLD-MAIL-POSTAL          
                IF K830-NUM-MAIL-ADDR = SPACES OR LOW-VALUES                    
                  IF (BE02F200-NEW-ADDR-NUM-LEVEL(1:2) =                        
                      K830-NUM-LVL-NUM)                AND                      
                     (BE02F200-NEW-ADDR-NUM-UNIT       =                        
                      K830-NUM-FLAT(5:5))              AND                      
                     (BE02F200-NEW-ADDR-POSTAL-CDE     =                        
                      K830-NUM-POSTAL)                                          
                      CONTINUE                                                  
                  ELSE                                                          
                     MOVE    K830-NUM-REGN             TO   L269-REG-NO         
                     PERFORM 5500-REWRITE-BP13K830-REC THRU 5500-EXIT           
                     IF WS-K800-FOUND = 'Y'                                     
                        IF K800-DTE-BALLOT = SPACES OR LOW-VALUES               
                           MOVE    'Y'     TO   WS-K838-AMIS                    
                        ELSE                                                    
                           MOVE    '1'     TO   WS-K838-AMIS                    
                        END-IF                                                  
                     ELSE                                                       
                        IF WS-K500-FOUND = 'Y'                                  
                           MOVE    '3'     TO   WS-K838-AMIS                    
                        END-IF                                                  
                     END-IF                                                     
                  END-IF                                                        
                ELSE                                                            
                  IF (BE02F200-NEW-ADDR-NUM-LEVEL(1:2) =                        
                      K830-NUM-MAIL-LVL-NUM)           AND                      
                     (BE02F200-NEW-ADDR-NUM-UNIT       =                        
                      K830-NUM-MAIL-FLAT(5:5))         AND                      
                     (BE02F200-NEW-ADDR-POSTAL-CDE     =                        
                      K830-NUM-MAIL-POSTAL)                                     
                     MOVE    K830-NUM-REGN             TO   L269-REG-NO         
                     MOVE    SPACES   TO K830-NUM-MAIL-ADDR                     
                     PERFORM 5500-REWRITE-BP13K830-REC  THRU 5500-EXIT          
                     IF WS-K800-FOUND = 'Y'                                     
                        MOVE    '1'      TO WS-K838-AMIS                        
                     ELSE                                                       
                        IF WS-K500-FOUND = 'Y'                                  
                           MOVE '3'      TO WS-K838-AMIS                        
                        END-IF                                                  
                     END-IF                                                     
                  ELSE                                                          
                     MOVE    K830-NUM-REGN             TO   L269-REG-NO         
                     PERFORM 5500-REWRITE-BP13K830-REC THRU 5500-EXIT           
                     IF WS-K800-FOUND = 'Y'                                     
                        MOVE    '2'      TO WS-K838-AMIS                        
                     ELSE                                                       
                        IF WS-K500-FOUND = 'Y'                                  
                           MOVE '4'      TO WS-K838-AMIS                        
                        END-IF                                                  
                     END-IF                                                     
                  END-IF                                                        
                END-IF                                                          
                                                                                
             WHEN 23                                                            
                  MOVE    K830-NUM-REGN            TO   L269A-UINFIN            
                  MOVE    'REGN NOT IN SOC ADDR'   TO   L269A-REMARKS           
                  PERFORM 7600-WRITE-BP13L269A     THRU 7600-EXIT               
                  DISPLAY 'RECORD NOT FOUND IN BP13K830 : '                     
                           K830-NUM-REGN                                        
             WHEN OTHER                                                         
                  DISPLAY 'READ ERROR BP13K830 : ' WS-K830-STATUS               
                  DISPLAY 'NRIC TO BE UPDATED  : ' K820-NUM-NRIC                
                  DISPLAY 'REGN READ           : ' K830-NUM-REGN                
                  PERFORM  9999-CLOSE-FILES THRU 9999-EXIT                      
             END-EVALUATE.                                                      
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       5350-READ-BP13K830-BY-REF.                                               
      *****************************************************************         
                                                                                
           READ  BP13K830 KEY IS K830-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K830-STATUS                                              
             WHEN 00                                                            
                ADD 1   TO  WS-K830-READ                                        
                MOVE  K830-NUM-RESIDENT-ADDR TO WS-OLD-K830-ADDR                
                MOVE  K830-NUM-MAIL-BLK      TO WS-K830-OLD-MAIL-BLK            
                MOVE  K830-NUM-MAIL-FLAT     TO WS-K830-OLD-MAIL-FLAT           
                MOVE  K830-NME-MAIL-STREET   TO WS-K830-OLD-MAIL-STREET         
                MOVE  K830-NUM-MAIL-POSTAL   TO WS-K830-OLD-MAIL-POSTAL         
                IF K830-NUM-MAIL-ADDR = SPACES OR LOW-VALUES                    
                  IF (BE02F200-NEW-ADDR-NUM-LEVEL(1:2) =                        
                      K830-NUM-LVL-NUM)                AND                      
                     (BE02F200-NEW-ADDR-NUM-UNIT       =                        
                      K830-NUM-FLAT(5:5))              AND                      
                     (BE02F200-NEW-ADDR-POSTAL-CDE     =                        
                      K830-NUM-POSTAL)                                          
                      CONTINUE                                                  
                  ELSE                                                          
                     MOVE    K830-KEY-FLD              TO   L269-REG-NO         
                     PERFORM 5500-REWRITE-BP13K830-REC THRU 5500-EXIT           
                     MOVE    '5'                       TO   WS-K838-AMIS        
                  END-IF                                                        
                ELSE                                                            
                  IF (BE02F200-NEW-ADDR-NUM-LEVEL(1:2) =                        
                      K830-NUM-MAIL-LVL-NUM)           AND                      
                     (BE02F200-NEW-ADDR-NUM-UNIT       =                        
                      K830-NUM-MAIL-FLAT(5:5))         AND                      
                     (BE02F200-NEW-ADDR-POSTAL-CDE     =                        
                      K830-NUM-MAIL-POSTAL)                                     
                     MOVE    K830-NUM-REGN             TO   L269-REG-NO         
                     MOVE SPACES  TO  K830-NUM-MAIL-ADDR                        
                     PERFORM 5500-REWRITE-BP13K830-REC  THRU 5500-EXIT          
                     MOVE    '5'                       TO   WS-K838-AMIS        
                  ELSE                                                          
                     MOVE    K830-NUM-REGN             TO   L269-REG-NO         
                     PERFORM 5500-REWRITE-BP13K830-REC  THRU 5500-EXIT          
                     MOVE    '6'                       TO   WS-K838-AMIS        
                  END-IF                                                        
                END-IF                                                          
             WHEN 23                                                            
                  MOVE    K830-KEY-FLD             TO   L269A-UINFIN            
                  MOVE    'REF-NO NOT IN SOC ADDR' TO   L269A-REMARKS           
                  PERFORM 7600-WRITE-BP13L269A     THRU 7600-EXIT               
                  DISPLAY 'RECORD NOT FOUND IN BP13K830 : '                     
                           K830-KEY-FLD                                         
             WHEN OTHER                                                         
                  DISPLAY 'READ ERROR BP13K830 : ' WS-K830-STATUS               
                  DISPLAY 'NRIC TO BE UPDATED  : ' K825-NUM-NRIC                
                  DISPLAY 'REF  READ           : ' K830-KEY-FLD                 
                  PERFORM  9999-CLOSE-FILES THRU 9999-EXIT                      
             END-EVALUATE.                                                      
                                                                                
       5350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5400-WRITE-BP13K838-REC.                                                 
      *****************************************************************         
                                                                                
           MOVE       SPACES TO BP13K838-REC.                                   
           INITIALIZE BP13K838-REC.                                             
                                                                                
           ADD    1                    TO  WS-NUM-COUNT.                        
           MOVE  'BP13D503'            TO  K838-NME-JOB.                        
           MOVE   WS-CURR-DTE          TO  K838-DTE-JOB-START.                  
           MOVE   WS-CURR-TIME         TO  K838-TME-JOB-START.                  
           MOVE   BE02F200-DTE-MHA-RUN TO  K838-DTE-MHA-RUN.                    
           MOVE   BE02F200-NUM-NRIC    TO  K838-NUM-NRIC.                       
           MOVE   BE02F200-DTE-MHA-REPORT-CHGE                                  
                                       TO  K838-DTE-MHA-REPORT-CHGE.            
           MOVE   WS-NUM-COUNT         TO  K838-NUM-COUNT.                      
           MOVE   BE02F200-HDB-PTY-INDICATOR                                    
                                       TO  K838-NUM-NEW-ADDR-TYPE.              
           MOVE   BE02F200-NEW-ADDR-INFO(1:110)                                 
                                       TO  K838-NEW-ADDR-INFO(2:110).           
           MOVE   WS-OLD-K830-ADDR                                              
                                       TO  K838-OLD-ADDR-INFO.                  
           MOVE   WS-K838-REGN         TO  K838-NUM-REGN.                       
           MOVE   WS-K838-NRIC2        TO  K838-NUM-NRIC2.                      
           MOVE   WS-K838-NRIC3        TO  K838-NUM-NRIC3.                      
           MOVE   WS-K838-NRIC4        TO  K838-NUM-NRIC4.                      
           MOVE   WS-K838-NT-ZONE      TO  K838-NUM-NT-ZONE.                    
           MOVE   WS-K838-FLAT-TYPE    TO  K838-NUM-FLAT-TYPE.                  
           MOVE   WS-K838-UPDATE       TO  K838-NUM-UPDATE.                     
           MOVE   WS-K838-AMIS         TO  K838-NUM-AMIS.                       
           WRITE  BP13K838-REC.                                                 
           ADD   1   TO WS-K838-WRITE.                                          
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5500-REWRITE-BP13K830-REC.                                               
      *****************************************************************         
                                                                                
           IF BE02F200-HDB-PTY-INDICATOR  = 'H'                                 
              MOVE 'BLK'                     TO  K830-NUM-BLK-HDR               
              MOVE BE02F200-NEW-ADDR-NUM-BLK TO  K830-NUM-BLK (5:6)             
           ELSE                                                                 
              MOVE BE02F200-NEW-ADDR-NUM-BLK TO  K830-NUM-BLK (1:10)            
           END-IF.                                                              
                                                                                
           MOVE BE02F200-HDB-PTY-INDICATOR TO K830-NUM-ADDR-TYPE.               
                                                                                
           IF BE02F200-HDB-PTY-INDICATOR  = 'H'                                 
              STRING '#'                                                        
                     BE02F200-NEW-ADDR-NUM-LEVEL (1:2)                          
                     '-'                                                        
                     BE02F200-NEW-ADDR-NUM-UNIT                                 
                     '  ' DELIMITED BY SIZE                                     
                INTO K830-NUM-FLAT                                              
              END-STRING                                                        
           ELSE                                                                 
              IF BE02F200-NEW-ADDR-NUM-LEVEL = SPACES OR LOW-VALUES             
                 MOVE SPACES TO K830-NUM-FLAT                                   
              ELSE                                                              
                 MOVE '#'  TO  K830-NUM-LVL-CHAR                                
                 MOVE BE02F200-NEW-ADDR-NUM-LEVEL(1:2) TO                       
                      K830-NUM-LVL-NUM                                          
                 MOVE '-'  TO  K830-NUM-LVL-DASH                                
                 MOVE BE02F200-NEW-ADDR-NUM-UNIT(1:4)  TO                       
                      K830-NUM-HSEMAIN                                          
                 MOVE BE02F200-NEW-ADDR-NUM-UNIT(5:1)  TO                       
                      K830-NUM-HSESUB                                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF BE02F200-HDB-PTY-INDICATOR  = 'H'                                 
              MOVE  BE02F200-NEW-ADDR-TEXT-LINE1 TO  K830-NME-STREET            
           ELSE                                                                 
              STRING BE02F200-NEW-ADDR-TEXT-LINE1                               
                     BE02F200-NEW-ADDR-TEXT-LINE2                               
                     DELIMITED BY SIZE                                          
                     INTO K830-NME-STREET-BLDG                                  
           END-IF.                                                              
                                                                                
           MOVE  BE02F200-NEW-ADDR-POSTAL-CDE TO  K830-NUM-POSTAL.              
           MOVE 'P13C269'                     TO  K830-NUM-USERID.              
           MOVE  WS-CURR-DTE                  TO  K830-DTE-UPDATE.              
                                                                                
           REWRITE BP13K830-REC.                                                
           MOVE   'U' TO  WS-K838-UPDATE.                                       
           ADD    1   TO  WS-K830-REWRITE.                                      
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       6000-READ-BP13K825.                                                      
      *****************************************************************         
                                                                                
           MOVE  'N'               TO  WS-K825-FOUND.                           
                                                                                
           READ BP13K825 KEY IS K825-NUM-NRIC.                                  
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN  00                                                             
                 ADD   1  TO WS-K825-READ                                       
                 MOVE 'N' TO WS-K838-UPDATE                                     
                 IF K825-NUM-RELATIONSHIP = '00'                                
                    MOVE 'Y'           TO WS-K825-FOUND                         
                 END-IF                                                         
           WHEN  02                                                             
                 ADD       1                     TO   WS-K825-DUP               
                 MOVE     'N'                    TO   WS-K838-UPDATE            
                 MOVE     K825-NUM-NRIC          TO   L269A-UINFIN              
                 MOVE    'DUP NRIC IN REQ OCCUP' TO   L269A-REMARKS             
                 PERFORM  7600-WRITE-BP13L269A   THRU 7600-EXIT                 
           WHEN  23                                                             
                 DISPLAY 'RECORD NOT FOUND BP13K825 : ' K825-NUM-NRIC           
                 MOVE    'N'                     TO   WS-K825-FOUND             
                 ADD      1                      TO   WS-K825-BYPASS            
           WHEN OTHER                                                           
                 DISPLAY 'READ ERROR BP13K825 : ' WS-K825-STATUS                
                 DISPLAY 'NRIC TO BE UPDATED  : ' K825-NUM-NRIC                 
                 PERFORM  9999-CLOSE-FILES THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6100-READ-BP13K730.                                                      
      *****************************************************************         
                                                                                
           MOVE 'N' TO WS-K730-FOUND.                                           
                                                                                
           READ BP13K730 KEY IS K730-KEY-NEW.                                   
                                                                                
           EVALUATE WS-K730-STATUS                                              
             WHEN  00                                                           
                 MOVE 'Y'                       TO   WS-K730-FOUND              
                 ADD  1                         TO   WS-K730-READ               
                                                                                
             WHEN  23                                                           
                 MOVE    'N'                        TO   WS-K730-FOUND          
                 MOVE     K825-NUM-NRIC             TO   L269A-UINFIN           
                 MOVE    'NRIC NOT IN REQUEST FILE' TO   L269A-REMARKS          
                 PERFORM  7600-WRITE-BP13L269A      THRU 7600-EXIT              
                 DISPLAY 'RECORD NOT FOUND BP13K730 : ' K730-KEY-NEW            
                 DISPLAY 'NRIC TO BE UPDATED        : ' K825-NUM-NRIC           
             WHEN OTHER                                                         
                 DISPLAY 'READ ERROR BP13K730 : ' WS-K730-STATUS                
                 DISPLAY 'NRIC TO BE UPDATED  : ' K825-NUM-NRIC                 
                 DISPLAY 'REF  READ           : ' K730-KEY-NEW                  
                 PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6500-READ-BP13K500.                                                      
      *****************************************************************         
                                                                                
           MOVE 'N' TO WS-K500-FOUND.                                           
                                                                                
           READ BP13K500 KEY IS K500-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K500-STATUS                                              
           WHEN  00                                                             
                 MOVE 'Y'                        TO   WS-K500-FOUND             
                 ADD   1                         TO   WS-K500-READ              
                                                                                
                 IF K500-DTE-BALLOT NOT = SPACES AND LOW-VALUES                 
                    MOVE K500-DTE-BALLOT (1:2)   TO   L269-BALLOT-CC            
                    MOVE K500-DTE-BALLOT (3:2)   TO   L269-BALLOT-YY            
                    MOVE K500-DTE-BALLOT (5:2)   TO   L269-BALLOT-MM            
                    MOVE '/'                     TO   L269-BALLOT-SLASH         
                 ELSE                                                           
                    MOVE SPACES                  TO   L269-BALLOT-YY            
                                                      L269-BALLOT-CC            
                                                      L269-BALLOT-MM            
                                                      L269-BALLOT-SLASH         
                 END-IF                                                         
                                                                                
                 IF K500-DTE-REQUEST NOT = SPACES AND LOW-VALUES                
                    MOVE  K500-DTE-REQUEST (1:4) TO   L269-REG-YY               
                    MOVE  K500-DTE-REQUEST (5:2) TO   L269-REG-MM               
                    MOVE  K500-DTE-REQUEST (7:2) TO   L269-REG-DD               
                    MOVE  '/'                    TO   L269-REG-SLASH1           
                    MOVE  '/'                    TO   L269-REG-SLASH2           
                 ELSE                                                           
                    MOVE  SPACES              TO   L269-REG-YY                  
                                                   L269-REG-MM                  
                                                   L269-REG-DD                  
                                                   L269-REG-SLASH1              
                                                   L269-REG-SLASH2              
                 END-IF                                                         
                 MOVE SPACES TO L269-BK-DATE                                    
                                L269-SA-DATE                                    
                                L269-TP-DATE                                    
                                                                                
           WHEN  23                                                             
                 MOVE    'N'                        TO   WS-K500-FOUND          
                 MOVE     K825-NUM-NRIC             TO   L269A-UINFIN           
                 MOVE    'NRIC NOT IN PREREGN FILE' TO   L269A-REMARKS          
                 PERFORM  7600-WRITE-BP13L269A      THRU 7600-EXIT              
                 DISPLAY 'RECORD NOT FOUND BP13K500 : ' K500-NUM-REGN           
                 DISPLAY 'NRIC TO BE UPDATED        : ' K825-NUM-NRIC           
           WHEN OTHER                                                           
                 DISPLAY 'READ ERROR BP13K500 : ' WS-K500-STATUS                
                 DISPLAY 'NRIC TO BE UPDATED  : ' K825-NUM-NRIC                 
                 DISPLAY 'REGN READ           : ' K500-NUM-REGN                 
                 PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       7000-MOVE-VALUES-TO-REPORT.                                              
      *****************************************************************         
                                                                                
      *--> GET CORR ADDR FROM BP13K830                                          
                                                                                
           MOVE  WS-SNO-L269       TO  L269-SNO.                                
           MOVE  WS-PG-CNT-L269    TO  L269-PAGE-NO.                            
           MOVE  WS-PRT-DD         TO  L269-PRT-DD.                             
           MOVE  WS-PRT-MM         TO  L269-PRT-MM.                             
           MOVE  WS-PRT-YY         TO  L269-PRT-YY.                             
           MOVE  BE02F200-NUM-NRIC TO  L269-NRIC.                               
           MOVE  WS-K830-OLD-MAIL-BLK    TO  L269-BLK-CORR.                     
           MOVE  WS-K830-OLD-MAIL-FLAT   TO  L269-NUM-FLAT-CORR.                
           MOVE  WS-K830-OLD-MAIL-STREET TO  L269-NME-STREET-CORR.              
           MOVE  WS-K830-OLD-MAIL-POSTAL TO  L269-NUM-POSTAL-CORR.              
                                                                                
           IF    WS-K830-OLD-MAIL-POSTAL = SPACES                               
                 MOVE  SPACES TO L269-NUM-POSTAL-CORR                           
           ELSE                                                                 
                 STRING 'S('                                                    
                         WS-K830-OLD-MAIL-POSTAL                                
                        ')' DELIMITED BY SIZE                                   
                   INTO  L269-NUM-POSTAL-CORR                                   
                 END-STRING                                                     
           END-IF.                                                              
                                                                                
           IF K830-DTE-UPDATE = SPACES OR LOW-VALUES OR ZEROES                  
              MOVE SPACES                  TO L269-UPD-DATE                     
           ELSE                                                                 
              MOVE K830-DTE-UPDATE  (1:4) TO L269-UPD-YY                        
              MOVE K830-DTE-UPDATE  (5:2) TO L269-UPD-MM                        
              MOVE K830-DTE-UPDATE  (7:2) TO L269-UPD-DD                        
              MOVE '/'                    TO L269-UPD-SLASH1                    
                                             L269-UPD-SLASH2                    
           END-IF.                                                              
                                                                                
                                                                                
      *--> GET OLD ADDR                                                         
                                                                                
           MOVE  K838-NUM-OLD-BLK      TO  L269-BLK-RES-O.                      
           MOVE  K838-NUM-OLD-FLAT     TO  L269-NUM-FLAT-O.                     
           MOVE  K838-NME-OLD-STREET   TO  L269-NME-STREET-O.                   
           IF    K838-NUM-OLD-POSTAL = SPACES                                   
                 MOVE  SPACES            TO  L269-NUM-POSTAL-O                  
           ELSE                                                                 
                 STRING 'S(' K838-NUM-OLD-POSTAL ')'                            
                             DELIMITED BY SIZE                                  
                             INTO  L269-NUM-POSTAL-O                            
                 END-STRING                                                     
           END-IF.                                                              
                                                                                
                                                                                
      *--> GET NEW ADDR                                                         
                                                                                
           IF   K838-NUM-NEW-ADDR-TYPE = 'H'                                    
                STRING 'BLK '                                                   
                        K838-NUM-NEW-BLK                                        
                        DELIMITED BY SIZE INTO  L269-BLK-RES-N                  
           ELSE                                                                 
                MOVE  K838-NUM-NEW-BLK      TO  L269-BLK-RES-N                  
           END-IF.                                                              
                                                                                
           IF K838-NUM-NEW-ADDR-TYPE = 'H'                                      
              STRING '#' K838-NUM-NEW-HSELVL                                    
                     '-' K838-NUM-NEW-HSEUNIT                                   
                     '  ' DELIMITED BY SIZE INTO L269-NUM-FLAT-N                
           ELSE                                                                 
              IF K838-NUM-NEW-HSELVL = SPACES OR LOW-VALUES                     
                 STRING K838-NUM-NEW-HSELVL                                     
                        K838-NUM-NEW-HSEUNIT                                    
                        '   ' DELIMITED BY SIZE INTO L269-NUM-FLAT-N            
              ELSE                                                              
                 STRING K838-NUM-NEW-HSELVL                                     
                        '-' K838-NUM-NEW-HSEUNIT                                
                        '  ' DELIMITED BY SIZE INTO L269-NUM-FLAT-N             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE  K838-NME-NEW-STREET-TXT1 TO L269-NME-STREET-N.                 
           IF    K838-NUM-NEW-POSTAL = SPACES                                   
                 MOVE  SPACES            TO  L269-NUM-POSTAL-N                  
           ELSE                                                                 
                 STRING 'S(' K838-NUM-NEW-POSTAL ')'                            
                         DELIMITED BY SIZE INTO  L269-NUM-POSTAL-N              
           END-IF.                                                              
                                                                                
           MOVE  K838-NUM-AMIS         TO  L269-AMIS.                           
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       7200-WRITE-BP13L269.                                                     
      *****************************************************************         
                                                                                
            IF WS-COUNT-L269 = 0 OR WS-COUNT-L269 > 50                          
               PERFORM 7300-PRINT-BP13L269-HDG THRU 7300-EXIT.                  
                                                                                
            PERFORM 7400-PRINT-BP13L269 THRU 7400-EXIT.                         
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       7300-PRINT-BP13L269-HDG.                                                 
      *****************************************************************         
                                                                                
           IF WS-COUNT-L269 > 50                                                
              MOVE ZERO  TO WS-COUNT-L269                                       
              ADD   1    TO WS-PG-CNT-L269.                                     
                                                                                
           WRITE BP13L269-REC  FROM         L269-PR-HEAD-01 AFTER PAGE.         
           WRITE BP13L269-REC  FROM         L269-PR-HEAD-02 AFTER 1.            
                                                                                
           WRITE BP13L269-REC  FROM         L269-PR-HEAD-03 AFTER 2.            
           WRITE BP13L269-REC  FROM         L269-PR-HEAD-04 AFTER 1.            
                                                                                
           WRITE BP13L269-REC  FROM         L269-DRAW-LINE  AFTER 1.            
                                                                                
           ADD   6              TO          WS-COUNT-L269.                      
                                                                                
       7300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       7400-PRINT-BP13L269.                                                     
      *****************************************************************         
                                                                                
            WRITE BP13L269-REC  FROM L269-PR-DETAILS   AFTER 2.                 
            WRITE BP13L269-REC  FROM L269-RES-ADDR2    AFTER 1.                 
            WRITE BP13L269-REC  FROM L269-POSTAL       AFTER 1.                 
                                                                                
            ADD   4             TO           WS-COUNT-L269.                     
            ADD   1             TO           WS-L269-WRITTEN.                   
            ADD   1             TO           WS-SNO-L269.                       
                                                                                
       7400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       7600-WRITE-BP13L269A.                                                    
      *****************************************************************         
                                                                                
           IF BE02F200-HDB-PTY-INDICATOR  = 'H'                                 
              STRING 'BLK ' BE02F200-NEW-ADDR-NUM-BLK                           
                 DELIMITED BY SIZE INTO  L269A-NEW-NUM-BLK                      
           ELSE                                                                 
              MOVE BE02F200-NEW-ADDR-NUM-BLK TO L269A-NEW-NUM-BLK               
           END-IF.                                                              
                                                                                
           IF BE02F200-HDB-PTY-INDICATOR  = 'H'                                 
              STRING '#'                                                        
                     BE02F200-NEW-ADDR-NUM-LEVEL                                
                     '-'                                                        
                     BE02F200-NEW-ADDR-NUM-UNIT                                 
                     '  ' DELIMITED BY SIZE                                     
                INTO L269A-NEW-FLAT                                             
              END-STRING                                                        
           ELSE                                                                 
              IF BE02F200-NEW-ADDR-NUM-LEVEL = SPACES OR LOW-VALUES             
                 STRING BE02F200-NEW-ADDR-NUM-LEVEL                             
                        BE02F200-NEW-ADDR-NUM-UNIT                              
                        '   ' DELIMITED BY SIZE                                 
                   INTO L269A-NEW-FLAT                                          
              ELSE                                                              
                 STRING BE02F200-NEW-ADDR-NUM-LEVEL                             
                        '-' BE02F200-NEW-ADDR-NUM-UNIT                          
                        '  ' DELIMITED BY SIZE                                  
                   INTO L269A-NEW-FLAT                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE BE02F200-NEW-ADDR-TEXT-LINE1 TO L269A-NEW-STREET-NME.           
                                                                                
           IF BE02F200-NEW-ADDR-POSTAL-CDE = SPACES                             
              MOVE SPACES TO L269A-NEW-POSTAL-CDE                               
           ELSE                                                                 
              STRING 'S('                                                       
                      BE02F200-NEW-ADDR-POSTAL-CDE                              
                     ')' DELIMITED BY SIZE                                      
              INTO L269A-NEW-POSTAL-CDE                                         
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           IF WS-COUNT-L269A = 0 OR WS-COUNT-L269A > 50                         
              PERFORM 7700-PRINT-BP13L269A-HDG   THRU 7700-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 7800-PRINT-BP13L269A          THRU 7800-EXIT.                
                                                                                
       7600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       7700-PRINT-BP13L269A-HDG.                                                
      *****************************************************************         
                                                                                
           IF WS-COUNT-L269A > 50                                               
              MOVE ZERO  TO WS-COUNT-L269A                                      
              ADD   1    TO WS-PG-CNT-L269A.                                    
                                                                                
           MOVE  WS-PG-CNT-L269A   TO  L269A-PAGE-NO.                           
           MOVE  WS-PRT-DD         TO  L269A-PRT-DD.                            
           MOVE  WS-PRT-MM         TO  L269A-PRT-MM.                            
           MOVE  WS-PRT-YY         TO  L269A-PRT-YY.                            
                                                                                
           WRITE P13L269A-REC  FROM         L269A-PR-HEAD-01 AFTER PAGE.        
           WRITE P13L269A-REC  FROM         L269A-PR-HEAD-02 AFTER 1.           
                                                                                
           WRITE P13L269A-REC  FROM         L269A-PR-HEAD-03 AFTER 2.           
                                                                                
           WRITE P13L269A-REC  FROM         L269-DRAW-LINE   AFTER 1.           
                                                                                
           ADD   5              TO          WS-COUNT-L269A.                     
                                                                                
       7700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       7800-PRINT-BP13L269A.                                                    
      *****************************************************************         
                                                                                
            MOVE  WS-SNO-L269A      TO  L269A-SNO.                              
            WRITE P13L269A-REC  FROM L269A-PR-DETAILS   AFTER 2.                
                                                                                
            ADD   2             TO           WS-COUNT-L269A.                    
            ADD   1             TO           WS-SNO-L269A.                      
            ADD   1             TO           WS-L269A-WRITTEN.                  
                                                                                
       7800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8000-WRITE-START-BE02K900.                                               
      *****************************************************************         
                                                                                
           MOVE    SPACES      TO  BE02K900-REC.                                
           MOVE  'BP13D503'    TO  BE02K900-JOB-NAME.                           
           MOVE   WS-CURR-DTE  TO  BE02K900-JOB-START-DATE.                     
           MOVE  '0'           TO  BE02K900-REC-TYPE.                           
           MOVE   WS-CURR-TIME TO  BE02K900-JOB-START-TIME.                     
           MOVE  'R'           TO  BE02K900-ACCESS-TYPE.                        
           MOVE   ZEROES       TO  BE02K900-NUM-REC-READ.                       
           MOVE   ZEROES       TO  BE02K900-NUM-REC-PROCESSED.                  
           MOVE   ZEROES       TO  BE02K900-NUM-REC-UPDATED.                    
           MOVE   ZEROES       TO  BE02K900-NUM-WORKING-DAYS.                   
                                                                                
           WRITE  BE02K900-REC.                                                 
           ADD   1    TO  WS-K900-WRITE.                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8100-WRITE-FINAL-BE02K900.                                               
      *****************************************************************         
                                                                                
           MOVE    SPACES         TO  BE02K900-REC.                             
           MOVE  'BP13D503'       TO  BE02K900-JOB-NAME.                        
           MOVE   WS-CURR-DTE     TO  BE02K900-JOB-END-DATE.                    
           MOVE  '1'              TO  BE02K900-REC-TYPE.                        
           MOVE   WS-CURR-TIME    TO  BE02K900-JOB-END-TIME.                    
           MOVE  'R'              TO  BE02K900-ACCESS-TYPE.                     
                                                                                
           MOVE   WS-F100-READ    TO  BE02K900-NUM-REC-READ.                    
           MOVE   WS-K838-WRITE   TO  BE02K900-NUM-REC-PROCESSED.               
           MOVE   WS-K830-REWRITE TO  BE02K900-NUM-REC-UPDATED.                 
           MOVE   '00'            TO  BE02K900-NUM-WORKING-DAYS.                
                                                                                
           WRITE  BE02K900-REC.                                                 
           ADD   1    TO  WS-K900-WRITE.                                        
                                                                                
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9999-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BE02F200                                                       
                 BP13K500                                                       
                 BP13K730                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K825                                                       
                 BP13K830                                                       
                 BE02K900                                                       
                 BP13K838                                                       
                 BP13L269                                                       
                 P13L269A.                                                      
                                                                                
           IF WS-K900-STATUS NOT = 0                                            
              DISPLAY ' ERROR ENCOUNTERED IN CLOSING FILE BE02K900 : '          
                                              WS-K900-STATUS                    
              MOVE WS-K900-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K500-STATUS NOT = 0                                            
              DISPLAY ' ERROR ENCOUNTERED IN CLOSING FILE  BP13K500 : '         
                                              WS-K500-STATUS                    
              MOVE WS-K500-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K730-STATUS NOT = 0                                            
              DISPLAY ' ERROR ENCOUNTERED IN CLOSING FILE  BP13K730 : '         
                                              WS-K730-STATUS                    
              MOVE WS-K730-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K800-STATUS NOT = 0                                            
              DISPLAY ' ERROR ENCOUNTERED IN CLOSING FILE  BP13K800 : '         
                                              WS-K800-STATUS                    
              MOVE WS-K800-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K820-STATUS NOT = 0                                            
              DISPLAY ' ERROR ENCOUNTERED IN CLOSING FILE  BP13K820 : '         
                                              WS-K820-STATUS                    
              MOVE WS-K820-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K825-STATUS NOT = 0                                            
              DISPLAY ' ERROR ENCOUNTERED IN CLOSING FILE  BP13K825 : '         
                                              WS-K825-STATUS                    
              MOVE WS-K825-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K830-STATUS NOT = 0                                            
              DISPLAY ' ERROR ENCOUNTERED IN CLOSING FILE BP13K830  : '         
                                              WS-K830-STATUS                    
              MOVE WS-K830-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K838-STATUS NOT = 0                                            
              DISPLAY ' ERROR ENCOUNTERED IN CLOSING FILE BP13K838 : '          
                                              WS-K838-STATUS                    
              MOVE WS-K838-STATUS TO RETURN-CODE.                               
                                                                                
           DISPLAY '----------------------------------------'.                  
           DISPLAY '               BP13C269                 '.                  
           DISPLAY '----------------------------------------'.                  
                                                                                
           DISPLAY ' NO OF BE02F200 RECS READ     : ' WS-F100-READ.             
           DISPLAY ' NO OF BP13K500 RECS READ     : ' WS-K500-READ.             
           DISPLAY ' NO OF BP13K730 RECS READ     : ' WS-K730-READ.             
           DISPLAY ' NO OF BP13K820 RECS READ     : ' WS-K820-READ.             
           DISPLAY ' NO OF BP13K820 RECS 2ND READ : ' WS-K820-2ND-READ.         
           DISPLAY ' NO OF BP13K820 RECS DUP      : ' WS-K820-DUP.              
           DISPLAY ' NO OF BP13K820 RECS BYPASS   : ' WS-K820-BYPASS.           
           DISPLAY ' NO OF BP13K825 RECS READ     : ' WS-K825-READ.             
           DISPLAY ' NO OF BP13K825 RECS 2ND READ : ' WS-K825-2ND-READ.         
           DISPLAY ' NO OF BP13K825 RECS DUP      : ' WS-K825-DUP.              
           DISPLAY ' NO OF BP13K825 RECS BYPASS   : ' WS-K825-BYPASS.           
           DISPLAY ' NO OF BP13K830 RECS READ     : ' WS-K830-READ.             
           DISPLAY ' NO OF BP13K830 RECS REWRITTEN: ' WS-K830-REWRITE.          
           DISPLAY ' NO OF BP13K838 RECS WRITTEN  : ' WS-K838-WRITE.            
           DISPLAY ' NO OF BE02K900 RECS WRITTEN  : ' WS-K900-WRITE.            
           DISPLAY ' NO OF BP13L269 RECS WRITTEN  : ' WS-L269-WRITTEN.          
           DISPLAY ' NO OF P13L269A RECS WRITTEN  : ' WS-L269A-WRITTEN.         
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
