      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.   BP13C397.                                                  
       AUTHOR.       JULITO A.MACAPAGAL.                                        
       DATE-WRITTEN. 02/08/2002.                                                
      *****************************************************************         
      *                                                               *         
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                           *         
      *                                                               *         
      *  SYSTEM ID   : BP13                                           *         
      *                                                               *         
      *  OBJECTIVE   :                                                *         
      *  1. TO PRINT MONTHLY REPORT ON KEYS BORROWED NOT              *         
      *     RETURNED AFTER 1 MONTH                                    *         
      *                                                               *         
      *  INPUT FILE(S):                                               *         
      *  1. BP13F435 (FROM BP13K435)                                  *         
      *                                                               *         
      *  OUTPUT FILE(S):                                              *         
      *  1. BP13L397                                                  *         
      *===============================================================*         
      * CHNGE #    BY    DATE     DESCRIPTION                         *         
      * --------- ------ -------- ------------------------------------*         
      * BP132128  JAM1   03082002 NEW PROGRAM                         *         
      * BP132180  JAM1   22082002 TO DELETE RECORDS WITH BORROWED DATE*         
      *                           AND RETURNED DATE.                  *         
      * BP132180  MTD    1209200D TO ADD KEY LOCATION IN THE REPORT   *         
      * BP132449  MTD    03022004 HANDLE CHANGES IN BP13K435          *         
      * BP132486  MTD    12032004 CHANGE BP13K437 TO BP13K431         *         
      * BP132648  LMS    25012005 TO PAGE BREAK BY KEY LOCATION       *         
      * BP139276  KAC1   05042024 REPLACE LIM KOK CHUN BY             *         
      *                                   KATHIJAH_BEE_ALI_MOHAMED    *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F435 ASSIGN TO BP13F435                                   
                  FILE STATUS IS WS-F435-STATUS.                                
                                                                                
           SELECT BP13K431 ASSIGN TO BP13K431                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K431-KEY-FLD                               
                  FILE STATUS     IS WS-K431-STATUS.                            
                                                                                
           SELECT BP13L397 ASSIGN TO BP13L397.                                  
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13K431                                                              
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K431.                                                   00320000
                                                                                
       FD BP13F435                                                              
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13F435.                                                           
                                                                                
       FD  BP13L397                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS OMITTED.                                               
       01  BP13L397-REC     PIC X(132).                                         
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-STATUS.                                                           
           05 WS-K431-STATUS    PIC  9(2) VALUE 0.                              
           05 WS-F435-STATUS    PIC  9(2) VALUE 0.                              
           05 WS-START-STATUS   PIC  9(2) VALUE 0.                              
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F435-CNT-READ     PIC 9(04) VALUE ZEROES.                      
           05 WS-MAX-LINES         PIC 9(05) VALUE 58.                          
           05 WS-LINE-CTR          PIC 9(05) VALUE 99.                          
           05 WS-SER-CTR           PIC 9(03) VALUE ZEROES.                      
           05 WS-NOT-DUE-CTR       PIC 9(04) VALUE ZEROES.                      
           05 WS-PAGE-CTR          PIC 9(04) VALUE ZEROES.                      
           05 WS-TOT-CTR           PIC 9(04) VALUE ZEROES.                      
           05 WS-TOT-BLK           PIC 9(04) VALUE ZEROES.                      
           05 WS-AGED-PERIOD       PIC S9(05) VALUE ZEROES.                     
           05 WS-LINES-REMAIN      PIC S9(05) VALUE ZEROES.                     
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05 WS-F435-FLAG      PIC  X(1) VALUE 'N'.                            
              88 WS-F435-EOF              VALUE 'Y'.                            
                                                                                
       01  WS-SYSTEM-DATETIME.                                                  
           05  WS-SYS-DATE             PIC 9(08) VALUE ZEROES.                  
           05  WS-SYS-TIME             PIC 9(08) VALUE ZEROES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
                                                                                
       01  WS-DISP-DATE.                                                        
           05  WS-DISP-DD              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-MM              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-CCYY            PIC X(04) VALUE SPACES.                  
                                                                                
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-F435-DTE-KEY-BORROW  PIC X(08).                               
           05  WS-F435-DTE-KEY-BORROW-N REDEFINES                               
               WS-F435-DTE-KEY-BORROW  PIC 9(08).                               
           05  WS-SAVE-BLK             PIC X(05) VALUE SPACES.                  
           05  WS-SAVE-STREET          PIC X(32) VALUE SPACES.                  
           05  WS-SAVE-KEY-LOCN        PIC X(04) VALUE SPACES.                  
                                                                                
       01  WS-LINER                 PIC X(132) VALUE ALL '~'.                   
                                                                                
       01  WS-L397-LINER1.                                                      
           10  FILLER               PIC X(132) VALUE ALL "-".                   
                                                                                
       01  WS-L397-TITLE1.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L397'.              
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'HDB3'.                  
           05  FILLER              PIC X(36)     VALUE SPACES.                  
           05  FILLER              PIC X(26)     VALUE                          
               'SOC - SYSTEM OF COMMITMENT'.                                    
           05  FILLER              PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  WS-L397-PAGENO      PIC ZZZZ.                                    
           05  FILLER              PIC X(13)     VALUE SPACES.                  
                                                                                
       01  WS-L397-TITLE2.                                                      
           10  FILLER               PIC X(46) VALUE SPACES.                     
           10  FILLER               PIC X(40) VALUE                             
               '(SALES OPERATIONS UNIT - AGREEMENT TEAM)'.                      
           10  FILLER               PIC X(46) VALUE SPACES.                     
                                                                                
       01  WS-L397-TITLE3.                                                      
           10  FILLER               PIC X(34) VALUE SPACES.                     
           10  FILLER               PIC X(44) VALUE                             
               'MONTHLY REPORT ON KEYS BORROWED NOT RETURNED'.                  
           10  FILLER               PIC X(20) VALUE                             
               ' AFTER 1 MONTH'.                                                
           10  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  WS-L397-TITLE4.                                                      
           05  FILLER               PIC X(29) VALUE                             
               'TO   :  SALES OPERATIONS UNIT'.                                 
           05  FILLER               PIC X(79) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE 'DATE : '.                  
           05  WS-L397-CUR-DATE.                                                
              10  WS-L397-CUR-DD    PIC X(02).                                  
              10  FILLER            PIC X(01) VALUE '/'.                        
              10  WS-L397-CUR-MM    PIC X(02).                                  
              10  FILLER            PIC X(01) VALUE '/'.                        
              10  WS-L397-CUR-CC    PIC X(02).                                  
              10  WS-L397-CUR-YY    PIC X(02).                                  
           05  FILLER               PIC X(07) VALUE SPACES.                     
                                                                                
       01  WS-L397-TITLE5.                                                      
           05  FILLER               PIC X(28) VALUE                             
              '        AGREEMENT TEAM      '.                                   
           05  FILLER               PIC X(104) VALUE SPACES.                    
                                                                                
       01  WS-L397-TITLE6.                                                      
           05  FILLER               PIC X(40) VALUE                             
              '        SEEO (KATHIJAH BEE ALI MOHAMED)'.                        
           05  FILLER               PIC X(104) VALUE SPACES.                    
                                                                                
       01  WS-L397-HEADER1.                                                     
           10  FILLER               PIC X(28) VALUE                             
              'SERIAL BLOCK/STREET ADDRESS '.                                   
           10  FILLER               PIC X(51) VALUE                             
              '                              KEY                  '.            
           10  FILLER               PIC X(27) VALUE                             
              'DATE         AGING PERIOD  '.                                    
           10  FILLER               PIC X(26) VALUE SPACES.                     
                                                                                
       01  WS-L397-HEADER2.                                                     
           10  FILLER               PIC X(28) VALUE                             
              'NO.           UNIT          '.                                   
           10  FILLER               PIC X(51) VALUE                             
              'BORROWER''S NAME             LOCATION     TEL NO.  '.            
           10  FILLER               PIC X(27) VALUE                             
              'BORROWED        (DAYS)     '.                                    
           10  FILLER               PIC X(26) VALUE SPACES.                     
                                                                                
       01  WS-L397-HEADER3.                                                     
           10  FILLER               PIC X(28) VALUE                             
              '---    ------------------   '.                                   
           10  FILLER               PIC X(51) VALUE                             
              '-------------------------   --------     --------  '.            
           10  FILLER               PIC X(27) VALUE                             
              '----------   ------------ '.                                     
           10  FILLER               PIC X(26) VALUE SPACES.                     
                                                                                
       01  WS-L397-BLK-DETL.                                                    
           10  FILLER               PIC X(08) VALUE SPACES.                     
           10  PRT-BLK              PIC X(05).                                  
           10  FILLER               PIC X(01) VALUE SPACES.                     
           10  PRT-STREET           PIC X(32).                                  
           10  PRT-BLK-CONT         PIC X(11) VALUE SPACES.                     
           10  FILLER               PIC X(66) VALUE SPACES.                     
                                                                                
       01  WS-L397-UNT-DETL.                                                    
           10  PRT-SNO              PIC ZZ9.                                    
           10  FILLER               PIC X(11) VALUE SPACES.                     
           10  PRT-LEVEL-UNIT       PIC X(11).                                  
           10  FILLER               PIC X(03) VALUE SPACES.                     
           10  PRT-BORROWER         PIC X(25).                                  
           10  FILLER               PIC X(05) VALUE SPACES.                     
           10  PRT-KEY-LOCN         PIC X(04).                                  
           10  FILLER               PIC X(07) VALUE SPACES.                     
           10  PRT-TELNO            PIC X(08).                                  
           10  FILLER               PIC X(02) VALUE SPACES.                     
           10  PRT-BORROW-DATE      PIC X(10).                                  
           10  FILLER               PIC X(07) VALUE SPACES.                     
           10  PRT-AGE-PERIOD       PIC ZZ9.                                    
           10  FILLER               PIC X(33) VALUE SPACES.                     
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION.                                                      
      *------------------*                                                      
                                                                                
       0000-MAIN-CONTROL.                                                       
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-F435-READ     THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS-PARA  THRU 3000-EXIT                            
                   UNTIL WS-F435-EOF.                                           
           PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                             
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------*                                                         
       1000-OPEN-FILES.                                                         
      *---------------*                                                         
                                                                                
           OPEN INPUT  BP13F435                                                 
                       BP13K431                                                 
                OUTPUT BP13L397.                                                
                                                                                
           IF WS-K431-STATUS NOT = ZEROES                                       
              DISPLAY 'OPEN BP13K431 FAIL STATUS = ' WS-K431-STATUS             
              MOVE WS-K431-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF WS-F435-STATUS NOT = ZEROES                                       
              DISPLAY 'OPEN BP13F435 FAIL STATUS = ' WS-F435-STATUS             
              MOVE WS-F435-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATETIME.                  
           MOVE WS-SYS-DATE(1:4)        TO WS-DISP-CCYY.                        
           MOVE WS-SYS-DATE(5:2)        TO WS-DISP-MM.                          
           MOVE WS-SYS-DATE(7:2)        TO WS-DISP-DD.                          
           MOVE WS-DISP-DATE            TO WS-L397-CUR-DATE.                    
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------*                                                          
       2000-F435-READ.                                                          
      *--------------*                                                          
            READ BP13F435                                                       
                 AT END MOVE 'Y' TO WS-F435-FLAG                                
                 GO TO 2000-EXIT.                                               
            ADD 1 TO WS-F435-CNT-READ.                                          
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       3000-PROCESS-PARA.                                                       
      *-----------------*                                                       
                                                                                
           IF F435-DTE-KEY-BORROW NOT = SPACES AND LOW-VALUES AND               
                                        ZEROES                                  
              MOVE F435-DTE-KEY-BORROW TO WS-F435-DTE-KEY-BORROW                
              COMPUTE WS-AGED-PERIOD =                                          
                 FUNCTION INTEGER-OF-DATE(WS-SYS-DATE) -                        
                 FUNCTION INTEGER-OF-DATE(WS-F435-DTE-KEY-BORROW-N)             
              IF WS-AGED-PERIOD >= 30                                           
                 PERFORM 3100-PRT-F435-REC THRU 3100-EXIT                       
              ELSE                                                              
                 ADD 1 TO WS-NOT-DUE-CTR                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-F435-READ     THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------*                                                   
       3100-PRT-F435-REC.                                                       
      *---------------------*                                                   
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINES OR                                     
              F435-NUM-KEY-LOCN NOT = WS-SAVE-KEY-LOCN                          
              PERFORM 3200-PRT-HEADINGS THRU 3200-EXIT                          
           END-IF.                                                              
           ADD 1 TO WS-TOT-CTR.                                                 
           IF F435-NUM-KEY-LOCN NOT = WS-SAVE-KEY-LOCN OR                       
              F435-NUM-BLK      NOT = WS-SAVE-BLK      OR                       
              F435-NME-STREET   NOT = WS-SAVE-STREET                            
              PERFORM 3400-PRT-BLK-DETLS THRU 3400-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE SPACES         TO WS-L397-UNT-DETL.                             
           MOVE WS-AGED-PERIOD TO PRT-AGE-PERIOD.                               
           STRING F435-DTE-KEY-BORROW(7:2) '/'                                  
                  F435-DTE-KEY-BORROW(5:2) '/'                                  
                  F435-DTE-KEY-BORROW(1:4) DELIMITED BY SIZE                    
             INTO PRT-BORROW-DATE.                                              
                                                                                
      **-- GET BORROWER NAME AND TEL NUMBER                                     
           MOVE F435-NUM-BORROWER       TO K431-NUM-BORROWER.                   
           PERFORM 3110-READ-BP13K431       THRU 3110-EXIT.                     
           MOVE K431-NME-BORROWER    TO PRT-BORROWER.                           
           PERFORM 3120-GET-TEL-NUMBER      THRU 3120-EXIT.                     
                                                                                
           MOVE F435-NUM-KEY-LOCN    TO PRT-KEY-LOCN.                           
           MOVE SPACES TO PRT-LEVEL-UNIT.                                       
           IF F435-NUM-UNIT-MAIN NOT = SPACES AND LOW-VALUES                    
              IF F435-NUM-UNIT-SUB NOT = SPACES AND LOW-VALUES                  
                 STRING F435-NUM-LEVEL '-'                                      
                        F435-NUM-UNIT-MAIN '-'                                  
                        F435-NUM-UNIT-SUB DELIMITED BY SIZE                     
                   INTO PRT-LEVEL-UNIT                                          
              ELSE                                                              
                 STRING F435-NUM-LEVEL '-'                                      
                        F435-NUM-UNIT-MAIN                                      
                        '    '            DELIMITED BY SIZE                     
                   INTO PRT-LEVEL-UNIT                                          
           ELSE                                                                 
              IF F435-NUM-UNIT-SUB NOT = SPACES AND LOW-VALUES                  
                 STRING F435-NUM-LEVEL '-'                                      
                        F435-NUM-UNIT-MAIN '-'                                  
                        F435-NUM-UNIT-SUB DELIMITED BY SIZE                     
                   INTO PRT-LEVEL-UNIT                                          
              END-IF                                                            
           END-IF.                                                              
           PERFORM 3300-PRT-UNT-DETLS THRU 3300-EXIT.                           
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------*                                                    
       3110-READ-BP13K431.                                                      
      *--------------------*                                                    
                                                                                
           READ BP13K431.                                                       
           EVALUATE WS-K431-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   MOVE SPACES     TO BP13K431-REC                              
                   DISPLAY 'RECORD NOT FOUND IN BP13K431, KEY: '                
                            F435-NUM-BORROWER                                   
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K431,STATUS ' WS-K431-STATUS         
                   MOVE WS-K431-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3120-GET-TEL-NUMBER.                                                     
      *--------------------*                                                    
                                                                                
           IF K431-NUM-TEL-OFFICE NOT = SPACES AND LOW-VALUES                   
              MOVE K431-NUM-TEL-OFFICE      TO PRT-TELNO                        
           ELSE                                                                 
              IF K431-NUM-TEL-HP NOT = SPACES AND LOW-VALUES                    
                 MOVE K431-NUM-TEL-HP       TO PRT-TELNO                        
              ELSE                                                              
                 IF K431-NUM-TEL-PAGER NOT = SPACES AND LOW-VALUES              
                    MOVE K431-NUM-TEL-PAGER TO PRT-TELNO                        
                 ELSE                                                           
                    MOVE SPACES             TO PRT-TELNO                        
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       3200-PRT-HEADINGS.                                                       
      *------------------*                                                      
                                                                                
           ADD 1 TO WS-PAGE-CTR.                                                
           MOVE WS-PAGE-CTR TO WS-L397-PAGENO.                                  
           WRITE BP13L397-REC FROM WS-L397-TITLE1 AFTER PAGE.                   
           WRITE BP13L397-REC FROM WS-L397-TITLE2 AFTER 1.                      
           WRITE BP13L397-REC FROM WS-L397-TITLE3 AFTER 1.                      
           WRITE BP13L397-REC FROM WS-L397-TITLE4 AFTER 2.                      
           WRITE BP13L397-REC FROM WS-L397-TITLE5 AFTER 1.                      
           WRITE BP13L397-REC FROM WS-L397-TITLE6 AFTER 1.                      
           WRITE BP13L397-REC FROM WS-L397-HEADER1 AFTER 2.                     
           WRITE BP13L397-REC FROM WS-L397-HEADER2 AFTER 1.                     
           WRITE BP13L397-REC FROM WS-L397-HEADER3 AFTER 1.                     
           MOVE 11 TO WS-LINE-CTR.                                              
           IF F435-NUM-KEY-LOCN = WS-SAVE-KEY-LOCN                              
              IF WS-PAGE-CTR > 1                                                
                 MOVE '- CONTINUED' TO PRT-BLK-CONT                             
                 MOVE WS-SAVE-BLK TO PRT-BLK                                    
                 MOVE WS-SAVE-STREET TO PRT-STREET                              
                 MOVE WS-SAVE-KEY-LOCN TO PRT-KEY-LOCN                          
                 WRITE BP13L397-REC FROM WS-L397-BLK-DETL AFTER 2               
                 ADD 2 TO WS-LINE-CTR                                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       3300-PRT-UNT-DETLS.                                                      
      *------------------*                                                      
                                                                                
           ADD 1 TO WS-SER-CTR.                                                 
           MOVE WS-SER-CTR         TO PRT-SNO.                                  
           IF WS-LINE-CTR = 11                                                  
              WRITE BP13L397-REC FROM WS-L397-UNT-DETL AFTER 2                  
              ADD 2 TO WS-LINE-CTR                                              
           ELSE                                                                 
              WRITE BP13L397-REC FROM WS-L397-UNT-DETL AFTER 1                  
              ADD 1 TO WS-LINE-CTR                                              
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       3400-PRT-BLK-DETLS.                                                      
      *-----------------*                                                       
           IF F435-NUM-BLK      NOT = WS-SAVE-BLK      OR                       
              F435-NME-STREET   NOT = WS-SAVE-STREET                            
              ADD  1 TO WS-TOT-BLK                                              
           END-IF.                                                              
           COMPUTE WS-LINES-REMAIN = WS-MAX-LINES - WS-LINE-CTR                 
           IF WS-LINES-REMAIN < 2                                               
              PERFORM 3200-PRT-HEADINGS THRU 3200-EXIT                          
           END-IF.                                                              
           MOVE SPACES TO PRT-BLK-CONT.                                         
           MOVE F435-NUM-BLK         TO PRT-BLK, WS-SAVE-BLK.                   
           MOVE F435-NME-STREET      TO PRT-STREET, WS-SAVE-STREET.             
           MOVE F435-NUM-KEY-LOCN    TO PRT-KEY-LOCN, WS-SAVE-KEY-LOCN.         
           WRITE BP13L397-REC FROM WS-L397-BLK-DETL AFTER 2.                    
           MOVE 0 TO WS-SER-CTR.                                                
           ADD 2 TO WS-LINE-CTR.                                                
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       9000-CLOSE-FILES.                                                        
      *----------------*                                                        
                                                                                
           MOVE SPACES TO BP13L397-REC.                                         
           IF WS-LINE-CTR > WS-MAX-LINES                                        
              PERFORM 3200-PRT-HEADINGS THRU 3200-EXIT                          
              STRING 'TOTALS:  UNITS = ' WS-TOT-CTR ', BLOCKS = '               
                      WS-TOT-BLK DELIMITED BY SIZE                              
                 INTO BP13L397-REC                                              
              WRITE BP13L397-REC                                                
           ELSE                                                                 
              MOVE ALL '-' TO BP13L397-REC                                      
              WRITE BP13L397-REC                                                
              MOVE SPACES TO BP13L397-REC                                       
              STRING 'TOTALS:  UNITS = ' WS-TOT-CTR '  BLOCKS = '               
                      WS-TOT-BLK DELIMITED BY SIZE                              
                 INTO BP13L397-REC                                              
              WRITE BP13L397-REC                                                
           END-IF.                                                              
                                                                                
           MOVE '>>>>>>>>>> RUN STATISTICS <<<<<<<<<<<<'                        
                                          TO BP13L397-REC.                      
           WRITE BP13L397-REC AFTER PAGE.                                       
           DISPLAY BP13L397-REC.                                                
           MOVE 'PROGRAM ID   : BP13C397' TO BP13L397-REC.                      
           WRITE BP13L397-REC AFTER 1.                                          
           DISPLAY BP13L397-REC.                                                
           MOVE SPACES TO BP13L397-REC.                                         
           STRING 'RUN DATE     : ' WS-DISP-DATE DELIMITED BY SIZE              
                INTO BP13L397-REC.                                              
           WRITE BP13L397-REC AFTER 1.                                          
           DISPLAY BP13L397-REC.                                                
           MOVE SPACES TO BP13L397-REC.                                         
           WRITE BP13L397-REC AFTER 1.                                          
           DISPLAY BP13L397-REC.                                                
           MOVE SPACES TO BP13L397-REC.                                         
           STRING 'NO OF RECS READ FROM BP13F435  :' WS-F435-CNT-READ           
                DELIMITED BY SIZE INTO BP13L397-REC.                            
           WRITE BP13L397-REC AFTER 1.                                          
           DISPLAY BP13L397-REC.                                                
           MOVE SPACES TO BP13L397-REC.                                         
           STRING 'NO OF UNITS W/O KEY RETURN DATE  '                           
                DELIMITED BY SIZE INTO BP13L397-REC.                            
           WRITE BP13L397-REC AFTER 1.                                          
           DISPLAY BP13L397-REC.                                                
           MOVE SPACES TO BP13L397-REC.                                         
           STRING '   30 DAYS AFTER BORROWED DATE :' WS-TOT-CTR                 
                DELIMITED BY SIZE INTO BP13L397-REC.                            
           WRITE BP13L397-REC AFTER 1.                                          
           DISPLAY BP13L397-REC.                                                
           MOVE SPACES TO BP13L397-REC.                                         
           STRING 'NO OF UNITS W/ KEY NOT YET DUE :' WS-NOT-DUE-CTR             
                DELIMITED BY SIZE INTO BP13L397-REC.                            
           WRITE BP13L397-REC AFTER 1.                                          
           DISPLAY BP13L397-REC.                                                
           MOVE SPACES TO BP13L397-REC.                                         
           STRING 'TOTAL NUMBER OF BLOCKS         :' WS-TOT-BLK                 
                DELIMITED BY SIZE INTO BP13L397-REC.                            
           WRITE BP13L397-REC AFTER 1.                                          
           DISPLAY BP13L397-REC.                                                
           MOVE '>>>>>>>>>> EOF BP13C397 <<<<<<<<<<<<<<'                        
                                          TO BP13L397-REC.                      
           WRITE BP13L397-REC AFTER 1.                                          
           DISPLAY BP13L397-REC.                                                
                                                                                
           CLOSE  BP13F435, BP13L397, BP13K431.                                 
                                                                                
                                                                                
           IF WS-K431-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K431 FAIL STATUS = ' WS-K431-STATUS            
           END-IF.                                                              
                                                                                
           IF WS-F435-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13F435 FAIL STATUS = ' WS-F435-STATUS            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
