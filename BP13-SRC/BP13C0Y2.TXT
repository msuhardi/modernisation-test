       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C0Y2.                                                  
      *DATE-WRITTEN. 09 MAY 2012.                                               
      *AUTHOR.       ALWYN BENNY.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) Email wiil be sent to the csm with hle in   *            
      *                LEAS  not match with soc and loan tag = 'b' *            
      *                or 'n'                                      *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP134519 09052012 AB9   NEW PGM                            *            
      * BP134507 22052012 AB9   ADD 'TSH19', 'KYP1' IN THE LIST    *            
      * BP134974 10072013 RJE1  REMOVE TSH19 AND REPLACE WITH TH1  *            
      * BP135537 20150303 RJB1  CHANGE THE EMAIL RECIPIENT FROM    *            
      *                         NICKNAME INTO COMPLETE NAME        *            
      * BP136082 20160113 PCL4  TO UPDATE EMAIL RECIPIENTS         *            
      * BP136477 20161018 RJB1  TO REPLACE KIM HUAT TO YEE MIEN    *            
      * BP136851 20170629 PCL4  TO UPDATE EMAIL RECIPIENTS         *            
      * BP138757 20210617 KV8   TO REPLACE hong HUAT TO bernice    *            
      * BP139030 20220107 ZAR7  REPLACE CHUA YEE MIEN TO LIM KOK CHUN           
      * BP139564 20230603 kac1 REPLACE LIM KOK CHUN TO ZANY *    * *            
      *                        REPLACE BERNICE GAN   TO HEE_WAN_MENG            
      *                        REPLACE kong yoke pheng to david soh*            
      * BP139175 20230603 KAC1 Replace Zany to Soffia_RAPHAY@hdb.gov.sg         
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F0Y1 ASSIGN TO BP13F0Y1.                                  
                                                                                
           SELECT BP13MAIL ASSIGN TO BP13MAIL.                                  
           SELECT P13MAILA ASSIGN TO P13MAILA.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F0Y1                                                             
           RECORD CONTAINS 100  CHARACTERS                                      
           RECORDING MODE  IS  F.                                               
       01  BP13F0Y1-REC.                                                        
           05 F0Y1-NUM-REGN          PIC X(08).                                 
           05 F0Y1-NRIC1             PIC X(09).                                 
           05 F0Y1-NRIC2             PIC X(09).                                 
           05 F0Y1-NRIC3             PIC X(09).                                 
           05 F0Y1-NRIC4             PIC X(09).                                 
           05 F0Y1-SCH-ACC           PIC X(11).                                 
           05 F0Y1-LOAN-TAG          PIC X(01).                                 
           05 F0Y1-NUM-HLE           PIC X(09).                                 
           05 F0Y1-NUM-HLE-SOC       PIC X(09).                                 
           05 F0Y1-CSM-NCK-NAME      PIC X(05).                                 
           05 FILLER                 PIC X(21).                                 
                                                                                
       FD  BP13MAIL                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING MODE IS F.                                                 
       01  MAIL-PRTREC              PIC X(80).                                  
                                                                                
       FD  P13MAILA                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING MODE IS F.                                                 
       01  MAIL-PRTRECA             PIC X(80).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F0Y1-READ             PIC 9(4)   VALUE ZERO.                      
       01  WS-F0Y1-EOF              PIC X      VALUE 'N'.                       
       01  WS-SNO                   PIC 9(4)   VALUE ZEROS.                     
       01  WS-PREV-CSM              PIC X(05)  VALUE SPACES.                    
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
      *  for testing purpose -------                                            
      *01  MAIL-HDR1.                                                           
      *    05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB3'.                 
      *    05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
      *01  MAIL-HDR2.                                                           
      *    05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
      *    05  MAIL-SENDID     PIC X(07)  VALUE 'CS93334'.                      
      *    05  FILLER          PIC X(09)  VALUE '@SGPHDB3>'.                    
      *    05  FILLER          PIC X(53)  VALUE SPACES.                         
       01  MAIL-DTL1.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-RCPT       PIC X(71)  VALUE spaces.                         
                                                                                
       01  MAIL-DTL1A.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(13)  VALUE 'Soffia_RAPHAY'.                
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL1B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(12)  VALUE 'HEE_WAN_MENG'.                 
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL1C.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(12)  VALUE 'DAVID_FG_SOH'.                 
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL2.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC - EMAIL ALERT'.                                             
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAIL-RCPT1      PIC X(76)  VALUE spaces.                         
                                                                                
       01  MAIL-DTL4A.                                                          
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  CC-MAIL-ID      PIC X(13)  VALUE 'Soffia_RAPHAY'.                
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
           05  FILLER          PIC X(60)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4B.                                                          
           05  FILLER          PIC X(04)  VALUE 'CC:<'.                         
           05  CC-MAIL-ID      PIC X(12)  VALUE 'HEE_WAN_MENG'.                 
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
           05  FILLER          PIC X(60)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4C.                                                          
           05  FILLER          PIC X(04)  VALUE 'CC:<'.                         
           05  CC-MAIL-ID      PIC X(12)  VALUE 'DAVID_FG_SOH'.                 
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
           05  FILLER          PIC X(60)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  MAIL-SUBJECT    PIC X(51)  VALUE                                 
               'HLE found in LEAS  which does not match with SOC.'.             
           05  FILLER          PIC X(9)   VALUE SPACES.                         
                                                                                
       01  MAIL-BODY01.                                                         
           05 FILLER                   PIC x(06)  VALUE 'S/NO.'.                
           05 FILLER                   PIC X(02)   VALUE SPACES.                
           05 FILLER                   PIC X(08)  VALUE 'Regn no'.              
           05 FILLER                   PIC X(02)   VALUE SPACES.                
           05 FILLER                   PIC X(11)  VALUE 'Sch Acc'.              
           05 FILLER                   PIC X(02)   VALUE SPACES.                
           05 FILLER                   PIC X(09)  VALUE 'LEAS  HLE'.            
           05 FILLER                   PIC X(02)   VALUE SPACES.                
           05 FILLER                   PIC X(09)  VALUE 'SOC HLE  '.            
           05 FILLER                   PIC X(01)   VALUE SPACES.                
           05 FILLER                   PIC X(04)  VALUE 'Loan'.                 
           05 FILLER                   PIC X(01)   VALUE SPACES.                
           05 FILLER                   PIC X(05)  VALUE 'CSM  '.                
           05 FILLER                   PIC X(18)  VALUE SPACES.                 
       01  MAIL-BODY02.                                                         
           05 FILLER                   PIC X(61)  VALUE ALL '-'.                
           05 FILLER                   PIC X(19)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY03.                                                         
           05 MAIL-SNO                 PIC ZZZZ9   VALUE ZEROS.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 MAIL-REGN-NO             PIC X(08)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 MAIL-SCH-ACC             PIC X(11)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 MAIL-LEASE-HLE           PIC X(09)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 MAIL-SOC-HLE             PIC X(09)  VALUE SPACES.                 
           05 FILLER                   PIC X(03)  VALUE SPACES.                 
           05 MAIL-LOAN-TAG            PIC X(01)  VALUE SPACES.                 
           05 FILLER                   PIC X(03)  VALUE SPACES.                 
           05 MAIL-CSM                 PIC X(05)  VALUE SPACES.                 
           05 FILLER                   PIC X(17)  VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F0Y1  THRU 2000-EXIT.                          
           PERFORM 3000-WRITE-MAIL     THRU 3000-EXIT                           
                   UNTIL WS-F0Y1-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F0Y1                                                 
                OUTPUT BP13MAIL                                                 
                OUTPUT P13MAILA.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F0Y1.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F0Y1 AT END                                                 
                MOVE 'Y' TO WS-F0Y1-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F0Y1-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-WRITE-MAIL.                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4000-WRITE-EMAIL THRU 4000-EXIT                              
                   UNTIL WS-F0Y1-EOF = 'Y'.                                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-EMAIL.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO MAIL-PRTREC MAIL-BODY03.                              
           INITIALIZE     MAIL-PRTREC.                                          
                                                                                
           IF F0Y1-CSM-NCK-NAME NOT = WS-PREV-CSM                               
             MOVE 1                  TO WS-SNO                                  
             MOVE F0Y1-CSM-NCK-NAME  TO WS-PREV-CSM                             
             MOVE WS-SNO TO MAIL-SNO                                            
             IF WS-F0Y1-READ NOT = 1                                            
                WRITE  MAIL-PRTREC  FROM    MAIL-END                            
             END-IF                                                             
             MOVE SPACES TO MAIL-RCPT MAIL-RCPT1                                
             STRING  F0Y1-CSM-NCK-NAME '@HDB.GOV.SG>'                           
             DELIMITED BY SPACE INTO MAIL-RCPT                                  
             STRING  F0Y1-CSM-NCK-NAME '@HDB.GOV.SG>'                           
             DELIMITED BY SPACE INTO MAIL-RCPT1                                 
             PERFORM 4500-PRINT-MAIL-HDR    THRU 4500-EXIT                      
           ELSE                                                                 
             ADD 1 TO WS-SNO                                                    
             MOVE WS-SNO TO MAIL-SNO                                            
           END-IF.                                                              
           IF WS-F0Y1-READ = 1                                                  
              PERFORM 4600-PRINT-MAIL-HDR    THRU 4600-EXIT                     
           END-IF.                                                              
           MOVE F0Y1-NUM-REGN      TO MAIL-REGN-NO.                             
           MOVE F0Y1-SCH-ACC       TO MAIL-SCH-ACC.                             
           MOVE F0Y1-NUM-HLE       TO MAIL-LEASE-HLE.                           
           MOVE F0Y1-NUM-HLE-SOC   TO MAIL-SOC-HLE.                             
           MOVE F0Y1-LOAN-TAG      TO MAIL-LOAN-TAG.                            
           MOVE F0Y1-CSM-NCK-NAME  TO MAIL-CSM.                                 
           WRITE MAIL-PRTREC FROM MAIL-BODY03.                                  
           MOVE WS-F0Y1-READ       TO MAIL-SNO.                                 
           WRITE MAIL-PRTRECA FROM MAIL-BODY03.                                 
           MOVE SPACES            TO MAIL-PRTREC.                               
           INITIALIZE                MAIL-PRTREC.                               
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           PERFORM 2000-READ-BP13F0Y1 THRU 2000-EXIT.                           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4500-PRINT-MAIL-HDR.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES       TO MAIL-PRTREC.                                    
           INITIALIZE                MAIL-PRTREC.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-HDR1.                                    
           WRITE MAIL-PRTREC FROM MAIL-HDR2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL1.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL3.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL4.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL5.                                    
           WRITE MAIL-PRTREC FROM MAIL-BODY01.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY02.                                  
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4600-PRINT-MAIL-HDR.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES       TO MAIL-PRTRECA.                                   
           INITIALIZE           MAIL-PRTRECA.                                   
                                                                                
           WRITE MAIL-PRTRECA FROM MAIL-HDR1.                                   
           WRITE MAIL-PRTRECA FROM MAIL-HDR2.                                   
           WRITE MAIL-PRTRECA FROM MAIL-DTL1A.                                  
           WRITE MAIL-PRTRECA FROM MAIL-DTL1B.                                  
           WRITE MAIL-PRTRECA FROM MAIL-DTL1C.                                  
           WRITE MAIL-PRTRECA FROM MAIL-DTL2.                                   
           WRITE MAIL-PRTRECA FROM MAIL-DTL3.                                   
           WRITE MAIL-PRTRECA FROM MAIL-DTL4A.                                  
           WRITE MAIL-PRTRECA FROM MAIL-DTL4B.                                  
           WRITE MAIL-PRTRECA FROM MAIL-DTL4C.                                  
           WRITE MAIL-PRTRECA FROM MAIL-DTL5.                                   
           WRITE MAIL-PRTRECA FROM MAIL-BODY01.                                 
           WRITE MAIL-PRTRECA FROM MAIL-BODY02.                                 
           WRITE MAIL-PRTRECA FROM MAIL-SPACES.                                 
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
                                                                                
           DISPLAY '- BP13C0Y2 CONTROL TOTAL --------'.                         
           DISPLAY ' NO OF REC READ FROM BP13F0Y1  = '                          
                     WS-F0Y1-READ.                                              
           DISPLAY ' NO OF REC WRITTEN TO BP13MAIL = '                          
                     WS-SNO.                                                    
           DISPLAY ' NO OF REC WRITTEN TO P13MAILA = '                          
                     WS-F0Y1-READ.                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F0Y1                                                 
                       BP13MAIL                                                 
                       P13MAILA.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
