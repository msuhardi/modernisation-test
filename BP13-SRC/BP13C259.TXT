       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C259.                                                  
       AUTHOR.       GD.                                                        
       DATE-WRITTEN. 06/05/95.                                                  
      * ====================================================== *                
      *                SYSTEM OF COMMITMENT (BP13)             *                
      * ====================================================== *                
      *    OBJECTIVE :                                         *                
      *      GET RECORDS FROM F230 AND PUT TO AN OUTPUT FILE   *                
      *      WHERE F230-DTE-ACCEPT >= USER-SPECIFIED-DATE.     *                
      * ====================================================== *                
      * CHG REQ  BY   DATE   DESCRIPTION                       *                
      * -------- ---- ------ --------------------------------- *                
      * C9505D1  LMS  951103 INCLUDE SEPARATE OPTS IN BP13B279 *                
      * BP130180 SSS  960907 INCLUDE IA COMMENTS               *                
      * BP130956 LMS  010110 TO CHANGE LRECL OF K230/K240 FROM *                
      *                      130 TO 250.                       *                
      * BP132278 JF4  280303 CHGE BP13F230/F240 LRECL TO 450.  *                
      * BP133386 ESA1 280410 EXCLUDE NPL CASES                 *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205   ASSIGN       TO BP13F205.                          
           SELECT BP13F230   ASSIGN       TO BP13F230.                          
           SELECT BP13F240   ASSIGN       TO BP13F240.                          
           SELECT P13F230A   ASSIGN       TO P13F230A.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F230                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13K230.                                                           
                                                                                
       FD   BP13F240                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13K240.                                                           
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS  80 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F205.                                                           
                                                                                
       FD   P13F230A                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F230A-REC           PIC X(450).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F230-READ            PIC 9(08) VALUE 0.                           
       01  WS-F240-READ            PIC 9(08) VALUE 0.                           
       01  WS-F230A-WRITE          PIC 9(08) VALUE 0.                           
                                                                                
       01  WS-INPUT-DATE           PIC 9(8)  VALUE 0.                           
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      **********                                                                
       000-MAIN.                                                                
      **********                                                                
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 205-READ-F205-REC THRU 205-EXIT.                             
           PERFORM 200-READ-F230-REC THRU 200-EXIT.                             
           PERFORM 300-PROCESS-F230  THRU 300-EXIT                              
                   UNTIL K230-CDE-INTV = HIGH-VALUE.                            
           PERFORM 400-READ-F240-REC THRU 400-EXIT.                             
           PERFORM 500-PROCESS-F240  THRU 500-EXIT                              
                   UNTIL K240-CDE-INTV = HIGH-VALUE.                            
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************                                                          
       100-OPEN-FILES.                                                          
      ****************                                                          
                                                                                
           OPEN INPUT  BP13F230                                                 
                       BP13F240                                                 
                       BP13F205.                                                
           OPEN OUTPUT P13F230A.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       200-READ-F230-REC.                                                       
      *******************                                                       
                                                                                
           READ BP13F230 AT END                                                 
                MOVE HIGH-VALUE TO K230-CDE-INTV                                
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F230-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       205-READ-F205-REC.                                                       
      *******************                                                       
                                                                                
           READ BP13F205 AT END                                                 
                GO TO 900-CLOSE-ROUTINE                                         
           END-READ.                                                            
                                                                                
           MOVE F205-DTE-ALLOCN     TO WS-INPUT-DATE.                           
                                                                                
       205-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************                                                         
       300-PROCESS-F230.                                                        
      *****************                                                         
                                                                                
           IF K230-NUM-SELECTION NOT = 'NPL'                                    
              IF (K230-DTE-ACCEPT >= WS-INPUT-DATE) AND                         
                 (K230-CDE-DETAIL = '02')                                       
                 MOVE K230-DETAIL-REC  TO P13F230A-REC                          
                 WRITE P13F230A-REC                                             
                 ADD  1                TO  WS-F230A-WRITE                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F230-REC THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       400-READ-F240-REC.                                                       
      *******************                                                       
                                                                                
           READ BP13F240 AT END                                                 
                MOVE HIGH-VALUE TO K240-CDE-INTV                                
                GO TO 400-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F240-READ.                                               
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       500-PROCESS-F240.                                                        
      *****************                                                         
                                                                                
           IF K240-NUM-SELECTION NOT = 'NPL'                                    
              IF (K240-DTE-UPDATE >= WS-INPUT-DATE) AND                         
                 (K240-CDE-HEADER = '01')                                       
                 MOVE K240-HEADER-REC  TO P13F230A-REC                          
                 WRITE P13F230A-REC                                             
                 ADD  1                TO  WS-F230A-WRITE                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 400-READ-F240-REC THRU 400-EXIT.                             
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************                                                        
       900-CLOSE-ROUTINE.                                                       
      ******************                                                        
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C259 '.                                    
           DISPLAY ' '.                                                         
           DISPLAY 'INPUT DATE  : ' WS-INPUT-DATE.                              
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM  F230  ' WS-F230-READ.              
           DISPLAY 'NO OF RECORDS READ FROM  F240  ' WS-F240-READ.              
           DISPLAY 'NO OF RECORDS WRITTEN TO F230A ' WS-F230A-WRITE.            
                                                                                
           CLOSE BP13F230                                                       
                 BP13F240                                                       
                 BP13F205                                                       
                 P13F230A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
