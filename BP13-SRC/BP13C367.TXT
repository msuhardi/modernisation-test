      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13C367.                                                 
       AUTHOR.        SK.                                                       
       DATE-WRITTEN.  29/01/2004.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  PRINT REPORT OF SURRENDER OF STUDIO APPARTMENT*        
      *                  WHEN SURRENDER/ABORTIVE DATE RECEIVED         *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F690  -  FIS/SLS INTERFACE FILE       *        
      *                                                                *        
      *   OUPUT FILES :  1.  BP13L367                                  *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP132292  SK   29/01/2004 NEW PGM                            *        
      *   BP132571  SK   27/07/2004 PRINT NULL REPORT                  *        
      *   BP135903  FNP1 04/02/2016 ADD LEASE TENURE                   *        
      *   BP136398  FNP1 17/08/2016 EXPAND F690 FROM 250 TO 600        *        
      *   BP138944  FP6  13/10/2021 EXPAND F690 FROM 600 TO 800        *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F690  ASSIGN        TO BP13F690.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
           SELECT BP13L367  ASSIGN        TO BP13L367.                          
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F690                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F690.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13L367                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS  132 CHARACTERS.                                     
                                                                                
       01  BP13L367-REC         PIC X(132).                                     
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F690-READ        PIC 9(8)  VALUE 0.                        
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F690-EOF             PIC X(1)  VALUE 'N'.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SEQ                  PIC 9(3)  VALUE 0.                        
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  WS-TP-DATE.                                                          
           05  WS-TP-CCYY               PIC X(4)    VALUE SPACES.               
           05  WS-TP-MM                 PIC X(2)    VALUE SPACES.               
           05  WS-TP-DD                 PIC X(2)    VALUE SPACES.               
                                                                                
       01  WS-TP-DATE1                  REDEFINES WS-TP-DATE.                   
           05  WS-TP-CCYY1              PIC 9(4).                               
           05  WS-TP-MM1                PIC 9(2).                               
           05  WS-TP-DD1                PIC 9(2).                               
                                                                                
       01  WS-EFF-DATE.                                                         
           05  WS-EFF-CCYY              PIC 9(4)    VALUE ZEROS.                
           05  WS-EFF-MM                PIC 9(2)    VALUE ZEROS.                
           05  WS-EFF-DD                PIC 9(2)    VALUE ZEROS.                
                                                                                
       01  WS-UNIT.                                                             
           05  WS-HSELVL                PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE '-'.                  
           05  WS-HSEMAIN               PIC X(4)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE '-'.                  
           05  WS-HSESUB                PIC X(3)    VALUE SPACES.               
                                                                                
       01  WS-DTE-SURR.                                                         
           05  WS-SURR-CCYY             PIC 9(4)    VALUE ZEROS.                
           05  WS-SURR-MM               PIC 9(2)    VALUE ZEROS.                
           05  WS-SURR-DD               PIC 9(2)    VALUE ZEROS.                
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-CNT-LINE-ON-PAGE       PIC 9(8)    VALUE 66.                   
           05 WS-CNT-PRINT-L367         PIC 9(8)    VALUE 1.                    
           05 WS-CNT-WRITE-L367         PIC 9(8)    VALUE 0.                    
           05 WS-NO-OF-PAGE             PIC 9(8)    VALUE 0.                    
           05 WS-DIFF-MTH               PIC 9(2)    VALUE ZEROS.                
           05 WS-TINC                   PIC 9(6)V99 VALUE ZEROS.                
           05 WS-YTD-INC                PIC 9(6)V99 VALUE ZEROS.                
           05 WS-BAL-DEF                PIC 9(6)V99 VALUE ZEROS.                
           05 WS-DIF-BET-YTD            PIC 9(6)V99 VALUE ZEROS.                
           05 WS-INC-SURR-AMT           PIC 9(8)V99 VALUE ZEROS.                
           05 WS-DIF-BET-BAL            PIC S9(8)V99 VALUE ZEROS.               
           05 WS-DIF-SUR-AMT            PIC S9(6)V99 VALUE ZEROS.               
           05 WS-SURR-DENOM             PIC 9(1)V9(10) VALUE ZEROS.             
           05 WS-SURR-AMT               PIC 9(7)V99 VALUE ZEROS.                
           05 WS-SURR-AMT-RND1          REDEFINES   WS-SURR-AMT.                
              10 WS-SURR-AMT-RND-INT    PIC 9(5).                               
              10 WS-SURR-AMT-RND-DEC    PIC 9(2).                               
              10 WS-SURR-AMT-RND-DEC1   PIC 9(2).                               
           05 WS-FT                     PIC X(2)    VALUE SPACES.               
           05 WS-FT-PREV                PIC X(2)    VALUE SPACES.               
           05 WS-FLAG                   PIC X(1)    VALUE 'Y'.                  
           05 WS-FLAG1                  PIC X(1)    VALUE 'N'.                  
           05 WS-LAST-SCH-ACC           PIC X(11)   VALUE SPACES.               
           05 WS-CNT                    PIC 9(01)   VALUE ZEROS.                
           05 WS-DIFF-LINE              PIC 9(02)   VALUE ZEROS.                
                                                                                
       01  WS-RECORD.                                                           
           05  FILLER                   PIC X(30) VALUE SPACES.                 
           05  WS-F690-NUM-BLOCK        PIC X(05)  VALUE SPACES.                
           05  FILLER                   PIC X(71)  VALUE SPACES.                
           05  WS-F690-NME-STREET       PIC X(32).                              
           05  WS-F690-NUM-UNIT.                                                
               10  WS-F690-NUM-HSELVL   PIC XX.                                 
               10  WS-F690-NUM-HSEMAIN  PIC X(4).                               
               10  WS-F690-NUM-HSESUB   PIC X(3).                               
           05  WS-F690-NUM-POSTAL       PIC X(6).                               
           05  FILLER                   PIC X(97)  VALUE SPACES.                
                                                                                
       01  WS-FOOTER-VAR.                                                       
           05  WS-DEF-INC-FT            PIC 9(8)V99 VALUE ZEROS.                
           05  WS-AMORT-INC-FT          PIC 9(8)V99 VALUE ZEROS.                
           05  WS-YTD-INC-FT            PIC 9(8)V99 VALUE ZEROS.                
           05  WS-BAL-DEF-FT            PIC 9(8)V99 VALUE ZEROS.                
           05  WS-SURR-AMT-FT           PIC 9(8)V99 VALUE ZEROS.                
           05  WS-SRDTE-FT              PIC 9(4)    VALUE ZEROS.                
           05  WS-ABDTE-FT              PIC 9(4)    VALUE ZEROS.                
           05  WS-TOT-COMP-FT           PIC S9(8)V99 VALUE ZEROS.               
                                                                                
       01  WS-SUBTOT-VAR.                                                       
           05  WS-DEF-INC-ST            PIC 9(8)V99 VALUE ZEROS.                
           05  WS-AMORT-INC-ST          PIC 9(8)V99 VALUE ZEROS.                
           05  WS-YTD-INC-ST            PIC 9(8)V99 VALUE ZEROS.                
           05  WS-BAL-DEF-ST            PIC 9(8)V99 VALUE ZEROS.                
           05  WS-SURR-AMT-ST           PIC 9(8)V99 VALUE ZEROS.                
           05  WS-SRDTE-ST              PIC 9(4)    VALUE ZEROS.                
           05  WS-ABDTE-ST              PIC 9(4)    VALUE ZEROS.                
           05  WS-TOT-COMP-ST           PIC S9(8)V99 VALUE ZEROS.               
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD           PIC 9(2).                                 
           05  FILLER                 PIC X(1)  VALUE '/'.                      
           05  WS-EDITED-MM           PIC 9(2).                                 
           05  FILLER                 PIC X(1)  VALUE '/'.                      
           05  WS-EDITED-CCYY         PIC 9(4).                                 
                                                                                
      *-----------------------------------------------------------*             
      *    WS FOR CONTROL LISTING                                 *             
      *-----------------------------------------------------------*             
                                                                                
       01  L367-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L367'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L367-PRINT-DATE-1        PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L367-PRINT-PAGE          PIC ZZZ9.                               
                                                                                
       01  L367-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(44) VALUE SPACES.                 
           05  FILLER                   PIC X(48) VALUE                         
                'SURRENDER/ABORT DATES OF STUDIO APPARMENT RECVD'.              
                                                                                
       01  L367-PR-HEAD-02-A.                                                   
           05  FILLER                   PIC X(47) VALUE SPACES.                 
           05  FILLER                   PIC X(15) VALUE                         
                'FOR THE MONTH'.                                                
           05  L367-MONTH               PIC X(15) VALUE SPACES.                 
                                                                                
       01  L367-PR-HEAD-03.                                                     
           05  FILLER          PIC X(4)  VALUE ' SN'.                           
           05  FILLER          PIC X(1)  VALUE SPACES.                          
           05  FILLER          PIC X(12) VALUE 'SCHEME A/C'.                    
           05  FILLER          PIC X(11) VALUE 'TP DTE'.                        
           05  FILLER          PIC X(10) VALUE 'DEFERRED'.                      
           05  FILLER          PIC X(3)  VALUE SPACES.                          
           05  FILLER          PIC X(08) VALUE 'AMRT INC'.                      
           05  FILLER          PIC X(1)  VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE 'YTD AMRT/'.                     
           05  FILLER          PIC X(3)  VALUE SPACES.                          
           05  FILLER          PIC X(08) VALUE 'BAL DEF'.                       
           05  FILLER          PIC X(1)  VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE '  TRNS '.                       
           05  FILLER          PIC X(3)  VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE 'REQ SURR'.                      
           05  FILLER          PIC X(2)  VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE 'SURR BAL'.                      
           05  FILLER          PIC X(2)  VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE 'ABRT DTE'.                      
           05  FILLER          PIC X(2)  VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE 'AMT COMPN'.                     
                                                                                
       01  L367-PR-HEAD-04.                                                     
           05  FILLER           PIC X(5)  VALUE SPACES.                         
           05  FILLER           PIC X(12) VALUE 'ADDRESS'.                      
           05  FILLER           PIC X(11) VALUE 'SALES EFF'.                    
           05  FILLER           PIC X(10) VALUE 'INCOME'.                       
           05  FILLER           PIC X(4)  VALUE SPACES.                         
           05  FILLER           PIC X(08) VALUE 'PER MTH'.                      
           05  FILLER           PIC X(1)  VALUE SPACES.                         
           05  FILLER           PIC X(10) VALUE 'NUM AMRT'.                     
           05  FILLER           PIC X(1)  VALUE SPACES.                         
           05  FILLER           PIC X(09) VALUE '  INC'.                        
           05  FILLER           PIC X(1)  VALUE SPACES.                         
           05  FILLER           PIC X(10) VALUE '  DATE'.                       
           05  FILLER           PIC X(3)  VALUE SPACES.                         
           05  FILLER           PIC X(10) VALUE 'DATE/NUM'.                     
           05  FILLER           PIC X(04) VALUE SPACES.                         
           05  FILLER           PIC X(06) VALUE 'TENURE'.                       
           05  FILLER           PIC X(04) VALUE SPACES.                         
           05  FILLER           PIC X(04) VALUE ' RMK'.                         
           05  FILLER           PIC X(10) VALUE SPACES.                         
           05  FILLER           PIC X(10) VALUE '/DATE'.                        
                                                                                
       01  L367-PR-HEAD-05.                                                     
           05  FILLER                PIC X(132) VALUE ALL '-'.                  
                                                                                
       01  L367-PR-DETAILS-1.                                                   
           05  L367-NUMBER           PIC ZZZ9  BLANK WHEN ZERO.                 
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-SCH-ACC          PIC X(11) VALUE SPACES.                    
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-TP-DTE           PIC X(10) VALUE SPACES.                    
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-DEF-INC          PIC $$$,$$9.99.                            
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-AMORT-INC        PIC $$$,$$9.99.                            
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-YTD-INC          PIC $$$,$$9.99.                            
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-BAL-DEF          PIC $,$$$,$$9.99.                          
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-TRNS-DTE         PIC X(10) VALUE SPACES.                    
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-REQ-SURR-DTE     PIC X(10) VALUE SPACES.                    
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-SURR-AMT         PIC $,$$$,$$9.99.                          
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-ABRT-DTE         PIC X(10) VALUE SPACES.                    
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-AMT-COMPEN       PIC $,$$$,$$9.99.                          
                                                                                
       01  L367-PR-DETAILS-2.                                                   
           05  FILLER                PIC X(17) VALUE SPACES.                    
           05  L367-EFF-DTE          PIC X(10) VALUE SPACES.                    
           05  FILLER                PIC X(26) VALUE SPACES.                    
           05  L367-AMORT-NUM        PIC Z(3)  VALUE SPACES.                    
           05  FILLER                PIC X(32) VALUE SPACES.                    
           05  L367-SURR-NUM         PIC Z(3)  VALUE SPACES.                    
           05  FILLER                PIC X(12) VALUE SPACES.                    
           05  L367-TENURE           PIC X(02) VALUE SPACES.                    
           05  FILLER                PIC X(03) VALUE SPACES.                    
           05  L367-RMK              PIC X(2)  VALUE SPACES.                    
           05  FILLER                PIC X(11) VALUE SPACES.                    
           05  L367-DTE-LAST         PIC X(10) VALUE SPACES.                    
                                                                                
       01  L367-PR-DETAILS-3.                                                   
           05  FILLER                PIC X(5)  VALUE SPACES.                    
           05  L367-ADDRESS          PIC X(70) VALUE SPACES.                    
                                                                                
       01  L367-PR-FOOTER.                                                      
           05  FILLER                PIC X(8)  VALUE 'TOTAL'.                   
           05  FILLER                PIC X(11) VALUE SPACES.                    
           05  L367-DEF-INC-FT       PIC $,$$$,$$$,$$9.99.                      
           05  L367-AMORT-INC-FT     PIC $$,$$$,$$9.99.                         
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-YTD-INC-FT       PIC $$,$$$,$$9.99.                         
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-BAL-DEF-FT       PIC $,$$$,$$$,$$9.99.                      
           05  FILLER                PIC X(5)  VALUE SPACES.                    
           05  FILLER                PIC X(1)  VALUE '#'.                       
           05  L367-TOT-SRDTE-FT     PIC ZZ9   VALUE ZERO.                      
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  L367-SURR-AMT-FT      PIC $,$$$,$$$,$$9.99.                      
           05  FILLER                PIC X(3)  VALUE '  #'.                     
           05  L367-TOT-ABDTE-FT     PIC ZZ9   VALUE ZERO.                      
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-TOT-COMP-FT      PIC $,$$$,$$$,$$9.99-.                     
                                                                                
       01  L367-PR-SUBTOT.                                                      
           05  FILLER                PIC X(14) VALUE 'SUB TOT FLAT:'.           
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-FT-ST            PIC X(2)  VALUE SPACES.                    
           05  FILLER                PIC X(2)  VALUE SPACES.                    
           05  L367-DEF-INC-ST       PIC $,$$$,$$$,$$9.99.                      
           05  L367-AMORT-INC-ST     PIC $$,$$$,$$9.99.                         
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-YTD-INC-ST       PIC $$,$$$,$$9.99.                         
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-BAL-DEF-ST       PIC $,$$$,$$$,$$9.99.                      
           05  FILLER                PIC X(5)  VALUE SPACES.                    
           05  FILLER                PIC X(1)  VALUE '#'.                       
           05  L367-TOT-SRDTE-ST     PIC ZZ9   VALUE ZERO.                      
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  L367-SURR-AMT-ST      PIC $,$$$,$$$,$$9.99.                      
           05  FILLER                PIC X(3)  VALUE '  #'.                     
           05  L367-TOT-ABDTE-ST     PIC ZZ9   VALUE ZERO.                      
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L367-TOT-COMP-ST      PIC $,$$$,$$$,$$9.99-.                     
                                                                                
       01  L367-PR-FLAT.                                                        
           05  FILLER           PIC X(5)  VALUE SPACES.                         
           05  FILLER           PIC X(12) VALUE 'FLAT TYPE : '.                 
           05  FILLER           PIC X(02) VALUE SPACES.                         
           05  L367-PRT-FLAT    PIC X(02) VALUE SPACES.                         
                                                                                
       01  L367-PR-FOOT-NOTE1.                                                  
           05  FILLER                   PIC X(05) VALUE 'RMK:'.                 
           05  FILLER                   PIC X(75) VALUE                         
               'NS = ABORT DATE RECEIVED WITH NO SURRENDER DATE'.               
       01  L367-PR-FOOT-NOTE2.                                                  
           05  FILLER                   PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(75) VALUE                         
               'AC = ABORT DATE RECEIVED WITH COMPENSATION AMT UPDATED'.        
                                                                                
       01  L367-PR-BLANK             PIC X(132) VALUE SPACES.                   
                                                                                
       01  L367-PR-NO-RECORD.                                                   
           05  FILLER                   PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(75) VALUE                         
               'NO RECORDS TO BE PRINTED!!!!'.                                  
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM  1000-OPEN-FILES      THRU    1000-EXIT.                     
           PERFORM  1100-READ-SY02F001   THRU    1100-EXIT.                     
           PERFORM  2000-READ-BP13F690   THRU    2000-EXIT.                     
           PERFORM  3000-PROCESS         THRU    3000-EXIT                      
                    UNTIL WS-F690-EOF = 'Y'.                                    
           IF WS-CNT-F690-READ < 1                                              
              PERFORM 4200-PRINT-HEADING   THRU 4200-EXIT                       
              WRITE BP13L367-REC  FROM  L367-PR-NO-RECORD AFTER 5               
           END-IF.                                                              
           PERFORM  4400-PRINT-FOOTER    THRU    4400-EXIT.                     
           PERFORM  9000-CLOSE-ROUTINE   THRU    9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13F690                                                 
                       SY02F001                                                 
                OUTPUT BP13L367.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
           MOVE WS-SYS-DD               TO WS-EDITED-DD.                        
           MOVE WS-SYS-MM               TO WS-EDITED-MM.                        
           MOVE WS-SYS-CCYY             TO WS-EDITED-CCYY.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1100-READ-SY02F001.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ SY02F001 AT END                                                 
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
                                                                                
           STRING  F001-TXT-CURRENT-MTH '-'                                     
                   F001-DTE-CURRENT(1:4)                                        
                   DELIMITED BY SIZE                                            
                   INTO L367-MONTH                                              
           END-STRING.                                                          
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------------*        
       2000-READ-BP13F690.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F690                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F690-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-CNT-F690-READ                                    
           END-READ.                                                            
                                                                                
           IF WS-LAST-SCH-ACC = SPACES OR LOW-VALUES                            
              MOVE  F690-NUM-SCH-ACC  TO WS-LAST-SCH-ACC                        
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           MOVE  0      TO WS-CNT.                                              
           MOVE SPACES  TO WS-RECORD.                                           
                                                                                
           PERFORM UNTIL F690-NUM-SCH-ACC NOT = WS-LAST-SCH-ACC                 
                   OR WS-F690-EOF = 'Y'                                         
              ADD  1 TO WS-CNT                                                  
              PERFORM 3100-PROCESS1      THRU 3100-EXIT                         
              MOVE F690-REC              TO WS-RECORD                           
              PERFORM 2000-READ-BP13F690 THRU 2000-EXIT                         
           END-PERFORM.                                                         
                                                                                
           PERFORM 4050-MOVE-ADDRESS   THRU 4050-EXIT.                          
           PERFORM 4150-WRITE-ADDRESS  THRU 4150-EXIT.                          
           MOVE F690-NUM-SCH-ACC       TO WS-LAST-SCH-ACC.                      
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3100-PROCESS1.                                                           
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 3500-COMPUTE-PARA THRU 3500-EXIT                             
                                                                                
           MOVE F690-CDE-TYP-ACTUSE TO WS-FT                                    
           IF (WS-FT NOT = WS-FT-PREV) AND WS-FLAG = 'N'                        
              PERFORM 4500-WRITE-SUBTOT THRU 4500-EXIT                          
           END-IF                                                               
                                                                                
           PERFORM 4600-COMPUTE-SUBTOT THRU 4600-EXIT                           
           PERFORM 4000-MOVE-PARA      THRU 4000-EXIT                           
           PERFORM 4100-WRITE-BP13L367 THRU 4100-EXIT                           
                                                                                
           MOVE WS-FT              TO WS-FT-PREV                                
           MOVE 'N'                TO WS-FLAG.                                  
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3500-COMPUTE-PARA.                                                       
      *----------------------------------------------------------------*        
                                                                                
           IF F690-AMT-DEFER-INCOME NOT NUMERIC                                 
              MOVE ZEROS TO F690-AMT-DEFER-INCOME                               
           END-IF.                                                              
                                                                                
           IF F690-AMT-AMORT-INCOME-MTH NOT NUMERIC                             
              MOVE ZEROS TO F690-AMT-AMORT-INCOME-MTH                           
           END-IF.                                                              
                                                                                
           IF F690-AMT-AMORT-INCOME-ACCUM NOT NUMERIC                           
              MOVE ZEROS TO F690-AMT-AMORT-INCOME-ACCUM                         
           END-IF.                                                              
                                                                                
           IF F690-AMT-COMPENSATION NOT NUMERIC                                 
              MOVE ZEROS TO F690-AMT-COMPENSATION                               
           END-IF.                                                              
                                                                                
           IF F690-AMT-SELLING-PRICE NOT NUMERIC                                
              MOVE ZEROS TO F690-AMT-SELLING-PRICE                              
           END-IF.                                                              
                                                                                
           COMPUTE WS-DEF-INC-FT   = WS-DEF-INC-FT +                            
                                     F690-AMT-DEFER-INCOME.                     
                                                                                
           COMPUTE WS-AMORT-INC-FT = WS-AMORT-INC-FT +                          
                                     F690-AMT-AMORT-INCOME-MTH.                 
                                                                                
           COMPUTE WS-YTD-INC-FT   = WS-YTD-INC-FT +                            
                                     F690-AMT-AMORT-INCOME-ACCUM.               
                                                                                
           COMPUTE WS-TOT-COMP-FT  = WS-TOT-COMP-FT +                           
                                     F690-AMT-COMPENSATION.                     
                                                                                
           COMPUTE WS-INC-SURR-AMT = F690-AMT-DEFER-INCOME -                    
                                     F690-AMT-AMORT-INCOME-ACCUM.               
                                                                                
           COMPUTE WS-BAL-DEF-FT   = WS-BAL-DEF-FT +                            
                                     WS-INC-SURR-AMT.                           
                                                                                
           COMPUTE WS-SURR-AMT-FT  = WS-SURR-AMT-FT +                           
                                     F690-AMT-SURRENDER-BAL.                    
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-MOVE-PARA.                                                          
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 4300-GET-EFF-DTE          THRU 4300-EXIT.                    
                                                                                
           IF WS-CNT = 1                                                        
              MOVE WS-CNT-PRINT-L367  TO    L367-NUMBER                         
              ADD   1                 TO    WS-CNT-PRINT-L367                   
           ELSE                                                                 
              MOVE 0                  TO    L367-NUMBER                         
           END-IF.                                                              
                                                                                
           MOVE SPACES                       TO BP13L367-REC.                   
      *-------> MOVING VALUES FOR LINE 1                                        
           MOVE  F690-NUM-SCH-ACC            TO L367-SCH-ACC.                   
                                                                                
           STRING F690-DTE-KEY-ISSUE(7:2) '/'                                   
                  F690-DTE-KEY-ISSUE(5:2) '/'                                   
                  F690-DTE-KEY-ISSUE                                            
                  DELIMITED BY SIZE                                             
                  INTO  L367-TP-DTE                                             
           END-STRING.                                                          
                                                                                
           MOVE  F690-AMT-DEFER-INCOME        TO L367-DEF-INC.                  
           MOVE  F690-AMT-AMORT-INCOME-MTH    TO L367-AMORT-INC.                
           MOVE  F690-AMT-AMORT-INCOME-ACCUM  TO L367-YTD-INC.                  
                                                                                
           MOVE  WS-INC-SURR-AMT         TO L367-BAL-DEF.                       
           IF F690-DTE-SURRENDER-REQUEST IS NUMERIC                             
              MOVE F690-DTE-SURRENDER-REQUEST TO WS-DTE-SURR                    
           ELSE                                                                 
              MOVE ZEROS                      TO WS-DTE-SURR                    
           END-IF.                                                              
                                                                                
           MOVE F690-AMT-SURRENDER-BAL  TO L367-SURR-AMT.                       
           MOVE F690-AMT-COMPENSATION   TO L367-AMT-COMPEN.                     
                                                                                
           IF F690-DTE-SURRENDER-REQUEST NOT = SPACES AND LOW-VALUES            
                                               AND ALL '0'                      
              ADD 1 TO WS-SRDTE-ST WS-SRDTE-FT                                  
           END-IF.                                                              
                                                                                
           IF F690-DTE-ABORTIVE NOT = SPACES AND LOW-VALUES AND ALL '0'         
              ADD 1 TO WS-ABDTE-ST  WS-ABDTE-FT                                 
           END-IF.                                                              
                                                                                
           STRING F690-DTE-SURRENDER-REQUEST(7:2) '/'                           
                  F690-DTE-SURRENDER-REQUEST(5:2) '/'                           
                  F690-DTE-SURRENDER-REQUEST(1:4)                               
                  DELIMITED BY SIZE                                             
                  INTO  L367-REQ-SURR-DTE                                       
           END-STRING.                                                          
                                                                                
           STRING F690-DTE-ABORTIVE(7:2) '/'                                    
                  F690-DTE-ABORTIVE(5:2) '/'                                    
                  F690-DTE-ABORTIVE(1:4)                                        
                  DELIMITED BY SIZE                                             
                  INTO  L367-ABRT-DTE                                           
           END-STRING.                                                          
                                                                                
           STRING F690-DTE-UPDATE(7:2) '/'                                      
                  F690-DTE-UPDATE(5:2) '/'                                      
                  F690-DTE-UPDATE(1:4)                                          
                  DELIMITED BY SIZE                                             
                  INTO  L367-TRNS-DTE                                           
           END-STRING.                                                          
                                                                                
      *-------> MOVING VALUES FOR LINE 2                                        
           STRING WS-EFF-DATE(7:2) '/'                                          
                  WS-EFF-DATE(5:2) '/'                                          
                  WS-EFF-DATE(1:4)                                              
                  DELIMITED BY SIZE                                             
                  INTO  L367-EFF-DTE                                            
           END-STRING.                                                          
                                                                                
           STRING F690-DTE-COMPENSATION-PD(7:2) '/'                             
                  F690-DTE-COMPENSATION-PD(5:2) '/'                             
                  F690-DTE-COMPENSATION-PD(1:4)                                 
                  DELIMITED BY SIZE                                             
                  INTO  L367-DTE-LAST                                           
           END-STRING.                                                          
                                                                                
           MOVE  F690-NUM-AMORT     TO L367-AMORT-NUM.                          
           MOVE  F690-TRANS-RMK     TO L367-RMK.                                
           PERFORM 4350-GET-DIFF-MTH         THRU 4350-EXIT.                    
           MOVE  WS-DIFF-MTH        TO L367-SURR-NUM.                           
                                                                                
           MOVE F690-NUM-LEASE-TENURE   TO L367-TENURE.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4050-MOVE-ADDRESS.                                                       
      *----------------------------------------------------------------*        
                                                                                
      *-------> MOVING VALUES FOR LINE 3                                        
           MOVE  WS-F690-NUM-HSELVL    TO WS-HSELVL                             
           MOVE  WS-F690-NUM-HSEMAIN   TO WS-HSEMAIN                            
           MOVE  WS-F690-NUM-HSESUB    TO WS-HSESUB                             
                                                                                
           STRING 'BLK-' WS-F690-NUM-BLOCK ' #' WS-UNIT                         
                  ' ' WS-F690-NME-STREET                                        
                  ' S(' WS-F690-NUM-POSTAL ')'                                  
                  DELIMITED BY SIZE                                             
                  INTO  L367-ADDRESS.                                           
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4100-WRITE-BP13L367.                                                     
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 48                                          
              PERFORM 4200-PRINT-HEADING THRU 4200-EXIT                         
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
                                                                                
           WRITE BP13L367-REC      FROM  L367-PR-DETAILS-1 AFTER 1.             
           WRITE BP13L367-REC      FROM  L367-PR-DETAILS-2 AFTER 1.             
                                                                                
           ADD   2                 TO    WS-CNT-LINE-ON-PAGE.                   
           ADD   1                 TO    WS-CNT-WRITE-L367.                     
                                                                                
           MOVE  SPACES            TO    L367-PR-DETAILS-1.                     
           MOVE  SPACES            TO    L367-PR-DETAILS-2.                     
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4150-WRITE-ADDRESS.                                                      
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 48                                          
              PERFORM 4200-PRINT-HEADING   THRU 4200-EXIT                       
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           WRITE BP13L367-REC       FROM  L367-PR-DETAILS-3 AFTER 1.            
           MOVE 1 TO WS-DIFF-LINE.                                              
           PERFORM 4450-WRITE-BLANK THRU 4450-EXIT.                             
                                                                                
           MOVE  SPACES            TO    L367-PR-DETAILS-3.                     
                                                                                
           ADD   1                 TO    WS-CNT-LINE-ON-PAGE.                   
           ADD   1                 TO    WS-CNT-WRITE-L367.                     
                                                                                
       4150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4200-PRINT-HEADING.                                                      
      *----------------------------------------------------------------*        
                                                                                
           ADD  1                TO     WS-NO-OF-PAGE.                          
           MOVE WS-NO-OF-PAGE    TO     L367-PRINT-PAGE.                        
           MOVE WS-EDITED-DATE   TO     L367-PRINT-DATE-1.                      
                                                                                
           MOVE SPACES           TO     BP13L367-REC.                           
                                                                                
      **   WRITE BP13L367-REC    AFTER  PAGE.                                   
           WRITE BP13L367-REC    FROM   L367-PR-HEAD-01   AFTER PAGE.           
           WRITE BP13L367-REC    FROM   L367-PR-HEAD-02   AFTER 1.              
           WRITE BP13L367-REC    FROM   L367-PR-HEAD-02-A AFTER 1.              
           WRITE BP13L367-REC    FROM   L367-PR-HEAD-03   AFTER 2.              
           WRITE BP13L367-REC    FROM   L367-PR-HEAD-04   AFTER 1.              
           WRITE BP13L367-REC    FROM   L367-PR-HEAD-05   AFTER 1.              
                                                                                
           IF WS-CNT-PRINT-L367 = 2 AND WS-FLAG1 = 'N'                          
              MOVE 1  TO WS-CNT-LINE-ON-PAGE                                    
              PERFORM 4450-WRITE-BLANK   THRU 4450-EXIT                         
              MOVE WS-FT                 TO L367-PRT-FLAT                       
              WRITE BP13L367-REC         FROM L367-PR-FLAT AFTER 2              
              ADD  3  TO WS-CNT-LINE-ON-PAGE                                    
              PERFORM 4450-WRITE-BLANK   THRU 4450-EXIT                         
              MOVE 'Y' TO WS-FLAG1                                              
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4250-PRINT-FOOT-NOTE.                                                    
      *----------------------------------------------------------------*        
                                                                                
           IF WS-DIFF-LINE = 0                                                  
              COMPUTE WS-DIFF-LINE = 50 - WS-CNT-LINE-ON-PAGE                   
           END-IF.                                                              
                                                                                
           PERFORM 4450-WRITE-BLANK   THRU 4450-EXIT.                           
                                                                                
           WRITE BP13L367-REC   FROM  L367-PR-FOOT-NOTE1 AFTER 1.               
           WRITE BP13L367-REC   FROM  L367-PR-FOOT-NOTE2 AFTER 1.               
                                                                                
       4250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4300-GET-EFF-DTE.                                                        
      *----------------------------------------------------------------*        
                                                                                
           MOVE F690-DTE-KEY-ISSUE TO WS-TP-DATE.                               
                                                                                
           IF WS-TP-DD1 < 16                                                    
              MOVE WS-TP-CCYY1  TO WS-EFF-CCYY                                  
              MOVE WS-TP-MM1    TO WS-EFF-MM                                    
              MOVE 1            TO WS-EFF-DD                                    
           ELSE                                                                 
           IF WS-TP-DD1 > 15                                                    
              COMPUTE WS-TP-MM1 =  WS-TP-MM1 + 1                                
              IF WS-TP-MM1 > 12                                                 
                 COMPUTE WS-TP-CCYY1 = WS-TP-CCYY1 + 1                          
                 MOVE 1  TO WS-TP-MM1                                           
              END-IF                                                            
                                                                                
              MOVE WS-TP-CCYY1  TO WS-EFF-CCYY                                  
              MOVE WS-TP-MM1    TO WS-EFF-MM                                    
              MOVE 1            TO WS-EFF-DD                                    
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4350-GET-DIFF-MTH.                                                       
      *----------------------------------------------------------------*        
                                                                                
           COMPUTE WS-DIFF-MTH = ((WS-SURR-CCYY * 12) + WS-SURR-MM) -           
                                 ((WS-EFF-CCYY * 12) + WS-EFF-MM) + 1.          
       4350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4400-PRINT-FOOTER.                                                       
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-WRITE-L367 < 1                                             
              GO TO 4400-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-FT = WS-FT-PREV) AND WS-FLAG = 'N'                            
              PERFORM 4500-WRITE-SUBTOT  THRU 4500-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 48                                          
              PERFORM 4200-PRINT-HEADING   THRU 4200-EXIT                       
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           MOVE WS-DEF-INC-FT         TO L367-DEF-INC-FT.                       
           MOVE WS-AMORT-INC-FT       TO L367-AMORT-INC-FT.                     
           MOVE WS-YTD-INC-FT         TO L367-YTD-INC-FT.                       
           MOVE WS-BAL-DEF-FT         TO L367-BAL-DEF-FT.                       
           MOVE WS-SURR-AMT-FT        TO L367-SURR-AMT-FT.                      
           MOVE WS-TOT-COMP-FT        TO L367-TOT-COMP-FT.                      
           MOVE WS-SRDTE-FT           TO L367-TOT-SRDTE-FT.                     
           MOVE WS-ABDTE-FT           TO L367-TOT-ABDTE-FT.                     
                                                                                
           WRITE   BP13L367-REC       FROM   L367-PR-FOOTER  AFTER 1.           
           WRITE   BP13L367-REC       FROM   L367-PR-HEAD-05 AFTER 1.           
                                                                                
           ADD 1   TO WS-CNT-LINE-ON-PAGE                                       
           IF WS-CNT-LINE-ON-PAGE > 45                                          
              PERFORM 4200-PRINT-HEADING   THRU 4200-EXIT                       
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           PERFORM 4250-PRINT-FOOT-NOTE THRU 4250-EXIT.                         
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4450-WRITE-BLANK.                                                        
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 48                                          
              PERFORM 4200-PRINT-HEADING   THRU 4200-EXIT                       
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           WRITE BP13L367-REC   FROM   L367-PR-BLANK AFTER WS-DIFF-LINE.        
                                                                                
           ADD   1              TO    WS-CNT-LINE-ON-PAGE.                      
           ADD   1              TO    WS-CNT-WRITE-L367.                        
                                                                                
       4450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4500-WRITE-SUBTOT.                                                       
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 48                                          
              PERFORM 4200-PRINT-HEADING   THRU 4200-EXIT                       
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           MOVE WS-DEF-INC-ST         TO L367-DEF-INC-ST.                       
           MOVE WS-AMORT-INC-ST       TO L367-AMORT-INC-ST.                     
           MOVE WS-YTD-INC-ST         TO L367-YTD-INC-ST.                       
           MOVE WS-BAL-DEF-ST         TO L367-BAL-DEF-ST.                       
           MOVE WS-SURR-AMT-ST        TO L367-SURR-AMT-ST.                      
           MOVE WS-FT-PREV            TO L367-FT-ST.                            
           MOVE WS-TOT-COMP-ST        TO L367-TOT-COMP-ST.                      
           MOVE WS-SRDTE-ST           TO L367-TOT-SRDTE-ST.                     
           MOVE WS-ABDTE-ST           TO L367-TOT-ABDTE-ST.                     
                                                                                
           WRITE BP13L367-REC   FROM L367-PR-HEAD-05 AFTER 1.                   
           WRITE BP13L367-REC   FROM L367-PR-SUBTOT  AFTER 1.                   
           ADD  1  TO WS-CNT-LINE-ON-PAGE.                                      
           PERFORM 4450-WRITE-BLANK   THRU 4450-EXIT.                           
                                                                                
           IF WS-F690-EOF NOT = 'Y'                                             
              PERFORM 4450-WRITE-BLANK   THRU 4450-EXIT                         
              MOVE WS-FT                 TO L367-PRT-FLAT                       
              WRITE BP13L367-REC         FROM L367-PR-FLAT AFTER 1              
              ADD  1  TO WS-CNT-LINE-ON-PAGE                                    
              PERFORM 4450-WRITE-BLANK   THRU 4450-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE ZEROS           TO   WS-DEF-INC-ST                              
                                     WS-AMORT-INC-ST                            
                                     WS-YTD-INC-ST                              
                                     WS-BAL-DEF-ST                              
                                     WS-SURR-AMT-ST                             
                                     WS-TOT-COMP-ST                             
                                     WS-SRDTE-ST                                
                                     WS-ABDTE-ST.                               
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4600-COMPUTE-SUBTOT.                                                     
      *----------------------------------------------------------------*        
                                                                                
           COMPUTE WS-DEF-INC-ST   = WS-DEF-INC-ST +                            
                                     F690-AMT-DEFER-INCOME.                     
                                                                                
           COMPUTE WS-AMORT-INC-ST = WS-AMORT-INC-ST +                          
                                     F690-AMT-AMORT-INCOME-MTH.                 
                                                                                
           COMPUTE WS-YTD-INC-ST   = WS-YTD-INC-ST +                            
                                     F690-AMT-AMORT-INCOME-ACCUM.               
                                                                                
           COMPUTE WS-BAL-DEF-ST   = WS-BAL-DEF-ST +                            
                                     WS-INC-SURR-AMT.                           
                                                                                
           COMPUTE WS-SURR-AMT-ST  = WS-SURR-AMT-ST +                           
                                     F690-AMT-SURRENDER-BAL.                    
                                                                                
           COMPUTE WS-TOT-COMP-ST  = WS-TOT-COMP-ST +                           
                                     F690-AMT-COMPENSATION.                     
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE BP13F690                                                       
                 SY02F001                                                       
                 BP13L367.                                                      
                                                                                
           DISPLAY '                                  '.                        
           DISPLAY '************ BP13C365 ************'.                        
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ       (BP13F690) = '                     
                    WS-CNT-F690-READ.                                           
           DISPLAY 'NO OF RECORD PRINTED     (BP13L367) = '                     
                    WS-CNT-WRITE-L367.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
