       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C49P.                                                 
       AUTHOR.        KARL MAGALONA CABUG.                                      
      *DATE-WRITTEN.  19/08/2020.                                               
      *************************************************************             
      *      SYSTEM OF COMMITMENT (BP13)                          *             
      *===========================================================*             
      *                                                           *             
      *  OBJECTIVES: 1. TO SEND OUT EMAIL FOR NOTIFICATION (D3)   *             
      *              2. PUT MQ MESSAGE INTO QUEUE WHICH CONTAINS  *             
      *                 CUSTOMER'S DETAILS TO BE SENT BY JAVA     *             
      *                 INTERFACE.                                *             
      *                                                           *             
      *     THIS PROGRAM WILL CATER FOR 12 MONTHS AND USE 'D3' TAG*             
      *                                                           *             
      *  PROGRAM LOGIC:                                           *             
      *          PROCESS INPUT FILES                              *             
      *          MQCONNECT TO QUEUE MANAGER                       *             
      *          MQOPEN QUEUE                                     *             
      *          MQPUT                                            *             
      *          MQCLOSE QUEUE                                    *             
      *          MQDISCONNECT FROM QUEUE MANAGER                  *             
      *                                                           *             
      *                                                           *             
      *                                                           *             
      *===========================================================*             
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHG REQ#    DATE    AUTHOR REMARKS                        *             
      * -------- ---------- ------ ------------------------------ *             
      * BP138418 19/08/2020 KAC1   NEW PROGRAM MIRRORED FROM C49M *             
      * BP139030 07/01/2022 ZAR7  REPLACE CHUA YEE MIEN TO LIM KOK CHUN         
      * BP139276 05/04/2024 KAC1  REPLACE LIM KOK CHUN BY         *             
      *                                   KATHIJAH_BEE_ALI_MOHAMED*             
      *************************************************************             
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       SPECIAL-NAMES.                                                           
           SYSIN IS PARM-INPUT.                                                 
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F491 ASSIGN       TO BP13F491.                            
                                                                                
           SELECT BP13K491 ASSIGN        TO BP13K491                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K491-NUM-REGN                       
                           FILE STATUS   IS WS-K491-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K491                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K491.                                                           
                                                                                
       FD  BP13F491                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13F491.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
      *    QUEUE MANAGER                                                        
      *    FOR PRODUCTION, QUEUE MANAGER IS 'MQP1'                              
      *    FOR TESTING,    QUEUE MANAGER IS 'MQT1'                              
      *-------------------------------------------------------------            
      *PROD QUEUE MANAGER                                                       
       01  WS-QM-NAME                    PIC X(48) VALUE 'MQP1'.                
      *TEST QUEUE MANAGER                                                       
      *01  WS-QM-NAME                    PIC X(48) VALUE 'MQT1'.                
                                                                                
       01  WS-BP13Q-NAME                 PIC X(48) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      *    OUTPUT QUEUE NAME TO CRIMSONLOGIC                                    
      *    FOR PROD, QUEUE NAME IS BP13.WIS.MDB.REQUESTQ                        
      *    FOR TESTING, QUEUE NAME IS BP13S.WIS.MDB.REQUESTQ                    
      *-------------------------------------------------------------            
      **PROD QUEUE                                                              
       01  WS-QUEUE-NAME   PIC X(48) VALUE 'BP13.WIS.MDB.REQUESTQ'.             
      **TEST QUEUE                                                              
      *01  WS-QUEUE-NAME   PIC X(48) VALUE 'BP13S.WIS.MDB.REQUESTQ'.            
                                                                                
      *-------------------------------------------------------------            
      *    MESSAGE LENGTH                                                       
      *-------------------------------------------------------------            
      *01  WS-MESSAGE-LENGTH             PIC 9(06) VALUE  51612.                
       01  WS-MESSAGE-LENGTH             PIC 9(06) VALUE  66011.                
                                                                                
      *-------------------------------------------------------------            
      *    MESSAGE QUEUE                                                        
      *-------------------------------------------------------------            
       01  WS-SEND-MESSAGE.                                                     
           05  WS-MSG-CDE                PIC X(03).                             
           05  WS-FILLER-01              PIC X(01).                             
           05  WS-MSG-APPT-TYP           PIC X(02).                             
           05  WS-FILLER-02              PIC X(01).                             
           05  WS-MSG-CTR                PIC 9(04).                             
           05  WS-MSG OCCURS 400 TIMES.                                         
               10  WS-FILLER-03          PIC X(01).                             
               10  WS-MSG-NUM-REGN       PIC X(08).                             
               10  WS-FILLER-04          PIC X(01).                             
               10  WS-MSG-EMAIL-TYP      PIC X(02).                             
               10  WS-FILLER-05          PIC X(01).                             
               10  WS-MSG-NUM-EMAIL      PIC X(50).                             
               10  WS-FILLER-06          PIC X(01).                             
               10  WS-MSG-NUM-OIC        PIC X(30).                             
               10  WS-FILLER-07          PIC X(01).                             
               10  WS-MSG-DTE-PCD        PIC X(18).                             
               10  WS-FILLER-08          PIC X(01).                             
               10  WS-MSG-ADDR.                                                 
                   15 WS-MSG-BLK         PIC X(05).                             
                   15 WS-FILLER-09       PIC X(01).                             
                   15 WS-MSG-LEVEL       PIC X(02).                             
                   15 WS-FILLER-10       PIC X(01).                             
                   15 WS-MSG-MAIN-UNIT   PIC X(04).                             
                   15 WS-FILLER-11       PIC X(01).                             
                   15 WS-MSG-STREET      PIC X(30).                             
                   15 WS-FILLER-12       PIC X(01).                             
                   15 WS-MSG-POSTAL      PIC X(06).                             
                                                                                
      *-------------------------------------------------------------            
      *    MQI - TEMPORARY VARIABLES FOR ERROR CODES                            
      *-------------------------------------------------------------            
       01  BP13C49P-REC.                                                        
           05  C49P-REASON               PIC 9(4).                              
           05  C49P-COMPLETION-CODE      PIC 9(4).                              
           05  C49P-ERR-MSG              PIC X(66).                             
           05  FILLER                    PIC X(27).                             
                                                                                
      *-------------------------------------------------------------            
      *    MQI STRUCTURE DECLARATION                                            
      *-------------------------------------------------------------            
       01  MY-MQ-CONSTANTS.                                                     
           COPY CMQV.                                                           
                                                                                
       01  OBJECT-DESCRIPTOR.                                                   
           COPY CMQODV.                                                         
                                                                                
       01  MESSAGE-DESCRIPTOR.                                                  
           COPY CMQMDV.                                                         
                                                                                
      *-------------------------------------------------------------            
      *    PUT MESSAGE OPTIONS                                                  
      *-------------------------------------------------------------            
       01  PMOPTIONS.                                                           
           COPY CMQPMOV.                                                        
                                                                                
      *-------------------------------------------------------------            
      *    VARIABLE DESCRIPTION:                                                
      *    (1) HCONN                                                            
      *        - CONNECTION HANDLE - REPRESENTS THE CONNECTION TO               
      *          THE QUEUE MANAGER. VALUE IS RETURNED BY MQCONN                 
      *          AND SUBSEQUENTLY REFERENCED IN MQOPEN, MQPUT,                  
      *          MQCLOSE AND MQDISC                                             
      *                                                                         
      *    (2) Q-HANDLE                                                         
      *        - ACCESS THAT HAS BEEN ESTABLISHED  TO THE OBJECT                
      *          CONNECTION HANDLE - VALUE IS RETURNED BY MQOPEN                
      *          AND SUBSEQUENTLY REFERENCED IN MQPUT AND MQCLOSE               
      *                                                                         
      *    (3) OBJECT-DESCRIPTOR                                                
      *        - THIS IDENTIFIES THE OBJECT TO BE OPENED                        
      *                                                                         
      *    (4) OPTIONS                                                          
      *        WHEN USED IN MQOPEN                                              
      *        - OPTIONS THAT CONTROL THE ACTION OF MQOPEN                      
      *        - OPTIONS USED IN THIS PROGRAM:                                  
      *          (A) MQOO-OUTPUT - THE QUEUE IS OPENED FOR USE WITH             
      *                            SUBSEQUENT MQPUT CALL                        
      *          (B) MQOO-FAIL-IF-QUIESCING - FORCES MQCALL TO FAIL             
      *                            IF QUEUE MANAGER IS IN QUIESCING             
      *                            STATE (I.E. PREPARING TO SHUTDOWN)           
      *                                                                         
      *        WHEN USED IN MQCLOSE                                             
      *        - OPTIONS THAT CONTROL THE ACTION OF MQCLOSE                     
      *        - OPTIONS USED IN THIS PROGRAM:                                  
      *          (A) MQCO-NONE   - NO OPTIONAL CLOSE PROCESSING                 
      *                            REQUIRED                                     
      *                                                                         
      *    (5) MESSAGE-DESCRIPTOR                                               
      *        - THIS DESCRIBES THE ATTRIBUTES OF THE MESSAGE                   
      *          BEING SENT, AND RECEIVES INFO ABOUT THE MESSAGE                
      *          AFTER THE PUT REQUEST IS COMPLETE                              
      *                                                                         
      *    (6) PMOPTIONS (PUT MESSAGE OPTIONS)                                  
      *        - OPTIONS THAT CONTROL THE ACTION OF MQPUT                       
      *        - OPTION USED IN THIS PROGRAM:                                   
      *          (A) MQMD-EXPIRY - MESSAGE LIFETIME. THE MESSAGE                
      *                            BECOMES ELIGIBLE TO BE DISCARDED             
      *                            IF IT HAS NOT BEEN REMOVED FROM              
      *                            FROM THE QUEUE BEFORE THIS PERIOD            
      *                            OF TIME ELAPSED                              
      *                          - MUST BE SET TO MQEI_UNLIMITED                
      *          (B) MQMD-PERSISTENCE = MQPER-PERSISTENT                        
      *                                                                         
      *    (7) BUFFER-LENGTH                                                    
      *        - LENGTH OF THE MESSAGE IN BUFFER                                
      *                                                                         
      *    (8) BUFFER                                                           
      *        - MESSAGE DATA                                                   
      *-------------------------------------------------------------            
       01  HCONN                         PIC S9(9) BINARY.                      
       01  Q-HANDLE                      PIC S9(9) BINARY.                      
       01  OPTIONS                       PIC S9(9) BINARY.                      
       01  COMPLETION-CODE               PIC S9(9) BINARY.                      
       01  OPEN-CODE                     PIC S9(9) BINARY.                      
       01  CON-REASON                    PIC S9(9) BINARY.                      
       01  REASON                        PIC S9(9) BINARY.                      
       01  MQMSGD                        PIC S9(9) BINARY.                      
       01  BUFFER-LENGTH                 PIC S9(9) BINARY.                      
       01  BUFFER                        PIC X(368012).                         
       01  QM-NAME                       PIC X(48) VALUE SPACES.                
       01  TARGET-QUEUE                  PIC X(48) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      *  FLAGS / SWITCHES                                                       
      *-------------------------------------------------------------            
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-IDX                    PIC 9(03) VALUE ZEROES.                
           05  WS-CURR-DATE              PIC X(08) VALUE SPACES.                
           05  WS-CURR-TIME              PIC X(08) VALUE SPACES.                
           05  WS-K491-STATUS            PIC 9(02) VALUE ZEROES.                
           05  WS-F491-EOF               PIC X(01) VALUE SPACES.                
           05  WS-REWRITE-CTR            PIC 9(06) VALUE ZEROES.                
           05  WS-NOTFND-CTR             PIC 9(06) VALUE ZEROES.                
           05  WS-F491-CTR               PIC 9(06) VALUE ZEROES.                
           05  WS-CTR                    PIC 9(06) VALUE ZEROES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-MAIN-PROCESS.                                                       
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-INITIALIZATION     THRU 1100-EXIT.                      
           PERFORM 2000-BP13F491-READ      THRU 2000-EXIT.                      
                                                                                
           IF WS-F491-EOF = 'Y'                                                 
              DISPLAY ' *** '                                                   
              DISPLAY ' *** NO RECORDS FOUND IN PRELIM FILE'                    
              DISPLAY ' *** '                                                   
                                                                                
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2100-INIT-CONNECTION    THRU 2100-EXIT.                      
                                                                                
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F491-EOF = 'Y'                                      
                      OR WS-MSG-CTR = 400.                                      
                                                                                
           DISPLAY 'WS-SEND-MESSAGE : ' WS-SEND-MESSAGE.                        
                                                                                
                                                                                
           PERFORM 4000-MQ-PUT-MESSAGE     THRU 4000-EXIT.                      
           DISPLAY ' *** APPOINTMENT NOTIFICATION SENT!'                        
           PERFORM 5000-MQ-CLOSE           THRU 5000-EXIT.                      
           PERFORM 5100-MQ-DISCONNECT      THRU 5100-EXIT.                      
           PERFORM 6000-UPDATE-BP13K491    THRU 6000-EXIT.                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
           DISPLAY ' ***** STARTING PROGRAM BP13C49P *****'.                    
           DISPLAY ' '.                                                         
                                                                                
           OPEN INPUT BP13F491                                                  
                I-O   BP13K491.                                                 
                                                                                
           IF WS-K491-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K491 - ERROR OPENING : ' WS-K491-STATUS              
              MOVE WS-K491-STATUS                   TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       1100-INITIALIZATION.                                                     
      *=============================================================            
           MOVE SPACES                    TO WS-SEND-MESSAGE.                   
                                                                                
           MOVE ';'                       TO WS-FILLER-01                       
                                             WS-FILLER-02.                      
                                                                                
           PERFORM VARYING WS-MSG-CTR FROM 1 BY 1                               
             UNTIL WS-MSG-CTR > 400                                             
                   MOVE ';'               TO WS-FILLER-03(WS-MSG-CTR)   TR)     
                                             WS-FILLER-04(WS-MSG-CTR)   TR)     
                                             WS-FILLER-05(WS-MSG-CTR)   TR)     
                                             WS-FILLER-06(WS-MSG-CTR)   TR)     
                                             WS-FILLER-07(WS-MSG-CTR)   TR)     
                                             WS-FILLER-08(WS-MSG-CTR)   TR)     
                                             WS-FILLER-09(WS-MSG-CTR)   TR)     
                                             WS-FILLER-10(WS-MSG-CTR)   TR)     
                                             WS-FILLER-11(WS-MSG-CTR)   TR)     
                                             WS-FILLER-12(WS-MSG-CTR)   TR)     
           END-PERFORM.                                                         
                                                                                
           MOVE 'EML'                     TO WS-MSG-CDE.                        
           MOVE ZEROES                    TO WS-MSG-CTR.                        
                                                                                
           ACCEPT WS-BP13Q-NAME         FROM PARM-INPUT.                        
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2000-BP13F491-READ.                                                      
      *=============================================================            
           READ BP13F491                                                        
              AT END MOVE 'Y'              TO   WS-F491-EOF                     
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1  TO WS-F491-CTR.                                               
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2100-INIT-CONNECTION.                                                    
      *=============================================================            
           PERFORM 2200-MQ-CONNECT         THRU 2200-EXIT                       
           PERFORM 2300-MQ-OPEN            THRU 2300-EXIT                       
                                                                                
           MOVE SPACES                     TO   BUFFER.                         
      *    MOVE 'K1'                       TO   WS-MSG-APPT-TYP.                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2200-MQ-CONNECT.                                                         
      *=============================================================            
      *-------------------------------------------------------------            
      *    SET THE QUEUE MANAGER NAME                                           
      *-------------------------------------------------------------            
           IF WS-BP13Q-NAME NOT = SPACES AND LOW-VALUES                         
              MOVE WS-BP13Q-NAME           TO   QM-NAME                         
           ELSE                                                                 
              MOVE WS-QM-NAME              TO   QM-NAME                         
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQCONN TO CONNECT TO QUEUE MANAGER                              
      *-------------------------------------------------------------            
           CALL 'MQCONN' USING QM-NAME, HCONN,                                  
                               COMPLETION-CODE, CON-REASON.                     
                                                                                
           IF COMPLETION-CODE = MQCC-FAILED                                     
              MOVE CON-REASON              TO   C49P-REASON                     
              MOVE COMPLETION-CODE         TO   C49P-COMPLETION-CODE            
              MOVE 'BP13CS49 MQCONN ERROR' TO   C49P-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2300-MQ-OPEN.                                                            
      *=============================================================            
      *-------------------------------------------------------------            
      *    USE DEFAULT WS-QUEUE-NAME                                            
      *-------------------------------------------------------------            
           MOVE WS-QUEUE-NAME              TO   MQOD-OBJECTNAME.                
           ADD  MQOO-OUTPUT MQOO-FAIL-IF-QUIESCING GIVING OPTIONS.              
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQOPEN TO OPEN CONNECTION                                       
      *-------------------------------------------------------------            
           CALL 'MQOPEN' USING HCONN, OBJECT-DESCRIPTOR,                        
                               OPTIONS, Q-HANDLE,                               
                               OPEN-CODE, REASON.                               
                                                                                
           IF REASON NOT = MQRC-NONE                                            
              MOVE REASON                  TO   C49P-REASON                     
              MOVE OPEN-CODE               TO   C49P-COMPLETION-CODE            
              MOVE 'MQOPEN ENDED WITH REASON CODE'                              
                                           TO   C49P-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF F491-NUM-REGN NOT = SPACES AND LOW-VALUES                         
              ADD 1                    TO WS-MSG-CTR                            
              MOVE F491-NUM-REGN       TO WS-MSG-NUM-REGN (WS-MSG-CTR)          
              MOVE 'D3'                TO WS-MSG-APPT-TYP                       
                                          WS-MSG-EMAIL-TYP(WS-MSG-CTR)          
              MOVE F491-NUM-EMAIL      TO WS-MSG-NUM-EMAIL(WS-MSG-CTR)          
      ***     MOVE F491-NME-OIC        TO WS-MSG-NUM-OIC  (WS-MSG-CTR)          
              MOVE 'KATHIJAH BEE ALI MOHAMED'                                   
                                         TO WS-MSG-NUM-OIC(WS-MSG-CTR)          
              MOVE F491-DTE-PCD        TO WS-MSG-DTE-PCD  (WS-MSG-CTR)          
              MOVE F491-NUM-BLK        TO WS-MSG-BLK      (WS-MSG-CTR)          
              MOVE F491-NUM-LEVEL      TO WS-MSG-LEVEL    (WS-MSG-CTR)          
              MOVE F491-NUM-MAIN-UNIT  TO WS-MSG-MAIN-UNIT(WS-MSG-CTR)          
              MOVE F491-NUM-STREET     TO WS-MSG-STREET   (WS-MSG-CTR)          
              MOVE F491-NUM-POSTAL     TO WS-MSG-POSTAL   (WS-MSG-CTR)          
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F491-READ THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4000-MQ-PUT-MESSAGE.                                                     
      *=============================================================            
                                                                                
           MOVE WS-SEND-MESSAGE            TO   BUFFER.                         
                                                                                
      *-------------------------------------------------------------            
      *    SET UP MESSAGE DESCRIPTOR                                            
      *-------------------------------------------------------------            
           MOVE MQEI-UNLIMITED             TO   MQMD-EXPIRY.                    
           MOVE MQPER-PERSISTENT           TO   MQMD-PERSISTENCE.               
           MOVE MQFMT-STRING               TO   MQMD-FORMAT.                    
           MOVE WS-MESSAGE-LENGTH          TO   BUFFER-LENGTH.                  
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQPUT TO PUT MESSAGE TO QUEUE                                   
      *-------------------------------------------------------------            
           CALL 'MQPUT' USING HCONN, Q-HANDLE,                                  
                              MESSAGE-DESCRIPTOR, PMOPTIONS,                    
                              BUFFER-LENGTH, BUFFER,                            
                              COMPLETION-CODE, REASON.                          
                                                                                
           IF REASON NOT = MQRC-NONE                                            
              MOVE REASON                  TO   C49P-REASON                     
              MOVE COMPLETION-CODE         TO   C49P-COMPLETION-CODE            
              MOVE 'BP13CS49 MQPUT ERROR'  TO   C49P-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       5000-MQ-CLOSE.                                                           
      *=============================================================            
                                                                                
           MOVE MQCO-NONE                  TO   OPTIONS.                        
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQCLOSE TO CLOSE CONNECTION                                     
      *-------------------------------------------------------------            
           CALL 'MQCLOSE' USING HCONN, Q-HANDLE, OPTIONS,                       
                                COMPLETION-CODE, REASON.                        
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                   TO   C49P-REASON                    
              MOVE COMPLETION-CODE          TO   C49P-COMPLETION-CODE           
              MOVE 'BP13CS49 MQCLOSE ERROR' TO   C49P-ERR-MSG                   
              PERFORM 9100-EXIT-MQCONN      THRU 9100-EXIT                      
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       5100-MQ-DISCONNECT.                                                      
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQDISC TO DISCONNECT FROM QUEUE MANAGER                         
      *-------------------------------------------------------------            
           CALL 'MQDISC' USING HCONN, COMPLETION-CODE, REASON.                  
                                                                                
           IF REASON NOT = MQRC-NONE                                            
              MOVE REASON                  TO   C49P-REASON                     
              MOVE COMPLETION-CODE         TO   C49P-COMPLETION-CODE            
              MOVE 'BP13CS49 MQDISC ERROR' TO   C49P-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       6000-UPDATE-BP13K491.                                                    
      *=============================================================            
           PERFORM VARYING WS-CTR FROM 1 BY 1                                   
              UNTIL WS-CTR > 400 OR WS-MSG-NUM-REGN(WS-CTR) = SPACES            
              IF WS-MSG-NUM-REGN(WS-CTR) NOT = SPACES AND LOW-VALUES            
                 MOVE WS-MSG-NUM-REGN(WS-CTR)      TO K491-NUM-REGN             
                 PERFORM 6100-READUDT-BP13K491   THRU 6100-EXIT                 
              END-IF                                                            
           END-PERFORM.                                                         
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       6100-READUDT-BP13K491.                                                   
      *=============================================================            
           READ BP13K491.                                                       
                                                                                
           EVALUATE WS-K491-STATUS                                              
               WHEN 00                                                          
                    MOVE '2'                        TO K491-TAG-FORM            
      ***           MOVE WS-CURR-DATE               TO K491-DTE-UPDATE          
                    PERFORM 6200-REWRITE-BP13K491 THRU 6200-EXIT                
               WHEN 23                                                          
                    DISPLAY 'K491 - REC NOTFND, KEY : ' K491-NUM-REGN           
                    ADD 1                           TO WS-NOTFND-CTR            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING OF BP13K491 : ' K491-NUM-REGN        
                            'STATUS(', WS-K491-STATUS ')'                       
                    MOVE WS-K491-STATUS             TO RETURN-CODE              
                    PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                 
           END-EVALUATE.                                                        
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       6200-REWRITE-BP13K491.                                                   
      *=============================================================            
           REWRITE BP13K491-REC.                                                
                                                                                
           IF WS-K491-STATUS = 00 OR 02                                         
              ADD 1 TO WS-REWRITE-CTR                                           
           ELSE                                                                 
              DISPLAY 'BP13K491 - REWRITE ERROR, KEY : ' K491-NUM-REGN          
                      ' , STATUS (' WS-K491-STATUS ')'                          
              MOVE WS-K491-STATUS                  TO RETURN-CODE               
              PERFORM 9000-CLOSE-ROUTINE         THRU 9000-EXIT                 
           END-IF.                                                              
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F491                                                       
                 BP13K491.                                                      
                                                                                
           IF RETURN-CODE NOT = ZERO                                            
              DISPLAY ' *** RETURN-CODE = ' RETURN-CODE ' ERROR'                
           ELSE                                                                 
              DISPLAY ' '                                                       
              DISPLAY ' *** SUCCESSFUL EXECUTION OF BP13C49P.'                  
              DISPLAY ' *** MESSAGE/S PUT INTO QUEUE SUCCESSFULLY!'             
              DISPLAY ' *** Q MANAGER : ' QM-NAME                               
              DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                         
              DISPLAY ' *** INPUT  (K491) : ' WS-F491-CTR                       
              DISPLAY ' *** REWRITE(K491) : ' WS-REWRITE-CTR                    
              DISPLAY ' *** NOTFND (K491) : ' WS-NOTFND-CTR                     
              DISPLAY ' '                                                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9100-EXIT-MQCONN.                                                        
      *=============================================================            
                                                                                
           DISPLAY ' *** PROGRAM ID:  BP13C49P '                                
           DISPLAY ' *** Q MANAGER : ' QM-NAME                                  
           DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                            
           DISPLAY ' '                                                          
                                                                                
           IF C49P-COMPLETION-CODE NOT = ZEROES AND LOW-VALUES                  
              DISPLAY ' *** MQ COMPLETION CODE: ' C49P-COMPLETION-CODE          
              DISPLAY ' *** MQ REASON         : ' C49P-REASON                   
              DISPLAY ' *** MQ ERROR MSG      : ' C49P-ERR-MSG                  
              MOVE 99                      TO  RETURN-CODE                      
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       9100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
      **********           END OF BP13C49P                **********            
      **************************************************************            
