       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C45C.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  24/10/2016.                                               
      * ========================================================== *            
      * OBJECTIVE    : MONTHLY PROGRAM TO COMPARE MANPOWER SUPPLY  *            
      *                (BP13K452) TO MANPOWER DEMAND (BP13KC30).   *            
      *                GENERATE REPORT FOR DAYS WITH INSUFFICIENT  *            
      *                MANPOWER AT HAND.                           *            
      *                UPDATE WORKPLAN MAX TOTAL FOR SELFBOOK      *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F452                               *            
      *              :  2.) BP13KC30                               *            
      * I-O    FILES :  1.) BP13K452                               *            
      * LISTING      :  1.) BP13L45C                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136561 KSJ3 24/10/2016 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F452  ASSIGN        TO BP13F452.                          
                                                                                
           SELECT BP13KC30  ASSIGN        TO BP13KC30                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC30-KEY-FLD                       
                            ALTERNATE KEY IS KC30-ALT-KEY1                      
                            FILE STATUS   IS WS-KC30-STATUS.                    
                                                                                
           SELECT BP13K452  ASSIGN        TO BP13K452                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K452-KEY-FLD                       
                            FILE STATUS   IS WS-K452-STATUS.                    
                                                                                
           SELECT BP13L45C  ASSIGN        TO BP13L45C.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F452                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 1000 CHARACTERS.                                     
           COPY BP13F452.                                                       
                                                                                
       FD  BP13KC30                                                             
           RECORD CONTAINS 200  CHARACTERS.                                     
           COPY BP13KC30.                                                       
                                                                                
       FD  BP13K452                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
           COPY BP13K452.                                                       
                                                                                
       FD  BP13L45C                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132  CHARACTERS.                                     
       01  BP13L45C-REC                    PIC X(132).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KC30-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K452-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                      PIC 9(2)   VALUE ZEROES.               
           05  WS-CNT-SLOTS              PIC 9(2)   VALUE ZEROES.               
           05  WS-CNT-F452-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K452-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-L45C-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE               PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE               PIC 9(2)   VALUE 99.                   
           05  WS-CNT-SNO                PIC 9(5)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F452-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC30-EOF               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
           05  WS-DTE-START.                                                    
               10  WS-DTE-START-CCYY       PIC 9(4).                            
               10  WS-DTE-START-MM         PIC 9(2).                            
               10  WS-DTE-START-DD         PIC 9(2).                            
           05  WS-DTE-END.                                                      
               10  WS-DTE-END-CCYY         PIC 9(4).                            
               10  WS-DTE-END-MM           PIC 9(2).                            
               10  WS-DTE-END-DD           PIC 9(2).                            
           05  WS-TBL-MTH-END-DD           PIC X(24)  VALUE                     
               '312831303130313130313031'.                                      
           05  FILLER REDEFINES WS-TBL-MTH-END-DD.                              
               10  FILLER OCCURS 12 TIMES.                                      
                   15  WS-MTH-END-DD       PIC 9(2).                            
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-DTE-APPT               PIC X(8)   VALUE SPACES.               
           05  WS-TME-APPT               PIC X(4)   VALUE SPACES.               
           05  WS-NUM-DIFF               PIC 9(2)   VALUE ZEROES.               
           05  WS-NUM-TOTAL              PIC 9(2)   VALUE ZEROES.               
           05  WS-NUM-MAX                PIC 9(2)   VALUE ZEROES.               
           05  WS-NUM-TAKEN-TP           PIC 9(2)   VALUE ZEROES.               
           05  WS-NUM-TAKEN-SA           PIC 9(2)   VALUE ZEROES.               
           05  WS-NUM-MAX-SB             PIC 9(2)   VALUE ZEROES.               
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT                                                        
      *-------------------------------------------------------------            
       01  L45C-PR-HEAD-01.                                                     
           05  FILLER                 PIC X(14)   VALUE 'BP13C45C'.             
           05  FILLER                 PIC X(8)    VALUE 'HDB3    '.             
           05  FILLER                 PIC X(25)   VALUE SPACES.                 
           05  FILLER                 PIC X(39)   VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                 PIC X(13)   VALUE SPACES.                 
           05  FILLER                 PIC X(7)    VALUE 'DATE: '.               
           05  PRINT-CURRENT-DATE     PIC X(10)   VALUE SPACES.                 
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  FILLER                 PIC X(7)    VALUE 'PAGE: '.               
           05  PRINT-PAGE             PIC ZZZ9.                                 
                                                                                
       01  L45C-PR-HEAD-02.                                                     
           05  FILLER                 PIC X(30)   VALUE SPACES.                 
           05  FILLER                 PIC X(72)   VALUE                         
              'DATES WITH INSUFFICIENT MANPOWER FOR MANUAL AND SELF BOOK        
      -       'ED APPOINTMENTS'.                                                
                                                                                
       01  L45C-PR-TITLE-1.                                                     
           05  FILLER                 PIC X(132)  VALUE                         
              'S/NO     DATE         TIME     MAX MANUAL SLOTS     TAKEN        
      -       ' TP SLOTS     TAKEN SA SLOTS     SELF BOOKING SLOTS'.            
                                                                                
       01  L45C-PR-TITLE-2.                                                     
           05  FILLER                 PIC X(132)  VALUE                         
              '----     --------     ----     ----------------     -----        
      -       '---------     --------------     ------------------'.            
                                                                                
       01  L45C-PR-DTL.                                                         
           05  L45C-NUM-SNO           PIC ZZZ9.                                 
           05  FILLER                 PIC X(5)    VALUE SPACES.                 
           05  L45C-DTE-APPT          PIC X(8).                                 
           05  FILLER                 PIC X(5)    VALUE SPACES.                 
           05  L45C-TME-APPT          PIC X(4).                                 
           05  FILLER                 PIC X(19)   VALUE SPACES.                 
           05  L45C-NUM-MAX           PIC Z9.                                   
           05  FILLER                 PIC X(17)   VALUE SPACES.                 
           05  L45C-NUM-TAKEN-TP      PIC Z9.                                   
           05  FILLER                 PIC X(17)   VALUE SPACES.                 
           05  L45C-NUM-TAKEN-SA      PIC Z9.                                   
           05  FILLER                 PIC X(21)   VALUE SPACES.                 
           05  L45C-NUM-MAX-SB        PIC Z9.                                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13F452-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F452-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F452                                                 
                       BP13KC30                                                 
                I-O    BP13K452                                                 
                OUTPUT BP13L45C.                                                
                                                                                
           IF WS-KC30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC30 ERROR ' WS-KC30-STATUS                  
              MOVE     WS-KC30-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K452-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K452 ERROR ' WS-K452-STATUS                  
              MOVE     WS-K452-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8                     
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
      *-------------------------------------------------------------            
      *    GET START DATE                                                       
      *-------------------------------------------------------------            
           MOVE WS-DTE-CURR-8              TO   WS-DTE-START.                   
                                                                                
           IF WS-DTE-START-MM = 12                                              
              MOVE 1                       TO   WS-DTE-START-MM                 
              ADD  1                       TO   WS-DTE-START-CCYY               
           ELSE                                                                 
              ADD  1                       TO   WS-DTE-START-MM                 
           END-IF.                                                              
                                                                                
           MOVE 1                          TO   WS-DTE-START-DD.                
                                                                                
      *-------------------------------------------------------------            
      *    GET END DATE                                                         
      *-------------------------------------------------------------            
           MOVE WS-DTE-START               TO   WS-DTE-END.                     
                                                                                
           IF WS-DTE-END-MM >= 11                                               
              COMPUTE WS-DTE-END-MM = 12 - WS-DTE-END-MM + 2                    
              ADD  1                       TO   WS-DTE-END-CCYY                 
           ELSE                                                                 
              ADD  2                       TO   WS-DTE-END-MM                   
           END-IF.                                                              
                                                                                
           MOVE 1                          TO   WS-DTE-END-DD.                  
                                                                                
           DISPLAY 'WS-DTE-START = ' WS-DTE-START.                              
           DISPLAY 'WS-DTE-END   = ' WS-DTE-END.                                
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F452-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F452                                                        
              AT END MOVE 'Y'              TO   WS-F452-EOF.                    
                                                                                
           IF WS-F452-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-F452-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF F452-CDE-APPT-TYPE = 'TP'     AND                                 
              F452-DTE-APPT >= WS-DTE-START AND                                 
              F452-DTE-APPT < WS-DTE-END                                        
                                                                                
              PERFORM 3100-CHK-AM-ALLOC    THRU 3100-EXIT                       
                      VARYING WS-I FROM 1 BY 1                                  
                        UNTIL (F452-TME-SLOT(WS-I) = SPACES OR                  
                                                LOW-VALUES OR ZEROES)           
                           OR (WS-I > 18)                                       
                                                                                
              PERFORM 3200-CHK-PM-ALLOC    THRU 3200-EXIT                       
                      VARYING WS-I FROM 1 BY 1                                  
                        UNTIL (F452-PM-TME-SLOT(WS-I) = SPACES OR               
                                                LOW-VALUES OR ZEROES)           
                           OR (WS-I > 18)                                       
                                                                                
              PERFORM 3300-CHK-OT-ALLOC    THRU 3300-EXIT                       
                      VARYING WS-I FROM 1 BY 1                                  
                        UNTIL (F452-OT-TME-SLOT(WS-I) = SPACES OR               
                                                LOW-VALUES OR ZEROES)           
                           OR (WS-I > 18)                                       
                                                                                
              MOVE BP13F452-REC            TO   BP13K452-REC                    
              PERFORM 3400-BP13K452-REWRITE     THRU 3400-EXIT                  
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F452-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3010-BP13KC30-STARTBR.                                                   
      *=============================================================            
                                                                                
           START BP13KC30 KEY >= KC30-ALT-KEY1.                                 
                                                                                
           EVALUATE WS-KC30-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              PERFORM 3020-BP13KC30-READNEXT    THRU 3020-EXIT                  
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-KC30-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START BP13KC30 ERROR '   WS-KC30-STATUS                  
              MOVE    WS-KC30-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3020-BP13KC30-READNEXT.                                                  
      *=============================================================            
                                                                                
           READ BP13KC30 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-KC30-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              IF KC30-NUM-APPT-TYPE NOT = F452-CDE-APPT-TYPE OR                 
                 KC30-DTE-APPT      NOT = WS-DTE-APPT        OR                 
                 KC30-TME-APPT      NOT = WS-TME-APPT                           
                 MOVE 'Y'                  TO   WS-KC30-EOF                     
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-KC30-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13KC30 ERROR '    WS-KC30-STATUS                  
              MOVE    WS-KC30-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-CHK-AM-ALLOC.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    CHECK SLOTS ALLOTTED FOR SELF BOOKING                                
      *-------------------------------------------------------------            
           MOVE 'N'                     TO   WS-KC30-EOF                        
           MOVE ZEROES                  TO   WS-CNT-SLOTS.                      
                                                                                
           MOVE SPACES                  TO   BP13KC30-REC.                      
           MOVE F452-CDE-APPT-TYPE      TO   KC30-NUM-APPT-TYPE.                
           MOVE F452-DTE-APPT           TO   KC30-DTE-APPT                      
                                             WS-DTE-APPT.                       
           MOVE F452-TME-SLOT(WS-I)     TO   WS-TME-APPT                        
                                             KC30-TME-APPT.                     
                                                                                
           PERFORM 3010-BP13KC30-STARTBR     THRU 3010-EXIT                     
                                                                                
           PERFORM UNTIL WS-KC30-EOF = 'Y'                                      
              ADD 1                     TO   WS-CNT-SLOTS                       
                                                                                
              PERFORM 3020-BP13KC30-READNEXT THRU 3020-EXIT                     
           END-PERFORM.                                                         
                                                                                
           MOVE WS-CNT-SLOTS            TO   F452-NUM-MAX-CASE-SB(WS-I).        
                                                                                
      *-------------------------------------------------------------            
      *    MATCH AGAINST SLOTS ALLOTTED FOR MANUAL BOOKING                      
      *-------------------------------------------------------------            
                                                                                
           COMPUTE WS-NUM-DIFF  = 20 - F452-NUM-MAX-CASE-SB(WS-I).              
           COMPUTE WS-NUM-TOTAL = F452-NUM-TOT-TP(WS-I) +                       
                                  F452-NUM-TOT-SA(WS-I).                        
                                                                                
           IF WS-NUM-DIFF < WS-NUM-TOTAL                                        
              MOVE F452-NUM-MAX-CASE(WS-I) TO   WS-NUM-MAX                      
              MOVE F452-NUM-TOT-TP(WS-I)   TO   WS-NUM-TAKEN-TP                 
              MOVE F452-NUM-TOT-SA(WS-I)   TO   WS-NUM-TAKEN-SA                 
              MOVE F452-NUM-MAX-CASE-SB(WS-I)                                   
                                           TO   WS-NUM-MAX-SB                   
              PERFORM 3500-PRINT-REPORT    THRU 3500-EXIT                       
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-CHK-PM-ALLOC.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    CHECK SLOTS ALLOTTED FOR SELF BOOKING                                
      *-------------------------------------------------------------            
           MOVE 'N'                     TO   WS-KC30-EOF                        
           MOVE ZEROES                  TO   WS-CNT-SLOTS.                      
                                                                                
           MOVE SPACES                  TO   BP13KC30-REC.                      
           MOVE F452-CDE-APPT-TYPE      TO   KC30-NUM-APPT-TYPE.                
           MOVE F452-DTE-APPT           TO   KC30-DTE-APPT                      
                                             WS-DTE-APPT.                       
           MOVE F452-PM-TME-SLOT(WS-I)  TO   WS-TME-APPT                        
                                             KC30-TME-APPT.                     
                                                                                
           PERFORM 3010-BP13KC30-STARTBR     THRU 3010-EXIT                     
                                                                                
           PERFORM UNTIL WS-KC30-EOF = 'Y'                                      
              ADD 1                     TO   WS-CNT-SLOTS                       
                                                                                
              PERFORM 3020-BP13KC30-READNEXT THRU 3020-EXIT                     
           END-PERFORM.                                                         
                                                                                
           MOVE WS-CNT-SLOTS            TO   F452-PM-MAX-CASE-SB(WS-I).         
                                                                                
      *-------------------------------------------------------------            
      *    MATCH AGAINST SLOTS ALLOTTED FOR MANUAL BOOKING                      
      *-------------------------------------------------------------            
                                                                                
           COMPUTE WS-NUM-DIFF  = 20 - F452-PM-MAX-CASE-SB(WS-I).               
           COMPUTE WS-NUM-TOTAL = F452-PM-TOT-TP(WS-I) +                        
                                  F452-PM-TOT-SA(WS-I).                         
                                                                                
           IF WS-NUM-DIFF < WS-NUM-TOTAL                                        
              MOVE F452-PM-MAX-CASE(WS-I)  TO   WS-NUM-MAX                      
              MOVE F452-PM-TOT-TP(WS-I)    TO   WS-NUM-TAKEN-TP                 
              MOVE F452-PM-TOT-SA(WS-I)    TO   WS-NUM-TAKEN-SA                 
              MOVE F452-PM-MAX-CASE-SB(WS-I)                                    
                                           TO   WS-NUM-MAX-SB                   
              PERFORM 3500-PRINT-REPORT    THRU 3500-EXIT                       
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-CHK-OT-ALLOC.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    CHECK SLOTS ALLOTTED FOR SELF BOOKING                                
      *-------------------------------------------------------------            
           MOVE 'N'                     TO   WS-KC30-EOF                        
           MOVE ZEROES                  TO   WS-CNT-SLOTS.                      
                                                                                
           MOVE SPACES                  TO   BP13KC30-REC.                      
           MOVE F452-CDE-APPT-TYPE      TO   KC30-NUM-APPT-TYPE.                
           MOVE F452-DTE-APPT           TO   KC30-DTE-APPT                      
                                             WS-DTE-APPT.                       
           MOVE F452-OT-TME-SLOT(WS-I)  TO   WS-TME-APPT                        
                                             KC30-TME-APPT.                     
                                                                                
           PERFORM 3010-BP13KC30-STARTBR     THRU 3010-EXIT                     
                                                                                
           PERFORM UNTIL WS-KC30-EOF = 'Y'                                      
              ADD 1                     TO   WS-CNT-SLOTS                       
                                                                                
              PERFORM 3020-BP13KC30-READNEXT THRU 3020-EXIT                     
           END-PERFORM.                                                         
                                                                                
           MOVE WS-CNT-SLOTS            TO   F452-OT-MAX-CASE-SB(WS-I).         
                                                                                
      *-------------------------------------------------------------            
      *    MATCH AGAINST SLOTS ALLOTTED FOR MANUAL BOOKING                      
      *-------------------------------------------------------------            
                                                                                
           COMPUTE WS-NUM-DIFF  = 20 - F452-OT-MAX-CASE-SB(WS-I).               
           COMPUTE WS-NUM-TOTAL = F452-OT-TOT-TP(WS-I) +                        
                                  F452-OT-TOT-SA(WS-I).                         
                                                                                
           IF WS-NUM-DIFF < WS-NUM-TOTAL                                        
              MOVE F452-OT-MAX-CASE(WS-I)  TO   WS-NUM-MAX                      
              MOVE F452-OT-TOT-TP(WS-I)    TO   WS-NUM-TAKEN-TP                 
              MOVE F452-OT-TOT-SA(WS-I)    TO   WS-NUM-TAKEN-SA                 
              MOVE F452-OT-MAX-CASE-SB(WS-I)                                    
                                           TO   WS-NUM-MAX-SB                   
              PERFORM 3500-PRINT-REPORT    THRU 3500-EXIT                       
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3400-BP13K452-REWRITE.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K452-REC.                                                
                                                                                
           EVALUATE WS-K452-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-K452-UPD                 
                                                                                
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13K452 ERROR '   WS-K452-STATUS                
              MOVE     WS-K452-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3500-PRINT-REPORT.                                                       
      *=============================================================            
                                                                                
           IF WS-CNT-LINE > 55                                                  
              PERFORM 3510-PRINT-REPORT-HDR     THRU 3510-EXIT                  
           END-IF.                                                              
                                                                                
           ADD 1                           TO   WS-CNT-SNO.                     
           MOVE WS-CNT-SNO                 TO   L45C-NUM-SNO.                   
           MOVE WS-DTE-APPT                TO   L45C-DTE-APPT.                  
           MOVE WS-TME-APPT                TO   L45C-TME-APPT.                  
           MOVE WS-NUM-MAX                 TO   L45C-NUM-MAX.                   
           MOVE WS-NUM-TAKEN-TP            TO   L45C-NUM-TAKEN-TP.              
           MOVE WS-NUM-TAKEN-SA            TO   L45C-NUM-TAKEN-SA.              
           MOVE WS-NUM-MAX-SB              TO   L45C-NUM-MAX-SB.                
                                                                                
           WRITE BP13L45C-REC FROM L45C-PR-DTL.                                 
                                                                                
           ADD 1                           TO   WS-CNT-LINE.                    
           ADD 1                           TO   WS-CNT-L45C-WRITE.              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3510-PRINT-REPORT-HDR.                                                   
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-PAGE.                    
           MOVE WS-CNT-PAGE                TO   PRINT-PAGE.                     
           MOVE WS-DTE-CURR-10             TO   PRINT-CURRENT-DATE.             
                                                                                
           WRITE BP13L45C-REC FROM L45C-PR-HEAD-01 AFTER PAGE.                  
           WRITE BP13L45C-REC FROM L45C-PR-HEAD-02.                             
           WRITE BP13L45C-REC FROM L45C-PR-TITLE-1 AFTER 2.                     
           WRITE BP13L45C-REC FROM L45C-PR-TITLE-2.                             
                                                                                
           MOVE SPACES                     TO   BP13L45C-REC.                   
           WRITE BP13L45C-REC.                                                  
                                                                                
           MOVE 6                          TO   WS-CNT-LINE.                    
                                                                                
       3510-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F452                                                       
                 BP13KC30                                                       
                 BP13K452                                                       
                 BP13L45C.                                                      
                                                                                
           IF WS-KC30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC30 ERROR ' WS-KC30-STATUS                  
              MOVE     WS-KC30-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K452-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC30 ERROR ' WS-K452-STATUS                  
              MOVE     WS-K452-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C45C CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F452 READ      : ' WS-CNT-F452-READ.          
           DISPLAY ' 2. NO OF BP13K452 UPDATED   : ' WS-CNT-K452-UPD.           
           DISPLAY ' 3. NO OF BP13L45C WRITTEN   : ' WS-CNT-L45C-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
