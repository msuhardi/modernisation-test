       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C856.                                                 
       AUTHOR.        MARIA THERESA R. SANTOS.                                  
      *DATE-WRITTEN.  06/07/1995.                                               
      *================================================================*        
      *   SYSTEM OF COMMITMENT (BP13)                                  *        
      *================================================================*        
      *                                                                *        
      *   OBJECTIVE   :  1.  TO PRINT HUDC CASES, IF HE IS A LESSEE    *        
      *                      OF A SOLD FLAT OR EX-LESSEE OF A HUDC     *        
      *                      FLAT WHICH EFF-DATE OF RESALE IS LESS     *        
      *                      THAN 30 MONTHS                            *        
      *                                                                *        
      *   INPUT FILE  :      BP13F558                                  *        
      *   OUTPUT FILE :      P13F558A                                  *        
      *                                                                *        
      *   LISTINGS    :      BP13L856                                  *        
      *                                                                *        
      *                                                                *        
      * CHGE-NO   DATE   BY   DESCRIPTION                              *        
      * -------  ------  ---  -------------------------------------    *        
      * BP130117 080796 LSB - IF NO CASES, ALSO PRINT REPORT HEADING   *        
      * BP130145 130796 LSB - BYPASS DTE-REGN = SPACES                 *        
      * BP130233 130796 CVG - IF NOT < '7911' AND NOT > '7999'         *        
      *                       OUTPUT P13F558A FROM BP13F558            *        
      *                     - IF WS-MONTH-EFF < 29                     *        
      *                       OUTPUT P13F558A FROM BP13F558            *        
      * BP130445 050298 SSS - EXPAND F558 RECL FROM 150 TO 400         *        
      * BP130418 070998 SIA - TO CATER FOR Y2K                         *        
      * BP130897 040800 AKT - EXPAND F558 RECL FROM 400 TO 600         *        
      *================================================================*        
       ENVIRONMENT DIVISION.                                                    
      *-------------------------------------------------------------            
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558  ASSIGN        TO BP13F558.                          
           SELECT P13F558A  ASSIGN        TO P13F558A.                          
           SELECT BP13L856  ASSIGN        TO BP13L856.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F558                                                            
            LABEL RECORDS STANDARD                                              
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 600 CHARACTERS.                                     
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   P13F558A                                                            
            LABEL RECORDS STANDARD                                              
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 600 CHARACTERS.                                     
                                                                                
       01   P13F558A-REC               PIC X(600).                              
                                                                                
       FD   BP13L856                                                            
            RECORD CONTAINS 132 CHARACTERS.                                     
                                                                                
       01   PRINT-REC                  PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       COPY  TABQTR.                                                            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-F558                      PIC X(1)  VALUE 'N'.            
           05  WS-VALID-SW                      PIC X(1)  VALUE 'Y'.            
           05  WS-FOUND-SW                      PIC X(1)  VALUE 'Y'.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-PAGE-CNT                      PIC 9(4)  VALUE ZEROES.         
           05  WS-REC-CNT                       PIC 9(8)  VALUE ZEROES.         
           05  WS-READ-F558-CNT                 PIC 9(8)  VALUE ZEROES.         
           05  WS-WRITE-F558A-CNT               PIC 9(8)  VALUE ZEROES.         
           05  WS-WRITE-F558-CNT                PIC 9(8)  VALUE ZEROES.         
           05  WS-BYPASSED-F558-CNT             PIC 9(8)  VALUE ZEROES.         
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CCYY                     PIC 9(4)  VALUE 00.             
           05  WS-CURR-MM                       PIC 99.                         
           05  WS-CHAR-MM  REDEFINES WS-CURR-MM PIC XX.                         
           05  WS-CURR-DD                       PIC 99    VALUE 00.             
                                                                                
       01  WS-RESALE-DATE.                                                      
           05  WS-RESALE-CCYY                   PIC 9(4).                       
           05  WS-RESALE-MM                     PIC 99.                         
           05  WS-RESALE-DD                     PIC 99.                         
                                                                                
       01  WS-ISSUE-DATE.                                                       
           05  WS-ISSUE-CCYY                    PIC 9(4).                       
           05  WS-ISSUE-MM                      PIC 99.                         
           05  WS-ISSUE-DD                      PIC 99.                         
                                                                                
       01  WS-BALLOT-DATE.                                                      
           05  WS-BALLOT-CCYY                   PIC 9(4).                       
           05  WS-BALLOT-MM                     PIC 99.                         
                                                                                
       01  WS-MISC.                                                             
           05  WS-SERNO                         PIC 9(4)  VALUE 0.              
           05  WS-MONTH-EFF                     PIC 9(4)  VALUE 0.              
                                                                                
      *----------------------------------------------------------------*        
      *   COMMON HEADER USE FOR BOTH REPORTS                           *        
      *----------------------------------------------------------------*        
       01  L856-HDG1.                                                           
           05  PRT-REPORT-TITLE         PIC X(9)  VALUE 'BP13L856'.             
           05  FILLER                   PIC X(37) VALUE SPACES.                 
           05  FILLER                   PIC X(40) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(31) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE :'.               
           05  PRT-DATE                 PIC X(10).                              
                                                                                
       01  L856-HDG2.                                                           
           05  FILLER                   PIC X(50) VALUE SPACES.                 
           05  FILLER                   PIC X(32) VALUE                         
               'LIST OF EXISTING HUDC LESSEE AND'.                              
           05  FILLER                   PIC X(35) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE : '.              
           05  PRT-PAGE                 PIC ZZZ9.                               
                                                                                
       01  L856-HDG2A.                                                          
           05  FILLER                   PIC X(40) VALUE SPACES.                 
           05  FILLER                   PIC X(53) VALUE                         
               'EX-LESSEE WHOSE EFFECTIVE OF RESALE LESS THAN 30 MTHS'.         
                                                                                
       01  L856-HDG3.                                                           
           05  FILLER                   PIC X(20) VALUE SPACES.                 
           05  FILLER                   PIC X(10) VALUE                         
               'S/NO  '.                                                        
           05  FILLER                   PIC X(43) VALUE                         
               'REGNO/REFNO              NRIC NO           '.                   
           05  FILLER                   PIC X(40) VALUE                         
               '   HDB-REF              DTE RESALE EFF  '.                      
                                                                                
      *----------------------------------------------------------------*        
      *   DETAIL RECORD                                                *        
      *----------------------------------------------------------------*        
       01  L856-DTL.                                                            
           05  FILLER                   PIC X(20) VALUE SPACES.                 
           05  PRT-SERNO                PIC ZZZ9.                               
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  PRT-REGNREF              PIC X(14).                              
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  PRT-NRIC                 PIC X(09).                              
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  PRT-HDB-REF              PIC X(14).                              
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PRT-DATE-RESALE          PIC X(10).                              
                                                                                
      *----------------------------------------------------------------*        
      *    05  FILLER                   PIC X(10) VALUE                         
      *        'S/NO  '.                                                        
      *        '9999  '.                                                        
      *    05  FILLER                   PIC X(43) VALUE                         
      *        'REGNO/REFNO              NRIC NO           '.                   
      *        'XXXX-XXXX-X-XX          XXXXXXXXX          '.                   
      *    05  FILLER                   PIC X(40) VALUE                         
      *        '   HDB-REF              DTE RESALE EFF  '.                      
      *        'XXXX-XXXX-X-XX            XX/XX/XXXX    '.                      
      *----------------------------------------------------------------*        
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
                                                                                
           PERFORM   100-OPEN-ROUTINE      THRU    100-EXIT.                    
                                                                                
           PERFORM   200-READ-F558         THRU    200-EXIT                     
                                                                                
           PERFORM   300-PROCESS-F558      THRU    300-EXIT                     
                         UNTIL WS-EOF-F558 = 'Y'.                               
                                                                                
           IF WS-SERNO = ZEROS                                                  
              PERFORM 500-PRINT-HEADER  THRU  500-EXIT.                         
                                                                                
           PERFORM   999-CLOSE-ROUTINE     THRU    999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT BP13F558                                                  
               OUTPUT P13F558A                                                  
                      BP13L856.                                                 
                                                                                
           MOVE ZEROES TO  WS-COUNTERS.                                         
           MOVE 50     TO  WS-REC-CNT                                           
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           STRING  WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                   
                DELIMITED BY SIZE INTO PRT-DATE.                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F558.                                                           
      *-------------------------------------------------------------            
           READ BP13F558                                                        
                AT END MOVE 'Y'  TO  WS-EOF-F558                                
                       GO TO 200-EXIT.                                          
                                                                                
           ADD  1  TO  WS-READ-F558-CNT.                                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F558.                                                        
      *-------------------------------------------------------------            
           IF  F558-NUM-HDB-REF-SMS (1:4)  NOT < '7911' AND                     
               F558-NUM-HDB-REF-SMS (1:4)  NOT > '7999'                         
               PERFORM 350-WRITE-P13F558A THRU  350-EXIT                        
               PERFORM 400-PRINT-BP13L856 THRU  400-EXIT                        
           ELSE                                                                 
               IF F558-NUM-HDB-REF-RSL (1:4)  NOT < '7911' AND                  
                  F558-NUM-HDB-REF-RSL (1:4)  NOT > '7999'                      
                  PERFORM 600-CHECK-DATE     THRU  600-EXIT                     
               ELSE                                                             
                  ADD 1 TO WS-BYPASSED-F558-CNT                                 
               END-IF                                                           
           END-IF                                                               
           PERFORM 200-READ-F558    THRU  200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       350-WRITE-P13F558A.                                                      
      *-------------------------------------------------------------            
           MOVE  SPACES        TO  P13F558A-REC.                                
           INITIALIZE              P13F558A-REC.                                
           MOVE  BP13F558-REC  TO  P13F558A-REC.                                
           WRITE  P13F558A-REC.                                                 
           ADD  1              TO  WS-WRITE-F558A-CNT.                          
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-PRINT-BP13L856.                                                      
      *-------------------------------------------------------------            
           IF WS-REC-CNT > 44                                                   
              PERFORM 500-PRINT-HEADER  THRU  500-EXIT.                         
                                                                                
           MOVE   SPACES                TO     PRINT-REC.                       
                                                                                
           ADD    1                     TO     WS-SERNO                         
           MOVE   WS-SERNO              TO     PRT-SERNO.                       
           MOVE   F558-NUM-REGN         TO     PRT-REGNREF.                     
           MOVE   F558-NUM-NRIC         TO     PRT-NRIC.                        
           STRING  F558-NUM-HDB-REF-RSL (1:4) '-'                               
                   F558-NUM-HDB-REF-RSL (5:4) '-'                               
                   F558-NUM-HDB-REF-RSL (9:1) '-'                               
                   F558-NUM-HDB-REF-RSL (10:2) '-' DELIMITED BY SIZE            
                   INTO PRT-HDB-REF.                                            
           IF F558-DTE-RESALE-EFF NOT = SPACES                                  
              STRING F558-DTE-RESALE-EFF (7:2) '/'                              
                     F558-DTE-RESALE-EFF (5:2) '/'                              
                     F558-DTE-RESALE-EFF (1:4) DELIMITED BY SIZE                
                     INTO     PRT-DATE-RESALE                                   
           ELSE                                                                 
              MOVE   SPACES                    TO  PRT-DATE-RESALE.             
           WRITE  PRINT-REC             FROM   L856-DTL AFTER 1.                
           ADD    1                     TO     WS-REC-CNT                       
                                               WS-WRITE-F558-CNT.               
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-PRINT-HEADER.                                                        
      *-------------------------------------------------------------            
           ADD    1                   TO     WS-PAGE-CNT.                       
           MOVE   WS-PAGE-CNT         TO     PRT-PAGE.                          
           WRITE  PRINT-REC   FROM L856-HDG1   AFTER PAGE.                      
           WRITE  PRINT-REC   FROM L856-HDG2   AFTER 1.                         
           WRITE  PRINT-REC   FROM L856-HDG2A  AFTER 1.                         
           WRITE  PRINT-REC   FROM L856-HDG3   AFTER 2.                         
           MOVE   SPACES      TO   PRINT-REC.                                   
           WRITE  PRINT-REC.                                                    
           MOVE   ZEROES      TO   WS-REC-CNT.                                  
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-CHECK-DATE.                                                          
      *-------------------------------------------------------------            
           IF F558-DTE-RESALE-EFF NOT NUMERIC OR                                
              F558-DTE-RESALE-EFF = '00000000' OR                               
              F558-DTE-ISSUE-REGN NOT NUMERIC  OR                               
              F558-DTE-ISSUE-REGN = '00000000'                                  
              ADD 1 TO WS-BYPASSED-F558-CNT                                     
              GO TO 600-EXIT.                                                   
                                                                                
           DISPLAY '* '  F558-DTE-RESALE-EFF                                    
                         ' ' F558-DTE-ISSUE-REGN.                               
           IF F558-NUM-REGN (7:2) = SPACES                                      
                                                                                
              PERFORM 700-SEARCH-TABQTR   THRU 700-EXIT                         
              MOVE WS-CURR-CCYY    TO  WS-BALLOT-CCYY                           
              MOVE F558-DTE-RESALE-EFF    TO  WS-RESALE-DATE                    
                                                                                
              IF WS-BALLOT-CCYY NOT < WS-RESALE-CCYY                            
                 COMPUTE WS-MONTH-EFF =                                         
                   ((WS-BALLOT-CCYY - WS-RESALE-CCYY) * 12) +                   
                    (WS-BALLOT-MM - WS-RESALE-MM)                               
              ELSE                                                              
                 MOVE 0 TO WS-MONTH-EFF                                         
              END-IF                                                            
                                                                                
           ELSE                                                                 
                                                                                
              MOVE F558-DTE-RESALE-EFF    TO  WS-RESALE-DATE                    
              MOVE F558-DTE-ISSUE-REGN    TO  WS-ISSUE-DATE                     
              COMPUTE WS-MONTH-EFF =                                            
                   ((WS-ISSUE-CCYY - WS-RESALE-CCYY) * 12) +                    
                    (WS-ISSUE-MM - WS-RESALE-MM)                                
           END-IF                                                               
                                                                                
           IF WS-MONTH-EFF < 29                                                 
              PERFORM 350-WRITE-P13F558A    THRU   350-EXIT                     
              PERFORM 400-PRINT-BP13L856    THRU   400-EXIT                     
           ELSE                                                                 
              ADD 1 TO WS-BYPASSED-F558-CNT.                                    
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       700-SEARCH-TABQTR.                                                       
      *-------------------------------------------------------------            
           SET WS-MTH  TO 1                                                     
           SEARCH WS-BALLOT-MTH                                                 
               WHEN WS-BALLOT-MONTH (WS-MTH) = WS-CHAR-MM                       
                  MOVE WS-BALLOT-QTR (WS-MTH) TO  WS-BALLOT-MM.                 
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           DISPLAY  SPACES.                                                     
           DISPLAY  '****   BP13C856   ****'.                                   
           DISPLAY  '  PROGRAM STATISTICS'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY '1) NO. UPGOCCP RECS -BP13F558 = ' WS-READ-F558-CNT.         
           DISPLAY '2) NO. WRITE  RECS  -P13F558A = '                           
                                                     WS-WRITE-F558A-CNT.        
           DISPLAY '3) NO. OUTPUT RECS  -BP13L856 = '                           
                                                     WS-WRITE-F558-CNT.         
           DISPLAY '4) NO. BYPASSED RECS          = '                           
                                                  WS-BYPASSED-F558-CNT.         
                                                                                
           CLOSE BP13F558                                                       
                 P13F558A                                                       
                 BP13L856.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
