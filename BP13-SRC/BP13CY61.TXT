       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY61.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  24/07/2009.                                               
                                                                                
      **********************************************************                
      *                                                        *                
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                    *                
      *                                                        *                
      *  SYSTEM ID   : BP13                                    *                
      *                                                        *                
      *  OBJECTIVE   : OUTPUT REPORT  WITH TITLE               *                
      *                                                        *                
      *  CHG-NO  BY   DATE   DESCRIPTION                       *                
      * -------- ---- ------ -------------------               *                
      * BP133653 JB8  240709 NEW PROGRAM                       *                
      * BP133954 PCL3 060810 TO ADD CDE-BALLOT-HOUSEHOLD AND   *                
      *                      BOOK-STATUS.                      *                
      **********************************************************                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F555 ASSIGN TO BP13F555.                                  
                                                                                
           SELECT BP13K595  ASSIGN        TO BP13K595                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K595-KEY-FLD                       
                            FILE STATUS   IS WS-K595-STATUS.                    
                                                                                
           SELECT BP13L555 ASSIGN TO BP13L555.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K595.                                                           
                                                                                
       FD   BP13F555                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F555.                                                           
                                                                                
       FD   BP13L555                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   BP13L555-REC                  PIC X(700).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-F555                PIC X(01)   VALUE SPACE.              
               88  F555-EOF               VALUE 'Y'.                            
           05  WS-K595-EOF                PIC X(01)   VALUE 'N'.                
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-K595-STATUS              PIC 9(02)   VALUE ZEROES.             
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-NUM-REGN                 PIC X(08)   VALUE SPACES.             
           05 WS-BAL-HH                   PIC X(02)   VALUE SPACES.             
           05 WS-BK-STAT                  PIC X(02)   VALUE SPACES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F555-READ              PIC 9(07)   VALUE ZEROES.             
           05  CNT-L555-WRITE             PIC 9(07)   VALUE ZEROES.             
           05  WS-SNO                     PIC 9(07)   VALUE ZEROES.             
           05  WS-CTR                     PIC 9(02)   VALUE ZEROES.             
                                                                                
       01  WS-REPORT-TITLE.                                                     
           05  P13F555-TITLE1.                                                  
               10  FILLER                 PIC X(06)   VALUE SPACES.             
               10  FILLER                 PIC X(50)   VALUE                     
                   '                                         {--------'.        
               10  FILLER                 PIC X(56)   VALUE                     
             '--------------------------------------------------------'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '-------------------- LATEST APPLICANT ------------'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '--------------------------------------------------'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '----------------------} {-------------------------'.        
               10  FILLER                 PIC X(56)   VALUE                     
             '------------------OTHER APPLICANT 1---------------------'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '------------------} {-----------------------------'.        
               10  FILLER                 PIC X(56)   VALUE                     
             '--------------OTHER APPLICANT 2-------------------------'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '--------------} {---------------------------------'.        
               10  FILLER                 PIC X(56)   VALUE                     
             '----------OTHER APPLICANT 3-----------------------------'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '----------} {---RESALE INFO--} {------------------'.        
               10  FILLER                 PIC X(56)   VALUE                     
                   '------NEW APPLICANT-------------------}          '.         
               10  FILLER                 PIC X(94)   VALUE SPACES.             
           05  P13F555-TITLE2.                                                  
               10  FILLER                 PIC X(06)   VALUE '  SNO '.           
               10  FILLER                 PIC X(50)   VALUE                     
                   'UIN       NON-SELN DATE DEBAR DEBAR EXP  REGN     '.        
               10  FILLER                 PIC X(56)   VALUE                     
             'MOD BALLOT CAT NT  FT HH BH BS QUEUE RSLT DATE APPT  ELG'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   ' ALO ETHNIC     NRIC1     AGE  INCOME ROLE NRIC2  '.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '   AGE  INCOME ROLE NRIC3     AGE  INCOME ROLE NRI'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   'C4     AGE  INCOME ROLE REGN     MOD BALLOT CAT NT'.        
               10  FILLER                 PIC X(56)   VALUE                     
             '  FT HH BH BS QUEUE RSLT DATE APPT  NRIC1     NRIC2     '.        
               10  FILLER                 PIC X(50)   VALUE                     
                   'NRIC3     NRIC4     REGN     MOD BALLOT CAT NT  FT'.        
               10  FILLER                 PIC X(56)   VALUE                     
             ' HH BH BS QUEUE RSLT DATE APPT  NRIC1     NRIC2     NRIC'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '3     NRIC4     REGN     MOD BALLOT CAT NT  FT HH '.        
               10  FILLER                 PIC X(56)   VALUE                     
             'BH BS QUEUE RSLT DATE APPT  NRIC1     NRIC2     NRIC3   '.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '  NRIC4     RCV APPLN  SCH BYR REGN     MOD NT  FT'.        
               10  FILLER                 PIC X(56)   VALUE                     
             ' BH BS BALLOT DATE APPLN SCH ACCT                       '.        
               10  FILLER                 PIC X(94)   VALUE SPACES.             
           05  P13F555-TITLE3.                                                  
               10  FILLER                 PIC X(06)   VALUE '----- '.           
               10  FILLER                 PIC X(50)   VALUE                     
                   '--------- -------- ---------- ---------- -------- '.        
               10  FILLER                 PIC X(56)   VALUE                     
             '--- ------ --- --- -- -- -- -- ----- ---- ---------- ---'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   ' --- ---------- --------- ---- ------ ---- -------'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '-- ---- ------ ---- --------- ---- ------ ---- ---'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '------ ---- ------ ---- -------- --- ------ --- --'.        
               10  FILLER                 PIC X(56)   VALUE                     
             '- -- -- -- -- ----- ---- ---------- --------- --------- '.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '--------- --------- -------- --- ------ --- --- --'.        
               10  FILLER                 PIC X(56)   VALUE                     
             ' -- -- -- ----- ---- ---------- --------- --------- ----'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '----- --------- -------- --- ------ --- --- -- -- '.        
               10  FILLER                 PIC X(56)   VALUE                     
             '-- -- ----- ---- ---------- --------- --------- --------'.        
               10  FILLER                 PIC X(50)   VALUE                     
                   '- --------- ---------- ------- -------- --- --- --'.        
               10  FILLER                 PIC X(56)   VALUE                     
             ' -- -- ------ ---------- --------------                 '.        
               10  FILLER                 PIC X(94)   VALUE SPACES.             
                                                                                
       01  WS-REPORT-FORMAT.                                                    
           05  L555-SNO                    PIC ZZZZ9.                           
           05  FILLER                      PIC X(01).                           
           05  L555-NUM-UIN                PIC X(09).                           
           05  FILLER                      PIC X(01).                           
           05  L555-NUM-NON-SELECTION      PIC X(08).                           
           05  FILLER                      PIC X(01).                           
           05  L555-DTE-DEBAR              PIC X(10).                           
           05  FILLER                      PIC X(01).                           
           05  L555-DTE-DEBAR-EXPIRY       PIC X(10).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-REGN        PIC X(08).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-ALLO-CAT    PIC X(03).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-DTE-BALLOT      PIC X(06).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-CAT         PIC X(03).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-NT          PIC X(03).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-FT          PIC X(02).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-HH          PIC X(02).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-BH          PIC X(02).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-ST          PIC X(02).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-QUEUE       PIC X(05).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-RESULT      PIC X(04).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-DTE-APPT        PIC X(10).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-ELIG-SCH    PIC X(03).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-ALLO-SCH    PIC X(03).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-NUM-ETHNIC      PIC X(10).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA1-NRIC        PIC X(09).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA1-AGE         PIC ZZZ9.                            
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA1-INCOME      PIC ZZZZZ9.                          
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA1-SMS-ROLE    PIC X(04).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA2-NRIC        PIC X(09).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA2-AGE         PIC ZZZ9.                            
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA2-INCOME      PIC ZZZZZ9.                          
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA2-SMS-ROLE    PIC X(04).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA3-NRIC        PIC X(09).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA3-AGE         PIC ZZZ9.                            
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA3-INCOME      PIC ZZZZZ9.                          
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA3-SMS-ROLE    PIC X(04).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA4-NRIC        PIC X(09).                           
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA4-AGE         PIC ZZZ9.                            
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA4-INCOME      PIC ZZZZZ9.                          
           05  FILLER                      PIC X(01).                           
           05  L555-LATEST-HA4-SMS-ROLE    PIC X(04).                           
           05  FILLER                      PIC X(01).                           
           05  L555-TABLE OCCURS 3 TIMES.                                       
               10  L555-NUM-REGN           PIC X(08).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-ALLO-CAT       PIC X(03).                           
               10  FILLER                  PIC X(01).                           
               10  L555-DTE-BALLOT         PIC X(06).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-CAT            PIC X(03).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-NT             PIC X(03).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-FT             PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-HH             PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-BH             PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-ST             PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-QUEUE          PIC X(05).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-RESULT         PIC X(04).                           
               10  FILLER                  PIC X(01).                           
               10  L555-DTE-APPT           PIC X(10).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-NRIC1          PIC X(09).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-NRIC2          PIC X(09).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-NRIC3          PIC X(09).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NUM-NRIC4          PIC X(09).                           
               10  FILLER                  PIC X(01).                           
           05  L555-RSL-DTE-RECEIVE-APPLN  PIC X(10).                           
           05  FILLER                      PIC X(01).                           
           05  L555-RSL-CDE-SCH-ALLOC-BYR  PIC X(07).                           
           05  FILLER                      PIC X(01).                           
           05  L555-NEW-APPLN.                                                  
               10  L555-NA-NUM-REGN        PIC X(08).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NA-NUM-MODE        PIC X(03).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NA-NUM-NT          PIC X(03).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NA-NUM-FT          PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NA-NUM-BH          PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NA-NUM-ST          PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NA-DTE-BALLOT      PIC X(06).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NA-DTE-APPLN       PIC X(10).                           
               10  FILLER                  PIC X(01).                           
               10  L555-NA-SCHACC          PIC X(14).                           
           05  FILLER                      PIC X(111).                          
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
      *        MAIN LINE PROCESS                                       *        
      ******************************************************************        
       MAIN-PROCESS.                                                            
           PERFORM 1000-OPEN-FILES              THRU 1000-EXIT.                 
           PERFORM 2000-READ-BP13F555           THRU 2000-EXIT.                 
           PERFORM 3000-PROCESS-RECORDS         THRU 3000-EXIT                  
             UNTIL F555-EOF.                                                    
           PERFORM 9000-CLOSE-FILES             THRU 9000-EXIT.                 
                                                                                
      *****************                                                         
       1000-OPEN-FILES.                                                         
      *****************                                                         
                                                                                
           OPEN INPUT  BP13F555                                                 
                       BP13K595                                                 
                OUTPUT BP13L555.                                                
                                                                                
           IF WS-K595-STATUS  NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13K595 : ' WS-K595-STATUS              
              MOVE     WS-K595-STATUS       TO RETURN-CODE                      
              PERFORM  9000-CLOSE-FILES   THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE P13F555-TITLE1                  TO BP13L555-REC.                
           WRITE BP13L555-REC.                                                  
                                                                                
           MOVE P13F555-TITLE2                  TO BP13L555-REC.                
           WRITE BP13L555-REC.                                                  
                                                                                
           MOVE P13F555-TITLE3                  TO BP13L555-REC.                
           WRITE BP13L555-REC.                                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       2000-READ-BP13F555.                                                      
      *********************                                                     
                                                                                
           READ BP13F555                                                        
             AT END                                                             
                MOVE 'Y'         TO WS-EOF-F555                                 
                GO               TO 2000-EXIT                                   
            NOT AT END                                                          
                ADD 1            TO CNT-F555-READ                               
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       3000-PROCESS-RECORDS.                                                    
      **********************                                                    
                                                                                
           MOVE SPACES                          TO WS-REPORT-FORMAT.            
           INITIALIZE                              WS-REPORT-FORMAT.            
                                                                                
           ADD 1                                TO WS-SNO.                      
           MOVE WS-SNO                          TO L555-SNO.                    
           PERFORM 3500-MOVE-VALUES           THRU 3500-EXIT.                   
           MOVE WS-REPORT-FORMAT                TO BP13L555-REC.                
           WRITE BP13L555-REC.                                                  
                                                                                
           PERFORM 2000-READ-BP13F555           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       3500-MOVE-VALUES.                                                        
      ******************                                                        
                                                                                
           MOVE F555-NUM-UIN                TO L555-NUM-UIN                     
           MOVE F555-NUM-NON-SELECTION      TO L555-NUM-NON-SELECTION           
           IF F555-DTE-DEBAR > ZEROES                                           
              STRING F555-DTE-DEBAR(7:2) '/'                                    
                     F555-DTE-DEBAR(5:2) '/'                                    
                     F555-DTE-DEBAR(1:4)                                        
                     DELIMITED BY SIZE INTO L555-DTE-DEBAR                      
              END-STRING                                                        
           END-IF                                                               
           IF F555-DTE-DEBAR-EXPIRY > ZEROES                                    
              STRING F555-DTE-DEBAR-EXPIRY(7:2) '/'                             
                     F555-DTE-DEBAR-EXPIRY(5:2) '/'                             
                     F555-DTE-DEBAR-EXPIRY(1:4)                                 
                     DELIMITED BY SIZE INTO L555-DTE-DEBAR-EXPIRY               
              END-STRING                                                        
           END-IF                                                               
           MOVE F555-LATEST-NUM-REGN        TO L555-LATEST-NUM-REGN             
                                               WS-NUM-REGN                      
                                                                                
           IF WS-NUM-REGN NOT = SPACES AND LOW-VALUES AND ZEROES                
              PERFORM 4000-READ-BP13K595  THRU 4000-EXIT                        
           ELSE                                                                 
              MOVE SPACES                   TO WS-BAL-HH                        
                                               WS-BK-STAT                       
           END-IF.                                                              
                                                                                
           MOVE F555-LATEST-NUM-ALLO-CAT    TO L555-LATEST-NUM-ALLO-CAT         
           MOVE F555-LATEST-DTE-BALLOT      TO L555-LATEST-DTE-BALLOT           
           MOVE F555-LATEST-NUM-CAT         TO L555-LATEST-NUM-CAT              
           MOVE F555-LATEST-NUM-NT          TO L555-LATEST-NUM-NT               
           MOVE F555-LATEST-NUM-FT          TO L555-LATEST-NUM-FT               
           MOVE F555-LATEST-NUM-HH          TO L555-LATEST-NUM-HH               
           MOVE WS-BAL-HH                   TO L555-LATEST-NUM-BH               
           MOVE WS-BK-STAT                  TO L555-LATEST-NUM-ST               
           MOVE F555-LATEST-NUM-QUEUE       TO L555-LATEST-NUM-QUEUE            
           MOVE F555-LATEST-NUM-RESULT      TO L555-LATEST-NUM-RESULT           
           IF F555-LATEST-DTE-APPT > ZEROES                                     
              STRING F555-LATEST-DTE-APPT(7:2) '/'                              
                     F555-LATEST-DTE-APPT(5:2) '/'                              
                     F555-LATEST-DTE-APPT(1:4)                                  
                     DELIMITED BY SIZE INTO L555-LATEST-DTE-APPT                
              END-STRING                                                        
           END-IF                                                               
           MOVE F555-LATEST-NUM-ELIG-SCH    TO L555-LATEST-NUM-ELIG-SCH         
           MOVE F555-LATEST-NUM-ALLO-SCH    TO L555-LATEST-NUM-ALLO-SCH         
           MOVE F555-LATEST-NUM-ETHNIC      TO L555-LATEST-NUM-ETHNIC           
           MOVE F555-LATEST-HA1-NRIC        TO L555-LATEST-HA1-NRIC             
           MOVE F555-LATEST-HA1-AGE         TO L555-LATEST-HA1-AGE              
           MOVE F555-LATEST-HA1-INCOME      TO L555-LATEST-HA1-INCOME           
           MOVE F555-LATEST-HA1-SMS-ROLE    TO L555-LATEST-HA1-SMS-ROLE         
           MOVE F555-LATEST-HA2-NRIC        TO L555-LATEST-HA2-NRIC             
           MOVE F555-LATEST-HA2-AGE         TO L555-LATEST-HA2-AGE              
           MOVE F555-LATEST-HA2-INCOME      TO L555-LATEST-HA2-INCOME           
           MOVE F555-LATEST-HA2-SMS-ROLE    TO L555-LATEST-HA2-SMS-ROLE         
           MOVE F555-LATEST-HA3-NRIC        TO L555-LATEST-HA3-NRIC             
           MOVE F555-LATEST-HA3-AGE         TO L555-LATEST-HA3-AGE              
           MOVE F555-LATEST-HA3-INCOME      TO L555-LATEST-HA3-INCOME           
           MOVE F555-LATEST-HA3-SMS-ROLE    TO L555-LATEST-HA3-SMS-ROLE         
           MOVE F555-LATEST-HA4-NRIC        TO L555-LATEST-HA4-NRIC             
           MOVE F555-LATEST-HA4-AGE         TO L555-LATEST-HA4-AGE              
           MOVE F555-LATEST-HA4-INCOME      TO L555-LATEST-HA4-INCOME           
           MOVE F555-LATEST-HA4-SMS-ROLE    TO L555-LATEST-HA4-SMS-ROLE         
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 3                  
              MOVE F555-NUM-REGN(WS-CTR)    TO L555-NUM-REGN(WS-CTR)            
                                               WS-NUM-REGN                      
                                                                                
              IF WS-NUM-REGN NOT = SPACES AND LOW-VALUES AND ZEROES             
                 PERFORM 4000-READ-BP13K595 THRU 4000-EXIT                      
              ELSE                                                              
                 MOVE SPACES                  TO WS-BAL-HH                      
                                                 WS-BK-STAT                     
              END-IF                                                            
                                                                                
              MOVE F555-NUM-ALLO-CAT(WS-CTR)                                    
                TO L555-NUM-ALLO-CAT(WS-CTR)                                    
              MOVE F555-DTE-BALLOT(WS-CTR)  TO L555-DTE-BALLOT(WS-CTR)          
              MOVE F555-NUM-CAT(WS-CTR)     TO L555-NUM-CAT(WS-CTR)             
              MOVE F555-NUM-NT(WS-CTR)      TO L555-NUM-NT(WS-CTR)              
              MOVE F555-NUM-FT(WS-CTR)      TO L555-NUM-FT(WS-CTR)              
              MOVE F555-NUM-HH(WS-CTR)      TO L555-NUM-HH(WS-CTR)              
              MOVE WS-BAL-HH                TO L555-NUM-BH(WS-CTR)              
              MOVE WS-BK-STAT               TO L555-NUM-ST(WS-CTR)              
              MOVE F555-NUM-QUEUE(WS-CTR)   TO L555-NUM-QUEUE(WS-CTR)           
              MOVE F555-NUM-RESULT(WS-CTR)  TO L555-NUM-RESULT(WS-CTR)          
              IF F555-DTE-APPT(WS-CTR) > ZEROES                                 
                 STRING F555-DTE-APPT(WS-CTR)(7:2) '/'                          
                        F555-DTE-APPT(WS-CTR)(5:2) '/'                          
                        F555-DTE-APPT(WS-CTR)(1:4)                              
                        DELIMITED BY SIZE INTO L555-DTE-APPT(WS-CTR)            
                 END-STRING                                                     
              END-IF                                                            
              MOVE F555-NUM-NRIC1(WS-CTR)   TO L555-NUM-NRIC1(WS-CTR)           
              MOVE F555-NUM-NRIC2(WS-CTR)   TO L555-NUM-NRIC2(WS-CTR)           
              MOVE F555-NUM-NRIC3(WS-CTR)   TO L555-NUM-NRIC3(WS-CTR)           
              MOVE F555-NUM-NRIC4(WS-CTR)   TO L555-NUM-NRIC4(WS-CTR)           
           END-PERFORM.                                                         
                                                                                
           IF F555-RSL-DTE-RECEIVE-APPLN > ZEROES                               
              STRING F555-RSL-DTE-RECEIVE-APPLN(7:2) '/'                        
                     F555-RSL-DTE-RECEIVE-APPLN(5:2) '/'                        
                     F555-RSL-DTE-RECEIVE-APPLN(1:4)                            
                     DELIMITED BY SIZE INTO L555-RSL-DTE-RECEIVE-APPLN          
              END-STRING                                                        
           END-IF.                                                              
           MOVE F555-RSL-CDE-SCH-ALLOC-BYR                                      
             TO L555-RSL-CDE-SCH-ALLOC-BYR                                      
                                                                                
           MOVE F555-NA-NUM-REGN            TO L555-NA-NUM-REGN                 
                                               WS-NUM-REGN.                     
                                                                                
           IF WS-NUM-REGN NOT = SPACES AND LOW-VALUES AND ZEROES                
              PERFORM 4000-READ-BP13K595  THRU 4000-EXIT                        
           ELSE                                                                 
              MOVE SPACES                   TO WS-BAL-HH                        
                                               WS-BK-STAT                       
           END-IF.                                                              
                                                                                
           MOVE F555-NA-NUM-MODE            TO L555-NA-NUM-MODE.                
           MOVE F555-NA-NUM-NT              TO L555-NA-NUM-NT.                  
           MOVE F555-NA-NUM-FT              TO L555-NA-NUM-FT.                  
           MOVE WS-BAL-HH                   TO L555-NA-NUM-BH.                  
           MOVE WS-BK-STAT                  TO L555-NA-NUM-ST.                  
           MOVE F555-NA-DTE-BALLOT          TO L555-NA-DTE-BALLOT.              
           IF F555-NA-DTE-APPLN > ZEROES                                        
              STRING F555-NA-DTE-APPLN(7:2) '/'                                 
                     F555-NA-DTE-APPLN(5:2) '/'                                 
                     F555-NA-DTE-APPLN(1:4)                                     
                     DELIMITED BY SIZE INTO L555-NA-DTE-APPLN                   
              END-STRING                                                        
           END-IF.                                                              
           IF F555-NA-SCHACC NOT = SPACES AND LOW-VALUES                        
              STRING F555-NA-SCHACC(1:4) '-'                                    
                     F555-NA-SCHACC(5:4) '-'                                    
                     F555-NA-SCHACC(9:1) '-'                                    
                     F555-NA-SCHACC(10:2)                                       
                     DELIMITED BY SIZE INTO L555-NA-SCHACC                      
              END-STRING                                                        
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13K595.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                         TO BP13K595-REC.                 
           INITIALIZE                             BP13K595-REC.                 
                                                                                
           MOVE SPACES                         TO  K595-KEY-FLD                 
                                                   WS-BAL-HH                    
                                                   WS-BK-STAT.                  
                                                                                
           MOVE WS-NUM-REGN                    TO  K595-NUM-REGN-REF.           
           MOVE 'N'                            TO  WS-K595-EOF.                 
                                                                                
           START BP13K595 KEY >= K595-KEY-FLD.                                  
           EVALUATE WS-K595-STATUS                                              
           WHEN 00                                                              
                PERFORM 4100-READNEXT-BP13K595 THRU 4100-EXIT                   
                  UNTIL K595-NUM-REGN-REF NOT = WS-NUM-REGN                     
                     OR WS-K595-EOF = 'Y'                                       
           WHEN 10                                                              
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR START BP13K595, STATUS  '                        
                        WS-K595-STATUS                                          
                        ',  REGN ' K595-NUM-REGN-REF                            
                MOVE WS-K595-STATUS TO RETURN-CODE                              
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-READNEXT-BP13K595.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13K595 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-K595-EOF.                                 
                                                                                
           EVALUATE WS-K595-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                IF K595-NUM-REGN-REF = WS-NUM-REGN                              
                   IF K595-CDE-BALLOT-HOUSEHOLD NOT = SPACES AND                
                                                      LOW-VALUES                
                      MOVE K595-CDE-BALLOT-HOUSEHOLD TO WS-BAL-HH               
                   END-IF                                                       
                                                                                
                   IF K595-NUM-BOOK-STATUS NOT = SPACES AND LOW-VALUES          
                      MOVE K595-NUM-BOOK-STATUS      TO WS-BK-STAT              
                   END-IF                                                       
                                                                                
                ELSE                                                            
                   MOVE 'Y'                          TO WS-K595-EOF             
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'                             TO WS-K595-EOF             
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K595, STATUS '                       
                        WS-K595-STATUS ',  REGN ' K595-NUM-REGN-REF             
                MOVE WS-K595-STATUS                  TO RETURN-CODE             
                PERFORM 9000-CLOSE-FILES           THRU 9000-EXIT               
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       9000-CLOSE-FILES.                                                        
      ******************                                                        
                                                                                
           DISPLAY '******  BP13CY61 *************'.                            
           DISPLAY 'NO OF F555 REC READ          : ' CNT-F555-READ.             
           DISPLAY 'NO OF BOOKED CASES WRITTEN   : ' WS-SNO.                    
                                                                                
           CLOSE BP13F555                                                       
                 BP13K595                                                       
                 BP13L555.                                                      
                                                                                
           IF WS-K595-STATUS  NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K595 : ' WS-K595-STATUS              
              MOVE     WS-K595-STATUS       TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
