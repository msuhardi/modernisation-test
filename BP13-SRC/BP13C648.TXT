      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C648.                                                 
      *AUTHOR.        SGK.                                                      
      *DATE-WRITTEN.  MAY 20 1998.                                              
                                                                                
      *****************************************************************         
      * OBJECTIVE  - TO PRINT REPORT ON PRE-ELIGIBILITY EN-BLOC LISTING         
      *                    FOR  TP & SA/TP  CASES INDIVIDUALLY                  
      *****************************************************************         
      * NOTE  : THE REPORT PAGE BREAK IS BY ESTATE/BLK/NEIGHBOURHOOD/           
      *                                     CONTRACT NO.                        
      *              ('C0' TRANSACTIONS FROM K310)                              
      * INPUT FILES:  BP13F310                                                  
      *               BP13K800                                                  
      *               BP13K820                                                  
      *               BM06K110                                                  
      *                                                                         
      * REPORT:       BP13L648 (TP CASES)                                       
      *               P13L648A (SA/TP CASES)                                    
      *                                                                         
      *----------------------------------------------------------------         
      * MODIFICATION HISTORY:                                                   
      * =====================                                                   
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                           
      * -------  ----    ---  -----------------------                           
      *BP130520 20/05/98 SGK  NEW PGM.                                          
      *BP130418 30/09/98 SGK  TO CATER FOR Y2K.                                 
      *BP130520 05/11/98 JMS  TO CHANGE REPORT NAMES.                           
      *BP130520 23/11/98 JMS  TO CHANGE REPORT NAMES FOR EN-BLOCK               
      *BP130915 07/08/00 MCC  TO PRINT ACCEPTANCE DATE                          
      *BP133667 18/08/10 ESD1 CHANGE CA TYPE COLUMN TO LOAN TAG.                
      *****************************************************************         
                                                                                
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
      *===============================================================*         
       CONFIGURATION SECTION.                                                   
      *===============================================================*         
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *===============================================================*         
       INPUT-OUTPUT SECTION.                                                    
      *===============================================================*         
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS K800-STATUS.                        
                                                                                
           SELECT BP13K820 ASSIGN        TO BP13K820                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K820-KEY-FLD                        
                           FILE STATUS   IS K820-STATUS.                        
                                                                                
           SELECT BM06K110 ASSIGN        TO BM06K110                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K110-KEY-FLD                        
                           FILE STATUS   IS K110-STATUS.                        
                                                                                
           SELECT BM06K510 ASSIGN        TO BM06K510                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K510-ESTCDE                         
                           FILE STATUS   IS K510-STATUS.                        
                                                                                
           SELECT BP13F310 ASSIGN        TO BP13F310.                           
           SELECT BP13L648 ASSIGN        TO BP13L648.                           
           SELECT P13L648A ASSIGN        TO P13L648A.                           
                                                                                
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
      *===============================================================*         
       FILE SECTION.                                                            
      *===============================================================*         
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS  2000 CHARACTERS.                                   
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS  400 CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD BM06K110                                                              
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD BM06K510                                                              
            RECORD CONTAINS  60 CHARACTERS.                                     
       COPY BM06K510.                                                           
                                                                                
       FD   BP13F310                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F310.                                                           
                                                                                
       FD   BP13L648                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 133 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS.                                        
       01   L648-PRINT-REC               PIC X(133).                            
                                                                                
       FD   P13L648A                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 133 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS.                                        
       01   L648A-PRINT-REC               PIC X(133).                           
                                                                                
      *===============================================================*         
       WORKING-STORAGE SECTION.                                                 
      *===============================================================*         
       01  WS-FILE-STATUS.                                                      
           05  K800-STATUS               PIC 9(02) VALUE 0.                     
           05  K820-STATUS               PIC 9(02) VALUE 0.                     
           05  K110-STATUS               PIC 9(02) VALUE 0.                     
           05  K510-STATUS               PIC 9(02) VALUE 0.                     
                                                                                
       01  WS-F310-EOF                   PIC X(01) VALUE 'N'.                   
       01  WS-REPORT-TYPE                PIC X.                                 
           88  TP-CASE                   VALUE  'T'.                            
           88  SA-TP-CASE                VALUE  'S'.                            
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-YYYY                    PIC X(04).                             
           05 WS-MM                      PIC X(02).                             
           05 WS-DD                      PIC X(02).                             
       01   WS-CURR-DATE-R  REDEFINES  WS-CURR-DATE  PIC 9(8).                  
                                                                                
       01  WS-DISP-DATE.                                                        
           05 WS-DD                      PIC X(02).                             
           05 FILLER                     PIC X(01) VALUE '/'.                   
           05 WS-MM                      PIC X(02).                             
           05 FILLER                     PIC X(01) VALUE '/'.                   
           05 WS-YYYY                    PIC X(04).                             
                                                                                
       01  WS-INT-DATE                   PIC S9(9).                             
       01  WS-PROCESS-DATE               PIC 9(8).                              
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F310-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-F310-ERROR          PIC 9(07) VALUE 0.                     
           05 WS-CNT-K800-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-K820-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-K110-READ           PIC 9(07) VALUE 0.                     
           05 WS-K820-FND                PIC X     VALUE 'N'.                   
           05 WS-TOTAL-CNT               PIC 9(07) VALUE 0.                     
           05 WS-TP-CASE-CNT             PIC 9(07) VALUE 0.                     
           05 WS-SA-TP-CASE-CNT          PIC 9(07) VALUE 0.                     
           05 WS-TP-TOT-CNT              PIC 9(07) VALUE 0.                     
           05 WS-SA-TP-TOT-CNT           PIC 9(07) VALUE 0.                     
           05 WS-TP-TOT-CNT-EDT          PIC ZZZZZZ9.                           
           05 WS-SA-TP-TOT-CNT-EDT       PIC ZZZZZZ9.                           
           05 WS-PAGE-CNT-TP             PIC 9(07) VALUE 0.                     
           05 WS-PAGE-CNT-SA-TP          PIC 9(07) VALUE 0.                     
           05 WS-SEQ-NO-A                PIC 9(07) VALUE ZERO.                  
           05 WS-SEQ-NO-B                PIC 9(07) VALUE ZERO.                  
           05 WS-TOTAL-REFUND            PIC S9(08)V99 VALUE 0.                 
           05 WS-PREV-BLK                PIC X(05) VALUE SPACES.                
           05 WS-PREV-TP-BLK             PIC X(05) VALUE SPACES.                
           05 WS-PREV-SA-BLK             PIC X(05) VALUE SPACES.                
           05 WS-PREV-ESTATE             PIC X(03) VALUE SPACES.                
           05 WS-PREV-NEIGHBOURHOOD      PIC X(04) VALUE SPACES.                
           05 WS-PREV-CONTRACT-NO        PIC X(04) VALUE SPACES.                
           05 WS-PAGE-BREAK              PIC X     VALUE 'N'.                   
           05 WS-PREV-TITLE              PIC X(39)  VALUE SPACES.               
           05 WS-CASE-CHK1               PIC X(01)  VALUE SPACES.               
           05 WS-CASE-CHK2               PIC X(01)  VALUE SPACES.               
           05 WS-CASE-CHK3               PIC X(01)  VALUE SPACES.               
           05 WS-CASE-CHK4               PIC X(01)  VALUE SPACES.               
                                                                                
      *-----------------------------------------------------------*             
      *    LISTING VARIABLES                                      *             
      *-----------------------------------------------------------*             
       01  L648-PRT-HEAD-01.                                                    
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 L648-REPORT-NAME             PIC X(08) VALUE SPACES.              
           05 FILLER                       PIC X(04) VALUE SPACES.              
           05 FILLER                       PIC X(08) VALUE 'HDB3    '.          
           05 FILLER                       PIC X(15) VALUE SPACES.              
           05 FILLER                       PIC X(39) VALUE                      
             'S Y S T E M   O F   C O M M I T M E N T'.                         
           05 FILLER                       PIC X(23) VALUE SPACES.              
           05 FILLER                       PIC X(07) VALUE 'DATE: '.            
           05 L648-PRINT-DATE              PIC X(10) VALUE SPACES.              
           05 FILLER                       PIC X(03) VALUE SPACES.              
           05 FILLER                       PIC X(07) VALUE 'PAGE: '.            
           05 L648-PRINT-PAGE              PIC ZZZZ9.                           
                                                                                
       01  L648-PRT-HEAD-02.                                                    
           05 FILLER                       PIC X(33) VALUE SPACES.              
           05 L648-REPORT-TITLE            PIC X(39)  VALUE SPACES.             
           05 L648-REPORT-TYPE              PIC X(16) VALUE SPACES.             
                                                                                
       01  L648-PRT-HEAD-03.                                                    
           05 FILLER                       PIC X(30) VALUE SPACES.              
           05 FILLER                       PIC X(46) VALUE                      
             '        SALES UNIT - AGREEMENT TEAM         '.                    
                                                                                
       01  L648-PRT-HEAD-03A.                                                   
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 FILLER                       PIC X(09) VALUE                      
                                                     'ESTATE : '.               
           05 L648-ESTATE-NAME             PIC X(27) VALUE SPACES.              
                                                                                
       01  L648-PRT-HEAD-03B.                                                   
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 L648-BLK                     PIC X(04) VALUE 'BLK '.              
           05 L648-BLK-NO                  PIC X(05) VALUE SPACES.              
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 L648-ST-NAME                 PIC X(20) VALUE SPACES.              
           05 FILLER                       PIC X(1)  VALUE SPACES.              
           05 L648-COUNTRY                 PIC X(02) VALUE 'S('.                
           05 L648-POSTCDE                 PIC X(10) VALUE SPACES.              
                                                                                
       01  L648-PRT-HEAD-03C.                                                   
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 L648-NEIGHBOURHOOD           PIC X(04) VALUE SPACES.              
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 L648-CONTRACT-NO             PIC X(04) VALUE SPACES.              
                                                                                
       01  L648-PRT-HEAD-04.                                                    
           05 FILLER                       PIC X(71)  VALUE SPACES.             
           05 FILLER                       PIC X(10)                            
                    VALUE 'SOC DEBTOR'.                                         
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 FILLER                       PIC X(08)                            
                    VALUE 'FILE    '.                                           
           05 FILLER                       PIC X(28) VALUE SPACES.              
           05 FILLER                       PIC X(10)                            
                    VALUE 'ACCEPTANCE'.                                         
                                                                                
       01  L648-PRT-HEAD-05.                                                    
           05 FILLER                       PIC X(04)                            
                    VALUE 'S.NO'.                                               
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(08)                            
                    VALUE 'REGN NO.'.                                           
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(09)                            
                    VALUE 'NRIC     '.                                          
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(31)                            
                    VALUE 'NAME (TRUNCATED)'.                                   
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(10)                            
                    VALUE 'UNIT NO  '.                                          
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(07)                            
                    VALUE 'CLEARED'.                                            
           05 FILLER                       PIC X(03) VALUE SPACES.              
           05 FILLER                       PIC X(08)                            
                    VALUE 'RCVD DTE'.                                           
           05 FILLER                       PIC X(04) VALUE SPACES.              
           05 FILLER                       PIC X(10) VALUE 'ED FOR TP'.         
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(10) VALUE 'LOAN TAG '.         
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(10)                            
                    VALUE '   DATE   '.                                         
                                                                                
       01  L648-PRT-DETAIL.                                                     
           05 L648-SNO                     PIC ZZZZ.                            
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 L648-REGN-NO                 PIC X(08) VALUE SPACES.              
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 L648-NRIC                    PIC X(09) VALUE SPACES.              
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 L648-NAME                    PIC X(31) VALUE SPACES.              
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 L648-UNIT-NO.                                                     
              07  L648-HASH                PIC X(1)  VALUE '#'.                 
              07  L648-LEVEL-NO            PIC X(02) VALUE SPACES.              
              07  L648-DASH                PIC X     VALUE '-'.                 
              07  L648-MAIN-UNIT           PIC X(5)  VALUE SPACES.              
              07  FILLER                   PIC X(1)  VALUE SPACES.              
           05 FILLER                       PIC X(05) VALUE SPACES.              
           05 L648-SOC-DEBTOR              PIC X(01) VALUE SPACES.              
           05 FILLER                       PIC X(06) VALUE SPACES.              
           05 L648-DTE-FILE-RCVD           PIC X(10) VALUE SPACES.              
           05 FILLER                       PIC X(03) VALUE SPACES.              
           05 L648-ED-FOR-TP               PIC X(10) VALUE SPACES.              
           05 FILLER                       PIC X(03) VALUE SPACES.              
           05 L648-CA-TAG                  PIC X(10) VALUE SPACES.              
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 L648-ACCPT-DATE              PIC X(10) VALUE SPACES.              
                                                                                
      *REPORT VARIABLES FOR INDIVIDUAL CASES.                                   
                                                                                
       01  L648-PRT-HEAD-04-A.                                                  
           05 FILLER                       PIC X(86)  VALUE SPACES.             
           05 FILLER                       PIC X(09)                            
                    VALUE 'SOC DEBTR'.                                          
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 FILLER                       PIC X(10)                            
                    VALUE 'FILE    '.                                           
           05 FILLER                       PIC X(11) VALUE SPACES.              
           05 FILLER                       PIC X(04)                            
                    VALUE 'LOAN'.                                               
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 FILLER                       PIC X(10)                            
                    VALUE 'ACCEPTANCE'.                                         
                                                                                
       01  L648-PRT-HEAD-05-A.                                                  
           05 FILLER                       PIC X(04)                            
                    VALUE 'S.NO'.                                               
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 FILLER                       PIC X(08)                            
                    VALUE 'REGN NO.'.                                           
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 FILLER                       PIC X(09)                            
                    VALUE 'NRIC     '.                                          
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 FILLER                       PIC X(30)                            
                    VALUE 'NAME (TRUNCATED)'.                                   
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 FILLER                       PIC X(30)                            
                    VALUE 'ADDRESS  '.                                          
           05 FILLER                       PIC X(02) VALUE SPACE.               
           05 FILLER                       PIC X(07)                            
                    VALUE 'CLEARED'.                                            
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(10)                            
                    VALUE 'RCVD DTE'.                                           
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 FILLER                       PIC X(10) VALUE 'ED FOR TP'.         
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 FILLER                       PIC X(03) VALUE 'TAG'.               
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 FILLER                       PIC X(10)                            
                    VALUE '   DATE   '.                                         
                                                                                
       01  L648-PRT-DETAIL-A.                                                   
           05 L648-SNO-A                   PIC ZZZZ.                            
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 L648-REGN-NO-A               PIC X(08) VALUE SPACES.              
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 L648-NRIC-A                  PIC X(09) VALUE SPACES.              
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 L648-NAME-A                  PIC X(30) VALUE SPACES.              
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 L648-ADDRESS                 PIC X(30) VALUE SPACES.              
           05 FILLER                       PIC X(05) VALUE SPACES.              
           05 L648-SOC-DEBTOR-A            PIC X(01) VALUE SPACE.               
           05 FILLER                       PIC X(05) VALUE SPACES.              
           05 L648-DTE-FILE-RCVD-A         PIC X(10) VALUE SPACES.              
           05 FILLER                       PIC X(01) VALUE SPACE.               
           05 L648-ED-FOR-TP-A             PIC X(10) VALUE SPACES.              
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 L648-CA-TAG-A                PIC X(01) VALUE SPACE.               
           05 FILLER                       PIC X(02) VALUE SPACE.               
           05 L648-ACCPT-DATE-A            PIC X(10) VALUE SPACES.              
                                                                                
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      *===============================================================*         
       0000-CONTROL.                                                            
      *===============================================================*         
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                UNTIL WS-F310-EOF = 'Y'.                                        
                                                                                
           PERFORM 9999-WRAP-UP.                                                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       1000-START-UP.                                                           
      *===============================================================*         
            OPEN INPUT  BP13F310                                                
                        BP13K800                                                
                        BP13K820                                                
                        BM06K110                                                
                        BM06K510                                                
                OUTPUT  BP13L648                                                
                        P13L648A.                                               
                                                                                
            IF K800-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13K800 ' K800-STATUS                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            IF K820-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13K820 ' K820-STATUS                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            IF K110-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BM06K110 ' K110-STATUS                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            READ BP13F310                                                       
                 AT END  DISPLAY 'NO RECORDS FOUND!'                            
                         MOVE 'Y'          TO WS-F310-EOF                       
                         GO TO 1000-EXIT                                        
            END-READ.                                                           
                                                                                
            MOVE 12                  TO WS-TP-CASE-CNT.                         
            MOVE 12                  TO WS-SA-TP-CASE-CNT.                      
            ADD  1                   TO WS-CNT-F310-READ.                       
            MOVE FUNCTION CURRENT-DATE(1:8)                                     
                                     TO WS-CURR-DATE.                           
            MOVE CORR WS-CURR-DATE   TO WS-DISP-DATE.                           
            MOVE WS-DISP-DATE        TO L648-PRINT-DATE.                        
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       2000-MAIN-ROUTINE.                                                       
      *===============================================================*         
            IF F310-CDE-TRANS  = 'C0'                                           
               PERFORM  3000-READ-K800  THRU  3000-EXIT                         
            ELSE                                                                
               ADD 1                   TO WS-CNT-F310-ERROR                     
            END-IF.                                                             
                                                                                
            READ BP13F310 AT END                                                
                 MOVE 'Y' TO WS-F310-EOF                                        
                 GO TO 2000-EXIT                                                
                 END-READ.                                                      
                                                                                
            ADD  1                          TO WS-CNT-F310-READ.                
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       3000-READ-K800.                                                          
      *===============================================================*         
            MOVE   F310-REGN-NO           TO   K800-NUM-REGN.                   
            READ   BP13K800.                                                    
                                                                                
            EVALUATE  K800-STATUS                                               
              WHEN 00                                                           
                       CONTINUE                                                 
              WHEN 23                                                           
                       DISPLAY  'BP13K800 - RECORD NOT FOUND, KEY : '           
                          K800-NUM-REGN                                         
                       GO TO 3000-EXIT                                          
              WHEN OTHER                                                        
                       DISPLAY  'BP13K800 - FILE READ ERROR, STATUS: '          
                          K800-STATUS                                           
                       PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                 
            END-EVALUATE.                                                       
                                                                                
            ADD      1              TO      WS-CNT-K800-READ.                   
                                                                                
            IF  K800-DTE-RAISE-AO IS NUMERIC  AND                               
                K800-DTE-RAISE-AO > '00000000'                                  
                MOVE    'T'       TO      WS-REPORT-TYPE                        
            ELSE                                                                
                MOVE    'S'       TO      WS-REPORT-TYPE                        
            END-IF.                                                             
                                                                                
            PERFORM  3500-READ-BM06K110  THRU  3500-EXIT.                       
                                                                                
            IF  K110-DTE-KEY-ISSUED IS NUMERIC AND                              
                K110-DTE-KEY-ISSUED > '00000000'                                
               DISPLAY 'KEY IS ISSUED FOR ' F310-REGN-NO ',SKIPING IT'          
                GO TO 3000-EXIT                                                 
            END-IF.                                                             
                                                                                
            IF F310-CDE-PRE-ELIG-LOOSE = 'Y'                                    
              MOVE 'PRE-ELIGIBILITY INDIVIDUAL LISTING (' TO                    
                                        L648-REPORT-TITLE                       
              PERFORM  6000-PRINT-INDIVIDUAL THRU 6000-EXIT                     
            ELSE                                                                
              MOVE 'PRE-ELIGIBILITY EN-BLOC    LISTING (' TO                    
                                        L648-REPORT-TITLE                       
              PERFORM  4000-PRINT-ENBLOC   THRU  4000-EXIT                      
            END-IF.                                                             
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       3500-READ-BM06K110.                                                      
      *===============================================================*         
            MOVE   K800-NUM-SCH-ACC       TO   K110-KEY-FLD.                    
            READ   BM06K110.                                                    
                                                                                
            EVALUATE  K110-STATUS                                               
              WHEN 00                                                           
                       CONTINUE                                                 
              WHEN 23                                                           
                       DISPLAY  'BM06K110 - RECORD NOT FOUND, KEY : '           
                          K800-NUM-SCH-ACC                                      
                       GO TO 3000-EXIT                                          
              WHEN OTHER                                                        
                       DISPLAY  'BM06K110 - FILE READ ERROR, STATUS: '          
                          K110-STATUS                                           
                       PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                 
            END-EVALUATE.                                                       
                                                                                
            ADD      1              TO      WS-CNT-K110-READ.                   
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       3750-GET-ESTATE-NAME.                                                    
      *===============================================================*         
             MOVE  K110-ESTATE    TO   K510-ESTCDE.                             
                                                                                
             READ   BM06K510.                                                   
                                                                                
              EVALUATE  K510-STATUS                                             
                WHEN 00                                                         
                      MOVE K510-ESTATE-NAME  TO  L648-ESTATE-NAME               
                WHEN 23                                                         
                         DISPLAY  'BM06K510 - RECORD NOT FOUND, KEY : '         
                            K510-ESTCDE                                         
                         GO TO 3000-EXIT                                        
                WHEN OTHER                                                      
                         DISPLAY  'BM06K510 - FILE READ ERROR, STATUS: '        
                            K510-STATUS                                         
                         PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT               
              END-EVALUATE.                                                     
                                                                                
              ADD      1              TO      WS-CNT-K110-READ.                 
                                                                                
       3750-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       4000-PRINT-ENBLOC.                                                       
      *===============================================================*         
            MOVE   SPACES          TO   L648-PRINT-REC.                         
            MOVE   SPACES          TO   L648A-PRINT-REC.                        
                                                                                
            IF TP-CASE                                                          
              WRITE  L648-PRINT-REC   AFTER 1                                   
            ELSE                                                                
              WRITE  L648A-PRINT-REC  AFTER 1.                                  
                                                                                
                                                                                
           IF  (K800-NUM-NRIC1 NOT = SPACES  AND  LOW-VALUES) OR                
               (K800-NUM-NRIC2 NOT = SPACES  AND  LOW-VALUES) OR                
               (K800-NUM-NRIC3 NOT = SPACES  AND  LOW-VALUES) OR                
               (K800-NUM-NRIC4 NOT = SPACES  AND  LOW-VALUES)                   
                MOVE  F310-NUM-MAIN        TO   L648-MAIN-UNIT                  
                MOVE  F310-NUM-LEVEL       TO   L648-LEVEL-NO                   
                MOVE  '#'                  TO   L648-HASH                       
                MOVE  '-'                  TO   L648-DASH                       
               IF TP-CASE                                                       
                  ADD       1            TO       WS-SEQ-NO-A                   
                  MOVE     WS-SEQ-NO-A   TO       L648-SNO                      
               ELSE                                                             
                  ADD       1            TO       WS-SEQ-NO-B                   
                  MOVE     WS-SEQ-NO-B   TO       L648-SNO                      
               END-IF                                                           
                                                                                
               MOVE     K800-NUM-REGN    TO       L648-REGN-NO                  
               STRING   K800-DTE-FILE-RECEIVED(7:2) '/'                         
                        K800-DTE-FILE-RECEIVED(5:2) '/'                         
                        K800-DTE-FILE-RECEIVED(1:4)                             
                        DELIMITED BY SIZE INTO L648-DTE-FILE-RCVD               
                                                                                
               IF K800-AMT-SOC-DEBTOR IS NOT NUMERIC                            
                  MOVE ZERO         TO     K800-AMT-SOC-DEBTOR                  
               END-IF                                                           
               IF K800-AMT-SOC-DEBTOR > ZERO                                    
                  MOVE 'N'          TO     L648-SOC-DEBTOR                      
               ELSE                                                             
                  MOVE '-'          TO     L648-SOC-DEBTOR                      
               END-IF                                                           
               MOVE  K800-NUM-LOAN-TAG    TO    L648-CA-TAG                     
               STRING K800-DTE-EXPIRED-TP(7:2) '/'                              
                      K800-DTE-EXPIRED-TP(5:2) '/'                              
                      K800-DTE-EXPIRED-TP(1:4) DELIMITED BY SIZE                
                      INTO   L648-ED-FOR-TP                                     
               STRING K110-DTE-ACCEPTANCE(7:2) '/'                              
                      K110-DTE-ACCEPTANCE(5:2) '/'                              
                      K110-DTE-ACCEPTANCE(1:4) DELIMITED BY SIZE                
                      INTO   L648-ACCPT-DATE                                    
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC1 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC1   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   MOVE 'Y' TO  WS-CASE-CHK1                                    
                   PERFORM  4075-SETUP-REPORT  THRU  4075-EXIT                  
                   MOVE     ZERO               TO    L648-SNO                   
                   MOVE     SPACES             TO    L648-REGN-NO               
                   MOVE     SPACES             TO    L648-SOC-DEBTOR            
                                                     L648-DTE-FILE-RCVD         
                                                     L648-CA-TAG                
                                                     L648-ED-FOR-TP             
                                                     L648-UNIT-NO               
                                                     L648-ACCPT-DATE            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC2 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC2   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   MOVE 'Y' TO  WS-CASE-CHK2                                    
                   PERFORM  4075-SETUP-REPORT  THRU  4075-EXIT                  
                   MOVE     ZERO               TO    L648-SNO                   
                   MOVE     SPACES             TO    L648-REGN-NO               
                   MOVE     SPACES             TO    L648-SOC-DEBTOR            
                                                     L648-DTE-FILE-RCVD         
                                                     L648-CA-TAG                
                                                     L648-ED-FOR-TP             
                                                     L648-UNIT-NO               
                                                     L648-ACCPT-DATE            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC3 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC3   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   MOVE 'Y' TO  WS-CASE-CHK3                                    
                   PERFORM  4075-SETUP-REPORT  THRU  4075-EXIT                  
                   MOVE     ZERO               TO    L648-SNO                   
                   MOVE     SPACES             TO    L648-REGN-NO               
                   MOVE     SPACES             TO    L648-SOC-DEBTOR            
                                                     L648-DTE-FILE-RCVD         
                                                     L648-CA-TAG                
                                                     L648-ED-FOR-TP             
                                                     L648-UNIT-NO               
                                                     L648-ACCPT-DATE            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC4 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC4   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   MOVE 'Y' TO  WS-CASE-CHK4                                    
                   PERFORM  4075-SETUP-REPORT  THRU  4075-EXIT                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
            IF WS-CASE-CHK1 = 'Y' OR                                            
               WS-CASE-CHK2 = 'Y' OR                                            
               WS-CASE-CHK3 = 'Y' OR                                            
               WS-CASE-CHK4 = 'Y'                                               
               IF TP-CASE                                                       
                   ADD 1 TO WS-TP-CASE-CNT WS-TP-TOT-CNT                        
               ELSE                                                             
                   ADD 1 TO WS-SA-TP-CASE-CNT WS-SA-TP-TOT-CNT                  
               END-IF                                                           
               MOVE 'N' TO WS-CASE-CHK1 WS-CASE-CHK2 WS-CASE-CHK3               
                           WS-CASE-CHK4                                         
            END-IF.                                                             
                                                                                
            MOVE   SPACES          TO   L648-PRINT-REC.                         
            MOVE   SPACES          TO   L648A-PRINT-REC.                        
                                                                                
            IF TP-CASE                                                          
               WRITE  L648-PRINT-REC  AFTER 1                                   
            ELSE                                                                
               WRITE  L648A-PRINT-REC AFTER 1.                                  
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       4050-READ-K820.                                                          
      *===============================================================*         
            MOVE  K800-NUM-REGN        TO    K820-NUM-REGN.                     
            MOVE  'N'                  TO    WS-K820-FND.                       
                                                                                
            READ   BP13K820.                                                    
                                                                                
            EVALUATE  K820-STATUS                                               
              WHEN 00                                                           
                        ADD       1    TO    WS-CNT-K820-READ                   
                        MOVE     'Y'   TO    WS-K820-FND                        
              WHEN 23                                                           
                        MOVE     'N'   TO    WS-K820-FND                        
              WHEN OTHER                                                        
                        DISPLAY  'BP13K820 - FILE READ ERROR, STATUS : '        
                                    K820-STATUS                                 
                        PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                
            END-EVALUATE.                                                       
                                                                                
       4050-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       4075-SETUP-REPORT.                                                       
      *===============================================================*         
            MOVE   K820-NUM-NRIC     TO     L648-NRIC.                          
            MOVE   K820-NME-OCCP     TO     L648-NAME.                          
            MOVE   SPACES            TO     WS-PREV-BLK.                        
                                                                                
            IF TP-CASE                                                          
               MOVE WS-PREV-TP-BLK   TO     WS-PREV-BLK                         
            ELSE                                                                
               MOVE WS-PREV-SA-BLK   TO     WS-PREV-BLK                         
            END-IF.                                                             
                                                                                
            IF     (WS-PREV-BLK           NOT = F310-NUM-BLK) OR                
                   (WS-PREV-ESTATE        NOT = K110-ESTATE) OR                 
                   (WS-PREV-NEIGHBOURHOOD NOT = K110-NEIGHBOURHOOD) OR          
                   (WS-PREV-CONTRACT-NO   NOT = K110-CONTRACT-NO) OR            
                   (WS-PREV-TITLE         NOT = L648-REPORT-TITLE)              
                MOVE   'Y'   TO    WS-PAGE-BREAK                                
                PERFORM 4080-UPDATE-PAGE-BREAK  THRU  4080-EXIT                 
                DISPLAY 'PAGE BREAK OCCURED'                                    
            ELSE                                                                
                MOVE   'N'   TO    WS-PAGE-BREAK                                
            END-IF.                                                             
                                                                                
            IF  TP-CASE                                                         
                IF (WS-TP-CASE-CNT  > 08) OR                                    
                   (WS-PAGE-BREAK = 'Y')                                        
                    PERFORM 4100-PRINT-HEADING-TP  THRU  4100-EXIT              
                    MOVE       1                TO    WS-TP-CASE-CNT            
                END-IF                                                          
                WRITE  L648-PRINT-REC  FROM L648-PRT-DETAIL AFTER 1             
            ELSE                                                                
                IF (WS-SA-TP-CASE-CNT  > 08) OR                                 
                   (WS-PAGE-BREAK = 'Y')                                        
                    PERFORM 4200-PRINT-HEADING-SA-TP  THRU  4200-EXIT           
                    MOVE       1                TO    WS-SA-TP-CASE-CNT         
                END-IF                                                          
                WRITE  L648A-PRINT-REC  FROM L648-PRT-DETAIL AFTER 1            
            END-IF.                                                             
                                                                                
       4075-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       4080-UPDATE-PAGE-BREAK.                                                  
      *===============================================================*         
             PERFORM 3750-GET-ESTATE-NAME THRU  3750-EXIT.                      
                                                                                
             MOVE  F310-NUM-BLK         TO   L648-BLK-NO.                       
             MOVE  F310-NME-STREET      TO   L648-ST-NAME.                      
             STRING F310-NUM-POSTAL ')' DELIMITED BY SIZE                       
                                        INTO L648-POSTCDE.                      
                                                                                
             MOVE   K110-NEIGHBOURHOOD  TO   L648-NEIGHBOURHOOD.                
             MOVE   K110-CONTRACT-NO    TO   L648-CONTRACT-NO.                  
                                                                                
            IF TP-CASE                                                          
               MOVE    F310-NUM-BLK     TO   WS-PREV-TP-BLK                     
            ELSE                                                                
               MOVE    F310-NUM-BLK     TO   WS-PREV-SA-BLK                     
            END-IF.                                                             
                                                                                
            MOVE    K110-ESTATE         TO   WS-PREV-ESTATE.                    
            MOVE    K110-NEIGHBOURHOOD  TO   WS-PREV-NEIGHBOURHOOD.             
            MOVE    K110-CONTRACT-NO    TO   WS-PREV-CONTRACT-NO.               
            MOVE    L648-REPORT-TITLE   TO   WS-PREV-TITLE.                     
                                                                                
       4080-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       4100-PRINT-HEADING-TP.                                                   
      *===============================================================*         
           ADD         1               TO    WS-PAGE-CNT-TP                     
           MOVE        WS-PAGE-CNT-TP  TO    L648-PRINT-PAGE                    
           MOVE    ' TP )'             TO    L648-REPORT-TYPE.                  
           MOVE    'BP13L648'          TO    L648-REPORT-NAME                   
                                                                                
           MOVE SPACES                 TO    L648-PRINT-REC.                    
           WRITE L648-PRINT-REC        AFTER PAGE.                              
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-01 AFTER 1.                 
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-02 AFTER 1.                 
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-03 AFTER 1.                 
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-03A AFTER 2.                
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-03B AFTER 1.                
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-03C AFTER 1.                
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-04 AFTER 2.                 
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-05 AFTER 1.                 
           MOVE  ALL '='                   TO  L648-PRINT-REC.                  
           WRITE L648-PRINT-REC  AFTER 1.                                       
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       4200-PRINT-HEADING-SA-TP.                                                
      *===============================================================*         
           ADD         1               TO    WS-PAGE-CNT-SA-TP                  
           MOVE    WS-PAGE-CNT-SA-TP   TO    L648-PRINT-PAGE                    
           MOVE    ' SA NOT SIGNED )'  TO    L648-REPORT-TYPE.                  
           MOVE    'P13L648A'          TO    L648-REPORT-NAME                   
                                                                                
           MOVE SPACES                 TO    L648A-PRINT-REC.                   
           WRITE L648A-PRINT-REC       AFTER PAGE.                              
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-01 AFTER 1.                
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-02 AFTER 1.                
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-03 AFTER 1.                
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-03A AFTER 2.               
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-03B AFTER 1.               
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-03C AFTER 1.               
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-04 AFTER 2.                
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-05 AFTER 1.                
           MOVE  ALL '='                   TO  L648A-PRINT-REC.                 
           WRITE L648A-PRINT-REC  AFTER 1.                                      
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       6000-PRINT-INDIVIDUAL.                                                   
      *===============================================================*         
            MOVE   SPACES          TO   L648-PRINT-REC.                         
            MOVE   SPACES          TO   L648A-PRINT-REC.                        
            MOVE   SPACES          TO   L648-ADDRESS.                           
                                                                                
           IF  K800-NUM-NRIC1 NOT = SPACES  AND  LOW-VALUES OR                  
               K800-NUM-NRIC2 NOT = SPACES  AND  LOW-VALUES OR                  
               K800-NUM-NRIC3 NOT = SPACES  AND  LOW-VALUES OR                  
               K800-NUM-NRIC4 NOT = SPACES  AND  LOW-VALUES                     
                STRING 'BLK ' F310-NUM-BLK ' #' F310-NUM-MAIN                   
                       '-'    F310-NUM-LEVEL                                    
                       DELIMITED BY SIZE INTO   L648-ADDRESS                    
               IF TP-CASE                                                       
                  ADD       1            TO       WS-SEQ-NO-A                   
                  MOVE     WS-SEQ-NO-A   TO       L648-SNO-A                    
               ELSE                                                             
                  ADD       1            TO       WS-SEQ-NO-B                   
                  MOVE     WS-SEQ-NO-B   TO       L648-SNO-A                    
               END-IF                                                           
               MOVE     K800-NUM-REGN    TO       L648-REGN-NO-A                
               STRING   K800-DTE-FILE-RECEIVED(7:2) '/'                         
                        K800-DTE-FILE-RECEIVED(5:2) '/'                         
                        K800-DTE-FILE-RECEIVED(1:4)                             
                        DELIMITED BY SIZE INTO L648-DTE-FILE-RCVD-A             
                                                                                
               IF K800-AMT-SOC-DEBTOR IS NOT NUMERIC                            
                  MOVE ZERO           TO     K800-AMT-SOC-DEBTOR                
               END-IF                                                           
               IF K800-AMT-SOC-DEBTOR > ZERO                                    
                  MOVE 'N'          TO     L648-SOC-DEBTOR-A                    
               ELSE                                                             
                  MOVE '-'          TO     L648-SOC-DEBTOR-A                    
               END-IF                                                           
               MOVE  K800-NUM-LOAN-TAG    TO    L648-CA-TAG-A                   
               STRING K800-DTE-EXPIRED-TP(7:2) '/'                              
                      K800-DTE-EXPIRED-TP(5:2) '/'                              
                      K800-DTE-EXPIRED-TP(1:4) DELIMITED BY SIZE                
                      INTO   L648-ED-FOR-TP-A                                   
               STRING K110-DTE-ACCEPTANCE(7:2) '/'                              
                      K110-DTE-ACCEPTANCE(5:2) '/'                              
                      K110-DTE-ACCEPTANCE(1:4) DELIMITED BY SIZE                
                      INTO   L648-ACCPT-DATE-A                                  
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC1 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC1   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   MOVE 'Y' TO WS-CASE-CHK1                                     
                   PERFORM  6075-SETUP-REPORT  THRU  6075-EXIT                  
                   MOVE     ZERO               TO  L648-SNO-A                   
                   MOVE     SPACES             TO  L648-REGN-NO-A               
                   MOVE     SPACES             TO  L648-SOC-DEBTOR-A            
                                                   L648-DTE-FILE-RCVD-A         
                                                   L648-CA-TAG-A                
                                                   L648-ED-FOR-TP-A             
                                                   L648-ACCPT-DATE-A            
                   MOVE  F310-NME-STREET       TO  L648-ADDRESS                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC2 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC2   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   MOVE 'Y' TO WS-CASE-CHK2                                     
                   PERFORM  6075-SETUP-REPORT  THRU  6075-EXIT                  
                   MOVE     ZERO               TO  L648-SNO-A                   
                   MOVE     SPACES             TO  L648-REGN-NO-A               
                   MOVE     SPACES             TO  L648-SOC-DEBTOR-A            
                                                   L648-DTE-FILE-RCVD-A         
                                                   L648-CA-TAG-A                
                                                   L648-ED-FOR-TP-A             
                                                   L648-ACCPT-DATE-A            
                   MOVE     SPACES             TO  L648-ADDRESS                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC3 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC3   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   MOVE 'Y' TO WS-CASE-CHK3                                     
                   PERFORM  6075-SETUP-REPORT  THRU  6075-EXIT                  
                   MOVE     ZERO               TO   L648-SNO-A                  
                   MOVE     SPACES             TO   L648-REGN-NO-A              
                   MOVE     SPACES             TO   L648-SOC-DEBTOR-A           
                                                    L648-DTE-FILE-RCVD-A        
                                                    L648-CA-TAG-A               
                                                    L648-ED-FOR-TP-A            
                                                    L648-ACCPT-DATE-A           
                                                    L648-ADDRESS                
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC4 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC4   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   MOVE 'Y' TO WS-CASE-CHK4                                     
                   PERFORM  6075-SETUP-REPORT  THRU  6075-EXIT                  
                   MOVE     SPACES             TO    L648-ADDRESS               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF L648-ADDRESS NOT = SPACES                                         
                   PERFORM  6075-SETUP-REPORT  THRU  6075-EXIT                  
            END-IF.                                                             
                                                                                
            IF WS-CASE-CHK1 = 'Y' OR                                            
               WS-CASE-CHK2 = 'Y' OR                                            
               WS-CASE-CHK3 = 'Y' OR                                            
               WS-CASE-CHK4 = 'Y'                                               
               IF TP-CASE                                                       
                   ADD 1 TO WS-TP-CASE-CNT WS-TP-TOT-CNT                        
               ELSE                                                             
                   ADD 1 TO WS-SA-TP-CASE-CNT WS-SA-TP-TOT-CNT                  
               END-IF                                                           
               MOVE 'N' TO WS-CASE-CHK1 WS-CASE-CHK2 WS-CASE-CHK3               
                           WS-CASE-CHK4                                         
            END-IF.                                                             
                                                                                
            MOVE   SPACES          TO   L648-PRINT-REC.                         
            MOVE   SPACES          TO   L648A-PRINT-REC.                        
                                                                                
            IF TP-CASE                                                          
               WRITE  L648-PRINT-REC  AFTER 1                                   
            ELSE                                                                
               WRITE  L648A-PRINT-REC  AFTER 1.                                 
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       6075-SETUP-REPORT.                                                       
      *===============================================================*         
            MOVE   K820-NUM-NRIC     TO     L648-NRIC-A.                        
            MOVE   K820-NME-OCCP     TO     L648-NAME-A.                        
                                                                                
            IF  TP-CASE                                                         
                IF  WS-TP-CASE-CNT  > 08 OR                                     
                    WS-PREV-TITLE   NOT = L648-REPORT-TITLE                     
                    PERFORM 6100-PRINT-HEADING-TP  THRU  6100-EXIT              
                    MOVE       1             TO    WS-TP-CASE-CNT               
                    MOVE   L648-REPORT-TITLE TO    WS-PREV-TITLE                
                END-IF                                                          
                WRITE  L648-PRINT-REC  FROM L648-PRT-DETAIL-A AFTER 1           
            ELSE                                                                
                IF  WS-SA-TP-CASE-CNT > 08 OR                                   
                    WS-PREV-TITLE   NOT = L648-REPORT-TITLE                     
                    MOVE    L648-REPORT-TITLE   TO    WS-PREV-TITLE             
                    PERFORM 6200-PRINT-HEADING-SA-TP  THRU  6200-EXIT           
                    MOVE       1          TO          WS-SA-TP-CASE-CNT         
                END-IF                                                          
                WRITE  L648A-PRINT-REC  FROM L648-PRT-DETAIL-A AFTER 1          
            END-IF.                                                             
                                                                                
            MOVE   SPACES            TO     K820-NUM-NRIC.                      
            MOVE   SPACES            TO     K820-NME-OCCP.                      
                                                                                
       6075-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       6100-PRINT-HEADING-TP.                                                   
      *===============================================================*         
           ADD         1               TO    WS-PAGE-CNT-TP                     
           MOVE        WS-PAGE-CNT-TP  TO    L648-PRINT-PAGE                    
           MOVE    ' TP )'             TO    L648-REPORT-TYPE.                  
           MOVE    'BP13L648'          TO    L648-REPORT-NAME                   
                                                                                
           MOVE SPACES                 TO    L648-PRINT-REC.                    
           WRITE L648-PRINT-REC        AFTER PAGE.                              
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-01 AFTER 1.                 
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-02 AFTER 1.                 
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-03 AFTER 1.                 
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-04-A AFTER 2.               
           WRITE L648-PRINT-REC  FROM L648-PRT-HEAD-05-A AFTER 1.               
           MOVE  ALL '='                   TO  L648-PRINT-REC.                  
           WRITE L648-PRINT-REC  AFTER 1.                                       
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       6200-PRINT-HEADING-SA-TP.                                                
      *===============================================================*         
           ADD         1               TO    WS-PAGE-CNT-SA-TP                  
           MOVE    WS-PAGE-CNT-SA-TP   TO    L648-PRINT-PAGE                    
           MOVE    ' SA/TP )'          TO    L648-REPORT-TYPE.                  
           MOVE    'P13L648A'          TO    L648-REPORT-NAME                   
                                                                                
           MOVE SPACES                 TO    L648A-PRINT-REC.                   
           WRITE L648A-PRINT-REC       AFTER PAGE.                              
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-01 AFTER 1.                
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-02 AFTER 1.                
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-03 AFTER 1.                
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-04-A AFTER 2.              
           WRITE L648A-PRINT-REC  FROM L648-PRT-HEAD-05-A AFTER 1.              
           MOVE  ALL '='                   TO  L648A-PRINT-REC.                 
           WRITE L648A-PRINT-REC  AFTER 1.                                      
                                                                                
       6200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       9900-HANDLE-ABEND.                                                       
      *===============================================================*         
           MOVE  WS-TP-TOT-CNT          TO     WS-TP-TOT-CNT-EDT                
           MOVE  WS-SA-TP-TOT-CNT       TO     WS-SA-TP-TOT-CNT-EDT.            
           MOVE  SPACES                 TO     L648-PRINT-REC.                  
           MOVE  SPACES                 TO     L648A-PRINT-REC.                 
           STRING  '         ' 'TOTAL TP CASES LISTED : '                       
                   WS-TP-TOT-CNT-EDT DELIMITED BY SIZE                          
                   INTO   L648-PRINT-REC.                                       
           WRITE L648-PRINT-REC  AFTER 2.                                       
                                                                                
           STRING  '         ' 'TOTAL SA/TP CASES LISTED : '                    
                   WS-SA-TP-TOT-CNT-EDT DELIMITED BY SIZE                       
                   INTO   L648A-PRINT-REC.                                      
           WRITE L648A-PRINT-REC  AFTER 2.                                      
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* BP13C648 ABEND TOTALS ********'.                    
           DISPLAY 'PROGRAM-ID : BP13C648'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13F310 RECS READ     = ' WS-CNT-F310-READ.          
           DISPLAY 'TOTAL BP13F310 ERR RECS      = ' WS-CNT-F310-ERROR.         
           DISPLAY 'TOTAL BP13K800 RECS READ     = ' WS-CNT-K800-READ.          
           DISPLAY 'TOTAL BP13K820 RECS READ     = ' WS-CNT-K820-READ.          
           DISPLAY 'TOTAL BM06K110 RECS READ     = ' WS-CNT-K110-READ.          
                                                                                
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BM06K110                                                       
                 BM06K510                                                       
                 BP13L648                                                       
                 P13L648A.                                                      
                                                                                
           IF K800-STATUS NOT = 0                                               
              MOVE K800-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K800 ' K800-STATUS                  
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 0                                               
              MOVE K820-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K820 ' K820-STATUS                  
           END-IF.                                                              
                                                                                
           IF K110-STATUS NOT = 0                                               
              MOVE K110-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BM06K110 ' K110-STATUS                  
           END-IF.                                                              
                                                                                
           IF K510-STATUS NOT = 0                                               
              MOVE K510-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BM06K510 ' K510-STATUS                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       9999-WRAP-UP.                                                            
      *===============================================================*         
           MOVE  WS-TP-TOT-CNT          TO     WS-TP-TOT-CNT-EDT                
           MOVE  WS-SA-TP-TOT-CNT       TO     WS-SA-TP-TOT-CNT-EDT.            
           MOVE  SPACES                 TO     L648-PRINT-REC.                  
           MOVE  SPACES                 TO     L648A-PRINT-REC.                 
                                                                                
           STRING  '         ' 'TOTAL TP CASES LISTED : '                       
                   WS-TP-TOT-CNT-EDT DELIMITED BY SIZE                          
                   INTO   L648-PRINT-REC.                                       
           WRITE L648-PRINT-REC  AFTER 2.                                       
                                                                                
           STRING  '         ' 'TOTAL SA/TP CASES LISTED : '                    
                   WS-SA-TP-TOT-CNT-EDT DELIMITED BY SIZE                       
                   INTO   L648A-PRINT-REC.                                      
           WRITE L648A-PRINT-REC  AFTER 2.                                      
                                                                                
           DISPLAY '******* BP13C648 AUDIT TOTALS ********'.                    
           DISPLAY 'PROGRAM-ID : BP13C648'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13F310 RECS READ     = ' WS-CNT-F310-READ.          
           DISPLAY 'TOTAL BP13F310 ERR RECS      = ' WS-CNT-F310-ERROR.         
           DISPLAY 'TOTAL BP13K800 RECS READ     = ' WS-CNT-K800-READ.          
           DISPLAY 'TOTAL BP13K820 RECS READ     = ' WS-CNT-K820-READ.          
           DISPLAY 'TOTAL BM06K110 RECS READ     = ' WS-CNT-K110-READ.          
           DISPLAY 'TOTAL BM06K110 RECS READ     = ' WS-CNT-K110-READ.          
           DISPLAY 'TOTAL TP CASES LISTED        = ' WS-TP-TOT-CNT.             
           DISPLAY 'TOTAL SA/TP CASES LISTED     = ' WS-SA-TP-TOT-CNT.          
                                                                                
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BM06K110                                                       
                 BP13L648                                                       
                 P13L648A.                                                      
                                                                                
           IF K800-STATUS NOT = 0                                               
              MOVE K800-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K800 ' K800-STATUS                  
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 0                                               
              MOVE K820-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K820 ' K820-STATUS                  
           END-IF.                                                              
                                                                                
           IF K110-STATUS NOT = 0                                               
              MOVE K110-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BM06K110 ' K110-STATUS                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
