       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C829                                                  
      *AUTHOR.        LSB.                                                      
      *DATE-WRITTEN.  25/01/95.                                                 
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO REPLACE REFERENCE NUMBER FOR APPLICATIONS  *                
      *          WHICH MAY HAVE CHANGED DURING THE LAPSE       *                
      *          BETWEEN MHA DATA TRANSFER                     *                
      *                                                        *                
      *    TASK :                                              *                
      *      1.  TO ENSURE FOR EVERY MATCHING NRIC, THE        *                
      *          REFERENCE NUMBER ALSO MATCHES.                *                
      *                                                        *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *      1  BP13F558                                       *                
      *      2  BP13F725                                       *                
      *                                                        *                
      *     OUTPUT FILE :                                      *                
      *      1  BP13OUT                                        *                
      *                                                        *                
      * CHG-NO  BY   ON       DESCRIPTION                      *                
      * ---------------------------------                      *                
      * BP130082 LSB 960117 EXPAND RECL FOR BP13F725 FROM 205  *                
      * BP132376 MJ16030729 USE REGNNO INSTEAD OF REFNO OF FILE*                
      *                     BP13F725                           *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT BP13F725 ASSIGN TO BP13F725.                                  
           SELECT BP13INP ASSIGN TO BP13INP.                                    
           SELECT BP13OUT ASSIGN TO BP13OUT.                                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F725                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 211 CHARACTERS                                      
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F725.                                                           
                                                                                
       FD   BP13INP                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 20 CHARACTERS                                       
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
                                                                                
       01   BP13INP-REC.                                                        
            05 INP-KEY.                                                         
               10 INP-REF          PIC X(8).                                    
               10 INP-NRIC         PIC X(9).                                    
            05 FILLER              PIC X(3).                                    
                                                                                
       FD   BP13OUT                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 211 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       01   BP13OUT-REC            PIC X(211).                                  
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F725-CNT             PIC 9(8)    VALUE ZERO.                      
       01  WS-F725-WRITE           PIC 9(8)    VALUE ZERO.                      
       01  WS-INPUT-CNT            PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F725-UNMATCH         PIC 9(8)    VALUE ZERO.                      
       01  WS-EOF-F725             PIC X(3).                                    
           88  EOF-OF-F725                     VALUE 'YES'.                     
       01  WS-EOF-INPUT            PIC X(3).                                    
           88  EOF-OF-INPUT                    VALUE 'YES'.                     
                                                                                
       01  WS-F725-KEY.                                                         
           05  WS-F725-REF         PIC X(8).                                    
           05  WS-F725-NRIC        PIC X(9).                                    
                                                                                
       PROCEDURE DIVISION.                                                      
       10-MAIN-LOGIC.                                                           
           PERFORM 20-INITIALIZATION  THRU 20-EXIT.                             
           PERFORM 200-READ-F725      THRU 200-EXIT.                            
           PERFORM 300-READ-INPUT     THRU 300-EXIT.                            
           PERFORM 400-MATCHING-ROUTINE THRU 400-EXIT                           
                   UNTIL EOF-OF-INPUT AND EOF-OF-F725.                          
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       20-INITIALIZATION.                                                       
           OPEN INPUT  BP13F725                                                 
                       BP13INP                                                  
               OUTPUT  BP13OUT.                                                 
                                                                                
           MOVE 'NO' TO WS-EOF-F725.                                            
           MOVE 'NO' TO WS-EOF-INPUT.                                           
                                                                                
       20-EXIT.                                                                 
           EXIT.                                                                
                                                                                
       200-READ-F725.                                                           
           READ BP13F725                                                        
                AT END MOVE 'YES' TO WS-EOF-F725                                
                MOVE HIGH-VALUE TO WS-F725-KEY                                  
                GO TO 200-EXIT.                                                 
           ADD 1 TO WS-F725-CNT.                                                
           MOVE F725-NUM-REGN TO WS-F725-REF.                                   
           MOVE F725-NUM-NRIC TO WS-F725-NRIC.                                  
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-INPUT.                                                          
           READ BP13INP                                                         
               AT END MOVE 'YES' TO WS-EOF-INPUT                                
               MOVE HIGH-VALUE TO INP-KEY                                       
               GO TO 300-EXIT.                                                  
           ADD 1 TO WS-INPUT-CNT.                                               
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH F725 WITH INPUT FILE (NUM-NRIC)                        *         
      *****************************************************************         
                                                                                
       400-MATCHING-ROUTINE.                                                    
           IF WS-F725-KEY = INP-KEY                                             
                ADD 1 TO WS-MATCH                                       .       
                PERFORM 410-WRITE-F725 THRU 410-EXIT                            
                  UNTIL WS-F725-KEY NOT = INP-KEY                               
                PERFORM 300-READ-INPUT THRU 300-EXIT                            
           ELSE                                                                 
           IF WS-F725-KEY > INP-KEY                                             
                PERFORM 300-READ-INPUT THRU 300-EXIT                            
           ELSE                                                                 
                ADD 1 TO WS-F725-UNMATCH                                .       
                PERFORM 200-READ-F725  THRU 200-EXIT.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-WRITE-F725.                                                          
           WRITE BP13OUT-REC FROM BP13F725-REC.                                 
           ADD 1 TO WS-F725-WRITE                                               
           PERFORM 200-READ-F725  THRU 200-EXIT.                                
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
           DISPLAY ' '.                                                         
           DISPLAY '---- BP13C829 --------------------------'.                  
           DISPLAY ' '.                                                         
           DISPLAY '1. RECORDS READ FROM BP13F725    = ' WS-F725-CNT.           
           DISPLAY '2. RECORDS READ FROM BP13INP    = ' WS-INPUT-CNT.           
           DISPLAY '3. MATCHED RECORDS              = ' WS-MATCH.               
           DISPLAY '4. F725 UNMATCH RECORDS         = ' WS-F725-UNMATCH.        
           DISPLAY '5. RECORDS WRITTEN OUT          = ' WS-F725-WRITE.          
           DISPLAY ' '.                                                         
                                                                                
           CLOSE      BP13INP                                                   
                      BP13F725                                                  
                      BP13OUT.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
