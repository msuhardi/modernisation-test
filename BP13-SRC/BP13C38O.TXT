       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C38O.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  25/08/2015.                                               
      * ========================================================== *            
      * OBJECTIVE    :  TO GENERATE FN DEBARMENT REPORT            *            
      * ========================================================== *            
      * INPUT FILES  :  1.) BP13F410                               *            
      *                 2.) BP13K820                               *            
      *                 3.) P04K010                                *            
      *                                                            *            
      * LISTING      :  1.) BP13L38O                               *            
      *                                                            *            
      * MAIL FILE    :  1.) MAILC38O                               *            
      * ========================================================== *            
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS           *            
      * ---------------------------------------------------------- *            
      * BP139160 17042024 KAC1 Update email recipient:             *            
      *                         TO: Chee Khiang CHIAM & Juariah ALI*            
      *                         CC: Soo Chiew low,Catherine HL TAN&*            
      *                             Catherine HL TAN               *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410  ASSIGN       TO BP13F410.                           
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT P04K010   ASSIGN       TO P04K010                             
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K010-PRIME-KEY                      
                            FILE STATUS  IS WS-K010-STATUS.                     
                                                                                
           SELECT SY02F001  ASSIGN       TO SY02F001.                           
                                                                                
           SELECT BP13L38O  ASSIGN       TO BP13L38O.                           
                                                                                
           SELECT MAILC38O  ASSIGN       TO MAILC38O.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F410                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13F410.                                                       
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
           COPY BP13K820.                                                       
                                                                                
       FD  P04K010                                                              
           RECORD CONTAINS 300 CHARACTERS.                                      
           COPY P04K010.                                                        
                                                                                
           COPY SY02F001.                                                       
                                                                                
       FD  BP13L38O                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L38O-REC                  PIC X(132).                            
                                                                                
       FD  MAILC38O                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 100 CHARACTERS.                                      
       01  MAILC38O-REC                  PIC X(100).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K820-STATUS            PIC 9(2)   VALUE 00.                   
           05  WS-K010-STATUS            PIC 9(2)   VALUE 00.                   
                                                                                
       01  WS-COUNT.                                                            
           05  WS-CNT-F410-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F410-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K820-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K010-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-NO-FN              PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE               PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE               PIC 9(2)   VALUE 60.                   
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F410-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K820-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K010-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-F410-SKIP              PIC X(1)   VALUE 'N'.                  
           05  WS-FND-FM                 PIC X(1)   VALUE 'N'.                  
           05  WS-FND-FN                 PIC X(1)   VALUE 'N'.                  
           05  WS-PRINTED                PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-FMT8-CCYY.                                            
                   15  WS-DTE-FMT8-CC    PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT8-YY    PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-FMT10-DD       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-MM       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-CCYY.                                           
                   15  WS-DTE-FMT10-CC   PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT10-YY   PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-TMP                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-LOW-INT            PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-LOW-FMT10          PIC X(10)  VALUE SPACES.               
           05  WS-DTE-LOW                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-HIGH-FMT10         PIC X(10)  VALUE SPACES.               
           05  WS-DTE-HIGH               PIC 9(8)   VALUE ZEROES.               
                                                                                
      *--- BP13L38O REPORT -----------------------------------------            
       01  L38O-HEADER1.                                                        
           05  FILLER                    PIC X(8)   VALUE 'BP13L38O'.           
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L38O-DTE-CURRENT          PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L38O-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  L38O-HEADER2.                                                        
           05  FILLER                    PIC X(36)  VALUE SPACES.               
           05  FILLER                    PIC X(35)  VALUE                       
               'EXCEPTION REPORT FOR FN DEBARMENT ('.                           
           05  L38O-DTE-LOW              PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(4)   VALUE ' TO '.               
           05  L38O-DTE-HIGH             PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X      VALUE ')'.                  
           05  FILLER                    PIC X(41)  VALUE SPACES.               
                                                                                
       01  L38O-HEADER3.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               '     S/NO      REGN NO      NRIC          FM CODE FOUND         
      -        '    DATE OF KEY COLLECTION'.                                    
                                                                                
       01  L38O-HEADER4.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               '     -----     --------     ---------     -------------         
      -        '    ----------------------'.                                    
                                                                                
       01  L38O-EMPTY-MSG.                                                      
           05  FILLER                    PIC X(53) VALUE SPACES.                
           05  FILLER                    PIC X(26) VALUE                        
               '*** NO RECORDS PRINTED ***'.                                    
           05  FILLER                    PIC X(53) VALUE SPACES.                
                                                                                
       01  L38O-FOOTER.                                                         
           05  FILLER                    PIC X(58) VALUE SPACES.                
           05  FILLER                    PIC X(15) VALUE                        
               'NOTHING FOLLOWS'.                                               
           05  FILLER                    PIC X(59) VALUE SPACES.                
                                                                                
       01  L38O-LINES.                                                          
           05  FILLER                    PIC X(132) VALUE ALL '-'.              
                                                                                
       01  L38O-DETAIL.                                                         
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L38O-SNO                  PIC ZZZZZ.                             
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L38O-NUM-REGN             PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L38O-NUM-NRIC             PIC X(9)   VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L38O-NUM-FM-FND           PIC X(1)   VALUE SPACES.               
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  L38O-DTE-COLLECT          PIC X(10).                             
                                                                                
      *--- MAILC38O REPORT -----------------------------------------            
       01  MAIL-COMMANDS.                                                       
           05  WS-MAIL-RPT1              PIC X(12)  VALUE                       
               'HELO SGPHDB1'.                                                  
           05  WS-MAIL-RPT2              PIC X(27)  VALUE                       
               'MAIL FROM:<OPCP@SGPHDB1>'.                                      
           05  WS-MAIL-RPT3              PIC X(50)  VALUE                       
               'RCPT TO:<CHIAM_Chee_Khiang@hdb.gov.sg>'.                        
           05  WS-MAIL-RPT3A             PIC X(50)  VALUE                       
               'RCPT TO:<JUARIAH_ALI@HDB.GOV.SG>'.                              
           05  WS-MAIL-RPT3B             PIC X(50)  VALUE                       
               'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                            
           05  WS-MAIL-RPT3C             PIC X(50)  VALUE                       
               'RCPT TO:<Catherine_HL_TAN@hdb.gov.sg>'.                         
           05  WS-MAIL-RPT3D             PIC X(50)  VALUE                       
               'RCPT TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT4              PIC X(4)   VALUE 'DATA'.               
           05  WS-MAIL-RPT5              PIC X(29)  VALUE                       
               'FROM:Soc System - Email Alert'.                                 
           05  WS-MAIL-RPT6              PIC X(50)  VALUE                       
               'TO:<CHIAM_Chee_Khiang@hdb.gov.sg>'.                             
           05  WS-MAIL-RPT6A             PIC X(50)  VALUE                       
               'TO:<JUARIAH_ALI@HDB.GOV.SG>'.                                   
           05  WS-MAIL-RPT6B             PIC X(50)  VALUE                       
               'CC:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                                 
           05  WS-MAIL-RPT6C             PIC X(50)  VALUE                       
               'CC:<Catherine_HL_TAN@hdb.gov.sg>'.                              
           05  WS-MAIL-RPT6D             PIC X(50)  VALUE                       
               'CC:<JAMILAH_BAHROM@HDB.GOV.SG>'.                                
           05  WS-MAIL-RPT7.                                                    
               10  FILLER                PIC X(44)  VALUE                       
                   'SUBJECT: Exception Report for FN Debarment ('.              
               10  WS-MAIL-DTE-LOW1      PIC X(10)  VALUE SPACES.               
               10  FILLER                PIC X(4)   VALUE ' to '.               
               10  WS-MAIL-DTE-HIGH1     PIC X(10)  VALUE SPACES.               
               10  FILLER                PIC X(1)   VALUE ')'.                  
           05  WS-MAIL-RPT8.                                                    
               10  FILLER                PIC X(6)   VALUE                       
                   'DATE: '.                                                    
               10  WS-MAIL-DTE-CURRENT1  PIC X(10)  VALUE SPACES.               
                                                                                
       01  MAIL-HEADER1.                                                        
           05  FILLER                    PIC X(8)   VALUE 'BP13L38O'.           
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(7)   VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(6)   VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  WS-MAIL-DTE-CURRENT2      PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  WS-MAIL-CNT-PAGE          PIC ZZZ9.                              
                                                                                
       01  MAIL-HEADER2.                                                        
           05  FILLER                    PIC X(20)  VALUE SPACES.               
           05  FILLER                    PIC X(35)  VALUE                       
               'EXCEPTION REPORT FOR FN DEBARMENT ('.                           
           05  WS-MAIL-DTE-LOW2          PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(4)   VALUE ' TO '.               
           05  WS-MAIL-DTE-HIGH2         PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X      VALUE ')'.                  
           05  FILLER                    PIC X(25)  VALUE SPACES.               
                                                                                
       01  MAIL-EMPTY-MSG.                                                      
           05  FILLER                    PIC X(38) VALUE SPACES.                
           05  FILLER                    PIC X(26) VALUE                        
               '*** NO RECORDS PRINTED ***'.                                    
           05  FILLER                    PIC X(38) VALUE SPACES.                
                                                                                
       01  MAIL-LINES.                                                          
           05  FILLER                    PIC X(100) VALUE ALL '-'.              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13F410-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
             UNTIL WS-F410-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K820                                                 
                       P04K010                                                  
                       SY02F001                                                 
                OUTPUT BP13L38O                                                 
                       MAILC38O.                                                
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS       TO   RETURN-CODE                    
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT.                     
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING P04K010 ERROR '  WS-K010-STATUS                  
              MOVE     WS-K010-STATUS       TO   RETURN-CODE                    
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT.                     
                                                                                
              PERFORM 1100-GET-DATES        THRU 1100-EXIT.                     
              PERFORM 8800-PRINT-HEADER     THRU 8800-EXIT.                     
              PERFORM 8900-GEN-EMAIL-HEADER THRU 8900-EXIT.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-GET-DATES.                                                          
      *-------------------------------------------------------------            
                                                                                
      *--- GET F001-DTE-CURRENT (FMT: CCYYMMDD)                                 
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT        TO   WS-DTE-FMT8.                       
           PERFORM 8100-CONV-DATE-TO-10 THRU 8100-EXIT.                         
           MOVE WS-DTE-FMT10            TO   L38O-DTE-CURRENT.                  
           DISPLAY 'CURRENT DATE    = ' L38O-DTE-CURRENT.                       
                                                                                
      *--- GET HIGH DATE RANGE (CURRENT DATE)                                   
           MOVE WS-DTE-FMT10            TO   L38O-DTE-HIGH.                     
           PERFORM 8000-CONV-DATE-TO-8  THRU 8000-EXIT.                         
           MOVE WS-DTE-FMT8             TO   WS-DTE-HIGH.                       
           DISPLAY 'DATE RANGE HIGH = ' WS-DTE-HIGH.                            
                                                                                
      *--- GET LOW DATE RANGE (CURRENT DATE - 6)                                
           MOVE WS-DTE-FMT8             TO   WS-DTE-TMP.                        
           COMPUTE WS-DTE-LOW-INT = FUNCTION INTEGER-OF-DATE                    
                                    (WS-DTE-TMP).                               
           COMPUTE WS-DTE-LOW-INT = WS-DTE-LOW-INT - 6.                         
           COMPUTE WS-DTE-TMP     = FUNCTION DATE-OF-INTEGER                    
                                    (WS-DTE-LOW-INT).                           
           MOVE WS-DTE-TMP              TO   WS-DTE-FMT8.                       
           PERFORM 8100-CONV-DATE-TO-10 THRU 8100-EXIT.                         
           MOVE WS-DTE-FMT10            TO   L38O-DTE-LOW.                      
           PERFORM 8000-CONV-DATE-TO-8  THRU 8000-EXIT.                         
           MOVE WS-DTE-FMT8             TO   WS-DTE-LOW.                        
           DISPLAY 'DATE RANGE LOW  = ' WS-DTE-LOW.                             
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-BP13F410-READ.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F410                                                        
                AT END MOVE 'Y'    TO  WS-F410-EOF.                             
                                                                                
           IF WS-F410-EOF NOT = 'Y'                                             
              ADD   1              TO  WS-CNT-F410-READ                         
              MOVE 'N'             TO  WS-PRINTED                               
                                                                                
      *--- ACCEPT RECORDS WHOSE KEY ISSUE DATE IS BETWEEN THE                   
      *--- GIVEN DATE RANGE ONLY                                                
                                                                                
              IF F410-DTE-KEY-ISSUE = ZEROES OR SPACES OR LOW-VALUES            
                 ADD   1           TO  WS-CNT-F410-SKIP                         
                 MOVE 'Y'          TO  WS-F410-SKIP                             
                                                                                
              ELSE                                                              
                 IF F410-DTE-KEY-ISSUE IS NUMERIC                               
                    IF F410-DTE-KEY-ISSUE >= WS-DTE-LOW AND                     
                       F410-DTE-KEY-ISSUE <= WS-DTE-HIGH                        
                       MOVE 'N'    TO  WS-F410-SKIP                             
                    ELSE                                                        
                       ADD   1     TO  WS-CNT-F410-SKIP                         
                       MOVE 'Y'    TO  WS-F410-SKIP                             
                    END-IF                                                      
                 ELSE                                                           
                    ADD   1        TO  WS-CNT-F410-SKIP                         
                    MOVE 'Y'       TO  WS-F410-SKIP                             
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
      * (1) MATCH FILTERED RECS TO BP13K820 (K820-NUM-REGN)                     
      * (2) IF MATCHED, MATCH TO P04K010 (K820-NUM-NRIC)                        
      * (3) IF MATCHED, CHECK K010-REASON-CODE FOR 'FM' AND 'FN'                
      * (4) IF 'FM' BUT NO 'FN', PRINT TO LIST                                  
      *-------------------------------------------------------------            
                                                                                
           IF WS-F410-SKIP = 'N'                                                
              INITIALIZE                       K820-KEY-FLD                     
              MOVE F410-NUM-REGN          TO   K820-NUM-REGN                    
                                                                                
              MOVE 'N'                    TO   WS-K820-EOF                      
                                                                                
              PERFORM 3100-BP13K820-START THRU 3100-EXIT                        
              PERFORM 4000-PROC-REGN      THRU 4000-EXIT                        
                UNTIL WS-K820-EOF = 'Y'                                         
                   OR K820-NUM-REGN NOT = F410-NUM-REGN                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F410-READ     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-BP13K820-START.                                                     
      *-------------------------------------------------------------            
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
                PERFORM 3200-BP13K820-READNEXT THRU 3200-EXIT                   
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'                   TO   WS-K820-EOF                     
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'START BP13K820 ERROR ' WS-K820-STATUS                  
                MOVE    WS-K820-STATUS     TO   RETURN-CODE                     
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-BP13K820-READNEXT.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13K820 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
                ADD 1                      TO   WS-CNT-K820-READ                
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'                   TO   WS-K820-EOF                     
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'READNEXT BP13K820 ERROR ' WS-K820-STATUS               
                MOVE    WS-K820-STATUS     TO   RETURN-CODE                     
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROC-REGN.                                                          
      *-------------------------------------------------------------            
                                                                                
           IF K820-NUM-NRIC NOT = SPACES OR LOW-VALUES                          
              INITIALIZE                       K010-PRIME-KEY                   
              MOVE K820-NUM-NRIC          TO   K010-NUM-UINFIN                  
                                                                                
              MOVE 'N'                    TO   WS-K010-EOF                      
              MOVE 'N'                    TO   WS-FND-FM                        
              MOVE 'N'                    TO   WS-FND-FN                        
                                                                                
              PERFORM 4100-P04K010-START  THRU 4100-EXIT                        
              PERFORM 5000-PROC-NRIC      THRU 5000-EXIT                        
                UNTIL WS-K010-EOF = 'Y'                                         
                   OR K010-NUM-UINFIN NOT = K820-NUM-NRIC                       
                                                                                
              IF WS-FND-FM = 'Y' AND WS-FND-FN = 'N'                            
                 PERFORM 8810-PRINT-LIST  THRU 8810-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3200-BP13K820-READNEXT THRU 3200-EXIT.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-P04K010-START.                                                      
      *-------------------------------------------------------------            
                                                                                
           START P04K010 KEY >= K010-PRIME-KEY.                                 
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
                PERFORM 4200-P04K010-READNEXT   THRU 4200-EXIT                  
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'                   TO   WS-K010-EOF                     
           WHEN OTHER                                                           
                DISPLAY 'START P04K010 ERROR '  WS-K010-STATUS                  
                MOVE    WS-K010-STATUS     TO   RETURN-CODE                     
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4200-P04K010-READNEXT.                                                   
      *-------------------------------------------------------------            
                                                                                
           READ P04K010 NEXT RECORD.                                            
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
                ADD 1                      TO   WS-CNT-K010-READ                
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'                   TO   WS-K010-EOF                     
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'READNEXT P04K010 ERROR ' WS-K010-STATUS                
                MOVE    WS-K010-STATUS     TO   RETURN-CODE                     
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-PROC-NRIC.                                                          
      *-------------------------------------------------------------            
                                                                                
           IF K010-REASON-CODE = 'FM'                                           
              MOVE 'Y'                    TO   WS-FND-FM                        
           END-IF.                                                              
                                                                                
           IF K010-REASON-CODE = 'FN'                                           
              MOVE 'Y'                    TO   WS-FND-FN                        
           END-IF.                                                              
                                                                                
           PERFORM 4200-P04K010-READNEXT THRU 4200-EXIT.                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8000-CONV-DATE-TO-8.                                                     
      *-------------------------------------------------------------            
      *    INPUT  - WS-DTE-FMT10                                                
      *    OUTPUT - WS-DTE-FMT8                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT10-CCYY  TO  WS-DTE-FMT8-CCYY.                        
           MOVE WS-DTE-FMT10-MM    TO  WS-DTE-FMT8-MM.                          
           MOVE WS-DTE-FMT10-DD    TO  WS-DTE-FMT8-DD.                          
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8100-CONV-DATE-TO-10.                                                    
      *-------------------------------------------------------------            
      *    INPUT  - WS-DTE-FMT8                                                 
      *    OUTPUT - WS-DTE-FMT10                                                
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT8-CCYY   TO  WS-DTE-FMT10-CCYY.                       
           MOVE WS-DTE-FMT8-MM     TO  WS-DTE-FMT10-MM.                         
           MOVE WS-DTE-FMT8-DD     TO  WS-DTE-FMT10-DD.                         
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8800-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
                                                                                
           ADD   1                 TO   WS-CNT-PAGE.                            
           MOVE  WS-CNT-PAGE       TO   L38O-CNT-PAGE                           
                                        WS-MAIL-CNT-PAGE.                       
           WRITE BP13L38O-REC      FROM L38O-HEADER1 AFTER PAGE.                
           WRITE BP13L38O-REC      FROM L38O-HEADER2.                           
           WRITE BP13L38O-REC      FROM L38O-HEADER3 AFTER 2.                   
           WRITE BP13L38O-REC      FROM L38O-HEADER4.                           
           MOVE  SPACES            TO   BP13L38O-REC.                           
           WRITE BP13L38O-REC.                                                  
           MOVE  6                 TO   WS-CNT-LINE.                            
                                                                                
       8800-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8810-PRINT-LIST.                                                         
      *-------------------------------------------------------------            
                                                                                
           IF WS-CNT-LINE > 55                                                  
              PERFORM 8800-PRINT-HEADER THRU 8800-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE SPACES                  TO   BP13L38O-REC                       
                                             L38O-DETAIL.                       
                                                                                
           IF WS-PRINTED = 'N'                                                  
              MOVE 'Y'                  TO   WS-PRINTED                         
                                                                                
              ADD 1                     TO   WS-CNT-NO-FN                       
              MOVE WS-CNT-NO-FN         TO   L38O-SNO                           
              MOVE F410-NUM-REGN        TO   L38O-NUM-REGN                      
              MOVE F410-DTE-KEY-ISSUE   TO   L38O-DTE-COLLECT                   
              MOVE F410-DTE-KEY-ISSUE   TO   WS-DTE-FMT8                        
              PERFORM 8100-CONV-DATE-TO-10 THRU 8100-EXIT                       
              MOVE WS-DTE-FMT10         TO   L38O-DTE-COLLECT                   
           END-IF.                                                              
                                                                                
           MOVE K820-NUM-NRIC           TO   L38O-NUM-NRIC.                     
           MOVE 'Y'                     TO   L38O-NUM-FM-FND.                   
           WRITE BP13L38O-REC           FROM L38O-DETAIL.                       
           WRITE MAILC38O-REC           FROM L38O-DETAIL.                       
                                                                                
           ADD 1                        TO   WS-CNT-LINE.                       
                                                                                
       8810-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8820-PRINT-EMTY-LIST.                                                    
      *-------------------------------------------------------------            
                                                                                
           WRITE BP13L38O-REC           FROM L38O-EMPTY-MSG AFTER 3.            
           WRITE MAILC38O-REC           FROM MAIL-EMPTY-MSG.                    
                                                                                
       8820-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8830-PRINT-FOOTER.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF WS-CNT-LINE > 55                                                  
              PERFORM 8800-PRINT-HEADER THRU 8800-EXIT                          
           END-IF.                                                              
                                                                                
           WRITE BP13L38O-REC           FROM L38O-LINES AFTER 2.                
      *    WRITE BP13L38O-REC           FROM L38O-FOOTER.                       
                                                                                
           WRITE MAILC38O-REC           FROM MAIL-LINES.                        
      *    WRITE MAILC38O-REC           FROM L38O-FOOTER.                       
                                                                                
       8830-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8900-GEN-EMAIL-HEADER.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE  L38O-DTE-LOW      TO   WS-MAIL-DTE-LOW1                        
                                        WS-MAIL-DTE-LOW2.                       
           MOVE  L38O-DTE-HIGH     TO   WS-MAIL-DTE-HIGH1                       
                                        WS-MAIL-DTE-HIGH2.                      
           MOVE  L38O-DTE-CURRENT  TO   WS-MAIL-DTE-CURRENT1                    
                                        WS-MAIL-DTE-CURRENT2.                   
                                                                                
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT1.                           
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT2.                           
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT3.                           
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT3A.                          
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT3B.                          
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT3C.                          
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT3D.                          
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT4.                           
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT5.                           
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT6.                           
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT6A.                          
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT6B.                          
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT6C.                          
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT6D.                          
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT7.                           
           WRITE MAILC38O-REC      FROM WS-MAIL-RPT8.                           
                                                                                
           MOVE SPACES             TO   MAILC38O-REC.                           
           WRITE MAILC38O-REC.                                                  
                                                                                
           WRITE MAILC38O-REC      FROM MAIL-HEADER1.                           
           WRITE MAILC38O-REC      FROM MAIL-HEADER2.                           
           MOVE SPACES             TO   MAILC38O-REC.                           
           WRITE MAILC38O-REC.                                                  
           WRITE MAILC38O-REC      FROM L38O-HEADER3.                           
           WRITE MAILC38O-REC      FROM L38O-HEADER4.                           
           MOVE SPACES             TO   MAILC38O-REC.                           
           WRITE MAILC38O-REC.                                                  
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
      *--- PRINT LIST EMPTY MESSAGE AND FOOTER                                  
           IF WS-CNT-NO-FN = 0                                                  
              PERFORM 8820-PRINT-EMTY-LIST THRU 8820-EXIT                       
           END-IF.                                                              
           PERFORM 8830-PRINT-FOOTER       THRU 8830-EXIT                       
                                                                                
           CLOSE BP13F410                                                       
                 BP13K820                                                       
                 P04K010                                                        
                 SY02F001                                                       
                 BP13L38O                                                       
                 MAILC38O.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE.                    
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING P04K010 ERROR '  WS-K010-STATUS                  
              MOVE     WS-K010-STATUS      TO   RETURN-CODE.                    
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C38O CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F410 READ      : ' WS-CNT-F410-READ.          
           DISPLAY ' 2. NO OF BP13F410 SKIPPED   : ' WS-CNT-F410-SKIP.          
           DISPLAY ' 3. NO OF BP13K820 READ      : ' WS-CNT-K820-READ.          
           DISPLAY ' 4. NO OF P04K010 READ       : ' WS-CNT-K010-READ.          
           DISPLAY ' 5. NO OF FM RECS WITHOUT FN : ' WS-CNT-NO-FN.              
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
