       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C924.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  25/05/2017.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *   OBJECTIVE   : UPDATE NUM-FLAT-PROJ AND NUM-BOOK-PROJ IN   *           
      *                 BP13FW3C                                    *           
      *                                                             *           
      *   NOTE        : FOR 2-ROOM, WRITE 2 RECORDS (2A & 2B)       *           
      *                    2A - FLOOR AREA <= 40                    *           
      *                    2B - FLOOR AREA >  40                    *           
      *                 FOR 5-ROOM, WRITE 2 RECORDS (00 & 5G)       *           
      *                    00 - MODL DESIGN = OTHERS                *           
      *                    5G - MODL DESIGN = 90 OR 91              *           
      *                                                             *           
      *   INPUT FILES : BP13FW3E - BOOKING SUMMARY PER PROJECT      *           
      *                 BP13FW3C - BOOKING SUMMARY                  *           
      *   OUTPUT FILE : P13FW3CA - UPDATED FILE                     *           
      *                                                             *           
      * =========================================================== *           
      * CHQ REQ  DATE       BY   DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136299 25/05/2017 SMR2 NEW PROGRAM                        *           
      * BP136299 30/05/2017 SMR2 CATER FOR NUM-FLAT-MGPS            *           
      * BP136832 02/06/2017 SMR2 CATER FOR FLAT-TYPE '3P'           *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FW3E ASSIGN       TO BP13FW3E.                            
                                                                                
           SELECT BP13FW3C ASSIGN       TO BP13FW3C.                            
                                                                                
           SELECT P13FW3CA ASSIGN       TO P13FW3CA.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FW3E                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 250 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       COPY BP13FW3E.                                                           
                                                                                
       FD   BP13FW3C                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       COPY BP13FW3C.                                                           
                                                                                
       FD   P13FW3CA                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       01   P13FW3CA-REC                    PIC X(500).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FW3E-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-FW3C-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-FW3CA-WRITE               PIC 9(06)   VALUE ZEROES.           
                                                                                
       01  WS-FW3E-COUNTERS.                                                    
           05  WS-FLAT-PROJ                 PIC 9(04)   VALUE ZEROES.           
           05  WS-FLAT-2F-TYP1              PIC 9(04)   VALUE ZEROES.           
           05  WS-FLAT-2F-TYP2              PIC 9(04)   VALUE ZEROES.           
           05  WS-FLAT-05                   PIC 9(04)   VALUE ZEROES.           
           05  WS-FLAT-5G                   PIC 9(04)   VALUE ZEROES.           
           05  WS-BOOK-PROJ                 PIC 9(04)   VALUE ZEROES.           
           05  WS-BOOK-2F-TYP1              PIC 9(04)   VALUE ZEROES.           
           05  WS-BOOK-2F-TYP2              PIC 9(04)   VALUE ZEROES.           
           05  WS-BOOK-05                   PIC 9(04)   VALUE ZEROES.           
           05  WS-BOOK-5G                   PIC 9(04)   VALUE ZEROES.           
           05  WS-FLAT-MGPS                 PIC 9(03)   VALUE ZEROES.           
           05  WS-SUPPLY                    PIC 9(04)   VALUE ZEROES.           
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CURR-DATE                 PIC X(08)   VALUE SPACES.           
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-FW3E-KEY.                                                     
               10  WS-FW3E-NME-PROJ         PIC X(40)   VALUE SPACES.           
           05  WS-FW3C-KEY.                                                     
               10  WS-FW3C-NME-PROJ         PIC X(40)   VALUE SPACES.           
           05  WS-BP13FW3C-REC              PIC X(500)  VALUE SPACES.           
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FW3E       THRU 2000-EXIT.                     
           PERFORM 2500-READ-BP13FW3C       THRU 2500-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-FW3E-KEY = HIGH-VALUES                                    
               AND WS-FW3C-KEY = HIGH-VALUES.                                   
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FW3E                                                 
                       BP13FW3C                                                 
                OUTPUT P13FW3CA.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-CURR-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13FW3E.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13FW3E AT END                                                 
                MOVE HIGH-VALUES            TO WS-FW3E-KEY                      
                GO TO 2000-EXIT.                                                
                                                                                
           MOVE FW3E-NME-LOCAL-PRCNCT       TO WS-FW3E-NME-PROJ.                
                                                                                
           ADD 1                            TO WS-FW3E-READ.                    
                                                                                
           MOVE ZEROES                      TO WS-FW3E-COUNTERS.                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2500-READ-BP13FW3C.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13FW3C AT END                                                 
                MOVE HIGH-VALUES            TO WS-FW3C-KEY                      
                GO TO 2500-EXIT.                                                
                                                                                
           MOVE FW3C-NME-PROJECT(1:40)      TO WS-FW3C-NME-PROJ.                
                                                                                
           ADD 1                            TO WS-FW3C-READ.                    
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-RECS.                                                       
      *-----------------*                                                       
                                                                                
           IF WS-FW3E-KEY = WS-FW3C-KEY                                         
              PERFORM 4000-MOVE-DETAILS        THRU 4000-EXIT                   
                UNTIL WS-FW3C-KEY > WS-FW3E-KEY                                 
                   OR WS-FW3C-KEY = HIGH-VALUES                                 
                                                                                
              PERFORM 2000-READ-BP13FW3E       THRU 2000-EXIT                   
           ELSE                                                                 
              IF WS-FW3E-KEY < WS-FW3C-KEY                                      
                 PERFORM 2000-READ-BP13FW3E    THRU 2000-EXIT                   
              ELSE                                                              
                 IF WS-FW3E-KEY > WS-FW3C-KEY                                   
                    PERFORM 2500-READ-BP13FW3C THRU 2500-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-MOVE-DETAILS.                                                       
      *------------------*                                                      
                                                                                
           EVALUATE FW3C-NUM-FLAT-TYPE(1:2)                                     
           WHEN 'SA'                                                            
                MOVE FW3E-NUM-UNIT-01       TO WS-FLAT-PROJ                     
                MOVE FW3E-NUM-BOOK-01       TO WS-BOOK-PROJ                     
                                                                                
           WHEN '2-'                                                            
                MOVE FW3E-NUM-UNIT-2A       TO WS-FLAT-2F-TYP1                  
                MOVE FW3E-NUM-BOOK-2A       TO WS-BOOK-2F-TYP1                  
                                                                                
                MOVE FW3E-NUM-UNIT-2B       TO WS-FLAT-2F-TYP2                  
                MOVE FW3E-NUM-BOOK-2B       TO WS-BOOK-2F-TYP2                  
                                                                                
                COMPUTE WS-SUPPLY = (FW3E-NUM-UNIT-2A +                         
                                     FW3E-NUM-UNIT-2B) * 0.15                   
                IF WS-SUPPLY < 20                                               
                   MOVE 20                  TO WS-FLAT-MGPS                     
                ELSE                                                            
                   MOVE WS-SUPPLY           TO WS-FLAT-MGPS                     
                END-IF                                                          
                                                                                
           WHEN '3-'                                                            
           WHEN '3P'                                                            
                MOVE FW3E-NUM-UNIT-03       TO WS-FLAT-PROJ                     
                MOVE FW3E-NUM-BOOK-03       TO WS-BOOK-PROJ                     
                                                                                
                COMPUTE WS-SUPPLY = FW3E-NUM-UNIT-03 * 0.15                     
                IF WS-SUPPLY < 20                                               
                   MOVE 20                  TO WS-FLAT-MGPS                     
                ELSE                                                            
                   MOVE WS-SUPPLY           TO WS-FLAT-MGPS                     
                END-IF                                                          
                                                                                
           WHEN '4-'                                                            
                MOVE FW3E-NUM-UNIT-04       TO WS-FLAT-PROJ                     
                MOVE FW3E-NUM-BOOK-04       TO WS-BOOK-PROJ                     
                                                                                
           WHEN '5-'                                                            
                MOVE FW3E-NUM-UNIT-05       TO WS-FLAT-05                       
                MOVE FW3E-NUM-BOOK-05       TO WS-BOOK-05                       
                                                                                
                MOVE FW3E-NUM-UNIT-5G       TO WS-FLAT-5G                       
                MOVE FW3E-NUM-BOOK-5G       TO WS-BOOK-5G                       
           END-EVALUATE.                                                        
                                                                                
           PERFORM 7000-WRITE-P13FW3CA      THRU 7000-EXIT.                     
           PERFORM 2500-READ-BP13FW3C       THRU 2500-EXIT.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       7000-WRITE-P13FW3CA.                                                     
      *--------------------*                                                    
                                                                                
           MOVE BP13FW3C-REC                TO WS-BP13FW3C-REC.                 
                                                                                
           EVALUATE FW3C-NUM-FT-CHAR2                                           
           WHEN '2 '                                                            
                MOVE WS-FLAT-2F-TYP1        TO FW3C-NUM-FLAT-PROJ               
                MOVE WS-BOOK-2F-TYP1        TO FW3C-NUM-BOOK-PROJ               
                MOVE WS-FLAT-MGPS           TO FW3C-NUM-FLAT-MGPS               
                MOVE '2A'                   TO FW3C-NUM-FT                      
                WRITE P13FW3CA-REC          FROM BP13FW3C-REC                   
                ADD 1                       TO WS-FW3CA-WRITE                   
                                                                                
                MOVE WS-BP13FW3C-REC        TO BP13FW3C-REC                     
                MOVE WS-FLAT-2F-TYP2        TO FW3C-NUM-FLAT-PROJ               
                MOVE WS-BOOK-2F-TYP2        TO FW3C-NUM-BOOK-PROJ               
                MOVE WS-FLAT-MGPS           TO FW3C-NUM-FLAT-MGPS               
                MOVE '2B'                   TO FW3C-NUM-FT                      
                WRITE P13FW3CA-REC          FROM BP13FW3C-REC                   
                ADD 1                       TO WS-FW3CA-WRITE                   
                                                                                
           WHEN '3 '                                                            
           WHEN '3P'                                                            
                MOVE WS-FLAT-PROJ           TO FW3C-NUM-FLAT-PROJ               
                MOVE WS-BOOK-PROJ           TO FW3C-NUM-BOOK-PROJ               
                MOVE WS-FLAT-MGPS           TO FW3C-NUM-FLAT-MGPS               
                WRITE P13FW3CA-REC          FROM BP13FW3C-REC                   
                ADD 1                       TO WS-FW3CA-WRITE                   
                                                                                
           WHEN '5 '                                                            
                MOVE WS-FLAT-05             TO FW3C-NUM-FLAT-PROJ               
                MOVE WS-BOOK-05             TO FW3C-NUM-BOOK-PROJ               
                WRITE P13FW3CA-REC          FROM BP13FW3C-REC                   
                ADD 1                       TO WS-FW3CA-WRITE                   
                                                                                
                IF WS-FLAT-5G > ZEROES                                          
                   MOVE WS-BP13FW3C-REC     TO BP13FW3C-REC                     
                   MOVE WS-FLAT-5G          TO FW3C-NUM-FLAT-PROJ               
                   MOVE WS-BOOK-5G          TO FW3C-NUM-BOOK-PROJ               
                   MOVE '5G'                TO FW3C-NUM-FT                      
                   WRITE P13FW3CA-REC       FROM BP13FW3C-REC                   
                   ADD 1                    TO WS-FW3CA-WRITE                   
                END-IF                                                          
                                                                                
           WHEN OTHER                                                           
                MOVE WS-FLAT-PROJ           TO FW3C-NUM-FLAT-PROJ               
                MOVE WS-BOOK-PROJ           TO FW3C-NUM-BOOK-PROJ               
                WRITE P13FW3CA-REC          FROM BP13FW3C-REC                   
                ADD 1                       TO WS-FW3CA-WRITE                   
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '*===========================================*'.             
           DISPLAY '  BP13C924 CONTROL TOTAL     : ' WS-CURR-DATE.              
           DISPLAY '*-------------------------------------------*'.             
           DISPLAY '  RECS READ FROM BP13FW3E    : ' WS-FW3E-READ.              
           DISPLAY '  RECS READ FROM BP13FW3C    : ' WS-FW3C-READ.              
           DISPLAY '  RECS WRITTEN IN P13FW3CA   : ' WS-FW3CA-WRITE.            
           DISPLAY '*===========================================*'.             
                                                                                
           CLOSE BP13FW3E                                                       
                 BP13FW3C                                                       
                 P13FW3CA.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
