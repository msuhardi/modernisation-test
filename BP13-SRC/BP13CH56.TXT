       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CH56.                                                  
      *AUTHOR.       ZDD1.                                                      
      *DATE-WRITTEN. 07/12/2015.                                                
      * ========================================================== *            
      * SYSTEMS OF COMMITMENT (BP13)                               *            
      * ========================================================== *            
      *  UPDATE - BP13KD07 (KD07-NUM-HALF-GRANT )                  *            
      *                                                            *            
      *  INPUT   :  BP17F000                                       *            
      *             BP17K200                                       *            
      *  I-O     :  BP13KD07                                       *            
      * ========================================================== *            
      * REF NO    DATE       BY     DESCRIPTIONS                   *            
      * ========================================================== *            
      * BP135225  09122015  ZDD1    NEW PGM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP17F000 ASSIGN TO BP17F000.                                  
                                                                                
           SELECT BP13KD07 ASSIGN TO BP13KD07                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD07-KEY-FLD                               
                  FILE STATUS     IS WS-KD07-STATUS.                            
                                                                                
           SELECT BP17K200 ASSIGN TO BP17K200                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K200-KEY-FLD                               
                  FILE STATUS     IS WS-K200-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP17F000                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP17F000.                                                           
                                                                                
       FD   BP13KD07                                                            
            RECORD CONTAINS 700 CHARACTERS.                                     
       COPY BP13KD07.                                                           
                                                                                
       FD   BP17K200                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP17K200.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F000-EOF                   PIC X    VALUE 'N'.                    
       01  WS-KD07-FND-TAG               PIC X    VALUE 'N'.                    
       01  WS-SERS-FTS-TAG               PIC X    VALUE 'N'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F000-READ              PIC 9(7) VALUE ZEROS.                  
           05  WS-KD07-WRITE             PIC 9(7) VALUE ZEROS.                  
           05  WS-KD07-FND               PIC 9(7) VALUE ZEROS.                  
           05  WS-KD07-NFND              PIC 9(7) VALUE ZEROS.                  
           05  WS-K200-FND               PIC 9(7) VALUE ZEROS.                  
           05  WS-K200-NFND              PIC 9(7) VALUE ZEROS.                  
           05  WS-NOT-SERS-FTS           PIC 9(7) VALUE ZEROS.                  
           05  WS-SERS-FTS               PIC 9(7) VALUE ZEROS.                  
                                                                                
       01  WS-STATUS.                                                           
           05  WS-KD07-STATUS            PIC X(2) VALUE SPACES.                 
           05  WS-K200-STATUS            PIC X(2) VALUE SPACES.                 
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F000    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-F000 THRU 300-EXIT                              
                    UNTIL WS-F000-EOF = 'Y'.                                    
            PERFORM 900-CLOSE-FILES  THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP17F000                                                 
                       BP17K200                                                 
                I-O    BP13KD07.                                                
                                                                                
           IF WS-KD07-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KD07 FILE, ' WS-KD07-STATUS            
              PERFORM 900-CLOSE-FILES         THRU 900-EXIT.                    
                                                                                
           IF WS-K200-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K200 FILE, ' WS-K200-STATUS            
              PERFORM 900-CLOSE-FILES         THRU 900-EXIT.                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F000.                                                           
      *-------------------------------------------------------------            
           READ BP17F000 AT END                                                 
                MOVE 'Y' TO WS-F000-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1         TO WS-F000-READ.                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F000.                                                        
      *-------------------------------------------------------------            
                                                                                
           PERFORM 400-READ-BP17K200   THRU 400-EXIT.                           
                                                                                
           IF WS-SERS-FTS-TAG       NOT = SPACES AND LOW-VALUES                 
              PERFORM 500-READ-BP13KD07        THRU 500-EXIT                    
              IF WS-KD07-FND-TAG = 'N'                                          
                 PERFORM 600-WRITE-BP13KD07    THRU 600-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F000       THRU 200-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-READ-BP17K200.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES               TO K200-KEY-FLD                            
                                        WS-SERS-FTS-TAG.                        
           MOVE F000-NUM-SCH-ACC     TO K200-NUM-SCH-ACC.                       
                                                                                
           READ BP17K200.                                                       
                                                                                
           IF WS-K200-STATUS = 00                                               
              ADD 1     TO WS-K200-FND                                          
              IF K200-NUM-SERS-FTS NOT = SPACES AND LOW-VALUES                  
                ADD   1                 TO  WS-SERS-FTS                         
                MOVE 'Y'                TO  WS-SERS-FTS-TAG                     
              ELSE                                                              
                ADD   1     TO WS-NOT-SERS-FTS                                  
              END-IF                                                            
           ELSE                                                                 
              IF WS-K200-STATUS = 23 OR 10                                      
                 ADD 1        TO WS-K200-NFND                                   
                 MOVE SPACES  TO BP17K200-REC                                   
              ELSE                                                              
                 DISPLAY 'ERROR READING BP17K200 : ' WS-K200-STATUS             
                 MOVE WS-K200-STATUS     TO RETURN-CODE                         
                 PERFORM 900-CLOSE-FILES THRU 900-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-READ-BP13KD07.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES               TO KD07-KEY-FLD.                           
           MOVE F000-NUM-REGNO       TO KD07-NUM-REGN.                          
                                                                                
           READ BP13KD07.                                                       
                                                                                
           IF WS-KD07-STATUS = 00                                               
                ADD 1    TO WS-KD07-FND                                         
                MOVE 'Y' TO WS-KD07-FND-TAG                                     
           ELSE                                                                 
              IF WS-KD07-STATUS = 23 OR 10                                      
                 ADD 1    TO WS-KD07-NFND                                       
                 MOVE 'N' TO WS-KD07-FND-TAG                                    
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13KD07 : ' WS-KD07-STATUS             
                 MOVE WS-KD07-STATUS     TO RETURN-CODE                         
                 PERFORM 900-CLOSE-FILES THRU 900-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-WRITE-BP13KD07.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES               TO   BP13KD07-REC.                         
           INITIALIZE                     BP13KD07-REC                          
                                                                                
           MOVE F000-NUM-REGNO       TO   KD07-NUM-REGN.                        
           MOVE K200-NUM-SERS-FTS    TO   KD07-NUM-HALF-GRANT.                  
                                                                                
           WRITE BP13KD07-REC.                                                  
                                                                                
           IF WS-KD07-STATUS = 00 OR 02                                         
              ADD 1 TO WS-KD07-WRITE                                            
           ELSE                                                                 
              DISPLAY 'ERROR WRITING BP13KD07 : ' KD07-NUM-REGN                 
                      '  STATUS ' WS-KD07-STATUS                                
              MOVE WS-KD07-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '- BP13CH56 CONTROL TOTAL -----------'.                      
           DISPLAY '1) NO OF REC READ    F000  = ' WS-F000-READ.                
           DISPLAY '2) NO OF REC WRITE   KD07  = ' WS-KD07-WRITE.               
           DISPLAY '3) NO OF REC FND     KD07  = ' WS-KD07-FND.                 
           DISPLAY '4) NO OF REC FND     K200  = ' WS-K200-FND.                 
           DISPLAY '5) NO OF REC SERS-FTS      = ' WS-SERS-FTS.                 
           DISPLAY '6) NO OF REC NOT SERS FTS  = ' WS-NOT-SERS-FTS.             
           DISPLAY '7) NO OF REC NOT FND K200  = ' WS-K200-NFND.                
                                                                                
           CLOSE BP17F000                                                       
                 BP17K200                                                       
                 BP13KD07.                                                      
                                                                                
           IF WS-KD07-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KD07 FILE, ' WS-KD07-STATUS.           
                                                                                
           IF WS-K200-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP17K200 FILE, ' WS-K200-STATUS.           
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
