       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CH54.                                                  
      *AUTHOR.       ZDD1.                                                      
      *DATE-WRITTEN. 12/11/2015.                                                
      * ========================================================== *            
      * FLAT INVENTORY SYSTEM (BM06)                               *            
      * ========================================================== *            
      *  UPDATE BP13.KH50.UPD.FP                                   *            
      *                                                            *            
      *  INPUT   :  BP13FH50                                       *            
      *  OUTPUT  :  BP13KH50                                       *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY     ON          DESCRIPTION                    *            
      * -------- ---    ----------  -----------                    *            
      * BP136307 ZDD1   04/10/2016  CATER FOR REWRITE OF KH50      *            
      * BP136439 MRN1   05/10/2016  ADDED CHECKING FOR NUMERIC     *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FH50 ASSIGN TO BP13FH50.                                  
                                                                                
                                                                                
           SELECT BP13KH50 ASSIGN TO BP13KH50                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KH50-KEY-FLD                               
                  FILE STATUS     IS WS-KH50-STATUS.                            
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13FH50                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 3000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13FH50.                                                           
                                                                                
       FD   BP13KH50                                                            
            RECORD CONTAINS 3000 CHARACTERS.                                    
       COPY BP13KH50.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FH50-EOF                   PIC X    VALUE 'N'.                    
       01  WS-KH50-EOF                   PIC X    VALUE 'N'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FH50-READ              PIC 9(7) VALUE ZEROS.                  
           05  WS-KH50-WRITE             PIC 9(7) VALUE ZEROS.                  
           05  WS-KH50-REWRITE           PIC 9(7) VALUE ZEROS.                  
           05  WS-KH50-READ              PIC 9(7) VALUE ZEROS.                  
           05  WS-KH50-STATUS            PIC X(2) VALUE SPACES.                 
           05  WS-CTR                    PIC 9(2) VALUE ZEROS.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-FH50    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-FH50 THRU 300-EXIT                              
                    UNTIL WS-FH50-EOF = 'Y'.                                    
            PERFORM 900-CLOSE-FILES  THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13FH50                                                 
                I-O    BP13KH50.                                                
                                                                                
           IF WS-KH50-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KH50 FILE, ' WS-KH50-STATUS            
              PERFORM 900-CLOSE-FILES         THRU 900-EXIT.                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-FH50.                                                           
      *-------------------------------------------------------------            
           READ BP13FH50 AT END                                                 
                MOVE 'Y' TO WS-FH50-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1         TO WS-FH50-READ.                                       
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 12                 
              IF FH50-AMT-INCOME-TP(WS-CTR) IS NOT NUMERIC                      
                 MOVE ZEROES TO FH50-AMT-INCOME-TP(WS-CTR)                      
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-FH50.                                                        
      *-------------------------------------------------------------            
                                                                                
           IF FH50-KEY-FLD = SPACES OR LOW-VALUES                               
              PERFORM 200-READ-FH50 THRU 200-EXIT                               
           END-IF.                                                              
                                                                                
           PERFORM 600-READ-BP13KH50   THRU 600-EXIT.                           
           PERFORM 200-READ-FH50       THRU 200-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-READ-BP13KH50.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES               TO KH50-KEY-FLD.                           
           MOVE FH50-NUM-REGN        TO KH50-NUM-REGN.                          
                                                                                
           READ BP13KH50.                                                       
                                                                                
           IF WS-KH50-STATUS = 00                                               
                ADD 1    TO WS-KH50-READ                                        
                PERFORM 800-REWRITE-BP13KH50   THRU 800-EXIT                    
           ELSE                                                                 
              IF WS-KH50-STATUS = 23 OR 10                                      
                 PERFORM 700-WRITE-BP13KH50   THRU 700-EXIT                     
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13KH50 : ' WS-KH50-STATUS             
                 MOVE WS-KH50-STATUS     TO RETURN-CODE                         
                 PERFORM 900-CLOSE-FILES THRU 900-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-WRITE-BP13KH50.                                                      
      *-------------------------------------------------------------            
           MOVE BP13FH50-REC       TO BP13KH50-REC.                             
                                                                                
           WRITE BP13KH50-REC.                                                  
                                                                                
           IF WS-KH50-STATUS = 00 OR 02                                         
              ADD 1 TO WS-KH50-WRITE                                            
           ELSE                                                                 
              DISPLAY 'ERROR WRITING BP13KH50 : ' KH50-NUM-REGN                 
                      '  STATUS ' WS-KH50-STATUS                                
              MOVE WS-KH50-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-REWRITE-BP13KH50.                                                    
      *-------------------------------------------------------------            
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 12                 
              IF KH50-AMT-INCOME-TP(WS-CTR) IS NOT NUMERIC                      
                 MOVE ZEROES TO KH50-AMT-INCOME-TP(WS-CTR)                      
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF KH50-NUM-CDE-MARITAL-STAT-CHG  =  SPACES OR LOW-VALUES            
              MOVE FH50-NUM-CDE-MARITAL-STAT-CHG  TO                            
                             KH50-NUM-CDE-MARITAL-STAT-CHG                      
           END-IF.                                                              
                                                                                
           IF KH50-NUM-CDE-MARITAL-STAT      =  SPACES OR LOW-VALUES            
              MOVE FH50-NUM-CDE-MARITAL-STAT      TO                            
                             KH50-NUM-CDE-MARITAL-STAT                          
           END-IF.                                                              
                                                                                
           IF KH50-NUM-CDE-FAMILY-COMP-CHG   =  SPACES OR LOW-VALUES            
              MOVE FH50-NUM-CDE-FAMILY-COMP-CHG   TO                            
                             KH50-NUM-CDE-FAMILY-COMP-CHG                       
           END-IF.                                                              
                                                                                
           IF KH50-NUM-CDE-FAMILY-COMP       =  SPACES OR LOW-VALUES            
              MOVE FH50-NUM-CDE-FAMILY-COMP       TO                            
                             KH50-NUM-CDE-FAMILY-COMP                           
           END-IF.                                                              
                                                                                
           IF KH50-NUM-CDE-CITI-CHG          =  SPACES OR LOW-VALUES            
              MOVE FH50-NUM-CDE-CITI-CHG          TO                            
                             KH50-NUM-CDE-CITI-CHG                              
           END-IF.                                                              
                                                                                
           IF KH50-NUM-CDE-CITI              =  SPACES OR LOW-VALUES            
              MOVE FH50-NUM-CDE-CITI              TO                            
                             KH50-NUM-CDE-CITI                                  
           END-IF.                                                              
                                                                                
           IF KH50-NUM-INTEREST-PRV-PROP     =  SPACES OR LOW-VALUES            
              MOVE FH50-NUM-INTEREST-PRV-PROP     TO                            
                             KH50-NUM-INTEREST-PRV-PROP                         
           END-IF.                                                              
                                                                                
           IF KH50-NUM-TAG-CHANGE            =  SPACES OR LOW-VALUES            
              MOVE FH50-NUM-TAG-CHANGE            TO                            
                             KH50-NUM-TAG-CHANGE                                
           END-IF.                                                              
                                                                                
           IF KH50-CDE-EMAIL-SENT            =  SPACES OR LOW-VALUES            
              MOVE FH50-CDE-EMAIL-SENT            TO                            
                             KH50-CDE-EMAIL-SENT                                
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(1) = SPACES OR LOW-VALUES                           
              MOVE FH50-NUM-NRIC(1)  TO KH50-NUM-NRIC(1)                        
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(2) = SPACES OR LOW-VALUES                           
              MOVE FH50-NUM-NRIC(2)  TO KH50-NUM-NRIC(2)                        
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(3) = SPACES OR LOW-VALUES                           
              MOVE FH50-NUM-NRIC(3)  TO KH50-NUM-NRIC(3)                        
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(4) = SPACES OR LOW-VALUES                           
              MOVE FH50-NUM-NRIC(4)  TO KH50-NUM-NRIC(4)                        
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(5) = SPACES OR LOW-VALUES                           
              MOVE FH50-NUM-NRIC(5)  TO KH50-NUM-NRIC(5)                        
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(6) = SPACES OR LOW-VALUES                           
              MOVE FH50-NUM-NRIC(6)  TO KH50-NUM-NRIC(6)                        
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(7) = SPACES OR LOW-VALUES                           
              MOVE FH50-NUM-NRIC(7)  TO KH50-NUM-NRIC(7)                        
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(8) = SPACES OR LOW-VALUES                           
              MOVE FH50-NUM-NRIC(8)  TO KH50-NUM-NRIC(8)                        
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(9) = SPACES OR LOW-VALUES                           
              MOVE FH50-NUM-NRIC(9)  TO KH50-NUM-NRIC(9)                        
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(10) = SPACES OR LOW-VALUES                          
              MOVE FH50-NUM-NRIC(10)  TO KH50-NUM-NRIC(10)                      
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(11) = SPACES OR LOW-VALUES                          
              MOVE FH50-NUM-NRIC(11)  TO KH50-NUM-NRIC(11)                      
           END-IF.                                                              
                                                                                
           IF KH50-NUM-NRIC(12) = SPACES OR LOW-VALUES                          
              MOVE FH50-NUM-NRIC(12)  TO KH50-NUM-NRIC(12)                      
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(1) = ZEROES OR SPACES OR LOW-VALUES            
              MOVE FH50-AMT-INCOME-TP(1) TO KH50-AMT-INCOME-TP(1)               
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(2) = ZEROES OR SPACES OR LOW-VALUES            
              MOVE FH50-AMT-INCOME-TP(2) TO KH50-AMT-INCOME-TP(2)               
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(3) = ZEROES OR SPACES OR LOW-VALUES            
              MOVE FH50-AMT-INCOME-TP(3) TO KH50-AMT-INCOME-TP(3)               
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(4) = ZEROES OR SPACES OR LOW-VALUES            
              MOVE FH50-AMT-INCOME-TP(4) TO KH50-AMT-INCOME-TP(4)               
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(5) = ZEROES OR SPACES OR LOW-VALUES            
              MOVE FH50-AMT-INCOME-TP(5) TO KH50-AMT-INCOME-TP(5)               
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(6) = ZEROES OR SPACES OR LOW-VALUES            
              MOVE FH50-AMT-INCOME-TP(6) TO KH50-AMT-INCOME-TP(6)               
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(7) = ZEROES OR SPACES OR LOW-VALUES            
              MOVE FH50-AMT-INCOME-TP(7) TO KH50-AMT-INCOME-TP(7)               
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(8) = ZEROES OR SPACES OR LOW-VALUES            
              MOVE FH50-AMT-INCOME-TP(8) TO KH50-AMT-INCOME-TP(8)               
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(9) = ZEROES OR SPACES OR LOW-VALUES            
              MOVE FH50-AMT-INCOME-TP(9) TO KH50-AMT-INCOME-TP(9)               
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(10) = ZEROES OR SPACES OR LOW-VALUES           
              MOVE FH50-AMT-INCOME-TP(10) TO KH50-AMT-INCOME-TP(10)             
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(11) = ZEROES OR SPACES OR LOW-VALUES           
              MOVE FH50-AMT-INCOME-TP(11) TO KH50-AMT-INCOME-TP(11)             
           END-IF.                                                              
                                                                                
           IF KH50-AMT-INCOME-TP(12) = ZEROES OR SPACES OR LOW-VALUES           
              MOVE FH50-AMT-INCOME-TP(12) TO KH50-AMT-INCOME-TP(12)             
           END-IF.                                                              
                                                                                
           IF KH50-NUM-USER-NRIC-1ST = SPACES OR LOW-VALUES                     
              MOVE FH50-NUM-USER-NRIC-1ST  TO                                   
                            KH50-NUM-USER-NRIC-1ST                              
           END-IF.                                                              
                                                                                
           IF KH50-DTE-REQUESTED-1ST = SPACES OR LOW-VALUES                     
              MOVE FH50-DTE-REQUESTED-1ST  TO                                   
                            KH50-DTE-REQUESTED-1ST                              
           END-IF.                                                              
                                                                                
           IF KH50-TME-REQUESTED-1ST = SPACES OR LOW-VALUES                     
              MOVE FH50-TME-REQUESTED-1ST  TO                                   
                            KH50-TME-REQUESTED-1ST                              
           END-IF.                                                              
                                                                                
           IF KH50-NUM-USER-NRIC    = SPACES OR LOW-VALUES                      
              MOVE FH50-NUM-USER-NRIC     TO                                    
                            KH50-NUM-USER-NRIC                                  
           END-IF.                                                              
                                                                                
           IF KH50-DTE-REQUESTED    = SPACES OR LOW-VALUES                      
              MOVE FH50-DTE-REQUESTED     TO                                    
                            KH50-DTE-REQUESTED                                  
           END-IF.                                                              
                                                                                
           IF KH50-TME-REQUESTED    = SPACES OR LOW-VALUES                      
              MOVE FH50-TME-REQUESTED     TO                                    
                            KH50-TME-REQUESTED                                  
           END-IF.                                                              
                                                                                
           REWRITE BP13KH50-REC.                                                
                                                                                
           IF WS-KH50-STATUS = 00 OR 02                                         
              ADD 1 TO WS-KH50-REWRITE                                          
           ELSE                                                                 
              DISPLAY 'ERROR WRITING BP13KH50 : ' KH50-NUM-REGN                 
                      '  STATUS ' WS-KH50-STATUS                                
              MOVE WS-KH50-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-IF.                                                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '- BP13CH54 CONTROL TOTAL -----------'.                      
           DISPLAY '1) NO OF REC READ    FH50  = ' WS-FH50-READ.                
           DISPLAY '2) NO OF REC READ    KH50  = ' WS-KH50-READ.                
           DISPLAY '3) NO OF REC WRITE   KH50  = ' WS-KH50-WRITE.               
           DISPLAY '4) NO OF REC REWRITE KH50  = ' WS-KH50-REWRITE.             
                                                                                
           CLOSE BP13FH50                                                       
                 BP13KH50.                                                      
                                                                                
           IF WS-KH50-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KH50 FILE, ' WS-KH50-STATUS.           
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
