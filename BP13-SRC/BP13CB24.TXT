       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB24.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  21/10/2016.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  WRITE CASES INTO BP13K828 FROM BP13F825    *            
      *                                                            *            
      * INPUT FILE   :  BP13F825  -  RECORDS TO BE PROCESSED       *            
      * I-O FILE     :  BP13K828  -  BP13.K828.HISTOCCP            *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP136360  SMR2  21102016  NEW PROGRAM                      *            
      * BP136505  SMR2  27122016  HANDLE FILE STATUS '02'          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F825  ASSIGN        TO BP13F825.                          
                                                                                
           SELECT BP13K828  ASSIGN        TO BP13K828                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K828-KEY-FLD                       
                            FILE STATUS   IS WS-K828-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F825                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13F825.                                                           
                                                                                
       FD   BP13K828                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K828.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K828-STATUS           PIC 9(02)  VALUE ZEROES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F825-READ             PIC 9(06)  VALUE ZEROES.                
           05  WS-K828-FND              PIC 9(06)  VALUE ZEROES.                
           05  WS-K828-NTFND            PIC 9(06)  VALUE ZEROES.                
           05  WS-K828-WRITE            PIC 9(06)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F825-EOF              PIC X(01)  VALUE 'N'.                   
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                  PIC X(08)  VALUE SPACES.                
           05  WS-DATE-EDIT             PIC X(10)  VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F825       THRU 2000-EXIT                      
             UNTIL WS-F825-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13F825                                                  
                I-O   BP13K828.                                                 
                                                                                
           IF WS-K828-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K828 ERROR ' WS-K828-STATUS                  
              MOVE WS-K828-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                         
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F825.                                                      
      *-------------------------------------------------------------            
           READ BP13F825                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F825-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F825-READ.                    
           PERFORM 3000-READ-BP13K828       THRU 3000-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K828.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                      TO BP13K828-REC.                    
           INITIALIZE                          BP13K828-REC.                    
                                                                                
           MOVE F825-KEY-FLD                TO K828-KEY-FLD.                    
                                                                                
           READ BP13K828.                                                       
                                                                                
           EVALUATE WS-K828-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K828-FND                      
                                                                                
           WHEN 23                                                              
              ADD 1                         TO WS-K828-NTFND                    
              PERFORM 5000-WRITE-BP13K828   THRU 5000-EXIT                      
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K828   : ' WS-K828-STATUS              
              MOVE WS-K828-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-WRITE-BP13K828.                                                     
      *-------------------------------------------------------------            
           MOVE BP13F825-REC                TO BP13K828-REC.                    
                                                                                
           WRITE BP13K828-REC.                                                  
                                                                                
           EVALUATE WS-K828-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              ADD 1                         TO WS-K828-WRITE                    
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING IN BP13K828. STATUS IS - '                 
                      WS-K828-STATUS                                            
              MOVE WS-K828-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '    BP13CB24 CONTROL TOTAL   : ' WS-DATE-EDIT.              
           DISPLAY '  '.                                                        
           DISPLAY ' NO OF BP13F825 READ         : ' WS-F825-READ.              
           DISPLAY ' NO OF FOUND RECORDS         : ' WS-K828-FND.               
           DISPLAY ' NO OF NOT FOUND RECORDS     : ' WS-K828-NTFND.             
           DISPLAY ' NO OF BP13K828 WRITTEN      : ' WS-K828-WRITE.             
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F825                                                       
                 BP13K828.                                                      
                                                                                
           IF WS-K828-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K828 ERROR ' WS-K828-STATUS                  
              MOVE WS-K828-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
