       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C937.                                                    
       AUTHOR.     SURESH.                                                      
       INSTALLATION. HDB.                                                       
       DATE-WRITTEN. 23-10-2001.                                                
       DATE-COMPILED.                                                           
      *-----------------------------------------------------------*             
      *        SYSTEM OF COMMITMENT - BP13                        *             
      *-----------------------------------------------------------*             
      * OBJECTIVES :                                              *             
      *                                                           *             
      *  TO MATCH RDB RECORD WITH MOBILITY RECORD IF  FOUND IN    *             
      *  MOBILITY WITH MOVE-OUT-DATE, DROP MATCHING F716 CASES    *             
      *                                                           *             
      * INPUT  FILE   : BP13F716 (RDB FILE)                       *             
      * INPUT  FILE   : P13F716A (MOBILITY FILE)                  *             
      * OUTPUT FILE   : P13F716B                                  *             
      *                                                           *             
      * REQ NO.   DATE    BY  DESCRIPTION                         *             
      * -------   ----    --- -----------                         *             
      * BP131046 23102001 SN  NEW PROGRAM                         *             
      * BP131048 20112001 ANC ADD READING OF K940 FILE            *             
      * BP132613 15122004 LMS TO ADD CHECKING OF MOVE-IN DATE     *             
      *-----------------------------------------------------------*             
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K940   ASSIGN       TO BP13K940                           
                  ACCESS MODE             IS  DYNAMIC                           
                  ORGANIZATION            IS  INDEXED                           
                  RECORD KEY              IS  K940-KEY-NEW                      
                  FILE STATUS             IS  BP13K940-STATUS.                  
                                                                                
           SELECT BP13F716   ASSIGN TO BP13F716.                                
                                                                                
           SELECT P13F716A   ASSIGN TO P13F716A.                                
                                                                                
           SELECT P13F716B   ASSIGN TO P13F716B.                                
                                                                                
           SELECT  BP13K943   ASSIGN       TO  BP13K943                         
                   ACCESS MODE             IS  DYNAMIC                          
                   ORGANIZATION            IS  INDEXED                          
                   RECORD KEY              IS  K943-KEY-FLD                     
                   FILE STATUS             IS  BP13K943-STATUS.                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13K940.                                                             
       COPY BP13K940.                                                           
                                                                                
       FD BP13F716.                                                             
       COPY BP13F716.                                                           
                                                                                
       FD BP13K943.                                                             
       COPY BP13K943.                                                           
                                                                                
       FD P13F716A.                                                             
       01 P13F716A-REC.                                                         
          05 F716A-REGN      PIC X(08).                                         
          05 F716A-NRIC      PIC X(09).                                         
          05 FILLER          PIC X(33).                                         
          05 F716A-SCH-ACC   PIC X(11).                                         
          05 FILLER          PIC X(53).                                         
          05 F716A-MOVE-IN   PIC X(08).                                         
          05 F716A-MOVE-OUT  PIC X(08).                                         
          05 FILLER          PIC X(220).                                        
                                                                                
       FD P13F716B.                                                             
       01 P13F716B-REC.                                                         
          05 FILLER       PIC X(350).                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CONTROL-COUNT.                                                    
           05  WS-F716-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-F716A-READ           PIC 9(8)    VALUE ZEROS.                 
           05  WS-K943-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-K940-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-P13F716B-WRITE       PIC 9(8)    VALUE ZEROS.                 
           05  WS-WRITE-BYPASS         PIC 9(8)    VALUE ZEROS.                 
           05  WS-DROP-CASE            PIC 9(8)    VALUE ZEROS.                 
           05  BP13K943-STATUS         PIC 9(2)    VALUE ZEROS.                 
           05  BP13K940-STATUS         PIC 9(2)    VALUE ZEROS.                 
                                                                                
       01  WS-F716-KEY.                                                         
           05 WS-F716-REGN             PIC X(08)   VALUE SPACES.                
           05 WS-F716-NRIC             PIC X(09)   VALUE SPACES.                
           05 WS-F716-SCH-ACC          PIC X(11)   VALUE SPACES.                
                                                                                
       01  WS-F716A-KEY.                                                        
           05 WS-F716A-REGN            PIC X(08)   VALUE SPACES.                
           05 WS-F716A-NRIC            PIC X(09)   VALUE SPACES.                
           05 WS-F716A-SCH-ACC         PIC X(11)   VALUE SPACES.                
                                                                                
       01  WS-SYS-DATE                 PIC 9(8)    VALUE ZEROES.                
       01  WS-PRT-DATE                 PIC X(10)   VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------*                 
       0000-MAIN.                                                               
      *-------------------------------------------------------*                 
            PERFORM 0500-OPEN-FILES       THRU 0500-EXIT.                       
            PERFORM 1000-READ-BP13F716    THRU 1000-EXIT.                       
            PERFORM 2000-READ-P13F716A    THRU 2000-EXIT.                       
            PERFORM 3000-PROCESS          THRU 3000-EXIT                        
                     UNTIL WS-F716-KEY  = HIGH-VALUES AND                       
                           WS-F716A-KEY = HIGH-VALUES.                          
            PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                       
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       0500-OPEN-FILES.                                                         
      *-------------------------------------------------------*                 
            OPEN INPUT  BP13F716                                                
                        P13F716A                                                
                        BP13K943                                                
                        BP13K940                                                
                 OUTPUT P13F716B.                                               
                                                                                
              IF BP13K943-STATUS NOT = 0 AND 97                                 
                 DISPLAY 'OPENING ERROR, BP13K943-STATUS '                      
                                         BP13K943-STATUS                        
                 MOVE BP13K943-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-FILES                                       
              END-IF.                                                           
                                                                                
              IF BP13K940-STATUS NOT = 0 AND 97                                 
                 DISPLAY 'OPENING ERROR, BP13K940-STATUS '                      
                                         BP13K940-STATUS                        
                 MOVE BP13K940-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-FILES                                       
              END-IF.                                                           
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-SYS-DATE.                   
                                                                                
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       1000-READ-BP13F716.                                                      
      *-------------------------------------------------------*                 
            READ BP13F716 AT END                                                
                 MOVE HIGH-VALUES TO WS-F716-KEY                                
                 GO TO 1000-EXIT.                                               
            MOVE F716-NUM-REGN    TO  WS-F716-REGN                              
            MOVE F716-NUM-NRIC    TO  WS-F716-NRIC                              
            MOVE F716-NUM-HDB-REF TO  WS-F716-SCH-ACC                           
            ADD 1                 TO WS-F716-READ.                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       2000-READ-P13F716A.                                                      
      *-------------------------------------------------------*                 
            READ P13F716A AT END                                                
                 MOVE HIGH-VALUES TO WS-F716A-KEY                               
                 GO TO 2000-EXIT.                                               
            MOVE F716A-REGN     TO  WS-F716A-REGN                               
            MOVE F716A-NRIC     TO  WS-F716A-NRIC                               
            MOVE F716A-SCH-ACC  TO  WS-F716A-SCH-ACC                            
                                                                                
            ADD 1                        TO WS-F716A-READ.                      
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       2500-READ-BP13K943.                                                      
      *-------------------------------------------------------*                 
            READ BP13K943 KEY IS K943-KEY-FLD                                   
                                                                                
            ADD 1                        TO WS-K943-READ.                       
                                                                                
       2500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       3000-PROCESS.                                                            
      *-------------------------------------------------------*                 
                                                                                
            MOVE F716-NUM-REGN         TO K940-KEY-NEW.                         
            PERFORM 3500-READ-BP13K940 THRU 3500-EXIT.                          
                                                                                
            IF WS-F716-KEY = WS-F716A-KEY                                       
               IF F716A-MOVE-OUT = SPACES                                       
                  PERFORM 3100-CHECK-MOVE-IN   THRU 3100-EXIT                   
               ELSE                                                             
                  ADD 1 TO WS-DROP-CASE                                         
                  DISPLAY 'DROP-CASES ' WS-F716-KEY                             
                          ' MOVE-OUT= ' F716A-MOVE-OUT                          
               END-IF                                                           
               PERFORM 1000-READ-BP13F716        THRU 1000-EXIT                 
               PERFORM 2000-READ-P13F716A        THRU 2000-EXIT                 
            ELSE                                                                
                 IF WS-F716-KEY > WS-F716A-KEY                                  
                    PERFORM 2000-READ-P13F716A     THRU 2000-EXIT               
                 ELSE                                                           
                    IF WS-F716-KEY < WS-F716A-KEY                               
                       PERFORM 4200-WRITE-P13F716B  THRU 4200-EXIT              
                       PERFORM 1000-READ-BP13F716  THRU 1000-EXIT               
                    END-IF                                                      
                 END-IF                                                         
            END-IF.                                                             
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       3100-CHECK-MOVE-IN.                                                      
      *-------------------------------------------------------*                 
                                                                                
            IF K940-DTE-IND-TOP >= F716A-MOVE-IN                                
               PERFORM 4200-WRITE-P13F716B  THRU 4200-EXIT                      
            ELSE                                                                
               ADD 1 TO WS-DROP-CASE                                            
               DISPLAY 'DROP-CASES ' WS-F716-KEY                                
                       ' MOVE-OUT= ' F716A-MOVE-OUT                             
            END-IF.                                                             
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       3500-READ-BP13K940.                                                      
      *-------------------------------------------------------*                 
            READ BP13K940 KEY IS K940-KEY-NEW                                   
                                                                                
            ADD 1                        TO WS-K940-READ.                       
                                                                                
            IF BP13K940-STATUS = 0                                              
               CONTINUE                                                         
            ELSE                                                                
               IF BP13K940-STATUS = 23                                          
                  DISPLAY 'RECORD NOT FOUND' BP13K940-STATUS                    
               ELSE                                                             
                  DISPLAY 'READ ERROR, BP13K940-STATUS = '                      
                                         BP13K940-STATUS                        
                  DISPLAY 'K940-KEY-NEW = ' K940-KEY-NEW                        
                  MOVE BP13K940-STATUS   TO RETURN-CODE                         
               END-IF                                                           
            END-IF.                                                             
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       4000-PROCESS-RELN.                                                       
      *-------------------------------------------------------*                 
            IF K943-NUM-NRIC = K940-NUM-NRIC1                                   
               MOVE 'H1' TO F716-NUM-RELATION-APPLN                             
            ELSE                                                                
               IF K943-NUM-NRIC = K940-NUM-NRIC2                                
                  MOVE 'H2' TO F716-NUM-RELATION-APPLN                          
               ELSE                                                             
                  IF K943-NUM-NRIC = K940-NUM-NRIC3                             
                     MOVE 'H3' TO F716-NUM-RELATION-APPLN                       
                  ELSE                                                          
                     IF K943-NUM-NRIC = K940-NUM-NRIC4                          
                        MOVE 'H4' TO F716-NUM-RELATION-APPLN                    
                     ELSE                                                       
                        MOVE 'OC' TO F716-NUM-RELATION-APPLN                    
                     END-IF                                                     
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       4200-WRITE-P13F716B.                                                     
      *-------------------------------------------------------*                 
                                                                                
              MOVE F716-NUM-REGN           TO K943-NUM-REGN.                    
              MOVE F716-NUM-NRIC           TO K943-NUM-NRIC.                    
              PERFORM 2500-READ-BP13K943 THRU 2500-EXIT.                        
              IF BP13K943-STATUS = 0                                            
                 IF K943-NUM-REGN = K940-KEY-NEW                                
                    PERFORM 4000-PROCESS-RELN THRU 4000-EXIT                    
                 END-IF                                                         
                 MOVE BP13F716-REC          TO P13F716B-REC                     
                 WRITE P13F716B-REC                                             
                 ADD 1                      TO WS-P13F716B-WRITE                
              ELSE                                                              
                 IF BP13K943-STATUS = 23                                        
                    ADD 1                  TO WS-WRITE-BYPASS                   
                    DISPLAY 'RECORD NOT FOUND' BP13K943-STATUS                  
                 ELSE                                                           
                    DISPLAY 'READ ERROR, BP13K943-STATUS = '                    
                                         BP13K943-STATUS                        
                    DISPLAY 'K943-KEY-FLD = ' K943-KEY-FLD                      
                    MOVE BP13K943-STATUS   TO RETURN-CODE                       
                 END-IF                                                         
              END-IF.                                                           
                                                                                
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------*                 
            STRING WS-SYS-DATE (7:2), '/',                                      
                   WS-SYS-DATE (5:2), '/',                                      
                   WS-SYS-DATE (1:4)                                            
                   DELIMITED BY SIZE                                            
                   INTO                                                         
                   WS-PRT-DATE.                                                 
                                                                                
            DISPLAY '   '.                                                      
            DISPLAY 'SUMMARY REPORT   BP13C937  RUNDATE ' WS-PRT-DATE.          
            DISPLAY '______________________________________________'.           
            DISPLAY '   '.                                                      
            DISPLAY 'NO OF RECORDS READ FROM BP13F716   : '                     
                                            WS-F716-READ                        
            DISPLAY 'NO OF RECORDS READ FROM P13F716A   : '                     
                                            WS-F716A-READ                       
            DISPLAY 'NO OF RECORDS READ FROM BP13K940   : '                     
                                            WS-K940-READ                        
            DISPLAY 'NO OF RECORDS WRITTEN ON TEMP      : '                     
                                            WS-P13F716B-WRITE                   
            DISPLAY 'NO OF RECORDS BYPASSED             : '                     
                                            WS-WRITE-BYPASS                     
            DISPLAY 'NO OF RECORDS DROP WITH MOVE-OUT-DATE : '                  
                                            WS-DROP-CASE                        
                                                                                
            CLOSE BP13F716                                                      
                  P13F716A                                                      
                  P13F716B                                                      
                  BP13K940                                                      
                  BP13K943.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
