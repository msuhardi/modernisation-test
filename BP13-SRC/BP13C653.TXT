       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C653.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
       DATE-WRITTEN.  08/04/92.                                                 
       DATE-COMPILED.                                                           
      * ======================================================== *              
      *               SYSTEM OF COMMITMENT  (BP13)               *              
      * ======================================================== *              
      *    OBJECTIVE :                                           *              
      *      THE PURPOSE OF THIS PROGRAM IS TO DELETE RECORDS    *              
      *      FROM RFF MASTER ONCE THE AMOUNT IS REFUNDED AND     *              
      *      THERE IS NO SOC-DEBTOR AND FILE SENT.               *              
      *                                                          *              
      *    INPUT FILE :                                          *              
      *      1.  BP13F125                                        *              
      *                                                          *              
      *    I-O FILE :                                            *              
      *      1.  BP13K800                                        *              
      *      2.  BP13K410                                        *              
      *      3.  BP13K893                                        *              
      *      4.  BP13K895                                        *              
      *    OUTPUT FILE :                                         *              
      *      1.  BP13L653 - ERROR LISTING                        *              
      *                                                          *              
      * ======================================================== *              
      * CHG-NO   DATE   BY   DESCRIPTION                         *              
      * -------- ------ ---  -----------                         *              
      * C920790  920722 LJL  TO UPDATE THE DATE OF BOOKING       *              
      *                      FEES REFUND TO ACCUM SO FILE FOR    *              
      *                      USER TO ENQUIRE.                    *              
      * I920718  930716 RML  CONVERT TO COBOL II.                *              
      * SOCPH8B  930716 RML  EXPAND REGN,NRIC, DEL SOC OCCP      *              
      * BP130037 950922 LJL  TO CATER FOR AA CASES.              *              
      * BP130020 951012 SSS  REPOSITION L653-HEAD1 TO ROW 1      *              
      * BP130418 980325 SIA  CATER FOR Y2000                     *              
      * BP132177 020716 MCC  INCLUDE CHECK OF REGN-DEP CASH+CPF  *              
      * BP132199 160802 ANC1 MOVE SYSTEM DATE TO DTE UPDATE IN   *              
      *                      K800 & K820 BEFORE WRITING.         *              
      * BP132627 121104 GAN1 FROM F800/F820 TO K893/K895         *              
      * BP132717 080705 JF4  CATER FOR CD CASH REFUND            *              
      * ======================================================== *              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F125 ASSIGN TO BP13F125.                                  
                                                                                
           SELECT BP13K893 ASSIGN TO BP13K893                                   
                  ORGANIZATION       INDEXED                                    
                  ACCESS   MODE      DYNAMIC                                    
                  RECORD   KEY       K893-KEY-FLD                               
                  FILE     STATUS    WS-K893-STATUS.                            
                                                                                
           SELECT BP13K895 ASSIGN TO BP13K895                                   
                  ORGANIZATION       INDEXED                                    
                  ACCESS   MODE      DYNAMIC                                    
                  RECORD   KEY       K895-KEY-FLD                               
                  FILE     STATUS    WS-K895-STATUS.                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION       INDEXED                                    
                  ACCESS   MODE      RANDOM                                     
                  RECORD   KEY       K800-NUM-REGN                              
                  FILE     STATUS    WS-K800-STATUS.                            
                                                                                
           SELECT BP13K840 ASSIGN TO BP13K840                                   
                  ORGANIZATION       INDEXED                                    
                  ACCESS   MODE      DYNAMIC                                    
                  RECORD   KEY       K840-KEY-FLD                               
                  FILE     STATUS    WS-K840-STATUS.                            
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION       INDEXED                                    
                  ACCESS   MODE      DYNAMIC                                    
                  RECORD   KEY       K820-KEY-FLD                               
                  FILE     STATUS    WS-K820-STATUS.                            
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ORGANIZATION       INDEXED                                    
                  ACCESS   MODE      DYNAMIC                                    
                  RECORD   KEY       K410-KEY-FLD                               
                  FILE     STATUS    WS-K410-STATUS.                            
                                                                                
           SELECT BP13L653 ASSIGN TO BP13L653.                                  
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F125                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F125.                                                           
                                                                                
       FD   BP13K893                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2050 CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K895                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS  450 CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K895.                                                           
                                                                                
       FD   BP13K800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD IS OMITTED.                                            
       COPY BP13K410.                                                           
                                                                                
       FD   BP13K820                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORD IS OMITTED.                                            
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K840                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS OMITTED.                                            
       COPY BP13K840.                                                           
                                                                                
       FD   BP13L653                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED.                                            
                                                                                
       01   PRINT-L653              PIC X(132).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-ACCESS-STATUS.                                               
           05  WS-K800-STATUS          PIC X(2)    VALUE SPACES.                
           05  WS-K893-STATUS          PIC X(2)    VALUE SPACES.                
           05  WS-K410-STATUS          PIC X(2)    VALUE SPACES.                
           05  WS-K820-STATUS          PIC X(2)    VALUE SPACES.                
           05  WS-K895-STATUS          PIC X(2)    VALUE SPACES.                
           05  WS-K840-STATUS          PIC X(2)    VALUE SPACES.                
                                                                                
       01  WS-NRIC1                    PIC X(9)    VALUE SPACES.                
       01  WS-NRIC2                    PIC X(9)    VALUE SPACES.                
       01  WS-NRIC3                    PIC X(9)    VALUE SPACES.                
       01  WS-NRIC4                    PIC X(9)    VALUE SPACES.                
                                                                                
       01  WS-NAME1                    PIC X(22)   VALUE SPACES.                
       01  WS-NAME2                    PIC X(22)   VALUE SPACES.                
       01  WS-NAME3                    PIC X(22)   VALUE SPACES.                
       01  WS-NAME4                    PIC X(22)   VALUE SPACES.                
                                                                                
       01  WS-F125-EOF                 PIC X       VALUE 'N'.                   
       01  WS-K820-EOF                 PIC X       VALUE 'N'.                   
                                                                                
       01  WS-SOC-AMOUNT               PIC S9(8)V99 VALUE +0.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-BP13K800-DELETED      PIC 9(5)  VALUE ZEROES.                  
           05 WS-BP13K820-DELETED      PIC 9(5)  VALUE ZEROES.                  
           05 WS-BP13K840-REWRITE      PIC 9(5)  VALUE ZEROES.                  
           05 WS-K410-REWRITE          PIC 9(5)  VALUE ZEROES.                  
           05 WS-BP13F125-READ         PIC 9(5)  VALUE ZEROES.                  
           05 WS-SNO                   PIC 9(5)  VALUE ZEROES.                  
           05 WS-PAGE                  PIC 9(5)  VALUE ZEROES.                  
           05 WS-LINE                  PIC 9(5)  VALUE 60.                      
           05 WS-1ST-K820-READ         PIC 9(5)  VALUE ZEROES.                  
                                                                                
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-YY          PIC 9(4)      VALUE ZEROES.                  
           05  WS-CURR-MM          PIC 9(2)      VALUE ZEROES.                  
           05  WS-CURR-DD          PIC 9(2)      VALUE ZEROES.                  
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD        PIC 9(2)      VALUE ZEROES.                  
           05  FILLER              PIC X         VALUE '/'.                     
           05  WS-EDITED-MM        PIC 9(2)      VALUE ZEROES.                  
           05  FILLER              PIC X         VALUE '/'.                     
           05  WS-EDITED-YY        PIC 9(4)      VALUE ZEROES.                  
                                                                                
       01  L653-HEAD1.                                                          
           05  FILLER              PIC X(8)     VALUE 'BP13L653'.               
           05  FILLER              PIC X(5)     VALUE SPACES .                  
           05  FILLER              PIC X(4)     VALUE 'HDB3'.                   
           05  FILLER              PIC X(22)    VALUE SPACES.                   
           05  FILLER              PIC X(61)    VALUE                           
              'S Y S T E M   O F   C O M M I T M E N T'.                        
           05  FILLER              PIC X(7)      VALUE 'DATE : '.               
           05  L653-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L653-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L653-HEAD2.                                                          
           05  FILLER              PIC X(38)     VALUE SPACES.                  
           05  FILLER              PIC X(56)     VALUE                          
           'ERROR REPORT ON DELETION OF RECORDS FROM RFF MASTER FILE'.          
                                                                                
       01  L653-HEAD3.                                                          
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE ' S/N'.                    
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'REGN-NO'.                 
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'REMARKS'.                 
                                                                                
       01  L653-DETAILS.                                                        
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  L653-SNO            PIC ZZZZ9.                                   
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  L653-REGNO          PIC X(08).                                   
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  L653-REMARK         PIC X(30) VALUE SPACES.                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *******************                                                       
       000-MAIN-ROUTINE.                                                        
      *******************                                                       
           PERFORM 100-OPEN-FILES      THRU 100-OPEN-EXIT.                      
           PERFORM 200-READ-BP13F125   THRU 200-F125-EXIT.                      
           PERFORM 320-READ-BP13K410   THRU 320-EXIT                            
                   UNTIL WS-F125-EOF = 'Y'.                                     
           PERFORM 999-CLOSE-ROUTINE   THRU 999-CLOSE-EXIT.                     
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
                                                                                
           OPEN INPUT BP13F125                                                  
                  I-O BP13K820                                                  
                      BP13K410                                                  
                      BP13K800                                                  
                      BP13K840                                                  
                      BP13K893                                                  
                      BP13K895                                                  
               OUTPUT BP13L653.                                                 
                                                                                
           IF WS-K800-STATUS  NOT = '00' AND '97'                               
              DISPLAY 'OPEN ERROR FOR BP13K800'  WS-K800-STATUS                 
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              GO TO 999-CLOSE-ROUTINE.                                          
                                                                                
           IF WS-K893-STATUS  NOT = '00' AND '97'                               
              DISPLAY 'OPEN ERROR FOR BP13K893'  WS-K893-STATUS                 
              MOVE WS-K893-STATUS           TO RETURN-CODE                      
              GO TO 999-CLOSE-ROUTINE.                                          
                                                                                
           IF WS-K410-STATUS  NOT = '00' AND '97'                               
              DISPLAY 'OPEN ERROR FOR BP13K410' WS-K410-STATUS                  
              MOVE WS-K410-STATUS           TO RETURN-CODE                      
              GO TO 999-CLOSE-ROUTINE.                                          
                                                                                
           IF WS-K840-STATUS NOT = '00' AND '97'                                
              DISPLAY 'OPEN ERROR FOR BP13K840'  WS-K840-STATUS                 
              MOVE WS-K840-STATUS          TO RETURN-CODE                       
              GO TO 999-CLOSE-ROUTINE.                                          
                                                                                
           IF WS-K820-STATUS NOT = '00' AND '97'                                
              DISPLAY 'OPEN ERROR FOR BP13K820'  WS-K820-STATUS                 
              MOVE WS-K820-STATUS          TO RETURN-CODE                       
              GO TO 999-CLOSE-ROUTINE.                                          
                                                                                
           IF WS-K895-STATUS NOT = '00' AND '97'                                
              DISPLAY 'OPEN ERROR FOR BP13K895'  WS-K895-STATUS                 
              MOVE WS-K895-STATUS          TO RETURN-CODE                       
              GO TO 999-CLOSE-ROUTINE.                                          
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
       100-OPEN-EXIT.                                                           
           EXIT.                                                                
                                                                                
       200-READ-BP13F125.                                                       
                                                                                
           READ BP13F125 AT END                                                 
           MOVE 'Y'                 TO WS-F125-EOF                              
           GO TO 200-F125-EXIT.                                                 
           ADD 1                    TO WS-BP13F125-READ.                        
                                                                                
       200-F125-EXIT.                                                           
           EXIT.                                                                
                                                                                
       320-READ-BP13K410.                                                       
                                                                                
              IF F125-NUM-REGN    = SPACES OR LOW-VALUES                        
                  PERFORM 200-READ-BP13F125   THRU 200-F125-EXIT                
                  GO TO 320-EXIT.                                               
                                                                                
              IF F125-CDE-PAYMENT-TYPE = 'CD'                                   
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE F125-NUM-REGN       TO K410-KEY-FLD                       
                 READ BP13K410                                                  
                                                                                
                 IF WS-K410-STATUS = '00'                                       
                    PERFORM 450-UPDATE-BP13K410 THRU 450-EXIT                   
                 ELSE                                                           
                    DISPLAY 'K410 NOT FND' F125-NUM-REGN                        
                    PERFORM 200-READ-BP13F125   THRU 200-F125-EXIT              
                    GO TO 320-EXIT                                              
              END-IF.                                                           
                                                                                
              MOVE SPACES              TO K800-NUM-REGN.                        
              MOVE F125-NUM-REGN       TO K800-NUM-REGN.                        
              READ BP13K800.                                                    
                                                                                
              IF WS-K800-STATUS = '00'                                          
                 PERFORM 330-CHECK-NUMERIC     THRU 330-EXIT                    
                 MOVE ZEROES             TO  WS-SOC-AMOUNT                      
                                                                                
                 COMPUTE WS-SOC-AMOUNT =   K800-AMT-SOC-CASH                    
                                         + K800-AMT-CPF1-PAID                   
                                         + K800-AMT-CPF2-PAID                   
                                         + K800-AMT-CPF3-PAID                   
                                         + K800-AMT-CPF4-PAID                   
                                                                                
                                                                                
                 IF K800-AMT-SOC-DEBTOR = ZEROS        AND                      
                    K800-AMT-CD         = ZEROS        AND                      
                    K800-AMT-BK-PAID    = ZEROS        AND                      
                    K800-AMT-REGN-DEPOSIT-CASH = ZEROS AND                      
                    K800-AMT-REGN-DEPOSIT-CPF1 = ZEROS AND                      
                    K800-AMT-REGN-DEPOSIT-CPF2 = ZEROS AND                      
                    K800-AMT-REGN-DEPOSIT-CPF3 = ZEROS AND                      
                    K800-AMT-REGN-DEPOSIT-CPF4 = ZEROS AND                      
                    WS-SOC-AMOUNT       = ZEROS                                 
                    PERFORM 500-DELETE-REC   THRU 500-EXIT                      
                 ELSE                                                           
                    MOVE 'REFUND OR SOC AMOUNT NOT ZEROS' TO                    
                                                          L653-REMARK           
                    PERFORM 600-ERROR-LISTING THRU 600-ERROR-EXIT               
                 END-IF                                                         
              ELSE                                                              
                 IF WS-K800-STATUS = '23'                                       
                    MOVE 'RECORD NOT FOUND IN BP13K800  ' TO                    
                                                          L653-REMARK           
                    PERFORM 600-ERROR-LISTING THRU 600-ERROR-EXIT               
                 ELSE                                                           
                    NEXT SENTENCE.                                              
                                                                                
              PERFORM 200-READ-BP13F125   THRU 200-F125-EXIT.                   
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       330-CHECK-NUMERIC.                                                       
      *--------------------*                                                    
           IF K800-AMT-SOC-CASH IS NOT NUMERIC                                  
              MOVE ZEROES           TO K800-AMT-SOC-CASH                        
           END-IF.                                                              
           IF K800-AMT-CPF1-PAID IS NOT NUMERIC                                 
              MOVE ZEROES           TO K800-AMT-CPF1-PAID                       
           END-IF.                                                              
           IF K800-AMT-CPF2-PAID IS NOT NUMERIC                                 
              MOVE ZEROES           TO K800-AMT-CPF2-PAID                       
           END-IF.                                                              
           IF K800-AMT-CPF3-PAID IS NOT NUMERIC                                 
              MOVE ZEROES           TO K800-AMT-CPF3-PAID                       
           END-IF.                                                              
           IF K800-AMT-CPF4-PAID IS NOT NUMERIC                                 
              MOVE ZEROES           TO K800-AMT-CPF4-PAID                       
           END-IF.                                                              
                                                                                
       330-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-UPDATE-BP13K410.                                                     
                                                                                
      *    READ BP13K410.                                                       
      *    IF WS-K410-STATUS = '00'                                             
              IF K410-CDE-SA-TP = 'Y'                                           
                    PERFORM 455-REWRITE-BP13K410 THRU 455-EXIT                  
              ELSE                                                              
              IF K410-NUM-BUYBACK = 'Y' OR 'N'                                  
                    PERFORM 455-REWRITE-BP13K410 THRU 455-EXIT                  
                    DISPLAY 'REGN NOT SA-TP '  F125-NUM-REGN.                   
      *             MOVE 'NOT A SA-TP CASE '              TO                    
      *                                                   L653-REMARK           
      *             PERFORM 600-ERROR-LISTING THRU 600-ERROR-EXIT.              
      *    ELSE                                                                 
      *        MOVE WS-K410-STATUS           TO RETURN-CODE                     
      *        GO TO 999-CLOSE-ROUTINE.                                         
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       455-REWRITE-BP13K410.                                                    
                                                                                
           MOVE F125-DTE-POST TO K410-DTE-BK-FEE-REFUND.                        
           REWRITE  BP13K410-REC.                                               
           IF WS-K410-STATUS = '00'                                             
              ADD 1 TO WS-K410-REWRITE                                          
           ELSE                                                                 
              MOVE WS-K410-STATUS           TO RETURN-CODE                      
              GO TO 999-CLOSE-ROUTINE.                                          
                                                                                
       455-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-DELETE-REC.                                                          
                                                                                
           MOVE K800-NUM-NRIC1    TO WS-NRIC1.                                  
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC2 TO WS-NRIC2.                                  
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC3 TO WS-NRIC3.                                  
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC4 TO WS-NRIC4.                                  
                                                                                
           MOVE  WS-CURR-DATE    TO   K800-DTE-UPDATE.                          
           MOVE  BP13K800-MASTER TO   BP13K893-MASTER.                          
           MOVE  K800-NUM-REGN   TO   K893-NUM-REGN-HIST.                       
           MOVE  WS-CURR-DATE    TO   K893-DTE-CREATE-HIST.                     
           ACCEPT K893-TME-CREATE-HIST FROM TIME.                               
           WRITE BP13K893-MASTER.                                               
                                                                                
           IF WS-K893-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'WRITE ERROR, K893-STATUS = ' WS-K893-STATUS              
                      ' K893-KEY = ' K893-KEY-FLD                               
              MOVE WS-K893-STATUS TO RETURN-CODE                                
              GO TO 999-CLOSE-ROUTINE.                                          
                                                                                
           DELETE BP13K800.                                                     
                                                                                
           IF WS-K800-STATUS  = 00 OR 02                                        
              ADD 1 TO WS-BP13K800-DELETED                                      
              PERFORM 510-START-BP13K820 THRU 510-EXIT                          
              PERFORM 520-UPDATE-K840 THRU 520-EXIT                             
           ELSE                                                                 
              DISPLAY 'DELETE ERROR BP13K800 '  WS-K800-STATUS                  
              DISPLAY 'K800-NUM-REGN '  K800-NUM-REGN.                          
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       510-START-BP13K820.                                                      
                                                                                
           MOVE SPACES            TO WS-K820-EOF.                               
           MOVE SPACES            TO K820-KEY-FLD.                              
           MOVE F125-NUM-REGN     TO K820-NUM-REGN.                             
           START BP13K820 KEY IS NOT LESS THAN  K820-KEY-FLD.                   
                                                                                
           IF WS-K820-STATUS = 00                                               
              PERFORM 550-READNEXT-K820 THRU 550-EXIT                           
              PERFORM 560-DELETE-OCCUPIER THRU 560-EXIT                         
                   UNTIL K820-NUM-REGN NOT =  F125-NUM-REGN                     
                   OR WS-K820-EOF  = 'Y'                                        
           ELSE                                                                 
               DISPLAY 'ERROR START BP13K820'  WS-K820-STATUS.                  
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
      * UPDATE BP13K840 *                                                       
      *-----------------*                                                       
       520-UPDATE-K840.                                                         
                                                                                
           MOVE F125-NUM-REGN     TO K840-NUM-REGN.                             
           READ BP13K840                                                        
           IF WS-K840-STATUS = 00                                               
              MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE                        
              MOVE WS-CURR-DATE     TO K840-DTE-CLOSE                           
              MOVE WS-NRIC1         TO K840-NUM-NRIC1                           
              MOVE WS-NAME1         TO K840-NME-APPL1                           
              MOVE WS-NRIC2         TO K840-NUM-NRIC2                           
              MOVE WS-NAME2         TO K840-NME-APPL2                           
              MOVE WS-NRIC3         TO K840-NUM-NRIC3                           
              MOVE WS-NAME3         TO K840-NME-APPL3                           
              MOVE WS-NRIC4         TO K840-NUM-NRIC4                           
              MOVE WS-NAME4         TO K840-NME-APPL4                           
              REWRITE BP13K840-REC                                              
              ADD 1 TO WS-BP13K840-REWRITE.                                     
                                                                                
           IF WS-K840-STATUS = 23                                               
              DISPLAY 'BP13K840 RECORD NOT FOUND'  WS-K840-STATUS               
              DISPLAY 'K840-NUM-REGN'    K840-NUM-REGN.                         
                                                                                
           IF WS-K840-STATUS NOT = 00 AND 23                                    
              DISPLAY 'K840-NUM-REGN'    K840-NUM-REGN                          
              DISPLAY 'ERROR READING BP13K840'   WS-K840-STATUS.                
      *       GO TO 999-CLOSE-ROUTINE.                                          
                                                                                
           MOVE SPACES           TO WS-NRIC1 WS-NRIC2 WS-NRIC3                  
                                    WS-NRIC4 WS-NAME1 WS-NAME2                  
                                    WS-NAME3 WS-NAME4.                          
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
       550-READNEXT-K820.                                                       
                                                                                
           READ BP13K820 NEXT AT END                                            
                  MOVE 'Y' TO WS-K820-EOF.                                      
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
       560-DELETE-OCCUPIER.                                                     
                                                                                
                                                                                
           IF K820-NUM-REGN = F125-NUM-REGN                                     
              PERFORM 570-GET-NRIC-NAME   THRU 570-EXIT                         
              MOVE  WS-CURR-DATE   TO   K820-DTE-UPDATE                         
              MOVE  BP13K820-REC   TO   BP13K895-REC                            
              MOVE  K820-NUM-REGN  TO   K895-NUM-REGN-HIST                      
              MOVE  K820-NUM-NRIC  TO   K895-NUM-NRIC-HIST                      
              MOVE  WS-CURR-DATE   TO   K895-DTE-CREATE-HIST                    
              ACCEPT K895-TME-CREATE-HIST FROM TIME                             
              WRITE BP13K895-REC                                                
                                                                                
              IF WS-K895-STATUS = 00 OR 02                                      
                 CONTINUE                                                       
              ELSE                                                              
                 DISPLAY 'WRITE ERROR, K895-STATUS = ' WS-K895-STATUS           
                         ' K895-KEY = ' K895-KEY-FLD                            
                 MOVE WS-K820-STATUS TO RETURN-CODE                             
                 GO TO 999-CLOSE-ROUTINE.                                       
                                                                                
              DELETE BP13K820.                                                  
                                                                                
              IF WS-K820-STATUS = 00 OR 02                                      
                 ADD 1 TO WS-BP13K820-DELETED                                   
              ELSE                                                              
                 DISPLAY 'ERROR DELETING BP13K820'  WS-K820-STATUS              
                 MOVE WS-K820-STATUS TO RETURN-CODE                             
                 GO TO 999-CLOSE-ROUTINE.                                       
                                                                                
              PERFORM 550-READNEXT-K820 THRU 550-EXIT.                          
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
       570-GET-NRIC-NAME.                                                       
                                                                                
           IF K820-NUM-NRIC = WS-NRIC1                                          
              MOVE K820-NME-OCCP     TO WS-NAME1                                
           ELSE                                                                 
           IF (WS-NRIC2 NOT = SPACES AND LOW-VALUES) AND                        
              (K820-NUM-NRIC = WS-NRIC2)                                        
                 MOVE K820-NME-OCCP  TO WS-NAME2                                
           ELSE                                                                 
           IF (WS-NRIC3 NOT = SPACES AND LOW-VALUES) AND                        
              (K820-NUM-NRIC = WS-NRIC3)                                        
                 MOVE K820-NME-OCCP  TO WS-NAME3                                
           ELSE                                                                 
           IF (WS-NRIC4 NOT = SPACES AND LOW-VALUES) AND                        
              (K820-NUM-NRIC = WS-NRIC4)                                        
                 MOVE K820-NME-OCCP  TO WS-NAME4.                               
                                                                                
       570-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      * PRINT ERROR LISTING                                                     
                                                                                
       600-ERROR-LISTING.                                                       
                                                                                
           IF WS-LINE > 55                                                      
              PERFORM 700-ERROR-HEADING THRU 700-ERRHEAD-EXIT.                  
                                                                                
           ADD   1                   TO WS-SNO WS-LINE.                         
           MOVE  WS-SNO              TO L653-SNO.                               
           MOVE  F125-NUM-REGN       TO L653-REGNO.                             
           WRITE PRINT-L653          FROM L653-DETAILS.                         
           MOVE SPACES               TO L653-DETAILS.                           
                                                                                
       600-ERROR-EXIT.                                                          
           EXIT.                                                                
                                                                                
       700-ERROR-HEADING.                                                       
                                                                                
            ADD  1                 TO WS-PAGE.                                  
            MOVE WS-PAGE           TO L653-PAGENO.                              
            MOVE WS-CURR-YY        TO WS-EDITED-YY.                             
            MOVE WS-CURR-MM        TO WS-EDITED-MM.                             
            MOVE WS-CURR-DD        TO WS-EDITED-DD.                             
            MOVE WS-EDITED-DATE    TO L653-DATE.                                
            MOVE SPACES            TO PRINT-L653.                               
            WRITE PRINT-L653 FROM L653-HEAD1 AFTER PAGE.                        
            WRITE PRINT-L653 FROM L653-HEAD2 AFTER 2.                           
            WRITE PRINT-L653 FROM L653-HEAD3 AFTER 2.                           
            MOVE SPACES            TO PRINT-L653.                               
            WRITE PRINT-L653 AFTER 2.                                           
            MOVE  7                TO WS-LINE.                                  
                                                                                
       700-ERRHEAD-EXIT.                                                        
           EXIT.                                                                
                                                                                
       999-CLOSE-ROUTINE.                                                       
                                                                                
           DISPLAY '******** BP13C653 *******'.                                 
           DISPLAY 'NO OF F125 RECS READ    :' WS-BP13F125-READ.                
           DISPLAY 'NO OF K800 RECS DELETED :' WS-BP13K800-DELETED.             
           DISPLAY 'NO OF K820 RECS DELETED :' WS-BP13K820-DELETED.             
           DISPLAY 'NO OF K840 RECS REWRITE :' WS-BP13K840-REWRITE.             
           DISPLAY 'NO OF K410 RECS REWRITE :' WS-K410-REWRITE.                 
                                                                                
           CLOSE BP13K893                                                       
                 BP13K895                                                       
                 BP13F125                                                       
                 BP13K800                                                       
                 BP13K840                                                       
                 BP13K820                                                       
                 BP13K410                                                       
                 BP13L653.                                                      
                                                                                
           IF WS-K800-STATUS  NOT = '00'                                        
              DISPLAY '***  CLOSING ERROR  WS-K800-STATUS'                      
              WS-K800-STATUS.                                                   
                                                                                
           IF WS-K893-STATUS  NOT = '00'                                        
              DISPLAY '***  CLOSING ERROR  WS-K893-STATUS'                      
              WS-K893-STATUS.                                                   
                                                                                
           IF WS-K410-STATUS  NOT = '00'                                        
              DISPLAY '***  CLOSING ERROR, WS-K410-STATUS'                      
              WS-K410-STATUS.                                                   
                                                                                
           IF WS-K840-STATUS NOT = '00'                                         
              DISPLAY '***  CLOSING ERROR, WS-K840-STATUS '                     
              WS-K840-STATUS.                                                   
                                                                                
           IF WS-K820-STATUS NOT = '00'                                         
              DISPLAY '***  CLOSING ERROR, WS-K820-STATUS '                     
              WS-K820-STATUS.                                                   
                                                                                
           IF WS-K895-STATUS NOT = '00'                                         
              DISPLAY '***  CLOSING ERROR, WS-K895-STATUS '                     
              WS-K895-STATUS.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       999-CLOSE-EXIT.                                                          
           EXIT.                                                                
