      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C270.                                                 
       AUTHOR.        SONNY A.                                                  
       DATE-WRITTEN.  11/03/97.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ***************************************************************           
      *             SYSTEM OF COMMITMENT (SOC)                      *           
      *=============================================================*           
      *                                                             *           
      *  OBJECTIVE : TO PRINT LETTERS FOR SALES                     *           
      *              WHERE F838-NUM-AMIS = 'Y'                      *           
      *                                                             *           
      *  INPUT FILES                                                *           
      *      BP13F838  --                                           *           
      *      BP13K820  --                                           *           
      *                                                             *           
      *  OUTPUT                                                     *           
      *      BP13K838  -- TO TAG K838-NUM-PRINTED = 'Y'             *           
      *      BP13P270  -- CIRCULAR TO APPLICANT ON CHANGE OF ADDRESS*           
      *                                                             *           
      *      BP13L270  -- CONTROL TOTAL REPORT ON PRINTED LETTER    *           
      *                                                             *           
      ***************************************************************           
      * MODIFICATIONS :                                             *           
      *                                                             *           
      * CHGE REQ #   DATE       AUTHOR    REMARKS                   *           
      * ----------   ----       ------    -------                   *           
      * BP130251     11/03/97    SIA      NEW PGM                   *           
      * BP130251     22/09/97    LMS      TO ADJUST ALIGNMENT OF    *           
      *                                   ADDR FOR AMIS.            *           
      * BP130383     09/01/98    LMS      TO CATER FOR CHANGE IN    *           
      *                                   LAYOUT OF BP13K838.       *           
      * BP130418     21/09/98    LMS      TO CATER FOR Y2000 CHG.   *           
      * BP132127     03/06/02    CKK      TO CHANGE LETTER LAYOUT   *           
      * BP137986     05/09/19    CC37     TO CHANGE LETTER LAYOUT   *           
      * BP137986     06/01/20    CC37     TO REMOVE PRINTING OF     *           
      *                                   BE0201 FORM               *           
      ***************************************************************           
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
      *======================                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *=====================                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F838 ASSIGN TO BP13F838.                                  
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                        ACCESS MODE          IS RANDOM                          
                        ORGANIZATION         IS INDEXED                         
                        RECORD KEY           IS K820-KEY-FLD                    
                        ALTERNATE RECORD KEY IS K820-NUM-NRIC                   
                                                WITH DUPLICATES                 
                        FILE STATUS          IS WS-K820-STATUS.                 
                                                                                
           SELECT BP13K838 ASSIGN TO BP13K838                                   
                        ACCESS MODE          IS RANDOM                          
                        ORGANIZATION         IS INDEXED                         
                        RECORD KEY           IS K838-KEY-FLD                    
                        FILE STATUS          IS WS-K838-STATUS.                 
                                                                                
           SELECT BP13P270 ASSIGN TO BP13P270.                                  
                                                                                
           SELECT BP13L270 ASSIGN TO BP13L270.                                  
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
      *=============                                                            
                                                                                
       FD  BP13K820                                                             
               RECORD CONTAINS 400 CHARACTERS.                                  
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F838                                                             
               RECORD CONTAINS 400 CHARACTERS.                                  
       COPY BP13F838.                                                           
                                                                                
       FD  BP13K838                                                             
               RECORD CONTAINS 400 CHARACTERS.                                  
       COPY BP13K838.                                                           
                                                                                
       FD  BP13P270                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  BP13P270-REC                PIC X(80).                               
                                                                                
       FD  BP13L270                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  BP13L270-REC                PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-DTE-CURRENT-YYMMDD.                                               
           05   WS-CURRENT-YY          PIC X(2).                                
           05   WS-CURRENT-MM          PIC X(2).                                
           05   WS-CURRENT-DD          PIC X(2).                                
                                                                                
       01  WS-CURR-DTE.                                                         
           05   WS-CURR-YY             PIC X(4).                                
           05   WS-CURR-MM             PIC X(2).                                
           05   WS-CURR-DD             PIC X(2).                                
                                                                                
       01  WS-PRT-DTE.                                                          
           05   WS-PRT-YY              PIC X(4).                                
           05   FILLER                 PIC X  VALUE '/'.                        
           05   WS-PRT-MM              PIC X(2).                                
           05   FILLER                 PIC X  VALUE '/'.                        
           05   WS-PRT-DD              PIC X(2).                                
                                                                                
       01  WS-PG-CNT-L270              PIC 999  VALUE ZEROS.                    
                                                                                
       01  WS-NUM-COUNT                PIC 9    VALUE ZEROS.                    
                                                                                
       01  WS-K820-FOUND               PIC X(1) VALUE SPACES.                   
      *-------------*                                                           
      * EOF FLAGS   *                                                           
      *-------------*                                                           
                                                                                
       01  EOF-BP13F838                PIC X      VALUE  'N'.                   
                                                                                
      *-------------*                                                           
      * FILE STATUS *                                                           
      *-------------*                                                           
                                                                                
       01  WS-K820-STATUS                 PIC 9(2) VALUE ZEROS.                 
       01  WS-K838-STATUS                 PIC 9(2) VALUE ZEROS.                 
                                                                                
      *--------*                                                                
      * COUNTS *                                                                
      *--------*                                                                
       01  WS-F838-READ                    PIC 9(6)  VALUE  ZEROS.              
       01  WS-K820-READ                    PIC 9(6)  VALUE  ZEROS.              
       01  WS-K838-WRITTEN                 PIC 9(6)  VALUE  ZEROS.              
       01  WS-K838-NOTFND                  PIC 9(6)  VALUE  ZEROS.              
       01  WS-COUNT-L270                   PIC 9(2)  VALUE  55.                 
       01  WS-SNO-L270                     PIC 9(6)  VALUE  ZEROS.              
       01  WS-L270-WRITTEN                 PIC 9(5)  VALUE  ZEROS.              
                                                                                
      *-----------------                                                        
      * PRINT LETTER.                                                           
      *-----------------                                                        
                                                                                
       01  WS-DJDE-HDBREF.                                                      
           05  FILLER                      PIC X(11) VALUE SPACES.              
           05  WS-DJDE-NT-ZONE             PIC X(3).                            
           05  WS-DJDE-FLAT-TYPE           PIC X(2).                            
           05  WS-DJDE-HDBREF-SLASH        PIC X     VALUE  '/'.                
           05  WS-DJDE-REGN                PIC X(8).                            
                                                                                
       01  WS-BP13H-DTE.                                                        
           05  FILLER                      PIC X(02) VALUE  SPACES.             
           05  WS-DJDE-DD                  PIC X(2).                            
           05  FILLER                      PIC X     VALUE  '/'.                
           05  WS-DJDE-MM                  PIC X(2).                            
           05  FILLER                      PIC X     VALUE  '/'.                
           05  WS-DJDE-YY                  PIC X(4).                            
                                                                                
       01  WS-BP13H-NAME1.                                                      
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 WS-DJDE-NAME1                PIC X(52) VALUE SPACES.              
           05 WS-DJDE-NAME1-LABEL          PIC X(14) VALUE SPACES.              
                                                                                
       01  WS-BP13H-NAME2.                                                      
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 WS-DJDE-NAME2                PIC X(52) VALUE SPACES.              
           05 WS-DJDE-NAME2-LABEL          PIC X(14) VALUE SPACES.              
                                                                                
       01  WS-NEW-ADDRESS.                                                      
           05  WS-BP13H-NEW-ADDR-INFO1.                                         
               10  FILLER                  PIC X(02) VALUE SPACES.              
               10  WS-DJDE-NEW-NUM-BLK     PIC X(10) VALUE SPACES.              
               10  FILLER                  PIC X(01) VALUE SPACE.               
               10  WS-DJDE-NEW-FLAT.                                            
                   15 WS-DJDE-NEW-CHAR     PIC X(01) VALUE SPACE.               
                   15 WS-DJDE-NEW-NUM-LVL  PIC X(03) VALUE SPACES.              
                   15 WS-DJDE-NEW-DASH     PIC X(01) VALUE SPACE.               
                   15 WS-DJDE-NEW-NUM-UNIT PIC X(05) VALUE SPACES.              
           05  WS-BP13H-NEW-ADDR-INFO2.                                         
               10  FILLER                  PIC X(02) VALUE SPACES.              
               10  WS-DJDE-NEW-STREET-NME  PIC X(20) VALUE SPACES.              
           05  WS-BP13H-NEW-ADDR-INFO3.                                         
               10  FILLER                  PIC X(02) VALUE SPACES.              
               10  WS-DJDE-NEW-POSTAL-CDE  PIC X(16) VALUE SPACES.              
                                                                                
       01  WS-DJDE-NRIC.                                                        
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(06) VALUE 'NRIC :'.            
           05  WS-DJDE-NRIC1               PIC X(09) VALUE SPACES.              
           05 FILLER                       PIC X(05) VALUE SPACES.              
           05 FILLER                       PIC X(06) VALUE 'NRIC :'.            
           05  WS-DJDE-NRIC2               PIC X(09) VALUE SPACES.              
           05 FILLER                       PIC X(06) VALUE SPACES.              
           05 FILLER                       PIC X(06) VALUE 'NRIC :'.            
           05  WS-DJDE-NRIC3               PIC X(09) VALUE SPACES.              
           05 FILLER                       PIC X(05) VALUE SPACES.              
           05 FILLER                       PIC X(06) VALUE 'NRIC :'.            
           05  WS-DJDE-NRIC4               PIC X(09) VALUE SPACES.              
                                                                                
       01  WS-DETAIL-LINE.                                                      
           05  WS-REGN               PIC X(8)    VALUE SPACES.                  
           05  WS-NAME1              PIC X(40)   VALUE SPACES.                  
           05  WS-OLD-RES-ADDR.                                                 
               10  WS-BLK-RES-O      PIC X(10)   VALUE SPACES.                  
               10  FILLER            PIC X(1)    VALUE SPACES.                  
               10  WS-NUM-FLAT-O.                                               
                   15  WS-CHAR-O     PIC X       VALUE SPACE.                   
                   15  WS-LEVEL-O    PIC X(2)    VALUE SPACES.                  
                   15  WS-DASH-O     PIC X       VALUE SPACE.                   
                   15  WS-HSE-MAIN-O PIC X(4)    VALUE SPACES.                  
                   15  WS-HSE-SUB-O  PIC X(3)    VALUE SPACES.                  
           05  WS-NEW-RES-ADDR.                                                 
               10  WS-BLK-RES-N      PIC X(10)   VALUE SPACES.                  
               10  FILLER            PIC X(1)    VALUE SPACES.                  
               10  WS-NUM-FLAT-N.                                               
                   15  WS-CHAR-N     PIC X       VALUE SPACE.                   
                   15  WS-LEVEL-N    PIC X(3)    VALUE SPACES.                  
                   15  WS-DASH-N     PIC X       VALUE SPACE.                   
                   15  WS-HSE-MAIN-N PIC X(5)    VALUE SPACES.                  
           05  WS-CORR-ADDR.                                                    
               10  WS-BLK-CORR          PIC X(10)   VALUE SPACES.               
               10  FILLER               PIC X(1)    VALUE SPACES.               
               10  WS-NUM-FLAT-CORR.                                            
                   15  WS-CHAR-CORR     PIC X       VALUE SPACE.                
                   15  WS-LEVEL-CORR    PIC X(2)    VALUE SPACES.               
                   15  WS-DASH-CORR     PIC X       VALUE SPACE.                
                   15  WS-HSE-MAIN-CORR PIC X(4)    VALUE SPACES.               
                   15  WS-HSE-SUB-CORR  PIC X(3)    VALUE SPACES.               
                                                                                
       01  WS-RES-ADDR2.                                                        
           05  WS-NAME2              PIC X(40)   VALUE SPACES.                  
           05  WS-NME-STREET-O       PIC X(20)   VALUE SPACES.                  
           05  WS-NME-STREET-N       PIC X(20)   VALUE SPACES.                  
                                                                                
       01  WS-RES-ADDR3.                                                        
           05  WS-NUM-POSTAL-O       PIC X(9)    VALUE SPACES.                  
           05  WS-NUM-POSTAL-N       PIC X(9)    VALUE SPACES.                  
                                                                                
      *-------------------                                                      
      * FOR DJDE PRINTING.                                                      
      *-------------------                                                      
       01  WS-DJDE-INFO                   PIC X(80) VALUE                       
           '$DJDE$ FORMS=BE0201,END;'.                                          
                                                                                
       01  WS-DJDE-BP13H1                 PIC X(80) VALUE                       
           '$DJDE$ JDE=13C24U,FORMS=BP13H1,COPIES=1,COLLATE=YES,JDL=BP13        
      -    ',DUPLEX=NO,END;'.                                                   
                                                                                
       01  WS-DJDE-BLANKF                 PIC X(80)   VALUE                     
           '$DJDE$ FORMS=BLANKF,SHIFT=(0,0),END;'.                              
                                                                                
      *01  WS-DJDE-BP13H1                 PIC X(80) VALUE                       
      *    '$DJDE$ FORMS=BP13H1,COPIES=1,DUPLEX=YES,END;'.                      
                                                                                
      *----------------------------------------------------------------*        
      * HEADER LINES FOR REPORT BP13L270                               *        
      *----------------------------------------------------------------*        
       01  L270-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(8)    VALUE 'BP13L270'.           
           05  FILLER                   PIC X(7)     VALUE SPACES .             
           05  FILLER                   PIC X(8)    VALUE 'HDBCAT 3'.           
           05  FILLER                   PIC X(21)   VALUE SPACES.               
           05  FILLER                   PIC X(39)   VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(17)   VALUE SPACES.               
           05  FILLER                   PIC X(7)    VALUE 'DATE :'.             
           05  L270-PRT-DTE.                                                    
               07  L270-PRT-DD          PIC XX       VALUE SPACES.              
               07  FILLER               PIC X        VALUE '/'.                 
               07  L270-PRT-MM          PIC XX       VALUE SPACES.              
               07  FILLER               PIC X        VALUE '/'.                 
               07  L270-PRT-YY          PIC X(4)     VALUE SPACES.              
           05  FILLER                   PIC X(3)     VALUE SPACES.              
           05  FILLER                   PIC X(7)     VALUE 'PAGE : '.           
           05  L270-PAGE-NO             PIC ZZ9 .                               
                                                                                
       01  L270-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(37)   VALUE SPACES .              
           05  FILLER                   PIC X(50)   VALUE                       
               '   SUMMARY REPORT OF LETTERS PRINTED FOR AMIS   '.              
                                                                                
       01  L270-PR-HEAD-03.                                                     
           05  FILLER                   PIC X(24)   VALUE                       
               'S/NO REGN NO   APPLICANT'.                                      
           05  FILLER                   PIC X(45)   VALUE SPACES.               
           05  FILLER                   PIC X(33)   VALUE                       
               'OLD RESIDENTIAL ADDRESS         '.                              
           05  FILLER                   PIC X(23)   VALUE                       
               'NEW RESIDENTIAL ADDRESS'.                                       
           05  FILLER                   PIC X(8)    VALUE SPACES.               
                                                                                
      *----------------------------------------------------------------*        
      * DETAIL LINE FOR REPORT BP13L270                                *        
      *----------------------------------------------------------------*        
       01  L270-PR-DETAILS.                                                     
           05  L270-SNO                 PIC ZZZ9.                               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L270-REGN                PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L270-NAME1               PIC X(52)   VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L270-OLD-RES-ADDR.                                               
               10  L270-BLK-RES-O       PIC X(10)   VALUE SPACES.               
               10  FILLER               PIC X(1)    VALUE SPACES.               
               10  L270-NUM-FLAT-O.                                             
                   15  L270-CHAR-O      PIC X       VALUE SPACE.                
                   15  L270-LEVEL-O     PIC X(2)    VALUE SPACES.               
                   15  L270-DASH-O      PIC X       VALUE SPACE.                
                   15  L270-HSE-MAIN-O  PIC X(4)    VALUE SPACES.               
                   15  L270-HSE-SUB-O   PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(10)   VALUE SPACES.               
           05  L270-NEW-RES-ADDR.                                               
               10  L270-BLK-RES-N       PIC X(10)   VALUE SPACES.               
               10  FILLER               PIC X(1)    VALUE SPACE.                
               10  L270-NUM-FLAT-N.                                             
                   15  L270-CHAR-N      PIC X       VALUE SPACE.                
                   15  L270-LEVEL-N     PIC X(3)    VALUE SPACES.               
                   15  L270-DASH-N      PIC X       VALUE SPACE.                
                   15  L270-HSE-MAIN-N  PIC X(5)    VALUE SPACES.               
                                                                                
       01  L270-RES-ADDR2.                                                      
           05  FILLER                   PIC X(15)   VALUE SPACES.               
           05  L270-NAME2               PIC X(52)   VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L270-NME-STREET-O        PIC X(20)   VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L270-NUM-POSTAL-O        PIC X(9)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L270-NME-STREET-N        PIC X(20)   VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L270-NUM-POSTAL-N        PIC X(9)    VALUE SPACES.               
                                                                                
       01  L270-DRAW-LINE.                                                      
           05  LINE1                    PIC X(132)  VALUE ALL '-'.              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-MAIN.                                                               
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES        THRU  1000-EXIT.                      
                                                                                
           PERFORM 1500-GET-DATE-TIME     THRU  1500-EXIT.                      
           MOVE    WS-CURR-DD             TO    WS-PRT-DD                       
                                                WS-DJDE-DD.                     
           MOVE    WS-CURR-MM             TO    WS-PRT-MM                       
                                                WS-DJDE-MM.                     
           MOVE    WS-CURR-YY             TO    WS-PRT-YY                       
                                                WS-DJDE-YY                      
                                                                                
           MOVE     1    TO WS-SNO-L270.                                        
           MOVE     1    TO WS-PG-CNT-L270.                                     
                                                                                
           PERFORM 3000-READ-BP13F838        THRU 3000-EXIT.                    
           PERFORM 4000-PROCESS-BP13F838     THRU 4000-EXIT                     
             UNTIL EOF-BP13F838 = 'Y'.                                          
           PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                             
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F838                                                 
                       BP13K820                                                 
                I-O    BP13K838                                                 
                OUTPUT BP13P270                                                 
                       BP13L270.                                                
                                                                                
           IF WS-K820-STATUS NOT = 0 AND 97                                     
              DISPLAY ' ERROR ENCOUNTERED IN OPENING FILE BP13K820 : '          
                                              WS-K820-STATUS                    
              MOVE WS-K820-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           IF WS-K838-STATUS NOT = 0 AND 97                                     
              DISPLAY ' ERROR ENCOUNTERED IN OPENING FILE BP13K838 : '          
                                              WS-K838-STATUS                    
              MOVE WS-K838-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1500-GET-DATE-TIME.                                                      
      *****************************************************************         
                                                                                
           ACCEPT WS-DTE-CURRENT-YYMMDD FROM DATE.                              
                                                                                
           MOVE WS-CURRENT-DD   TO     WS-CURR-DD.                              
           MOVE WS-CURRENT-MM   TO     WS-CURR-MM.                              
           MOVE WS-CURRENT-YY   TO     WS-CURR-YY (3:2).                        
                                                                                
           IF WS-CURR-YY (3:2)  > '90'                                          
              MOVE '19'      TO WS-CURR-YY (1:2)                                
           ELSE                                                                 
              MOVE '20'      TO WS-CURR-YY (1:2)                                
           END-IF.                                                              
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-BP13F838.                                                      
      *****************************************************************         
                                                                                
           READ BP13F838                                                        
                AT END  MOVE  'Y' TO    EOF-BP13F838.                           
                                                                                
           IF EOF-BP13F838 NOT = 'Y'                                            
              ADD   1             TO    WS-F838-READ.                           
                                                                                
           MOVE SPACES            TO    L270-PR-DETAILS                         
                                        L270-RES-ADDR2.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-PROCESS-BP13F838.                                                   
      *****************************************************************         
                                                                                
           PERFORM 7000-MOVE-VALUES-TO-LETTER THRU 7000-EXIT.                   
           PERFORM 7100-PRINT-DJDE            THRU 7100-EXIT.                   
           PERFORM 7600-WRITE-BP13L270        THRU 7600-EXIT.                   
      *    PERFORM 4100-REWRITE-BP13K838      THRU 4100-EXIT.                   
           PERFORM 3000-READ-BP13F838         THRU 3000-EXIT.                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4100-REWRITE-BP13K838.                                                   
      *****************************************************************         
                                                                                
           INITIALIZE BP13K838-REC.                                             
           MOVE F838-KEY-FLD   TO   K838-KEY-FLD.                               
                                                                                
           READ BP13K838.                                                       
                                                                                
           EVALUATE  WS-K838-STATUS                                             
           WHEN 00                                                              
                MOVE   'Y'  TO K838-NUM-PRINTED                                 
                REWRITE BP13K838-REC                                            
                IF  WS-K838-STATUS = 00                                         
                    ADD     1   TO WS-K838-WRITTEN                              
                ELSE                                                            
                    DISPLAY 'REWRITE ERROR  K838 : ' WS-K838-STATUS             
                    DISPLAY 'REGN READ           : ' F838-NUM-REGN              
                    GO TO 9999-CLOSE-FILES                                      
                END-IF                                                          
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND BP13K838 : ' F838-NUM-REGN            
                ADD      1                       TO WS-K838-NOTFND              
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR BP13K838 : ' WS-K838-STATUS                 
                DISPLAY 'NRIC READ           : ' K838-NUM-NRIC                  
                GO TO 9999-CLOSE-FILES                                          
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       6000-READ-BP13K820.                                                      
      *****************************************************************         
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
                ADD 1                TO   WS-K820-READ                          
                MOVE 'Y'             TO   WS-K820-FOUND                         
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND BP13K820 : '                          
                                K820-NUM-NRIC                                   
                MOVE 'N'             TO   WS-K820-FOUND                         
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR BP13K820   : '                              
                                WS-K820-STATUS                                  
                DISPLAY 'NRIC                  : ' K820-NUM-NRIC                
                PERFORM  9999-CLOSE-FILES THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       7000-MOVE-VALUES-TO-LETTER.                                              
      *****************************************************************         
                                                                                
           MOVE F838-NUM-REGN      TO  WS-DJDE-REGN                             
                                       WS-REGN.                                 
           MOVE F838-NUM-NT-ZONE   TO  WS-DJDE-NT-ZONE.                         
           MOVE F838-NUM-FLAT-TYPE TO  WS-DJDE-FLAT-TYPE.                       
           MOVE '/'                TO  WS-DJDE-HDBREF-SLASH.                    
                                                                                
           IF F838-NUM-NRIC  NOT = SPACES AND LOW-VALUES                        
              MOVE F838-NUM-NRIC       TO WS-DJDE-NRIC1                         
              MOVE SPACES              TO WS-K820-FOUND                         
              MOVE SPACES              TO K820-KEY-FLD                          
              MOVE F838-NUM-REGN       TO K820-NUM-REGN                         
              MOVE WS-DJDE-NRIC1       TO K820-NUM-NRIC                         
              PERFORM 6000-READ-BP13K820 THRU 6000-EXIT                         
              IF WS-K820-FOUND = 'Y'                                            
                 MOVE  K820-NME-OCCP  TO   WS-DJDE-NAME1                        
                                           WS-NAME1                             
              ELSE                                                              
                 MOVE  SPACES         TO   WS-DJDE-NAME1                        
                                           WS-NAME1                             
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES              TO WS-DJDE-NRIC1                         
              MOVE SPACES              TO WS-DJDE-NAME1                         
                                          WS-NAME1                              
           END-IF.                                                              
                                                                                
           IF (F838-NUM-NRIC2 NOT = SPACES AND LOW-VALUES) AND                  
              (F838-NUM-NRIC2 (1:1) NOT = '#')                                  
              MOVE F838-NUM-NRIC2      TO WS-DJDE-NRIC2                         
              MOVE SPACES              TO WS-K820-FOUND                         
              MOVE SPACES              TO K820-KEY-FLD                          
              MOVE F838-NUM-REGN       TO K820-NUM-REGN                         
              MOVE WS-DJDE-NRIC2       TO K820-NUM-NRIC                         
              PERFORM 6000-READ-BP13K820 THRU 6000-EXIT                         
              IF WS-K820-FOUND = 'Y'                                            
                 MOVE  K820-NME-OCCP  TO   WS-DJDE-NAME2                        
                                           WS-NAME2                             
              ELSE                                                              
                 MOVE  SPACES         TO   WS-DJDE-NAME2                        
                                           WS-NAME2                             
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES              TO WS-DJDE-NRIC2                         
              MOVE SPACES              TO WS-DJDE-NAME2                         
                                          WS-NAME2                              
           END-IF.                                                              
                                                                                
           IF (F838-NUM-NRIC3 NOT = SPACES AND LOW-VALUES) AND                  
              (F838-NUM-NRIC3 (1:1) NOT = '#')                                  
              MOVE F838-NUM-NRIC3      TO WS-DJDE-NRIC3                         
           ELSE                                                                 
              MOVE SPACES              TO WS-DJDE-NRIC3                         
           END-IF.                                                              
                                                                                
           IF (F838-NUM-NRIC4 NOT = SPACES AND LOW-VALUES) AND                  
              (F838-NUM-NRIC4 (1:1) NOT = '#')                                  
              MOVE F838-NUM-NRIC4      TO WS-DJDE-NRIC4                         
           ELSE                                                                 
              MOVE SPACES              TO WS-DJDE-NRIC4                         
           END-IF.                                                              
                                                                                
      *-----------------------------------------------                          
      * NEW ADDRESS FOR LETTER                                                  
      *-----------------------------------------------                          
                                                                                
           IF  F838-NUM-NEW-ADDR-TYPE = 'H'                                     
               STRING 'BLK ' F838-NUM-NEW-BLK                                   
                       DELIMITED BY SIZE  INTO  WS-DJDE-NEW-NUM-BLK             
           ELSE                                                                 
               MOVE  F838-NUM-NEW-BLK       TO  WS-DJDE-NEW-NUM-BLK             
           END-IF.                                                              
                                                                                
           IF  F838-NUM-NEW-ADDR-TYPE = 'H'                                     
               STRING '#'  F838-NUM-NEW-HSELVL                                  
                      '-'  F838-NUM-NEW-HSEUNIT                                 
                      '  ' DELIMITED BY SIZE  INTO WS-DJDE-NEW-FLAT             
           ELSE                                                                 
              IF F838-NUM-NEW-HSELVL = SPACES OR LOW-VALUES                     
                 STRING F838-NUM-NEW-HSELVL                                     
                        F838-NUM-NEW-HSEUNIT                                    
                        '   ' DELIMITED BY SIZE  INTO WS-DJDE-NEW-FLAT          
              ELSE                                                              
                 STRING F838-NUM-NEW-HSELVL                                     
                        '-' F838-NUM-NEW-HSEUNIT                                
                        '  ' DELIMITED BY SIZE  INTO WS-DJDE-NEW-FLAT           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE  F838-NME-NEW-STREET-TXT1 TO  WS-DJDE-NEW-STREET-NME.           
                                                                                
           IF F838-NUM-NEW-POSTAL = SPACES                                      
              MOVE  SPACES TO WS-DJDE-NEW-POSTAL-CDE                            
           ELSE                                                                 
              STRING 'SINGAPORE '   F838-NUM-NEW-POSTAL                         
                      DELIMITED BY SIZE  INTO WS-DJDE-NEW-POSTAL-CDE            
              END-STRING                                                        
           END-IF.                                                              
                                                                                
      *-----------------------------------------------                          
      * OLD ADDRESS FOR REPORT                                                  
      *-----------------------------------------------                          
                                                                                
           IF F838-NUM-OLD-ADDR-TYPE  = 'H'                                     
              MOVE F838-NUM-OLD-BLK         TO WS-BLK-RES-O                     
              MOVE F838-NME-OLD-STREET      TO WS-NME-STREET-O                  
              MOVE F838-NUM-OLD-LVL-CHAR    TO WS-CHAR-O                        
              MOVE F838-NUM-OLD-LVL-NUM     TO WS-LEVEL-O                       
              MOVE F838-NUM-OLD-LVL-DASH    TO WS-DASH-O                        
              MOVE F838-NUM-OLD-HSEMAIN     TO WS-HSE-MAIN-O                    
              MOVE F838-NUM-OLD-HSESUB      TO WS-HSE-SUB-O                     
           ELSE                                                                 
              MOVE F838-NUM-OLD-BLK         TO WS-BLK-RES-O                     
              MOVE F838-NUM-OLD-FLAT        TO WS-NUM-FLAT-O                    
              MOVE F838-NME-OLD-STREET-BLDG TO WS-NME-STREET-O                  
           END-IF.                                                              
                                                                                
           IF F838-NUM-OLD-POSTAL = SPACES                                      
              MOVE  SPACES TO WS-NUM-POSTAL-O                                   
           ELSE                                                                 
              STRING 'S('   F838-NUM-OLD-POSTAL                                 
                      ')'                                                       
                      DELIMITED BY SIZE  INTO WS-NUM-POSTAL-O                   
              END-STRING                                                        
           END-IF.                                                              
                                                                                
      *-----------------------------------------------                          
      * NEW ADDRESS FOR REPORT                                                  
      *-----------------------------------------------                          
                                                                                
           IF  F838-NUM-NEW-ADDR-TYPE = 'H'                                     
               STRING 'BLK ' F838-NUM-NEW-BLK                                   
                       DELIMITED BY SIZE  INTO  WS-BLK-RES-N                    
           ELSE                                                                 
               MOVE  F838-NUM-NEW-BLK       TO  WS-BLK-RES-N                    
           END-IF.                                                              
                                                                                
           IF  F838-NUM-NEW-ADDR-TYPE = 'H'                                     
               STRING '#'  F838-NUM-NEW-HSELVL                                  
                      '-'  F838-NUM-NEW-HSEUNIT                                 
                      '  ' DELIMITED BY SIZE  INTO WS-NUM-FLAT-N                
               END-STRING                                                       
           ELSE                                                                 
              IF F838-NUM-NEW-HSELVL = SPACES OR LOW-VALUES                     
                 STRING F838-NUM-NEW-HSELVL                                     
                        F838-NUM-NEW-HSEUNIT                                    
                        '   ' DELIMITED BY SIZE  INTO WS-NUM-FLAT-N             
              ELSE                                                              
                 STRING F838-NUM-NEW-HSELVL                                     
                        '-' F838-NUM-NEW-HSEUNIT                                
                        '  ' DELIMITED BY SIZE  INTO WS-NUM-FLAT-N              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE  F838-NME-NEW-STREET-TXT1 TO  WS-NME-STREET-N.                  
                                                                                
           IF F838-NUM-NEW-POSTAL = SPACES                                      
              MOVE  SPACES TO WS-NUM-POSTAL-N                                   
           ELSE                                                                 
              STRING 'S('   F838-NUM-NEW-POSTAL                                 
                      ')'                                                       
                      DELIMITED BY SIZE  INTO WS-NUM-POSTAL-N                   
              END-STRING                                                        
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       7100-PRINT-DJDE.                                                         
      *****************************************************************         
                                                                                
           WRITE BP13P270-REC  FROM  WS-DJDE-BP13H1         AFTER PAGE          
      *    WRITE BP13P270-REC  FROM  WS-DJDE-HDBREF          AFTER 4            
      *    WRITE BP13P270-REC  FROM  WS-BP13H-DTE            AFTER 1            
           WRITE BP13P270-REC  FROM  WS-BP13H-NAME1          AFTER 11           
           WRITE BP13P270-REC  FROM  WS-BP13H-NAME2          AFTER 1            
           WRITE BP13P270-REC  FROM  WS-BP13H-NEW-ADDR-INFO1 AFTER 2            
           WRITE BP13P270-REC  FROM  WS-BP13H-NEW-ADDR-INFO2 AFTER 1            
           WRITE BP13P270-REC  FROM  WS-BP13H-NEW-ADDR-INFO3 AFTER 1            
           WRITE BP13P270-REC  FROM  WS-BP13H-DTE            AFTER 2            
           WRITE BP13P270-REC  FROM  WS-DJDE-HDBREF          AFTER 2            
           WRITE BP13P270-REC  FROM  WS-BP13H-NEW-ADDR-INFO1 AFTER 15           
           WRITE BP13P270-REC  FROM  WS-BP13H-NEW-ADDR-INFO2 AFTER 1            
           WRITE BP13P270-REC  FROM  WS-BP13H-NEW-ADDR-INFO3 AFTER 1            
      *    WRITE BP13P270-REC  FROM  WS-DJDE-INFO AFTER PAGE.                   
           MOVE  SPACES        TO    BP13P270-REC.                              
      *    WRITE BP13P270-REC  AFTER PAGE.                                      
           WRITE BP13P270-REC  FROM  WS-DJDE-BLANKF AFTER PAGE.                 
           WRITE BP13P270-REC AFTER 5.                                          
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       7600-WRITE-BP13L270.                                                     
      *****************************************************************         
                                                                                
            IF  WS-COUNT-L270 = 0 OR WS-COUNT-L270 > 50                         
                MOVE  ZERO              TO  WS-COUNT-L270                       
                MOVE  WS-PG-CNT-L270    TO  L270-PAGE-NO                        
                MOVE  WS-PRT-DD         TO  L270-PRT-DD                         
                MOVE  WS-PRT-MM         TO  L270-PRT-MM                         
                MOVE  WS-PRT-YY         TO  L270-PRT-YY                         
                WRITE BP13L270-REC  FROM  L270-PR-HEAD-01 AFTER PAGE            
                WRITE BP13L270-REC  FROM  L270-PR-HEAD-02 AFTER 1               
                WRITE BP13L270-REC  FROM  L270-PR-HEAD-03 AFTER 2               
                WRITE BP13L270-REC  FROM  L270-DRAW-LINE  AFTER 1               
                ADD   5              TO   WS-COUNT-L270                         
                ADD   1              TO   WS-PG-CNT-L270.                       
                                                                                
            MOVE  WS-SNO-L270        TO    L270-SNO.                            
            MOVE  WS-REGN            TO    L270-REGN.                           
            MOVE  WS-NAME1           TO    L270-NAME1.                          
            MOVE  WS-OLD-RES-ADDR    TO    L270-OLD-RES-ADDR.                   
            MOVE  WS-NEW-RES-ADDR    TO    L270-NEW-RES-ADDR.                   
            WRITE BP13L270-REC       FROM  L270-PR-DETAILS   AFTER 2.           
            MOVE  WS-NAME2           TO    L270-NAME2.                          
            MOVE  WS-NME-STREET-O    TO    L270-NME-STREET-O.                   
            MOVE  WS-NUM-POSTAL-O    TO    L270-NUM-POSTAL-O.                   
            MOVE  WS-NME-STREET-N    TO    L270-NME-STREET-N.                   
            MOVE  WS-NUM-POSTAL-N    TO    L270-NUM-POSTAL-N.                   
            WRITE BP13L270-REC       FROM  L270-RES-ADDR2    AFTER 1.           
                                                                                
            ADD   3             TO           WS-COUNT-L270.                     
            ADD   1             TO           WS-L270-WRITTEN.                   
            ADD   1             TO           WS-SNO-L270.                       
                                                                                
       7600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       9999-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F838                                                       
                 BP13K820                                                       
                 BP13K838                                                       
                 BP13P270                                                       
                 BP13L270.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 0                                            
              DISPLAY ' ERROR ENCOUNTERED IN CLOSING FILE  BP13K820 : '         
                                              WS-K820-STATUS                    
              MOVE WS-K820-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K838-STATUS NOT = 0                                            
              DISPLAY ' ERROR ENCOUNTERED IN CLOSING FILE  BP13K838 : '         
                                              WS-K838-STATUS                    
              MOVE WS-K838-STATUS TO RETURN-CODE.                               
                                                                                
                                                                                
           DISPLAY '----------------------------------------'.                  
           DISPLAY '               BP13C270                 '.                  
           DISPLAY '----------------------------------------'.                  
                                                                                
           DISPLAY ' NO OF BP13F838 RECS READ     : ' WS-F838-READ.             
           DISPLAY ' NO OF BP13K838 RECS WRITTEN  : ' WS-K838-WRITTEN.          
           DISPLAY ' NO OF BP13K820 RECS READ     : ' WS-K820-READ.             
           DISPLAY ' NO OF BP13L270 RECS WRITTEN  : ' WS-L270-WRITTEN.          
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
