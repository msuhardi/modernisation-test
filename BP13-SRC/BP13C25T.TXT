       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C25T.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  06 MARCH 2015.                                            
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE  : TO CREATE INTERFACE FILE FOR HLA FOR CASES    *            
      *              W/O HLA NO FROM 30 DAYS AFTER ACCEPTANCE      *            
      *                                                            *            
      * INPUT      : BP13F800                                      *            
      *              SY02F001                                      *            
      * OUTPUT     : BP13F313 - OUTPUT FILE FOR HLA                *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTIONS                       *            
      * -------- -------- ----- ------------                       *            
      * BP135664 20150305 ESA1  NEW PROGRAM                        *            
      * BP135664 20150511 ESA1  TO CHANGE TO CODE '06' IN BP13F313 *            
      * BP135905 20150806 ESA1  TO ADD UPDATE OF K800-DTE-ACCEPTANCE            
      * BP136951 20170927 KAM4  TO EXTEND THE DETECTION PERIOD TO  *            
      *                         THAT OF 60 DAYS AFTER FLAT SELECTION            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS K110-STATUS.                               
                                                                                
           SELECT BP13F313 ASSIGN TO BP13F313.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD   BP13F313                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13K313.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-VARIABLES.                                                         
          05 WS-F800-EOF                    PIC X     VALUE 'N'.                
          05 WS-F001-EOF                    PIC X     VALUE 'N'.                
          05 WS-F800-READ                   PIC 9(6)  VALUE ZEROS.              
          05 WS-F001-READ                   PIC 9(6)  VALUE ZEROS.              
          05 WS-K313-WRITE                  PIC 9(6)  VALUE ZEROS.              
          05 WS-K800-REWRITE                PIC 9(6)  VALUE ZEROS.              
          05 WS-INTEGER-CUR-DATE            PIC S9(9) VALUE ZEROES.             
          05 WS-DATE-N                      PIC  9(8) VALUE ZEROES.             
          05 WS-DATEN REDEFINES WS-DATE-N   PIC  X(8).                          
                                                                                
          05  WS-DATE.                                                          
              10 WS-CCYY.                                                       
                 15 WS-CC                    PIC 99.                            
                 15 WS-YY                    PIC 99.                            
              10 WS-CCYY1 REDEFINES WS-CCYY  PIC 9(4).                          
              10 WS-MM                       PIC 99.                            
              10 WS-DD                       PIC 99.                            
          05  WS-DATE9 REDEFINES WS-DATE     PIC 9(8).                          
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 K800-STATUS                     PIC X(2)  VALUE SPACES.            
          05 K110-STATUS                     PIC X(2)  VALUE SPACES.            
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE     THRU 1000-EXIT.                       
            PERFORM 2000-READ-SY02F001    THRU 2000-EXIT.                       
            PERFORM 3000-READ-BP13F800    THRU 3000-EXIT.                       
            PERFORM 4000-PROCESS-DETAIL   THRU 4000-EXIT                        
                    UNTIL WS-F800-EOF = 'Y'.                                    
            PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN  INPUT  BP13F800                                               
                         SY02F001                                               
                         BM06K110                                               
                  I-O    BP13K800                                               
                  OUTPUT BP13F313.                                              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR FILE BP13K800 ' K800-STATUS                   
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR FILE BM06K100 ' K110-STATUS                   
              MOVE K110-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-SY02F001.                                                      
      *-------------------------------------------------------------            
            READ SY02F001                                                       
                 AT END MOVE 'Y' TO WS-F001-EOF                                 
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD 1   TO WS-F001-READ.                                            
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
            READ BP13F800                                                       
                 AT END MOVE 'Y' TO WS-F800-EOF                                 
                 GO TO 3000-EXIT.                                               
                                                                                
            ADD 1   TO WS-F800-READ.                                            
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF F800-DTE-ACCEPTANCE = SPACES OR LOW-VALUES OR ZEROES              
              PERFORM 6000-READ-K110   THRU  6000-EXIT                          
           END-IF.                                                              
           IF F800-DTE-ACCEPTANCE NOT = SPACES AND LOW-VALUES AND               
                                        ZEROES                                  
              IF F800-DTE-ACCEPTANCE NOT NUMERIC                                
                 MOVE ZEROES              TO F800-DTE-ACCEPTANCE                
              END-IF                                                            
              MOVE F800-DTE-ACCEPTANCE    TO WS-DATE                            
                                                                                
              COMPUTE WS-INTEGER-CUR-DATE =                                     
                  FUNCTION INTEGER-OF-DATE(WS-DATE9) + 60                       
                                                                                
              COMPUTE WS-DATE-N =                                               
                  FUNCTION DATE-OF-INTEGER(WS-INTEGER-CUR-DATE)                 
                                                                                
              IF WS-DATE-N = F001-DTE-CURRENT                                   
                 IF F800-NUM-HLA = SPACES OR LOW-VALUES                         
                    MOVE SPACES            TO  BP13K313-REC                     
                    MOVE '06'              TO  K313-NUM-TRAN-TYPE               
                    MOVE F800-NUM-REGN     TO  K313-NUM-REGN                    
                    WRITE BP13K313-REC                                          
                    ADD   1 TO WS-K313-WRITE                                    
                    PERFORM 5000-READ-K800 THRU 5000-EXIT                       
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13F800  THRU 3000-EXIT.                          
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5000-READ-K800.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                TO BP13K800-MASTER.                       
                                                                                
           MOVE F800-NUM-REGN         TO K800-NUM-REGN.                         
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE K800-STATUS                                                 
             WHEN 00                                                            
                PERFORM 5500-REWRITE-K800 THRU 5500-EXIT                        
             WHEN 10                                                            
             WHEN 23                                                            
                DISPLAY 'RECORD NOT FOUND IN BP13K800 ' K800-STATUS             
                DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                        
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING BP13K800 ' K800-STATUS                   
                PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------*                                                   
       5500-REWRITE-K800.                                                       
      *---------------------*                                                   
                                                                                
           IF K800-DTE-ACCEPTANCE =  SPACES OR LOW-VALUES                       
              MOVE F800-DTE-ACCEPTANCE  TO K800-DTE-ACCEPTANCE                  
           END-IF.                                                              
                                                                                
           MOVE 'N'               TO K800-NUM-LOAN-TAG.                         
           MOVE FUNCTION CURRENT-DATE(1:8)                                      
                                  TO K800-DTE-UPDATE.                           
           MOVE 'BP13C25T'        TO K800-NUM-USERID.                           
                                                                                
           REWRITE  BP13K800-MASTER.                                            
                                                                                
           EVALUATE K800-STATUS                                                 
             WHEN 00                                                            
                ADD  1            TO WS-K800-REWRITE                            
             WHEN OTHER                                                         
                DISPLAY 'ERROR UPDATING BP13K800, STAT = ' K800-STATUS          
                DISPLAY 'K800-NUM-REGN                 = ' K800-NUM-REGN        
                MOVE     K800-STATUS             TO  RETURN-CODE                
                PERFORM  9000-CLOSE-ROUTINE      THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6000-READ-K110.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                TO K110-KEY-FLD.                          
                                                                                
           MOVE F800-NUM-SCH-ACC      TO K110-KEY-FLD.                          
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE K110-STATUS                                                 
             WHEN 00                                                            
                MOVE K110-DTE-ACCEPTANCE  TO F800-DTE-ACCEPTANCE                
             WHEN 10                                                            
             WHEN 23                                                            
                MOVE ZEROES               TO F800-DTE-ACCEPTANCE                
                DISPLAY 'RECORD NOT FOUND IN BM06K110 ' K110-STATUS             
                DISPLAY 'K110-KEY-FLD  = ' K110-KEY-FLD                         
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING BM06K110 ' K110-STATUS                   
                PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY '- BP13C25T CONTROL LIST --'                                 
           DISPLAY '---------------------------------------------'.             
           DISPLAY ' 1) TOT BP13F800 READ     : ' WS-F800-READ.                 
           DISPLAY ' 2) TOT BP13F313 WRITE    : ' WS-K313-WRITE.                
           DISPLAY ' 3) TOT BP13K800 UPDATE   : ' WS-K800-REWRITE.              
           DISPLAY ' '.                                                         
                                                                                
           CLOSE  BP13F800                                                      
                  SY02F001                                                      
                  BM06K110                                                      
                  BP13K800                                                      
                  BP13F313.                                                     
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR FILE BP13K800 ' K800-STATUS                  
              MOVE K800-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR FILE BM06K110 ' K110-STATUS                  
              MOVE K110-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
