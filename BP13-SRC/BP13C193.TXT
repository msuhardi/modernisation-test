       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C193                                                  
      *AUTHOR.        SRA.                                                      
      *DATE-WRITTEN.  16/07/1999.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE   : TO CREATE FILE TO PRINT SUBMISSION REPORT   *            
      *                FOR WALK-IN SELECTION OF 3-RM FLATS.        *            
      *                                                            *            
      *    INPUT FILE  : BM06F120                                  *            
      *   OUTPUT FILE  : BP13F200                                  *            
      *                  BP13F205                                  *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-NO   BY   DATE     DETAILS                             *            
      * -------- --- --------  -------                             *            
      * BP130712 SRA 19990716  NEW PGM                             *            
      * BP130819 MCC 20000406  DO NOT ALLOW DUPLICATE BP13F205 REC *            
      * BP132095 MTD 20020422  EXPAND LENGTH OF BP13F200           *            
      *                        (FROM 100 TO 300)                   *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BM06F120 ASSIGN TO BM06F120.                                  
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BM06F120                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 700 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BM06F120.                                                           
                                                                                
       FD  BP13F200                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED.                                           
       COPY BP13F200.                                                           
                                                                                
       FD  BP13F205                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED.                                           
       COPY BP13F205.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05 WS-F120-READ               PIC 9(7)    VALUE ZEROS.               
           05 WS-F200-WRITE              PIC 9(7)    VALUE ZEROS.               
           05 WS-F205-WRITE              PIC 9(7)    VALUE ZEROS.               
                                                                                
       01  WS-EOF-F120                   PIC X     VALUE 'N'.                   
       01  WS-F205-UPD                   PIC 9(7)  VALUE ZEROES.                
       01  WS-PREV-F205-REC              PIC X(80) VALUE SPACES.                
                                                                                
       01  WS-DATE                       PIC X(8)  VALUE SPACES.                
       01  WS-TODAY.                                                            
           05 WS-TODAY-CCYY              PIC 9999.                              
           05 WS-TODAY-MM                PIC 99.                                
           05 WS-TODAY-DD                PIC 99.                                
       01  WS-DTE-ACCEPTANCE.                                                   
           05 WS-DTE-ACCP-CCYY           PIC 9999.                              
           05 WS-DTE-ACCP-MM             PIC 99.                                
           05 WS-DTE-ACCP-DD             PIC 99.                                
       01  WS-COMPUTE-MONTH              PIC 99    VALUE ZEROES.                
       01  WS-COMPUTE-YEAR               PIC 99    VALUE ZEROES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-F120        THRU 2000-EXIT.                        
           PERFORM 3000-MAIN-ROUTINE     THRU 3000-EXIT                         
                   UNTIL WS-EOF-F120 = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BM06F120                                                 
                OUTPUT BP13F200                                                 
                       BP13F205.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           IF WS-DATE NUMERIC                                                   
              MOVE WS-DATE TO WS-TODAY                                          
           ELSE                                                                 
              MOVE ZEROES  TO WS-TODAY.                                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-F120.                                                          
      *-------------------------------------------------------------            
           READ BM06F120 AT END MOVE 'Y'  TO WS-EOF-F120                        
                GO TO 2000-EXIT.                                                
           ADD 1 TO WS-F120-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 4000-PROCESS-PARA       THRU 4000-EXIT.                      
           PERFORM 2000-READ-F120          THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-PARA.                                                       
      *-------------------------------------------------------------            
           IF F120-DTE-ACCEPTANCE NUMERIC                                       
              MOVE F120-DTE-ACCEPTANCE TO WS-DTE-ACCEPTANCE                     
           ELSE                                                                 
              MOVE ZEROES              TO WS-DTE-ACCEPTANCE.                    
                                                                                
           IF F120-DTE-ALLOCN(1:6) > '199906'                                   
              IF WS-DTE-ACCP-CCYY = WS-TODAY-CCYY                               
                 COMPUTE WS-COMPUTE-MONTH =                                     
                       WS-TODAY-MM  -  WS-DTE-ACCP-MM                           
                 IF WS-COMPUTE-MONTH = 1 AND                                    
                    F120-CDE-TYP-ACTUSE = '03'                                  
                    PERFORM 5000-WRITE-BP13F200 THRU 5000-EXIT                  
                 END-IF                                                         
              ELSE                                                              
                 COMPUTE WS-COMPUTE-YEAR =                                      
                       WS-TODAY-CCYY  -  WS-DTE-ACCP-CCYY                       
                 IF WS-COMPUTE-YEAR = 1    AND                                  
                    WS-TODAY-MM = 01       AND                                  
                    WS-DTE-ACCP-MM = 12    AND                                  
                    F120-CDE-TYP-ACTUSE = '03'                                  
                    PERFORM 5000-WRITE-BP13F200 THRU 5000-EXIT                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-F205-UPD = 1                                                   
              PERFORM 6000-WRITE-BP13F205 THRU 6000-EXIT                        
              MOVE ZEROES TO WS-F205-UPD.                                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       5000-WRITE-BP13F200.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES           TO BP13F200-REC.                               
           MOVE '3R'             TO F200-NUM-NT-ZONE.                           
           MOVE '3'              TO F200-NUM-FLAT-TYPE.                         
           MOVE F120-DTE-ALLOCN  TO F200-DTE-ALLOC.                             
           MOVE F120-REGN-NO     TO F200-NUM-REGN                               
                                    F200-NUM-CUR-REGN                           
                                    F200-NUM-SEQUENCE.                          
           MOVE SPACES           TO F200-NUM-JB-QUEUE-SEQUENCE.                 
           MOVE ZERO             TO F200-NUM-HHTY-QUEUE-SERIAL.                 
           MOVE 'H'              TO F200-NUM-HHTY-QUEUE-ALPHA.                  
           WRITE BP13F200-REC.                                                  
           ADD 1                 TO WS-F200-WRITE                               
                                    WS-F205-UPD.                                
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       6000-WRITE-BP13F205.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES           TO F205-REC.                                   
           MOVE '3R'             TO F205-NUM-NT-ZONE.                           
           MOVE '3'              TO F205-NUM-FLAT-TYPE.                         
           MOVE F120-DTE-ALLOCN  TO F205-DTE-ALLOCN.                            
           MOVE 'T'              TO F205-NUM-NT-ZONE-SW.                        
           IF F205-REC NOT EQUAL WS-PREV-F205-REC                               
              MOVE F205-REC      TO WS-PREV-F205-REC                            
              WRITE F205-REC                                                    
              ADD 1              TO WS-F205-WRITE.                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY 'BP13C193 CONTROL TOTAL'.                                    
           DISPLAY '----------------------------------------'.                  
           DISPLAY 'NO OF RECORDS READ FROM F120 : ' WS-F120-READ.              
           DISPLAY 'NO OF REORDS WRITE OUT  F200 : ' WS-F200-WRITE.             
           DISPLAY 'NO OF REORDS WRITE OUT  F205 : ' WS-F205-WRITE.             
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BM06F120                                                       
                 BP13F200                                                       
                 BP13F205.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
