       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C069.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  29 JUL 2010.                                              
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE: PRINT DEBARMENT REPORT FOR SC/SPR OPTION         *           
      *            (NO DEBARMENT)                                   *           
      *-------------------------------------------------------------*           
      * INPUT FILES: BP13F310                                       *           
      *              BP13K800                                       *           
      *              BP13K820                                       *           
      *              BP04K010                                       *           
      * OUTPUT FILE: BP13L069 (REPORT)                              *           
      *-------------------------------------------------------------*           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      *--------- ---  ------  --------------------------------------*           
      * BP134054 ESA1 100728  NEW PROGRAM.                          *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT BP04K010  ASSIGN       TO BP04K010                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K010-PRIME-KEY                      
                            FILE STATUS  IS WS-K010-STATUS.                     
                                                                                
           SELECT BP13F310  ASSIGN TO BP13F310.                                 
                                                                                
           SELECT BP13L069  ASSIGN TO BP13L069.                                 
                                                                                
       DATA DIVISION.                                                           
      *--------------------------------------------------------------           
       FILE SECTION.                                                            
      *--------------------------------------------------------------           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP04K010                                                             
           RECORD CONTAINS 300 CHARACTERS.                                      
       COPY P04K010.                                                            
                                                                                
       FD  BP13F310                                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F310.                                                           
                                                                                
       FD  BP13L069                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  BP13L069-REC                   PIC X(132).                           
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-WORK-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE         PIC X(08) VALUE SPACES.                  
               10  FILLER              PIC X(13) VALUE SPACES.                  
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-MM          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-CCYY        PIC X(04) VALUE SPACES.                  
           05  WS-SUB                  PIC 9(02) VALUE ZEROES.                  
           05  WS-K010-PRIME-KEY       PIC X(09) VALUE SPACES.                  
           05  WS-K820-TABLE           OCCURS 12.                               
               10  WS-K820-NUM-NRIC    PIC X(09).                               
               10  WS-K820-NME-OCCP    PIC X(66).                               
               10  WS-K820-APPL        PIC X(03).                               
           05  WS-RETCODE              PIC -ZZZZ.                               
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K010-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F310-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-F310       VALUE 'Y'.                               
           05  WS-K800-REGN-FND-SW     PIC X(01) VALUE SPACES.                  
               88  C-K800-REGN-FND     VALUE 'Y'.                               
           05  WS-END-OF-K820-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-K820       VALUE 'Y'.                               
           05  WS-END-OF-K010-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-K010       VALUE 'Y'.                               
           05  WS-PRT-REPORT-SW        PIC X(01) VALUE SPACES.                  
               88  C-PRT-REPORT        VALUE 'Y'.                               
           05  WS-SAVE-FLAG            PIC X(01) VALUE 'N'.                     
           05  WS-SC-FND               PIC X(01) VALUE 'N'.                     
           05  WS-SP-FND               PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F310-READ-CTR        PIC 9(06) VALUE ZEROES.                  
           05  WS-K800-READ-CTR        PIC 9(06) VALUE ZEROES.                  
           05  WS-PRT-CTR              PIC 9(06) VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(02) VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(04) VALUE ZEROES.                  
           05  WS-SNO-CTR              PIC 9(04) VALUE ZEROES.                  
           05  WS-SC-CTR               PIC 9(04) VALUE ZEROES.                  
           05  WS-SP-CTR               PIC 9(04) VALUE ZEROES.                  
           05  WS-NON-SP-CTR           PIC 9(04) VALUE ZEROES.                  
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01 L069-HEADER-1.                                                        
          05 FILLER              PIC X(08)  VALUE 'BP13L069'.                   
          05 FILLER              PIC X(05)  VALUE SPACES .                      
          05 FILLER              PIC X(08)  VALUE 'HDBCAT3'.                    
          05 FILLER              PIC X(22)  VALUE SPACES.                       
          05 FILLER              PIC X(40)  VALUE                               
             'S Y S T E M   O F   C O M M I T M E N T'.                         
          05 FILLER              PIC X(28)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE 'DATE :'.                     
          05 L069-DATE           PIC X(10)  VALUE SPACES.                       
                                                                                
       01 L069-HEADER-2.                                                        
          05 FILLER              PIC X(30)  VALUE SPACES.                       
          05 FILLER              PIC X(35)  VALUE                               
             'DEBARMENT REPORT FOR SC/SPR OPTION'.                              
          05 FILLER              PIC X(31)  VALUE                               
             '- MISSING / INCORRECT DEBARMENT'.                                 
          05 FILLER              PIC X(15)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE 'PAGE : '.                    
          05 L069-PAGE           PIC ZZZ9 .                                     
                                                                                
       01 L069-HEADER-2A.                                                       
          05 FILLER              PIC X(10)  VALUE SPACES .                      
          05 FILLER              PIC X(45)  VALUE                               
             'S/NO          NT           FT          QUEUE'.                    
          05 FILLER              PIC X(10)  VALUE SPACES .                      
          05 FILLER              PIC X(47)  VALUE                               
             'HH TYPE           HH CITIZENSHIP     SC/SPR OPT'.                 
          05 FILLER              PIC X(20)  VALUE                               
             '        BALLOT QTR  '.                                            
                                                                                
       01 L069-HEADER-3.                                                        
          05 FILLER              PIC X(24)  VALUE SPACES .                      
          05 FILLER              PIC X(35)  VALUE                               
             'REGN         NAME OF APPLICANTS '.                                
          05 FILLER              PIC X(09)  VALUE SPACES .                      
          05 FILLER              PIC X(38)  VALUE                               
             'NRIC                 APPL     '.                                  
          05 FILLER              PIC X(15)  VALUE SPACES .                      
                                                                                
       01 L069-HEADER-3A.                                                       
          05 FILLER              PIC X(15)  VALUE SPACES .                      
          05 FILLER              PIC X(20)  VALUE                               
             '         SCH-ACC-NO '.                                            
          05 FILLER              PIC X(92) VALUE SPACES .                       
                                                                                
       01 L069-DETAIL-0.                                                        
          10 FILLER              PIC X(10)  VALUE SPACES.                       
          10 L069-SNO            PIC ZZ99.                                      
          10 FILLER              PIC X(10)  VALUE SPACES.                       
          10 L069-NEW-TOWN       PIC X(03)  VALUE SPACES.                       
          10 FILLER              PIC X(10)  VALUE SPACES.                       
          10 L069-FLAT-TYPE      PIC X(02)  VALUE SPACES.                       
          10 FILLER              PIC X(10)  VALUE SPACES.                       
          10 L069-QUEUE          PIC X(05)  VALUE SPACES.                       
          10 FILLER              PIC X(14)  VALUE SPACES.                       
          10 L069-HH             PIC X(01)  VALUE SPACES.                       
          10 FILLER              PIC X(20)  VALUE SPACES.                       
          10 L069-HH-CITZ        PIC X(03)  VALUE SPACES.                       
          10 FILLER              PIC X(14)  VALUE SPACES.                       
          10 L069-SPR-OPT        PIC X(01)  VALUE SPACES.                       
          10 FILLER              PIC X(15)  VALUE SPACES.                       
          10 L069-BALLOT-QTR     PIC X(07)  VALUE SPACES.                       
                                                                                
       01 L069-DETAIL.                                                          
          05 FILLER              PIC X(15)  VALUE SPACES.                       
          05 L069-HDB-REF.                                                      
             10 FILLER           PIC X(06).                                     
             10 L069-SCH-ACC     PIC X(11).                                     
          05 L069-SEQ-REGN  REDEFINES L069-HDB-REF.                             
             10 FILLER           PIC X(09).                                     
             10 L069-NUM-REGN    PIC X(08).                                     
          05 FILLER              PIC X(05)  VALUE SPACES.                       
          05 L069-NME-APPL       PIC X(27)  VALUE SPACES.                       
          05 FILLER              PIC X(04)  VALUE SPACES.                       
          05 L069-NUM-NRIC-APPL  PIC X(09)  VALUE SPACES.                       
          05 FILLER              PIC X(12)  VALUE SPACES.                       
          05 L069-APPL           PIC X(03)  VALUE SPACES.                       
          05 FILLER              PIC X(52)  VALUE SPACES.                       
                                                                                
       01  L069-LINE             PIC X(132)    VALUE ALL '-'.                   
                                                                                
      *--------------------------------------------------------------           
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------           
                                                                                
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
             UNTIL C-END-OF-F310.                                               
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BP13F310                                               
                         BP13K800                                               
                         BP13K820                                               
                         BP04K010                                               
                OUTPUT   BP13L069.                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP04K010-STATUS ' WS-K010-STATUS              
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           MOVE   ZEROES           TO WS-SC-CTR WS-SP-CTR                       
                                      WS-NON-SP-CTR.                            
                                                                                
           READ BP13F310 AT END                                                 
                MOVE 'Y'     TO WS-END-OF-F310-SW                               
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F310-READ-CTR.                                         
                                                                                
           MOVE SPACES          TO WS-K800-REGN-FND-SW.                         
           MOVE SPACES          TO BP13K800-MASTER.                             
           INITIALIZE              BP13K800-MASTER.                             
           MOVE F310-REGN-NO    TO K800-NUM-REGN.                               
           PERFORM 3700-READ-BP13K800         THRU 3700-EXIT.                   
           IF C-K800-REGN-FND                                                   
              IF (K800-NUM-SCSPR-OPT = 'Y' OR 'N') AND                          
                 (K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES) AND             
                 (K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCH' OR 'SCP')              
                 ADD 1        TO WS-K800-READ-CTR                               
                 PERFORM 3100-CHECK-RECORD         THRU 3100-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3100-CHECK-RECORD.                                                       
      *--------------------------------*                                        
                                                                                
           MOVE SPACES                   TO WS-END-OF-K820-SW                   
                                            WS-PRT-REPORT-SW.                   
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 12                 
              MOVE SPACES        TO WS-K820-NME-OCCP (WS-SUB)                   
                                    WS-K820-NUM-NRIC (WS-SUB)                   
                                    WS-K820-APPL(WS-SUB)                        
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES                   TO WS-SUB.                             
                                                                                
           MOVE SPACES                   TO K820-KEY-FLD.                       
           MOVE K800-NUM-REGN            TO K820-NUM-REGN.                      
           PERFORM 3110-START-BP13K820      THRU 3110-EXIT.                     
           PERFORM 3120-READNXT-BP13K820    THRU 3120-EXIT                      
             UNTIL K800-NUM-REGN NOT = K820-NUM-REGN                            
                OR C-END-OF-K820 OR WS-SUB > 12.                                
                                                                                
           IF K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCP'                            
              IF WS-NON-SP-CTR > 0                                              
                 MOVE 'Y'        TO WS-PRT-REPORT-SW                            
              END-IF                                                            
           ELSE                                                                 
              IF K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCH'                         
                 IF WS-SC-CTR < 2                                               
                    MOVE 'Y'     TO WS-PRT-REPORT-SW                            
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF C-PRT-REPORT                                                      
              ADD 1   TO WS-PRT-CTR                                             
              PERFORM 3300-PRINT-REPORT       THRU 3300-EXIT                    
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3110-START-BP13K820.                                                     
      *--------------------------------*                                        
                                                                                
           START BP13K820 KEY NOT LESS THAN K820-KEY-FLD.                       
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                   CONTINUE                                                     
              WHEN 10                                                           
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY: '                
                            K820-KEY-FLD                                        
                   MOVE 'Y'        TO WS-END-OF-K820-SW                         
                   MOVE SPACES     TO BP13K820-REC                              
              WHEN OTHER                                                        
                   DISPLAY 'START ERROR BP13K820,STATUS ' WS-K820-STATUS        
                   MOVE WS-K820-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3120-READNXT-BP13K820.                                                   
      *--------------------------------*                                        
           MOVE 'N'          TO WS-SC-FND WS-SP-FND.                            
                                                                                
           READ BP13K820 NEXT AT END                                            
                MOVE 'Y'     TO WS-END-OF-K820-SW                               
                GO TO 3120-EXIT.                                                
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                   IF K800-NUM-REGN = K820-NUM-REGN                             
                      PERFORM 3200-CHECK-BP04K010    THRU 3200-EXIT             
                   END-IF                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                   MOVE 'Y'       TO WS-END-OF-K820-SW                          
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K820,STATUS ' WS-K820-STATUS         
                   MOVE WS-K820-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3200-CHECK-BP04K010.                                                     
      *--------------------------------*                                        
                                                                                
           MOVE SPACES         TO WS-END-OF-K010-SW.                            
                                                                                
           MOVE SPACES         TO K010-PRIME-KEY.                               
           MOVE K820-NUM-NRIC  TO K010-PRIME-KEY(1:9).                          
           PERFORM 3210-START-BP04K010      THRU 3210-EXIT.                     
           PERFORM 3220-READNXT-BP04K010    THRU 3220-EXIT                      
             UNTIL K820-NUM-NRIC NOT = K010-PRIME-KEY(1:9)                      
                OR C-END-OF-K010.                                               
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------*                                        
       3210-START-BP04K010.                                                     
      *--------------------------------*                                        
                                                                                
           MOVE SPACES     TO WS-K010-PRIME-KEY.                                
                                                                                
           START BP04K010 KEY NOT LESS THAN K010-PRIME-KEY.                     
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                   CONTINUE                                                     
              WHEN 10                                                           
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP04K010, KEY: '                
                            K010-PRIME-KEY                                      
                   MOVE 'Y'        TO WS-END-OF-K010-SW                         
                   MOVE SPACES     TO P04K010-REC                               
              WHEN OTHER                                                        
                   DISPLAY 'START ERROR BP04K010,STATUS ' WS-K010-STATUS        
                   MOVE WS-K010-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3220-READNXT-BP04K010.                                                   
      *--------------------------------*                                        
                                                                                
           READ BP04K010 NEXT AT END                                            
                MOVE 'Y'     TO WS-END-OF-K010-SW                               
                GO TO 3220-EXIT.                                                
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                   IF K820-NUM-NRIC = K010-PRIME-KEY(1:9)                       
                      IF K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCP'                 
                         IF K010-REASON-CODE = 'SP'                             
                            ADD  1           TO      WS-SP-CTR                  
                            MOVE 'Y'         TO      WS-SP-FND                  
                         END-IF                                                 
                      ELSE                                                      
                         IF K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCH'              
                            IF K010-REASON-CODE = 'SC'                          
                               ADD 1      TO      WS-SC-CTR                     
                               MOVE 'Y'   TO      WS-SC-FND                     
                            END-IF                                              
                         END-IF                                                 
                      END-IF                                                    
                   ELSE                                                         
                      IF K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCP' AND             
                         WS-SP-FND = 'N'                                        
                         ADD  1           TO      WS-NON-SP-CTR                 
                         PERFORM 3230-SAVE-K820 THRU 3230-EXIT                  
                      ELSE                                                      
                         IF K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCH' AND          
                            WS-SC-FND = 'N'                                     
                            PERFORM 3230-SAVE-K820 THRU 3230-EXIT               
                         END-IF                                                 
                      END-IF                                                    
                      MOVE 'Y'       TO WS-END-OF-K010-SW                       
                   END-IF                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                   IF K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCP' AND                
                      WS-SP-FND = 'N'                                           
                      ADD  1           TO      WS-NON-SP-CTR                    
                      PERFORM 3230-SAVE-K820 THRU 3230-EXIT                     
                   ELSE                                                         
                      IF K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCH' AND             
                         WS-SC-FND = 'N'                                        
                         PERFORM 3230-SAVE-K820 THRU 3230-EXIT                  
                      END-IF                                                    
                   END-IF                                                       
                   MOVE 'Y'       TO WS-END-OF-K010-SW                          
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP04K010,STATUS ' WS-K010-STATUS         
                   MOVE WS-K010-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3230-SAVE-K820.                                                          
      *--------------------------------*                                        
                                                                                
           IF WS-SUB >= 1                                                       
              IF WS-K820-NUM-NRIC (WS-SUB) NOT = K820-NUM-NRIC                  
                 MOVE 'Y'         TO WS-SAVE-FLAG                               
              ELSE                                                              
                 MOVE 'N'         TO WS-SAVE-FLAG                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-SUB = 0 OR WS-SAVE-FLAG = 'Y'                                  
              ADD 1       TO WS-SUB                                             
              MOVE K820-NUM-NRIC     TO WS-K820-NUM-NRIC (WS-SUB)               
              MOVE K820-NME-OCCP     TO WS-K820-NME-OCCP (WS-SUB)               
                                                                                
              IF K820-NUM-NRIC = K800-NUM-NRIC1 OR                              
                 K820-NUM-NRIC = K800-NUM-NRIC2 OR                              
                 K820-NUM-NRIC = K800-NUM-NRIC3 OR                              
                 K820-NUM-NRIC = K800-NUM-NRIC4                                 
                 MOVE 'HA '          TO WS-K820-APPL (WS-SUB)                   
              ELSE                                                              
                 MOVE 'OCC'          TO WS-K820-APPL (WS-SUB)                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3230-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3300-PRINT-REPORT.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 3400-CHECK-LINE-CTR          THRU 3400-EXIT.                 
                                                                                
           MOVE SPACES    TO L069-DETAIL.                                       
           WRITE BP13L069-REC  FROM L069-DETAIL.                                
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
           PERFORM 3400-CHECK-LINE-CTR          THRU 3400-EXIT.                 
                                                                                
           MOVE SPACES                TO L069-DETAIL.                           
           ADD 1                      TO WS-SNO-CTR.                            
           MOVE WS-SNO-CTR            TO L069-SNO.                              
           MOVE K800-NUM-NEW-TOWN     TO L069-NEW-TOWN.                         
           MOVE K800-NUM-FLAT-TYPE    TO L069-FLAT-TYPE.                        
           MOVE K800-NUM-NT-FT-QUEUE  TO L069-QUEUE.                            
           MOVE K800-NUM-HOUSEHOLD    TO L069-HH.                               
           MOVE K800-CDE-CITIZENSHIP-HOUSEHOLD                                  
                                      TO L069-HH-CITZ.                          
           MOVE K800-NUM-SCSPR-OPT    TO L069-SPR-OPT.                          
           STRING K800-DTE-BALLOT(1:4) '/'                                      
                  K800-DTE-BALLOT(5:2)                                          
                  DELIMITED BY SIZE INTO L069-BALLOT-QTR.                       
                                                                                
           WRITE BP13L069-REC  FROM L069-DETAIL-0.                              
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
           PERFORM 3400-CHECK-LINE-CTR          THRU 3400-EXIT.                 
                                                                                
           MOVE SPACES                TO L069-DETAIL.                           
                                                                                
           MOVE K800-NUM-REGN         TO L069-NUM-REGN.                         
           MOVE WS-K820-NUM-NRIC(1)   TO L069-NUM-NRIC-APPL.                    
           MOVE WS-K820-NME-OCCP(1)   TO L069-NME-APPL.                         
           MOVE WS-K820-APPL(1)       TO L069-APPL.                             
                                                                                
           WRITE BP13L069-REC  FROM L069-DETAIL.                                
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
      **-- GET NAME OF OCCUPIER (2)                                             
           MOVE SPACES                   TO L069-DETAIL.                        
           MOVE K800-NUM-SCH-ACC         TO L069-SCH-ACC.                       
           MOVE WS-K820-NUM-NRIC(2)      TO L069-NUM-NRIC-APPL.                 
           MOVE WS-K820-NME-OCCP(2)      TO L069-NME-APPL.                      
           MOVE WS-K820-APPL(2)          TO L069-APPL.                          
                                                                                
           WRITE BP13L069-REC  FROM L069-DETAIL.                                
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
      **-- GET REMAINING OCCUPIER                                               
           MOVE SPACES                   TO L069-DETAIL                         
           PERFORM VARYING WS-SUB FROM 3 BY 1 UNTIL WS-SUB > 12                 
              IF WS-K820-NUM-NRIC(WS-SUB) NOT = SPACES AND LOW-VALUES           
                 PERFORM 3400-CHECK-LINE-CTR      THRU 3400-EXIT                
                 MOVE SPACES                   TO L069-DETAIL                   
                 MOVE WS-K820-NUM-NRIC(WS-SUB) TO L069-NUM-NRIC-APPL            
                 MOVE WS-K820-NME-OCCP(WS-SUB) TO L069-NME-APPL                 
                 MOVE WS-K820-APPL(WS-SUB)     TO L069-APPL                     
                 WRITE BP13L069-REC  FROM L069-DETAIL                           
                 ADD 1          TO WS-LINE-CTR                                  
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3400-CHECK-LINE-CTR.                                                     
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 55  OR                                              
              WS-LINE-CTR = 0                                                   
              PERFORM 3500-PRINT-PAGE-BREAK   THRU 3500-EXIT                    
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3500-PRINT-PAGE-BREAK.                                                   
      *--------------------------------*                                        
                                                                                
           ADD   1   TO WS-PAGE-CTR.                                            
           MOVE WS-PAGE-CTR    TO L069-PAGE.                                    
           MOVE WS-DISP-DATE   TO L069-DATE.                                    
           WRITE BP13L069-REC  FROM L069-HEADER-1 AFTER PAGE.                   
           WRITE BP13L069-REC  FROM L069-HEADER-2.                              
           WRITE BP13L069-REC  FROM L069-LINE    AFTER 2.                       
           WRITE BP13L069-REC  FROM L069-HEADER-2A.                             
           WRITE BP13L069-REC  FROM L069-HEADER-3.                              
           WRITE BP13L069-REC  FROM L069-HEADER-3A.                             
           WRITE BP13L069-REC  FROM L069-LINE.                                  
           MOVE  8        TO   WS-LINE-CTR.                                     
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3700-READ-BP13K800.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                   MOVE 'Y'      TO WS-K800-REGN-FND-SW                         
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K800, KEY: '                
                            K800-NUM-REGN                                       
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K800,STATUS ' WS-K800-STATUS         
                   MOVE WS-K800-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C069'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BP13F310 RECORDS READ       : ' WS-F310-READ-CTR.           
           DISPLAY 'BP13K800 RECORDS READ(SP/SC): ' WS-K800-READ-CTR.           
           DISPLAY 'NO. OF RECORDS PRINTED      : ' WS-PRT-CTR.                 
                                                                                
           CLOSE BP13K820                                                       
                 BP13K820                                                       
                 BP13K800                                                       
                 BP04K010                                                       
                 BP13F310                                                       
                 BP13L069.                                                      
                                                                                
            IF WS-K820-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K820-STATUS ' WS-K820-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K800-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K800-STATUS ' WS-K800-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K820-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K820-STATUS ' WS-K820-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K010-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP04K010-STATUS ' WS-K010-STATUS            
            END-IF.                                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
