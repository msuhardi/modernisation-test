       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C106.                                                 
      *AUTHOR.        CHEE K C.                                                 
      *DATE-WRITTEN.  04/05/89.                                                 
      * ======================================================= *               
      *               SYSTEM OF COMMITMENT  (BP13)              *               
      * ======================================================= *               
      *    OBJECTIVE  :                                         *               
      *        1.  THIS PROGRAMME WILL CHECK THE TOTAL BALANCE  *               
      *            IN SOC MASTER FILE BP13K800 AND THE BALAN-   *               
      *            CING FILE BP13K160. IF THE BALANCE DO NOT    *               
      *            TALLY, A RETURN CODE OF 99 WILL BE MOVED TO  *               
      *            THE CONDITION CODE.                          *               
      *                                                         *               
      *     INPUT FILES:                                        *               
      *           1. BP13K800                                   *               
      *           2. BP13K160                                   *               
      *                                                         *               
      *     OUTPUT FILES:                                       *               
      *           NONE.                                         *               
      *                                                         *               
      *     LISTING:                                            *               
      *           1. BP13L106 (REPORT)                          *               
      *           2. BP13P106 (CONTROL LIST)                    *               
      *                                                         *               
      * ======================================================= *               
      * CHG-NO   BY  ON      DESCRIPTION                        *               
      * -------- --- ------  -----------                        *               
      * SOC-PH8  JEP 930709  USE BP13K800 FILE INSTEAD OF       *               
      *                      P10K020  FILE.                     *               
      * SOC-PH9  YPH 941209  EXCLUDE BALANCING OF REGN FEE      *               
      * BP130020 SSS 951012  REPOSITION REPORT-ID TO COL 1      *               
      * SOC-PH8F EG1 951203  INCLUDE K885 IN THE INPUT PROCESSING               
      * BP130103 EG1 960316  TO CATER FOR SOC ENHANCEMENT.      *               
      * BP130320 SGK 970618  TO CATER FOR RDE.                  *               
      * BP130418 NOR 980711  MAKE PGRM Y2K COMPLAINT            *               
      * BP130719 SRN 990810  TO DISPLAY TOTAL OF SOC POSTED AND *               
      *                      BALANCE                            *               
      * ======================================================= *               
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
      ******************************************************************        
      *   BP13K800 IS READ SEQUENTIALLY FROM THE BEGINNING TO THE END. *        
      *   THE FIELDS REGN-FEE-PAID,BOOK-FEE-PAID,SOC-DEBTOR AND        *        
      *   COMMITTMENT-DEPOSIT WILL BE TOTALLED.                        *        
      ******************************************************************        
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ACCESS MODE  IS SEQUENTIAL                           
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS K800-STATUS.                         
                                                                                
      ******************************************************************        
      *   BP13K885 IS READ SEQUENTIALLY FROM THE BEGINNING TO THE END. *        
      *   THE FIELDS AMT-CD,  AMT-REFUND,  TOT-SUSPENSE                *        
      *                       WILL BE TOTALLED.                        *        
      ******************************************************************        
           SELECT BP13K885 ASSIGN       TO BP13K885                             
                           ACCESS MODE  IS SEQUENTIAL                           
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K885-KEY-FLD                         
                           FILE STATUS  IS K885-STATUS.                         
                                                                                
      ****************************************************************          
      *    FILE BP13K160 IS THE BALANCING FILE. THE FIELD K160-AMT-  *          
      *    CLOSE FOR THE DIFFERENT PAYMENT TYPES ARE MOVE TO WORKING *          
      *    STORAGE FOR LATER COMPARISON WITH TOTALS FROM BP13K800.   *          
      ****************************************************************          
           SELECT BP13K160 ASSIGN       TO BP13K160                             
                           ACCESS MODE  IS SEQUENTIAL                           
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K160-KEY-FLD                         
                           FILE STATUS  IS K160-STATUS.                         
                                                                                
      ****************************************************************          
      *   BP13L106 IS THE OUTPUT LISTING GIVING TOTALS FOR THE       *          
      *   DIFFERENT PAYMENT TYPES EXTRACTED FROM BP13K800 AND        *          
      *   BP13K160. A REPORT OF THOSE PAYMENT TYPES THAT DO NOT      *          
      *   BALANCE IS ALSO LISTED.                                    *          
      ****************************************************************          
           SELECT BP13L106 ASSIGN TO BP13L106.                                  
                                                                                
      ************************************************************              
      *    BP13P106 REPORTS THE TOTAL NUMBER OF RECORDS READ IN  *              
      *    BP13K800 AND BP13K160.                                *              
      ************************************************************              
           SELECT BP13P106 ASSIGN TO BP13P106.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K885                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K885.                                                           
                                                                                
       FD  BP13K160                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K160.                                                           
                                                                                
       FD  BP13L106                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE  IS  F                                                
           DATA RECORD     IS  BP13L106-REPORT.                                 
       01  BP13L106-REPORT                PIC X(132).                           
                                                                                
       FD  BP13P106                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE  IS  F                                                
           DATA RECORD     IS  BP13P106-CONTROL-LIST.                           
       01  BP13P106-CONTROL-LIST          PIC X(132).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  K800-STATUS                PIC 99         VALUE ZEROS.           
           05  K885-STATUS                PIC 99         VALUE ZEROS.           
           05  K160-STATUS                PIC 99         VALUE ZEROS.           
           05  WS-K800-EOF-FLAG           PIC X          VALUE 'N'.             
           05  WS-K885-EOF-FLAG           PIC X          VALUE 'N'.             
           05  WS-K160-EOF-FLAG           PIC X          VALUE 'N'.             
                                                                                
       01  WS-TOT-K800-BK-PAID            PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K800-SOC-DEBTOR         PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K800-SOC-POSTED         PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K800-SOC-RDE            PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K800-AMT-CD             PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K885-CD-EXAPPLT         PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K885-CD-EXAPPLT-TR      PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K885-AMT-REFUND         PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K885-AMT-SUSPENSE       PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K160-AMT-CLOSE-BK-FEE   PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K160-AMT-CLOSE-CD       PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K160-AMT-CLOSE-SUS      PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K160-AMT-CLOSE-DEBTOR   PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K160-AMT-CLOSE-POSTED   PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOT-K160-AMT-CLOSE-RDE      PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOTAL-CD-K800-EXAPPLT       PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOTAL-POSTED-SUSP           PIC S9(10)V99  VALUE ZEROS.           
       01  WS-TOTAL-POSTED-REFUND         PIC S9(10)V99  VALUE ZEROS.           
                                                                                
       01  WS-READ-K160-CLOSE-BK-CNT      PIC 9(5)       VALUE ZEROS.           
       01  WS-READ-K160-CLOSE-CD-CNT      PIC 9(5)       VALUE ZEROS.           
       01  WS-READ-K160-CLOSE-SUS-CNT     PIC 9(5)       VALUE ZEROS.           
       01  WS-READ-K160-CLOSE-DEBTOR-CNT  PIC 9(5)       VALUE ZEROS.           
       01  WS-READ-K160-CLOSE-POSTED-CNT  PIC 9(5)       VALUE ZEROS.           
       01  WS-READ-K160-CLOSE-RDE-CNT     PIC 9(5)       VALUE ZEROS.           
       01  WS-READ-K800-COUNT             PIC 9(5)       VALUE ZEROS.           
       01  WS-READ-K885-COUNT             PIC 9(5)       VALUE ZEROS.           
       01  WS-READ-K160-COUNT             PIC 9(5)       VALUE ZEROS.           
       01  WS-PAGE-COUNT                  PIC 9(5)       VALUE ZEROS.           
                                                                                
      ****************************************************************          
      *    DEFINITION OF SYSTEM DATE.                                *          
      ****************************************************************          
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-CCYY            PIC 9999.                             
           05  WS-CURRENT-MM              PIC 99.                               
           05  WS-CURRENT-DD              PIC 99.                               
                                                                                
      ****************************************************************          
      *    DEFINE THE REPORT TITLES                                  *          
      ****************************************************************          
       01  WS-TITLE-1.                                                          
           05  FILLER                    PIC X(8)   VALUE 'BP13L106'.           
           05  FILLER                    PIC X(47)  VALUE SPACES.               
           05  FILLER                    PIC X(20)  VALUE                       
               'SYSTEM OF COMMITMENT'.                                          
           05  FILLER                    PIC X(40)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'PAGE :'.             
           05  WS-PAGE                   PIC ZZZZ9 .                            
           05  FILLER                    PIC X(6)   VALUE SPACES.               
                                                                                
       01  WS-TITLE-2.                                                          
           05  FILLER                    PIC X(29)  VALUE SPACES.               
           05  FILLER                    PIC X(20)  VALUE SPACES.               
           05  FILLER                    PIC X(32)  VALUE                       
               'REPORT OF PROCESS BALANCING FILE'.                              
           05  FILLER                    PIC X(32)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE :'.             
           05  WS-EDITED-DATE.                                                  
               10  WS-DD                 PIC 99.                                
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-MM                 PIC 99.                                
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-CCYY               PIC 9999.                              
           05  FILLER                    PIC X(5)   VALUE SPACES.               
                                                                                
      ****************************************************************          
      *    DEFINES THE REPORT LAYOUT FOR THE TOTALS OF ALL PAYMENT   *          
      *    TYPES READ FROM FILES BP13K800 AND BP13K160 AND THE       *          
      *    STATUS.       .                                           *          
      ****************************************************************          
       01  WS-HEADER-1.                                                         
           05  FILLER                     PIC X(34)   VALUE SPACES.             
           05  FILLER                     PIC X(18)   VALUE                     
               '  TOTAL IN'.                                                    
           05  FILLER                     PIC X(5)    VALUE SPACES.             
           05  FILLER                     PIC X(18)   VALUE                     
               'CLOSING AMT'.                                                   
           05  FILLER                     PIC X(40)   VALUE SPACES.             
                                                                                
       01  WS-HEADER-2.                                                         
           05  FILLER                     PIC X(30)   VALUE                     
               'TYPE OF PAYMENT/FEES          '.                                
           05  FILLER                     PIC X(4)    VALUE SPACES.             
           05  FILLER                     PIC X(18)   VALUE                     
               'BP13K800/K885'.                                                 
           05  FILLER                     PIC X(5)    VALUE SPACES.             
           05  FILLER                     PIC X(18)   VALUE                     
               'IN BP13K160'.                                                   
           05  FILLER                     PIC X(1)    VALUE SPACES.             
           05  FILLER                     PIC X(6)    VALUE 'STATUS'.           
           05  FILLER                     PIC X(30)   VALUE SPACES.             
                                                                                
      ******************************************************************        
      * N940017 SOC-PH9 EXCLUDE REGN FEE                                        
      *-----------------------------------------------------------------        
      *01  WS-DETAIL-LINE-1.                                                    
      *    05  FILLER                     PIC X(30)   VALUE                     
      *        'REGISTRATION FEE PAID         '.                                
      *    05  FILLER                     PIC X(1)    VALUE SPACES.             
      *    05  L106-REGN-FEE-SOC          PIC Z,ZZZ,ZZZ,ZZ9.99DB.               
      *    05  FILLER                     PIC X(17)   VALUE SPACES.             
      *    05  L106-REGN-BAL              PIC Z,ZZZ,ZZZ,ZZ9.99DB.               
      *    05  FILLER                     PIC X(14)   VALUE SPACES.             
      *    05  WS-STATUS-1                PIC X(2)    VALUE '**'.               
      *    05  FILLER                     PIC X(32)   VALUE SPACES.             
      ***************************************************************           
       01  WS-DETAIL-LINE-2.                                                    
           05  FILLER                     PIC X(30)   VALUE                     
               '1. BOOKING FEE PAID           '.                                
           05  FILLER                     PIC X(1)    VALUE SPACES.             
           05  L106-BK-FEE-SOC            PIC Z,ZZZ,ZZZ,ZZ9.99DB.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  L106-BK-BAL                PIC Z,ZZZ,ZZZ,ZZ9.99DB.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  WS-STATUS-2                PIC X(2)    VALUE '**'.               
           05  FILLER                     PIC X(32)   VALUE SPACES.             
                                                                                
       01  WS-DETAIL-LINE-3-A.                                                  
           05  FILLER                     PIC X(30)   VALUE                     
               '2. COMMITMENT DEPOSIT PAID    '.                                
           05  FILLER                     PIC X(1)    VALUE SPACES.             
           05  L106-AMT-CD-SOC            PIC Z,ZZZ,ZZZ,ZZ9.99DB.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  L106-CD-BAL                PIC Z,ZZZ,ZZZ,ZZ9.99DB.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  WS-STATUS-3                PIC X(2)    VALUE '**'.               
           05  FILLER                     PIC X(32)   VALUE SPACES.             
                                                                                
       01  WS-DETAIL-LINE-3-B.                                                  
           05  FILLER                     PIC X(30)   VALUE                     
               '                        (K885)'.                                
           05  FILLER                     PIC X(1)    VALUE SPACES.             
           05  L106-AMT-CD-EXAPPLT        PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(84)   VALUE SPACES.             
                                                                                
       01  WS-DETAIL-LINE-4.                                                    
           05  FILLER                     PIC X(30)   VALUE                     
               '3. SOC DEBTOR                 '.                                
           05  FILLER                     PIC X(1)    VALUE SPACES.             
           05  L106-SOC-DEBTOR-SOC        PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  L106-SOC-DEBTOR-BAL        PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  WS-STATUS-4                PIC X(2)    VALUE '**'.               
           05  FILLER                     PIC X(32)   VALUE SPACES.             
                                                                                
       01  WS-DETAIL-LINE-5-A.                                                  
           05  FILLER                     PIC X(30)   VALUE                     
               '4. SOC POSTED                 '.                                
           05  FILLER                     PIC X(1)    VALUE SPACES.             
           05  L106-SOC-POSTED-SOC        PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  L106-SOC-POSTED-BAL        PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  FILLER                     PIC X(34)   VALUE SPACES.             
                                                                                
       01  WS-DETAIL-LINE-5-B.                                                  
           05  FILLER                     PIC X(30)   VALUE                     
               '5.                      (K885)'.                                
           05  FILLER                     PIC X(1)    VALUE SPACES.             
           05  L106-AMT-REFUND            PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  L106-AMT-SUS-BAL           PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(48)   VALUE SPACES.             
                                                                                
       01  WS-DETAIL-LINE-5-C.                                                  
           05  FILLER                     PIC X(30)   VALUE                     
               '6.          (K885 CD-TRANSFER)'.                                
           05  FILLER                     PIC X(1)    VALUE SPACES.             
           05  L106-AMT-CD-TRANSFER       PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(17)   VALUE SPACES.             
           05  FILLER                     PIC X(18)   VALUE SPACES.             
           05  FILLER                     PIC X(48)   VALUE SPACES.             
                                                                                
       01  WS-DETAIL-LINE-5-D.                                                  
           05  FILLER                     PIC X(30)   VALUE                     
               '7. TOTAL (4 + 5 + 6)          '.                                
           05  FILLER                     PIC X(1)    VALUE SPACES.             
           05  L106-AMT-SOC-TOT           PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  L106-AMT-BAL-TOT           PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  WS-STATUS-5                PIC X(2)    VALUE '**'.               
           05  FILLER                     PIC X(32)   VALUE SPACES.             
                                                                                
       01  WS-DETAIL-LINE-6.                                                    
           05  FILLER                     PIC X(30)   VALUE                     
               '8. SUSPENSE  AMOUNT     (K885)'.                                
           05  FILLER                     PIC X(1)    VALUE SPACES.             
           05  L106-AMT-SUSPENSE          PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  L106-SUS-BAL               PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  WS-STATUS-6                PIC X(2)    VALUE '**'.               
           05  FILLER                     PIC X(32)   VALUE SPACES.             
                                                                                
       01  WS-DETAIL-LINE-7.                                                    
           05  FILLER                     PIC X(30)   VALUE                     
               '9. REGN.DEPOSIT AMOUNT        '.                                
           05  FILLER                     PIC X(1)    VALUE SPACES.             
           05  L106-AMT-RDE               PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  L106-RDE-BAL               PIC Z,ZZZ,ZZZ,ZZ9.99CR.               
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  WS-STATUS-7                PIC X(2)    VALUE '**'.               
           05  FILLER                     PIC X(32)   VALUE SPACES.             
                                                                                
       01  WS-FOOTER-LINE-1.                                                    
           05  FILLER                     PIC X(11)   VALUE SPACES.             
           05  FILLER                     PIC X(49)   VALUE                     
               'NOTE : STATUS OF ** IMPLIES THE ACCOUNTS BALANCES'.             
           05  FILLER                     PIC X(72)   VALUE SPACES.             
                                                                                
       01  WS-FOOTER-LINE-2.                                                    
           05  FILLER                     PIC X(18)   VALUE SPACES.             
           05  FILLER                     PIC X(48)   VALUE                     
               'STATUS OF 99 IMPLIES THE ACCOUNTS DO NOT BALANCE'.              
           05  FILLER                     PIC X(66)   VALUE SPACES.             
                                                                                
      ********************************************************                  
      *    DEFINES REPORT LAYOUT FOR CONTROL LIST.           *                  
      *    FOR CSD INTERNAL USE ONLY.                        *                  
      ********************************************************                  
       01  WS-CONTROL-LIST-1.                                                   
           05  FILLER                  PIC X(19)   VALUE SPACES.                
           05  FILLER                  PIC X(44)   VALUE                        
               'NUMBER OF RECORDS READ FROM FILE BP13K800 IS'.                  
           05  FILLER                  PIC X(24)   VALUE SPACES.                
           05  P106-K800-CNT           PIC ZZZZ9 .                              
           05  FILLER                  PIC X(40)   VALUE SPACES.                
                                                                                
       01  WS-CONTROL-LIST-2.                                                   
           05  FILLER                  PIC X(19)   VALUE SPACES.                
           05  FILLER                  PIC X(44)   VALUE                        
               'NUMBER OF RECORDS READ FROM FILE BP13K885 IS'.                  
           05  FILLER                  PIC X(24)   VALUE SPACES.                
           05  P106-K885-CNT           PIC ZZZZ9 .                              
           05  FILLER                  PIC X(40)   VALUE SPACES.                
                                                                                
       01  WS-CONTROL-LIST-3.                                                   
           05  FILLER                  PIC X(19)   VALUE SPACES.                
           05  FILLER                  PIC X(44)   VALUE                        
               'NUMBER OF RECORDS READ FROM FILE BP13K160 IS'.                  
           05  FILLER                  PIC X(24)   VALUE SPACES.                
           05  P106-K160-CNT           PIC ZZZZ9 .                              
           05  FILLER                  PIC X(40)   VALUE SPACES.                
                                                                                
       01  WS-CONTROL-LIST-5.                                                   
           05  FILLER                  PIC X(29)   VALUE SPACES.                
           05  FILLER                  PIC X(45)   VALUE                        
               'NUMBER OF CLOSING BALANCE BOOKING FEE READ IS'.                 
           05  FILLER                  PIC X(13)   VALUE SPACES.                
           05  P106-BAL-BK-CNT         PIC ZZZZ9 .                              
           05  FILLER                  PIC X(40)   VALUE SPACES.                
                                                                                
       01  WS-CONTROL-LIST-6.                                                   
           05  FILLER                  PIC X(29)   VALUE SPACES.                
           05  FILLER                  PIC X(53)   VALUE                        
               'NUMBER OF CLOSING BALANCE COMMITTMENT DEPOSIT READ IS'.         
           05  FILLER                  PIC X(5)    VALUE SPACES.                
           05  P106-BAL-CD-CNT         PIC ZZZZ9 .                              
           05  FILLER                  PIC X(40)   VALUE SPACES.                
                                                                                
       01  WS-CONTROL-LIST-7.                                                   
           05  FILLER                  PIC X(29)   VALUE SPACES.                
           05  FILLER                  PIC X(44)   VALUE                        
               'NUMBER OF CLOSING BALANCE SOC DEBTOR READ IS'.                  
           05  FILLER                  PIC X(14)   VALUE SPACES.                
           05  P106-BAL-DEBTOR-CNT     PIC ZZZZ9 .                              
           05  FILLER                  PIC X(40)   VALUE SPACES.                
                                                                                
       01  WS-CONTROL-LIST-8.                                                   
           05  FILLER                  PIC X(29)   VALUE SPACES.                
           05  FILLER                  PIC X(44)   VALUE                        
               'NUMBER OF CLOSING BALANCE SOC POSTED READ IS'.                  
           05  FILLER                  PIC X(14)   VALUE SPACES.                
           05  P106-BAL-POSTED-CNT     PIC ZZZZ9 .                              
           05  FILLER                  PIC X(40)   VALUE SPACES.                
                                                                                
       01  WS-CONTROL-LIST-9.                                                   
           05  FILLER                  PIC X(29)   VALUE SPACES.                
           05  FILLER                  PIC X(44)   VALUE                        
               'NUMBER OF CLOSING BALANCE SUSPENSE   READ IS'.                  
           05  FILLER                  PIC X(14)   VALUE SPACES.                
           05  P106-BAL-SUS-CNT        PIC ZZZZ9 .                              
           05  FILLER                  PIC X(40)   VALUE SPACES.                
                                                                                
       01  WS-CONTROL-LIST-10.                                                  
           05  FILLER                  PIC X(29)   VALUE SPACES.                
           05  FILLER                  PIC X(44)   VALUE                        
               'NUMBER OF CLOSING BALANCE RDE        READ IS'.                  
           05  FILLER                  PIC X(14)   VALUE SPACES.                
           05  P106-BAL-RDE-CNT        PIC ZZZZ9 .                              
           05  FILLER                  PIC X(40)   VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      *        MAIN LINE PROCESS                                       *        
      ******************************************************************        
       000-MAIN.                                                                
           PERFORM 50-OPEN-FILE               THRU 50-EXIT.                     
           PERFORM 100-SET-READ-K800-POINTER  THRU 100-EXIT.                    
           PERFORM 200-READ-K800              THRU 200-EXIT                     
                   UNTIL WS-K800-EOF-FLAG = 'Y'.                                
           PERFORM 110-SET-READ-K885-POINTER  THRU 110-EXIT.                    
           PERFORM 300-READ-K885              THRU 300-EXIT                     
                   UNTIL WS-K885-EOF-FLAG = 'Y'.                                
           PERFORM 500-SET-READ-K160-POINTER  THRU 500-EXIT.                    
           PERFORM 600-READ-K160              THRU 600-EXIT                     
                   UNTIL WS-K160-EOF-FLAG = 'Y'.                                
           PERFORM 700-PRINT-TITLE            THRU 700-EXIT.                    
           PERFORM 800-PRINT-HEADER           THRU 800-EXIT.                    
           PERFORM 900-PRINT-DETAIL-LINE      THRU 900-EXIT.                    
           PERFORM 1000-PRINT-CONTROL-LIST    THRU 1000-EXIT.                   
           PERFORM 1100-CLOSE-ROUTINE         THRU 1100-EXIT.                   
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***************************************************************           
      *    OPENING FILES SECTION                                    *           
      ***************************************************************           
       50-OPEN-FILE.                                                            
           OPEN INPUT  BP13K800                                                 
                       BP13K885                                                 
                       BP13K160                                                 
                OUTPUT BP13L106                                                 
                       BP13P106.                                                
                                                                                
           IF K800-STATUS NOT = 00 AND                                          
              K800-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13K800 STATUS ' K800-STATUS            
              MOVE K800-STATUS  TO RETURN-CODE                                  
              PERFORM 1100-CLOSE-ROUTINE.                                       
                                                                                
           IF K885-STATUS NOT = 00 AND                                          
              K885-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13K885 STATUS ' K885-STATUS            
              MOVE K885-STATUS  TO RETURN-CODE                                  
              PERFORM 1100-CLOSE-ROUTINE.                                       
                                                                                
           IF K160-STATUS NOT = 00 AND                                          
              K160-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13K160 STATUS ' K160-STATUS            
              MOVE K160-STATUS  TO RETURN-CODE                                  
              PERFORM 1100-CLOSE-ROUTINE.                                       
                                                                                
       50-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    SET READ POINTER FOR FILE BP13K800 TO THE BEGINING         *         
      *****************************************************************         
       100-SET-READ-K800-POINTER.                                               
                                                                                
           MOVE LOW-VALUES    TO K800-NUM-REGN                                  
           START BP13K800 KEY >  K800-NUM-REGN                                  
           IF K800-STATUS  NOT = 00                                             
              DISPLAY 'START ERROR, BP13K800 STATUS ' K800-STATUS               
              MOVE K800-STATUS   TO RETURN-CODE                                 
              PERFORM 1100-CLOSE-ROUTINE   THRU  1100-EXIT.                     
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    SET READ POINTER FOR FILE BP13K885 TO THE BEGINING         *         
      *    THIS FILE CHECK WILL TRIP IF THERE IS NO RECS  IN THE FILE.*         
      *****************************************************************         
       110-SET-READ-K885-POINTER.                                               
                                                                                
           MOVE LOW-VALUES    TO K885-KEY-FLD.                                  
           START BP13K885 KEY >  K885-KEY-FLD.                                  
           IF K885-STATUS  NOT = 00                                             
              DISPLAY 'START ERROR, BP13K885 STATUS ' K885-STATUS               
              MOVE K885-STATUS   TO RETURN-CODE                                 
              PERFORM 1100-CLOSE-ROUTINE   THRU  1100-EXIT.                     
                                                                                
       110-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    READ FILE BP13K800 AND TOTAL THE DIFFERENT PAYMENT TYPES    *        
      ******************************************************************        
       200-READ-K800.                                                           
                                                                                
           READ BP13K800 NEXT AT END                                            
                MOVE 'Y' TO WS-K800-EOF-FLAG                                    
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-READ-K800-COUNT.                                         
                                                                                
           IF K800-AMT-REGN-PAID   IS NOT NUMERIC                               
              MOVE ZEROES          TO K800-AMT-REGN-PAID.                       
           IF K800-AMT-BK-PAID     IS NOT NUMERIC                               
              MOVE ZEROES          TO K800-AMT-BK-PAID.                         
           IF K800-AMT-SOC-DEBTOR  IS NOT NUMERIC                               
              MOVE ZEROES          TO K800-AMT-SOC-DEBTOR.                      
           IF K800-AMT-CD          IS NOT NUMERIC                               
              MOVE ZEROES          TO K800-AMT-CD.                              
           IF K800-AMT-SOC-CASH    IS NOT NUMERIC                               
              MOVE ZEROS           TO K800-AMT-SOC-CASH.                        
                                                                                
           IF K800-AMT-CPF1-PAID   IS NOT NUMERIC                               
              MOVE ZEROS           TO K800-AMT-CPF1-PAID.                       
           IF K800-AMT-CPF2-PAID   IS NOT NUMERIC                               
              MOVE ZEROS           TO K800-AMT-CPF2-PAID.                       
           IF K800-AMT-CPF3-PAID   IS NOT NUMERIC                               
              MOVE ZEROS           TO K800-AMT-CPF3-PAID.                       
           IF K800-AMT-CPF4-PAID   IS NOT NUMERIC                               
              MOVE ZEROS           TO K800-AMT-CPF4-PAID.                       
                                                                                
           ADD K800-AMT-BK-PAID    TO WS-TOT-K800-BK-PAID.                      
           ADD K800-AMT-CD         TO WS-TOT-K800-AMT-CD.                       
           ADD K800-AMT-SOC-DEBTOR TO WS-TOT-K800-SOC-DEBTOR.                   
                                                                                
           COMPUTE WS-TOT-K800-SOC-POSTED  = WS-TOT-K800-SOC-POSTED +           
                                             K800-AMT-SOC-CASH      +           
                                             K800-AMT-CPF1-PAID     +           
                                             K800-AMT-CPF2-PAID     +           
                                             K800-AMT-CPF3-PAID     +           
                                             K800-AMT-CPF4-PAID.                
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CASH IS NOT NUMERIC                         
                MOVE  ZERO         TO     K800-AMT-REGN-DEPOSIT-CASH.           
           IF K800-AMT-REGN-DEPOSIT-CPF1 IS NOT NUMERIC                         
                MOVE  ZERO         TO     K800-AMT-REGN-DEPOSIT-CPF1.           
           IF K800-AMT-REGN-DEPOSIT-CPF2 IS NOT NUMERIC                         
                MOVE  ZERO         TO     K800-AMT-REGN-DEPOSIT-CPF2.           
           IF K800-AMT-REGN-DEPOSIT-CPF3 IS NOT NUMERIC                         
                MOVE  ZERO         TO     K800-AMT-REGN-DEPOSIT-CPF3.           
           IF K800-AMT-REGN-DEPOSIT-CPF4 IS NOT NUMERIC                         
                MOVE  ZERO         TO     K800-AMT-REGN-DEPOSIT-CPF4.           
                                                                                
                                                                                
           COMPUTE WS-TOT-K800-SOC-RDE    =  WS-TOT-K800-SOC-RDE      +         
                                           K800-AMT-REGN-DEPOSIT-CASH +         
                                           K800-AMT-REGN-DEPOSIT-CPF1 +         
                                           K800-AMT-REGN-DEPOSIT-CPF2 +         
                                           K800-AMT-REGN-DEPOSIT-CPF3 +         
                                           K800-AMT-REGN-DEPOSIT-CPF4.          
                                                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    READ FILE BP13K885 AND TOTAL THE DIFFERENT PAYMENT TYPES    *        
      ******************************************************************        
       300-READ-K885.                                                           
                                                                                
           READ BP13K885 NEXT AT END                                            
                MOVE 'Y' TO WS-K885-EOF-FLAG                                    
                GO TO 300-EXIT.                                                 
                                                                                
           ADD 1 TO WS-READ-K885-COUNT.                                         
                                                                                
           IF K885-NUM-CONFIRM = 'Y'                                            
              COMPUTE WS-TOT-K885-CD-EXAPPLT = K885-AMT-CD-PAID +               
                                     WS-TOT-K885-CD-EXAPPLT                     
              COMPUTE WS-TOT-K885-CD-EXAPPLT-TR = K885-AMT-CD-TRANSFER +        
                                     WS-TOT-K885-CD-EXAPPLT-TR                  
              COMPUTE WS-TOT-K885-AMT-REFUND = K885-AMT-REFUND +                
                                     WS-TOT-K885-AMT-REFUND                     
              COMPUTE WS-TOT-K885-AMT-SUSPENSE = K885-AMT-SUSPENSE +            
                                     WS-TOT-K885-AMT-SUSPENSE                   
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *************************************************************             
      *    SET READ POINTER FOR FILE BP13K160 TO THE BEGINING     *             
      *************************************************************             
       500-SET-READ-K160-POINTER.                                               
                                                                                
           MOVE LOW-VALUES    TO K160-KEY-FLD                                   
           START BP13K160 KEY >  K160-KEY-FLD                                   
           IF K160-STATUS  NOT = 00                                             
               DISPLAY 'START ERROR, BP13K160 STATUS ' K160-STATUS              
               MOVE K160-STATUS   TO RETURN-CODE                                
               PERFORM 1100-CLOSE-ROUTINE   THRU 1100-EXIT.                     
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    READ FILE BP13K160 AND MOVE THE DIFFERENT PAYMENT TYPES    *         
      *    TO WORKING STORAGE FOR COMPARISION.                        *         
      *    CDE-PAYMENT-TYPE = REG  REGISTRATION FEE                   *         
      *                       BK   BOOKING FEE                        *         
      *                       CD   COMMITMENT DEPOSIT                 *         
      *                       SOC  SOC DEBTOR                         *         
      *                       SUS  SUSPENSE                           *         
      *                       KR   KIV REGN FEE                       *         
      *                       KB   KIV BOOKING FEE                    *         
      *                       KC   KIV COMMITMENT DEPOSIT             *         
      *                       KS   KIV SOC DEBTOR                     *         
      *****************************************************************         
       600-READ-K160.                                                           
                                                                                
           READ BP13K160 NEXT AT END                                            
                MOVE 'Y' TO WS-K160-EOF-FLAG                                    
                GO TO 600-EXIT.                                                 
                                                                                
           ADD 1 TO WS-READ-K160-COUNT.                                         
                                                                                
           IF K160-CDE-PAYMENT-TYPE = 'BK '                                     
              MOVE K160-AMT-CLOSE   TO WS-TOT-K160-AMT-CLOSE-BK-FEE             
              ADD 1                 TO WS-READ-K160-CLOSE-BK-CNT.               
                                                                                
           IF K160-CDE-PAYMENT-TYPE = 'CD '                                     
              MOVE K160-AMT-CLOSE   TO WS-TOT-K160-AMT-CLOSE-CD                 
              ADD 1                 TO WS-READ-K160-CLOSE-CD-CNT.               
                                                                                
           IF K160-CDE-PAYMENT-TYPE = 'SOC'                                     
              MOVE K160-AMT-CLOSE      TO WS-TOT-K160-AMT-CLOSE-DEBTOR          
              MOVE K160-AMT-SOC-POSTED TO WS-TOT-K160-AMT-CLOSE-POSTED          
              ADD 1                   TO WS-READ-K160-CLOSE-DEBTOR-CNT.         
                                                                                
           IF K160-CDE-PAYMENT-TYPE = 'SUS'                                     
              MOVE K160-AMT-CLOSE   TO WS-TOT-K160-AMT-CLOSE-SUS                
              ADD 1                 TO WS-READ-K160-CLOSE-SUS-CNT.              
                                                                                
           IF K160-CDE-PAYMENT-TYPE = 'RDE'                                     
              MOVE K160-AMT-CLOSE   TO WS-TOT-K160-AMT-CLOSE-RDE                
              ADD 1                 TO WS-READ-K160-CLOSE-RDE-CNT.              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    PRINT REPORT TITLES                                        *         
      *****************************************************************         
       700-PRINT-TITLE.                                                         
                                                                                
      *    ACCEPT WS-CURRENT-DATE FROM DATE.                                    
           MOVE   FUNCTION CURRENT-DATE(1:8)  TO  WS-CURRENT-DATE.              
           MOVE   WS-CURRENT-CCYY TO   WS-CCYY.                                 
           MOVE   WS-CURRENT-MM   TO   WS-MM.                                   
           MOVE   WS-CURRENT-DD   TO   WS-DD.                                   
           ADD    1               TO   WS-PAGE-COUNT.                           
           MOVE   WS-PAGE-COUNT   TO   WS-PAGE.                                 
           WRITE  BP13L106-REPORT FROM WS-TITLE-1 AFTER PAGE.                   
           WRITE  BP13L106-REPORT FROM WS-TITLE-2 AFTER 2.                      
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    PRINT REPORT FOR TOTALS OF ALL PAYMENT TYPE EXTRACTED FROM *         
      *    FILES BP13K800 AND BP13K160.                               *         
      *****************************************************************         
       800-PRINT-HEADER.                                                        
                                                                                
           MOVE WS-TOT-K800-BK-PAID             TO L106-BK-FEE-SOC.             
           MOVE WS-TOT-K800-SOC-DEBTOR          TO L106-SOC-DEBTOR-SOC.         
           MOVE WS-TOT-K800-AMT-CD              TO L106-AMT-CD-SOC.             
           MOVE WS-TOT-K800-SOC-POSTED          TO L106-SOC-POSTED-SOC.         
           MOVE WS-TOT-K160-AMT-CLOSE-BK-FEE    TO L106-BK-BAL.                 
           MOVE WS-TOT-K160-AMT-CLOSE-CD        TO L106-CD-BAL.                 
           MOVE WS-TOT-K160-AMT-CLOSE-DEBTOR    TO L106-SOC-DEBTOR-BAL.         
           MOVE WS-TOT-K160-AMT-CLOSE-POSTED    TO L106-SOC-POSTED-BAL.         
           MOVE WS-TOT-K160-AMT-CLOSE-SUS       TO L106-SUS-BAL                 
                                                   L106-AMT-SUS-BAL.            
           MOVE WS-TOT-K885-CD-EXAPPLT          TO L106-AMT-CD-EXAPPLT.         
           MOVE WS-TOT-K885-CD-EXAPPLT-TR       TO L106-AMT-CD-TRANSFER.        
           MOVE WS-TOT-K885-AMT-REFUND          TO L106-AMT-REFUND.             
           MOVE WS-TOT-K885-AMT-SUSPENSE        TO L106-AMT-SUSPENSE.           
                                                                                
           MOVE WS-TOT-K800-SOC-RDE             TO L106-AMT-RDE                 
           MOVE WS-TOT-K160-AMT-CLOSE-RDE       TO L106-RDE-BAL.                
           WRITE BP13L106-REPORT FROM WS-HEADER-1 AFTER 4.                      
           WRITE BP13L106-REPORT FROM WS-HEADER-2.                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
      *    PRINT BALANCE REPORT FOR THOSE PAYMENT TYPES THAT DO  *              
      *    BALANCE.                                              *              
      ************************************************************              
       900-PRINT-DETAIL-LINE.                                                   
                                                                                
           COMPUTE WS-TOTAL-CD-K800-EXAPPLT = WS-TOT-K800-AMT-CD       +        
                                              WS-TOT-K885-CD-EXAPPLT.           
           COMPUTE WS-TOTAL-POSTED-SUSP = WS-TOT-K160-AMT-CLOSE-POSTED +        
                                          WS-TOT-K160-AMT-CLOSE-SUS.            
           COMPUTE WS-TOTAL-POSTED-REFUND = WS-TOT-K800-SOC-POSTED     +        
                                            WS-TOT-K885-AMT-REFUND     +        
                                            WS-TOT-K885-CD-EXAPPLT-TR.          
                                                                                
           MOVE WS-TOTAL-POSTED-REFUND     TO L106-AMT-SOC-TOT                  
                                                                                
           MOVE WS-TOTAL-POSTED-SUSP       TO L106-AMT-BAL-TOT                  
                                                                                
           IF WS-TOT-K160-AMT-CLOSE-BK-FEE NOT = WS-TOT-K800-BK-PAID            
              MOVE '99' TO RETURN-CODE WS-STATUS-2.                             
           IF WS-TOT-K160-AMT-CLOSE-CD NOT = WS-TOT-K800-AMT-CD                 
              MOVE '99' TO RETURN-CODE WS-STATUS-3.                             
           IF WS-TOT-K160-AMT-CLOSE-DEBTOR NOT = WS-TOT-K800-SOC-DEBTOR         
              MOVE '99' TO RETURN-CODE WS-STATUS-4.                             
                                                                                
           IF WS-TOT-K160-AMT-CLOSE-POSTED > ZEROS AND                          
              WS-TOTAL-POSTED-SUSP  NOT = WS-TOTAL-POSTED-REFUND                
              MOVE '99' TO RETURN-CODE WS-STATUS-5.                             
                                                                                
           IF WS-TOT-K160-AMT-CLOSE-SUS NOT = WS-TOT-K885-AMT-SUSPENSE          
              MOVE '99' TO RETURN-CODE WS-STATUS-6.                             
                                                                                
           IF WS-TOT-K160-AMT-CLOSE-RDE NOT = WS-TOT-K800-SOC-RDE               
              MOVE '99' TO RETURN-CODE WS-STATUS-7.                             
                                                                                
           WRITE BP13L106-REPORT FROM WS-DETAIL-LINE-2 AFTER 2.                 
           WRITE BP13L106-REPORT FROM WS-DETAIL-LINE-3-A AFTER 2.               
      *    WRITE BP13L106-REPORT FROM WS-DETAIL-LINE-3-B AFTER 1.               
           WRITE BP13L106-REPORT FROM WS-DETAIL-LINE-4 AFTER 2.                 
           WRITE BP13L106-REPORT FROM WS-DETAIL-LINE-5-A AFTER 2.               
           WRITE BP13L106-REPORT FROM WS-DETAIL-LINE-5-B AFTER 1.               
           WRITE BP13L106-REPORT FROM WS-DETAIL-LINE-5-C AFTER 1.               
           WRITE BP13L106-REPORT FROM WS-DETAIL-LINE-5-D AFTER 1.               
           WRITE BP13L106-REPORT FROM WS-DETAIL-LINE-6 AFTER 2.                 
           WRITE BP13L106-REPORT FROM WS-DETAIL-LINE-7 AFTER 2.                 
           WRITE BP13L106-REPORT FROM WS-FOOTER-LINE-1 AFTER 2.                 
           WRITE BP13L106-REPORT FROM WS-FOOTER-LINE-2 AFTER 2.                 
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    PRINTS CONTROL LIST REPORT FOR INTERNAL CONTROL.           *         
      *****************************************************************         
       1000-PRINT-CONTROL-LIST.                                                 
                                                                                
           MOVE WS-READ-K800-COUNT             TO P106-K800-CNT.                
           MOVE WS-READ-K885-COUNT             TO P106-K885-CNT.                
           MOVE WS-READ-K160-COUNT             TO P106-K160-CNT.                
           MOVE WS-READ-K160-CLOSE-BK-CNT      TO P106-BAL-BK-CNT.              
           MOVE WS-READ-K160-CLOSE-CD-CNT      TO P106-BAL-CD-CNT.              
           MOVE WS-READ-K160-CLOSE-SUS-CNT     TO P106-BAL-SUS-CNT.             
           MOVE WS-READ-K160-CLOSE-DEBTOR-CNT  TO P106-BAL-DEBTOR-CNT.          
           MOVE WS-READ-K160-CLOSE-RDE-CNT     TO P106-BAL-RDE-CNT.             
                                                                                
           WRITE BP13P106-CONTROL-LIST FROM WS-CONTROL-LIST-1 AFTER 2.          
           WRITE BP13P106-CONTROL-LIST FROM WS-CONTROL-LIST-2 AFTER 2.          
           WRITE BP13P106-CONTROL-LIST FROM WS-CONTROL-LIST-3 AFTER 2.          
           WRITE BP13P106-CONTROL-LIST FROM WS-CONTROL-LIST-5 AFTER 2.          
           WRITE BP13P106-CONTROL-LIST FROM WS-CONTROL-LIST-6 AFTER 2.          
           WRITE BP13P106-CONTROL-LIST FROM WS-CONTROL-LIST-7 AFTER 2.          
           WRITE BP13P106-CONTROL-LIST FROM WS-CONTROL-LIST-8 AFTER 2.          
           WRITE BP13P106-CONTROL-LIST FROM WS-CONTROL-LIST-9 AFTER 2.          
           WRITE BP13P106-CONTROL-LIST FROM WS-CONTROL-LIST-10 AFTER 2.         
                                                                                
        1000-EXIT.                                                              
             EXIT.                                                              
                                                                                
      ***************************************************************           
      *     CLOSE ALL FILES AND STOP RUN.                           *           
      ***************************************************************           
       1100-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY '******  BP13C106 *************'.                            
           DISPLAY 'NO OF K800 READ    :' WS-READ-K800-COUNT.                   
           DISPLAY 'NO OF K885 READ    :' WS-READ-K885-COUNT.                   
           DISPLAY 'NO OF K160 READ    :' WS-READ-K160-COUNT.                   
           DISPLAY 'NO OF K160 READ-BK :' WS-READ-K160-CLOSE-BK-CNT.            
           DISPLAY 'NO OF K160 READ-CD :' WS-READ-K160-CLOSE-CD-CNT.            
           DISPLAY 'NO OF K160 READ-SUS:' WS-READ-K160-CLOSE-SUS-CNT.           
           DISPLAY 'NO OF K160 READ-DEB:' WS-READ-K160-CLOSE-DEBTOR-CNT.        
           DISPLAY 'NO OF K160 READ-RDE:' WS-READ-K160-CLOSE-RDE-CNT.           
                                                                                
           CLOSE BP13K800                                                       
                 BP13K885                                                       
                 BP13K160                                                       
                 BP13L106                                                       
                 BP13P106.                                                      
                                                                                
           IF K800-STATUS NOT = 00 AND                                          
              K800-STATUS NOT = 97                                              
              DISPLAY ' CLOSING ERROR, BP13K800 STATUS ' K800-STATUS            
              MOVE K800-STATUS  TO RETURN-CODE.                                 
                                                                                
           IF K885-STATUS NOT = 00 AND                                          
              K885-STATUS NOT = 97                                              
              DISPLAY ' CLOSING ERROR, BP13K885 STATUS ' K885-STATUS            
              MOVE K885-STATUS  TO RETURN-CODE.                                 
                                                                                
           IF K160-STATUS NOT = 00 AND                                          
              K160-STATUS NOT = 97                                              
              DISPLAY ' CLOSING ERROR, BP13K160 STATUS ' K160-STATUS            
              MOVE K160-STATUS  TO RETURN-CODE.                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
