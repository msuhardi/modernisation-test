       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C503.                                                 
       AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
       DATE-WRITTEN.  12/03/2007.                                               
      * ========================================================= *             
      *             UPDATE APPLICANT INFO IN K225 AND             *             
      *             BOOKING APPOINTMENT INFO IN K800              *             
      * ========================================================= *             
      *  PURPOSE OF THIS PROGRAM: UPDATE APPLICANT INFORMATION    *             
      *                           IN BP13K225 AND BOOKING APPT    *             
      *                           DETAILS IN BP13K800             *             
      *                                                           *             
      *  INPUT        :  BP13K820, BP13K225                       *             
      *  INPUT/OUTPUT :  BP13F200, BP13K800                       *             
      *  OUTPUT       :  BP13F225                                 *             
      *                                                           *             
      * ----------------------------------------------------------*             
      * CHG REF  DATE     BY   DESCRIPTION                        *             
      * -------- -------- --- ------------------------------------*             
      * BP133026 12/03/07 RB12 NEW PGM                            *             
      * BP133129 16/08/07 RB12 BLANK OUT APPMT DETAILS FOR WIC    *             
      *                        WITH QUEUE NO > '03000'            *             
      * BP133213 17/12/07 RB12 BLANK OUT APPMT DETAILS FOR WIB    *             
      *                        WITH QUEUE NO > 'B2000'            *             
      * BP133258 19/02/08 RB12 BLANK OUT APPMT DETAILS FOR WIC    *             
      *                        WITH QUEUE NO > '02000'            *             
      * BP133311 16/04/08 RB12 BLANK OUT APPMT DETAILS FOR WIA    *             
      *                        WITH QUEUE NO > 'A2500'            *             
      * BP133071 17/09/08 RB12 CATER FOR 3R SA                    *             
      * BP133430 15/10/08 RB12 BLANK OUT APPMT DETAILS FOR WIH    *             
      *                        WITH QUEUE NO > '02000'            *             
      * ==========================================================*             
                                                                                
       ENVIRONMENT    DIVISION.                                                 
       CONFIGURATION  SECTION.                                                  
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F200  ASSIGN       TO BP13F200.                           
                                                                                
           SELECT BP13F225  ASSIGN       TO BP13F225.                           
                                                                                
           SELECT BP13K225  ASSIGN        TO BP13K225                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K225-KEY-FLD                       
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS K225-FILE-STATUS.                  
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            ACCESS MODE  IS RANDOM                              
                            FILE STATUS  IS K800-FILE-STATUS.                   
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            ACCESS MODE  IS RANDOM                              
                            FILE STATUS  IS K820-FILE-STATUS.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F200                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13F225                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F225.                                                           
                                                                                
       FD   BP13K225                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K225.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  K225-FILE-STATUS               PIC 9(2) VALUE 0.                     
       01  K800-FILE-STATUS               PIC 9(2) VALUE 0.                     
       01  K820-FILE-STATUS               PIC 9(2) VALUE 0.                     
       01  WS-K225-KEY-FND                PIC 9(8) VALUE ZEROES.                
       01  WS-F225-WRITE-CNT              PIC 9(8) VALUE ZEROES.                
       01  WS-K800-READ-ERR               PIC 9(8) VALUE ZEROES.                
       01  WS-K800-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-K800-REWRITE-CNT            PIC 9(8) VALUE ZEROES.                
       01  WS-F200-REWRITE-CNT            PIC 9(8) VALUE ZEROES.                
       01  WS-K800-REWRITE-ERR            PIC 9(8) VALUE ZEROES.                
       01  WS-K820-READ-ERR               PIC 9(8) VALUE ZEROES.                
       01  WS-K820-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-F200-READ-CNT               PIC 9(8) VALUE ZEROES.                
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-CCYY                PIC 9(4) VALUE ZEROES.                
           05  WS-DTE-MM                  PIC 9(2) VALUE ZEROES.                
           05  WS-DTE-DD                  PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-APP-INFO.                                                         
           05  WS-NME-HA1                 PIC X(30).                            
           05  WS-NUM-TEL                 PIC X(8).                             
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F200-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-F200-EOF-YES               VALUE 'Y'.                      
               88 WS-F200-EOF-NO                VALUE 'N'.                      
           05  WS-K225-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-K225-EOF-YES               VALUE 'Y'.                      
               88 WS-K225-EOF-NO                VALUE 'N'.                      
           05  WS-K225-KEY-FOUND-FLAG     PIC X VALUE 'N'.                      
               88 WS-K225-KEY-FOUND             VALUE 'Y'.                      
               88 WS-K225-KEY-NOT-FOUND         VALUE 'N'.                      
           05  WS-K800-KEY-FOUND-FLAG     PIC X VALUE 'N'.                      
               88 WS-K800-KEY-FOUND             VALUE 'Y'.                      
               88 WS-K800-KEY-NOT-FOUND         VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-PRE-PROCESS         THRU 1000-EXIT.                     
           PERFORM 1500-READ-F200           THRU 1500-EXIT.                     
           PERFORM 2000-PROCESS-MASTER      THRU 2000-EXIT                      
                   UNTIL WS-F200-EOF-YES.                                       
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-PRE-PROCESS.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT  BP13K820                                                 
                       BP13K225                                                 
                  I-O  BP13F200                                                 
                       BP13K800                                                 
                OUTPUT BP13F225.                                                
                                                                                
           IF K225-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           IF K800-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K800 : ' K800-FILE-STATUS              
              MOVE     K800-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           IF K820-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K820 : ' K820-FILE-STATUS              
              MOVE     K820-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1500-READ-F200.                                                          
      *-------------------------------------------------------------            
           READ BP13F200                                                        
             AT END                                                             
                SET WS-F200-EOF-YES TO TRUE                                     
           END-READ.                                                            
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-PROCESS-MASTER.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES               TO K225-KEY-FLD.                           
                                                                                
           EVALUATE F200-NUM-SELTYPE                                            
              WHEN '3R '                                                        
                IF F200-NUM-FLAT-TYPE = '1A'                                    
                   STRING 'S' F200-NUM-HHTY-QUEUE-SERIAL(2:4)                   
                     DELIMITED BY SIZE                                          
                        INTO K225-NUM-QUEUE                                     
                ELSE                                                            
                   STRING 'R' F200-NUM-HHTY-QUEUE-SERIAL(2:4)                   
                     DELIMITED BY SIZE                                          
                        INTO K225-NUM-QUEUE                                     
                END-IF                                                          
              WHEN 'WIH'                                                        
                MOVE F200-NUM-HHTY-QUEUE-SERIAL TO K225-NUM-QUEUE               
           END-EVALUATE.                                                        
                                                                                
           READ BP13K225.                                                       
                                                                                
           EVALUATE K225-FILE-STATUS                                            
                                                                                
           WHEN 00                                                              
           WHEN 02                                                              
              PERFORM 2100-PROCESS-K225 THRU 2100-EXIT                          
                                                                                
           WHEN 23                                                              
              DISPLAY 'K225 KEY NOT FOUND IN READING K225: '                    
                          K225-KEY-FLD                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K225'                                  
              DISPLAY 'K225-FILE-STATUS: ' K225-FILE-STATUS                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
           SET WS-K225-KEY-NOT-FOUND TO TRUE.                                   
                                                                                
           ADD 1 TO WS-F200-READ-CNT.                                           
                                                                                
           PERFORM 1500-READ-F200 THRU 1500-EXIT.                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2100-PROCESS-K225.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF K225-NUM-REGN = LOW-VALUES OR SPACES                              
                                                                                
              SET WS-K225-KEY-FOUND     TO   TRUE                               
              SET WS-K800-KEY-NOT-FOUND TO   TRUE                               
              PERFORM 2110-UPDATE-FILES THRU 2110-EXIT                          
              ADD 1 TO WS-K225-KEY-FND                                          
                                                                                
           ELSE                                                                 
              SET WS-K225-KEY-NOT-FOUND TO TRUE                                 
                                                                                
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2110-UPDATE-FILES.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 2120-UPDATE-BP13K800   THRU 2120-EXIT.                       
                                                                                
           IF WS-K800-KEY-FOUND                                                 
              PERFORM 2130-WRITE-BP13F225 THRU 2130-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2123-REWRITE-BP13F200  THRU 2123-EXIT.                       
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2120-UPDATE-BP13K800.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE F200-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE K800-FILE-STATUS                                            
                                                                                
              WHEN 00                                                           
                ADD 1 TO WS-K800-READ-CNT                                       
                SET WS-K800-KEY-FOUND TO TRUE                                   
                PERFORM 2121-REWRITE-BP13K800 THRU 2121-EXIT                    
                PERFORM 2122-READ-BP13K820    THRU 2122-EXIT                    
                                                                                
              WHEN 23                                                           
                DISPLAY 'KEY NOT FOUND IN READING K800: '                       
                         K800-NUM-REGN                                          
                ADD 1 TO WS-K800-READ-ERR                                       
                                                                                
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K800: ' K800-FILE-STATUS             
                MOVE     K800-FILE-STATUS TO RETURN-CODE                        
                PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2120-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2121-REWRITE-BP13K800.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE K225-DTE-APPOINTMENT  TO K800-DTE-BK-APPT.                      
           MOVE K225-TME-APPOINTMENT  TO K800-TME-BK-APPT.                      
           MOVE K225-NUM-QUEUE        TO K800-NUM-NT-FT-QUEUE.                  
                                                                                
           IF (K225-NUM-SALES-MODE = 'WIC' AND                                  
               K225-NUM-QUEUE > '02000') OR                                     
              (K225-NUM-SALES-MODE = 'WIB' AND                                  
               K225-NUM-QUEUE > 'B2000') OR                                     
              (K225-NUM-SALES-MODE = 'WIA' AND                                  
               K225-NUM-QUEUE > 'A2500') OR                                     
              (K225-NUM-SALES-MODE = 'WIH' AND                                  
               K225-NUM-QUEUE > '02000')                                        
              MOVE SPACES             TO K800-DTE-BK-APPT                       
                                         K800-TME-BK-APPT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO K800-DTE-UPDATE.                       
           MOVE 'BP13C503'            TO K800-NUM-USERID.                       
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF K800-FILE-STATUS EQUAL 00                                         
              ADD 1 TO WS-K800-REWRITE-CNT                                      
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING K800 - ' K800-FILE-STATUS                
                      ' K800 REC NOT UPDATED: ' K800-NUM-REGN                   
              ADD 1 TO WS-K800-REWRITE-ERR                                      
           END-IF.                                                              
                                                                                
       2121-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2122-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                 TO WS-APP-INFO.                          
                                                                                
           MOVE K800-NUM-REGN          TO K820-NUM-REGN.                        
                                                                                
           MOVE K800-NUM-NRIC1         TO K820-NUM-NRIC.                        
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE K820-FILE-STATUS                                            
                                                                                
              WHEN 00                                                           
                MOVE K820-NME-OCCP(1:30)      TO WS-NME-HA1                     
                IF K820-NUM-HP-PGR NOT EQUAL SPACES AND LOW-VALUES              
                   MOVE K820-NUM-HP-PGR       TO WS-NUM-TEL                     
                ELSE                                                            
                   IF K820-NUM-PHONE-OFF NOT EQUAL SPACES                       
                                         AND LOW-VALUES                         
                      MOVE K820-NUM-PHONE-OFF TO WS-NUM-TEL                     
                   ELSE                                                         
                      IF K820-NUM-PHONE NOT EQUAL SPACES                        
                                         AND LOW-VALUES                         
                         MOVE K820-NUM-PHONE  TO WS-NUM-TEL                     
                      END-IF                                                    
                   END-IF                                                       
                END-IF                                                          
                ADD 1 TO WS-K820-READ-CNT                                       
              WHEN 23                                                           
                DISPLAY 'KEY NOT FOUND IN READING K820: '                       
                         K820-KEY-FLD                                           
                ADD 1 TO WS-K820-READ-ERR                                       
                                                                                
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K820: ' K820-FILE-STATUS             
                MOVE     K820-FILE-STATUS TO RETURN-CODE                        
                PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2122-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2123-REWRITE-BP13F200.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE K225-DTE-APPOINTMENT  TO F200-DTE-BKAPPMT-DATE.                 
           MOVE K225-TME-APPOINTMENT  TO F200-TME-BKAPPMT.                      
           MOVE K225-NUM-QUEUE        TO F200-NUM-HHTY-QUEUE-SERIAL.            
                                                                                
           REWRITE BP13F200-REC.                                                
                                                                                
           ADD 1 TO WS-F200-REWRITE-CNT.                                        
                                                                                
       2123-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2130-WRITE-BP13F225.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE BP13K225-REC          TO BP13F225-REC.                          
                                                                                
           MOVE F200-NUM-REGN         TO F225-NUM-REGN.                         
           MOVE K800-NUM-NRIC1        TO F225-NUM-NRIC.                         
           MOVE WS-NME-HA1            TO F225-NME-HA1.                          
           MOVE WS-NUM-TEL            TO F225-NUM-TEL.                          
           MOVE K800-NUM-CAT          TO F225-NUM-CAT.                          
           MOVE K800-DTE-BALLOT(1:4)  TO F225-DTE-YEAR-LAUNCH.                  
           MOVE K800-DTE-BALLOT(5:2)  TO F225-DTE-MONTH-LAUNCH.                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO F225-DTE-UPDATE.                       
           ACCEPT F225-TME-UPDATE   FROM TIME.                                  
           MOVE 'BP13C503'            TO F225-NUM-USERID.                       
                                                                                
           WRITE BP13F225-REC.                                                  
                                                                                
           ADD 1                      TO WS-F225-WRITE-CNT.                     
                                                                                
       2130-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '******  BP13C503  *************'.                           
           DISPLAY 'RECS READ FROM  BP13F200    : ' WS-F200-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'KEYS FOUND IN BP13K225      : ' WS-K225-KEY-FND.            
           DISPLAY 'RECS WRITTEN  IN BP13F225   : ' WS-F225-WRITE-CNT.          
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ   FROM BP13K800   : ' WS-K800-READ-CNT.           
           DISPLAY 'RECS NOTFND   IN BP13K800   : ' WS-K800-READ-ERR.           
           DISPLAY 'RECS WRITTEN  IN BP13K800   : ' WS-K800-REWRITE-CNT.        
           DISPLAY 'RECS ERROR REWRITE BP13K800 : ' WS-K800-REWRITE-ERR.        
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ   FROM BP13K820   : ' WS-K820-READ-CNT.           
           DISPLAY 'RECS NOTFND   IN BP13K820   : ' WS-K820-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  IN BP13F200   : ' WS-F200-REWRITE-CNT.        
                                                                                
           CLOSE   BP13F200                                                     
                   BP13F225                                                     
                   BP13K225                                                     
                   BP13K800                                                     
                   BP13K820.                                                    
                                                                                
           IF K225-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           IF K800-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K800 : ' K800-FILE-STATUS              
              MOVE     K800-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           IF K820-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K820 : ' K820-FILE-STATUS              
              MOVE     K820-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
