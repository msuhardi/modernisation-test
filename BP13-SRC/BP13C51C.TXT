       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C51C.                                                 
      *AUTHOR.        ED1.                                                      
      *DATE-WRITTEN.  APR 05, 2007.                                             
      *****************************************************************         
      * OBJECTIVE - DELETE BP13K730 & BP13K825 RECS FND IN BP13F740   *         
      *---------------------------------------------------------------*         
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                 *         
      * -------  ----    ---  -----------------------                 *         
      * BP133017 070405  ED1  NEW PGM                                 *         
      * BP133017 120405  ED1  CHANGED K730-KEY-FLD TO K730-KEY-NEW    *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN       TO BP13F740.                            
                                                                                
           SELECT BP13K730 ASSIGN       TO BP13K730                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K730-KEY-NEW                         
                           FILE STATUS  IS K730-STATUS.                         
                                                                                
           SELECT BP13K825 ASSIGN       TO BP13K825                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K825-KEY-FLD                         
                           FILE STATUS  IS K825-STATUS.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K730.                                                           
       COPY BP13K730.                                                           
                                                                                
       FD   BP13K825.                                                           
       COPY BP13K825.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  K730-STATUS                   PIC 9(02) VALUE 0.                     
       01  K825-STATUS                   PIC 9(02) VALUE 0.                     
       01  WS-K825-EOF                   PIC X(01) VALUE 'N'.                   
       01  WS-F740-EOF                   PIC X(01) VALUE 'N'.                   
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                  PIC X(08) VALUE SPACES.             
           05  WS-SYS-TIME                  PIC X(08) VALUE SPACES.             
           05  FILLER                       PIC X(05) VALUE SPACES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F740-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-K730-DEL            PIC 9(07) VALUE 0.                     
           05 WS-CNT-K825-DEL            PIC 9(07) VALUE 0.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      **************                                                            
       0000-CONTROL.                                                            
      **************                                                            
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 1100-READ-BP13F740    THRU 1100-EXIT.                        
                                                                                
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                UNTIL WS-F740-EOF = 'Y'.                                        
                                                                                
           PERFORM 9000-CLOSING-ROUTINE  THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      **************                                                            
       1000-START-UP.                                                           
      **************                                                            
           OPEN INPUT  BP13F740                                                 
                I-O    BP13K730                                                 
                       BP13K825.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
                                                                                
           IF  K730-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13K730 ' K730-STATUS                  
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF  K825-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13K825 ' K825-STATUS                  
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ********************                                                      
       1100-READ-BP13F740.                                                      
      ********************                                                      
           READ BP13F740 AT END                                                 
                MOVE 'Y' TO WS-F740-EOF                                         
                GO       TO 1100-EXIT                                           
                END-READ.                                                       
                                                                                
           ADD  1        TO WS-CNT-F740-READ.                                   
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *******************                                                       
       2000-MAIN-ROUTINE.                                                       
      *******************                                                       
           IF F740-NUM-REGN NOT = SPACES AND LOW-VALUES                         
              PERFORM 3000-READ-BP13K730   THRU 3000-EXIT                       
              PERFORM 4000-READ-BP13K825   THRU 4000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F740      THRU 1100-EXIT.                      
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ********************                                                      
       3000-READ-BP13K730.                                                      
      ********************                                                      
           INITIALIZE            K730-KEY-NEW.                                  
           MOVE SPACES        TO K730-KEY-NEW.                                  
           MOVE F740-NUM-REGN TO K730-KEY-NEW.                                  
           READ BP13K730.                                                       
                                                                                
           EVALUATE K730-STATUS                                                 
           WHEN 00                                                              
                PERFORM 3100-DELETE-BP13K730   THRU 3100-EXIT                   
                                                                                
           WHEN 23                                                              
                DISPLAY 'BP13K730 - RECORD NOT FOUND, KEY: '                    
                         K730-KEY-NEW                                           
           WHEN OTHER                                                           
                DISPLAY 'BP13K730 - ERROR IN READ, STATUS: '                    
                         K730-STATUS                                            
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      **********************                                                    
       3100-DELETE-BP13K730.                                                    
      **********************                                                    
           DELETE BP13K730.                                                     
                                                                                
           EVALUATE K730-STATUS                                                 
           WHEN 00                                                              
                ADD 1 TO WS-CNT-K730-DEL                                        
                                                                                
           WHEN OTHER                                                           
              MOVE K730-STATUS   TO RETURN-CODE                                 
              DISPLAY 'ERROR DELETING BP13K730, STATUS = '                      
                   K730-STATUS                                                  
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ********************                                                      
       4000-READ-BP13K825.                                                      
      ********************                                                      
           MOVE 'N'           TO WS-K825-EOF.                                   
           INITIALIZE            K825-KEY-FLD.                                  
           MOVE SPACES        TO K825-KEY-FLD.                                  
           MOVE F740-NUM-REGN TO K825-NUM-REGN.                                 
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           EVALUATE K825-STATUS                                                 
           WHEN 00                                                              
                PERFORM 4100-READNXT-BP13K825   THRU  4100-EXIT                 
                        UNTIL WS-K825-EOF       = 'Y' OR                        
                              K825-NUM-REGN NOT = F740-NUM-REGN                 
                                                                                
           WHEN 23                                                              
                DISPLAY 'BP13K825 - RECORD NOT FOUND, KEY: '                    
                         K825-NUM-REGN                                          
           WHEN OTHER                                                           
                DISPLAY 'BP13K825 - ERROR IN STARTBR, STATUS: '                 
                         K825-STATUS                                            
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ***********************                                                   
       4100-READNXT-BP13K825.                                                   
      ***********************                                                   
           READ BP13K825 NEXT RECORD.                                           
                                                                                
           EVALUATE K825-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
                IF F740-NUM-REGN = K825-NUM-REGN                                
                   PERFORM 4200-DELETE-BP13K825  THRU 4200-EXIT                 
                END-IF                                                          
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y' TO WS-K825-EOF                                         
           WHEN OTHER                                                           
                DISPLAY 'BP13K825 - ERROR IN READNXT, STATUS: '                 
                         K825-STATUS                                            
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      **********************                                                    
       4200-DELETE-BP13K825.                                                    
      **********************                                                    
           DELETE BP13K825.                                                     
                                                                                
           EVALUATE K825-STATUS                                                 
           WHEN 00                                                              
                ADD 1 TO WS-CNT-K825-DEL                                        
                                                                                
           WHEN OTHER                                                           
              MOVE K825-STATUS   TO RETURN-CODE                                 
              DISPLAY 'ERROR DELETING BP13K825, STATUS = '                      
                   K825-STATUS                                                  
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      **********************                                                    
       9000-CLOSING-ROUTINE.                                                    
      **********************                                                    
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C51C'.                                     
           DISPLAY 'RUN DATE   : ' WS-SYS-DATE.                                 
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13K740 RECS READ     = ' WS-CNT-F740-READ.          
           DISPLAY 'TOTAL BP13K730 RECS DELETED  = ' WS-CNT-K730-DEL.           
           DISPLAY 'TOTAL BP13K825 RECS DELETED  = ' WS-CNT-K825-DEL.           
                                                                                
           CLOSE BP13K730                                                       
                 BP13K825                                                       
                 BP13F740.                                                      
                                                                                
           IF  K730-STATUS NOT = 0                                              
               DISPLAY 'CLOSING ERROR - BP13K730, STATUS ' K730-STATUS          
               MOVE K730-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  K825-STATUS NOT = 0                                              
               DISPLAY 'CLOSING ERROR - BP13K825, STATUS ' K825-STATUS          
               MOVE K825-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
