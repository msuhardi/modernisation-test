       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD90.                                                 
      *AUTHOR.        IRVIN CHING.                                              
      *DATE-WRITTEN.  04/05/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *                                                            *            
      *    TO SEND EMAIL TO RESALE SECTION WHEN OUTGOING RESALES   *            
      *    IS AFFECTED BY 2ND LOAN CREAMOFF, ONCE THE PP DUE IS    *            
      *    UPDATED IN SOC                                          *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE(YMD)  BY    DESCRIPTION                      *            
      * -------- ---------- ----- -------------------------------- *            
      * BP134515 2012-06-04 IMC1  NEW PROGRAM                      *            
      * BP134269 2012-06-20 IMC1  FIXED ERROR THAT OCCURS WHEN DATA*            
      *                           IS NOT SEQUENCED                 *            
      * BP134571 2012-07-12 IMC1  CHANGE LSC13 TO CYM3 AND JL47    *            
      * BP134744 2013-03-12 RJE1  REMOVED TGM1, KCH1, TSL39, YC1   *            
      *                           NCK1. ADDED NST1                 *            
      * BP134974 2013-07-09 RJE1  REMOVED LJL1 AND NST1            *            
      * BP136477 2016-10-31 RJB1  REMOVED LKH2                     *            
      * BP136509 2016-11-02 RJB1  REMOVED JL47                     *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT SY02F001 ASSIGN        TO SY02F001.                           
           SELECT BP13FD90 ASSIGN        TO BP13FD90.                           
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS BP13K800-STATUS.                    
           SELECT BP13UPD  ASSIGN        TO BP13UPD.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FD90                                                            
            RECORD CONTAINS 4000 CHARACTERS.                                    
       COPY BP13FD90.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD  BP13UPD                                                              
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE IS F.                                                
       01  UPD-PRTREC  PIC X(80).                                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       COPY BP13FD30.                                                           
                                                                                
       01  WS-VARIABLES.                                                        
           05  CNT-FD90-READ        PIC 9(05)  VALUE ZEROES.                    
           05  CNT-K800-NOTFND      PIC 9(05)  VALUE ZEROES.                    
           05  CNT-K800-NOTBUY1ST   PIC 9(05)  VALUE ZEROES.                    
           05  WS-I                 PIC 9(02)  VALUE ZEROES.                    
           05  WS-CNT               PIC 9(02)  VALUE ZEROES.                    
           05  WS-FD90-EOF          PIC X(01)  VALUE SPACES.                    
               88  FD90-EOF                    VALUE 'Y'.                       
           05  WS-K800-FND          PIC X(01)  VALUE SPACES.                    
               88  K800-FND                    VALUE 'Y'.                       
           05  WS-K010-FND          PIC X(01)  VALUE SPACES.                    
               88  K010-FND                    VALUE 'Y'.                       
           05  BP13K800-STATUS      PIC 9(02)  VALUE ZEROES.                    
           05  BP13FD90-STATUS      PIC 9(02)  VALUE ZEROES.                    
           05  BB24K010-STATUS      PIC 9(02)  VALUE ZEROES.                    
           05  WS-SYS-DATE          PIC X(8)   VALUE SPACES.                    
           05  WS-UPD-HDR-WRITE     PIC X(1)   VALUE SPACES.                    
           05  WS-FD90-KEY-FLD.                                                 
               10 WS-FD90-DTE-LOG                      PIC X(08).               
               10 WS-FD90-TME-LOG.                                              
                  15 WS-FD90-TME-LOG-HH                PIC 9(02).               
                15 WS-FD90-TME-LOG-MM                  PIC 9(02).               
                15 WS-FD90-TME-LOG-SS                  PIC 9(02).               
                15 WS-FD90-TME-LOG-MS                  PIC 9(02).               
             10 FD90-NUM-USER                          PIC X(09).               
       01  WS-FD30-REC.                                                         
           05  WS-FD30-KEY-FLD.                                                 
               10  WS-FD30-NUM-REGN               PIC X(8).                     
           05  FILLER                             PIC X(10).                    
           05  WS-FD30-NUM-SCH-ACC.                                             
               10  WS-FD30-NUM-SCH                PIC X(4).                     
               10  WS-FD30-NUM-ACC                PIC X(5).                     
               10  WS-FD30-NUM-LESSEE-CODE        PIC X(2).                     
           05  WS-FD30-AMT-CURR-SELLPR            PIC 9(8).                     
           05  WS-FD30-NUM-FLAT-TYPE              PIC X(2).                     
           05  FILLER                             PIC X(20).                    
           05  WS-FD30-PP-SCHEDULE-TOT OCCURS 15 TIMES.                         
               10  WS-FD30-NUM-PP-SCHDL           PIC X(2).                     
               10  WS-FD30-NUM-PP-PERCENT         PIC X(2).                     
               10  WS-FD30-DTE-PP-DUE             PIC X(8).                     
               10  WS-FD30-AMT-PP-REQ             PIC S9(6)V99.                 
               10  WS-FD30-AMT-PP-PAID            PIC S9(6)V99.                 
               10  WS-FD30-AMT-PP-LOAN            PIC S9(6)V99.                 
               10  WS-FD30-AMT-PP-INSTAL          PIC S9(6)V99.                 
               10  WS-FD30-DTE-FIN-PROCESS        PIC X(8).                     
               10  WS-FD30-DTE-CHEQUE-SENT        PIC X(8).                     
               10  WS-FD30-NUM-PV-SYSTEM          PIC X(11).                    
               10  ws-FD30-NUM-PV-MANUAL          PIC X(11).                    
               10  FILLER                         PIC X(18).                    
           05  FILLER                             PIC X(416).                   
           05  WS-FD30-DTE-UPDATE                 PIC X(8).                     
           05  WS-FD30-TME-UPDATE                 PIC X(9).                     
           05  WS-FD30-NUM-USERID                 PIC X(8).                     
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  WS-MAILLIST.                                                         
           05  WS-UPD-OIC-LIST                PIC X(160) VALUE                  
      *        'TOtgm1  TOcym3  TOlkh2  TOkch1  TOgsh2  TOtsl39 TOhbb1          
      *        'TOyc1   TOnck1  TOcwl1  TOjk3   TOjl47  CCljl1  '.              
               'TOcym3  TOgsh2  TOhbb1  TOcwl1  TOjk3'.                         
           05  REDEFINES WS-UPD-OIC-LIST.                                       
               10  FILLER OCCURS 20 TIMES.                                      
                   20  WS-UPD-TYP             PIC X(02).                        
                   20  WS-UPD-OIC             PIC X(06).                        
                                                                                
       01  MAIL-HDR-HELO.                                                       
           05  FILLER PIC X(80)  VALUE 'HELO SGPHDB1'.                          
                                                                                
       01  MAIL-HDR-MAIL-FROM.                                                  
           05  FILLER PIC X(80)  VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.              
      * ACJOB HEADER                                                            
      *    05  FILLER PIC X(80)  VALUE 'MAIL FROM:<ACJOB@SGPHDB1>'.             
                                                                                
       01  MAIL-HDR-DATA.                                                       
           05  FILLER PIC X(80) VALUE 'DATA'.                                   
                                                                                
       01  MAIL-HDR-FROM.                                                       
           05  FILLER PIC X(80) VALUE 'FROM: SOC System - Email Alert'.         
                                                                                
       01  MAIL-UPD-SUBJECT.                                                    
           05  FILLER PIC X(80)  VALUE 'SUBJECT : Buy 1st DBSS/SOC cases        
      -        ' which have updated PP due date in SOC'.                        
                                                                                
       01  MAIL-HDR-DATE.                                                       
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-DTL-HDR-UPD.                                                    
           05 FILLER                   PIC X(80)  VALUE                         
              ' Regn No  Prjct Purchase Flat  Time of update Sched Old P        
      -       'P Date  New PP Due Date'.                                        
                                                                                
       01  MAIL-DTL-FIL-UPD.                                                    
           05 FILLER                   PIC X(80)  VALUE                         
              ' ======== ===== ============== ============== ===== =====        
      -       '======  ==============='.                                        
                                                                                
       01  MAIL-DTL-CASE-UPD.                                                   
           05 FILLER                   PIC X(01)  VALUE SPACES.                 
           05 MAIL-UPD-REGN            PIC X(08)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 MAIL-UPD-PRJCT           PIC X(03)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 MAIL-UPD-FLAT-NEW        PIC X(14)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 MAIL-UPD-TIME            PIC X(11)  VALUE SPACES.                 
           05 FILLER                   PIC X(04)  VALUE SPACES.                 
           05 MAIL-UPD-NUM-SCHED       PIC X(02)  VALUE SPACES.                 
           05 FILLER                   PIC X(03)  VALUE SPACES.                 
           05 MAIL-UPD-OLD-DUEDTE      PIC X(12)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 MAIL-UPD-NEW-DUEDTE      PIC X(10)  VALUE SPACES.                 
                                                                                
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE         THRU 1000-EXIT.                   
            PERFORM 2000-READ-BP13FD90        THRU 2000-EXIT                    
            PERFORM 2100-CHECK-BP13FD90       THRU 2100-EXIT                    
              UNTIL FD90-EOF.                                                   
                                                                                
            IF WS-UPD-HDR-WRITE = 'Y'                                           
               WRITE UPD-PRTREC FROM MAIL-END                                   
            END-IF.                                                             
                                                                                
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  BP13FD90                                                 
                       SY02F001                                                 
                       BP13K800                                                 
                OUTPUT BP13UPD.                                                 
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K800 - OPEN ERROR (' BP13K800-STATUS ')'             
              MOVE BP13K800-STATUS  TO RETURN-CODE                              
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT   TO WS-SYS-DATE.                              
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
                  DELIMITED BY SIZE INTO MAIL-DATE.                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FD90.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FD90                                                        
             AT END                                                             
                MOVE 'Y'   TO WS-FD90-EOF                                       
                GO         TO 2000-EXIT                                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2100-CHECK-BP13FD90.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF FD90-DTE-LOG = WS-SYS-DATE                                        
              ADD 1        TO CNT-FD90-READ                                     
              IF FD90-CDE-ACTION = 'U'                                          
                 MOVE FD90-NUM-CONTENT            TO WS-FD30-REC                
                 MOVE FD90-KEY-FLD                TO WS-FD90-KEY-FLD            
                 PERFORM 3000-PROCESS-DETAIL      THRU 3000-EXIT                
              END-IF                                                            
              PERFORM 2000-READ-BP13FD90          THRU 2000-EXIT                
           ELSE                                                                 
              MOVE 'Y'     TO WS-FD90-EOF                                       
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
                                                                                
           PERFORM 2000-READ-BP13FD90       THRU 2000-EXIT.                     
           MOVE FD90-NUM-CONTENT            TO BP13FD30-REC                     
                                                                                
           MOVE FD30-NUM-REGN               TO K800-NUM-REGN                    
           PERFORM 8001-READ-BP13K800       THRU 8001-EXIT                      
                                                                                
           IF K800-FND                                                          
              IF FD90-NUM-RETCODE = ZEROES                                      
                 IF K800-NUM-BUY-SELL-TAG = 'B'                                 
                    MOVE 1                    TO WS-CNT                         
                    PERFORM 3100-MAIL-UPD     THRU 3100-EXIT                    
                      UNTIL WS-CNT > 15                                         
                 ELSE                                                           
                    ADD  1                    TO CNT-K800-NOTBUY1ST             
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              ADD 1                           TO CNT-K800-NOTFND                
           END-IF.                                                              
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-MAIL-UPD.                                                           
      *-------------------------------------------------------------            
                                                                                
           IF WS-FD30-DTE-PP-DUE(WS-CNT) = FD30-DTE-PP-DUE(WS-CNT)              
              ADD 1 TO WS-CNT                                                   
              GO TO 3100-EXIT                                                   
           END-IF                                                               
                                                                                
           IF WS-UPD-HDR-WRITE NOT = 'Y'                                        
              WRITE UPD-PRTREC FROM MAIL-HDR-HELO                               
              WRITE UPD-PRTREC FROM MAIL-HDR-MAIL-FROM                          
              PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 20                  
                 IF WS-UPD-OIC(WS-I) NOT = SPACES AND LOW-VALUES                
                    MOVE SPACES  TO UPD-PRTREC                                  
                    STRING 'RCPT TO:<' DELIMITED BY SIZE                        
                           WS-UPD-OIC(WS-I) DELIMITED BY SPACES                 
                           '@hdb.gov.sg>'                                       
                           DELIMITED BY SPACES INTO UPD-PRTREC                  
                    WRITE UPD-PRTREC                                            
                 END-IF                                                         
              END-PERFORM                                                       
              WRITE UPD-PRTREC FROM MAIL-HDR-DATA                               
              WRITE UPD-PRTREC FROM MAIL-HDR-FROM                               
              PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 20                  
                 IF WS-UPD-OIC(WS-I) NOT = SPACES AND LOW-VALUES                
                    MOVE SPACES  TO UPD-PRTREC                                  
                    STRING WS-UPD-TYP(WS-I) ':<'                                
                           WS-UPD-OIC(WS-I) '@hdb.gov.sg>'                      
                           DELIMITED BY SPACES INTO UPD-PRTREC                  
                    WRITE UPD-PRTREC                                            
                 END-IF                                                         
              END-PERFORM                                                       
              WRITE UPD-PRTREC FROM MAIL-UPD-SUBJECT                            
              WRITE UPD-PRTREC FROM MAIL-HDR-DATE                               
              WRITE UPD-PRTREC FROM MAIL-SPACES                                 
              WRITE UPD-PRTREC FROM MAIL-DTL-HDR-UPD                            
              WRITE UPD-PRTREC FROM MAIL-DTL-FIL-UPD                            
              MOVE 'Y'                      TO WS-UPD-HDR-WRITE                 
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-REGN               TO MAIL-UPD-REGN                    
           MOVE K800-NUM-DBSS-PRJCT         TO MAIL-UPD-PRJCT                   
           STRING K800-NUM-SCH-ACC(1:4) '-' K800-NUM-SCH-ACC(5:4) '-'           
                  K800-NUM-SCH-ACC(9:1) '-' K800-NUM-SCH-ACC(10:2)              
                  DELIMITED BY SIZE INTO MAIL-UPD-FLAT-NEW.                     
      *    STRING K482-NUM-HDBREF-DISPOSAL(1:4) '-'                             
      *           K482-NUM-HDBREF-DISPOSAL(5:4) '-'                             
      *           K482-NUM-HDBREF-DISPOSAL(9:1) '-'                             
      *           K482-NUM-HDBREF-DISPOSAL(10:2)                                
      *           DELIMITED BY SIZE INTO MAIL-UPD-FLAT-SOLD.                    
           MOVE WS-FD30-NUM-PP-SCHDL(WS-CNT) TO MAIL-UPD-NUM-SCHED              
           STRING WS-FD90-TME-LOG-HH ':'                                        
                  WS-FD90-TME-LOG-MM ':'                                        
                  WS-FD90-TME-LOG-SS ':'                                        
                  WS-FD90-TME-LOG-MS                                            
                  DELIMITED BY SIZE INTO MAIL-UPD-TIME.                         
                                                                                
           IF WS-FD30-DTE-PP-DUE(WS-CNT) = SPACES                               
              MOVE ZEROES           TO WS-FD30-DTE-PP-DUE(WS-CNT)               
           END-IF.                                                              
                                                                                
           STRING WS-FD30-DTE-PP-DUE(WS-CNT)(7:2) '/'                           
                  WS-FD30-DTE-PP-DUE(WS-CNT)(5:2) '/'                           
                  WS-FD30-DTE-PP-DUE(WS-CNT)(1:4)                               
                  DELIMITED BY SIZE INTO MAIL-UPD-NEW-DUEDTE.                   
                                                                                
           IF FD30-DTE-PP-DUE(WS-CNT) = SPACES                                  
              MOVE ZEROES           TO FD30-DTE-PP-DUE(WS-CNT)                  
           END-IF.                                                              
                                                                                
           STRING FD30-DTE-PP-DUE(WS-CNT)(7:2) '/'                              
                  FD30-DTE-PP-DUE(WS-CNT)(5:2) '/'                              
                  FD30-DTE-PP-DUE(WS-CNT)(1:4)                                  
                  DELIMITED BY SIZE INTO MAIL-UPD-OLD-DUEDTE.                   
                                                                                
           WRITE UPD-PRTREC FROM MAIL-DTL-CASE-UPD.                             
                                                                                
           ADD 1                    TO WS-CNT.                                  
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8001-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF BP13K800-STATUS = 00 OR 02                                        
              MOVE 'Y'               TO WS-K800-FND                             
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 MOVE 'N'            TO WS-K800-FND                             
              ELSE                                                              
                 DISPLAY 'BP13K800 - READ ERROR (' BP13K800-STATUS ')'          
                 MOVE BP13K800-STATUS TO RETURN-CODE                            
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' - BP13CD90 CONTROL TOTALS -----'.                          
           DISPLAY ' '.                                                         
           DISPLAY ' NO. OF BP13FD90 RECS READ   = ' CNT-FD90-READ.             
           DISPLAY ' NO. OF BP13K800 NOT FND     = ' CNT-K800-NOTFND.           
           DISPLAY ' NO. OF BP13K800 NOT BUY 1ST = ' CNT-K800-NOTBUY1ST.        
           DISPLAY ' '.                                                         
                                                                                
            CLOSE BP13FD90                                                      
                  BP13K800                                                      
                  BP13UPD.                                                      
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'BP13K800 - OPEN ERROR (' BP13K800-STATUS ')'             
           END-IF.                                                              
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
