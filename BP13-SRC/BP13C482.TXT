      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C482.                                                 
      *AUTHOR.        MALC.                                                     
      *DATE-WRITTEN.  14APR2001.                                                
                                                                                
      *========================================================*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *========================================================*                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. TO PRINT A WEEKLY REPORT FOR ALLOCATED RENTAL    *                
      *       FLAT = Y'. TO ENSURE DELIVERY OF VACANT          *                
      *       POSSESSION OF THE RENTAL FLATS WITHIN 2 MONTHS   *                
      *       FROM DATE OF TP.                                 *                
      *                                                        *                
      *    INPUT  FILE  : BP13F410                             *                
      *                   BP13K830 - CORR ADDRESS FILE         *                
      *                                                        *                
      *    LISTING      : BP13L482 - WEEKLY LIST OF STUDIO APT *                
      *                              LESSEES                   *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY    DATE    DETAILS                          *                
      * ======  ==   ====== ===================================*                
      *BP130998 MALC 140501 NEW PGM                            *                
      *========================================================*                
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K830-KEY-FLD                               
                  ALTERNATE RECORD KEY IS K830-NUM-REGN                         
                                  WITH DUPLICATES                               
                  FILE STATUS     IS WS-K830-STATUS.                            
                                                                                
           SELECT BP13L482 ASSIGN TO BP13L482.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD   BP13F410                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F410.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13L482                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L482-REC                 PIC X(132).                            
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-SWITCHES.                                                         
           05  WS-F410-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K830-STATUS                 PIC 9(2)  VALUE ZEROS.            
           05  WS-CHAR-NOT-SPACE              PIC X     VALUE 'N'.              
                                                                                
       01  WORK-VARIABLES.                                                      
           05  WS-ADDRESS.                                                      
               10 WS-NUM-BLK                  PIC X(16) VALUE SPACES.           
               10 WS-NUM-FLAT                 PIC X(11) VALUE SPACES.           
               10 WS-NME-STREET-BLDG          PIC X(65) VALUE SPACES.           
               10 WS-NUM-POSTAL               PIC X(06) VALUE SPACES.           
           05  WS-ADD-TABLE.                                                    
               10 WS-ADD-CHAR                 OCCURS 100                        
                                              PIC X.                            
           05  WS-REP-TABLE.                                                    
               10 WS-REP-CHAR                 OCCURS 100                        
                                              PIC X.                            
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CTR                         PIC 999   VALUE ZEROS.            
           05  WS-REP-CTR                     PIC 999   VALUE ZEROS.            
           05  WS-CNT-NFND-K830               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-SKIP                    PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-F410               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-WRIT-L482               PIC 9(6)  VALUE ZEROS.            
           05  WS-L482-PAGE-CTR               PIC 9(04) VALUE ZEROS.            
           05  WS-L482-LINE-CTR               PIC 9(02) VALUE 55.               
           05  WS-L482-SNO-CTR                PIC 9(06) VALUE ZEROS.            
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  L482-HDG1.                                                           
           05  FILLER                   PIC X(14) VALUE 'BP13L482'.             
           05  FILLER                   PIC X(36) VALUE SPACES.                 
           05  FILLER                   PIC X(20) VALUE                         
               'SYSTEM OF COMMITMENT'.                                          
           05  FILLER                   PIC X(20) VALUE SPACES.                 
           05  FILLER                   PIC X(07) VALUE 'DATE : '.              
           05  L482-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(07) VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE 'PAGE: '.               
           05  L482-PAGE-NO             PIC ZZZ,ZZ9 VALUE ZEROS.                
                                                                                
       01  L482-HDG2.                                                           
           05  FILLER                   PIC X(46) VALUE SPACES.                 
           05  FILLER                   PIC X(27) VALUE                         
              'SALES UNIT (AGREEMENT TEAM)'.                                    
                                                                                
       01  L482-HDG3.                                                           
           05  FILLER                   PIC X(36) VALUE SPACES.                 
           05  FILLER                   PIC X(47) VALUE                         
              'WEEKLY LIST OF STUDIO APTS LESSEES TO SURRENDER'.                
                                                                                
       01  L482-HDG4.                                                           
           05  FILLER                   PIC X(44) VALUE SPACES.                 
           05  FILLER                   PIC X(29) VALUE                         
              'RENTAL FLAT AFTER KEYS ISSUED'.                                  
                                                                                
       01  L482-HDG5.                                                           
           05  FILLER               PIC X(01) VALUE SPACES.                     
           05  FILLER               PIC X(09) VALUE 'SERIAL NO'.                
           05  FILLER               PIC X(03) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE 'REGN NO'.                  
           05  FILLER               PIC X(24) VALUE SPACES.                     
           05  FILLER               PIC X(26) VALUE                             
               'ALLOC RENTAL FLAT ADDRESS'.                                     
           05  FILLER               PIC X(52) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE 'SO DATE'.                  
           05  FILLER               PIC X(05) VALUE SPACES.                     
                                                                                
       01  L482-HDG6.                                                           
           05  FILLER               PIC X(132) VALUE ALL '-'.                   
       01  WS-SPACES-L482           PIC X(132) VALUE SPACES.                    
                                                                                
       01  L482-DETAIL.                                                         
           05  FILLER                PIC X(03) VALUE SPACES.                    
           05  L482-SNO              PIC ZZZ,ZZ9.                               
           05  FILLER                PIC X(02) VALUE SPACES.                    
           05  L482-NUM-REGN         PIC X(08) VALUE SPACES.                    
           05  FILLER                PIC X(02) VALUE SPACES.                    
           05  L482-ALLOC-RENT-ADD   PIC X(98) VALUE SPACES.                    
           05  FILLER                PIC X(02) VALUE SPACES.                    
           05  L482-DTE-SO-EXECUTION PIC X(10) VALUE SPACES.                    
                                                                                
       01  L482-TRAILER-A.                                                      
           05  FILLER               PIC X(01) VALUE SPACES.                     
           05  TRAILER-DESC         PIC X(80) VALUE SPACES.                     
                                                                                
       01  L482-TRAILER-B.                                                      
           05  FILLER               PIC X(42) VALUE SPACES.                     
           05  FILLER               PIC X(40) VALUE                             
                 '* * *   E N D  O F  R E P O R T   * * *'.                     
                                                                                
       01  L482-TRAILER-C.                                                      
           05  FILLER               PIC X(48) VALUE SPACES.                     
           05  FILLER               PIC X(35) VALUE                             
                 '* * *   N O   R E C O R D S   * * *'.                         
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-PROCESS-F410      THRU 2000-EXIT                        
             UNTIL (WS-F410-EOF-SW = 'Y').                                      
                                                                                
           IF WS-CNT-WRIT-L482 > ZEROES                                         
              PERFORM 4020-PRINT-TRAILER    THRU 4020-EXIT                      
           ELSE                                                                 
              PERFORM 4030-PRINT-NO-RECORDS THRU 4030-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
           OPEN INPUT  BP13F410                                                 
                       BP13K830                                                 
                OUTPUT BP13L482.                                                
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K830 OPEN ERROR, STATUS IS ' WS-K830-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           STRING  FUNCTION CURRENT-DATE(7:2) '/'                               
                   FUNCTION CURRENT-DATE(5:2) '/'                               
                   FUNCTION CURRENT-DATE(1:4)                                   
           DELIMITED BY SIZE INTO L482-DATE.                                    
                                                                                
           PERFORM 4010-PRINT-HDG THRU 4010-EXIT.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2000-PROCESS-F410.                                                       
      ****************************************************************          
           READ BP13F410                                                        
              AT END     MOVE 'Y'        TO WS-F410-EOF-SW                      
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD +1                        TO WS-CNT-READ-F410.                   
                                                                                
           IF F410-CDE-ALLOC-RENTAL-FLAT = 'Y' AND                              
              (F410-DTE-KEY-ISSUE NUMERIC      AND                              
               F410-DTE-KEY-ISSUE > ZEROES)                                     
               PERFORM 3000-READ-K830    THRU 3000-EXIT                         
               PERFORM 4000-WRITE-L482   THRU 4000-EXIT                         
           ELSE                                                                 
              ADD 1                      TO WS-CNT-SKIP                         
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-READ-K830.                                                          
      ****************************************************************          
           MOVE F410-NUM-REGN  TO K830-NUM-REGN.                                
           READ BP13K830   KEY IS K830-NUM-REGN.                                
                                                                                
           IF WS-K830-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
           IF WS-K830-STATUS = 23                                               
              ADD +1  TO WS-CNT-NFND-K830                                       
              DISPLAY 'BP13K830 RECORD NOT FOUND, KEY IS ' K830-NUM-REGN        
              GO TO 2000-EXIT                                                   
           ELSE                                                                 
              DISPLAY 'BP13K830 READ ERROR, STATUS IS ' WS-K830-STATUS          
              DISPLAY 'K830 KEY-FLD-' K830-NUM-REGN                             
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4000-WRITE-L482.                                                         
      ****************************************************************          
           MOVE SPACES                TO BP13L482-REC.                          
           INITIALIZE                    BP13L482-REC.                          
           MOVE SPACES                TO L482-DETAIL.                           
           INITIALIZE                    L482-DETAIL.                           
                                                                                
           MOVE F410-NUM-REGN         TO L482-NUM-REGN.                         
           STRING  F410-DTE-SO-EXECUTION(7:2) '/'                               
                   F410-DTE-SO-EXECUTION(5:2) '/'                               
                   F410-DTE-SO-EXECUTION(1:4)                                   
           DELIMITED BY SIZE INTO L482-DTE-SO-EXECUTION.                        
                                                                                
           MOVE    K830-NUM-BLK         TO WS-NUM-BLK.                          
           MOVE    K830-NUM-FLAT        TO WS-NUM-FLAT.                         
           MOVE    K830-NME-STREET-BLDG TO WS-NME-STREET-BLDG.                  
           MOVE    K830-NUM-POSTAL      TO WS-NUM-POSTAL.                       
                                                                                
           MOVE SPACES                  TO WS-ADD-TABLE.                        
           MOVE SPACES                  TO WS-REP-TABLE.                        
           MOVE WS-ADDRESS              TO WS-ADD-TABLE.                        
           MOVE ZEROES                  TO WS-REP-CTR.                          
           MOVE 'N'                     TO WS-CHAR-NOT-SPACE.                   
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 100                
              IF WS-ADD-CHAR(WS-CTR) NOT = ' ' AND LOW-VALUES                   
                 ADD   1  TO WS-REP-CTR                                         
                 IF WS-CHAR-NOT-SPACE = 'N'                                     
                    MOVE SPACES           TO WS-REP-CHAR(WS-REP-CTR)            
                    ADD   1               TO WS-REP-CTR                         
                 END-IF                                                         
                 IF WS-CTR = 93                                                 
                    MOVE 'S'              TO WS-REP-CHAR(WS-REP-CTR)            
                    ADD 1                 TO WS-REP-CTR                         
                    MOVE '('              TO WS-REP-CHAR(WS-REP-CTR)            
                    ADD 1                 TO WS-REP-CTR                         
                 END-IF                                                         
                 MOVE WS-ADD-CHAR(WS-CTR) TO WS-REP-CHAR(WS-REP-CTR)            
                 MOVE 'Y'                 TO WS-CHAR-NOT-SPACE                  
                 IF WS-CTR = 98                                                 
                    ADD 1                 TO WS-REP-CTR                         
                    MOVE ')'              TO WS-REP-CHAR(WS-REP-CTR)            
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'N'                 TO WS-CHAR-NOT-SPACE                  
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           MOVE WS-REP-TABLE          TO L482-ALLOC-RENT-ADD.                   
           ADD  1                     TO WS-L482-SNO-CTR.                       
           MOVE WS-L482-SNO-CTR       TO L482-SNO.                              
                                                                                
           IF WS-L482-LINE-CTR = 55                                             
              PERFORM 4010-PRINT-HDG THRU 4010-EXIT                             
           END-IF.                                                              
                                                                                
           ADD  1  TO WS-L482-LINE-CTR.                                         
           WRITE BP13L482-REC FROM L482-DETAIL AFTER 1.                         
                                                                                
           ADD  1  TO WS-CNT-WRIT-L482.                                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4010-PRINT-HDG.                                                          
      ****************************************************************          
           ADD 1                      TO WS-L482-PAGE-CTR.                      
           MOVE WS-L482-PAGE-CTR      TO L482-PAGE-NO.                          
           WRITE BP13L482-REC FROM L482-HDG1      AFTER PAGE.                   
           WRITE BP13L482-REC FROM L482-HDG2      AFTER 1.                      
           WRITE BP13L482-REC FROM WS-SPACES-L482 AFTER 1.                      
           WRITE BP13L482-REC FROM L482-HDG3      AFTER 1.                      
           WRITE BP13L482-REC FROM L482-HDG4      AFTER 1.                      
           WRITE BP13L482-REC FROM WS-SPACES-L482 AFTER 1.                      
           WRITE BP13L482-REC FROM L482-HDG5      AFTER 1.                      
           WRITE BP13L482-REC FROM L482-HDG6      AFTER 1.                      
           MOVE 8                     TO WS-L482-LINE-CTR.                      
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4020-PRINT-TRAILER.                                                      
      ****************************************************************          
           IF WS-L482-LINE-CTR > 50                                             
              PERFORM 4010-PRINT-HDG THRU 4010-EXIT                             
           END-IF.                                                              
                                                                                
           WRITE BP13L482-REC FROM WS-SPACES-L482 AFTER 1.                      
           MOVE 'CC'     TO TRAILER-DESC.                                       
           WRITE BP13L482-REC FROM L482-TRAILER-A AFTER 1.                      
           MOVE 'HBO(            )              }      FOR YOUR FOLLOW-U        
      -            'P'                  TO TRAILER-DESC.                        
           WRITE BP13L482-REC FROM L482-TRAILER-A AFTER 1.                      
           MOVE 'H(RENTAL HOUSING SECTION)      }      ON THE SURRENDER         
      -            'OF THE RENTAL FLAT' TO TRAILER-DESC.                        
           WRITE BP13L482-REC FROM L482-TRAILER-A AFTER 1.                      
           WRITE BP13L482-REC FROM WS-SPACES-L482 AFTER 1.                      
           WRITE BP13L482-REC FROM L482-TRAILER-B AFTER 1.                      
                                                                                
       4020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4030-PRINT-NO-RECORDS.                                                   
      ****************************************************************          
           IF WS-L482-LINE-CTR > 50                                             
              PERFORM 4010-PRINT-HDG THRU 4010-EXIT                             
           END-IF.                                                              
                                                                                
           MOVE SPACES        TO   BP13L482-REC.                                
           WRITE BP13L482-REC FROM WS-SPACES-L482 AFTER 2.                      
           MOVE SPACES        TO   BP13L482-REC.                                
           WRITE BP13L482-REC FROM L482-TRAILER-C AFTER 1.                      
                                                                                
       4030-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
           CLOSE BP13F410                                                       
                 BP13K830                                                       
                 BP13L482.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : '  L482-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C482 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F410 RECORDS READ        = ' WS-CNT-READ-F410.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13K830 RECORDS NOT FOUND   = ' WS-CNT-NFND-K830.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F410 RECORDS SKIPPED     = ' WS-CNT-SKIP.               
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13L482 RECORDS WRITTEN     = ' WS-CNT-WRIT-L482.          
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
