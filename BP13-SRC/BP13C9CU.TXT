       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C9CU.                                                 
       AUTHOR.        LIZ PATIAG.                                               
       DATE-WRITTEN.  09/01/2018.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SOC                                   *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO OUTPUT BP13F23A                    *                
      *                                                        *                
      *    INPUT FILES : BP13F800                              *                
      *                  BP13K857                              *                
      *                  BP13K820                              *                
      *                  BP13K800                              *                
      *    OUTPUT FILES: BP13F23A                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY     DATE      DESCRIPTION                 *                
      * ------    --     ----      -----------                 *                
      * BP137161  FNP1  22/01/18   NEW PROGRAM                 *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN       TO BP13F800.                            
                                                                                
           SELECT BP13K857 ASSIGN       TO BP13K857                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K857-KEY-FLD                         
                           FILE STATUS  IS K857-STATUS.                         
                                                                                
           SELECT BP13K764 ASSIGN       TO BP13K764                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K764-KEY-FLD                         
                           FILE STATUS  IS K764-STATUS.                         
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS K800-STATUS.                         
                                                                                
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           ALTERNATE KEY IS K820-NUM-NRIC                       
                           FILE STATUS  IS K820-STATUS.                         
                                                                                
           SELECT BP13F23A ASSIGN       TO BP13F23A.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K857                                                             
           RECORD CONTAINS 200  CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K764                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K764.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS  400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F800                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F800.                                                           
                                                                                
       FD  BP13F23A                                                             
           RECORD CONTAINS 200  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F23A.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CURR-DATE                   PIC X(8)  VALUE SPACES.               
                                                                                
       01  K857-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K800-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K820-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K764-STATUS                    PIC 9(2)  VALUE ZERO.                 
                                                                                
       01  WS-F800-READ                   PIC 9(6)  VALUE ZERO.                 
       01  WS-K857-READ                   PIC 9(6)  VALUE ZERO.                 
       01  WS-K800-READ                   PIC 9(6)  VALUE ZERO.                 
       01  WS-K820-READ                   PIC 9(6)  VALUE ZERO.                 
       01  WS-K764-READ                   PIC 9(6)  VALUE ZERO.                 
       01  WS-F23A-WRITTEN                PIC 9(6)  VALUE ZERO.                 
                                                                                
       01  WS-EOF-F800                    PIC X     VALUE SPACE.                
           88 EOF-F800                    VALUE 'Y'.                            
                                                                                
       01  WS-K820-EOF                    PIC X     VALUE SPACES.               
       01  WS-K820-FND                    PIC X     VALUE SPACES.               
       01  WS-SCH-FND                     PIC X     VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F800     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-BP13F800  THRU 3000-EXIT                        
             UNTIL EOF-F800.                                                    
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K857                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K764                                                 
                OUTPUT BP13F23A.                                                
                                                                                
           IF K857-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K857-STATUS ' K857-STATUS                    
              MOVE K857-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K800-STATUS ' K800-STATUS                    
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K820-STATUS ' K820-STATUS                    
              MOVE K820-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K764-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K764-STATUS ' K764-STATUS                    
              MOVE K764-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F800.                                                      
      *---------------------------------------------------------------*         
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y' TO WS-EOF-F800                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-BP13F800.                                                   
      *---------------------------------------------------------------*         
           MOVE SPACES TO WS-SCH-FND.                                           
                                                                                
           PERFORM 3500-START-BP13K820  THRU 3500-EXIT.                         
                                                                                
           IF WS-SCH-FND NOT = 'Y'                                              
              PERFORM 4000-WRITE-F23A  THRU 4000-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3500-START-BP13K820.                                                     
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES          TO K820-KEY-FLD.                                
           MOVE F800-NUM-NRIC1  TO K820-NUM-NRIC.                               
           MOVE 'N'             TO WS-K820-EOF.                                 
                                                                                
                                                                                
           START BP13K820 KEY >= K820-NUM-NRIC                                  
                                                                                
           EVALUATE K820-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
                PERFORM 3600-READNEXT-BP13K820 THRU 3600-EXIT                   
                    UNTIL WS-K820-EOF = 'Y'                                     
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y' TO WS-K820-EOF                                         
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR, K820-STATUS ' K820-STATUS                  
                DISPLAY 'K820-NUM-NRIC = ' K820-NUM-NRIC                        
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3600-READNEXT-BP13K820.                                                  
      *---------------------*                                                   
                                                                                
           READ BP13K820 NEXT RECORD                                            
                                                                                
           EVALUATE K820-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
              IF K820-NUM-NRIC = F800-NUM-NRIC1                                 
                 ADD 1 TO WS-K820-READ                                          
                 IF K820-NUM-REGN NOT = F800-NUM-REGN                           
                    PERFORM 3700-READ-BP13K800 THRU 3700-EXIT                   
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'        TO WS-K820-EOF                                 
              END-IF                                                            
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'           TO WS-K820-EOF                                 
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K820, STATUS IS: '                     
                        K820-STATUS  ' -' F800-NUM-NRIC1                        
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3700-READ-BP13K800.                                                      
      *---------------------*                                                   
                                                                                
           MOVE SPACES        TO K800-NUM-REGN.                                 
           MOVE K820-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800                                                        
                                                                                
           EVALUATE K800-STATUS                                                 
           WHEN 00                                                              
              ADD 1 TO WS-K800-READ                                             
              IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES AND               
                                        ZEROES                                  
                 MOVE 'Y'        TO WS-SCH-FND                                  
              END-IF                                                            
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K800: ' K800-NUM-REGN            
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K800, STATUS IS: '                     
                        K800-STATUS  ' -' K800-NUM-REGN                         
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-WRITE-F23A.                                                         
      *********************                                                     
           MOVE SPACES           TO BP13F23A-REC.                               
           INITIALIZE               BP13F23A-REC.                               
                                                                                
                                                                                
           MOVE F800-NUM-REGN    TO F23A-NUM-REGN.                              
           MOVE 'SL'             TO F23A-TYPE-APPMT.                            
                                                                                
           PERFORM 4100-READ-BP13K857  THRU 4100-EXIT.                          
           MOVE K857-NUM-EMAIL   TO F23A-NUM-EMAIL.                             
                                                                                
           IF F23A-NUM-EMAIL = SPACES OR LOW-VALUES                             
              GO TO 4000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 4200-READ-BP13K764  THRU 4200-EXIT.                          
           MOVE K764-DTE-SALES-END TO F23A-DTE-ACCEPTANCE.                      
           MOVE K764-DTE-BALLOT    TO F23A-DTE-BALLOT.                          
                                                                                
           WRITE BP13F23A-REC.                                                  
           ADD 1 TO WS-F23A-WRITTEN.                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       4100-READ-BP13K857.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K857-REC.                    
           INITIALIZE                          BP13K857-REC.                    
                                                                                
           MOVE F800-NUM-REGN                  TO K857-NUM-REGN.                
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE K857-STATUS                                                 
           WHEN 00                                                              
                ADD 1 TO WS-K857-READ                                           
           WHEN 23                                                              
                MOVE SPACES       TO K857-NUM-EMAIL                             
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K857 : ' K857-STATUS                 
                MOVE K857-STATUS         TO RETURN-CODE                         
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4200-READ-BP13K764.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K764-REC.                    
           INITIALIZE                          BP13K764-REC.                    
                                                                                
           MOVE 'BTO'                       TO K764-KEY-FLD.                    
                                                                                
           READ BP13K764.                                                       
                                                                                
           EVALUATE K764-STATUS                                                 
           WHEN 00                                                              
                ADD 1 TO WS-K764-READ                                           
           WHEN 23                                                              
                MOVE SPACES       TO K764-DTE-SALES-END                         
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K764 : ' K764-STATUS                 
                MOVE K764-STATUS         TO RETURN-CODE                         
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-FILES.                                                        
      *---------------------------------------------------------------*         
                                                                                
           CLOSE BP13F800                                                       
                 BP13K857                                                       
                 BP13K820                                                       
                 BP13K800                                                       
                 BP13K764                                                       
                 BP13F23A.                                                      
                                                                                
           IF K857-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K857-STATUS ' K857-STATUS                   
              MOVE K857-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K800-STATUS ' K800-STATUS                   
              MOVE K800-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K820-STATUS ' K820-STATUS                   
              MOVE K820-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K764-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K764-STATUS ' K764-STATUS                   
              MOVE K764-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C9CS ================*'.              
           DISPLAY '   NO. OF F800 RECORDS READ      = ' WS-F800-READ.          
           DISPLAY '   NO. OF K857 RECORDS READ      = ' WS-K857-READ.          
           DISPLAY '   NO. OF K800 READ              = ' WS-K800-READ.          
           DISPLAY '   NO. OF K820 READ              = ' WS-K820-READ.          
           DISPLAY '   NO. OF K764 READ              = ' WS-K764-READ.          
           DISPLAY '   NO. OF F23A RECORDS WRITTEN   = ' WS-F23A-WRITTEN        
           DISPLAY '*==========================================*'.              
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
