       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF51.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  13/03/2014.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : GENERATE THE NCS CASES ELIGIBLE FOR MCPS    *            
      *                (STAY TOGETHER WITH PARENTS)                *            
      *                                                            *            
      * INPUT  FILES : BP13F730 - REQUEST DATA ENTRY FILE          *            
      *                BP13F825 - REQUEST OCCUPIER FILE            *            
      *                BP13K060 - CODE TRANSCRIPTION FILE          *            
      * I-O    FILE  : BP13K730 - REQUEST DATA ENTRY FILE          *            
      * OUTPUT FILES : BP13LF51 - LIST OF TAGGED MCPS CASES        *            
      *                P13LF51B - LIST OF NON-TAGGED MCPS          *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP135254  SMR2  13032014  NEW PROGRAM                      *            
      * BP135254  SMR2  30052014  ADD GO TO 4000-EXIT              *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13F825 ASSIGN       TO BP13F825.                            
                                                                                
           SELECT BP13K060 ASSIGN       TO BP13K060                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K060-KEY-FLD                         
                           FILE STATUS  IS WS-K060-STATUS.                      
                                                                                
           SELECT BP13K730 ASSIGN       TO BP13K730                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K730-NUM-REGN                        
                           FILE STATUS  IS WS-K730-STATUS.                      
                                                                                
           SELECT BP13LF51 ASSIGN       TO BP13LF51.                            
           SELECT P13LF51B ASSIGN       TO P13LF51B.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F825                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  200 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F825.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS   25 CHARACTERS.                                    
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K730.                                                           
                                                                                
       FD   BP13LF51                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   BP13LF51-REC                   PIC X(132).                          
                                                                                
       FD   P13LF51B                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   P13LF51B-REC                   PIC X(132).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K060-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K730-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(05)   VALUE ZEROES.            
           05  WS-K730-UPD                 PIC 9(05)   VALUE ZEROES.            
           05  WS-OCCP-CTR                 PIC 9(02)   VALUE ZEROES.            
           05  WS-CTR                      PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-F825-EOF                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-LF51-CONTROLS.                                                    
           05  WS-LF51-LINECTR             PIC 9(02)   VALUE 50.                
           05  WS-LF51-PAGECTR             PIC 9(05)   VALUE ZEROES.            
           05  WS-LF51-SNO                 PIC 9(05)   VALUE ZEROES.            
           05  WS-LF51-SNO-X               PIC 9(05)   VALUE ZEROES.            
                                                                                
       01  WS-LF51B-CONTROLS.                                                   
           05  WS-LF51B-LINECTR            PIC 9(02)   VALUE 50.                
           05  WS-LF51B-PAGECTR            PIC 9(05)   VALUE ZEROES.            
           05  WS-LF51B-SNO                PIC 9(05)   VALUE ZEROES.            
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-BLANKS                   PIC X(132)  VALUE SPACES.            
           05  WS-UPDATE-TAG               PIC X(01)   VALUE SPACES.            
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC           PIC 9(02).                           
                   15  WS-SYS-YY           PIC 9(02).                           
               10  WS-SYS-MM               PIC 9(02).                           
               10  WS-SYS-DD               PIC 9(02).                           
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD            PIC 9(02).                           
               10  FILLER                  PIC X(01)   VALUE '/'.               
               10  WS-EDITED-MM            PIC 9(02).                           
               10  FILLER                  PIC X(01)   VALUE '/'.               
               10  WS-EDITED-YY            PIC 9(04).                           
                                                                                
      *============================================================*            
      *         BP13LF51  -  R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  LF51-HEADER-1A.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13LF51'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LF51-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LF51-PAGE-NO        PIC ZZZZ9.                                   
                                                                                
       01  LF51-HEADER-1B.                                                      
           05  FILLER              PIC X(08)     VALUE 'P13LF51B'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LF51-RUN-DATE2      PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LF51-PAGE-NO2       PIC ZZZZ9.                                   
                                                                                
       01  LF51-HEADER-02.                                                      
           05  FILLER              PIC X(29)     VALUE SPACES.                  
           05  FILLER              PIC X(74)     VALUE                          
               'REPORT ON TAGGING OF MCP SCHEME FOR MULTI-TIER FAMILY UN        
      -        'DER NCS - ELIGIBLE'.                                            
                                                                                
       01  LF51-HEADER-02B.                                                     
           05  FILLER              PIC X(27)     VALUE SPACES.                  
           05  FILLER              PIC X(78)     VALUE                          
               'REPORT ON TAGGING OF MCP SCHEME FOR MULTI-TIER FAMILY UN        
      -        'DER NCS - NOT ELIGIBLE'.                                        
                                                                                
       01  LF51-HEADER-03.                                                      
           05  FILLER              PIC X(117)    VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'ELIG'.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'MC'.                    
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'EST'.                   
                                                                                
       01  LF51-HEADER-03B.                                                     
           05  FILLER              PIC X(05)     VALUE ' S/N '.                 
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REF NO'.                
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'NRIC'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(45)     VALUE 'NAME'.                  
           05  FILLER              PIC X(04)     VALUE 'RELN'.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE 'RELN DESC'.             
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'CIT'.                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'MAR STAT'.              
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'SCH '.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'PS'.                    
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'FT '.                   
                                                                                
       01  LF51-HEADER-04.                                                      
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  LF51-DETAIL.                                                         
           05  LF51-SNO            PIC X(05).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LF51-REFNO          PIC X(08).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LF51-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LF51-NAME           PIC X(45).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LF51-RELN           PIC X(03).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LF51-RELN-DESC      PIC X(20).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LF51-CITIZEN        PIC X(03).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LF51-MAR-STAT       PIC X(10).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LF51-ELIG-SCH       PIC X(04).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LF51-HA1-TAG        PIC X(02).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LF51-EST-FT         PIC X(03).                                   
                                                                                
       01  WS-RPT-DTLS.                                                         
           05  WS-RPT-ARRAY OCCURS 15 TIMES.                                    
               10  WS-REFNO        PIC X(08).                                   
               10  WS-NRIC         PIC X(09).                                   
               10  WS-NAME         PIC X(48).                                   
               10  WS-RELN         PIC X(03).                                   
               10  WS-CITZ         PIC X(02).                                   
               10  WS-MAR-STAT     PIC X(10).                                   
               10  WS-HA-TAG       PIC X(01).                                   
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT.                     
           PERFORM 3000-READ-BP13F825       THRU 3000-EXIT.                     
           PERFORM 4000-PROCESS-RECS        THRU 4000-EXIT                      
             UNTIL WS-F730-EOF = 'Y'                                            
                OR WS-F825-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F825                                                 
                       BP13K060                                                 
                I-O    BP13K730                                                 
                OUTPUT BP13LF51                                                 
                       P13LF51B.                                                
                                                                                
           IF WS-K060-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K060 FILE ERROR ' WS-K060-STATUS             
              MOVE WS-K060-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K730 FILE ERROR ' WS-K730-STATUS             
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYSTEM-DATE.                  
                                                                                
           MOVE WS-SYS-DD                   TO WS-EDITED-DD.                    
           MOVE WS-SYS-MM                   TO WS-EDITED-MM.                    
           MOVE WS-SYS-CCYY                 TO WS-EDITED-YY.                    
                                                                                
           MOVE WS-EDITED-DATE              TO LF51-RUN-DATE                    
                                               LF51-RUN-DATE2.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F730.                                                      
      *********************                                                     
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                    TO WS-F730-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F730-READ.                    
           MOVE 'N'                         TO WS-UPDATE-TAG.                   
           MOVE ZEROES                      TO WS-OCCP-CTR.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       3000-READ-BP13F825.                                                      
      *********************                                                     
                                                                                
           READ BP13F825 AT END                                                 
                MOVE 'Y'                    TO WS-F825-EOF                      
                GO TO 3000-EXIT.                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       4000-PROCESS-RECS.                                                       
      ********************                                                      
                                                                                
           IF F730-CDE-RESIDENT-HA1 NOT = 'M'                                   
              PERFORM 2000-READ-BP13F730    THRU 2000-EXIT                      
              GO TO 4000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F730-NUM-REGN = F825-NUM-REGN                                     
              PERFORM 5000-PROCESS-NUCLEUS  THRU 5000-EXIT                      
                UNTIL (F825-NUM-REGN > F730-NUM-REGN)                           
                   OR (WS-F825-EOF = 'Y')                                       
                   OR (WS-OCCP-CTR > 14)                                        
              IF WS-UPDATE-TAG = 'Y'                                            
                 PERFORM 6000-UPDATE-BP13K730  THRU 6000-EXIT                   
              END-IF                                                            
              PERFORM 7000-PRINT-RPT        THRU 7000-EXIT                      
              VARYING WS-CTR FROM 1 BY 1                                        
                UNTIL WS-CTR > WS-OCCP-CTR                                      
              PERFORM 2000-READ-BP13F730    THRU 2000-EXIT                      
           ELSE                                                                 
              IF F730-NUM-REGN > F825-NUM-REGN                                  
                 PERFORM 3000-READ-BP13F825 THRU 3000-EXIT                      
                   UNTIL (F825-NUM-REGN >= F730-NUM-REGN)                       
                      OR (WS-F825-EOF = 'Y')                                    
              ELSE                                                              
                 PERFORM 2000-READ-BP13F730 THRU 2000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       5000-PROCESS-NUCLEUS.                                                    
      ***********************                                                   
                                                                                
           ADD 1                            TO WS-OCCP-CTR.                     
           IF (F825-NUM-RELATIONSHIP = '02' OR '12' OR '22' OR '32') AND        
              (F825-NUM-CITIZENSHIP = '10' OR '20')                             
              MOVE 'Y'                      TO WS-UPDATE-TAG                    
           END-IF.                                                              
                                                                                
           MOVE F825-NUM-REF                TO WS-REFNO(WS-OCCP-CTR).           
           MOVE F825-NUM-NRIC               TO WS-NRIC(WS-OCCP-CTR).            
           MOVE F825-NME-OCCP               TO WS-NAME(WS-OCCP-CTR).            
           MOVE F825-NUM-RELATIONSHIP       TO WS-RELN(WS-OCCP-CTR).            
           MOVE F825-NUM-CITIZENSHIP        TO WS-CITZ(WS-OCCP-CTR).            
           MOVE F825-NUM-MARITAL-STATUS     TO WS-MAR-STAT(WS-OCCP-CTR).        
                                                                                
           IF F825-NUM-NRIC = F730-NUM-NRIC1 OR F730-NUM-NRIC2 OR               
                              F730-NUM-NRIC3 OR F730-NUM-NRIC4                  
              MOVE 'H'                      TO WS-HA-TAG(WS-OCCP-CTR)           
           ELSE                                                                 
              MOVE 'O'                      TO WS-HA-TAG(WS-OCCP-CTR)           
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13F825       THRU 3000-EXIT.                     
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       6000-UPDATE-BP13K730.                                                    
      ***********************                                                   
                                                                                
           MOVE SPACES                      TO BP13K730-REC.                    
           INITIALIZE                          BP13K730-REC.                    
                                                                                
           MOVE F730-NUM-REGN               TO K730-NUM-REGN.                   
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              MOVE '1'                      TO K730-CDE-RESIDENT-HA1            
              PERFORM 6500-REWRITE-K730     THRU 6500-EXIT                      
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K730. KEY IS '                   
                       F730-NUM-REGN                                            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K730 STATUS IS ' WS-K730-STATUS        
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       6500-REWRITE-K730.                                                       
      ********************                                                      
                                                                                
           REWRITE BP13K730-REC.                                                
                                                                                
           IF WS-K730-STATUS = ZEROES                                           
              ADD 1                         TO WS-K730-UPD                      
           ELSE                                                                 
              DISPLAY 'BP13K730 UPDATE ERROR, STATUS IS ' WS-K730-STATUS        
              DISPLAY 'BP13K730 RECORD KEY IS ' F730-NUM-REGN                   
           END-IF.                                                              
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************                                                         
       7000-PRINT-RPT.                                                          
      *****************                                                         
                                                                                
           PERFORM 7500-MOVE-VALUES      THRU 7500-EXIT.                        
                                                                                
           IF WS-UPDATE-TAG = 'Y'                                               
              IF WS-LF51-LINECTR >= 50                                          
                 ADD 1                   TO WS-LF51-PAGECTR                     
                 MOVE WS-LF51-PAGECTR    TO LF51-PAGE-NO                        
                 WRITE BP13LF51-REC      FROM LF51-HEADER-1A AFTER PAGE         
                 WRITE BP13LF51-REC      FROM LF51-HEADER-02                    
                 WRITE BP13LF51-REC      FROM LF51-HEADER-03 AFTER 2            
                 WRITE BP13LF51-REC      FROM LF51-HEADER-03B                   
                 WRITE BP13LF51-REC      FROM LF51-HEADER-04                    
                 WRITE BP13LF51-REC      FROM LF51-DETAIL                       
                 MOVE 7                  TO WS-LF51-LINECTR                     
              ELSE                                                              
                 ADD 1                   TO WS-LF51-LINECTR                     
                 WRITE BP13LF51-REC      FROM LF51-DETAIL                       
              END-IF                                                            
           ELSE                                                                 
              IF WS-LF51B-LINECTR >= 50                                         
                 ADD 1                   TO WS-LF51B-PAGECTR                    
                 MOVE WS-LF51B-PAGECTR   TO LF51-PAGE-NO2                       
                 WRITE P13LF51B-REC      FROM LF51-HEADER-1B AFTER PAGE         
                 WRITE P13LF51B-REC      FROM LF51-HEADER-02B                   
                 WRITE P13LF51B-REC      FROM LF51-HEADER-03 AFTER 2            
                 WRITE P13LF51B-REC      FROM LF51-HEADER-03B                   
                 WRITE P13LF51B-REC      FROM LF51-HEADER-04                    
                 WRITE P13LF51B-REC      FROM LF51-DETAIL                       
                 MOVE 7                  TO WS-LF51B-LINECTR                    
              ELSE                                                              
                 ADD 1                   TO WS-LF51B-LINECTR                    
                 WRITE P13LF51B-REC      FROM LF51-DETAIL                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       7500-MOVE-VALUES.                                                        
      *******************                                                       
                                                                                
           IF WS-UPDATE-TAG = 'Y'                                               
              IF WS-CTR = 1                                                     
                 ADD 1                      TO WS-LF51-SNO                      
                 MOVE WS-LF51-SNO           TO WS-LF51-SNO-X                    
                 MOVE WS-LF51-SNO-X         TO LF51-SNO                         
                 MOVE F730-CDE-ELIG-SCH     TO LF51-ELIG-SCH                    
                 MOVE K730-CDE-RESIDENT-HA1 TO LF51-HA1-TAG                     
                 WRITE BP13LF51-REC         FROM WS-BLANKS                      
                 ADD 1                      TO WS-LF51-LINECTR                  
              ELSE                                                              
                 MOVE SPACES                TO LF51-ELIG-SCH                    
                                               LF51-SNO                         
                 MOVE WS-HA-TAG(WS-CTR)     TO LF51-HA1-TAG                     
              END-IF                                                            
           ELSE                                                                 
              IF WS-CTR = 1                                                     
                 ADD 1                      TO WS-LF51B-SNO                     
                 MOVE WS-LF51B-SNO          TO WS-LF51-SNO-X                    
                 MOVE WS-LF51-SNO-X         TO LF51-SNO                         
                 MOVE F730-CDE-ELIG-SCH     TO LF51-ELIG-SCH                    
                 MOVE ' '                   TO LF51-HA1-TAG                     
                 WRITE P13LF51B-REC         FROM WS-BLANKS                      
                 ADD 1                      TO WS-LF51B-LINECTR                 
              ELSE                                                              
                 MOVE SPACES                TO LF51-ELIG-SCH                    
                                               LF51-SNO                         
                 MOVE WS-HA-TAG(WS-CTR)     TO LF51-HA1-TAG                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-CTR = 1                                                        
              MOVE F730-CDE-NT1             TO LF51-EST-FT                      
           ELSE                                                                 
              IF WS-CTR = 2                                                     
                 MOVE F730-CDE-FLAT-TYPE    TO LF51-EST-FT                      
              ELSE                                                              
                 MOVE SPACES                TO LF51-EST-FT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-REFNO(WS-CTR)            TO LF51-REFNO.                      
           MOVE WS-NRIC(WS-CTR)             TO LF51-NRIC.                       
           MOVE WS-NAME(WS-CTR)             TO LF51-NAME.                       
           MOVE WS-RELN(WS-CTR)             TO LF51-RELN.                       
                                                                                
           MOVE SPACES                      TO BP13K060-REC.                    
           MOVE '35'                        TO K060-SERIAL-NO.                  
           MOVE WS-RELN(WS-CTR)             TO K060-CODE.                       
           PERFORM 8000-READ-K060           THRU 8000-EXIT.                     
           MOVE K060-DESC                   TO LF51-RELN-DESC.                  
                                                                                
           MOVE SPACES                      TO BP13K060-REC.                    
           MOVE '41'                        TO K060-SERIAL-NO.                  
           MOVE WS-CITZ(WS-CTR)             TO K060-CODE.                       
           PERFORM 8000-READ-K060           THRU 8000-EXIT.                     
           MOVE K060-DESC                   TO LF51-CITIZEN.                    
                                                                                
           MOVE SPACES                      TO BP13K060-REC.                    
           MOVE '21'                        TO K060-SERIAL-NO.                  
           MOVE WS-MAR-STAT(WS-CTR)         TO K060-CODE.                       
           PERFORM 8000-READ-K060           THRU 8000-EXIT.                     
           MOVE K060-DESC                   TO LF51-MAR-STAT.                   
                                                                                
       7500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************                                                         
       8000-READ-K060.                                                          
      *****************                                                         
                                                                                
           READ BP13K060.                                                       
                                                                                
           EVALUATE WS-K060-STATUS                                              
           WHEN ZEROES                                                          
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
              DISPLAY 'BP13K060 RECORD NOT FOUND, KEY IS ' K060-KEY-FLD         
              DISPLAY 'F730-KEY-FLD-' F730-KEY-FLD                              
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K060 READ ERROR, STATUS IS ' WS-K060-STATUS          
              DISPLAY 'K060-KEY-FLD-' K060-KEY-FLD                              
                      '& F730-KEY-FLD-' F730-KEY-FLD                            
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*========== BP13CF51 CONTROL TOTALS ==========*'.           
           DISPLAY SPACES.                                                      
           DISPLAY '  BP13F730 RECORDS READ       = ' WS-F730-READ.             
           DISPLAY '  BP13LF51 RECORDS WRITTEN    = ' WS-LF51-SNO.              
           DISPLAY '  P13LF51B RECORDS WRITTEN    = ' WS-LF51B-SNO.             
           DISPLAY '  BP13K730 RECORDS UPDATED    = ' WS-K730-UPD.              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============================================*'.           
                                                                                
           CLOSE BP13F730                                                       
                 BP13F825                                                       
                 BP13K060                                                       
                 BP13K730                                                       
                 BP13LF51                                                       
                 P13LF51B.                                                      
                                                                                
           IF WS-K060-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K060 FILE ERROR ' WS-K060-STATUS             
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K730 FILE ERROR ' WS-K730-STATUS             
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        9000-EXIT.                                                              
            EXIT.                                                               
                                                                                
