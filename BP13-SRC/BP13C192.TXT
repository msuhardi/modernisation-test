       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C192.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  09/11/2004.                                               
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *   OBJECTIVE  :  TO MATCH BP13K740 WITH BP13K825 AND OUTPUT  *           
      *                 BP13F825 OF MATCHED CASES                   *           
      *                                                             *           
      *   INPUT      :  BP13F740 -- MATUREBA FILE                   *           
      *              :  BP13K825 -- OCCUPIER FILE                   *           
      *   OUTPUT     :  BP13F825 -- OCCUPIER FILE                   *           
      *                                                             *           
      *   AMENDMENTS:                                               *           
      *    REF      DATE     BY          DESCRIPTIONS               *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133056 19/05/2007 CKK  NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K825 ASSIGN TO BP13K825                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K825-KEY-FLD                               
                  FILE STATUS     IS WS-K825-STATUS.                            
                                                                                
           SELECT BP13F825 ASSIGN TO BP13F825.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F740       BLOCK  CONTAINS 0 RECORDS                             
                          RECORD CONTAINS 500 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K825       RECORD CONTAINS 200 CHARACTERS.                       
       COPY BP13K825.                                                           
                                                                                
       FD  BP13F825       BLOCK  CONTAINS 0 RECORDS                             
                          RECORD CONTAINS 200 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F825.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F740-READ-CTR        PIC 9(8)    VALUE ZERO.                      
       01  WS-K825-READ-CTR        PIC 9(8)    VALUE ZERO.                      
       01  WS-K825-NOTFND-CTR      PIC 9(8)    VALUE ZERO.                      
       01  WS-K825-OUTPUT-CTR      PIC 9(8)    VALUE ZEROES.                    
       01  WS-K825-STATUS          PIC 9(2)    VALUE 00.                        
                                                                                
       01  WS-EOF-F740             PIC X       VALUE SPACE.                     
       01  WS-EOF-K825             PIC X       VALUE SPACE.                     
       01  WS-K825-NOTFND          PIC X       VALUE SPACES.                    
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC   PIC 9(2).                                    
                   15  WS-SYS-YY   PIC 9(2).                                    
               10  WS-SYS-MM       PIC 9(2).                                    
               10  WS-SYS-DD       PIC 9(2).                                    
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-READ-F740     THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
              UNTIL WS-EOF-F740 = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K825                                                 
                OUTPUT BP13F825.                                                
           MOVE 'N' TO WS-EOF-F740                                              
                       WS-EOF-K825.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-F740.                                                          
      *****************************************************************         
           READ BP13F740 AT END                                                 
                MOVE 'Y' TO WS-EOF-F740.                                        
                                                                                
           IF WS-EOF-F740 NOT = 'Y'                                             
              ADD 1 TO WS-F740-READ-CTR                                         
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-PROCESS.                                                            
      *****************************************************************         
           MOVE SPACES                 TO BP13K825-REC.                         
           MOVE 'N'                    TO WS-EOF-K825.                          
                                                                                
           PERFORM 4000-STARTBR-K825 THRU 4000-EXIT.                            
           PERFORM 2000-READ-F740    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-STARTBR-K825.                                                       
      *****************************************************************         
           MOVE SPACES                        TO K825-KEY-FLD.                  
           MOVE SPACES                        TO K825-NUM-REGN.                 
           MOVE F740-NUM-REGN                 TO K825-NUM-REGN.                 
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN 00                                                              
                ADD 1                         TO WS-K825-READ-CTR               
                PERFORM 5000-K825-READ      THRU 5000-EXIT                      
                        UNTIL (K825-NUM-REGN NOT = F740-NUM-REGN) OR            
                              (WS-EOF-K825 = 'Y')                               
           WHEN 23                                                              
           WHEN 10                                                              
                ADD 1                         TO WS-K825-NOTFND-CTR             
                MOVE 'Y'                      TO WS-EOF-K825                    
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K825 :' WS-K825-STATUS               
                DISPLAY 'K825-NUM-REGN          :' K825-NUM-REGN                
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-K825-READ.                                                          
      *****************************************************************         
                                                                                
           READ BP13K825 NEXT.                                                  
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN 00                                                              
                IF F740-NUM-REGN = K825-NUM-REGN                                
                   PERFORM 6000-OUTPUT-K825 THRU 6000-EXIT                      
                END-IF                                                          
           WHEN 23                                                              
                ADD 1                         TO WS-K825-NOTFND-CTR             
                MOVE 'Y'                      TO WS-EOF-K825                    
           WHEN 10                                                              
                MOVE 'Y'                      TO WS-EOF-K825                    
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K825 :' WS-K825-STATUS               
                DISPLAY 'K825-NUM-REGN          :' K825-NUM-REGN                
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6000-OUTPUT-K825.                                                        
      *****************************************************************         
           MOVE BP13K825-REC TO BP13F825-REC.                                   
           WRITE BP13F825-REC.                                                  
           ADD 1 TO WS-K825-OUTPUT-CTR.                                         
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-ROUTINE.                                                      
      *****************************************************************         
                                                                                
           DISPLAY '*-----------  BP13C192 ------------*'.                      
           DISPLAY ' DATE                ' WS-SYSTEM-DATE.                      
           DISPLAY ' F740-READ           ' WS-F740-READ-CTR.                    
           DISPLAY ' K825-READ           ' WS-K825-READ-CTR.                    
           DISPLAY ' K825-NOTFND         ' WS-K825-NOTFND-CTR.                  
           DISPLAY ' K825-OUTPUT         ' WS-K825-OUTPUT-CTR.                  
           CLOSE     BP13F740                                                   
                     BP13F825                                                   
                     BP13K825.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
