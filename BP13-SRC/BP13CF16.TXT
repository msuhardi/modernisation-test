       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF16.                                                 
      *AUTHOR.        ZARITA DANE. D. SARMIENTO.                                
      *DATE-WRITTEN.  JUN 08 2010.                                              
                                                                                
      * SYSTEM OF COMMITMENT (SOC)                                              
      * ============================================================            
      * OBJECTIVE  - PRINT LETTERS ON NOTIFICATION OF                           
      *              HAND-OVER CEREMONY AT THE TELOK BLANGAH                    
      *                                                                         
      * FORM USED:                                                              
      *                                                                         
      * INPUT FILES:  BP13F421                                                  
      *               BP13K800                                                  
      *               BP13K820                                                  
      *                                                                         
      * REPORT:       BP13JAS                                                   
      *                                                                         
      *                                                                         
      * ------------------------------------------------------------            
      * REF NO    DATE  BY  DESCRIPTIONS                                        
      * -------- ------ --- ------------                                        
      * BP133925 080710 ZDD1 NEW PGM                                            
      * BP134227 230511 ZDD1 FOR SEAGR MEADOWS                                  
      * BP134244 310511 ZDD1 FOR JADE SPRING @ YISHUN                           
      * BP134435 170112 ZDD1 FOR TELOK BLANGAH                                  
      * ============================================================            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F421 ASSIGN       TO BP13F421.                            
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS K800-STATUS.                        
                                                                                
           SELECT BP13K820 ASSIGN        TO BP13K820                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K820-KEY-FLD                        
                           FILE STATUS   IS K820-STATUS.                        
                                                                                
           SELECT BM06K110 ASSIGN        TO BM06K110                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K110-KEY-FLD                        
                           FILE STATUS   IS K110-STATUS.                        
                                                                                
           SELECT BP13JAS ASSIGN        TO BP13JAS.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F421                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS.                                        
       COPY BP13F421.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13JAS            RECORDING MODE IS F                              
                               RECORD CONTAINS 120 CHARACTERS                   
                               BLOCK  CONTAINS 0   RECORDS.                     
       01   JAS-PRTREC         PIC X(120).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  K800-STATUS                   PIC 99    VALUE 0.                     
       01  K820-STATUS                   PIC 99    VALUE 0.                     
       01  K110-STATUS                   PIC 99    VALUE 0.                     
       01  WS-F421-EOF                   PIC X     VALUE 'N'.                   
                                                                                
       01  WS-FLAT-TYPE                  PIC X     VALUE SPACE.                 
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F421-READ           PIC 9(7)  VALUE 0.                     
           05 WS-CNT-LETTER              PIC 9(7)  VALUE 0.                     
           05 WS-CNT-COPY                PIC 9(7)  VALUE 0.                     
           05 WS-CNT-K800-MISS           PIC 9(7)  VALUE 0.                     
           05 WS-CNT-K820-MISS           PIC 9(7)  VALUE 0.                     
                                                                                
           05 WS-ERROR-FL                PIC X(1) VALUE 'N'.                    
              88 WS-NO-ERROR             VALUE 'N'.                             
              88 WS-ERROR                VALUE 'Y'.                             
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(90).                          
           05 WS-PARSED-STRING              PIC X(90).                          
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
       01  WS-ADDRESS.                                                          
           05 LJAS-BLK-NO                PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X     VALUE SPACES.                
           05 LJAS-FLAT-NO.                                                     
              10 FILLER                  PIC X     VALUE '#'.                   
              10 LJAS-LVL-NO             PIC X(2)  VALUE SPACES.                
              10 FILLER                  PIC X     VALUE '-'.                   
              10 LJAS-UNIT-NO            PIC X(4)  VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 LJAS-STREET                PIC X(47) VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE 'S('.                  
           05 LJAS-POSTAL-CODE           PIC X(6)  VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE ')'.                   
                                                                                
                                                                                
      *-------------------------------------------------------------            
      *    LETTER LAYOUT                                                        
      *-------------------------------------------------------------            
                                                                                
       01  JAS-HEADER-UP.                                                       
           05 FILLER                     PIC X(28) VALUE SPACES.                
           05 FILLER                     PIC X(1)   VALUE '#'.                  
           05 JAS-FLAT-NUM.                                                     
              10  JAS-LVL-NO             PIC X(2)  VALUE SPACES.                
              10 FILLER                  PIC X     VALUE '-'.                   
              10  JAS-UNIT-NO            PIC X(4)  VALUE SPACES.                
                                                                                
       01  JAS-LINE01.                                                          
           05 FILLER                     PIC X(15)  VALUE SPACES.               
           05 JAS-HEADER1                PIC X(32)                              
              VALUE 'I/We acknowledge receipt of the '.                         
           05 JAS-KEYS                   PIC X(02) VALUE SPACES.                
           05 JAS-HEADER2                PIC X(21)                              
              VALUE ' keys to the flat at '.                                    
                                                                                
       01  JAS-LINE02.                                                          
           05 FILLER                     PIC X(15)  VALUE SPACES.               
           05 JAS-BLK                    PIC X(04)  VALUE 'BLK '.               
           05 JAS-STREET                 PIC X(67)  VALUE SPACES.               
                                                                                
       01  JAS-LINE03.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(30)                             
               VALUE '_____________________________'.                           
                                                                                
       01  JAS-LINE04.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(26)                             
               VALUE 'RTP/Signature of Purchaser'.                              
                                                                                
       01  JAS-LINE05.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(07)                             
               VALUE 'Name : '.                                                 
           05  JAS-APP1-NAME              PIC X(66)  VALUE SPACES.              
                                                                                
       01  JAS-LINE06.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(06)                             
               VALUE 'UIN : '.                                                  
           05  JAS-APP1-UIN               PIC X(09)  VALUE SPACES.              
                                                                                
       01  JAS-LINE07.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(33)                             
               VALUE '_________________________________'.                       
                                                                                
       01  JAS-LINE08.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(29)                             
               VALUE 'RTP/Signature of Co-Purchaser'.                           
                                                                                
       01  JAS-LINE09.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(07)                             
               VALUE 'Name : '.                                                 
           05  JAS-APP2-NAME              PIC X(66)  VALUE SPACES.              
                                                                                
       01  JAS-LINE10.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(06)                             
               VALUE 'UIN : '.                                                  
           05  JAS-APP2-UIN               PIC X(09)  VALUE SPACES.              
                                                                                
       01  JAS-LINE11.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(33)                             
               VALUE '_________________________________'.                       
                                                                                
       01  JAS-LINE12.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(29)                             
               VALUE 'RTP/Signature of Co-Purchaser'.                           
                                                                                
       01  JAS-LINE13.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(07)                             
               VALUE 'Name : '.                                                 
           05  JAS-APP3-NAME              PIC X(66)  VALUE SPACES.              
                                                                                
       01  JAS-LINE14.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(06)                             
               VALUE 'UIN : '.                                                  
           05  JAS-APP3-UIN               PIC X(09)  VALUE SPACES.              
                                                                                
       01  JAS-LINE15.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(33)                             
               VALUE '_________________________________'.                       
                                                                                
       01  JAS-LINE16.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(29)                             
               VALUE 'RTP/Signature of Co-Purchaser'.                           
                                                                                
       01  JAS-LINE17.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(07)                             
               VALUE 'Name : '.                                                 
           05  JAS-APP4-NAME              PIC X(66)  VALUE SPACES.              
                                                                                
       01  JAS-LINE18.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(06)                             
               VALUE 'UIN : '.                                                  
           05  JAS-APP4-UIN               PIC X(09)  VALUE SPACES.              
                                                                                
       01  JAS-LINE19.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(17)                             
               VALUE 'Sales Regn No. : '.                                       
           05  JAS-REGNUM                 PIC X(08)  VALUE SPACES.              
                                                                                
       01  JAS-LINE20.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(07)                             
               VALUE 'Date : '.                                                 
           05  JAS-DATE                   PIC X(12)                             
               VALUE '04 Feb 2012'.                                             
                                                                                
       01  JAS-LINE21.                                                          
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(10)                             
               VALUE '***copy***'.                                              
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13C913.               *                 
      *-------------------------------------------------------*                 
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                   UNTIL WS-F421-EOF = 'Y'.                                     
           PERFORM 9900-HANDLE-ABEND.                                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-START-UP.                                                           
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F421                                                
                        BP13K800                                                
                        BP13K820                                                
                        BM06K110                                                
                OUTPUT  BP13JAS.                                                
                                                                                
                                                                                
                                                                                
            IF K800-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K800,STATUS ' K800-STATUS           
               MOVE K800-STATUS TO RETURN-CODE                                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            IF K820-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K820,STATUS ' K820-STATUS           
               MOVE K820-STATUS TO RETURN-CODE                                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            IF K110-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BM06K110,STATUS ' K110-STATUS           
               MOVE K110-STATUS TO RETURN-CODE                                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            READ BP13F421                                                       
                 AT END  DISPLAY 'NO RECORDS FOUND!'                            
                         MOVE 'Y'          TO WS-F421-EOF                       
                         GO TO 1000-EXIT                                        
            END-READ.                                                           
                                                                                
            ADD  1                          TO WS-CNT-F421-READ.                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           SET WS-NO-ERROR           TO TRUE.                                   
      *-------------------------------------------------------------*           
      * READS BP13F421 AND PROCESS EACH RECORD UNTIL END OF FILE    *           
      *-------------------------------------------------------------*           
                                                                                
           PERFORM 2100-RETRIEVE-DATA   THRU 2100-EXIT.                         
           PERFORM 2200-PROCESS-DATA    THRU 2200-EXIT.                         
           PERFORM 3100-PRINT-LETTER    THRU 3100-EXIT.                         
                                                                                
           READ BP13F421 AT END                                                 
                MOVE 'Y' TO WS-F421-EOF                                         
                GO TO 2000-EXIT                                                 
                END-READ.                                                       
                                                                                
           ADD  1                          TO WS-CNT-F421-READ.                 
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2100-RETRIEVE-DATA.                                                      
      *-------------------------------------------------------------            
           MOVE F421-num-regn              TO K800-NUM-REGN.                    
           PERFORM 6000-READ-K800          THRU 6000-EXIT.                      
           MOVE K800-NUM-SCH-ACC           TO K110-KEY-FLD.                     
           IF WS-NO-ERROR                                                       
              PERFORM 9000-READ-K110       THRU 9000-EXIT                       
              PERFORM 2200-PROCESS-DATA    THRU 2200-EXIT                       
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2200-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                     TO   JAS-STREET                      
                                                JAS-APP1-NAME                   
                                                JAS-APP1-UIN                    
                                                JAS-APP2-NAME                   
                                                JAS-APP2-UIN                    
                                                JAS-APP3-NAME                   
                                                JAS-APP3-UIN                    
                                                JAS-APP4-NAME                   
                                                JAS-APP4-UIN                    
                                                JAS-REGNUM                      
                                                JAS-KEYS.                       
           MOVE F421-NUM-REGN   TO  JAS-REGNUM.                                 
                                                                                
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES                        
              IF K800-NUM-NRIC1(1:2) NOT = '##'                                 
                 MOVE K800-NUM-REGN           TO   K820-NUM-REGN                
                 MOVE K800-NUM-NRIC1          TO   K820-NUM-NRIC                
                                                   JAS-APP1-UIN                 
                 PERFORM 7000-READ-K820       THRU 7000-EXIT                    
                 MOVE K820-NME-OCCP           TO   JAS-APP1-NAME                
                                                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              IF K800-NUM-NRIC2(1:2) NOT = '##'                                 
                 MOVE K800-NUM-REGN           TO   K820-NUM-REGN                
                 MOVE K800-NUM-NRIC2          TO   K820-NUM-NRIC                
                                                   JAS-APP2-UIN                 
                 PERFORM 7000-READ-K820       THRU 7000-EXIT                    
                 MOVE K820-NME-OCCP           TO   JAS-APP2-NAME                
                                                                                
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              IF K800-NUM-NRIC3(1:2) NOT = '##'                                 
                 MOVE K800-NUM-REGN           TO   K820-NUM-REGN                
                 MOVE K800-NUM-NRIC3          TO   K820-NUM-NRIC                
                                                   JAS-APP3-UIN                 
                 PERFORM 7000-READ-K820       THRU 7000-EXIT                    
                 MOVE K820-NME-OCCP           TO   JAS-APP3-NAME                
                                                                                
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              IF K800-NUM-NRIC4(1:2) NOT = '##'                                 
                 MOVE K800-NUM-REGN           TO   K820-NUM-REGN                
                 MOVE K800-NUM-NRIC4          TO   K820-NUM-NRIC                
                                                   JAS-APP4-UIN                 
                 PERFORM 7000-READ-K820       THRU 7000-EXIT                    
                 MOVE K820-NME-OCCP           TO   JAS-APP4-NAME                
                                                                                
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE SPACES                    TO WS-FLAT-TYPE.                      
                                                                                
           MOVE K800-NUM-FLAT-TYPE(1:1)   TO WS-FLAT-TYPE.                      
           EVALUATE WS-FLAT-TYPE                                                
             WHEN '3'                                                           
               IF K110-CDE-POD-TAG = 'Y' OR 'Z'                                 
                  MOVE '15'  TO JAS-KEYS                                        
               ELSE                                                             
                 IF K110-CDE-POD-TAG = 'N'                                      
                   MOVE '9'  TO JAS-KEYS                                        
                 ELSE                                                           
                   MOVE SPACES  TO JAS-KEYS                                     
                 END-IF                                                         
               END-IF                                                           
             WHEN '4'                                                           
             WHEN '5'                                                           
               IF K110-CDE-POD-TAG = 'Y' OR 'Z'                                 
                  MOVE '18'  TO JAS-KEYS                                        
               ELSE                                                             
                 IF K110-CDE-POD-TAG = 'N'                                      
                   MOVE '9'  TO JAS-KEYS                                        
                 ELSE                                                           
                   MOVE SPACES  TO JAS-KEYS                                     
                 END-IF                                                         
               END-IF                                                           
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2300-GET-NRIC-ADDR    THRU 2300-EXIT.                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2300-GET-NRIC-ADDR.                                                      
      *------------------------------------------------------                   
                                                                                
      *  TO CALL SUBROUTINE BP13C913,GETTING ADDRESS                            
           MOVE SPACES                   TO WS-LINK-REC.                        
           MOVE K800-NUM-SCH-ACC(1:4)    TO WS-LINK-NUM-SCH.                    
           MOVE K800-NUM-SCH-ACC(5:4)    TO WS-LINK-NUM-ACC.                    
           MOVE K800-NUM-SCH-ACC(9:1)    TO WS-LINK-NUM-CHK.                    
           PERFORM 8000-CALL-BP13C913      THRU 8000-EXIT.                      
                                                                                
           MOVE   COMM8-NUM-BLK    TO  LJAS-BLK-NO                              
                                                                                
           MOVE   COMM8-NUM-LEVEL         TO LJAS-LVL-NO                        
                                              JAS-LVL-NO                        
                                                                                
           MOVE   COMM8-NUM-UNIT-MAIN     TO LJAS-UNIT-NO                       
                                              JAS-UNIT-NO                       
                                                                                
           MOVE   COMM8-NME-STREET        TO LJAS-STREET                        
                                                                                
           MOVE   COMM8-NUM-POSTAL-CODE   TO LJAS-POSTAL-CODE                   
                                                                                
                                                                                
           MOVE SPACES                 TO WS-STRING-TO-PARSE.                   
           MOVE WS-ADDRESS             TO WS-STRING-TO-PARSE.                   
           MOVE LENGTH OF WS-ADDRESS                                            
                                       TO WS-STRING-LENGTH.                     
           MOVE 'Y'                    TO WS-INSERT-SPACE-FLAG.                 
           PERFORM 3010-PARSE-ADDRESS  THRU 3010-EXIT.                          
           MOVE WS-PARSED-STRING       TO  JAS-STREET.                          
                                                                                
       2300-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3010-PARSE-ADDRESS.                                                      
      *--------------------------------------------------------------           
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 3020-GET-CHAR          THRU 3020-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3020-GET-CHAR.                                                           
      *---------------------------------------------------------------          
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       3100-PRINT-LETTER.                                                       
      *-------------------------------------------------------------            
                                                                                
      * LETTER TO BE SENT OUT                                                   
      *-------------------------------------------------------------            
           ADD   1                   TO WS-CNT-LETTER.                          
           MOVE  SPACES              TO JAS-PRTREC.                             
           WRITE JAS-PRTREC  AFTER PAGE.                                        
           WRITE JAS-PRTREC FROM JAS-HEADER-UP.                                 
           WRITE JAS-PRTREC FROM JAS-LINE01 AFTER 13.                           
           WRITE JAS-PRTREC FROM JAS-LINE02.                                    
                                                                                
           WRITE JAS-PRTREC FROM JAS-LINE03 AFTER 08.                           
           WRITE JAS-PRTREC FROM JAS-LINE04.                                    
           WRITE JAS-PRTREC FROM JAS-LINE05.                                    
           WRITE JAS-PRTREC FROM JAS-LINE06.                                    
                                                                                
           WRITE JAS-PRTREC FROM JAS-LINE07 AFTER 5.                            
           WRITE JAS-PRTREC FROM JAS-LINE08.                                    
           WRITE JAS-PRTREC FROM JAS-LINE09.                                    
           WRITE JAS-PRTREC FROM JAS-LINE10.                                    
                                                                                
           WRITE JAS-PRTREC FROM JAS-LINE11 AFTER 5.                            
           WRITE JAS-PRTREC FROM JAS-LINE12.                                    
           WRITE JAS-PRTREC FROM JAS-LINE13.                                    
           WRITE JAS-PRTREC FROM JAS-LINE14.                                    
                                                                                
           WRITE JAS-PRTREC FROM JAS-LINE15 AFTER 5.                            
           WRITE JAS-PRTREC FROM JAS-LINE16.                                    
           WRITE JAS-PRTREC FROM JAS-LINE17.                                    
           WRITE JAS-PRTREC FROM JAS-LINE18.                                    
                                                                                
           WRITE JAS-PRTREC FROM JAS-LINE19 AFTER 6.                            
           WRITE JAS-PRTREC FROM JAS-LINE20.                                    
                                                                                
           PERFORM 3200-PRINT-COPY  THRU 3200-EXIT.                             
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3200-PRINT-COPY.                                                         
      *-------------------------------------------------------------            
                                                                                
      * COPY                                                                    
      *-------------------------------------------------------------            
           ADD   1                   TO WS-CNT-COPY.                            
           MOVE  SPACES              TO JAS-PRTREC.                             
           WRITE JAS-PRTREC  AFTER PAGE.                                        
           WRITE JAS-PRTREC FROM JAS-HEADER-UP AFTER 1.                         
           WRITE JAS-PRTREC FROM JAS-LINE01 AFTER 13.                           
           WRITE JAS-PRTREC FROM JAS-LINE02.                                    
                                                                                
           WRITE JAS-PRTREC FROM JAS-LINE03 AFTER 8.                            
           WRITE JAS-PRTREC FROM JAS-LINE04.                                    
           WRITE JAS-PRTREC FROM JAS-LINE05.                                    
           WRITE JAS-PRTREC FROM JAS-LINE06.                                    
                                                                                
           WRITE JAS-PRTREC FROM JAS-LINE07 AFTER 5.                            
           WRITE JAS-PRTREC FROM JAS-LINE08.                                    
           WRITE JAS-PRTREC FROM JAS-LINE09.                                    
           WRITE JAS-PRTREC FROM JAS-LINE10.                                    
                                                                                
           WRITE JAS-PRTREC FROM JAS-LINE11 AFTER 5.                            
           WRITE JAS-PRTREC FROM JAS-LINE12.                                    
           WRITE JAS-PRTREC FROM JAS-LINE13.                                    
           WRITE JAS-PRTREC FROM JAS-LINE14.                                    
                                                                                
           WRITE JAS-PRTREC FROM JAS-LINE15 AFTER 5.                            
           WRITE JAS-PRTREC FROM JAS-LINE16.                                    
           WRITE JAS-PRTREC FROM JAS-LINE17.                                    
           WRITE JAS-PRTREC FROM JAS-LINE18.                                    
                                                                                
           WRITE JAS-PRTREC FROM JAS-LINE19 AFTER 6.                            
           WRITE JAS-PRTREC FROM JAS-LINE20.                                    
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6000-READ-K800.                                                          
      *-------------------------------------------------------------            
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           EVALUATE K800-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD  1                  TO WS-CNT-K800-MISS                 
                    DISPLAY 'K800 REC NOT FOUND : ' k800-num-regn               
                                                                                
                    SET WS-ERROR            TO TRUE                             
                    GO TO 6000-EXIT                                             
               WHEN OTHER                                                       
                    MOVE K800-STATUS              TO RETURN-CODE                
                    DISPLAY 'ERROR READING BP13K800, STATUS '                   
                             K800-STATUS                                        
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       7000-READ-K820.                                                          
      *-------------------------------------------------------------            
           READ BP13K820 KEY IS K820-KEY-FLD.                                   
                                                                                
           EVALUATE K820-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD  1                  TO WS-CNT-K820-MISS                 
                    DISPLAY 'K820 REC NOT FOUND : ' K800-NUM-REGN               
                    GO TO 7000-EXIT                                             
               WHEN OTHER                                                       
                    MOVE K820-STATUS              TO RETURN-CODE                
                    DISPLAY 'ERROR READING BP13K820, STATUS '                   
                             K820-STATUS                                        
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8000-CALL-BP13C913.                                                      
      *-------------------------------------------------------------            
                                                                                
           CALL 'BP13C913' USING                                                
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9000-READ-K110.                                                          
      *-------------------------------------------------------------            
           READ BM06K110.                                                       
                                                                                
           EVALUATE K110-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'K110 REC NOT FOUND : ' K110-KEY-FLD                
               WHEN OTHER                                                       
                    MOVE K110-STATUS              TO RETURN-CODE                
                    DISPLAY 'ERROR READING BM06K110, STATUS '                   
                             K110-STATUS                                        
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9900-HANDLE-ABEND.                                                       
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '-  CONTROL TOTAL -------------'.                            
           DISPLAY ' TOTAL BP13F421 REC READ : ' WS-CNT-F421-READ.              
           DISPLAY ' TOTAL LETTERS PRINTED   : ' WS-CNT-LETTER.                 
           DISPLAY ' TOTAL COPY    PRINTED   : ' WS-CNT-COPY.                   
           DISPLAY ' TOTAL MISSING K800 REC  : ' WS-CNT-K800-MISS.              
           DISPLAY ' TOTAL MISSING K820 REC  : ' WS-CNT-K820-MISS.              
                                                                                
           CLOSE BP13K800                                                       
                 BP13K820                                                       
                 BP13F421                                                       
                 BP13JAS.                                                       
                                                                                
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BP13K820 ' K820-STATUS                  
              MOVE K820-STATUS TO RETURN-CODE.                                  
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BP13K800 ' K800-STATUS                  
              MOVE K800-STATUS TO RETURN-CODE.                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
