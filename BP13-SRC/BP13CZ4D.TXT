       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CZ4D.                                                 
      *AUTHOR.        SMP3.                                                     
      *DATE-WRITTEN.  23/02/2021.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE    : UPDATE CCA-SUCC FOR ELIGIBLE APPLICATIONS  *            
      *                 SHORTLISTED                                *            
      *                                                            *            
      *  INPUT FILE   : BP13F740 - SUCCESSFUL CASES                *            
      *  I-O   FILE   : BP13K740 - BP13.K740.MATUREBA              *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-REF# BY   DATE     DESCRIPTION                         *            
      * -------- ---- -------- ----------------------------------- *            
      * BP138600 SMP3 23022021 NEW PROGRAM                         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN       TO BP13F740.                            
                                                                                
           SELECT BP13K740 ASSIGN       TO BP13K740                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K740-KEY-FLD                         
                           FILE STATUS  IS WS-K740-STATUS.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K740.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K740-STATUS              PIC 9(02)  VALUE ZEROES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ                PIC 9(08)  VALUE ZEROES.             
           05  WS-K740-FND                 PIC 9(08)  VALUE ZEROES.             
           05  WS-K740-NTFND               PIC 9(08)  VALUE ZEROES.             
           05  WS-K740-UPD                 PIC 9(08)  VALUE ZEROES.             
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF                 PIC X(01)  VALUE 'N'.                
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                     PIC X(08)  VALUE SPACES.             
           05  WS-DATE-EDIT                PIC X(10)  VALUE SPACES.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT                       
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT BP13F740                                                  
                I-O   BP13K740.                                                 
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR OPENING BP13K740, STATUS: ' WS-K740-STATUS         
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-DATE.                          
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F740-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F740-READ.                     
           PERFORM 3000-READ-BP13K740      THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K740.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                     TO BP13K740-REC.                     
           INITIALIZE                         BP13K740-REC.                     
                                                                                
           MOVE F740-KEY-FLD               TO K740-KEY-FLD.                     
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                      TO WS-K740-FND                       
                PERFORM 5000-UPD-BP13K740  THRU 5000-EXIT                       
                                                                                
           WHEN 23                                                              
                ADD 1                      TO WS-K740-NTFND                     
                DISPLAY 'RECORD NOT FOUND IN BP13K740: ' K740-KEY-FLD           
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K740, STATUS: '                      
                         WS-K740-STATUS                                         
                MOVE WS-K740-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-UPD-BP13K740.                                                       
      *-------------------------------------------------------------            
           MOVE 'Y'                        TO K740-NUM-CCA-SUCC.                
                                                                                
           REWRITE BP13K740-REC.                                                
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                      TO WS-K740-UPD                       
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR UPDATING BP13K740, STATUS: '                     
                         WS-K740-STATUS                                         
                MOVE WS-K740-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '=========================================='.                
           DISPLAY '      PROGRAM : BP13CZ4D  ' WS-DATE-EDIT.                   
           DISPLAY '=========================================='.                
           DISPLAY ' '.                                                         
           DISPLAY ' 1. BP13F740 RECORDS READ      : ' WS-F740-READ.            
           DISPLAY ' 2. BP13K740 RECORDS FOUND     : ' WS-K740-FND.             
           DISPLAY ' 3. BP13K740 RECORDS NOT FOUND : ' WS-K740-NTFND.           
           DISPLAY ' 4. BP13K740 RECORDS UPDATED   : ' WS-K740-UPD.             
           DISPLAY ' '.                                                         
           DISPLAY ' CHECK : '.                                                 
           DISPLAY '    1 = 2 + 3 '.                                            
           DISPLAY '    2 = 4 '.                                                
           DISPLAY ' '.                                                         
           DISPLAY '=========================================='.                
                                                                                
           CLOSE  BP13F740                                                      
                  BP13K740.                                                     
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR CLOSING BP13K740, STATUS: ' WS-K740-STATUS         
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
