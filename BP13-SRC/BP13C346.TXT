      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.   BP13C346.                                                  
      *AUTHOR.       MCC                                                        
      *DATE WRITTEN. 08 JANUARY 2003.                                           
                                                                                
      ******************************************************************        
      *                                                                *        
      *  OBJECTIVE: TO PRINT A REPORT ON CPF S&P ACKNOWLEDGEMENT FILE  *        
      *                                                                *        
      *  INPUT  FILE :- BP13F106                                       *        
      *  OUTPUT FILE :- BP13L346                                       *        
      *                                                                *        
      ******************************************************************        
      * CR #       DATE      ID          CHANGES                       *        
      * BP139544 03/05/23   ZAR7    TO USE THIS PGM AGAIN              *        
      ******************************************************************        
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F106 ASSIGN TO BP13F106.                                  
                                                                                
           SELECT BP13L346 ASSIGN TO BP13L346.                                  
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K110-KEY-FLD                    
                               FILE STATUS   IS WS-K110-STATUS.                 
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F106                                                             
           RECORD CONTAINS 250 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F106.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD  BP13L346                                                             
           RECORDING MODE IS F.                                                 
       01 BP13L346-REC       PIC X(132).                                        
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-SWITCHES.                                                         
           05  WS-F106-EOF               PIC X    VALUE 'N'.                    
               88 F106-EOF                        VALUE 'Y'.                    
           05  WS-FIRST-RECORD           PIC X    VALUE 'N'.                    
           05  WS-F106-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-LINE-CNT               PIC 9(5) VALUE 60.                     
           05  WS-PAGE-CNT               PIC 9(5) VALUE ZEROS.                  
           05  WS-SNO                    PIC 9(5) VALUE ZEROS.                  
           05  WS-REC-PRINTED            PIC 9(5) VALUE ZEROS.                  
           05  WS-F110-CNT               PIC 9(5) VALUE ZEROS.                  
           05  WS-F110-CNT1              PIC 9(5) VALUE ZEROS.                  
           05  WS-K110-STATUS            PIC 9(2) VALUE ZEROS.                  
                                                                                
       01  WS-DATE                       PIC X(8) VALUE SPACES.                 
                                                                                
      *============================================================*            
      *         BP13L346  -  R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  L346-HEADER-01.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L346'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(40)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T '.                      
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L346-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L346-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  L346-HEADER-02.                                                      
           05  FILLER              PIC X(45)     VALUE SPACES.                  
           05  FILLER              PIC X(40)     VALUE                          
               'ACKNOWLEDGEMENT/REJECTION FILE FROM CPF'.                       
           05  FILLER              PIC X(43)     VALUE SPACES.                  
                                                                                
       01  L346-HEADER-03.                                                      
           05  FILLER              PIC X(24)     VALUE                          
               'HDB TRANSMISSION DATE : '.                                      
           05  L346-TRANS-DTE      PIC X(10)     VALUE SPACES.                  
                                                                                
       01  L346-HEADER-04.                                                      
           05  FILLER              PIC X(24)     VALUE                          
               'CPF PROCESS DATE      : '.                                      
           05  L346-PROCESS-DTE    PIC X(10)     VALUE SPACES.                  
                                                                                
       01  L346-HEADER-05.                                                      
           05  FILLER              PIC X(24)     VALUE                          
               'REMARKS               : '.                                      
           05  L346-HDR-REMARKS    PIC X(40)     VALUE SPACES.                  
                                                                                
       01  L346-HEADER-06.                                                      
           05  FILLER              PIC X(05)     VALUE 'SNO  '.                 
           05  FILLER              PIC X(02)     VALUE  SPACES.                 
           05  FILLER              PIC X(08)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(02)     VALUE  SPACES.                 
           05  FILLER              PIC X(11)     VALUE 'SCHEME ACCT'.           
           05  FILLER              PIC X(02)     VALUE  SPACES.                 
           05  FILLER              PIC X(08)     VALUE 'REC TYPE'.              
           05  FILLER              PIC X(02)     VALUE  SPACES.                 
           05  FILLER              PIC X(10)     VALUE 'ACTION TAG'.            
           05  FILLER              PIC X(02)     VALUE  SPACES.                 
           05  FILLER              PIC X(40)     VALUE 'REMARKS'.               
                                                                                
       01  L346-HEADER-07.                                                      
           05  FILLER              PIC X(132)    VALUE ALL '-'.                 
                                                                                
       01  L346-DETAIL.                                                         
           05  L346-SNO            PIC ZZZZ9.                                   
           05  FILLER              PIC X(02)     VALUE  SPACES.                 
           05  L346-REGN           PIC X(08)     VALUE  SPACES.                 
           05  FILLER              PIC X(02)     VALUE  SPACES.                 
           05  L346-SCHACC         PIC X(11)     VALUE  SPACES.                 
           05  FILLER              PIC X(02)     VALUE  SPACES.                 
           05  L346-REC-TYPE       PIC X(08)     VALUE  SPACES.                 
           05  FILLER              PIC X(02)     VALUE  SPACES.                 
           05  L346-ACTION-TAG     PIC X(10)     VALUE  SPACES.                 
           05  FILLER              PIC X(02)     VALUE  SPACES.                 
           05  L346-REMARKS        PIC X(40)     VALUE  SPACES.                 
                                                                                
       01  L346-CONTROL-01.                                                     
           05  FILLER              PIC X(10)     VALUE  SPACES.                 
           05  FILLER              PIC X(09)     VALUE 'RECS SENT'.             
           05  FILLER              PIC X(03)     VALUE  SPACES.                 
           05  FILLER              PIC X(08)     VALUE 'REJECTED'.              
                                                                                
       01  L346-NO-ERROR.                                                       
           05  FILLER              PIC X(47)     VALUE  SPACES.                 
           05  FILLER              PIC X(38)     VALUE                          
                    'SUCCESSFUL TRANSMISSON : NO REJECTIONS'.                   
                                                                                
       01  L346-CONTROL-02.                                                     
           05  FILLER              PIC X(08)     VALUE 'PROPERTY'.              
           05  FILLER              PIC X(04)     VALUE  SPACES.                 
           05  L346-PROP-TXN       PIC ZZZZZZ9   VALUE  ZEROES.                 
           05  FILLER              PIC X(04)     VALUE  SPACES.                 
           05  L346-PROP-REJ       PIC ZZZZZZ9   VALUE  ZEROES.                 
                                                                                
       01  L346-CONTROL-03.                                                     
           05  FILLER              PIC X(08)     VALUE 'BUYER   '.              
           05  FILLER              PIC X(04)     VALUE  SPACES.                 
           05  L346-BUYER-TXN      PIC ZZZZZZ9   VALUE  ZEROES.                 
           05  FILLER              PIC X(04)     VALUE  SPACES.                 
           05  L346-BUYER-REJ      PIC ZZZZZZ9   VALUE  ZEROES.                 
                                                                                
       01  L346-CONTROL-04.                                                     
           05  FILLER              PIC X(08)     VALUE 'SELLER  '.              
           05  FILLER              PIC X(04)     VALUE  SPACES.                 
           05  L346-SELLER-TXN     PIC ZZZZZZ9   VALUE  ZEROES.                 
           05  FILLER              PIC X(04)     VALUE  SPACES.                 
           05  L346-SELLER-REJ     PIC ZZZZZZ9   VALUE  ZEROES.                 
                                                                                
       01  L346-CONTROL-05.                                                     
           05  FILLER              PIC X(12)     VALUE  SPACES.                 
           05  FILLER              PIC X(7)      VALUE  ALL '='.                
           05  FILLER              PIC X(04)     VALUE  SPACES.                 
           05  FILLER              PIC X(7)      VALUE  ALL '='.                
                                                                                
       01  L346-CONTROL-06.                                                     
           05  FILLER              PIC X(08)     VALUE 'TOTAL   '.              
           05  FILLER              PIC X(04)     VALUE  SPACES.                 
           05  L346-TOTAL-TXN      PIC ZZZZZZ9   VALUE  ZEROES.                 
           05  FILLER              PIC X(04)     VALUE  SPACES.                 
           05  L346-TOTAL-REJ      PIC ZZZZZZ9   VALUE  ZEROES.                 
                                                                                
       01  L346-CONTROL-07.                                                     
           05  FILLER              PIC X(07)     VALUE  SPACES.                 
           05  FILLER              PIC X(13)     VALUE '(SENT BY HDB)'.         
           05  FILLER              PIC X(01)     VALUE  SPACES.                 
           05  FILLER              PIC X(17)  VALUE '(REJECTED BY CPF)'.        
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *-----------------------------*                                           
       000-MAIN-LOGIC.                                                          
      *-----------------------------*                                           
            PERFORM 100-OPEN-FILES     THRU 100-EXIT.                           
            PERFORM 200-READ-F106      THRU 200-EXIT.                           
            PERFORM 300-PROCESS-UPDATE THRU 300-EXIT                            
                    UNTIL F106-EOF.                                             
            PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------*                                           
       100-OPEN-FILES.                                                          
      *-----------------------------*                                           
           OPEN INPUT  BP13F106                                                 
                       BM06K110                                                 
                OUTPUT BP13L346.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BM06K110. STATUS : ' WS-K110-STATUS        
              MOVE WS-K110-STATUS       TO  RETURN-CODE                         
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2)  '/'  WS-DATE(5:2)  '/'  WS-DATE(1:4)            
                  DELIMITED BY SIZE INTO L346-RUN-DATE.                         
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------*                                           
       200-READ-F106.                                                           
      *-----------------------------*                                           
           READ BP13F106 AT END                                                 
           MOVE 'Y'                      TO WS-F106-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F106-READ.                       
                                                                                
           IF F106-HEADER-FLD = LOW-VALUES                                      
              PERFORM 500-PRINT-HEADER  THRU 500-EXIT.                          
                                                                                
           IF F106-CONTROL-FLD = HIGH-VALUES AND NOT F106-EOF                   
              PERFORM 600-PRINT-CONTROL THRU 600-EXIT                           
              MOVE 'Y' TO WS-F106-EOF                                           
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------*                                           
       300-PROCESS-UPDATE.                                                      
      *-----------------------------*                                           
           IF WS-LINE-CNT > 50                                                  
              PERFORM 400-WRITE-HEADING THRU 400-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE SPACES TO L346-DETAIL.                                          
           INITIALIZE     L346-DETAIL.                                          
                                                                                
           ADD 01                     TO WS-SNO.                                
           MOVE WS-SNO                TO L346-SNO.                              
                                                                                
           EVALUATE F106-CDE-ACTION-TAG                                         
           WHEN '1'                                                             
              MOVE 'NEW CASE '           TO L346-ACTION-TAG                     
           WHEN '2'                                                             
              MOVE 'AMENDMENT'           TO L346-ACTION-TAG                     
           WHEN '3'                                                             
              MOVE 'CANCELLED'           TO L346-ACTION-TAG                     
           END-EVALUATE.                                                        
                                                                                
           EVALUATE F106-NUM-PROP-REC-TYP                                       
           WHEN '1'                                                             
              MOVE F106-NUM-PROP-HDB-REF  TO L346-SCHACC                        
              MOVE 'PROPERTY'             TO L346-REC-TYPE                      
              MOVE F106-NUM-PROP-REMARK   TO L346-REMARKS                       
           WHEN '2'                                                             
              MOVE F106-NUM-BUYER-HDB-REF TO L346-SCHACC                        
              MOVE 'BUYER   '             TO L346-REC-TYPE                      
              MOVE F106-NUM-BUYER-REMARK  TO L346-REMARKS                       
           END-EVALUATE.                                                        
                                                                                
           PERFORM 350-START-BM06K110   THRU 350-EXIT.                          
                                                                                
           MOVE SPACES TO BP13L346-REC.                                         
           WRITE BP13L346-REC FROM L346-DETAIL.                                 
                                                                                
           ADD 01 TO WS-REC-PRINTED.                                            
           ADD 01 TO WS-LINE-CNT.                                               
                                                                                
           PERFORM 200-READ-F106    THRU 200-EXIT.                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       350-START-BM06K110.                                                      
      *-------------------*                                                     
             MOVE SPACES                           TO K110-REC.                 
             INITIALIZE                               K110-REC.                 
                                                                                
             MOVE SPACES                           TO K110-KEY-FLD.             
             MOVE L346-SCHACC                      TO K110-KEY-FLD.             
                                                                                
             START BM06K110 KEY >= K110-KEY-FLD.                                
             EVALUATE WS-K110-STATUS                                            
                 WHEN 00                                                        
                 WHEN 02                                                        
                     PERFORM 360-READ-BM06K110  THRU 360-EXIT                   
                     PERFORM 360-READ-BM06K110  THRU 360-EXIT                   
                 WHEN 23                                                        
                     CONTINUE                                                   
                 WHEN OTHER                                                     
                     DISPLAY 'ERROR STARTING BM06K110: ' WS-K110-STATUS         
                                ' KEY = ' K110-KEY-FLD                          
                     PERFORM  999-CLOSE-FILES THRU 999-EXIT                     
             END-EVALUATE.                                                      
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       360-READ-BM06K110.                                                       
      *------------------*                                                      
            READ  BM06K110 NEXT AT END                                          
                  MOVE SPACES               TO K110-KEY-FLD.                    
                                                                                
            EVALUATE WS-K110-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                       IF K110-KEY-FLD(1:9) = L346-SCHACC                       
                          ADD 1               TO WS-F110-CNT                    
                          MOVE K110-REGN-NO   TO L346-REGN                      
                       END-IF                                                   
                WHEN 23                                                         
                       ADD 1               TO WS-F110-CNT1                      
                WHEN OTHER                                                      
                      DISPLAY 'ERROR READING BM06K110 : ' WS-K110-STATUS        
                               ' KEY = ' K110-KEY-FLD                           
                      PERFORM  999-CLOSE-FILES     THRU 999-EXIT                
            END-EVALUATE.                                                       
                                                                                
       360-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------*                                           
       400-WRITE-HEADING.                                                       
      *-----------------------------*                                           
            MOVE   ZEROS                TO   WS-LINE-CNT.                       
            ADD    1                    TO   WS-PAGE-CNT.                       
            MOVE   WS-PAGE-CNT          TO   L346-PAGE-NO.                      
            MOVE   SPACES               TO   BP13L346-REC.                      
            WRITE  BP13L346-REC         FROM L346-HEADER-01 AFTER PAGE.         
            MOVE   SPACES               TO   BP13L346-REC.                      
            WRITE  BP13L346-REC         FROM L346-HEADER-02.                    
            MOVE   SPACES               TO   BP13L346-REC.                      
            WRITE  BP13L346-REC.                                                
                                                                                
            WRITE  BP13L346-REC         FROM L346-HEADER-06.                    
            WRITE  BP13L346-REC         FROM L346-HEADER-07.                    
            ADD 5                       TO   WS-LINE-CNT.                       
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------*                                           
       500-PRINT-HEADER.                                                        
      *-----------------------------*                                           
           MOVE    ZEROS                TO   WS-LINE-CNT.                       
           ADD     1                    TO   WS-PAGE-CNT.                       
           MOVE    WS-PAGE-CNT          TO   L346-PAGE-NO.                      
                                                                                
           MOVE    SPACES               TO   BP13L346-REC.                      
           WRITE   BP13L346-REC         FROM L346-HEADER-01 AFTER PAGE.         
           MOVE    SPACES               TO   BP13L346-REC.                      
           WRITE   BP13L346-REC         FROM L346-HEADER-02.                    
           MOVE    SPACES               TO   BP13L346-REC.                      
           WRITE   BP13L346-REC.                                                
                                                                                
           MOVE    F106-DTE-HDB-SEND    TO   L346-TRANS-DTE.                    
           MOVE    SPACES               TO   BP13L346-REC.                      
           WRITE   BP13L346-REC         FROM L346-HEADER-03.                    
                                                                                
           MOVE    F106-DTE-CPF-PROCESS TO   L346-PROCESS-DTE.                  
           MOVE    SPACES               TO   BP13L346-REC.                      
           WRITE   BP13L346-REC         FROM L346-HEADER-04.                    
                                                                                
           IF F106-NUM-REJECTION-REMARK NOT = SPACES AND LOW-VALUES             
              MOVE   F106-NUM-REJECTION-REMARK TO L346-HDR-REMARKS              
              MOVE   SPACES                    TO BP13L346-REC                  
              WRITE  BP13L346-REC         FROM L346-HEADER-05                   
              MOVE 'Y'                         TO WS-F106-EOF                   
           ELSE                                                                 
              MOVE   SPACES                    TO BP13L346-REC                  
              WRITE  BP13L346-REC                                               
              MOVE   SPACES                    TO BP13L346-REC                  
              WRITE  BP13L346-REC         FROM L346-HEADER-06                   
              MOVE   SPACES                    TO BP13L346-REC                  
              WRITE  BP13L346-REC         FROM L346-HEADER-07                   
              PERFORM 200-READ-F106  THRU 200-EXIT                              
           END-IF.                                                              
                                                                                
           ADD 7  TO  WS-LINE-CNT.                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------*                                           
       600-PRINT-CONTROL.                                                       
      *-----------------------------*                                           
           IF WS-REC-PRINTED = ZEROES                                           
              MOVE   SPACES         TO   BP13L346-REC                           
              WRITE  BP13L346-REC   FROM L346-NO-ERROR                          
              GO TO 600-EXIT.                                                   
                                                                                
           IF WS-LINE-CNT > 40                                                  
              PERFORM 400-WRITE-HEADING THRU 400-EXIT                           
           END-IF.                                                              
                                                                                
            MOVE   SPACES TO BP13L346-REC.                                      
            WRITE  BP13L346-REC.                                                
                                                                                
            MOVE   SPACES                   TO   BP13L346-REC.                  
            WRITE  BP13L346-REC             FROM L346-CONTROL-01.               
                                                                                
            MOVE   F106-NUM-PROP-RECS       TO   L346-PROP-TXN.                 
            MOVE   F106-NUM-PROP-RECS-REJ   TO   L346-PROP-REJ.                 
            MOVE   SPACES                   TO   BP13L346-REC.                  
            WRITE  BP13L346-REC             FROM L346-CONTROL-02.               
                                                                                
            MOVE   F106-NUM-BUYER-RECS      TO   L346-BUYER-TXN.                
            MOVE   F106-NUM-BUYER-RECS-REJ  TO   L346-BUYER-REJ.                
            MOVE   SPACES                   TO   BP13L346-REC.                  
            WRITE  BP13L346-REC             FROM L346-CONTROL-03.               
                                                                                
            MOVE   F106-NUM-SELLER-RECS     TO   L346-SELLER-TXN.               
            MOVE   F106-NUM-SELLER-RECS-REJ TO   L346-SELLER-REJ.               
            MOVE   SPACES                   TO   BP13L346-REC.                  
            WRITE  BP13L346-REC             FROM L346-CONTROL-04.               
                                                                                
            MOVE   SPACES                   TO   BP13L346-REC.                  
            WRITE  BP13L346-REC             FROM L346-CONTROL-05.               
                                                                                
            MOVE   F106-NUM-REC-CNT-HDB     TO   L346-TOTAL-TXN.                
            MOVE   F106-NUM-REC-CNT-CPF     TO   L346-TOTAL-REJ.                
            MOVE   SPACES                   TO   BP13L346-REC.                  
            WRITE  BP13L346-REC             FROM L346-CONTROL-06.               
                                                                                
            MOVE   SPACES                   TO   BP13L346-REC.                  
            WRITE  BP13L346-REC             FROM L346-CONTROL-07.               
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------*                                           
       999-CLOSE-FILES.                                                         
      *-----------------------------*                                           
           CLOSE BP13F106                                                       
                 BM06K110                                                       
                 BP13L346.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BM06K110. STATUS : ' WS-K110-STATUS        
              MOVE WS-K110-STATUS       TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------------------ BP13C346 -------------------'.          
                                                                                
           DISPLAY 'NO OF REC READ      BP13F106     = '                        
                    WS-F106-READ.                                               
           DISPLAY 'NO OF REC READ      BM06K110     = '                        
                    WS-F110-CNT.                                                
           DISPLAY 'NO OF REC NOT FND   BM06K110     = '                        
                    WS-F110-CNT1.                                               
           DISPLAY 'NO OF RECS WRITTEN               = '                        
                    WS-REC-PRINTED.                                             
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
