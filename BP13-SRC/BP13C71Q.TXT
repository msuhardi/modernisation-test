      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID.   BP13C71Q.                                                  
      *AUTHOR.       IL5                                                        
      *DATE-WRITTEN. 05/03/2009.                                                
      *--------------------------------------------------------*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES  : MATCHING & UPDATING BP13K595          *                
      *                                                        *                
      *    INPUT FILES  : BP13F595                             *                
      *    I-O   FILES  : BP13K595                             *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE      DETAILS                         *                
      *-------- ---- --------  --------------------------------*                
      *BP13XXXX IL5  20080305  NEW PGM                         *                
      *BP135170 SMR2 20140116  CATER FOR DUPLICATE RECORDS     *                
      *--------------------------------------------------------*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595   ASSIGN TO BP13F595.                                
                                                                                
           SELECT BP13K595   ASSIGN TO BP13K595                                 
                             ORGANIZATION  IS INDEXED                           
                             ACCESS MODE   IS DYNAMIC                           
                             RECORD KEY    IS K595-KEY-FLD                      
                             FILE STATUS   IS WS-K595-STATUS.                   
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   BP13F595                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F595.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
                                                                                
       COPY BP13K595.                                                           
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WS-FILE-VARIABLES.                                                   
           05 WS-F595-EOF               PIC X(1)    VALUE 'N'.                  
                                                                                
           05 WS-READ-F595              PIC 9(8)    VALUE 0.                    
           05 WS-K595-WRITE             PIC 9(8)    VALUE 0.                    
           05 WS-K595-STATUS            PIC 9(2)    VALUE 00.                   
           05 WS-K595-WRITE-DUPL        PIC 9(8)    VALUE ZEROES.               
           05 WS-TME-SYSTEM             PIC 9(8)    VALUE ZEROES.               
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F595     THRU 2000-EXIT.                           
                                                                                
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
                   UNTIL WS-F595-EOF = 'Y'.                                     
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F595                                                 
                I-O    BP13K595.                                                
                                                                                
           EVALUATE WS-K595-STATUS                                              
               WHEN 00                                                          
               WHEN 97                                                          
                  CONTINUE                                                      
               WHEN OTHER                                                       
                  MOVE WS-K595-STATUS        TO RETURN-CODE                     
                  DISPLAY 'OPEN BP13K595 FAIL. STATUS=' WS-K595-STATUS          
                  PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F595.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F595                                                        
                AT END MOVE 'Y' TO WS-F595-EOF                                  
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F595.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-PROCESS.                                                            
      *--------------------------------------------------------*                
                                                                                
           PERFORM 4000-WRITE-K595       THRU 4000-EXIT                         
           PERFORM 2000-READ-F595      THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-WRITE-K595.                                                         
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES TO BP13K595-REC.                                         
           INITIALIZE     BP13K595-REC.                                         
                                                                                
           MOVE BP13F595-REC   TO BP13K595-REC.                                 
                                                                                
           WRITE BP13K595-REC.                                                  
                                                                                
           EVALUATE WS-K595-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1 TO WS-K595-WRITE                                         
              WHEN 22                                                           
                 PERFORM 4100-WRITE-K595    THRU 4100-EXIT                      
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR UPDATING BP13K595, STATUS = '                  
                         WS-K595-STATUS                                         
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4100-WRITE-K595.                                                         
      *--------------------------------------------------------*                
                                                                                
           MOVE F595-TME-SYSTEM             TO WS-TME-SYSTEM.                   
           ADD 1                            TO WS-TME-SYSTEM.                   
           MOVE WS-TME-SYSTEM               TO K595-TME-SYSTEM.                 
                                                                                
           WRITE BP13K595-REC.                                                  
                                                                                
           EVALUATE WS-K595-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1                      TO WS-K595-WRITE-DUPL               
              WHEN 22                                                           
                 DISPLAY 'DUPLICATE KEY IN BP13K595, REF  = '                   
                         K595-NUM-REGN-REF                                      
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR UPDATING BP13K595, STATUS = '                  
                         WS-K595-STATUS                                         
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '**********  BP13C71Q  *************'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS READ    :'                                    
           DISPLAY '   BP13F595           : '  WS-READ-F595.                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS WRITE   :'                                    
           DISPLAY '   BP13K595           : '  WS-K595-WRITE.                   
           DISPLAY '   BP13K595 - DUPL    : '  WS-K595-WRITE-DUPL.              
           DISPLAY SPACES.                                                      
           DISPLAY '***********************************'.                       
                                                                                
           CLOSE BP13F595                                                       
                 BP13K595.                                                      
                                                                                
           EVALUATE WS-K595-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K595-STATUS        TO RETURN-CODE                      
                 DISPLAY 'CLOSE BP13K595 FAIL. STATUS=' WS-K595-STATUS          
           END-EVALUATE.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
