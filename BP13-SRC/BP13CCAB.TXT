      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CCAB.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  13/02/2009.                                               
      *****************************************************************         
      * OBJECTIVE - MATCH BP13F136 AND BM06F120 TO GET FT                       
      *                                                                         
      *     INPUT FILE :                                                        
      *                  1. BP13F136                                            
      *                  2. BM06F120                                            
      *                                                                         
      *     OUTPUT FILE:                                                        
      *                  1. P13F136A                                            
      *----------------------------------------------------------------         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS                        
      * -------   --------  ---  ------------------------                       
      * BP133545  20090213  JB8  NEW PROGRAM.                                   
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F136  ASSIGN        TO BP13F136.                         
           SELECT  BM06F120  ASSIGN        TO BM06F120.                         
           SELECT  P13F136A  ASSIGN        TO P13F136A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F136                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 150 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F136.                                                           
                                                                                
       FD   BM06F120                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 700 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BM06F120.                                                           
                                                                                
       FD  P13F136A                    BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 150 CHARACTERS           
                                       LABEL RECORDS ARE OMITTED                
                                       RECORDING MODE IS F.                     
       01  P13F136A-REC                PIC X(150).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  WS-VARIBLES.                                                         
           05  WS-F136-READ              PIC 9(06) VALUE ZEROS.                 
           05  WS-F120-READ              PIC 9(06) VALUE ZEROS.                 
           05  WS-F136A-WRITE            PIC 9(06) VALUE ZEROS.                 
           05  CNT-F120-NTFND            PIC 9(06) VALUE ZEROS.                 
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN-PARA.                                                          
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP           THRU  1000-EXIT.                     
           PERFORM 2000-READ-BP13F136      THRU  2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORD     THRU  3000-EXIT                      
             UNTIL F136-NUM-REGN = HIGH-VALUES                                  
               AND F120-REGN-NO  = HIGH-VALUES.                                 
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F136                                                 
                       BM06F120                                                 
                OUTPUT P13F136A.                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-BP13F136.                                                      
      ******************************************************************        
                                                                                
           READ BP13F136           AT   END                                     
                MOVE HIGH-VALUES   TO   F136-NUM-REGN                           
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F136-READ.                           
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2500-READ-BM06F120.                                                      
      ******************************************************************        
                                                                                
           READ BM06F120           AT   END                                     
                MOVE HIGH-VALUES   TO   F120-REGN-NO                            
                GO                 TO   2500-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F120-READ.                           
                                                                                
       2500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORD.                                                     
      ******************************************************************        
                                                                                
           EVALUATE TRUE                                                        
              WHEN F136-NUM-REGN = F120-REGN-NO                                 
                   PERFORM 4000-WRITE-REPORT   THRU 4000-EXIT                   
                     UNTIL F136-NUM-REGN NOT = F120-REGN-NO                     
                        OR F136-NUM-REGN = HIGH-VALUES                          
                   PERFORM 2500-READ-BM06F120  THRU 2500-EXIT                   
              WHEN F136-NUM-REGN < F120-REGN-NO                                 
                   MOVE SPACES                   TO F120-CDE-TYP-ACTUSE         
                                                    F120-SCH-ACC-NO             
                   ADD 1 TO CNT-F120-NTFND                                      
                   PERFORM 4000-WRITE-REPORT   THRU 4000-EXIT                   
              WHEN F136-NUM-REGN > F120-REGN-NO                                 
                   PERFORM 2500-READ-BM06F120  THRU 2500-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4000-WRITE-REPORT.                                                       
      ******************************************************************        
                                                                                
           MOVE F120-CDE-TYP-ACTUSE      TO F136-NUM-FLAT-TYPE-SA.              
           MOVE F120-SCH-ACC-NO          TO F136-NUM-SCH-ACCT.                  
           WRITE P13F136A-REC FROM BP13F136-CASHTRAN.                           
           ADD 1 TO WS-F136A-WRITE.                                             
                                                                                
           PERFORM 2000-READ-BP13F136  THRU 2000-EXIT.                          
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CCAB'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F136 RECORDS READ..........: '                    
                    WS-F136-READ.                                               
           DISPLAY 'NO OF BM06F120 RECORDS WRITTEN.......: '                    
                    WS-F120-READ.                                               
           DISPLAY 'NO OF BM06F120 RECORDS NOT MATCH ....: '                    
                    CNT-F120-NTFND.                                             
           DISPLAY 'NO OF P13F136A RECORDS WRITTEN.......: '                    
                    WS-F136A-WRITE.                                             
                                                                                
           CLOSE BP13F136                                                       
                 BM06F120                                                       
                 P13F136A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
