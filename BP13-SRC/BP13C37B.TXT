       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C37B.                                                  
      *DATE-WRITTEN. 21 MAR 2016.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) To update BP13K73E key issue date and date  *            
      *                BR10 passed                                 *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136196 20160321 KVL1  NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F73E  ASSIGN        TO BP13F73E.                          
           SELECT BP13F410  ASSIGN        TO BP13F410.                          
                                                                                
           SELECT BP13K73E  ASSIGN        TO BP13K73E                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K73E-KEY-FLD                       
                            FILE STATUS   IS BP13K73E-STATUS.                   
                                                                                
                                                                                
           SELECT BP13L73E  ASSIGN        TO BP13L73E.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F73E                                                             
           RECORD CONTAINS 500  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F73E.                                                           
                                                                                
       FD  BP13F410                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F410.                                                           
                                                                                
       FD  BP13K73E                                                             
           RECORD CONTAINS 500  CHARACTERS.                                     
       COPY BP13K73E.                                                           
                                                                                
       FD  BP13L73E                                                             
           RECORD CONTAINS 19   CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13L73E-REC.                                                        
           05 L73E-NUM-REGN     PIC X(8).                                       
           05 L73E-SEQ-NUM      PIC 9(2).                                       
           05 L73E-NUM-NRIC     PIC X(9).                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-READ-F73E-CNT    PIC 9(05)  VALUE ZEROES.                         
       01  WS-READ-F410-CNT    PIC 9(05)  VALUE ZEROES.                         
       01  WS-WRITE-CNT        PIC 9(05)  VALUE ZEROES.                         
       01  BP13K73E-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BP13F410-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  WS-BP13F73E-EOF     PIC X      VALUE 'N'.                            
       01  WS-BP13F410-EOF     PIC X      VALUE 'N'.                            
       01  WS-SAVE-REGN-NUM    PIC X(08)  VALUE SPACES.                         
       01  WS-SYS-DATE         PIC X(08)  VALUE SPACES.                         
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYS-DATE.                     
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F73E       THRU 2000-EXIT.                     
           PERFORM 2100-READ-BP13F410       THRU 2100-EXIT.                     
           IF WS-BP13F73E-EOF = 'Y' OR WS-BP13F410-EOF = 'Y'                    
              IF WS-BP13F73E-EOF = 'Y'                                          
                 DISPLAY 'FILE BP13F73E IS  EMPTY'                              
              END-IF                                                            
              IF WS-BP13F410-EOF = 'Y'                                          
                 DISPLAY 'FILE BP13F410 IS  EMPTY'                              
              END-IF                                                            
           ELSE                                                                 
              PERFORM 3000-MATCHING         THRU 3000-EXIT                      
                UNTIL WS-BP13F73E-EOF = 'Y' OR                                  
                      WS-BP13F410-EOF = 'Y'                                     
           END-IF                                                               
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F410                                                 
                       BP13F73E.                                                
           OPEN I-O    BP13K73E.                                                
           OPEN OUTPUT BP13L73E.                                                
                                                                                
           IF BP13K73E-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K73E OPEN ERROR, STATUS : ' BP13K73E-STATUS          
              MOVE     BP13K73E-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       2000-READ-BP13F73E.                                                      
      *--------------------*                                                    
           READ BP13F73E AT END                                                 
                         MOVE 'Y'  TO WS-BP13F73E-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-F73E-CNT                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       2100-READ-BP13F410.                                                      
      *--------------------*                                                    
           READ BP13F410 AT END                                                 
                         MOVE 'Y'  TO WS-BP13F410-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-F410-CNT                          
           END-READ.                                                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-READ-BP13K73E.                                                      
      *-------------------------------------------------------------            
           READ BP13K73E.                                                       
                                                                                
           EVALUATE BP13K73E-STATUS                                             
              WHEN 00                                                           
                 PERFORM 3100-MOVE-TO-OUTPUT  THRU 3100-EXIT                    
                 WRITE BP13L73E-REC                                             
                 PERFORM 2300-UPDATE-BP13K73E THRU 2300-EXIT                    
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 DISPLAY 'REGN NUM NOT FOUND IN BP13K73E. KEY IS '              
                            F73E-NUM-REGN                                       
                 MOVE BP13K73E-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K73E. STATUS IS '                   
                            BP13K73E-STATUS                                     
                 MOVE BP13K73E-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-UPDATE-BP13K73E.                                                    
      *-------------------------------------------------------------            
           REWRITE  BP13K73E-REC.                                               
                                                                                
           EVALUATE BP13K73E-STATUS                                             
             WHEN 00                                                            
                CONTINUE                                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13K73B REWRITE ERROR, STATUS : '                     
                         BP13K73E-STATUS                                        
                MOVE BP13K73E-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MATCHING.                                                           
      *-------------------------------------------------------------            
           IF F73E-NUM-REGN        =   F410-NUM-REGN                            
              MOVE SPACES                      TO   BP13K73E-REC                
              MOVE F73E-NUM-REGN               TO   K73E-NUM-REGN               
                                                    WS-SAVE-REGN-NUM            
              MOVE F73E-SEQ-NUM                TO   K73E-SEQ-NUM                
              MOVE F73E-NUM-NRIC               TO   K73E-NUM-NRIC               
              PERFORM 2200-READ-BP13K73E       THRU 2200-EXIT                   
              PERFORM 2000-READ-BP13F73E       THRU 2000-EXIT                   
              IF F73E-NUM-REGN NOT =   WS-SAVE-REGN-NUM                         
                 PERFORM 2100-READ-BP13F410    THRU 2100-EXIT                   
              END-IF                                                            
           ELSE                                                                 
              IF F73E-NUM-REGN     <   F410-NUM-REGN                            
                 PERFORM 2000-READ-BP13F73E    THRU 2000-EXIT                   
              ELSE                                                              
                 PERFORM 2100-READ-BP13F410    THRU 2100-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-MOVE-TO-OUTPUT.                                                     
      *-------------------------------------------------------------            
           MOVE F410-DTE-KEY-ISSUE      TO K73E-DTE-KEY-ISSUE.                  
           IF K73E-PPO-MONITOR = 'Y'                                            
              MOVE WS-SYS-DATE          TO K73E-DTE-BR10-PASSED                 
           END-IF.                                                              
                                                                                
           MOVE F73E-NUM-REGN           TO L73E-NUM-REGN.                       
           MOVE F73E-SEQ-NUM            TO L73E-SEQ-NUM.                        
           MOVE F73E-NUM-NRIC           TO L73E-NUM-NRIC.                       
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '---- BP13C37B CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF REC READ FROM BP13F73E  = '                          
                     WS-READ-F73E-CNT                                           
           DISPLAY ' NO OF REC READ FROM BP13F410  = '                          
                     WS-READ-F410-CNT                                           
           DISPLAY ' NO OF REC KEY ISS UPDATED     = '                          
                     WS-WRITE-CNT.                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F410                                                 
                       BP13F73E                                                 
                       BP13K73E                                                 
                       BP13L73E.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
