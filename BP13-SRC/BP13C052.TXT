      *------------------------------------------------------------*            
       IDENTIFICATION DIVISION.                                                 
      *------------------------------------------------------------*            
       PROGRAM-ID.   BP13C052.                                                  
      *AUTHOR.       SK.                                                        
      *DATE-WRITTEN. 04/03/2004.                                                
      *------------------------------------------------------------*            
      *                   SYSTEM OF COMMITMENT  (BP13)             *            
      *------------------------------------------------------------*            
      *                                                            *            
      *  OBJECTIVE   : 1.  TO RE-ASSIGN OIC FROM HISTORY FILE      *            
      *                                                            *            
      *  INPUT FILES : 1.  BP13F022  -  ASIGN OIC FILE             *            
      *                2.  BP13F028  -  OIC HISTORY FILE           *            
      *                3.  BP13K022  -  OIC FILE                   *            
      *                4.  BP13K028  -  OIC HISTORY FILE           *            
      *                                                            *            
      *  REPORT      : 1.  BP13L052  -  LISTING                    *            
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *------------------------------------------------------------*            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F028 ASSIGN TO BP13F028.                                  
           SELECT BP13F026 ASSIGN TO BP13F026.                                  
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
           SELECT BP13K026 ASSIGN TO BP13K026                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K026-KEY-FLD                               
                  FILE STATUS     IS WS-K026-STATUS.                            
                                                                                
           SELECT BP13K028 ASSIGN TO BP13K028                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K028-KEY-FLD                               
                  FILE STATUS     IS WS-K028-STATUS.                            
                                                                                
           SELECT BP13L052 ASSIGN TO BP13L052.                                  
                                                                                
      *------------------------------------------------------------*            
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *------------------------------------------------------------*            
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K026                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K026.                                                           
                                                                                
       FD   BP13K028                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K028.                                                           
                                                                                
       FD   BP13F028                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       COPY BP13F028.                                                           
                                                                                
       FD   BP13F026                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       COPY BP13F026.                                                           
                                                                                
       FD   BP13L052                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   BP13L052-REC     PIC X(132).                                        
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
                                                                                
       01  WORK-AREAS.                                                          
           05  WS-F028-READ             PIC 9(08)  VALUE 0.                     
           05  WS-F026-READ             PIC 9(08)  VALUE 0.                     
           05  WS-K022-NFND             PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-K022-WRITE            PIC 9(08)  VALUE 0.                     
           05  WS-K022-DELETE           PIC 9(08)  VALUE 0.                     
           05  WS-K026-DELETE           PIC 9(08)  VALUE 0.                     
           05  WS-K028-WRITE            PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-K022-STATUS           PIC 9(02)  VALUE 0.                     
           05  WS-K026-STATUS           PIC 9(02)  VALUE 0.                     
           05  WS-K028-STATUS           PIC 9(02)  VALUE 0.                     
                                                                                
           05  WS-F028-EOF              PIC X(01)  VALUE 'N'.                   
           05  WS-F026-EOF              PIC X(01)  VALUE 'N'.                   
           05  WS-K022-FOUND            PIC X(01)  VALUE 'N'.                   
           05  WS-FLAG                  PIC X(01)  VALUE 'N'.                   
           05  WS-LAST-OIC              PIC X(05)  VALUE SPACES.                
           05  WS-OIC                   PIC X(05)  VALUE SPACES.                
                                                                                
           05  WS-CNT-LINE-ON-PAGE      PIC 9(8)   VALUE 66.                    
           05  WS-CNT-PRINT-L052        PIC 9(8)   VALUE 1.                     
           05  WS-CNT-WRITE-L052        PIC 9(8)   VALUE 0.                     
           05  WS-NO-OF-PAGE            PIC 9(8)   VALUE 0.                     
                                                                                
       01  WS-DATE-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY          PIC 9(04).                              
               10  WS-SYS-MM            PIC 9(02).                              
               10  WS-SYS-DD            PIC 9(02).                              
                                                                                
           05  WS-EDITED-DATE           PIC X(10)  VALUE SPACES.                
                                                                                
      *------------------------------------------------------------*            
      *    BP13L052 - LAYOUT                                       *            
      *------------------------------------------------------------*            
                                                                                
       01  L052-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L052'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L052-PRINT-DATE          PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L052-PRINT-PAGE          PIC ZZZ9.                               
                                                                                
       01  L052-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(45) VALUE SPACES.                 
           05  FILLER                   PIC X(75) VALUE                         
                'RE-ASSIGNING FROM OIC-HISTORY TO PREVIOUS OIC'.                
                                                                                
       01  L052-PR-HEAD-03.                                                     
           05  FILLER           PIC X(05)  VALUE ' S-NO'.                       
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(10)  VALUE 'REGN NO'.                     
           05  FILLER           PIC X(12)  VALUE 'FROM OIC'.                    
           05  FILLER           PIC X(03)  VALUE SPACES.                        
           05  FILLER           PIC X(08)  VALUE 'TO OIC'.                      
                                                                                
       01  L052-PR-HEAD-04.                                                     
           05  FILLER                PIC X(132) VALUE ALL '-'.                  
                                                                                
       01  L052-PR-DETAILS.                                                     
           05  L052-NUMBER           PIC ZZZZ9.                                 
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L052-REGN-NO          PIC X(10)  VALUE SPACES.                   
           05  L052-FROM-OIC         PIC X(12)  VALUE SPACES.                   
           05  FILLER                PIC X(06)  VALUE SPACES.                   
           05  L052-TO-OIC           PIC X(05)  VALUE SPACES.                   
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 1100-READ-F026     THRU 1100-EXIT.                           
           PERFORM 1200-READ-F028     THRU 1200-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
                   UNTIL WS-F026-EOF = 'Y' OR                                   
                         WS-F028-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1000-OPEN-FILES.                                                         
      *------------------------------------------------------------*            
                                                                                
           OPEN INPUT  BP13F026                                                 
                       BP13F028                                                 
                I-O    BP13K022                                                 
                       BP13K026                                                 
                       BP13K028                                                 
                OUTPUT BP13L052.                                                
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BP13K022 FAIL. STATUS - '                        
                                            WS-K022-STATUS                      
                 MOVE  WS-K022-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K026-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BP13K026 FAIL. STATUS - '                        
                                            WS-K026-STATUS                      
                 MOVE  WS-K026-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K028-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BP13K028 FAIL. STATUS - '                        
                                            WS-K028-STATUS                      
                 MOVE  WS-K028-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING  WS-SYSTEM-DATE(7:2) '/'                                      
                   WS-SYSTEM-DATE(5:2) '/'                                      
                   WS-SYSTEM-DATE(1:4)                                          
                   DELIMITED BY SIZE                                            
                   INTO WS-EDITED-DATE                                          
           END-STRING.                                                          
                                                                                
           MOVE WS-EDITED-DATE  TO L052-PRINT-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1100-READ-F026.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13F026                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F026-EOF                                      
                NOT AT END                                                      
                   ADD 1    TO WS-F026-READ                                     
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1200-READ-F028.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13F028                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F028-EOF                                      
                NOT AT END                                                      
                   ADD 1    TO WS-F028-READ                                     
           END-READ.                                                            
                                                                                
           IF WS-LAST-OIC = SPACES OR LOW-VALUES                                
              MOVE F028-NUM-OIC   TO WS-LAST-OIC                                
           END-IF.                                                              
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2000-PROCESS.                                                            
      *------------------------------------------------------------*            
                                                                                
           IF (F026-NUM-TO-OIC = F028-NUM-OIC)                                  
              IF (F026-DTE-UPDATE = F028-DTE-HISTORY)                           
                 PERFORM 2500-PROCESS  THRU 2500-EXIT                           
                         UNTIL (WS-F028-EOF = 'Y' OR                            
                                F028-NUM-OIC NOT = WS-LAST-OIC)                 
              END-IF                                                            
              PERFORM 3500-DELETE-K026  THRU 3500-EXIT                          
              PERFORM 1100-READ-F026    THRU 1100-EXIT                          
           ELSE                                                                 
              IF F026-NUM-TO-OIC <  F028-NUM-OIC                                
                 PERFORM 1100-READ-F026  THRU 1100-EXIT                         
              ELSE                                                              
                 PERFORM 1200-READ-F028  THRU 1200-EXIT                         
                 MOVE F028-NUM-OIC       TO WS-LAST-OIC                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2500-PROCESS.                                                            
      *------------------------------------------------------------*            
                                                                                
           MOVE 'N'                  TO WS-K022-FOUND.                          
           MOVE F028-NUM-REGN        TO K022-NUM-REGN.                          
                                                                                
           PERFORM 3000-READ-K022    THRU 3000-EXIT.                            
                                                                                
           IF WS-K022-FOUND = 'Y'                                               
              PERFORM 3100-READ-K028 THRU 3100-EXIT                             
           END-IF                                                               
                                                                                
           PERFORM 1200-READ-F028    THRU 1200-EXIT.                            
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-READ-K022.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 MOVE K022-NUM-OIC    TO WS-OIC                                 
                 MOVE 'Y'             TO WS-K022-FOUND                          
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'REGN NO NOT FOUND IN BP13K022 : '                     
                                            K022-NUM-REGN                       
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K022. STATUS : '                   
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3100-READ-K028.                                                          
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES             TO K028-KEY-FLD.                             
           MOVE F028-KEY-FLD       TO K028-KEY-FLD.                             
                                                                                
           READ BP13K028.                                                       
                                                                                
           EVALUATE WS-K028-STATUS                                              
              WHEN 00                                                           
                 PERFORM 3150-PROCESS        THRU 3150-EXIT                     
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'NOT FOUND IN  BP13K028. KEY : '                       
                                            F028-KEY-FLD                        
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K028. STATUS : '                   
                                            WS-K028-STATUS                      
                 MOVE WS-K028-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3150-PROCESS.                                                            
      *------------------------------------------------------------*            
                                                                                
           PERFORM 3200-DELETE-K022    THRU 3200-EXIT.                          
           PERFORM 3300-WRITE-K028     THRU 3300-EXIT.                          
           PERFORM 3400-WRITE-K022     THRU 3400-EXIT.                          
           PERFORM 4000-PRINT-REPORT   THRU 4000-EXIT.                          
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3200-DELETE-K022.                                                        
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES            TO K022-KEY-FLD.                              
           MOVE WS-OIC            TO K022-NUM-OIC.                              
           MOVE F028-NUM-REGN     TO K022-NUM-REGN.                             
                                                                                
           DELETE BP13K022.                                                     
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
                 ADD 1  TO WS-K022-DELETE                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13K022. STATUS : '                  
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3300-WRITE-K028.                                                         
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES               TO BP13K028-REC.                           
           INITIALIZE                   BP13K028-REC.                           
                                                                                
           MOVE K022-NUM-OIC         TO K028-NUM-OIC.                           
           MOVE K022-NUM-REGN        TO K028-NUM-REGN.                          
           MOVE WS-SYSTEM-DATE       TO K028-DTE-HISTORY.                       
           MOVE K022-CDE-ALLO-CAT    TO K028-CDE-ALLO-CAT.                      
           MOVE K022-CDE-BANKLOAN    TO K028-CDE-BANKLOAN.                      
           MOVE K022-CDE-ALERT       TO K028-CDE-ALERT.                         
           MOVE K022-NUM-USERID      TO K028-NUM-USERID.                        
           MOVE K022-DTE-UPDATE      TO K028-DTE-UPDATE.                        
                                                                                
           WRITE  BP13K028-REC.                                                 
                                                                                
           EVALUATE WS-K028-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K028-WRITE                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR WRITING  BP13K028, STATUS = '                  
                         WS-K028-STATUS                                         
                 MOVE WS-K028-STATUS  TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3400-WRITE-K022.                                                         
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES               TO BP13K022-REC.                           
           INITIALIZE                   BP13K022-REC.                           
                                                                                
           MOVE F026-NUM-TO-OIC      TO K022-NUM-OIC.                           
           MOVE F028-NUM-REGN        TO K022-NUM-REGN.                          
           MOVE F028-CDE-ALLO-CAT    TO K022-CDE-ALLO-CAT.                      
           MOVE F028-CDE-BANKLOAN    TO K022-CDE-BANKLOAN.                      
           MOVE F028-CDE-ALERT       TO K022-CDE-ALERT.                         
           MOVE F028-NUM-USERID      TO K022-NUM-USERID.                        
           MOVE F028-DTE-UPDATE      TO K022-DTE-UPDATE.                        
                                                                                
           WRITE  BP13K022-REC.                                                 
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K022-WRITE                                         
              WHEN 23                                                           
                 DISPLAY 'DUPLICATE RECORD IN BP13K022. KEY : '                 
                          K022-KEY-FLD                                          
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR WRITING  BP13K022. STATUS : '                  
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3500-DELETE-K026.                                                        
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES               TO K026-KEY-FLD.                           
           MOVE 'HIST '              TO K026-NUM-FROM-OIC.                      
           MOVE SPACES               TO K026-CDE-BANKLOAN.                      
           MOVE F026-NUM-TO-OIC      TO K026-NUM-TO-OIC.                        
                                                                                
           DELETE BP13K026.                                                     
                                                                                
           EVALUATE WS-K026-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K026-DELETE                                        
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13K026. STATUS : '                  
                                            WS-K026-STATUS                      
                 MOVE WS-K026-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4000-PRINT-REPORT.                                                       
      *------------------------------------------------------------*            
                                                                                
           MOVE F028-NUM-REGN                TO L052-REGN-NO.                   
           MOVE WS-OIC                       TO L052-FROM-OIC.                  
           MOVE F028-NUM-OIC                 TO L052-TO-OIC.                    
                                                                                
           PERFORM 4100-WRITE-BP13L052       THRU 4100-EXIT.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4100-WRITE-BP13L052.                                                     
      *------------------------------------------------------------*            
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 53                                          
              PERFORM 4200-PRINT-HEADING THRU 4200-EXIT                         
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           MOVE WS-CNT-PRINT-L052  TO    L052-NUMBER.                           
                                                                                
           WRITE BP13L052-REC      FROM  L052-PR-DETAILS   AFTER 1.             
                                                                                
           ADD   1                 TO    WS-CNT-LINE-ON-PAGE                    
                                         WS-CNT-PRINT-L052                      
                                         WS-CNT-WRITE-L052.                     
                                                                                
           MOVE  SPACES            TO    L052-PR-DETAILS.                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4200-PRINT-HEADING.                                                      
      *------------------------------------------------------------*            
                                                                                
           ADD  1                TO     WS-NO-OF-PAGE.                          
           MOVE WS-NO-OF-PAGE    TO     L052-PRINT-PAGE.                        
                                                                                
           MOVE SPACES           TO     BP13L052-REC.                           
                                                                                
           WRITE BP13L052-REC    AFTER  PAGE.                                   
           WRITE BP13L052-REC    FROM   L052-PR-HEAD-01 AFTER 1.                
           WRITE BP13L052-REC    FROM   L052-PR-HEAD-02 AFTER 1.                
           WRITE BP13L052-REC    FROM   L052-PR-HEAD-03 AFTER 2.                
           WRITE BP13L052-REC    FROM   L052-PR-HEAD-04 AFTER 1.                
                                                                                
           MOVE 5  TO WS-CNT-LINE-ON-PAGE.                                      
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------*            
                                                                                
           CLOSE BP13K022                                                       
                 BP13K026                                                       
                 BP13K028                                                       
                 BP13F026                                                       
                 BP13F028                                                       
                 BP13L052.                                                      
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BP13K022 FAIL. STATUS-'                         
                                         WS-K022-STATUS                         
                 MOVE WS-K022-STATUS  TO RETURN-CODE                            
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K026-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BP13K026 FAIL. STATUS-'                         
                                         WS-K026-STATUS                         
                 MOVE WS-K026-STATUS  TO RETURN-CODE                            
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K028-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BP13K028 FAIL. STATUS-'                         
                                         WS-K028-STATUS                         
                 MOVE WS-K028-STATUS  TO RETURN-CODE                            
           END-EVALUATE.                                                        
                                                                                
                                                                                
           DISPLAY '*----------- BP13C052 -------------*'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY ' 1. BP13F026 READ      : '  WS-F026-READ.                   
           DISPLAY ' 2. BP13F028 READ      : '  WS-F028-READ.                   
           DISPLAY ' 3. BP13K028 WRITE     : '  WS-K028-WRITE.                  
           DISPLAY ' 4. BP13K022 DELETE    : '  WS-K022-DELETE.                 
           DISPLAY ' 5. BP13K022 WRITE     : '  WS-K022-WRITE.                  
           DISPLAY ' 6. BP13K026 DELETE    : '  WS-K026-DELETE.                 
           DISPLAY '*----------------------------------*'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
