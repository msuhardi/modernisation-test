       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C008.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  14/02/02.                                                 
      *===============================================================*         
      *               SYSTEM OF COMMITMENT (BP13) *                   *         
      *===============================================================*         
      *                                                               *         
      * OBJECTIVE :  THIS PROGRAM IS USED TO WRITE NRIC AND NAME OF   *         
      *              SPOUSE IN THE OUTFILE P13F853A                   *         
      *           :  NRIC NOT FOUND IN MARRIAGE TABLE WILL BE OUTPUT  *         
      *              TO BP13K517                                      *         
      *1. FILE DETAILS :                                              *         
      *    INPUT   :  BP13F853  ( MCPS OCCUPIER FILE )                *         
      *    OUTPUT  :  P13F853A  ( OCCUPIER FILE )                     *         
      *               BP13K517  ( ROM FILE )                          *         
      *               BP13L008  ( REPORT )                            *         
      *                                                               *         
      *                                                               *         
      * --------------------------------------------------------------*         
      * CHG-NO    CHG-DATE     BY   DESCRIPTION                       *         
      * ------    --------     --   -----------                       *         
      * BP130850  14/02/2002   AS1  NEW PROGRAM                       *         
      * BP132313  28/04/2003   MJ16 INCLUDED SUBROUTINE AV02C013      *         
      * BP132438  10/11/2003   SK   TO FETCH RECORD IN THE ORDER BY   *         
      *                             MARRIAGE DATE                     *         
      *****************************************************************         
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
      *----------------------*                                                  
       CONFIGURATION SECTION.                                                   
      *----------------------*                                                  
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *----------------------*                                                  
       INPUT-OUTPUT SECTION.                                                    
      *----------------------*                                                  
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT    BP13F853   ASSIGN       TO   BP13F853.                     
                                                                                
           SELECT    BP13K517   ASSIGN       TO   BP13K517                      
                                ACCESS MODE  IS   DYNAMIC                       
                                ORGANIZATION IS   INDEXED                       
                                RECORD KEY   IS   K517-KEY-FLD                  
                                FILE STATUS  IS   WS-K517-STATUS.               
                                                                                
           SELECT    P13F853A   ASSIGN       TO   P13F853A.                     
                                                                                
           SELECT    BP13L008   ASSIGN       TO   BP13L008.                     
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
      *-------------*                                                           
                                                                                
       FD  BP13F853                                                             
           RECORD    CONTAINS 250 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F853.                                                           
                                                                                
       FD  BP13K517                                                             
           RECORD    CONTAINS 250 CHARACTERS.                                   
       COPY BP13K517.                                                           
                                                                                
       FD  P13F853A                                                             
           RECORD    CONTAINS 250 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       01  WS-P13F853A-REC.                                                     
           05 WS-F853A-NUM-REGN        PIC X(08).                               
           05 WS-F853A-NUM-NRIC        PIC X(09).                               
           05 FILLER                   PIC X(01).                               
           05 WS-F853A-NME-OCCP        PIC X(66).                               
           05 WS-F853A-NUM-NRIC-SPOUSE PIC X(09).                               
           05 WS-F853A-NME-SPOUSE      PIC X(66).                               
           05 WS-F853A-NUM-NEW-TOWN    PIC X(03).                               
           05 WS-F853A-NUM-FLAT-TYPE   PIC X(02).                               
           05 WS-F853A-DTE-BALLOT      PIC X(06).                               
           05 WS-F853A-NUM-OCCP-MT     PIC X(01).                               
           05 WS-F853A-NUM-SPOUSE-MT   PIC X(01).                               
           05 FILLER                   PIC X(62).                               
           05 WS-F853A-DTE-UPDATE      PIC X(08).                               
           05 WS-F853A-NUM-USERID      PIC X(08).                               
                                                                                
       FD  BP13L008                                                             
           RECORD    CONTAINS 133 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       01  WS-P13L008-REC              PIC X(133).                              
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-SWITCHES.                                                         
           03  F853-EOF-IND            PIC X        VALUE SPACES.               
               88 F853-EOF             VALUE 'Y'.                               
                                                                                
       01  WS-REMARKS                  PIC X(18)   VALUE SPACES.                
       01  WS-WRITE-FLAG               PIC X(1)    VALUE 'N'.                   
       01  WS-CTR                      PIC 9(1)    VALUE 0.                     
       01  WS-COUNTER                  PIC 9(1)    VALUE 0.                     
       01  WS-LAST-REGN                PIC X(8)    VALUE SPACES.                
       01  WS-LAST-PRINT-REGN          PIC X(8)    VALUE SPACES.                
       01  WS-LAST-DTE-MRG1            PIC X(10)   VALUE SPACES.                
       01  WS-LAST-DTE-MRG2            PIC X(10)   VALUE SPACES.                
       01  BP13F853-TREC1              PIC X(250)  VALUE SPACES.                
       01  BP13F853-TREC2              PIC X(250)  VALUE SPACES.                
       01  P13F853A-TREC1              PIC X(132)  VALUE SPACES.                
       01  P13F853A-TREC2              PIC X(132)  VALUE SPACES.                
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K517-STATUS          PIC 9(2)     VALUE ZEROES.               
                                                                                
       01  WS-ACCUMULATORS.                                                     
           03  WS-NO-WRT-F853A         PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-READ-F853         PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-READ-PSN          PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-NOTFND-PSN        PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-READ-MRG          PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-NOTFND-MRG        PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-READ-IDNTY        PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-NOTFND-IDNTY      PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-READ-PSN-SP       PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-NOTFND-PSN-SP     PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-READ-IDNTY-SP     PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-NOTFND-IDNTY-SP   PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-WRT-L008          PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-WRT-K517          PIC 9(6)     VALUE ZEROES.               
                                                                                
           03  WS-ED-WRT-F853A         PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-READ-F853         PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-READ-PSN          PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-NOTFND-PSN        PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-READ-MRG          PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-NOTFND-MRG        PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-READ-IDNTY        PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-NOTFND-IDNTY      PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-READ-PSN-SP       PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-NOTFND-PSN-SP     PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-READ-IDNTY-SP     PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-NOTFND-IDNTY-SP   PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-WRT-L008          PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-WRT-K517          PIC Z,ZZ,ZZ9 VALUE ZEROES.               
       01  WS-TEMP.                                                             
           05  WS-SYS-DATE             PIC X(08)    VALUE SPACES.               
           05  WS-DATE-RUN             PIC X(10)    VALUE SPACES.               
           05  WS-NUM-NRIC             PIC X(09)    VALUE SPACES.               
           05  WS-NUM-CSTMR            PIC X(09)    VALUE SPACES.               
           05  WS-NUM-CHECK-CSTMR      PIC X(09)    VALUE SPACES.               
           05  WS-NUM-APPL-NRIC        PIC X(09)    VALUE SPACES.               
           05  WS-NUM-APPL-NAME        PIC X(66)    VALUE SPACES.               
           05  WS-NUM-SPOUSE-NRIC      PIC X(09)    VALUE SPACES.               
           05  WS-NUM-SPOUSE-NAME      PIC X(66)    VALUE SPACES.               
           05  WS-NUM-CSTMR-HSBND      PIC X(09)    VALUE SPACES.               
           05  WS-NUM-CSTMR-WIFE       PIC X(09)    VALUE SPACES.               
           05  WS-NUM-CSTMR-SPOUSE     PIC X(09)    VALUE SPACES.               
           05  WS-NUM-MRTL-OCCP        PIC X(01)    VALUE SPACES.               
           05  WS-NUM-MRTL-SPOUSE      PIC X(01)    VALUE SPACES.               
           05  WS-DTE-MRG              PIC X(10)    VALUE SPACES.               
           05  WS-PREV-REGN            PIC X(08)    VALUE SPACES.               
           05  WS-NUM-NRIC-TYPE        PIC X(03)    VALUE SPACES.               
                                                                                
      ***************************************************************           
      *   AV02C013 SUBROUTINE VARIABLES                             *           
      ***************************************************************           
                                                                                
       01  WS-NUM-UINFIN            PIC  X(09).                                 
       01  WS-VALIDATION-FLAG       PIC  X(01).                                 
       01  WS-NUM-UFIN-TYPE         PIC  X(03).                                 
                                                                                
       01  WS-ERROR-REPORT.                                                     
           05 WS-LINE-CTR              PIC 9(6)     VALUE 65.                   
           05 WS-PAGE-CTR              PIC 9(5)     VALUE ZEROES.               
           05 WS-SEQN-CTR              PIC 9(6)     VALUE ZEROES.               
                                                                                
      *----------------------*                                                  
      *   ERROR REPORT LAYOUT                                                   
      *----------------------*                                                  
                                                                                
       01  L008-PR-HEAD-01.                                                     
           05  FILLER                  PIC X(8)  VALUE 'P13L008A'.              
           05  FILLER                  PIC X(5)  VALUE SPACES .                 
           05  FILLER                  PIC X(8)  VALUE 'HDBCAT 3'.              
           05  FILLER                  PIC X(24) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  L008-DATE-RUN           PIC X(10).                               
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  L008-PAGENO             PIC ZZZZ9 .                              
                                                                                
       01  L008-PR-HEAD-02.                                                     
           05  FILLER                  PIC X(30)  VALUE SPACES .                
           05  FILLER                  PIC X(100) VALUE                         
               'PRINT REPORT OF NRIC , NAME , SPOUSE NRIC AND SPOUSE NAM        
      -        'E'.                                                             
       01  L008-PR-HEAD-03.                                                     
           05  FILLER                  PIC X(06) VALUE 'S/NO'.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'REGN NO'.               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'NRIC'.                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'NAME'.                  
           05  FILLER                  PIC X(33) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'SPNRIC'.                
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'SPNAME'.                
           05  FILLER                  PIC X(31) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'REMARKS'.               
           05  FILLER                  PIC X(13) VALUE SPACES.                  
                                                                                
       01  L008-PR-DTL-01.                                                      
           05  L008-SNO                PIC Z(6)  VALUE ZEROES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L008-REGN-NO            PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L008-NRIC               PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L008-NAME               PIC X(35) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L008-SP-NRIC            PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L008-SP-NAME            PIC X(35) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L008-REMARKS            PIC X(18) VALUE SPACES.                  
                                                                                
       01  L008-PR-LINE                PIC X(132) VALUE ALL '-'.                
                                                                                
      *COPY BE01W902.                                                           
                                                                                
            EXEC SQL INCLUDE SQLCA     END-EXEC.                                
            EXEC SQL INCLUDE P13PERSN  END-EXEC.                                
            EXEC SQL INCLUDE P13IDNO   END-EXEC.                                
            EXEC SQL INCLUDE P13VMRG   END-EXEC.                                
                                                                                
      ******************************************************************        
      *            DEFINE THE CURSOR SNMEMRG                           *        
      ******************************************************************        
                                                                                
            EXEC SQL                                                            
                 DECLARE SNMEMRG  CURSOR FOR                                    
                 SELECT  NUM_CSTMR_HSBND ,                                      
                         NUM_CSTMR_WIFE ,                                       
                         DTE_MRG                                                
                   FROM  MRG                                                    
                   WHERE NUM_CSTMR_HSBND = :WS-NUM-CSTMR OR                     
                         NUM_CSTMR_WIFE  = :WS-NUM-CSTMR                        
                         ORDER BY DTE_MRG DESC                                  
            END-EXEC.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *----------------*                                                        
       0000-MAIN-PROCESS.                                                       
      *----------------*                                                        
                                                                                
             PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                   
             PERFORM 2000-READ-BP13F853       THRU 2000-EXIT.                   
                                                                                
             PERFORM 3000-MAIN-PROCESS        THRU 3000-EXIT                    
               UNTIL F853-EOF.                                                  
                                                                                
             PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-ROUTINE.                                                       
      *----------------*                                                        
                                                                                
           OPEN       INPUT   BP13F853                                          
                      I-O     BP13K517                                          
                      OUTPUT  P13F853A                                          
                              BP13L008.                                         
                                                                                
           IF WS-K517-STATUS NOT = 00 AND 97                                    
              DISPLAY "OPENING ERROR , BP13K517 "  "   "                        
                      "WS-K517-STATUS : " WS-K517-STATUS                        
              PERFORM 9999-CLOSE-ROUTINE  THRU  9999-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE   FUNCTION CURRENT-DATE(1:8) TO   WS-SYS-DATE.                  
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
                  DELIMITED BY SIZE          INTO WS-DATE-RUN.                  
           MOVE   WS-DATE-RUN                TO   L008-DATE-RUN.                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2000-READ-BP13F853.                                                      
      *-----------------*                                                       
                                                                                
           READ BP13F853 AT END  MOVE 'Y' TO F853-EOF-IND                       
                PERFORM 4400-WRITE-OUTFILE   THRU 4400-EXIT                     
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-NO-READ-F853.                                            
                                                                                
           IF WS-NO-READ-F853 = 1                                               
              MOVE F853-NUM-REGN TO WS-LAST-REGN                                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3000-MAIN-PROCESS.                                                       
      *---------------------*                                                   
                                                                                
           MOVE   SPACES              TO    WS-NUM-NRIC                         
                                            WS-NUM-NRIC-TYPE                    
                                            WS-NUM-APPL-NRIC                    
                                            WS-NUM-APPL-NAME                    
                                            WS-NUM-SPOUSE-NRIC                  
                                            WS-NUM-SPOUSE-NAME                  
                                            WS-NUM-CSTMR                        
                                            WS-NUM-CHECK-CSTMR                  
                                            WS-NUM-CSTMR-HSBND                  
                                            WS-NUM-CSTMR-WIFE                   
                                            WS-NUM-CSTMR-SPOUSE                 
                                            WS-NUM-MRTL-OCCP                    
                                            WS-NUM-MRTL-SPOUSE.                 
                                                                                
           MOVE   SPACES              TO    L008-PR-DTL-01.                     
           INITIALIZE L008-PR-DTL-01.                                           
                                                                                
      *    MOVE   SPACES              TO    BE01W902-COMMAREA.                  
      *    INITIALIZE BE01W902-COMMAREA.                                        
                                                                                
      *    MOVE F853-NUM-NRIC         TO   BE01W902-IN-NUM-IDNTY-NO.            
                                                                                
           MOVE SPACES TO WS-NUM-UINFIN.                                        
           MOVE SPACES TO WS-VALIDATION-FLAG.                                   
           MOVE SPACES TO WS-NUM-UFIN-TYPE.                                     
                                                                                
           MOVE F853-NUM-NRIC         TO   WS-NUM-UINFIN.                       
           PERFORM 3050-CONVERT-IDNTY-TYPE THRU 3050-EXIT.                      
                                                                                
           MOVE   F853-NUM-NRIC       TO    WS-NUM-NRIC                         
                                            WS-NUM-APPL-NRIC.                   
                                                                                
            EXEC SQL                                                            
                 SELECT NUM_CSTMR                                               
                  INTO :DCLIDNTY-NO.NUM-CSTMR                                   
                  FROM  IDNTY_NO                                                
                  WHERE NUM_IDNTY_NO = :WS-NUM-NRIC                             
                    AND NUM_IDNTY_NO_TYPE = :WS-NUM-NRIC-TYPE                   
                                                                                
            END-EXEC.                                                           
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 000                                                         
                    ADD        1                 TO  WS-NO-READ-IDNTY           
                    PERFORM  3100-RETRIEVE-PERSON-TBL THRU 3100-EXIT            
               WHEN 100                                                         
                    MOVE     F853-NUM-NRIC       TO  L008-NRIC                  
                    MOVE    'IC NOTFND IN IDNTY' TO  L008-REMARKS               
                    PERFORM  6000-PRINT-REPORT   THRU 6000-EXIT                 
                                                                                
                    ADD        1                 TO  WS-NO-NOTFND-IDNTY         
                    DISPLAY  "REC. NOT FOUND IN IDNTY_NO TABLE  : "             
                              WS-NUM-NRIC                                       
               WHEN OTHER                                                       
                    DISPLAY 'SQL ERROR IN IDNTY_NO TABLE (APPL)'                
                    DISPLAY 'F853-NUM-NRIC   :  '  F853-NUM-NRIC                
                    DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                      
                    DISPLAY SPACES                                              
                                                                                
                    PERFORM 9000-SQL-ERROR  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE F853-NUM-REGN   TO WS-LAST-REGN.                                
           PERFORM   2000-READ-BP13F853     THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       3050-CONVERT-IDNTY-TYPE.                                                 
      *-----------------------*                                                 
                                                                                
      *     CALL 'BE01C902' USING BE01W902-COMMAREA                             
                                                                                
                                                                                
            IF WS-NUM-UINFIN NOT = SPACES AND LOW-VALUES                        
               CALL 'AV02C013' USING WS-NUM-UINFIN  WS-VALIDATION-FLAG          
                                 WS-NUM-UFIN-TYPE                               
                                                                                
               PERFORM  3075-EVALUATE-UFIN-TYPE  THRU 3075-EXIT                 
            ELSE                                                                
               DISPLAY 'INVALID NUM-NRIC'                                       
            END-IF.                                                             
                                                                                
      *     IF BE01W902-OUT-RTN-MSGNO = SPACES                                  
      *        MOVE BE01W902-OUT-NUM-IDNTY-NO-TYPE  TO WS-NUM-NRIC-TYPE         
      *     ELSE                                                                
      *      IF BE01W902-OUT-RTN-MSGNO  = '0127'                                
      *        MOVE        'XIN'                    TO WS-NUM-NRIC-TYPE         
      *      ELSE                                                               
      *        DISPLAY ' ERROR IN BE01C902' ' ' 'NRIC : ' F853-NUM-NRIC         
      *        DISPLAY 'BE01W902-OUT-RTN-MSGNO'                                 
      *      END-IF                                                             
      *     END-IF.                                                             
                                                                                
                                                                                
       3050-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------*                                                 
       3075-EVALUATE-UFIN-TYPE.                                                 
      *-----------------------*                                                 
                                                                                
           EVALUATE WS-NUM-UFIN-TYPE                                            
            WHEN 'UIN'                                                          
      *        MOVE WS-NUM-UFIN-TYPE TO BE01W902-OUT-NUM-IDNTY-NO-TYPE          
               MOVE WS-NUM-UFIN-TYPE TO WS-NUM-NRIC-TYPE                        
            WHEN 'FIN'                                                          
      *        MOVE WS-NUM-UFIN-TYPE TO BE01W902-OUT-NUM-IDNTY-NO-TYPE          
               MOVE WS-NUM-UFIN-TYPE TO WS-NUM-NRIC-TYPE                        
            WHEN OTHER                                                          
      *        MOVE WS-NUM-UFIN-TYPE TO BE01W902-OUT-NUM-IDNTY-NO-TYPE          
               MOVE 'XIN'            TO WS-NUM-NRIC-TYPE                        
      *        MOVE '0127'           TO BE01W902-OUT-RTN-MSGNO                  
      *        MOVE 'AV02C013'       TO BE01W902-OUT-RTN-PGMID                  
           END-EVALUATE.                                                        
                                                                                
       3075-EXIT.                                                               
            EXIT.                                                               
      *------------------------*                                                
       3100-RETRIEVE-PERSON-TBL.                                                
      *------------------------*                                                
                                                                                
            MOVE    SPACES                   TO   WS-NUM-CSTMR                  
                                                  WS-NUM-CHECK-CSTMR            
            MOVE    NUM-CSTMR OF DCLIDNTY-NO TO   WS-NUM-CSTMR                  
                                                  WS-NUM-CHECK-CSTMR            
                                                                                
            PERFORM 3110-READ-PERSON-TABLE   THRU 3110-EXIT                     
                                                                                
            EVALUATE SQLCODE                                                    
                WHEN 000                                                        
                     MOVE NME-PERSON OF DCLPERSONS  TO WS-NUM-APPL-NAME         
                     MOVE NUM-MRTL-STATUS OF DCLPERSONS                         
                       TO WS-NUM-MRTL-OCCP                                      
                     ADD      1     TO  WS-NO-READ-PSN                          
                     PERFORM 4000-READ-MRG-TABLE THRU 4000-EXIT                 
                WHEN 100                                                        
                     MOVE   F853-NUM-NRIC           TO L008-NRIC                
                     MOVE   'NAME NOTFND IN PSN'    TO L008-REMARKS             
                     PERFORM 6000-PRINT-REPORT      THRU 6000-EXIT              
                                                                                
                     ADD       1                    TO WS-NO-NOTFND-PSN         
                     DISPLAY 'REC NOT IN PERSONS TABLE FOR '                    
                            ' ' F853-NUM-NRIC                                   
                WHEN OTHER                                                      
                     DISPLAY 'SQL ERROR IN PERSON   TABLE (APPL)'               
                     DISPLAY 'F853-NUM-NRIC   :  '  F853-NUM-NRIC               
                     DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                     
                     DISPLAY SPACES                                             
                                                                                
                     PERFORM  9000-SQL-ERROR  THRU  9000-EXIT                   
            END-EVALUATE.                                                       
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------*                                                   
       3110-READ-PERSON-TABLE.                                                  
      *---------------------*                                                   
                                                                                
            EXEC SQL                                                            
                 SELECT   NME_PERSON ,                                          
                          NUM_CSTMR  ,                                          
                          NUM_MRTL_STATUS                                       
                   INTO  :DCLPERSONS.NME-PERSON ,                               
                         :DCLPERSONS.NUM-CSTMR ,                                
                         :DCLPERSONS.NUM-MRTL-STATUS                            
                   FROM   PERSONS                                               
                   WHERE  NUM_CSTMR = :WS-NUM-CSTMR                             
                                                                                
            END-EXEC.                                                           
                                                                                
       3110-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       4000-READ-MRG-TABLE.                                                     
      *-------------------*                                                     
                                                                                
            EXEC SQL                                                            
                 SELECT  NUM_CSTMR_HSBND ,                                      
                         NUM_CSTMR_WIFE ,                                       
                         DTE_MRG                                                
                 INTO   :DCLMRG.NUM-CSTMR-HSBND ,                               
                        :DCLMRG.NUM-CSTMR-WIFE  ,                               
                        :DCLMRG.DTE-MRG                                         
                                                                                
                 FROM    MRG                                                    
                 WHERE   NUM_CSTMR_HSBND = :WS-NUM-CSTMR OR                     
                         NUM_CSTMR_WIFE  = :WS-NUM-CSTMR                        
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
                WHEN 000                                                        
                     PERFORM  4250-SPOUSE-INFO         THRU  4250-EXIT          
                WHEN 100                                                        
                     MOVE F853-NUM-NRIC        TO   L008-NRIC                   
                     MOVE WS-NUM-APPL-NAME     TO   L008-NAME                   
                     MOVE 'REC NOTFND  IN MRG' TO   L008-REMARKS                
                     PERFORM 6000-PRINT-REPORT THRU 6000-EXIT                   
                     PERFORM 7000-WRITE-BP13K517 THRU 7000-EXIT                 
                                                                                
                     ADD     1                 TO   WS-NO-NOTFND-MRG            
                     DISPLAY "REC. NOT FOUND IN MRG TABLE FOR : "               
                              F853-NUM-NRIC                                     
                WHEN - 811                                                      
                     PERFORM  4100-RETRIEVE-DUPLICATE  THRU  4100-EXIT          
                WHEN OTHER                                                      
                     DISPLAY 'SQL ERROR IN MRG      TABLE       '               
                     DISPLAY 'F853-NUM-NRIC   :  '  F853-NUM-NRIC               
                     DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                     
                     DISPLAY SPACES                                             
                                                                                
                     PERFORM  9000-SQL-ERROR  THRU  9000-EXIT                   
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------*                                                 
       4100-RETRIEVE-DUPLICATE.                                                 
      *-----------------------*                                                 
                                                                                
            EXEC SQL OPEN SNMEMRG END-EXEC.                                     
                                                                                
            IF SQLCODE NOT = 000                                                
               DISPLAY 'SQL ERROR IN OPEN CURSOR MRG    TABLE       '           
               DISPLAY 'F853-NUM-NRIC   :  '  F853-NUM-NRIC                     
               DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                           
               DISPLAY SPACES                                                   
                                                                                
               PERFORM  9000-SQL-ERROR  THRU  9000-EXIT                         
            END-IF.                                                             
                                                                                
      *     PERFORM 4200-FETCH-SNMEMRG  THRU  4200-EXIT UNTIL                   
      *             SQLCODE NOT = 000                                           
                                                                                
            PERFORM 4200-FETCH-SNMEMRG  THRU  4200-EXIT                         
                                                                                
            EVALUATE SQLCODE                                                    
                WHEN 000                                                        
                WHEN 100                                                        
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'SQL ERROR IN FETCH CURSOR MRG    TABLE '          
                     DISPLAY 'F853-NUM-NRIC   :  '  F853-NUM-NRIC               
                     DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                     
                     DISPLAY SPACES                                             
                     PERFORM 9000-SQL-ERROR  THRU 9000-EXIT                     
            END-EVALUATE.                                                       
                                                                                
            EXEC SQL CLOSE SNMEMRG END-EXEC.                                    
                                                                                
            IF SQLCODE NOT = 000                                                
               DISPLAY 'SQL ERROR IN CLOSE CURSOR MRG    TABLE '                
               DISPLAY 'F853-NUM-NRIC   :  '  F853-NUM-NRIC                     
               DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                           
               DISPLAY SPACES                                                   
                                                                                
               PERFORM  9000-SQL-ERROR  THRU  9000-EXIT                         
            END-IF.                                                             
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       4200-FETCH-SNMEMRG.                                                      
      *------------------*                                                      
                                                                                
            MOVE   SPACES    TO   DCLMRG                                        
                                  WS-NUM-CSTMR-SPOUSE                           
                                  WS-NUM-CSTMR-HSBND                            
                                  WS-NUM-CSTMR-WIFE                             
                                  WS-DTE-MRG.                                   
                                                                                
            EXEC   SQL  FETCH  SNMEMRG                                          
                        INTO   :DCLMRG.NUM-CSTMR-HSBND ,                        
                               :DCLMRG.NUM-CSTMR-WIFE  ,                        
                               :DCLMRG.DTE-MRG                                  
            END-EXEC.                                                           
                                                                                
            IF  SQLCODE = 000                                                   
                PERFORM   4250-SPOUSE-INFO     THRU 4250-EXIT                   
            END-IF.                                                             
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------*                                                        
       4250-SPOUSE-INFO.                                                        
      *----------------*                                                        
                                                                                
            ADD    1                       TO WS-NO-READ-MRG                    
            MOVE NUM-CSTMR-HSBND OF DCLMRG TO WS-NUM-CSTMR-HSBND                
            MOVE NUM-CSTMR-WIFE  OF DCLMRG TO WS-NUM-CSTMR-WIFE                 
            MOVE DTE-MRG OF DCLMRG         TO WS-DTE-MRG                        
                                                                                
            IF (F853-NUM-REGN = WS-LAST-REGN) AND                               
                WS-LAST-DTE-MRG1 = SPACES OR LOW-VALUES                         
                MOVE WS-DTE-MRG   TO WS-LAST-DTE-MRG1                           
            ELSE                                                                
                IF WS-LAST-DTE-MRG2 = SPACES OR LOW-VALUES                      
                   MOVE WS-DTE-MRG   TO WS-LAST-DTE-MRG2                        
                END-IF                                                          
            END-IF                                                              
                                                                                
            IF WS-NUM-CSTMR-HSBND = WS-NUM-CHECK-CSTMR                          
               MOVE   WS-NUM-CSTMR-WIFE    TO   WS-NUM-CSTMR-SPOUSE             
            ELSE                                                                
               MOVE   WS-NUM-CSTMR-HSBND   TO   WS-NUM-CSTMR-SPOUSE             
            END-IF                                                              
                                                                                
            EXEC SQL                                                            
                 SELECT  NUM_IDNTY_NO ,                                         
                         NUM_CSTMR                                              
                   INTO :DCLIDNTY-NO.NUM-IDNTY-NO ,                             
                        :DCLIDNTY-NO.NUM-CSTMR                                  
                   FROM  IDNTY_NO                                               
                  WHERE  NUM_CSTMR = :WS-NUM-CSTMR-SPOUSE                       
                    AND  NUM_DSPLY = 'D'                                        
            END-EXEC                                                            
                                                                                
            EVALUATE SQLCODE                                                    
                WHEN 000                                                        
                     ADD         1     TO    WS-NO-READ-IDNTY-SP                
                     MOVE    NUM-IDNTY-NO OF DCLIDNTY-NO  TO                    
                             WS-NUM-SPOUSE-NRIC                                 
                     PERFORM 4300-RETRIEVE-SPOUSE-NAME    THRU                  
                             4300-EXIT                                          
                WHEN 100                                                        
                     MOVE WS-NUM-NRIC          TO   L008-NRIC                   
                     MOVE WS-NUM-APPL-NAME     TO   L008-NAME                   
                     MOVE 'SP-IC N/F IN IDNTY' TO   L008-REMARKS                
                     PERFORM 6000-PRINT-REPORT THRU 6000-EXIT                   
                                                                                
                     ADD   1          TO WS-NO-NOTFND-IDNTY-SP                  
                     DISPLAY  "SPOUSE NRIC NOT FOUND IN IDNTY_NO                
      -                       "TBL FOR : "  WS-NUM-NRIC                         
                WHEN OTHER                                                      
                     DISPLAY 'SQL ERROR IN IDNTY_NO TABLE (SPOUSE)'             
                     DISPLAY 'CSTMR -ID       :  '  WS-NUM-CSTMR                
                     DISPLAY 'SPOUSE IC  FOR  :  '  F853-NUM-NRIC               
                     DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                     
                     DISPLAY SPACES                                             
                                                                                
                     PERFORM 9000-SQL-ERROR  THRU 9000-EXIT                     
            END-EVALUATE.                                                       
                                                                                
       4250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------*                                                
       4300-RETRIEVE-SPOUSE-NAME.                                               
      *------------------------*                                                
                                                                                
            MOVE     SPACES                   TO   WS-NUM-CSTMR.                
            MOVE     WS-NUM-CSTMR-SPOUSE      TO   WS-NUM-CSTMR.                
                                                                                
            PERFORM  3110-READ-PERSON-TABLE   THRU 3110-EXIT.                   
                                                                                
            EVALUATE SQLCODE                                                    
                WHEN 000                                                        
                     ADD   1                       TO WS-NO-READ-PSN-SP         
                     MOVE NME-PERSON OF DCLPERSONS TO WS-NUM-SPOUSE-NAME        
                     MOVE NUM-MRTL-STATUS OF DCLPERSONS                         
                       TO WS-NUM-MRTL-SPOUSE                                    
                     PERFORM 4400-WRITE-OUTFILE    THRU 4400-EXIT               
                WHEN 100                                                        
                     MOVE WS-NUM-NRIC              TO L008-NRIC                 
                     MOVE WS-NUM-APPL-NAME         TO L008-NAME                 
                     MOVE WS-NUM-SPOUSE-NRIC       TO L008-SP-NRIC              
                     MOVE 'SP-NAME N/F IN PSN'     TO L008-REMARKS              
                     PERFORM   6000-PRINT-REPORT   THRU 6000-EXIT               
                                                                                
                     ADD       1                  TO WS-NO-NOTFND-PSN-SP        
                     DISPLAY 'REC NOT IN PERSONS TABLE : '                      
                            ' ' WS-NUM-SPOUSE-NRIC                              
                WHEN OTHER                                                      
                     DISPLAY 'SQL ERROR IN PERSON(SPOUSE)  TABLE '              
                     DISPLAY 'SPOUSE IC       :  '  WS-NUM-SPOUSE-NRIC          
                     DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                     
                     DISPLAY SPACES                                             
                                                                                
                     PERFORM  9000-SQL-ERROR  THRU  9000-EXIT                   
            END-EVALUATE.                                                       
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       4400-WRITE-OUTFILE.                                                      
      *-------------------*                                                     
                                                                                
            IF F853-EOF-IND = 'Y'                                               
               PERFORM 5000-WRITE-P13F853A THRU 5000-EXIT                       
               GO TO 4400-EXIT                                                  
            END-IF                                                              
                                                                                
            IF (F853-NUM-REGN NOT = WS-LAST-REGN)                               
               IF WS-LAST-DTE-MRG1 NOT = WS-LAST-DTE-MRG2                       
                  AND WS-COUNTER = 1                                            
                  MOVE 'MRG DTE NOT EQUAL '                                     
                                              TO WS-REMARKS                     
               END-IF                                                           
                                                                                
               PERFORM 5100-WRITE-P13F853A    THRU 5100-EXIT                    
               PERFORM 4500-MOVE-DATA         THRU 4500-EXIT                    
               MOVE SPACES                    TO WS-LAST-DTE-MRG1               
                                                 WS-LAST-DTE-MRG2               
                                                 WS-REMARKS                     
                  MOVE WS-DTE-MRG   TO WS-LAST-DTE-MRG1                         
               MOVE 0 TO WS-COUNTER                                             
            ELSE                                                                
               PERFORM 4500-MOVE-DATA         THRU 4500-EXIT                    
               ADD 1 TO WS-COUNTER                                              
            END-IF.                                                             
                                                                                
       4400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       4500-MOVE-DATA.                                                          
      *-------------------*                                                     
                                                                                
            MOVE SPACES                 TO   WS-P13F853A-REC.                   
                                                                                
      *     MOVE F853-NUM-REGN         TO    WS-F853A-NUM-REGN.                 
            MOVE F853-NUM-REGN         TO    WS-F853A-NUM-REGN.                 
                                                                                
            MOVE WS-NUM-APPL-NRIC      TO    WS-F853A-NUM-NRIC                  
                                             L008-NRIC.                         
            MOVE WS-NUM-APPL-NAME      TO    WS-F853A-NME-OCCP                  
                                             L008-NAME.                         
            MOVE WS-NUM-SPOUSE-NAME    TO    WS-F853A-NME-SPOUSE                
                                             L008-SP-NAME.                      
            MOVE WS-NUM-SPOUSE-NRIC    TO    WS-F853A-NUM-NRIC-SPOUSE           
                                             L008-SP-NRIC.                      
            MOVE F853-NUM-NEW-TOWN     TO    WS-F853A-NUM-NEW-TOWN.             
            MOVE F853-NUM-FLAT-TYPE    TO    WS-F853A-NUM-FLAT-TYPE.            
            MOVE F853-DTE-BALLOT       TO    WS-F853A-DTE-BALLOT.               
            MOVE WS-NUM-MRTL-OCCP      TO    WS-F853A-NUM-OCCP-MT.              
            MOVE WS-NUM-MRTL-SPOUSE    TO    WS-F853A-NUM-SPOUSE-MT.            
            MOVE WS-SYS-DATE           TO    WS-F853A-DTE-UPDATE.               
            MOVE 'BP13C008'            TO    WS-F853A-NUM-USERID.               
                                                                                
            MOVE  WS-P13F853A-REC  TO BP13F853-TREC1.                           
            MOVE  L008-PR-DTL-01   TO P13F853A-TREC1.                           
                                                                                
                                                                                
            MOVE SPACES                TO    WS-P13F853A-REC.                   
                                                                                
            MOVE F853-NUM-REGN         TO    WS-F853A-NUM-REGN.                 
                                                                                
            MOVE WS-NUM-SPOUSE-NRIC    TO    WS-F853A-NUM-NRIC                  
                                             L008-NRIC.                         
            MOVE WS-NUM-SPOUSE-NAME    TO    WS-F853A-NME-OCCP                  
                                             L008-NAME.                         
            MOVE WS-NUM-APPL-NAME      TO    WS-F853A-NME-SPOUSE                
                                             L008-SP-NAME.                      
            MOVE WS-NUM-APPL-NRIC      TO    WS-F853A-NUM-NRIC-SPOUSE           
                                             L008-SP-NRIC.                      
            MOVE F853-NUM-NEW-TOWN     TO    WS-F853A-NUM-NEW-TOWN.             
            MOVE F853-NUM-FLAT-TYPE    TO    WS-F853A-NUM-FLAT-TYPE.            
            MOVE F853-DTE-BALLOT       TO    WS-F853A-DTE-BALLOT.               
            MOVE WS-NUM-MRTL-OCCP      TO    WS-F853A-NUM-OCCP-MT.              
            MOVE WS-NUM-MRTL-SPOUSE    TO    WS-F853A-NUM-SPOUSE-MT.            
            MOVE WS-SYS-DATE           TO    WS-F853A-DTE-UPDATE.               
            MOVE 'BP13C008'            TO    WS-F853A-NUM-USERID.               
                                                                                
            MOVE  WS-P13F853A-REC  TO BP13F853-TREC2.                           
            MOVE  L008-PR-DTL-01   TO P13F853A-TREC2.                           
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       5000-WRITE-P13F853A.                                                     
      *-------------------*                                                     
      *----------------------------------------------------------------*        
      *           WRITE THE OCCUPIER RECORD                            *        
      *----------------------------------------------------------------*        
                                                                                
            MOVE SPACES                 TO   WS-P13F853A-REC.                   
                                                                                
            MOVE F853-NUM-REGN         TO    WS-F853A-NUM-REGN.                 
                                                                                
            MOVE WS-NUM-APPL-NRIC      TO    WS-F853A-NUM-NRIC                  
                                             L008-NRIC.                         
            MOVE WS-NUM-APPL-NAME      TO    WS-F853A-NME-OCCP                  
                                             L008-NAME.                         
            MOVE WS-NUM-SPOUSE-NAME    TO    WS-F853A-NME-SPOUSE                
                                             L008-SP-NAME.                      
            MOVE WS-NUM-SPOUSE-NRIC    TO    WS-F853A-NUM-NRIC-SPOUSE           
                                             L008-SP-NRIC.                      
            MOVE F853-NUM-NEW-TOWN     TO    WS-F853A-NUM-NEW-TOWN.             
            MOVE F853-NUM-FLAT-TYPE    TO    WS-F853A-NUM-FLAT-TYPE.            
            MOVE F853-DTE-BALLOT       TO    WS-F853A-DTE-BALLOT.               
            MOVE WS-NUM-MRTL-OCCP      TO    WS-F853A-NUM-OCCP-MT.              
            MOVE WS-NUM-MRTL-SPOUSE    TO    WS-F853A-NUM-SPOUSE-MT.            
            MOVE WS-SYS-DATE           TO    WS-F853A-DTE-UPDATE.               
            MOVE 'BP13C008'            TO    WS-F853A-NUM-USERID.               
                                                                                
            PERFORM  6000-PRINT-REPORT THRU  6000-EXIT.                         
            WRITE WS-P13F853A-REC.                                              
                                                                                
      *----------------------------------------------------------------*        
      *           WRITE THE SPOUSE  RECORD                             *        
      *----------------------------------------------------------------*        
                                                                                
            MOVE SPACES                TO    WS-P13F853A-REC.                   
                                                                                
            MOVE F853-NUM-REGN         TO    WS-F853A-NUM-REGN.                 
                                                                                
            MOVE WS-NUM-SPOUSE-NRIC    TO    WS-F853A-NUM-NRIC                  
                                             L008-NRIC.                         
            MOVE WS-NUM-SPOUSE-NAME    TO    WS-F853A-NME-OCCP                  
                                             L008-NAME.                         
            MOVE WS-NUM-APPL-NAME      TO    WS-F853A-NME-SPOUSE                
                                             L008-SP-NAME.                      
            MOVE WS-NUM-APPL-NRIC      TO    WS-F853A-NUM-NRIC-SPOUSE           
                                             L008-SP-NRIC.                      
            MOVE F853-NUM-NEW-TOWN     TO    WS-F853A-NUM-NEW-TOWN.             
            MOVE F853-NUM-FLAT-TYPE    TO    WS-F853A-NUM-FLAT-TYPE.            
            MOVE F853-DTE-BALLOT       TO    WS-F853A-DTE-BALLOT.               
            MOVE WS-NUM-MRTL-OCCP      TO    WS-F853A-NUM-OCCP-MT.              
            MOVE WS-NUM-MRTL-SPOUSE    TO    WS-F853A-NUM-SPOUSE-MT.            
            MOVE WS-SYS-DATE           TO    WS-F853A-DTE-UPDATE.               
            MOVE 'BP13C008'            TO    WS-F853A-NUM-USERID.               
                                                                                
            PERFORM  6000-PRINT-REPORT THRU  6000-EXIT.                         
            WRITE WS-P13F853A-REC.                                              
                                                                                
            ADD   2                    TO    WS-NO-WRT-F853A.                   
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       5100-WRITE-P13F853A.                                                     
      *-------------------*                                                     
                                                                                
            MOVE  P13F853A-TREC1 TO L008-PR-DTL-01.                             
            MOVE  WS-REMARKS     TO L008-REMARKS.                               
            PERFORM  6000-PRINT-REPORT THRU  6000-EXIT.                         
            WRITE WS-P13F853A-REC FROM BP13F853-TREC1.                          
                                                                                
                                                                                
            MOVE  P13F853A-TREC2 TO L008-PR-DTL-01.                             
            PERFORM  6000-PRINT-REPORT THRU  6000-EXIT.                         
            WRITE WS-P13F853A-REC FROM BP13F853-TREC2.                          
                                                                                
            ADD   2                    TO    WS-NO-WRT-F853A.                   
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       6000-PRINT-REPORT.                                                       
      *-----------------*                                                       
                                                                                
           IF WS-LINE-CTR > 55                                                  
              ADD       1             TO     WS-PAGE-CTR                        
              MOVE  WS-PAGE-CTR       TO     L008-PAGENO                        
              MOVE      6             TO     WS-LINE-CTR                        
                                                                                
              WRITE WS-P13L008-REC    FROM   L008-PR-HEAD-01 AFTER PAGE         
              WRITE WS-P13L008-REC    FROM   L008-PR-HEAD-02                    
              WRITE WS-P13L008-REC    FROM   L008-PR-LINE     AFTER 2           
              WRITE WS-P13L008-REC    FROM   L008-PR-HEAD-03                    
              WRITE WS-P13L008-REC    FROM   L008-PR-LINE                       
           END-IF.                                                              
                                                                                
           IF F853-NUM-REGN       NOT =  WS-PREV-REGN                           
              IF WS-LAST-REGN = WS-PREV-REGN                                    
                 MOVE ZEROES          TO     L008-SNO                           
                 MOVE SPACES          TO     L008-REGN-NO                       
              ELSE                                                              
                 ADD      1           TO     WS-SEQN-CTR                        
                 MOVE WS-SEQN-CTR     TO     L008-SNO                           
                 MOVE WS-LAST-REGN    TO     L008-REGN-NO                       
                 MOVE WS-LAST-REGN    TO     WS-PREV-REGN                       
                 MOVE 1 TO WS-CTR                                               
              END-IF                                                            
           ELSE                                                                 
              MOVE ZEROES             TO     L008-SNO                           
              MOVE SPACES             TO     L008-REGN-NO                       
              IF F853-EOF-IND = 'Y' AND WS-CTR = 0                              
                 ADD      1           TO     WS-SEQN-CTR                        
                                             WS-CTR                             
                 MOVE WS-SEQN-CTR     TO     L008-SNO                           
                 MOVE F853-NUM-REGN   TO     L008-REGN-NO                       
                 MOVE WS-LAST-REGN    TO     WS-PREV-REGN                       
              END-IF                                                            
           END-IF.                                                              
      *    IF F853-EOF-IND NOT = 'Y'                                            
      *    IF F853-NUM-REGN       NOT =  WS-PREV-REGN                           
      *       ADD      1              TO     WS-SEQN-CTR                        
      *       MOVE WS-SEQN-CTR        TO     L008-SNO                           
      *       MOVE F853-NUM-REGN      TO     L008-REGN-NO                       
      *    ELSE                                                                 
      *       MOVE ZEROES             TO     L008-SNO                           
      *       MOVE SPACES             TO     L008-REGN-NO                       
      *    END-IF                                                               
      *    END-IF.                                                              
                                                                                
           IF F853-EOF-IND = 'Y' AND WS-CTR = 0                                 
              ADD         1           TO     WS-SEQN-CTR                        
                                             WS-CTR                             
              MOVE WS-SEQN-CTR        TO     L008-SNO                           
              MOVE F853-NUM-REGN      TO     L008-REGN-NO                       
              MOVE WS-LAST-REGN       TO     WS-PREV-REGN                       
           END-IF.                                                              
                                                                                
           WRITE WS-P13L008-REC       FROM   L008-PR-DTL-01.                    
           MOVE  SPACES               TO     L008-PR-DTL-01.                    
           ADD          1             TO     WS-NO-WRT-L008.                    
           ADD          1             TO     WS-LINE-CTR.                       
                                                                                
           MOVE SPACES                TO     WS-LAST-DTE-MRG1                   
                                             WS-LAST-DTE-MRG2.                  
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       7000-WRITE-BP13K517.                                                     
      *-------------------*                                                     
                                                                                
            MOVE      SPACES             TO   BP13K517-REC.                     
            INITIALIZE BP13K517-REC.                                            
                                                                                
            MOVE      WS-NUM-APPL-NAME   TO   K517-NME.                         
            MOVE      F853-NUM-NRIC      TO   K517-NUM-NRIC.                    
            MOVE      'BP13C008'         TO   K517-NUM-USERID.                  
            MOVE      F853-NUM-REGN      TO   K517-NUM-REGN.                    
            MOVE      WS-SYS-DATE        TO   K517-DTE-SENT.                    
                                                                                
            WRITE     BP13K517-REC.                                             
                                                                                
            EVALUATE  WS-K517-STATUS                                            
                WHEN  00                                                        
                      ADD  1    TO  WS-NO-WRT-K517                              
                WHEN  22                                                        
                      DISPLAY "REC. ALREADY EXISTED IN BP13K517"                
                              "   " "FILE STATUS : " WS-K517-STATUS             
                      DISPLAY "F853-NUM-NRIC : " F853-NUM-NRIC                  
                WHEN  OTHER                                                     
                      DISPLAY "ERROR IN WRITTING THE FILE BP13K517"             
                              "   " "FILE STATUS :  " WS-K517-STATUS            
                      DISPLAY "F853-NUM-NRIC : " F853-NUM-NRIC                  
                      PERFORM 9999-CLOSE-ROUTINE  THRU  9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
      *   ABORT PROCESSING DUE TO SQL ERRORS                          *         
      *****************************************************************         
      *--------------*                                                          
       9000-SQL-ERROR.                                                          
      *--------------*                                                          
                                                                                
           DISPLAY ' *****   BP13C008 ABEND   *****'.                           
                                                                                
           CALL 'DBATIAR'  USING SQLCA.                                         
           EXEC SQL   ROLLBACK   END-EXEC.                                      
           PERFORM SQL-INITIAL UNTIL SQL-INIT-DONE                              
           CALL 'DSNHLI' USING SQL-PLIST8.                                      
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ROLLBACK FAILED, SQLCODE IS : ' SQLCODE            
           END-EVALUATE.                                                        
                                                                                
           MOVE 12                        TO   RETURN-CODE.                     
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       9999-CLOSE-ROUTINE.                                                      
      *------------------*                                                      
                                                                                
           MOVE  WS-NO-READ-F853          TO  WS-ED-READ-F853.                  
           MOVE  WS-NO-READ-IDNTY         TO  WS-ED-READ-IDNTY.                 
           MOVE  WS-NO-READ-PSN           TO  WS-ED-READ-PSN.                   
           MOVE  WS-NO-READ-IDNTY-SP      TO  WS-ED-READ-IDNTY-SP.              
           MOVE  WS-NO-READ-PSN-SP        TO  WS-ED-READ-PSN-SP.                
           MOVE  WS-NO-NOTFND-IDNTY       TO  WS-ED-NOTFND-IDNTY.               
           MOVE  WS-NO-NOTFND-PSN         TO  WS-ED-NOTFND-PSN.                 
           MOVE  WS-NO-NOTFND-IDNTY-SP    TO  WS-ED-NOTFND-IDNTY-SP.            
           MOVE  WS-NO-NOTFND-PSN-SP      TO  WS-ED-NOTFND-PSN-SP.              
           MOVE  WS-NO-READ-MRG           TO  WS-ED-READ-MRG.                   
           MOVE  WS-NO-NOTFND-MRG         TO  WS-ED-NOTFND-MRG.                 
           MOVE  WS-NO-WRT-F853A          TO  WS-ED-WRT-F853A.                  
           MOVE  WS-NO-WRT-L008           TO  WS-ED-WRT-L008.                   
           MOVE  WS-NO-WRT-K517           TO  WS-ED-WRT-K517.                   
                                                                                
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY  '**************************************************'        
           DISPLAY  'DATE : ' WS-DATE-RUN '       ' 'BP13C008'.                 
           DISPLAY  '**************************************************'        
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY  ' 1. NO. OF REC. READ  FROM BP13F853            : '         
                     WS-ED-READ-F853                                            
           DISPLAY  ' 2. NO. OF REC. READ  FROM IDNTY   TBL (APPL)  : '         
                     WS-ED-READ-IDNTY                                           
           DISPLAY  ' 3. NO. OF REC. READ  FROM PERSONS TBL (APPL)  : '         
                     WS-ED-READ-PSN                                             
           DISPLAY  ' 4. NO. OF REC. READ  FROM IDNTY   TBL (SPOUSE): '         
                     WS-ED-READ-IDNTY-SP                                        
           DISPLAY  ' 5. NO. OF REC. READ  FROM PERSONS TBL (SPOUSE): '         
                     WS-ED-READ-PSN-SP                                          
           DISPLAY  ' 6. NO. OF REC. NOTFND IN  IDNTY   TBL (APPL)  : '         
                     WS-ED-NOTFND-IDNTY                                         
           DISPLAY  ' 7. NO. OF REC. NOTFND IN  PERSONS TBL (APPL)  : '         
                     WS-ED-NOTFND-PSN                                           
           DISPLAY  ' 8. NO. OF REC. NOTFND IN  IDNTY   TBL (SPOUSE): '         
                     WS-ED-NOTFND-IDNTY-SP                                      
           DISPLAY  ' 9. NO. OF REC. NOTFND IN  PERSONS TBL (SPOUSE): '         
                     WS-ED-NOTFND-PSN-SP                                        
           DISPLAY  '10. NO. OF REC. READ  FROM MRG     TBL         : '         
                     WS-ED-READ-MRG                                             
           DISPLAY  '11. NO. OF NOTFND   IN     MRG     TBL         : '         
                     WS-ED-NOTFND-MRG                                           
           DISPLAY  '12. NO. OF REC. WRITTEN INTO P13F853A          : '         
                     WS-ED-WRT-F853A                                            
           DISPLAY  '13. NO. OF REC. WRITTEN INTO BP13L008          : '         
                     WS-ED-WRT-L008                                             
           DISPLAY  '14. NO. OF REC. WRITTEN INTO BP13K517          : '         
                     WS-ED-WRT-K517                                             
           DISPLAY  SPACES.                                                     
                                                                                
           CLOSE BP13F853                                                       
                 P13F853A                                                       
                 BP13K517                                                       
                 BP13L008.                                                      
                                                                                
           IF WS-K517-STATUS NOT = 00 AND 97                                    
              DISPLAY "CLOSING ERROR , BP13K517 "  "   "                        
                      "WS-K517-STATUS : " WS-K517-STATUS                        
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
