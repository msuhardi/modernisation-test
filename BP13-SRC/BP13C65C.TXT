      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C65C.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  MAY 15, 2014.                                             
      *===============================================================*         
      *   OBJECTIVE:  TO PRINT REPORT FOR SERS CASES UNDER BANK LOAN  *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F410                                  *         
      *                  2. BP13K670                                  *         
      *                  3. BP13K022                                  *         
      *                  4. BP17K110                                  *         
      *                  5. BP17K040                                  *         
      *                  6. BM06K100                                  *         
      *                  7. BM06K110                                  *         
      *                  8. BP17K000                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. BP13L800                                  *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP135325 15/05/2014 PCL4 NEW PROGRAM.                         *         
      * BP135325 08/07/2014 PCL4 TO UPDATE SUBJECT TITLE              *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
           SELECT  BP13L800  ASSIGN        TO BP13L800.                         
                                                                                
           SELECT  BP13K022  ASSIGN        TO BP13K022                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K022-KEY-FLD                      
                             ALTERNATE RECORD KEY IS K022-NUM-REGN              
                             FILE STATUS   IS WS-K022-STATUS.                   
                                                                                
           SELECT  BP17K110  ASSIGN        TO BP17K110                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY IS K110-KEY-FLD OF BP17K110-REC         
                             ALTERNATE RECORD KEY IS K110-NUM-BLDNG-GL          
                             FILE STATUS   IS WS-K111-STATUS.                   
                                                                                
           SELECT  BP17K040  ASSIGN        TO BP17K040                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K040-KEY-FLD                      
                             ALTERNATE RECORD KEY IS K040-NUM-OIC               
                             FILE STATUS   IS WS-K040-STATUS.                   
                                                                                
           SELECT  BP17K000  ASSIGN        TO BP17K000                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K000-KEY-FLD                      
                             ALTERNATE RECORD KEY IS K000-NUM-REGNO             
                             FILE STATUS   IS WS-K000-STATUS.                   
                                                                                
           SELECT  BP13K670  ASSIGN       TO BP13K670                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K670-KEY-FLD                       
                             FILE STATUS  IS WS-K670-STATUS.                    
                                                                                
           SELECT  BM06K110  ASSIGN       TO BM06K110                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K110-KEY-FLD OF K110-REC           
                             FILE STATUS  IS WS-K110-STATUS.                    
                                                                                
           SELECT  BM06K100  ASSIGN       TO BM06K100                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K100-KEY-FLD                       
                             FILE STATUS  IS WS-K110-STATUS.                    
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K410.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BP17K040                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP17K040.                                                           
                                                                                
       FD   BP17K110                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP17K110.                                                           
                                                                                
       FD   BP13K670                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K670.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP17K000                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP17K000.                                                           
       FD  BP13L800                                                             
                                                                                
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORDING MODE IS V                              
                               LABEL RECORD IS STANDARD.                        
       01  BP13L800-REC                  PIC X(200).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  BP13L800-TITLE.                                                      
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(38) VALUE                          
                'SERS BANK LOAN CASES KEY COLLECTED ON '.                       
           05  FILLER                  PIC X(01).                               
           05  WS-DD                   PIC X(02).                               
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-MM                   PIC X(02).                               
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-YY                   PIC X(04).                               
                                                                                
                                                                                
       01  BP13L800-HDR.                                                        
           05  FILLER                  PIC X(05) VALUE 'S/NO'.                  
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(08) VALUE 'REGN NO.'.              
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(05) VALUE 'POSB'.                  
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(05) VALUE 'SERS'.                  
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(50) VALUE 'FLAT ADDRESS'.          
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(11) VALUE 'SCH-ACCNT'.             
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(50) VALUE 'SERS ADDRESS'.          
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(11) VALUE 'SCH-ACCNT'.             
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(12) VALUE 'WA-RECV-DATE'.          
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(03) VALUE 'IDP'.                   
           05  FILLER                  PIC X(01).                               
           05  FILLER                  PIC X(10) VALUE 'AMT CONTRA'.            
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-F410-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K040             PIC X(1)  VALUE 'N'.                     
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K111-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K040-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K670-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K022-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K100-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K000-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01   BP13L800-REC1.                                                      
            05  WS-SNO                  PIC ZZZZZ.                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-REGN            PIC X(08).                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-POSB            PIC X(05).                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-SER             PIC X(05).                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-FLAT-ADDRESS    PIC X(50).                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-FLAT-SCH-ACC    PIC X(11).                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-SER-ADDRESS     PIC X(50).                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-SER-SCH-ACC     PIC X(11).                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-RECV-DTE        PIC X(10).                              
            05  FILLER                  PIC X(03).                              
            05  WS-L800-IDP             PIC X(03).                              
            05  FILLER                  PIC X(01).                              
            05  WS-L800-CONTRA          PIC $$$,999.99.                         
                                                                                
                                                                                
       01  WS-ADDRESS.                                                          
           05 WS-BLK-NO                PIC X(05) VALUE SPACES.                  
           05 FILLER                   PIC X     VALUE SPACES.                  
           05 WS-FLAT-NO.                                                       
              10 FILLER                PIC X     VALUE '#'.                     
              10 WS-LVL-NO             PIC X(2)  VALUE SPACES.                  
              10 FILLER                PIC X     VALUE '-'.                     
              10 WS-UNIT-NO            PIC X(4)  VALUE SPACES.                  
           05 FILLER                   PIC X(1)  VALUE SPACES.                  
           05 WS-STREET                PIC X(25) VALUE SPACES.                  
           05 WS-SINGAPORE             PIC X(01) VALUE 'S'.                     
           05 FILLER                   PIC X(1)  VALUE SPACES.                  
           05 WS-OPEN                  PIC X(01) VALUE '('.                     
           05 WS-POSTAL-CODE           PIC X(6)  VALUE SPACES.                  
           05 WS-CLOSE                 PIC X(01) VALUE ')'.                     
                                                                                
       01  WS-ADDRESS1.                                                         
           05 WS-BLK-NO1                PIC X(05) VALUE SPACES.                 
           05 FILLER                   PIC X     VALUE SPACES.                  
           05 WS-FLAT-NO1.                                                      
              10 FILLER                PIC X     VALUE '#'.                     
              10 WS-LVL-NO1            PIC X(2)  VALUE SPACES.                  
              10 FILLER                PIC X     VALUE '-'.                     
              10 WS-UNIT-NO1           PIC X(4)  VALUE SPACES.                  
           05 FILLER                   PIC X(1)  VALUE SPACES.                  
           05 WS-STREET1               PIC X(25) VALUE SPACES.                  
           05 WS-SINGAPORE1            PIC X(01) VALUE 'S'.                     
           05 FILLER                   PIC X(1)  VALUE SPACES.                  
           05 WS-OPEN1                 PIC X(01) VALUE '('.                     
           05 WS-POSTAL-CODE1          PIC X(6)  VALUE SPACES.                  
           05 WS-CLOSE1                PIC X(01) VALUE ')'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT                  PIC 9(2)  VALUE ZEROES.                  
           05  WS-I                    PIC 9(2)  VALUE ZEROES.                  
           05  WS-CNT-FND              PIC 9(08) VALUE ZEROES.                  
           05  WS-CNT-NFND             PIC 9(08) VALUE ZEROES.                  
           05  WS-CNT-L800             PIC 9(08) VALUE ZEROES.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-CURR-YY              PIC 9(4)  VALUE ZEROES.                  
           05  WS-CURR-MM              PIC 9(2)  VALUE ZEROES.                  
           05  WS-CURR-DD              PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-F410-NUM-REGN         PIC X(08) VALUE SPACES.                  
           05 WS-AMT-SER-CONTRA        PIC 9(06)V99 VALUE ZEROES.               
           05 WS-SERS-SCH              PIC X(11) VALUE SPACES.                  
           05 WS-DTE-RECV-ACT          PIC X(10) VALUE SPACES.                  
           05 WS-SER-OIC               PIC X(05) VALUE SPACES.                  
           05 WS-POSB-OIC              PIC X(05) VALUE SPACES.                  
           05 WS-NUM-OIC               PIC X(05) VALUE SPACES.                  
           05 WS-NUM-NRIC              PIC X(09) OCCURS 4.                      
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-YEAR-CODE             PIC X(4)  VALUE SPACES.                  
           05 WS-HA-INFO.                                                       
              10 HA-INFO OCCURS 4 TIMES.                                        
                  20  HA-NRIC          PIC  X(09).                              
                  20  HA-NAME          PIC  X(66).                              
           05 WS-DTE-PAID              PIC 9(08) VALUE ZEROES.                  
           05 WS-DTE-CURRENT.                                                   
              10 WS-DTE-YY             PIC 9(4)  VALUE ZEROES.                  
              10 WS-DTE-MM             PIC 9(2)  VALUE ZEROES.                  
              10 WS-DTE-DD             PIC 9(2)  VALUE ZEROES.                  
           05 WS-DTE-PREV.                                                      
              10 WS-LST-YY             PIC 9(4)  VALUE ZEROES.                  
              10 WS-LST-MM             PIC 9(2)  VALUE ZEROES.                  
              10 WS-LST-DD             PIC 9(2)  VALUE ZEROES.                  
           05 WS-1ST-WRITE-L440        PIC X(1)  VALUE 'Y'.                     
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13C913.               *                 
      *-------------------------------------------------------*                 
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01  WS-LINK-BP13C913.                                                    
           05  LINK-NUM-SCH-ACC.                                                
               10  LINK-NUM-SCH        PIC X(4).                                
               10  LINK-NUM-ACC        PIC X(5).                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE            THRU 1000-EXIT.                 
           PERFORM 2000-READ-BP13F410           THRU 2000-EXIT.                 
                                                                                
           PERFORM 3000-PROCESS-RECORDS         THRU 3000-EXIT                  
             UNTIL WS-F410-EOF = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE           THRU 9999-EXIT.                 
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BP13K670                                                 
                       BP13K022                                                 
                       BP17K110                                                 
                       BP17K040                                                 
                       BP17K000                                                 
               OUTPUT  BP13L800.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - ERROR OPENING : ' WS-K100-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K111-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP17K110 - ERROR OPENING : ' WS-K111-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP17K000 - ERROR OPENING : ' WS-K000-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K670-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K670 - ERROR OPENING : ' WS-K670-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR OPENING : ' WS-K022-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K040-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP17K040 - ERROR OPENING : ' WS-K040-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)    TO WS-DATE.                       
                                                                                
           MOVE WS-CURR-DD                    TO WS-DD.                         
           MOVE WS-CURR-MM                    TO WS-MM.                         
           MOVE WS-CURR-YY                    TO WS-YY.                         
                                                                                
           WRITE BP13L800-REC               FROM BP13L800-TITLE.                
           MOVE SPACES                        TO BP13L800-REC.                  
           WRITE BP13L800-REC.                                                  
                                                                                
                                                                                
           WRITE BP13L800-REC  FROM BP13L800-HDR.                               
                                                                                
           MOVE SPACES           TO BP13L800-REC.                               
           WRITE BP13L800-REC.                                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT END                                       
                MOVE 'Y'           TO WS-F410-EOF                               
                GO                 TO 2000-EXIT.                                
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF K410-NUM-REGN NOT = SPACES AND LOW-VALUES                         
              IF K410-NUM-REGN NOT = WS-F410-NUM-REGN                           
                 MOVE K410-NUM-REGN TO WS-F410-NUM-REGN                         
                 IF K410-CDE-ALLOC-SCH   = 'SER' AND                            
                    K410-NUM-LOAN-TAG    = 'B'                                  
                    PERFORM 3100-PROCESS-SERS-CASE   THRU 3100-EXIT             
                 ELSE                                                           
                    ADD 1                            TO WS-CNT-NFND             
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410                THRU 2000-EXIT.            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-PROCESS-SERS-CASE.                                                  
      ******************************************************************        
                                                                                
           PERFORM 5200-READ-BP17K000                THRU 5200-EXIT.            
                                                                                
           IF WS-SERS-SCH NOT = SPACES AND LOW-VALUES                           
              MOVE SPACES                       TO BP13COMM8-REC                
              INITIALIZE                           BP13COMM8-REC                
                                                                                
              MOVE SPACES                       TO WS-BLK-NO1                   
                                                   WS-LVL-NO1                   
                                                   WS-UNIT-NO1                  
                                                   WS-STREET1                   
                                                   WS-POSTAL-CODE1              
              MOVE WS-SERS-SCH(1:9)             TO LINK-NUM-SCH-ACC             
              PERFORM 5000-CALL-BP13C913      THRU 5000-EXIT                    
                                                                                
              MOVE COMM8-NUM-BLK                TO WS-BLK-NO1                   
              MOVE COMM8-NUM-LEVEL              TO WS-LVL-NO1                   
              MOVE COMM8-NUM-UNIT-MAIN          TO WS-UNIT-NO1                  
              MOVE COMM8-NME-STREET             TO WS-STREET1                   
              MOVE COMM8-NUM-POSTAL-CODE        TO WS-POSTAL-CODE1              
           END-IF.                                                              
                                                                                
           IF K410-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              MOVE SPACES                       TO BP13COMM8-REC                
              INITIALIZE                           BP13COMM8-REC                
                                                                                
              MOVE SPACES                       TO WS-BLK-NO                    
                                                   WS-LVL-NO                    
                                                   WS-UNIT-NO                   
                                                   WS-STREET                    
                                                   WS-POSTAL-CODE               
                                                                                
              MOVE K410-NUM-SCH-ACC             TO LINK-NUM-SCH-ACC             
              PERFORM 5000-CALL-BP13C913      THRU 5000-EXIT                    
                                                                                
              MOVE COMM8-NUM-BLK                TO WS-BLK-NO                    
              MOVE COMM8-NUM-LEVEL              TO WS-LVL-NO                    
              MOVE COMM8-NUM-UNIT-MAIN          TO WS-UNIT-NO                   
              MOVE COMM8-NME-STREET             TO WS-STREET                    
              MOVE COMM8-NUM-POSTAL-CODE        TO WS-POSTAL-CODE               
           END-IF.                                                              
                                                                                
           PERFORM 3200-READ-BP13K022                THRU 3200-EXIT.            
           PERFORM 3300-READ-BP13K670                THRU 3300-EXIT.            
           PERFORM 3400-READ-BM06K110                THRU 3400-EXIT.            
                                                                                
           PERFORM 6000-WRITE-BP13L800               THRU 6000-EXIT.            
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-READ-BP13K022.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                            TO BP13K022-REC.              
           INITIALIZE                                BP13K022-REC.              
                                                                                
           MOVE K410-NUM-REGN                     TO K022-NUM-REGN.             
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K022-NUM-OIC             TO WS-POSB-OIC                
                                                                                
               WHEN 23                                                          
                    MOVE SPACES                   TO WS-POSB-OIC                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K022 : ' WS-K022-STATUS          
                            ' REGN = ' K410-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-READ-BP13K670.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                            TO BP13K670-REC.              
           INITIALIZE                                BP13K670-REC.              
                                                                                
           MOVE K410-NUM-REGN                     TO K670-NUM-REGN.             
                                                                                
           READ BP13K670.                                                       
                                                                                
           EVALUATE WS-K670-STATUS                                              
               WHEN 00                                                          
                    IF K670-DTE-RECV-ACT NOT = SPACES AND LOW-VALUES            
                                                      AND ZEROES                
                       STRING K670-DTE-RECV-ACT(7:2) '/'                        
                              K670-DTE-RECV-ACT(5:2) '/'                        
                              K670-DTE-RECV-ACT(1:4)                            
                          DELIMITED BY SIZE INTO WS-DTE-RECV-ACT                
                    END-IF                                                      
                                                                                
               WHEN 23                                                          
                    MOVE SPACES               TO WS-DTE-RECV-ACT                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K670 : ' WS-K670-STATUS          
                            ' REGN = ' K410-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-READ-BM06K110.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO K110-REC.                           
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE K410-NUM-SCH-ACC         TO K110-KEY-FLD OF K110-REC.           
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3500-READ-BM06K100          THRU 3500-EXIT          
                                                                                
               WHEN 23                                                          
                    MOVE SPACES                           TO K110-REC           
                    INITIALIZE                               K110-REC           
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K110 : ' WS-K110-STATUS          
                            ' KEY  = ' K110-KEY-FLD OF K110-REC                 
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-READ-BM06K100.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                      TO K100-REC.                        
           INITIALIZE                          K100-REC.                        
                                                                                
           MOVE K110-AIX1                   TO K100-KEY-FLD.                    
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE WS-K100-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3600-READ-BP17K110          THRU 3600-EXIT          
                                                                                
               WHEN 23                                                          
                    MOVE SPACES                           TO K100-REC           
                    INITIALIZE                               K100-REC           
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K100 : ' WS-K100-STATUS          
                            ' KEY  = ' K100-KEY-FLD                             
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3600-READ-BP17K110.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                            TO BP17K110-REC.              
           INITIALIZE                                BP17K110-REC.              
                                                                                
           MOVE K100-CDE-BLK                      TO K110-NUM-BLDNG-GL.         
                                                                                
           READ BP17K110 KEY IS K110-NUM-BLDNG-GL.                              
                                                                                
           EVALUATE WS-K111-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3700-PROCESS-BP17K040 THRU 3700-EXIT                
                                                                                
               WHEN 23                                                          
                    MOVE SPACES                   TO BP17K110-REC               
                    INITIALIZE                       BP17K110-REC               
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP17K110 : ' WS-K111-STATUS          
                            ' CDE-BLK = ' K110-NUM-BLDNG-GL                     
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3700-PROCESS-BP17K040.                                                   
      ******************************************************************        
                                                                                
           MOVE SPACES                              TO BP17K040-REC.            
           INITIALIZE                                  BP17K040-REC.            
                                                                                
           PERFORM 3800-GET-OIC-INFO              THRU 3800-EXIT.               
                                                                                
           MOVE 'N'                                 TO WS-EOF-K040.             
                                                                                
           MOVE SPACES                              TO K040-KEY-FLD.            
                                                                                
           START BP17K040 KEY >= K040-NUM-OIC.                                  
                                                                                
           EVALUATE WS-K040-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3900-READNEXT-BP17K040 THRU 3900-EXIT               
                    PERFORM 4000-PROCESS-BP17K040  THRU 4000-EXIT               
                            UNTIL WS-EOF-K040 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR BP17K040 : ' WS-K040-STATUS            
                            ' OIC  : ' K040-NUM-OIC                             
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       3700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3800-GET-OIC-INFO.                                                       
      ******************************************************************        
                                                                                
           IF (K110-NUM-OIC4-SAU-START-LVL NOT = SPACES AND LOW-VALUES)         
              IF (K110-NUM-OIC4-SAU-START-LVL <= COMM8-NUM-LEVEL)               
                 MOVE K110-NUM-OIC4-SAU       TO K040-NUM-OIC                   
              ELSE                                                              
                IF (K110-NUM-OIC4-SAU-START-LVL > COMM8-NUM-LEVEL) AND          
                   (K110-NUM-OIC3-SAU-START-LVL <= COMM8-NUM-LEVEL)             
                   MOVE K110-NUM-OIC3-SAU     TO K040-NUM-OIC                   
                ELSE                                                            
                  IF K110-NUM-OIC3-SAU-START-LVL > COMM8-NUM-LEVEL AND          
                     K110-NUM-OIC2-SAU-START-LVL <= COMM8-NUM-LEVEL             
                     MOVE K110-NUM-OIC2-SAU    TO K040-NUM-OIC                  
                  ELSE                                                          
                    IF K110-NUM-OIC2-SAU-START-LVL > COMM8-NUM-LEVEL            
                       AND                                                      
                       K110-NUM-OIC1-SAU-START-LVL <= COMM8-NUM-LEVEL           
                       MOVE K110-NUM-OIC-SAU   TO K040-NUM-OIC                  
                    END-IF                                                      
                  END-IF                                                        
                END-IF                                                          
              END-IF                                                            
           ELSE                                                                 
                                                                                
           IF (K110-NUM-OIC3-SAU-START-LVL NOT = SPACES AND LOW-VALUES)         
              IF (K110-NUM-OIC3-SAU-START-LVL <= COMM8-NUM-LEVEL)               
                 MOVE K110-NUM-OIC3-SAU       TO K040-NUM-OIC                   
              ELSE                                                              
                IF K110-NUM-OIC3-SAU-START-LVL > COMM8-NUM-LEVEL AND            
                   K110-NUM-OIC2-SAU-START-LVL <= COMM8-NUM-LEVEL               
                   MOVE K110-NUM-OIC2-SAU     TO K040-NUM-OIC                   
                ELSE                                                            
                  IF K110-NUM-OIC2-SAU-START-LVL > COMM8-NUM-LEVEL AND          
                     K110-NUM-OIC1-SAU-START-LVL <= COMM8-NUM-LEVEL             
                     MOVE K110-NUM-OIC-SAU    TO K040-NUM-OIC                   
                  END-IF                                                        
                END-IF                                                          
              END-IF                                                            
           ELSE                                                                 
                                                                                
            IF (K110-NUM-OIC2-SAU-START-LVL NOT = SPACES AND LOW-VALUES)        
               IF (K110-NUM-OIC2-SAU-START-LVL <= COMM8-NUM-LEVEL)              
                  MOVE K110-NUM-OIC2-SAU       TO K040-NUM-OIC                  
               ELSE                                                             
                 IF K110-NUM-OIC2-SAU-START-LVL > COMM8-NUM-LEVEL AND           
                    K110-NUM-OIC1-SAU-START-LVL <= COMM8-NUM-LEVEL              
                    MOVE K110-NUM-OIC-SAU      TO K040-NUM-OIC                  
                 END-IF                                                         
               END-IF                                                           
            ELSE                                                                
               MOVE K110-NUM-OIC-SAU        TO K040-NUM-OIC                     
            END-IF                                                              
           END-IF                                                               
           END-IF.                                                              
                                                                                
           MOVE K040-NUM-OIC                TO WS-NUM-OIC.                      
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3900-READNEXT-BP17K040.                                                  
      ******************************************************************        
                                                                                
           READ BP17K040 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K040.                        
                                                                                
       3900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-BP17K040.                                                   
      ******************************************************************        
                                                                                
           IF K040-NUM-OIC = WS-NUM-OIC                                         
              IF K040-NUM-UNIT = 'SAU'                                          
                 MOVE K040-NUM-OIC                 TO WS-SER-OIC                
                 MOVE 'Y'                          TO WS-EOF-K040               
              END-IF                                                            
                                                                                
              PERFORM 3900-READNEXT-BP17K040     THRU 3900-EXIT                 
           ELSE                                                                 
              MOVE 'Y'                             TO WS-EOF-K040               
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5000-CALL-BP13C913.                                                      
      ******************************************************************        
                                                                                
           CALL 'BP13C913' USING WS-LINK-BP13C913,                              
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       5200-READ-BP17K000.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                            TO BP17K000-REC.              
           INITIALIZE                                BP17K000-REC.              
                                                                                
           MOVE SPACES                            TO WS-SERS-SCH.               
           MOVE K410-NUM-REGN                     TO K000-NUM-REGNO.            
                                                                                
           READ BP17K000 KEY IS K000-NUM-REGNO.                                 
                                                                                
           EVALUATE WS-K000-STATUS                                              
               WHEN 00                                                          
                    MOVE K000-NUM-SCH-ACC         TO WS-SERS-SCH                
                                                                                
               WHEN 23                                                          
                    MOVE SPACES                   TO WS-SERS-SCH                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP17K000 : ' WS-K000-STATUS          
                            ' REGN = ' K410-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-WRITE-BP13L800.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                        TO BP13L800-REC                   
                                                 BP13L800-REC1.                 
                                                                                
           INITIALIZE                            BP13L800-REC                   
                                                 BP13L800-REC1.                 
                                                                                
                                                                                
           ADD 1                              TO WS-CNT-L800.                   
                                                                                
           IF K410-AMT-SER-CONTRA-STAMP-FEES NOT NUMERIC                        
              MOVE ZEROES TO K410-AMT-SER-CONTRA-STAMP-FEES                     
           END-IF.                                                              
                                                                                
           IF K410-AMT-SER-CONTRA-1STMTHINST NOT NUMERIC                        
              MOVE ZEROES TO K410-AMT-SER-CONTRA-1STMTHINST                     
           END-IF.                                                              
                                                                                
           IF K410-AMT-SER-CONTRA-CONVY-FEES NOT NUMERIC                        
              MOVE ZEROES TO K410-AMT-SER-CONTRA-CONVY-FEES                     
           END-IF.                                                              
                                                                                
           IF K410-AMT-SER-CONTRA-ICP NOT NUMERIC                               
              MOVE ZEROES TO K410-AMT-SER-CONTRA-ICP                            
           END-IF.                                                              
                                                                                
           IF K410-AMT-SER-CONTRA-MISC-FEE NOT NUMERIC                          
              MOVE ZEROES TO K410-AMT-SER-CONTRA-MISC-FEE                       
           END-IF.                                                              
                                                                                
           COMPUTE WS-AMT-SER-CONTRA = K410-AMT-SER-CONTRA-STAMP-FEES +         
                                       K410-AMT-SER-CONTRA-1STMTHINST +         
                                       K410-AMT-SER-CONTRA-CONVY-FEES +         
                                       K410-AMT-SER-CONTRA-ICP        +         
                                       K410-AMT-SER-CONTRA-MISC-FEE.            
                                                                                
           MOVE WS-CNT-L800                   TO WS-SNO.                        
           MOVE K410-NUM-REGN                 TO WS-L800-REGN.                  
           MOVE WS-POSB-OIC                   TO WS-L800-POSB.                  
           MOVE WS-SER-OIC                    TO WS-L800-SER.                   
           MOVE WS-ADDRESS                    TO WS-L800-FLAT-ADDRESS.          
           MOVE K410-NUM-SCH-ACC              TO WS-L800-FLAT-SCH-ACC.          
           MOVE WS-ADDRESS1                   TO WS-L800-SER-ADDRESS.           
           MOVE WS-SERS-SCH                   TO WS-L800-SER-SCH-ACC.           
           MOVE WS-DTE-RECV-ACT               TO WS-L800-RECV-DTE.              
           MOVE K410-CDE-IDP-SCH              TO WS-L800-IDP.                   
           MOVE WS-AMT-SER-CONTRA             TO WS-L800-CONTRA.                
                                                                                
           WRITE BP13L800-REC  FROM BP13L800-REC1.                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           IF WS-CNT-L800 = ZEROES                                              
              MOVE SPACES           TO BP13L800-REC                             
              WRITE BP13L800-REC                                                
                                                                                
              MOVE '              NO RECORD FOUND!           '                  
                                  TO BP13L800-REC                               
              WRITE BP13L800-REC                                                
           END-IF.                                                              
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C65C'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1)  NO OF BP13F410 RECORDS READ............. : '           
                    WS-F410-READ.                                               
           DISPLAY '(2)  NO OF BP13F410 RECORDS BYPASS   ..... : '              
                    WS-CNT-NFND.                                                
           DISPLAY '(3)  NO OF BP13F410 RECORDS WRITTEN  ..... : '              
                    WS-CNT-L800.                                                
                                                                                
           CLOSE    BP13F410                                                    
                    BM06K110                                                    
                    BM06K100                                                    
                    BP13K670                                                    
                    BP13K022                                                    
                    BP17K110                                                    
                    BP17K040                                                    
                    BP17K000                                                    
                    BP13L800.                                                   
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP17K000 - ERROR CLOSING : ' WS-K000-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - ERROR CLOSING : ' WS-K100-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K111-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP17K110 - ERROR CLOSING : ' WS-K111-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K670-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K670 - ERROR CLOSING : ' WS-K670-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K040-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP17K040 - ERROR CLOSING : ' WS-K040-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR CLOSING : ' WS-K022-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
