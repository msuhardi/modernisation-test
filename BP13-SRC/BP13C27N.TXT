       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C27N.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  20 APRIL 2018.                                            
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *  OBJECTIVES : SEND EMAIL ALERT OF SERS REPORT               *           
      *               BP13T.L290.SERSRPT                            *           
      *                                                             *           
      *  INPUT FILES  - BP13L290                                    *           
      *  OUTPUT FILES - BP13MAIL                                    *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY    DATE       DESCRIPTION                       *           
      * -------- ----  ---------- -----------                       *           
      * BP137234 ESA1  20/04/2018 NEW PROGRAM                       *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13L290  ASSIGN        TO BP13L290.                          
                                                                                
           SELECT BP13MAIL  ASSIGN        TO BP13MAIL.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13L290                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS  200 CHARACTERS.                                     
       01  BP13L290-REC               PIC X(200).                               
                                                                                
       FD  BP13MAIL                                                             
           RECORDING  MODE  IS F                                                
           RECORD CONTAINS  200 CHARACTERS.                                     
       01  MAIL-PRTREC                PIC X(200).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  BP13L290-EOF-TAG        PIC X(01).                               
               88  BP13L290-EOF        VALUE 'Y'.                               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-L290-READ-CNT        PIC 9(06)     VALUE ZEROES.              
           05  WS-L290-CNT             PIC 9(06)     VALUE ZEROES.              
           05  WS-MAIL-SEND-CNT        PIC 9(06)     VALUE ZEROES.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-DATE                 PIC X(08)     VALUE SPACES.              
           05  WS-EDIT-DATE            PIC X(10)     VALUE SPACES.              
           05  WS-SNO                  PIC 9(03)     VALUE ZEROES.              
           05  WS-EDIT-CNT             PIC Z(05)9.                              
                                                                                
      *------------------------------------------------------------*            
      * EMAIL LAYOUT                                               *            
      *------------------------------------------------------------*            
       01  MAIL-HEADER-1.                                                       
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(118) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-2.                                                       
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  SEND-ID         PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(106) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-3.                                                       
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  TO-MAIL-ID      PIC X(76)  VALUE                                 
               'TEEU_Hui@hdb.gov.sg>'.                                          
           05  FILLER          PIC X(115) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-4.                                                       
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(126) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-5.                                                       
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  FROM-MAIL-ID    PIC X(24)  VALUE                                 
               'SOC SYSTEM - EMAIL ALERT'.                                      
           05  FILLER          PIC X(101) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-6.                                                       
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  TO-MAIL-ID-A    PIC X(76)  VALUE                                 
               'TEEU_Hui@hdb.gov.sg>'.                                          
           05  FILLER          PIC X(117) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-7.                                                       
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  SUBJECT-1       PIC X(39)  VALUE                                 
               'Daily report for SERS EINV'.                                    
           05  FILLER          PIC X(81)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-10A     PIC X(41)  VALUE                                 
           'Mime-Version: 1.0                        '.                         
       01  MAIL-HEADER-10B     PIC X(130)                                       
           VALUE 'Content-type: multipart/mixed; boundary="Mime.Part.Bou        
      -     'ndary"'.                                                           
       01  MAIL-HEADER-10C          PIC X(130)                                  
           VALUE 'This is a multi-part message in MIME format.'.                
       01  MAIL-HEADER-10D          PIC X(130)                                  
           VALUE '--Mime.Part.Boundary                        '.                
       01  MAIL-HEADER-10E          PIC X(130)                                  
           VALUE 'Content-Type: text/plain; charset="iso-8859-1"'.              
       01  MAIL-HEADER-10F          PIC X(130)                                  
           VALUE 'Content-Transfer-Encoding: 7bit               '.              
       01  MAIL-HEADER-10G          PIC X(130)                                  
           VALUE 'Content-disposition: attachment;              '.              
       01  MAIL-HEADER-10H          PIC X(130)                                  
           VALUE '           FILENAME="BP13L290.TXT"            '.              
                                                                                
                                                                                
       01  MAIL-BODY-BLANK-LINE.                                                
           05 FILLER          PIC X(130) VALUE SPACES.                          
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13L290     THRU 2000-EXIT.                       
           PERFORM 3000-START-PROCESS     THRU 3000-EXIT                        
                   UNTIL BP13L290-EOF.                                          
           WRITE MAIL-PRTREC    FROM MAIL-HEADER-10D.                           
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13L290                                                 
                OUTPUT BP13MAIL.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DATE.                        
           STRING WS-DATE(7:2) '/'                                              
                  WS-DATE(5:2) '/'                                              
                  WS-DATE(1:4)                                                  
                  DELIMITED BY SIZE        INTO WS-EDIT-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13L290.                                                      
      *-------------------------------------------------------------            
           READ BP13L290 AT END                                                 
                MOVE    'Y'       TO   BP13L290-EOF-TAG                         
                NOT AT END                                                      
                ADD      1        TO   WS-L290-READ-CNT                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-START-PROCESS.                                                      
      *-------------------------------------------------------------            
            IF WS-L290-READ-CNT = 1                                             
              PERFORM 4100-SETUP-HEADER      THRU 4100-EXIT                     
            END-IF.                                                             
            WRITE MAIL-PRTREC         FROM BP13L290-REC                         
            ADD  1                    TO   WS-MAIL-SEND-CNT                     
                                                                                
           PERFORM 2000-READ-BP13L290 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-SETUP-HEADER.                                                       
      *-------------------------------------------------------------            
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-1.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-2.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-3.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-4.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-5.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-6.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-7.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10A.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10B.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-BLANK-LINE.                   
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10C.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10D.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10E.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10F.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10G.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10H.                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY ' BP13C27N CONTROL LIST    DATE : ' WS-EDIT-DATE.            
           DISPLAY '*------------------------------------------*'.              
           DISPLAY ' '.                                                         
           MOVE WS-L290-READ-CNT     TO WS-EDIT-CNT.                            
           DISPLAY 'CASES READ FROM BP13L290    : ' WS-EDIT-CNT.                
           MOVE WS-MAIL-SEND-CNT     TO WS-EDIT-CNT.                            
           DISPLAY 'CASES WRITTEN TO EMAIL FILE : ' WS-EDIT-CNT.                
           DISPLAY ' '.                                                         
           DISPLAY '*------------------------------------------*'.              
           DISPLAY ' '.                                                         
                                                                                
           CLOSE  BP13L290                                                      
                  BP13MAIL.                                                     
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
