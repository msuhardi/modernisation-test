       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C745.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
       DATE-WRITTEN.  17/07/92.                                                 
       DATE-COMPILED.                                                           
      ************************************************************              
      *                                                          *              
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                    *              
      *                                                          *              
      *    SYSTEM ID : TO PRINT REPORT TO SHOW THE BEFORE AND    *              
      *                AFTER BALLOT INFORMATION DONE BY USER     *              
      *                AFTER PIGEONHOLING OR AFTER SYSTEM ASSIGN *              
      *                THE WEIGHTAGE.                            *              
      *                                                          *              
      *    OBJECTIVE :                                           *              
      *                                                          *              
      *     INPUT FILES:                                         *              
      *      1. BP13F595                                         *              
      *      2. BP13K740                                         *              
      *                                                          *              
      *     OUTPUT FILES :                                       *              
      *      1. BP13L745                                         *              
      *                                                          *              
      * AMENDMENTS:                                              *              
      * REF      DATE      BY    DESCRIPTIONS                    *              
      * -------  -------   ----  ------------                    *              
      * C930335  20/05/93  TON   TO INCLUDE NUM-USER-ID          *              
      * C9309B4  28/09/93  LSB   TO INCLUDE RESIDENT HA3/HA4     *              
      * C94XXXX  28/06/94  LSB   TO INCLUDE DTE-BALLOT           *              
      * N940014  03/10/94  OCT   TO INCLUDE ZONE                 *              
      * C950106  11/01/95  RGM   CHANGE REPORT FORMAT & ADD RE-  *              
      *                          TRIEVAL OF DATA FROM K740 IF    *              
      *                          K520-CDE-ZONE IS BLANK          *              
      * C950206  13/02/95  YCH   PIGEONHOLE TIME                 *              
      * BP130031 28/09/95  MS1   PRINT GRO-RANK INSTEAD OF       *              
      *                          JB-REGN                         *              
      * BP130099 15/03/96  EM1   PRINT ALLOC-SCH AFTER UPDATE    *              
      * BP130652 22/02/99  NOR   EXPAND K520 F595 & K740 RECL    *              
      *                          FROM 300 TO 500 CHARACTERS      *              
      * BP130974 18/01/01  AKT   TO INCLUDE BP13K730             *              
      * BP132255 28/11/02  ANC1  EXPAND GRO-RANK TO 2BYTES       *              
      * BP132463 09/12/03  SK    CATER FOR HA1,HA2,HA3 & HA4     *              
      * BP132555 19/07/04  CKK   UPDATE FILENAME                 *              
      * BP132810 01/02/06  BA3   CHANGE NUM-REF FROM 6 BYTES     *              
      *                          TO 8 BYTES.                     *              
      * BP136157 09/03/16  FNP1  CHANGE LAYOUT OF THE REPORT,    *              
      *                          REMOVE READING OF BP13K520      *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K740-KEY-FLD                               
                  FILE STATUS     IS BP13K740-STATUS.                           
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K730-KEY-NEW                               
                  FILE STATUS     IS BP13K730-STATUS.                           
                                                                                
           SELECT BP13F595 ASSIGN TO BP13F595.                                  
                                                                                
           SELECT BP13L745 ASSIGN TO BP13L745.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F595                                                            
            BLOCK     CONTAINS 0   RECORDS                                      
            RECORD    CONTAINS 500 CHARACTERS                                   
            LABEL     RECORD   IS  STANDARD                                     
            RECORDING MODE     IS  F.                                           
       COPY BP13F595.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K740.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   BP13L745                                                            
            BLOCK     CONTAINS 0   RECORDS                                      
            RECORD    CONTAINS 132 CHARACTERS                                   
            LABEL     RECORD   IS  OMITTED                                      
            RECORDING MODE     IS  F.                                           
       01  BP13L745-REC                  PIC X(132).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-ACCESS-STATUS.                                               
           05  BP13K730-STATUS           PIC 9(2)  VALUE ZEROES.                
           05  BP13K740-STATUS           PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F595-EOF               PIC X     VALUE 'N'.                   
               88 F595-EOF                         VALUE 'Y'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-LINE                   PIC 9(2)  VALUE 70.                    
           05  WS-PAGE                   PIC 9(4)  VALUE ZEROES.                
           05  WS-K520-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-K730-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-K740-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-F595-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-NUM-PRINT              PIC 9(6)  VALUE ZEROES.                
           05  WS-PRINT-TAG              PIC X     VALUE SPACES.                
                                                                                
       01  WS-FORMAT-DATE.                                                      
           05  WS-PRT-YY                 PIC X(4).                              
           05  WS-PRT-MM                 PIC X(2).                              
           05  WS-PRT-DD                 PIC X(2).                              
                                                                                
       01  WS-PRINT-DATE.                                                       
           05  WS-PRT-DD                 PIC X(2).                              
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-PRT-MM                 PIC X(2).                              
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-PRT-YY                 PIC X(4).                              
                                                                                
       01  WS-BALLOT-DATE.                                                      
           05  WS-BALLOT-YY              PIC X(4).                              
           05  WS-BALLOT-MM              PIC X(2).                              
                                                                                
       01  WS-QTR                        PIC X(03).                             
       01  WS-NT-ZONE                    PIC X(03).                             
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-YY                     PIC X(4).                              
           05  WS-MM                     PIC X(2).                              
           05  WS-DD                     PIC X(2).                              
                                                                                
       01  WS-EDIT-DATE.                                                        
           05  WS-DD                     PIC X(2).                              
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-MM                     PIC X(2).                              
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-YY                     PIC X(4).                              
                                                                                
       01  WS-SYS-DATE.                                                         
           05  WS-SYS-YY                 PIC 9999.                              
           05  WS-SYS-MM                 PIC 99.                                
           05  WS-SYS-DD                 PIC 99.                                
                                                                                
      **************** REPORT FORMAT *******************                        
                                                                                
       01  L745-HEAD1.                                                          
           03  FILLER              PIC X(14)   VALUE 'BP13L745'.                
           03  FILLER              PIC X(8)    VALUE 'HDB3'.                    
           03  FILLER              PIC X(18)   VALUE SPACES.                    
           03  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           03  FILLER              PIC X(33)   VALUE SPACES.                    
           03  FILLER              PIC X(8)    VALUE 'DATE : '.                 
           03  HEAD1-DATE          PIC X(10)   VALUE SPACES.                    
                                                                                
       01  L745-HEAD2.                                                          
           03  FILLER              PIC X(37) VALUE SPACES.                      
           03  FILLER              PIC X(64) VALUE                              
               'AUDIT CHECK ON APPLICATIONS UPDATED IN BALLOT AFTER SHOR        
      -        'TLISTING'.                                                      
           03  FILLER              PIC X(11)   VALUE SPACES.                    
           03  FILLER              PIC X(8)    VALUE 'PAGE : '.                 
           03  HEAD1-PAGE          PIC ZZZZ9.                                   
                                                                                
       01  L745-HEAD6.                                                          
           05  FILLER              PIC X(05)   VALUE '  SN'.                    
           05  FILLER              PIC X(10)   VALUE 'BAL QTR'.                 
           05  FILLER              PIC X(07)   VALUE 'SALES'.                   
           05  FILLER              PIC X(23)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE 'BEFORE UPDATE'.           
           05  FILLER              PIC X(29)   VALUE SPACES.                    
           05  FILLER              PIC X(12)   VALUE 'AFTER UPDATE'.            
                                                                                
       01  L745-HEAD7.                                                          
           05  FILLER              PIC X(15)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'MODE'.                    
           05  FILLER              PIC X(04)   VALUE 'FT'.                      
           05  FILLER              PIC X(10)   VALUE 'REG NO'.                  
           05  FILLER              PIC X(34)   VALUE                            
               'STAT/COHORT/SUCC ZN/NT/UNSUCC CODE'.                            
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(34)   VALUE                            
               'STAT/COHORT/SUCC ZN/NT/UNSUCC CODE'.                            
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(09)   VALUE 'USER-ID'.                 
           05  FILLER              PIC X(10)   VALUE 'DTE-UPD'.                 
                                                                                
       01  L745-DETAIL1.                                                        
           03  L745-SERIAL         PIC ZZ9.                                     
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  L745-BALLOT-QTR     PIC X(10).                                   
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  L745-SALES-MODE     PIC X(03).                                   
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  L745-FT             PIC X(02).                                   
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  L745-REGN           PIC X(08).                                   
           03  FILLER              PIC X(09) VALUE SPACES.                      
           03  L745-STATUS-BEF     PIC X(23).                                   
           03  FILLER              PIC X(11) VALUE SPACES.                      
           03  L745-STATUS-AFT     PIC X(23).                                   
           03  FILLER              PIC X(11) VALUE SPACES.                      
           03  L745-USER-ID        PIC X(08).                                   
           03  FILLER              PIC X(02).                                   
           03  L745-UPD-DATE       PIC X(10).                                   
                                                                                
       01  L745-FOOTER1.                                                        
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  FILLER              PIC X(50) VALUE                              
                  'CHECKED BY    : _________________________________'.          
                                                                                
       01  L745-FOOTER2.                                                        
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  FILLER              PIC X(50) VALUE                              
                  'REVIEWED BY   : _________________________________'.          
                                                                                
       01  L745-FOOTER3.                                                        
           03  FILLER              PIC X(02)  VALUE SPACES.                     
           03  FILLER              PIC X(50) VALUE                              
                  '*** NO RECORDS PRINTED ***'.                                 
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-ROUTINE.                                                        
                                                                                
           PERFORM 100-OPEN-FILES        THRU 100-EXIT.                         
                                                                                
           PERFORM 500-PRINT-HEAD        THRU 500-EXIT.                         
                                                                                
           PERFORM 200-READ-BP13F595     THRU 200-EXIT.                         
                                                                                
           PERFORM 300-PROCESS-RECORD    THRU 300-EXIT                          
                   UNTIL F595-EOF.                                              
                                                                                
           PERFORM 600-PRINT-FOOTER      THRU 600-EXIT.                         
                                                                                
           PERFORM 900-CLOSE-ROUTINE     THRU 900-EXIT.                         
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
                                                                                
           OPEN  INPUT BP13K730                                                 
                       BP13K740                                                 
                       BP13F595                                                 
                OUTPUT BP13L745.                                                
                                                                                
           IF BP13K730-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR FOR BP13K730, STATUS '                        
                                             BP13K730-STATUS                    
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           IF BP13K740-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR FOR BP13K740, STATUS '                        
                                             BP13K740-STATUS                    
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO  WS-SYS-DATE.                     
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F595.                                                       
                                                                                
           READ BP13F595 AT END                                                 
              MOVE 'Y'                   TO WS-F595-EOF                         
                 GO TO 200-EXIT.                                                
                                                                                
           ADD   1                       TO WS-F595-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
                                                                                
           IF F595-CDE-UPD-AFTER-BALLOT NOT = 'T'                               
               PERFORM 200-READ-BP13F595 THRU 200-EXIT                          
               GO TO 300-EXIT.                                                  
                                                                                
           PERFORM 350-DTE-UPDATE-CHECK  THRU 350-EXIT.                         
                                                                                
           IF WS-PRINT-TAG = 'Y'                                                
              PERFORM 400-PRINT-REPORT  THRU 400-EXIT.                          
                                                                                
           PERFORM 200-READ-BP13F595     THRU 200-EXIT.                         
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-DTE-UPDATE-CHECK.                                                    
                                                                                
           MOVE SPACES             TO WS-PRINT-TAG.                             
           MOVE WS-SYS-YY          TO WS-BALLOT-YY.                             
                                                                                
           IF F595-KEY-FLD(10:4) = WS-SYS-YY                                    
              IF (WS-SYS-MM = 01 OR 02 OR 03) AND                               
                 (F595-KEY-FLD(14:2) = 01 OR 02 OR 03)                          
                  MOVE 'Y' TO WS-PRINT-TAG                                      
              ELSE                                                              
              IF (WS-SYS-MM = 04 OR 05 OR 06) AND                               
                 (F595-KEY-FLD(14:2) = 04 OR 05 OR 06)                          
                  MOVE 'Y' TO WS-PRINT-TAG                                      
              ELSE                                                              
                IF (WS-SYS-MM = 07 OR 08 OR 09) AND                             
                   (F595-KEY-FLD(14:2) = 07 OR 08 OR 09)                        
                    MOVE 'Y' TO WS-PRINT-TAG                                    
                ELSE                                                            
                  IF (WS-SYS-MM = 10 OR 11 OR 12) AND                           
                     (F595-KEY-FLD(14:2) = 10 OR 11 OR 12)                      
                     MOVE 'Y' TO WS-PRINT-TAG.                                  
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-PRINT-REPORT.                                                        
                                                                                
           MOVE SPACES                   TO L745-STATUS-BEF                     
                                            L745-STATUS-AFT                     
                                            WS-NT-ZONE.                         
           ADD 1                         TO WS-NUM-PRINT.                       
           MOVE WS-NUM-PRINT             TO L745-SERIAL.                        
                                                                                
                                                                                
           IF F595-CDE-NT1 NOT = SPACES                                         
              MOVE F595-CDE-NT1          TO WS-NT-ZONE                          
              MOVE F595-NUM-REGN         TO L745-REGN                           
           ELSE                                                                 
              MOVE F595-CDE-ZONE         TO WS-NT-ZONE                          
              MOVE SPACES                TO L745-REGN                           
           END-IF.                                                              
                                                                                
           MOVE F595-DTE-BALLOT   TO L745-BALLOT-QTR.                           
           MOVE F595-CDE-FLAT-TYPE       TO L745-FT.                            
           MOVE F595-NUM-ALLO-CAT        TO L745-SALES-MODE.                    
                                                                                
           MOVE F595-KEY-FLD(10:8)       TO WS-FORMAT-DATE.                     
           MOVE CORR WS-FORMAT-DATE      TO WS-PRINT-DATE.                      
           MOVE WS-PRINT-DATE            TO L745-UPD-DATE.                      
                                                                                
           STRING F595-CDE-REQUEST-STATUS ' / '                                 
                  F595-CDE-COHORT       ' / '                                   
                  F595-CDE-SUCC-NT-ZONE ' / '                                   
                  WS-NT-ZONE            ' / '                                   
                  F595-CDE-UNSUCC       ' / '                                   
                DELIMITED BY SIZE INTO L745-STATUS-BEF.                         
                                                                                
           MOVE F595-NUM-USER-ID         TO L745-USER-ID.                       
                                                                                
           PERFORM 420-RETRIEVE-DATA-K740  THRU 420-EXIT.                       
                                                                                
           IF WS-LINE > 60                                                      
              PERFORM 500-PRINT-HEAD     THRU 500-EXIT.                         
                                                                                
           WRITE BP13L745-REC            FROM L745-DETAIL1 AFTER 2.             
                                                                                
           ADD 3                         TO WS-LINE.                            
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       420-RETRIEVE-DATA-K740.                                                  
                                                                                
           IF F595-NUM-RANDOM  =  SPACES OR  LOW-VALUES                         
               PERFORM 422-RETRIEVE-DATA-K730  THRU 422-EXIT                    
           ELSE                                                                 
                                                                                
           MOVE F595-NUM-RANDOM  TO K740-KEY-FLD                                
           READ BP13K740                                                        
           IF BP13K740-STATUS = 00                                              
               ADD     1                     TO   WS-K740-READ                  
               MOVE    K740-DTE-BALLOT       TO   WS-BALLOT-DATE                
               STRING K740-CDE-REQUEST-STATUS ' / '                             
                      K740-CDE-COHORT       ' / '                               
                      K740-CDE-SUCC-NT-ZONE ' / '                               
                      WS-NT-ZONE            ' / '                               
                      K740-CDE-UNSUCC       ' / '                               
                   DELIMITED BY SIZE INTO L745-STATUS-AFT                       
           ELSE                                                                 
               MOVE     SPACES TO  BP13L745-REC                                 
               DISPLAY 'RECORD NOT FOUND IN BP13K740, STATUS '                  
                        BP13K740-STATUS ' ' K740-KEY-FLD.                       
                                                                                
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
       422-RETRIEVE-DATA-K730.                                                  
                                                                                
                                                                                
           MOVE F595-NUM-REGN    TO K730-KEY-NEW                                
           READ BP13K730                                                        
           IF BP13K730-STATUS = 00                                              
               ADD     1                     TO   WS-K730-READ                  
               MOVE    K730-DTE-BALLOT       TO   WS-BALLOT-DATE                
               STRING K730-CDE-REQUEST-STATUS ' / '                             
                      K730-CDE-COHORT       ' / '                               
                      K730-CDE-SUCC-NT-ZONE ' / '                               
                      WS-NT-ZONE            ' / '                               
                      K730-CDE-UNSUCC       ' / '                               
                   DELIMITED BY SIZE INTO L745-STATUS-AFT                       
           ELSE                                                                 
               MOVE     SPACES TO  BP13L745-REC                                 
               DISPLAY 'RECORD NOT FOUND IN BP13K730, STATUS '                  
                        BP13K730-STATUS ' ' K730-KEY-NEW.                       
                                                                                
                                                                                
       422-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-PRINT-HEAD.                                                          
                                                                                
           ADD  1                        TO WS-PAGE.                            
           MOVE WS-PAGE                  TO HEAD1-PAGE.                         
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CURRENT-DATE.                
           MOVE CORR WS-CURRENT-DATE     TO WS-EDIT-DATE.                       
                                                                                
           MOVE WS-EDIT-DATE             TO HEAD1-DATE.                         
                                                                                
           WRITE BP13L745-REC     FROM L745-HEAD1 AFTER PAGE.                   
           WRITE BP13L745-REC     FROM L745-HEAD2 AFTER 1.                      
           WRITE BP13L745-REC     FROM L745-HEAD6 AFTER 2.                      
           WRITE BP13L745-REC     FROM L745-HEAD7 AFTER 1.                      
           MOVE  SPACES           TO   BP13L745-REC.                            
           WRITE BP13L745-REC.                                                  
           MOVE 06                TO WS-LINE.                                   
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-PRINT-FOOTER.                                                        
                                                                                
           MOVE  SPACES           TO   BP13L745-REC.                            
           WRITE BP13L745-REC.                                                  
                                                                                
           IF WS-NUM-PRINT = 0                                                  
              WRITE BP13L745-REC     FROM L745-FOOTER3 AFTER 4                  
              WRITE BP13L745-REC     FROM L745-FOOTER1 AFTER 2                  
              WRITE BP13L745-REC     FROM L745-FOOTER2 AFTER 2                  
           ELSE                                                                 
              WRITE BP13L745-REC     FROM L745-FOOTER1 AFTER 4                  
              WRITE BP13L745-REC     FROM L745-FOOTER2 AFTER 2                  
           END-IF.                                                              
                                                                                
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
                                                                                
           DISPLAY '************** BP13C745 ***************'.                   
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM BP13F595 : ' WS-F595-READ.          
           DISPLAY 'NO OF RECORDS READ FROM BP13K730 : ' WS-K730-READ.          
           DISPLAY 'NO OF RECORDS READ FROM BP13K740 : ' WS-K740-READ.          
           DISPLAY 'NO OF RECORDS PRINTED,  BP13L745 : ' WS-NUM-PRINT.          
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F595                                                       
                 BP13K730                                                       
                 BP13K740                                                       
                 BP13L745.                                                      
                                                                                
           IF BP13K740-STATUS NOT = 00 AND 23 AND 97                            
              DISPLAY '***  CLOSING ERROR, BP13K740-STATUS '                    
              BP13K740-STATUS.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
