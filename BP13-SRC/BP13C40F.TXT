       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C40F.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  25/11/2021.                                               
      **************************************************************            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      **************************************************************            
      * OBJECTIVE : TO WRITE A REPORT ON EJV FOR SERS CONTRA THAT  *            
      *             ARE STILL PENDING AND SEND TO USERS VIA EMAIL  *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- -----------                         *            
      * BP138086 20211125 FP6   NEW PGM                            *            
      * BP138086 20211203 FP6   ADD  NEW NAMES FOR EMAIL           *            
      **************************************************************            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F410         ASSIGN BP13F410.                             
                                                                                
           SELECT BP13LEJV         ASSIGN BP13LEJV.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F410           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 2000 characters                  
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BP13F410.                                                           
                                                                                
       FD  BP13LEJV                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 160 CHARACTERS.                                      
       01  P13LEJV-REC                 PIC X(160).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FLAG-N-STATUS.                                                    
           05  WS-F410-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-F410-EOF              VALUE 'Y'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F410-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-LEJV-WRTN                 PIC 9(05)  VALUE ZEROS.             
           05  WS-NUM-SNO-LEJV              PIC 9(04)  VALUE ZEROES.            
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
                                                                                
      *---------------------------------------------------------------*         
      *        MAIL HEADER                                            *         
      *---------------------------------------------------------------*         
       01  MAIL-HDR-HELO.                                                       
           05  FILLER PIC X(160) VALUE 'HELO SGPHDB1'.                          
                                                                                
       01  MAIL-HDR-MAIL-FROM.                                                  
           05  FILLER PIC X(160) VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.              
                                                                                
       01  MAIL-HDR-MAIL-RCPT-TO.                                               
           05  FILLER PIC X(160) VALUE 'RCPT TO:<Joseph_YY_KUAH@hdb.gov.        
      -      'sg>'.                                                             
                                                                                
       01  MAIL-HDR-MAIL-RCPT1.                                                 
           05  FILLER PIC X(160) VALUE 'RCPT TO:<Foo_Chee_Wee@hdb.gov.sg        
      -      '>'.                                                               
                                                                                
       01  MAIL-HDR-MAIL-RCPT2.                                                 
           05  FILLER PIC X(160) VALUE "RCPT TO:<Nor'ain_MOHAMAD_JOHAR@h        
      -      "db.gov.sg>".                                                      
                                                                                
       01  MAIL-HDR-MAIL-RCPT3.                                                 
           05  FILLER PIC X(160) VALUE 'RCPT TO:<CHONG_Yeng_Mui@hdb.gov.        
      -      'sg>'.                                                             
                                                                                
       01  MAIL-HDR-MAIL-RCPT4.                                                 
           05  FILLER PIC X(160) VALUE 'RCPT TO:<Siti_Zainah_MISONEP@hdb        
      -      '.gov.sg>'.                                                        
                                                                                
       01  MAIL-HDR-MAIL-RCPT5.                                                 
           05  FILLER PIC X(160) VALUE 'RCPT TO:<Ivy_M_BALANAG@hdb.gov.s        
      -      'g>'.                                                              
                                                                                
       01  MAIL-HDR-MAIL-RCPT6.                                                 
           05  FILLER PIC X(160) VALUE 'RCPT TO:<Melvin_SH_LIM@hdb.gov.s        
      -      'g>'.                                                              
                                                                                
       01  MAIL-HDR-MAIL-RCPT7.                                                 
           05  FILLER PIC X(160) VALUE 'RCPT TO:<CHOY_Miu_Yeng@hdb.gov.s        
      -      'g>'.                                                              
                                                                                
       01  MAIL-HDR-MAIL-RCPT-CC1.                                              
           05  FILLER PIC X(160) VALUE 'RCPT CC:<Serena_JM_TEH@hdb.gov.s        
      -      'g>'.                                                              
                                                                                
       01  MAIL-HDR-DATA.                                                       
           05  FILLER PIC X(160) VALUE 'DATA'.                                  
                                                                                
       01  MAIL-HDR-FROM.                                                       
           05  FILLER PIC X(160) VALUE 'FROM:Soc System - Email Alert'.         
                                                                                
       01  MAIL-HDR-MAIL-TO.                                                    
           05  FILLER PIC X(160)                                                
               VALUE 'TO:<Joseph_YY_KUAH@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-HDR-MAIL-TO1.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'TO:<FOO_Chee_Wee@hdb.gov.sg>'.                            
                                                                                
       01  MAIL-HDR-MAIL-TO2.                                                   
           05  FILLER PIC X(160)                                                
               VALUE "TO:<Nor'ain_MOHAMAD_JOHAR@hdb.gov.sg>".                   
                                                                                
       01  MAIL-HDR-MAIL-TO3.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'TO:<CHONG_Yeng_Mui@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-HDR-MAIL-TO4.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'TO:<Siti_Zainah_MISONEP@hdb.gov.sg>'.                     
                                                                                
       01  MAIL-HDR-MAIL-TO5.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'TO:<Ivy_M_BALANAG@hdb.gov.sg>'.                           
                                                                                
       01  MAIL-HDR-MAIL-TO6.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'TO:<Melvin_SH_LIM@hdb.gov.sg>'.                           
                                                                                
       01  MAIL-HDR-MAIL-TO7.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'TO:<CHOY_Miu_Yeng@hdb.gov.sg>'.                           
                                                                                
       01  MAIL-HDR-MAIL-CC1.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'CC:<Serena_JM_TEH@hdb.gov.sg>'.                           
                                                                                
       01  MAIL-SUBJECT-LEJV.                                                   
           05  FILLER PIC X(160) VALUE 'SUBJECT : Report for eJV for SER        
      -       'S CONTRA Still Pending'.                                         
                                                                                
       01  MAIL-BODY-LEJV              PIC X(160) VALUE SPACES.                 
                                                                                
       01  MAIL-END-LEJV               PIC X(160) VALUE SPACES.                 
                                                                                
       01  MAIL-HDR-DATE.                                                       
           05  FILLER                  PIC X(6)   VALUE 'DATE: '.               
           05  MAIL-DATE               PIC X(10)  VALUE SPACES.                 
           05  FILLER                  PIC X(124) VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(160) VALUE SPACES.                 
                                                                                
      *---------------------------------------------------------------*         
      *                      REPORT DETAILS                           *         
      *---------------------------------------------------------------*         
       01  LEJV-HEADER.                                                         
           05  FILLER               PIC X(29)   VALUE SPACES.                   
           05  FILLER               PIC X(97)   VALUE                           
                       'Report for eJV for SERS CONTRA Still Pending'.          
           05  FILLER               PIC X(13)   VALUE SPACES.                   
           05  FILLER               PIC X(6)    VALUE 'DATE: '.                 
           05  LEJV-NUM-DTE         PIC X(10)   VALUE SPACES.                   
                                                                                
       01  LEJV-LINE                PIC X(160)  VALUE ALL '-'.                  
                                                                                
       01  LEJV-HEADER1.                                                        
           05  FILLER              PIC X(04)   VALUE ' S/N'.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'Regn'.                    
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(18)   VALUE                            
                                             'Date of Collection'.              
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(14)   VALUE 'SERS-IP Cheque'.          
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'Loan Tag'.                
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE 'Sers Contra Date'.        
                                                                                
                                                                                
       01  LEJV-DETAIL1.                                                        
           05  LEJV-SNO            PIC ZZZ9    VALUE ZEROES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  LEJV-REGN           PIC X(08)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  LEJV-DTE-KEY        PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(17)   VALUE SPACES.                    
           05  LEJV-SER-CHQ        PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE SPACES.                    
           05  LEJV-LOAN-TAG       PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE SPACES.                    
           05  LEJV-DTE-SERS       PIC X(10)   VALUE SPACES.                    
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F410       THRU 2000-EXIT.                     
           PERFORM 3000-MAIN-ROUTINE        THRU 3000-EXIT                      
                   UNTIL WS-F410-EOF.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13F410                                                  
               OUTPUT BP13LEJV.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8               TO  WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8            TO  WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10                TO  WS-DTE-CURR-10.                 
                                                                                
           PERFORM 1200-PRINT-LEJV-HEADER   THRU 1200-EXIT.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1200-PRINT-LEJV-HEADER.                                                  
      *-------------------------------------------------------------            
           MOVE WS-DTE-CURR-10              TO MAIL-DATE                        
                                               LEJV-NUM-DTE.                    
                                                                                
           WRITE P13LEJV-REC                FROM MAIL-HDR-HELO.                 
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-FROM.            
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-RCPT-TO.         
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-RCPT1.           
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-RCPT2.           
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-RCPT3.           
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-RCPT4.           
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-RCPT5.           
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-RCPT6.           
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-RCPT7.           
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-RCPT-CC1.        
           WRITE P13LEJV-REC                FROM MAIL-HDR-DATA.                 
           WRITE P13LEJV-REC                FROM MAIL-HDR-FROM.                 
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-TO.              
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-TO1.             
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-TO2.             
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-TO3.             
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-TO4.             
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-TO5.             
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-TO6.             
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-TO7.             
           WRITE P13LEJV-REC                FROM MAIL-HDR-MAIL-CC1.             
           WRITE P13LEJV-REC                FROM MAIL-SUBJECT-LEJV.             
           WRITE P13LEJV-REC                FROM MAIL-HDR-DATE.                 
                                                                                
           WRITE P13LEJV-REC                FROM MAIL-SPACES.                   
           WRITE P13LEJV-REC                FROM MAIL-SPACES.                   
                                                                                
           WRITE P13LEJV-REC                FROM LEJV-HEADER.                   
           WRITE P13LEJV-REC                FROM MAIL-SPACES.                   
                                                                                
           WRITE P13LEJV-REC                FROM LEJV-LINE.                     
           WRITE P13LEJV-REC                FROM LEJV-HEADER1.                  
           WRITE P13LEJV-REC                FROM LEJV-LINE.                     
           WRITE P13LEJV-REC                FROM MAIL-SPACES.                   
                                                                                
           MOVE ZEROES                      TO WS-NUM-SNO-LEJV.                 
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F410.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F410                                                        
              AT END MOVE 'Y'               TO WS-F410-EOF-FLAG.                
                                                                                
           IF WS-F410-EOF-FLAG NOT = 'Y'                                        
              ADD 1                         TO WS-F410-READ                     
           ELSE                                                                 
              WRITE P13LEJV-REC             FROM MAIL-SPACES                    
              WRITE P13LEJV-REC             FROM MAIL-SPACES                    
              WRITE P13LEJV-REC             FROM MAIL-END                       
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF F410-NUM-REGN NOT = SPACES AND LOW-VALUES                         
              PERFORM 3100-WRITE-LEJV-REPORT     THRU 3100-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-WRITE-LEJV-REPORT.                                                  
      *---------------------------------------------------------------*         
           INITIALIZE P13LEJV-REC.                                              
           MOVE SPACES                      TO P13LEJV-REC.                     
           ADD 1                            TO WS-LEJV-WRTN                     
                                               WS-NUM-SNO-LEJV                  
                                                                                
           MOVE WS-NUM-SNO-LEJV         TO LEJV-SNO.                            
           MOVE F410-NUM-REGN           TO LEJV-REGN.                           
                                                                                
           IF F410-DTE-KEY-ISSUE NOT = SPACES AND LOW-VALUES                    
              STRING F410-DTE-KEY-ISSUE(1:4) '/'                                
                     F410-DTE-KEY-ISSUE(5:2) '/'                                
                     F410-DTE-KEY-ISSUE(7:2)                                    
                     DELIMITED BY SIZE INTO LEJV-DTE-KEY                        
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES            TO LEJV-DTE-KEY                            
           END-IF.                                                              
                                                                                
           MOVE F410-NUM-SER-CHQ        TO LEJV-SER-CHQ.                        
           MOVE F410-NUM-LOAN-TAG       TO LEJV-LOAN-TAG.                       
                                                                                
           IF F410-DTE-SERS-CONTRA-JV NOT = SPACES AND LOW-VALUES               
              STRING F410-DTE-SERS-CONTRA-JV(1:4) '/'                           
                     F410-DTE-SERS-CONTRA-JV(5:2) '/'                           
                     F410-DTE-SERS-CONTRA-JV(7:2)                               
                     DELIMITED BY SIZE INTO LEJV-DTE-SERS                       
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES               TO LEJV-DTE-SERS                        
           END-IF.                                                              
                                                                                
           WRITE P13LEJV-REC               FROM LEJV-DETAIL1.                   
           WRITE P13LEJV-REC               FROM MAIL-SPACES.                    
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '------- BP13C40F -------'.                                  
           DISPLAY ' RECORDS READ    IN BP13F410 : ' WS-F410-READ.              
           DISPLAY ' RECORDS WRITTEN IN BP13LEJV : ' WS-LEJV-WRTN.              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE BP13F410                                                       
                 BP13LEJV.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
