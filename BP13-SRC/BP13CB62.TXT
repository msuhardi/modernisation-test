       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         BP13CB62.                                            
      *AUTHOR.             ZARITA SARMIENTO.                                    
      *DATE-WRITTEN.       18 JUN 2009.                                         
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  PROGRAM TO PROCESS SPOUSE NRIC              *            
      *                                                            *            
      *                                                            *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F580                                 *            
      *                2. BP13K828                                 *            
      *  OUTPUT FILE:  1. P13F580                                  *            
      *                                                            *            
      *                                                            *            
      *  OUTPUT FILE:  1. BP13F010                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133646  ZDD1 18062009 NEW PROGRAM                         *           
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
            SELECT BP13F580 ASSIGN TO BP13F580                                  
                            FILE STATUS  IS WS-F580-STAT.                       
                                                                                
            SELECT BP13K828 ASSIGN TO BP13K828                                  
                            FILE STATUS  IS WS-K828-STAT                        
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K828-KEY-FLD.                       
                                                                                
                                                                                
            SELECT P13F580  ASSIGN TO P13F580.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F580                                                            
                RECORD CONTAINS  200 CHARACTERS                                 
                LABEL RECORDS ARE STANDARD                                      
                RECORDING MODE IS F.                                            
                                                                                
       COPY BP13F580.                                                           
                                                                                
       FD   BP13K828                                                            
                RECORD CONTAINS  200  CHARACTERS.                               
                                                                                
       COPY BP13K828.                                                           
                                                                                
       FD   P13F580                                                             
                RECORD CONTAINS  200  CHARACTERS                                
                LABEL RECORDS ARE STANDARD                                      
                RECORDING MODE IS F.                                            
                                                                                
       01   P13F580-REC   PIC X(200).                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01 WS-VARIABLES.                                                         
          10 WS-F580-READ-CTR         PIC 9(8) VALUE ZEROES.                    
          10 WS-K828-READ-CTR         PIC 9(8) VALUE ZEROES.                    
          10 WS-K828-NOTFND-CTR       PIC 9(8) VALUE ZEROES.                    
          10 WS-F580-WRITE-CTR        PIC 9(8) VALUE ZEROES.                    
                                                                                
          10 WS-F580-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-K828-STAT             PIC X(2) VALUE SPACES.                    
                                                                                
          10 WS-EOF-F580              PIC X(1)  VALUE 'N'.                      
          10 WS-EOF-K828              PIC X(1)  VALUE 'N'.                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************                                              
       0000-MAIN-CONTROL.                                                       
      ****************************                                              
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F580  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-RECORD THRU 3000-EXIT                           
                   UNTIL WS-EOF-F580 = 'Y'.                                     
           PERFORM 9999-CLOSE-FILES    THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1000-OPEN-FILES.                                                         
      ****************************                                              
                                                                                
           OPEN INPUT  BP13F580                                                 
                       BP13K828                                                 
                                                                                
                OUTPUT P13F580.                                                 
                                                                                
                                                                                
                                                                                
           IF WS-K828-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP13K828 STATUS - '                     
                              WS-K828-STAT                                      
              MOVE WS-K828-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2000-READ-BP13F580.                                                      
      ****************************                                              
                                                                                
           READ BP13F580 AT END                                                 
                MOVE 'Y' TO WS-EOF-F580                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F580-READ-CTR.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3000-PROCESS-RECORD.                                                     
      ****************************                                              
                                                                                
           MOVE SPACES        TO  K828-KEY-FLD.                                 
           MOVE F580-NUM-REGN TO  K828-NUM-REGN.                                
                                                                                
           PERFORM 4000-START-BP13K828  THRU 4000-EXIT.                         
           PERFORM 2000-READ-BP13F580   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4000-START-BP13K828.                                                     
      ****************************                                              
                                                                                
           MOVE 'N' TO WS-EOF-K828.                                             
                                                                                
           START BP13K828 KEY IS >= K828-KEY-FLD.                               
           EVALUATE WS-K828-STAT                                                
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 4001-READNEXT-K828   THRU 4001-EXIT                    
                   UNTIL F580-NUM-REGN NOT = K828-NUM-REGN OR                   
                          WS-EOF-K828 = 'Y'                                     
              WHEN 10                                                           
              WHEN 23                                                           
                   ADD 1 TO WS-K828-NOTFND-CTR                                  
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K828..' WS-K828-STAT              
                   MOVE WS-K828-STAT TO RETURN-CODE                             
                   PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4001-READNEXT-K828.                                                      
      ****************************                                              
                                                                                
           READ BP13K828 NEXT RECORD                                            
                                                                                
           EVALUATE WS-K828-STAT                                                
               WHEN 00                                                          
               WHEN 22                                                          
                    ADD 1 TO WS-K828-READ-CTR                                   
                    IF F580-NUM-REGN  = K828-NUM-REGN                           
                      IF K828-NUM-NRIC-SPOUSE = SPACES                          
                         PERFORM 5000-ELIG-SCH-FS  THRU 5000-EXIT               
                      ELSE                                                      
                         PERFORM 5001-ELIG-SCH-NOT-FS THRU 5001-EXIT            
                      END-IF                                                    
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO WS-K828-NOTFND-CTR                                 
                    MOVE 'Y' TO WS-EOF-K828                                     
               WHEN OTHER                                                       
                   DISPLAY 'ERROR PROCESSING BP13K828..' WS-K828-STAT           
                   MOVE WS-K828-STAT TO RETURN-CODE                             
                   PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       4001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       5000-ELIG-SCH-FS.                                                        
      ****************************                                              
           IF (K828-NUM-NRIC NOT = F580-NUM-NRIC1 AND                           
                                  F580-NUM-NRIC2 AND                            
                                  F580-NUM-NRIC3 AND                            
                                  F580-NUM-NRIC4)                               
              AND K828-NUM-RELATIONSHIP = '09'                                  
                MOVE K828-NUM-NRIC          TO F580-NUM-NRIC1                   
                PERFORM 6000-WRITE-P13F580  THRU 6000-EXIT                      
           END-IF.                                                              
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       5001-ELIG-SCH-NOT-FS.                                                    
      ****************************                                              
                                                                                
           IF (K828-NUM-NRIC = F580-NUM-NRIC1 OR                                
                              F580-NUM-NRIC2 OR                                 
                              F580-NUM-NRIC3 OR                                 
                              F580-NUM-NRIC4)                                   
              AND (K828-NUM-NRIC-SPOUSE NOT = F580-NUM-NRIC1 AND                
                                             F580-NUM-NRIC2 AND                 
                                             F580-NUM-NRIC3 AND                 
                                             F580-NUM-NRIC4 AND                 
                                             SPACES AND                         
                                             LOW-VALUES)                        
                MOVE K828-NUM-NRIC-SPOUSE   TO F580-NUM-NRIC1                   
                PERFORM 6000-WRITE-P13F580  THRU 6000-EXIT                      
           END-IF.                                                              
                                                                                
       5001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       6000-WRITE-P13F580.                                                      
      ****************************                                              
                                                                                
           WRITE P13F580-REC  FROM BP13F580-REC.                                
           ADD 1 TO WS-F580-WRITE-CTR.                                          
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       9999-CLOSE-FILES.                                                        
      ****************************                                              
           DISPLAY ' '.                                                         
           DISPLAY '---- BP13CB60 CONTROL LIST ----'.                           
           DISPLAY ' TOTAL F580 READ         : ' WS-F580-READ-CTR               
           DISPLAY ' TOTAL K828 READ         : ' WS-K828-READ-CTR.              
           DISPLAY ' TOTAL K828 NOT FOUND    : ' WS-K828-NOTFND-CTR.            
           DISPLAY ' TOTAL F580 WRITE        : ' WS-F580-WRITE-CTR.             
                                                                                
           CLOSE  BP13F580                                                      
                  BP13K828                                                      
                  P13F580.                                                      
                                                                                
                                                                                
                                                                                
           IF WS-K828-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP13K828 STATUS - '                     
                              WS-K828-STAT                                      
              MOVE WS-K828-STAT              TO RETURN-CODE                     
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
