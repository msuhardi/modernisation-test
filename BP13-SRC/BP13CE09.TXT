      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CE09.                                                 
       AUTHOR.        MRN1.                                                     
       DATE-WRITTEN.  15032012.                                                 
      *===============================================================*         
      *   OBJECTIVE:  TO CREATE REPORT WITH TOTAL OUTSTANDING AMOUNT  *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13FD09                                  *         
      *                                                               *         
      *     REPORT FILE:                                              *         
      *                  1. BP13LE09                                  *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP134970 15/03/2012 MRN1 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13FD09  ASSIGN        TO BP13FD09.                         
                                                                                
           SELECT  BP13LE09  ASSIGN        TO BP13LE09.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FD09                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13KD09.                                                           
                                                                                
       FD  BP13LE09                                                             
           RECORD CONTAINS 900 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  BP13LE09-REC                  PIC X(900).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-FD09-EOF             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FD09-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE-TIME.                                              
              10 WS-SYSTEM-DATE        PIC X(08) VALUE ZEROES.                  
              10 WS-SYSTEM-TIME        PIC X(08) VALUE ZEROES.                  
           05 WS-DATE                  PIC X(08) VALUE SPACES.                  
           05 WS-TOT-OUTSTDG           PIC S9(8)V99 VALUE ZEROES.               
                                                                                
       01  WS-HEADER-LINE1.                                                     
           05  FILLER                 PIC X(50) VALUE                           
               '  S/N   REGNO     SCHACCT    HSGNT GRANT   AHG DIS'.            
           05  FILLER                 PIC X(50) VALUE                           
               'BURSED  SHG DISBURSED  CANCELLATION  SUB TRANSFER '.            
           05  FILLER                 PIC X(50) VALUE                           
               ' AHG RECOVERED  SHG RECOVERED   TOT OUTSTANDING   '.            
           05  FILLER                 PIC X(50) VALUE                           
               'CRM                                               '.            
                                                                                
       01  WS-HEADER-LINE1A.                                                    
           05  FILLER                 PIC X(50) VALUE                           
               '                            DISBURSED DTE     AMOU'.            
           05  FILLER                 PIC X(50) VALUE                           
               'NT         AMOUNT          DATE          DATE     '.            
           05  FILLER                 PIC X(50) VALUE                           
               '    AMOUNT         AMOUNT           AMOUNT       ('.            
           05  FILLER                 PIC X(50) VALUE                           
               'OIC)                                              '.            
                                                                                
       01  WS-HEADER-LINE2.                                                     
           05  FILLER                 PIC X(50) VALUE                           
               '------ -------- ----------- -------------  -------'.            
           05  FILLER                 PIC X(50) VALUE                           
               '------  -------------  ------------  ------------ '.            
           05  FILLER                 PIC X(50) VALUE                           
               ' -------------  -------------    -------------   -'.            
           05  FILLER                 PIC X(50) VALUE                           
               '----                                              '.            
                                                                                
       01  WS-SPACES.                                                           
           05  FILLER       PIC X(900)   VALUE SPACES.                          
                                                                                
       01  WS-LE09-DETAIL.                                                      
           05 WS-LE09-SNO                   PIC ZZZZZ9.                         
           05 FILLER                        PIC X(01) VALUE SPACES.             
           05 WS-LE09-NUM-REGN              PIC X(08).                          
           05 FILLER                        PIC X(02) VALUE SPACES.             
           05 WS-LE09-SCH-ACCNT             PIC X(09).                          
           05 FILLER                        PIC X(04) VALUE SPACES.             
           05 WS-LE09-HSGNT-DISB-DTE        PIC X(10).                          
           05 FILLER                        PIC X(05) VALUE SPACES.             
           05 WS-LE09-AMT-AHG-GRANT         PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(05) VALUE SPACES.             
           05 WS-LE09-AMT-SHG-GRANT         PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(05) VALUE SPACES.             
           05 WS-LE09-DTE-CANCEL            PIC X(10).                          
           05 FILLER                        PIC X(04) VALUE SPACES.             
           05 WS-LE09-DTE-SUBTRANS          PIC X(10).                          
           05 FILLER                        PIC X(04) VALUE SPACES.             
           05 WS-LE09-AMT-AHG-RECOVER       PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(05) VALUE SPACES.             
           05 WS-LE09-AMT-SHG-RECOVER       PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(07) VALUE SPACES.             
           05 WS-LE09-AMT-TOT-OUTSTDG       PIC -ZZZ,ZZ9.99.                    
           05 FILLER                        PIC X(05) VALUE SPACES.             
           05 WS-LE09-OIC                   PIC X(05).                          
           05 FILLER                        PIC X(20) VALUE SPACES.             
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13FD09      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-FD09-EOF = 'Y'.                                           
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FD09                                                 
               OUTPUT  BP13LE09.                                                
                                                                                
           WRITE BP13LE09-REC FROM WS-HEADER-LINE1.                             
           WRITE BP13LE09-REC FROM WS-HEADER-LINE1A.                            
           WRITE BP13LE09-REC FROM WS-HEADER-LINE2.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13FD09.                                                      
      ******************************************************************        
                                                                                
           READ BP13FD09           AT END                                       
                MOVE 'Y'           TO WS-FD09-EOF                               
                GO                 TO 2000-EXIT.                                
                                                                                
           IF KD09-NUM-REGN = SPACES OR LOW-VALUES                              
              GO TO 2000-READ-BP13FD09.                                         
                                                                                
           ADD  1                  TO WS-FD09-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 3500-CHECK-NON-NUMERIC THRU 3500-EXIT.                       
           INITIALIZE WS-LE09-DETAIL.                                           
           PERFORM 4000-MOVE-VALUES-TO-REPORT THRU 4000-EXIT.                   
                                                                                
           ADD 1 TO WS-SNO.                                                     
           MOVE WS-SNO                   TO WS-LE09-SNO.                        
           WRITE BP13LE09-REC FROM WS-LE09-DETAIL.                              
                                                                                
           PERFORM 2000-READ-BP13FD09 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-CHECK-NON-NUMERIC.                                                  
      ******************************************************************        
           IF KD09-AMT-SHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROES TO KD09-AMT-SHG-GRANT                                 
           END-IF.                                                              
                                                                                
           IF KD09-AMT-SHG-RECOVER IS NOT NUMERIC                               
              MOVE ZEROES TO KD09-AMT-SHG-RECOVER                               
           END-IF.                                                              
                                                                                
           IF KD09-AMT-AHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROES TO KD09-AMT-AHG-GRANT                                 
           END-IF.                                                              
                                                                                
           IF KD09-AMT-AHG-RECOVER IS NOT NUMERIC                               
              MOVE ZEROES TO KD09-AMT-AHG-RECOVER                               
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-MOVE-VALUES-TO-REPORT.                                              
      ******************************************************************        
           MOVE KD09-NUM-REGN                TO WS-LE09-NUM-REGN.               
           MOVE KD09-NUM-SCH-ACCNT           TO WS-LE09-SCH-ACCNT.              
           MOVE KD09-DTE-AHG-PROCESS         TO WS-LE09-HSGNT-DISB-DTE.         
           MOVE KD09-AMT-AHG-GRANT           TO WS-LE09-AMT-AHG-GRANT.          
           MOVE KD09-AMT-SHG-GRANT           TO WS-LE09-AMT-SHG-GRANT.          
           IF KD09-DTE-CANCEL NOT = SPACES AND LOW-VALUES                       
              STRING KD09-DTE-CANCEL(7:2) '/'                                   
                     KD09-DTE-CANCEL(5:2) '/'                                   
                     KD09-DTE-CANCEL(1:4)                                       
                     DELIMITED BY SIZE      INTO WS-LE09-DTE-CANCEL             
           END-IF.                                                              
           MOVE SPACES                    TO WS-LE09-DTE-SUBTRANS.              
           MOVE KD09-AMT-AHG-RECOVER      TO WS-LE09-AMT-AHG-RECOVER.           
           MOVE KD09-AMT-SHG-RECOVER      TO WS-LE09-AMT-SHG-RECOVER.           
           MOVE KD09-NUM-OIC              TO WS-LE09-OIC.                       
                                                                                
           COMPUTE WS-TOT-OUTSTDG =                                             
                   (KD09-AMT-AHG-GRANT + KD09-AMT-SHG-GRANT) -                  
                   (KD09-AMT-AHG-RECOVER + KD09-AMT-SHG-RECOVER)                
                                                                                
           MOVE WS-TOT-OUTSTDG            TO WS-LE09-AMT-TOT-OUTSTDG.           
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CE09'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13FD09 RECORDS READ............. : '            
                    WS-FD09-READ.                                               
                                                                                
           CLOSE    BP13FD09                                                    
                    BP13LE09.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
