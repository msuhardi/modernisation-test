       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C41J.                                                 
      *AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  DEC 20 2017.                                              
      * ============================================================            
      * SYSTEM OF COMMITMENT (SOC)                                              
      * ============================================================            
      * OBJECTIVE  -  TO ENHANCE SOC TO CATER FOR NEW CRITERION ON              
      *               BUYERS WHO ARE BANKRUPT                                   
      *                                                                         
      * INPUT FILES : BP13F800                                                  
      *               BP04K010                                                  
      *                                                                         
      * INPUT/OUTPUT: BP13K820                                                  
      *               BP13K800                                                  
      *                                                                         
      * OUTPUT FILE : BP13L41J                                                  
      * ------------------------------------------------------------            
      * ------------------------------------------------------------            
      * REF NO    DATE  BY   DESCRIPTIONS                                       
      * -------- ------ ---  ------------                                       
      * BP136278 201217 RJB1 NEW PGM                                            
      * BP136278 211217 RJB1 TO CATER FOR DEBAR CODE 'BK' ONLY AND              
      *                      FLAT TYPE >= 3 ROOM                                
      * BP136278 090118 zs13 TO CATER FOR DISPLAY STATEMENT                     
      * ============================================================            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K820 ASSIGN        TO BP13K820                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K820-KEY-FLD                        
                           FILE STATUS   IS K820-STATUS.                        
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS K800-STATUS.                        
                                                                                
           SELECT P04K010  ASSIGN        TO P04K010                             
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS DYNAMIC                             
                           RECORD KEY    IS K010-PRIME-KEY                      
                           FILE STATUS   IS K010-STATUS.                        
                                                                                
           SELECT BP13F800 ASSIGN        TO BP13F800.                           
           SELECT BP13L41J ASSIGN        TO BP13L41J.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800           RECORDING MODE IS F                              
                               RECORD CONTAINS 2000 CHARACTERS                  
                               BLOCK  CONTAINS 0   RECORDS.                     
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K820           RECORD CONTAINS 400 CHARACTERS.                  
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K800           RECORD CONTAINS 2000 CHARACTERS.                 
       COPY BP13K800.                                                           
                                                                                
       FD   P04K010            RECORD CONTAINS 300 CHARACTERS.                  
       COPY P04K010.                                                            
                                                                                
       FD   BP13L41J           RECORDING MODE IS F                              
                               RECORD CONTAINS 80 CHARACTERS                    
                               BLOCK  CONTAINS 0   RECORDS.                     
       01   L41J-PRTREC        PIC X(80).                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  K820-STATUS                   PIC 99    VALUE 0.                     
       01  K800-STATUS                   PIC 99    VALUE 0.                     
       01  K010-STATUS                   PIC 99    VALUE 0.                     
                                                                                
       01  WS-F800-EOF                   PIC X     VALUE 'N'.                   
       01  WS-K010-EOF                   PIC X     VALUE 'N'.                   
       01  WS-K800-FND                   PIC X     VALUE 'N'.                   
       01  WS-K820-FND                   PIC X     VALUE 'N'.                   
       01  WS-K010-FND                   PIC X     VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-I                       PIC 9(7)  VALUE 0.                     
           05 WS-CNT-F800-READ           PIC 9(7)  VALUE 0.                     
           05 WS-CNT-K010-READ           PIC 9(7)  VALUE 0.                     
           05 WS-CNT-K820-READ           PIC 9(7)  VALUE 0.                     
           05 WS-CNT-K800-READ           PIC 9(7)  VALUE 0.                     
           05 WS-CNT-K800-UPDT           PIC 9(7)  VALUE 0.                     
           05 WS-CNT-K820-UPDT           PIC 9(7)  VALUE 0.                     
           05 WS-CNT-L41J-WRTE           PIC 9(7)  VALUE 0.                     
                                                                                
       01  WS-APPLICANTS.                                                       
           05 WS-NUM-NRIC OCCURS 4 TIMES PIC X(9)  VALUE SPACES.                
                                                                                
       01  WS-FLAT-TYPE                  PIC X(1).                              
           88 VALID-FT                             VALUE '3', '4', '5'.         
                                                                                
       01  WS-DATE                       PIC X(8)  VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-BOMAILID   PIC X(60)  VALUE                                 
               'SERENA_JM_TEH@HDB.GOV.SG>'.                                     
           05  FILLER          PIC X(11)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAIL-BOMAILID1  PIC X(60)  VALUE                                 
               'SERENA_JM_TEH@HDB.GOV.SG>'.                                     
           05  FILLER          PIC X(16)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  MAIL-SUBJECT    PIC X(60)  VALUE                                 
               'EMAIL ALERT FOR BANKRUPT CASES'.                                
                                                                                
       01  MAIL-DTL6.                                                           
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
      *****                                                                     
       01  L41J-HEADER.                                                         
           05 FILLER                    PIC X(80) VALUE                         
           'REGN       NRIC        DATE BALLOT   DEBAR CODE'.                   
       01  L41J-HEADER2.                                                        
           05 FILLER                    PIC X(80) VALUE                         
           '========   =========   ===========   =========='.                   
       01  L41J-DETAIL.                                                         
           05 L41J-REGN-NO              PIC X(8)  VALUE SPACES.                 
           05 FILLER                    PIC X(3)  VALUE SPACES.                 
           05 L41J-NRIC                 PIC X(09) VALUE SPACES.                 
           05 FILLER                    PIC X(3)  VALUE SPACES.                 
           05 L41J-DTE-BALLOT           PIC X(8)  VALUE SPACES.                 
           05 FILLER                    PIC X(6)  VALUE SPACES.                 
           05 L41J-DEBAR                PIC X(2)  VALUE SPACES.                 
                                                                                
      ****                                                                      
       01  WS-END-OF-REPORT.                                                    
           05  FILLER                    PIC X(29) VALUE SPACES.                
           05  FILLER                    PIC X(21)                              
               VALUE '*** END OF REPORT ***'.                                   
           05  FILLER                    PIC X(30) VALUE SPACES.                
                                                                                
       01  WS-EMPTY-REPORT.                                                     
           05  FILLER                    PIC X(30) VALUE SPACES.                
           05  FILLER                    PIC X(20)                              
               VALUE '*** EMPTY REPORT ***'.                                    
           05  FILLER                    PIC X(30) VALUE SPACES.                
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                     PIC X(80) VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9999-WRAP-UP.                                                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-START-UP.                                                           
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F800                                                
                        P04K010                                                 
                 I-O    BP13K800                                                
                        BP13K820                                                
                 OUTPUT BP13L41J.                                               
                                                                                
            IF K820-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K820,STATUS ' K820-STATUS           
               MOVE K820-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP THRU 9999-EXIT                              
            END-IF.                                                             
                                                                                
            IF K800-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K800,STATUS ' K800-STATUS           
               MOVE K800-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP THRU 9999-EXIT                              
            END-IF.                                                             
                                                                                
            IF K010-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP04K010,STATUS ' K010-STATUS           
               MOVE K010-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP THRU 9999-EXIT                              
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                         
            STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)               
                   DELIMITED BY SIZE INTO MAIL-DATE.                            
                                                                                
            WRITE L41J-PRTREC     FROM MAIL-HDR1                                
            WRITE L41J-PRTREC     FROM MAIL-HDR2.                               
            WRITE L41J-PRTREC     FROM MAIL-DTL1.                               
            WRITE L41J-PRTREC     FROM MAIL-DTL2.                               
            WRITE L41J-PRTREC     FROM MAIL-DTL3.                               
            WRITE L41J-PRTREC     FROM MAIL-DTL4.                               
            WRITE L41J-PRTREC     FROM MAIL-DTL5.                               
            WRITE L41J-PRTREC     FROM MAIL-DTL6.                               
                                                                                
            WRITE L41J-PRTREC     FROM L41J-HEADER.                             
            WRITE L41J-PRTREC     FROM L41J-HEADER2.                            
                                                                                
            READ BP13F800                                                       
                 AT END  DISPLAY 'NO RECORDS FOUND!'                            
                         MOVE 'Y'          TO WS-F800-EOF                       
                         GO TO 1000-EXIT                                        
            END-READ.                                                           
                                                                                
            ADD  1                         TO WS-CNT-F800-READ.                 
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
      *-------------------------------------------------------------*           
      * READS BP13F800 AND PROCESS EACH RECORD UNTIL END OF FILE    *           
      *-------------------------------------------------------------*           
                                                                                
           MOVE F800-NUM-ROOM              TO WS-FLAT-TYPE.                     
                                                                                
           IF NOT VALID-FT                                                      
              GO TO 2000-READ-NEXT-REC                                          
           END-IF.                                                              
                                                                                
           IF F800-DTE-BALLOT < '201602'                                        
              GO TO 2000-READ-NEXT-REC                                          
           END-IF.                                                              
                                                                                
           MOVE 'N'                        TO WS-K800-FND                       
                                              WS-K820-FND                       
                                              WS-K010-FND.                      
           MOVE SPACES                     TO WS-NUM-NRIC(1)                    
                                              WS-NUM-NRIC(2)                    
                                              WS-NUM-NRIC(3)                    
                                              WS-NUM-NRIC(4).                   
                                                                                
           MOVE F800-NUM-NRIC1             TO WS-NUM-NRIC(1).                   
           MOVE F800-NUM-NRIC2             TO WS-NUM-NRIC(2).                   
           MOVE F800-NUM-NRIC3             TO WS-NUM-NRIC(3).                   
           MOVE F800-NUM-NRIC4             TO WS-NUM-NRIC(4).                   
                                                                                
           DISPLAY 'REG NUM - ' F800-NUM-REGN                                   
                   ' FT - ' F800-NUM-ROOM                                       
                   ' DTE-BAL - ' F800-DTE-BALLOT                                
                   ' NRIC1   - ' WS-NUM-NRIC(1)                                 
                   ' NRIC2   - ' WS-NUM-NRIC(2)                                 
                   ' NRIC3   - ' WS-NUM-NRIC(3)                                 
                   ' NRIC4   - ' WS-NUM-NRIC(4).                                
                                                                                
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                      
              IF WS-NUM-NRIC(WS-I) NOT = SPACES                                 
                 PERFORM 5000-START-K010          THRU 5000-EXIT                
                 IF WS-K010-FND = 'Y'                                           
                    PERFORM 4000-READ-K800        THRU 4000-EXIT                
                    IF WS-K800-FND = 'Y'                                        
                       PERFORM 3000-READ-K820     THRU 3000-EXIT                
                       IF WS-K820-FND = 'Y'                                     
                          PERFORM 3500-UPDT-K820  THRU 3500-EXIT                
                          DISPLAY ' K820 KEY - ' K820-KEY-FLD                   
                                  ' BK   UPD - ' K820-NUM-BANKRUPT              
                          MOVE SPACES               TO L41J-DETAIL              
                          INITIALIZE                   L41J-DETAIL              
                                                                                
                          MOVE F800-NUM-REGN        TO L41J-REGN-NO             
                          MOVE F800-DTE-BALLOT      TO L41J-DTE-BALLOT          
                          MOVE K820-NUM-NRIC        TO L41J-NRIC                
                          MOVE K010-REASON-CODE     TO L41J-DEBAR               
                                                                                
                          WRITE L41J-PRTREC       FROM L41J-DETAIL              
                          ADD 1                     TO WS-CNT-L41J-WRTE         
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF WS-K800-FND = 'Y'                                                 
              PERFORM 4500-UPDT-K800              THRU 4500-EXIT                
              DISPLAY ' K800 KEY - ' K800-NUM-REGN                              
                      ' BK   UPD - ' K800-CDE-BANKRUPT                          
           END-IF.                                                              
                                                                                
       2000-READ-NEXT-REC.                                                      
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y' TO WS-F800-EOF                                         
                GO TO 2000-EXIT                                                 
                END-READ.                                                       
                                                                                
           ADD  1                          TO WS-CNT-F800-READ.                 
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3000-READ-K820.                                                          
      *-------------------------------------------------------------            
           MOVE 'N'                       TO WS-K820-FND.                       
           MOVE SPACES                    TO K820-KEY-FLD.                      
           MOVE F800-NUM-REGN             TO K820-NUM-REGN                      
           MOVE WS-NUM-NRIC(WS-I)         TO K820-NUM-NRIC.                     
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE K820-STATUS                                                 
           WHEN 00                                                              
              MOVE 'Y'                    TO WS-K820-FND                        
              ADD   1                     TO WS-CNT-K820-READ                   
           WHEN 10                                                              
           WHEN 23                                                              
              DISPLAY 'K820 REC NOT FOUND: ' K820-KEY-FLD                       
              MOVE 'N'                    TO WS-K820-FND                        
           WHEN OTHER                                                           
              DISPLAY 'READ ERROR BP13K820 - ' K820-STATUS                      
                      ', KEY IS ' K820-KEY-FLD                                  
              PERFORM 9999-WRAP-UP      THRU 9999-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3500-UPDT-K820.                                                          
      *-------------------------------------------------------------            
           MOVE 'Y'                       TO K820-NUM-BANKRUPT.                 
                                                                                
           REWRITE BP13K820-REC.                                                
                                                                                
           EVALUATE K820-STATUS                                                 
           WHEN 00                                                              
              ADD 1                       TO WS-CNT-K820-UPDT                   
              DISPLAY 'K820 REC UPDATED: ' K820-KEY-FLD                         
           WHEN OTHER                                                           
              DISPLAY 'REWRITE ERROR BP13K820 - ' K820-STATUS                   
                      ', KEY IS ' K820-KEY-FLD                                  
              PERFORM 9999-WRAP-UP      THRU 9999-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4000-READ-K800.                                                          
      *-------------------------------------------------------------            
           MOVE F800-NUM-REGN             TO K800-NUM-REGN.                     
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE K800-STATUS                                                 
           WHEN 00                                                              
              MOVE 'Y'                    TO WS-K800-FND                        
              ADD   1                     TO WS-CNT-K800-READ                   
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                    TO WS-K800-FND                        
           WHEN OTHER                                                           
              DISPLAY 'READ ERROR BP13K800 - ' K800-STATUS                      
                      ', KEY IS ' K800-NUM-REGN                                 
              PERFORM 9999-WRAP-UP      THRU 9999-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4500-UPDT-K800.                                                          
      *-------------------------------------------------------------            
           MOVE 'Y'                       TO K800-CDE-BANKRUPT.                 
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE K800-STATUS                                                 
           WHEN 00                                                              
              ADD 1                       TO WS-CNT-K800-UPDT                   
              DISPLAY 'K800 REC UPDATED: ' K800-NUM-REGN                        
           WHEN OTHER                                                           
              DISPLAY 'REWRITE ERROR BP13K800 - ' K800-STATUS                   
                      ', KEY IS ' K800-NUM-REGN                                 
              PERFORM 9999-WRAP-UP      THRU 9999-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       5000-START-K010.                                                         
      *****************************************************************         
           MOVE SPACES                  TO P04K010-REC                          
           INITIALIZE                      P04K010-REC.                         
                                                                                
           MOVE 'N'                     TO WS-K010-FND                          
                                           WS-K010-EOF                          
           MOVE WS-NUM-NRIC(WS-I)       TO K010-NUM-UINFIN                      
                                                                                
           START P04K010 KEY >= K010-PRIME-KEY.                                 
                                                                                
           EVALUATE K010-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
                PERFORM 5200-READNEXT-BP04K010  THRU 5200-EXIT                  
                  UNTIL WS-NUM-NRIC(WS-I) NOT = K010-NUM-UINFIN                 
                     OR WS-K010-EOF = 'Y' OR WS-K010-FND = 'Y'                  
           WHEN 10                                                              
           WHEN 23                                                              
                DISPLAY 'K010 REC NOT FOUND : ' K010-NUM-UINFIN                 
           WHEN OTHER                                                           
                MOVE K010-STATUS           TO RETURN-CODE                       
                DISPLAY 'ERROR STARTBR BP04K010, STATUS '                       
                         K010-STATUS                                            
                PERFORM 9999-WRAP-UP     THRU 9999-EXIT                         
           END-EVALUATE.                                                        
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5200-READNEXT-BP04K010.                                                  
      *****************************************************************         
           READ P04K010 NEXT RECORD                                             
                                                                                
           EVALUATE K010-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
              IF K010-NUM-UINFIN = WS-NUM-NRIC(WS-I)                            
                 DISPLAY ' PROC NRIC - ' WS-NUM-NRIC(WS-I)                      
                         ' RSN CODE - ' K010-REASON-CODE                        
                 IF K010-REASON-CODE = 'BK'                                     
                    MOVE 'Y'              TO WS-K010-FND                        
                    ADD   1               TO WS-CNT-K010-READ                   
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'                 TO WS-K010-EOF                        
              END-IF                                                            
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                    TO WS-K010-EOF                        
           WHEN OTHER                                                           
              MOVE 'Y'                    TO WS-K010-EOF                        
              DISPLAY 'READNEXT ERROR BP04K010 - ' K010-STATUS                  
                      ', KEY IS ' K010-NUM-UINFIN                               
              PERFORM 9999-WRAP-UP      THRU 9999-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9999-WRAP-UP.                                                            
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY '- BP13C41J CONTROL TOTAL -------------'.                    
           DISPLAY ' TOTAL BP13F800 REC READ : ' WS-CNT-F800-READ.              
           DISPLAY ' TOTAL BP04K010 REC READ : ' WS-CNT-K010-READ.              
           DISPLAY ' TOTAL BP13K800 REC READ : ' WS-CNT-K800-READ.              
           DISPLAY ' TOTAL BP13K820 REC READ : ' WS-CNT-K820-READ.              
           DISPLAY ' TOTAL BP13K800 REC UPDT : ' WS-CNT-K800-UPDT.              
           DISPLAY ' TOTAL BP13K820 REC UPDT : ' WS-CNT-K820-UPDT.              
           DISPLAY ' TOTAL LISTING WRITTEN   : ' WS-CNT-L41J-WRTE.              
                                                                                
           CLOSE BP13K820                                                       
                 BP13K800                                                       
                 BP13F800                                                       
                 P04K010.                                                       
                                                                                
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BP13K820 ' K820-STATUS                  
              MOVE K820-STATUS TO RETURN-CODE.                                  
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BP13K800 ' K800-STATUS                  
              MOVE K800-STATUS TO RETURN-CODE.                                  
                                                                                
           IF K010-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BP04K010 ' K010-STATUS                  
              MOVE K010-STATUS TO RETURN-CODE.                                  
                                                                                
           IF WS-CNT-L41J-WRTE = ZEROES                                         
              MOVE SPACES                  TO L41J-PRTREC                       
              WRITE L41J-PRTREC          FROM WS-EMPTY-REPORT                   
           ELSE                                                                 
              MOVE  SPACES                 TO L41J-PRTREC                       
              WRITE L41J-PRTREC          FROM MAIL-SPACES                       
              MOVE  SPACES                 TO L41J-PRTREC                       
              WRITE L41J-PRTREC          FROM WS-END-OF-REPORT                  
           END-IF.                                                              
                                                                                
           CLOSE BP13L41J.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
